.----------------------------------------------------------------------
. Program       : COMWEB08
.
. Function      : EDW Information web server
.
.----------------------------------------------------------------------
. Modifications : 
.----------------------------------------------------------------------
. V11.04.01 05/12/2023  Nikitha B     TSK 0940877
.           Modified KEY900 to KEY1810 to accomodate whole header in 
.           EDWARD extract.
. V11.02.01 09/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V10.13.02 09/08/2018  Jill Parkinsson Task 0861206
.           Increased line length to allow up to 4000
. V10.13.01 25/07/2018  Jill Parkinsson Task 0860951
.           Changed ViewTextFile to use " so ' are not an issue
. V10.12.03 15/03/2018  Ebon Clements  Task 0848319           
.           Fixed container stream display tag
. V10.12.02 06/03/2018  Ebon Clements  Task 0846913           
.           Added processing ok to - UPDREX00
. V10.12.01 22/02/2018  Jill Parkinson Task 0846167           
.           Created Web Server
. V10.12.00 19/01/2018  Jill Parkinson Task 0846167           
.           Created Web Server
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       COMEDFFD/INC
          INC       COMEDWFD/INC
          INC       PATDO1FD/INC   * Doctor File
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATMASTD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       OUTSITFD/INC
          INC       PATCOMFD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATHSPFD/INC
          INC       PATFN1FD/INC
          INC       PATMISTD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       PATCMNFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       PMSUCDFD/INC
          INC       PMSUCHFD/INC
          INC       PMSCCDFD/INC            * Concession Card Details
          INC       VISCMTFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC

.----------------------------------------------------------------------
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
STARTKEY  DIM       10      * Starting Key
.
CATD1     INIT      "D1"
CATD2     INIT      "D2"
CATD3     INIT      "D3"
CATD4     INIT      "D4"
CATD5     INIT      "D5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
ENVVNAME  INIT      "DPATH"
.
CTXTFILE  FILE      ASCII,FIXED=4096
CDDRCTRY  DIM       100
CONTAINR  DIM       8
CURRDATE  DIM       8       * Current Date
DATASTRM  DIM       3
DEFTVIEW  DIM       3
DOCTCODE  DIM       8
DPATHVAR  DIM       100
DSTREAM   DIM       30
FORM8     FORM      8 
FORM3     FORM      3 
HTMLLNK2  DIM       127                     * HTML link 2
KEY1810   DIM       1810
KEY4000   DIM       4000
NEWDATE   DATE      8
TEXTFILE  DIM       100
PSAGETYP  DIM       3
MBSDESC   DIM       70
PSAGECON  DIM       3
REMOTENO  FORM      2
.
.----------------------------------------------------------------------
PRGID     INIT      "COMWEB08"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "EDW Information Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      EDWC0000        * EDW Container Information
          GOTO      MAIN1000
.
MAIN1200  CALL      REMS0000        * remote scripts
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
+
.------------------------------------------------------------
. EDW Container details
.------------------------------------------------------------
EDWC0000  CALL      CLREDW00
.
          MATCH     "U",UPDTTYPE
          IF        @EQUAL
            CALL      UPDREX00     * update re-extracted date only
            GOTO      EDWC9999
          ENDIF
.
          MATCH     SP70,DATASTRM
          GOTO      EDWC1000 IF EQUAL
.
          MATCH     SP70,CONTAINR
          GOTO      EDWC1000 IF EQUAL
.
          PACK      KEY11,DATASTRM,CONTAINR,SP70
          CALL      RDCMEDW1
          BRANCH    OVRCD,EDWC9000
.
EDWC1000  CALL      CURPER00
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          CLEAR     WEBTITLE
          APPEND    "EDW Container Details",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,EDWC9999
.
          CALL      WEBBOD00
.
          CALL      WEBEND00
.
          GOTO      EDWC9999
.
EDWC9000  MOVE      "Invalid Container Record",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
EDWC9999  RETURN
+
.------------------------------------------------------------------------------
. Clear comedsaf
.------------------------------------------------------------------------------
CLREDW00  MOVE      SP70,CMEDDSTR
          MOVE      SP70,CMEDCNTN
          MOVE      SP70,CMEDSDAT
          MOVE      SP70,CMEDEDAT
          MOVE      SP70,CMEDCDAT
          MOVE      SP70,CMEDCTIM
          MOVE      SP70,CMEDCUID
          MOVE      SP70,CMEDRDAT
          MOVE      SP70,CMEDRTIM
          MOVE      SP70,CMEDRUID
          MOVE      SP70,CMEDSNTD
          MOVE      SP70,CMEDSNTT
          MOVE      SP70,CMEDSUID
          MOVE      SP70,CMEDRSND
          MOVE      SP70,CMEDRSNT
          MOVE      SP70,CMEDRSUI
          MOVE      SP70,CMEDSPAR
.
CLREDW99  RETURN
+
.------------------------------------------------------------------------------
. Remote Script
.------------------------------------------------------------------------------
REMS0000  CALL      XMLHED00
          BRANCH    EXIT,REMS9999
.
          BRANCH    REMOTENO,REMS0010
.
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                    " INVALID UPDATE TYPE ":
                    "</RETURN_VALUE>"
          GOTO      REMS9000
.
REMS0010  CALL      GETCDT00
          GOTO      REMS9000
.
REMS9000  CALL      XMLEND00
.
REMS9999  RETURN
+
.------------------------------------------------------------
. Get Next Container Date 
.------------------------------------------------------------
GETCDT00  PACK      KEY19,DATASTRM,Z70
          CALL      RSCMEDW2
          CALL      RPCMEDW2
          BRANCH    OVRCD,GETCDT90
.
          MATCH     DATASTRM,CMEDDSTR
          GOTO      GETCDT90 IF NOT EQUAL
.
          MOVE      CMEDEDAT,NEWDATE
          ADD       ONE,NEWDATE
          UNPACK    NEWDATE,CCENT,CYEAR,CMON,CDAY
.
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             CDAY,CMON,CCENT,CYEAR,"</RETURN_VALUE>"
          GOTO      GETCDT90
.
GETCDT90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                              SP70,"</RETURN_VALUE>"
          GOTO      GETCDT99
.
GETCDT99  RETURN
+
.------------------------------------------------------------
. Update Re-Extracted Date
.------------------------------------------------------------
UPDREX00  PACK      KEY11,DATASTRM,CONTAINR,SP70
          CALL      RDCMEDW1
          BRANCH    OVRCD,UPDREX90
.
          CALL      IBACLOCK
          PACK      CMEDRDAT,CCC,CYY,CMM,CDD
          REP       " 0",CMEDRDAT
          MOVE      CTIMEIS,CMEDRTIM
          MOVE      WBSEUID,CMEDRUID
.
          CALL      UPCMEDW1
.
          CALL      OPENHTML
          BRANCH    EXIT,UPDREX99
.
          WRITE     HTMLFILE;"PROCESSING_OK:"
.
          CALL      CLOSHTML
          GOTO      UPDREX99
.
UPDREX90  CLEAR     WEBTITLE
          APPEND    "Invalid container record -  ",WEBTITLE
          APPEND    KEY11,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
UPDREX99  RETURN
+

.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM 
          OPEN      COMEDWA1,"comedwaf"
          OPEN      COMEDWA2,"comedwaf"
          OPEN      COMEDWA3,"comedwaf"
          OPEN      COMEDFA1,"comedfaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSCCDA1,"pmsccdaf"
.
          GETENV    ENVVNAME,DPATHVAR            * get DPATH
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,DEFTVIEW
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,VIEWTYPE
.
          MOVE      SP70,DATASTRM
          MOVE      SP70,CONTAINR
          MOVE      ZERO,REMOTENO
          MOVE      SP256,TEXTFILE
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "datastrm=",QRYNAME
          IF        @EQUAL
            PACK      DATASTRM,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "containr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CONTAINR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "deftview=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEFTVIEW
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "remoteno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REMOTENO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "textfile=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEXTFILE
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99 
.
PROFLD11  CALL      TABE0000    * EDW Container table
          GOTO      PROFLD99 
.
PROFLD12  CALL      CONTD000    * Container Details
          GOTO      PROFLD99 
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22,PROFLD23
          GOTO      PROFLD99
.
PROFLD21  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD22  CALL      MISF0000    * Admission File Info
          GOTO      PROFLD99
.
PROFLD23  CALL      DSCF0000    * Discharge File Info
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------
. EDW Container table
.------------------------------------------------------------
TABE0000  BRANCH    FLDITMNO,TABE0100,TABE0200,TABE0300,TABE0400,TABE0500:
                             TABE0600,TABE0700,TABE0800,TABE0900,TABE1000:
                             TABE1100,TABE1200
          GOTO      TABE9999
.
TABE0100  CALL      NEXT0000
          GOTO      TABE9999
.
TABE0200  CALL      PREV0000
          GOTO      TABE9999
.
TABE0300  CALL      CURSTD00
          GOTO      TABE9999
.
TABE0400  CALL      CUREND00
          GOTO      TABE9999
.
TABE0500  CALL      CURYR000
          GOTO      TABE9999
.
TABE0600  CALL      CURMON00
          GOTO      TABE9999
.
TABE0700  CALL      SELV0000
          GOTO      TABE9999
.
TABE0800  CALL      EDWLST00
          GOTO      TABE9999
.
TABE0900  WRITE     HTMLFILE;TEMPLATE;
          GOTO      TABE9999
.
TABE1000  WRITE     HTMLFILE;REPORTNO;
          GOTO      TABE9999
.
TABE1100  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      TABE9999
.
TABE1200  WRITE     HTMLFILE;DATASTRM;
          GOTO      TABE9999
.
TABE9999  RETURN
+
.------------------------------------------------------------
. EDW Container Details
.------------------------------------------------------------
CONTD000  BRANCH    FLDITMNO,CONTD010,CONTD020,CONTD030,CONTD040,CONTD050:
                             CONTD060,CONTD070,CONTD080,CONTD090,CONTD100:
                             CONTD110
          GOTO      CONTD999
.
CONTD010  UNPACK    DIM127,D1,KEY1,DIM127     * stream 0=Code 1=Description
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,CONTD015
.
          WRITE     HTMLFILE;CMEDDSTR;
          GOTO      CONTD999
.
CONTD015  MATCH     "1 ",CMEDDSTR
          IF        @EQUAL
            WRITE     HTMLFILE;"Client";
          ENDIF
.
          MATCH     "3 ",CMEDDSTR
          IF        @EQUAL
            WRITE     HTMLFILE;"Individual Service Provider";
          ENDIF
.
          MATCH     "18",CMEDDSTR
          IF        @EQUAL
            WRITE     HTMLFILE;"Emergency Department";
          ENDIF
.
          MATCH     "6 ",CMEDDSTR
          IF        @EQUAL
            WRITE     HTMLFILE;"Service Scheduling";
          ENDIF
.
          MATCH     "17",CMEDDSTR
          IF        @EQUAL
            WRITE     HTMLFILE;"Admitted Patient";
          ENDIF
.
          MATCH     "5 ",CMEDDSTR
          IF        @EQUAL
            WRITE     HTMLFILE;"Non Admitted Patient Service";
          ENDIF
.
          GOTO      CONTD999
.
CONTD020  WRITE     HTMLFILE;CMEDCNTN;   * container number
          GOTO      CONTD999
.
CONTD030  MATCH     CMEDSDAT,SP70        * start date
          GOTO      CONTD999 IF EQUAL
          UNPACK    CMEDSDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      CONTD999
.
CONTD040  MATCH     CMEDEDAT,SP70        * end date
          GOTO      CONTD999 IF EQUAL
          UNPACK    CMEDEDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      CONTD999
.
CONTD050  MATCH     CMEDCDAT,SP70        * date created
          GOTO      CONTD999 IF EQUAL
          UNPACK    CMEDCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      CONTD999
.
CONTD060  WRITE     HTMLFILE;CMEDCTIM;   * time created      
          GOTO      CONTD999
.
CONTD070  MATCH     SP70,CMEDCUID
          GOTO      CONTD999 IF EQUAL
.
          PACK      KEY10,CMEDCUID,SP70
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      CMEDCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      CONTD999
.
CONTD080  UNPACK    CMEDSDAT,CCENT,CYEAR,CMON,CDAY  * start date
          WRITE     HTMLFILE;CDAY,CMON,CCENT,CYEAR;
          GOTO      CONTD999
.
CONTD090  UNPACK    CMEDEDAT,CCENT,CYEAR,CMON,CDAY  * end date
          WRITE     HTMLFILE;CDAY,CMON,CCENT,CYEAR;
          GOTO      CONTD999
.
CONTD100  CALL      CFLST000   * Container file list
          GOTO      CONTD999
.
CONTD110  CALL      OUTF0000   * Output raw text file
          GOTO      CONTD999
.
CONTD999  RETURN
+
.-----------------------------------------------------------------------------
. Dump Raw File
.-----------------------------------------------------------------------------
OUTF0000  OPEN      CTXTFILE,TEXTFILE
OUTF1000  READ      CTXTFILE,SEQ;KEY1810
          GOTO      OUTF9999 IF OVER
          WRITE     HTMLFILE;KEY1810
          GOTO      OUTF1000
OUTF9999  RETURN
+
.------------------------------------------------------------
. Output list of files within a container
.------------------------------------------------------------
CFLST000  PACK      KEY14,CMEDDSTR,CMEDCNTN,SP70
          CALL      RSCMEDF1
CFLST100  CALL      RKCMEDF1
          BRANCH    OVRCD,CFLST999
.
          MATCH     CMEDDSTR,CMEFDSTR
          GOTO      CFLST999 IF NOT EQUAL
.
          MATCH     CMEDCNTN,CMEFCNTN
          GOTO      CFLST999 IF NOT EQUAL
.
          STRIP     CMEFFNAM
          WRITE     HTMLFILE;*+,"<tr><td><a href=javascript:ViewTextFile(#"",CMEFFNAM,"#")>",CMEFFNAM,"</a></td></tr>"
          GOTO      CFLST100
.
CFLST999  RETURN
+
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          REP       " +",DATASTRM
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          WRITE     HTMLFILE;"comweb08.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",NXTFDATE:
                             "&lastdate=",NXTLDATE:
                             "&viewtype=",VIEWTYPE:
                             "&deftview=",DEFTVIEW:
                             "&selprint=",SELPRINT:
                             "&datastrm=",DATASTRM;
          REP       "+ ",DATASTRM
.
NEXT9999  RETURN
+
.------------------------------------------------------------
. Previous Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          REP       " +",DATASTRM
          WRITE     HTMLFILE;"comweb08.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&viewtype=",VIEWTYPE:
                             "&deftview=",DEFTVIEW:
                             "&datastrm=",DATASTRM;
          REP       "+ ",DATASTRM
.
PREV9999  RETURN
.------------------------------------------------------------
. Displays EDW Container List
.------------------------------------------------------------
EDWLST00  CALL      IBACLOCK            * Current Date
.
          PACK      KEY19,FRSTDATE,SP70
          CALL      RSCMEDW3
EDWLST20  CALL      RKCMEDW3
          BRANCH    OVRCD,EDWLST99
.
          MATCH     CMEDCDAT,LASTDATE
          GOTO      EDWLST99 IF LESS
.
          MATCH     SP70,DATASTRM
          IF        !@EQUAL
            MATCH      DATASTRM,CMEDDSTR
            GOTO       EDWLST20 IF NOT EQUAL
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:showFiles('",HTMLLINK
          APPEND    CMEDDSTR,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    CMEDCNTN,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      SP70,DSTREAM
          MATCH     "1",CMEDDSTR
          IF        @EQUAL
            MOVE      "Client",DSTREAM
          ENDIF
.
          MATCH     "3",CMEDDSTR
          IF        @EQUAL
            MOVE      "Individual Service Provider",DSTREAM
          ENDIF
.
          MATCH     "5",CMEDDSTR
          IF        @EQUAL
            MOVE      "Non Admitted Patient Service",DSTREAM
          ENDIF
.
          MATCH     "6",CMEDDSTR
          IF        @EQUAL
            MOVE      "Service Scheduling",DSTREAM
          ENDIF
.
          MATCH     "17",CMEDDSTR
          IF        @EQUAL
            MOVE      "Admitted Patient",DSTREAM
          ENDIF
.
          MATCH     "18",CMEDDSTR
          IF        @EQUAL
            MOVE      "Emergency Department",DSTREAM
          ENDIF
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:ReExtract('",HTMLLNK2
          APPEND    CMEDDSTR,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    CMEDCNTN,HTMLLNK2
          APPEND    "')",HTMLLNK2
          RESET     HTMLLNK2
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",DSTREAM,"#",":
                             "#"",CMEDCNTN,"#",":
                             "#"",CMEDSDAT,"#",":
                             "#"",CMEDEDAT,"#",":
                             "#"",CMEDCDAT,"#",":
                             "#"",CMEDRDAT,"#",":
                             "#"",HTMLLNK2,"#");";
.
          GOTO      EDWLST20
.
EDWLST99  RETURN
+
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       COMEDFIO/INC
          INC       COMEDWIO/INC
          INC       PATDO1IO/INC   * Doctor File
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PATALRIO/INC
          INC       OUTSITIO/INC
          INC       PATCOMIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATFN1IO/INC
          INC       PATHSPIO/INC
          INC       PATWR1IO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       PATCMNIO/INC
          INC       PATITMIO/INC
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATCMCIO/INC
          INC       PATASFIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       APSMASIO/INC
          INC       PATIN1IO/INC
          INC       PATDSCIO/INC
          INC       PMSUCDIO/INC
          INC       PMSUCHIO/INC
          INC       PMSCCDIO/INC            * Concession Card Details
.
          INC       VISCMTIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
.
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       PATMASTM
          INC       PATDOCTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PATDSCTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATMISTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK


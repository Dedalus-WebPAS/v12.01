.******************************************************************************
.* System    :   Emergency                                                    *
.* Program   :   IBAEMR54                                                     *
.* Desc      :   ED SA Health Department Transmission                         *
.******************************************************************************
.* Date      :   21/09/2022                                                   *
.* Author    :   Bella Turco                                                  *
.* Mods      :                                                                *
.******************************************************************************
.* V11.03.05  24/01/2023  Bella Turco            TSK 0919532                  *
.*            Fixed 'Indigenous Status' so it extracts HDP Equiv Posn 1       *
.*            instead of whole HDP Equiv value in VIST0000                    *
.* V11.03.04  22/12/2022  Bella Turco            TSK 0919532                  *
.*            Added conditional mandatory check in VALD0000 for XADMITS field *
.* V11.03.03  24/11/2022  Bella Turco            TSK 0919532                  *
.*            Added check in NEWS0000 if TEMP1 is empty the us1 script        *
.*            isn't run                                                       *
.* V11.03.02  21/11/2022  Bella Turco            TSK 0919532                  *
.*            Fixed positioning of "|" in PROC9000                            *
.* V11.03.01  17/11/2022  Bella Turco            TSK 0919532                  *
.*            Checked if IBCNMHOS=1 before displaying "Hospital :" in report  *
.* V11.02.00  21/09/2022  Bella Turco            TSK 0919532                  *
.*            Created Program                                                 *
.******************************************************************************
.
          INC       STD001FD
.
. File Description Includes
. -------------------------
          INC       IBAPOSFD/INC 
          INC       EMRVISFD/INC
          INC       EMRVCDFD/INC
          INC       EMRICDFD/INC
          INC       PMSCCDFD/INC
          INC       PATCT1FD/INC 
          INC       PATCMPFD/INC
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Control file
          INC       PATDSCFD/INC            * Discharge file
          INC       PATMI1FD/INC            * Admission file
          INC       IBACONFD/INC            * Control file
          INC       PATHSPFD/INC            * Hospital File
          INC       EMRCONFD/INC            * Emergency Control File
          INC       PATMA1FD/INC            * Master File
          INC       PMSVX1FD/INC            * Visit Extension File 
          INC       PMSPX2FD/INC            * PMI Extension File
          INC       PATVISFD/INC            * Visit file
.
. Text File Definition 
. -----------------------------------------
. 
TEMP1     FILE       ASCII, FIXED=557
.
.NAME     TYPE    LENGTH    START   DESCRIPTION
.----     ----    ------    -----   -----------
XVISITNO  DIM       49         1  * Visit Number (emrvisaf.demviadm)
XHOSPNO   DIM        4        50  * Hospital Number 
XURNO     DIM       10        54  * Patient U/R Number (emrvisaf.emviurno)
XPSEX     DIM       49        64  * Patient Sex (patma1af.psex)
XPGEND    DIM       49       113  * Patient Gender
XPDOB     DIM        8       162  * Patient Date of Birth (patma1af.pbdate)
XPCONT    DIM        4       170  * Patient Country of Birth (patma1af.pcont)
XPINDIG   DIM       49       174  * Indigenous Status (pmsvx1af.pmvxabrg)
XPSUBRB   DIM       49       223  * Suburb/Locality of Address 
.                                 * (patma1af.psubrb)
XPPOST    DIM        4       272  * Postcode (patma1af.ppost)
XINHCID   DIM       16       276  * Indiv Healthcare Identifier
XINHCRC   DIM        1       292  * Indiv Healthcare Identifier Rec Status
XINHCNU   DIM        1       293  * Indiv Healthcare Identifier Number
XPMEDI    DIM       10       294  * Medicare Number (patma1af.pmedi)
XMEDICOD  DIM        1       304  * Medicare IRN (patmx1af.ptmxmccd)
XCOMPCOD  DIM       49       305  * Hospital Insurance Status Code
.                                 * (emrvisaf.emvicomp)
XVIDATE   DIM       12       354  * Arrival Date/Time 
.                                 * (emrvisaf.emvidate/emvitime)
XVIMODE   DIM       49       366  * Mode of Arrival (emrvisaf.emvimode)
XVIDSTA   DIM        1       415  * Departure Status (emrvisaf.emvidsta)
XATTTYP   DIM        1       416  * Visit Type (emrvisaf.emviuc13)
XPRESCMP  DIM        4       417  * Presenting Complaint (emrvisaf.emviuc20)
XVISRCE   DIM       49       421  * Source of Referral (emrvisaf.emvisrce)
XADMITS   DIM        4       470  * Referral from Hospital (patdadaf.ptdaadts)
XDSCHREF  DIM       49       474  * Departure Referral (emrvisaf.emviuc11)
XTRIGCAT  DIM        1       523  * Triage Category (emrvisaf.emvitrig)
XSEENBY   DIM       12       524  * Seen By Date/Time
XDEPDATM  DIM       12       536  * Departure Date/Time 
.                                 * (emrvisaf.emviddat/emvidtim)
XPRNCDIA  DIM        9       548  * Principal Diagnosis Code
.
.End of Record               557 
.
. Local Variable Definition 
. -------------------------
.
.
CATAA     INIT      "AA"        * Category AA - Triage Category
CATC      INIT      "C "        * Category C - Country of Birth
CATCL     INIT      "CL"        * Category CL - Claim Type
CATEA     INIT      "EA"        * Category EA - Mode of Arrival
CATEC     INIT      "ec"        * Category ec - Departure Referral
CATED     INIT      "ED"        * Category ED - Departure Status
CATEE     INIT      "ee"        * Category ee - Visit Type
CATEL     INIT      "el"        * Category el - Presenting Complaint
CATEM     INIT      "em"        * Category em - Referral Source
CATG      INIT      "G "        * Category G - Sex
CATVA     INIT      "VA"        * Category VA - Indigenous Status
CMDLINE   DIM       100
CODE      DIM       3
CDAY1     DIM       2
CMON1     DIM       2
CCENT1    DIM       2
CYEAR1    DIM       2
CURDATE   DIM       8
DIM1A     DIM       1
DIM3      DIM       3
DIM4      DIM       4
DIM4A     DIM       4
DIM45     DIM       45
DIM6      DIM       6
DIM8      DIM       8
DOYR2022  FORM      1
ENDDTE    DIM       8           * End date for new transmission
ENDDTESL  DIM       10          * Slash formatted end date for new transmission
ERRDESC   DIM       70          * Error description
ERRCNT    FORM      9
ERRFND    FORM      1           * 0=No errors 1=Errors found
ERRORMSG  DIM       127
ERRTYPE   DIM       1
ERRORS    FORM      1
FILENAME  DIM       8
FNAMEB    DIM       8
FORM3     FORM      3
FROMDATE  DATE      8
HOSPNUM   DIM       4 
RECCNT    FORM      9
SAVADMN   DIM       8           * Saved Admission number
SAVURNO   DIM       8           * Saved UR number
SDATE     DIM       8
SEENBYNS  DIM       12        
NSF12     FORM      12
DRF12     FORM      12
MPF12     FORM      12
SEENBYDR  DIM       12
SEENBYMP  DIM       12
TABDELM   EQU       ","
TPSYFLG   FORM      1           * 1=Psychatric Admission 0=Not Psychiatric Adm
ZEROTEN   INIT      "0000000000"
z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
.
.---------------------------------------------------------------------------
PRGID     INIT      "IBAEMR54"
PRGNAM    INIT      "SA Health NAEC Extract"
VERSION   INIT      "V12.01.00"
.---------------------------------------------------------------------------
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          BRANCH    COPTION,ML1000         * New Submission
          GOTO      ML9999
.
ML1000    CALL      CLEA0000
          CALL      NEWS0000               * Process a new submission
.
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading 
. 
          DISPLAY   *P56:24,"Opening ibapostf";
          OPEN      IBAPOST1,"ibapostf"
. 
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      EMRVISA4,"emrvisaf"
.
          OPEN      EMRICDA1,"emricdaf"
.
          OPEN      EMRVCDA1,"emrvcdaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          READ      CONTROLF,TEN;*54,CFILENO:
                                 *94,CAGECOFF
          READ      CONTROLF,TEN3;*245,CHOSTYP
.
          READ      CONTROLF,TWENTY1;*220,PTNSWHDP,*216,PTCNDTRF
          READ      CONTROLF,EIGHTY8;*242,PNSWRACE:
                                     *59,PTCNI10D  
          READ      CONTROLF,SEVENTY9;*82,PTCNDSCI
          READ      CONTROLF,HUND22;*18,EMCNSDSA
.
          MOVE      CFILENO,XHOSPNO             * Hospital Number
          REP       " 0",XHOSPNO
.
          PACK      CURDATE,CCC,CYY,CMM,CDD 
          REP       " 0",CURDATE
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". New Submission";
.
OPTN0500  KEYIN     *P1:9,*EL,"Select Option : ",*DV,UNDLN1: 
                    *P16:9,*V2LON,COPTION                   
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                NEWS0000             Called by: ML0000    *
.*                             New Submission                               *
.****************************************************************************
.
NEWS0000  CALL      SDAT0000                    * Get Start Date From Parameter
          CALL      EDAT0000                    * Enter End Date
          CALL      KHOS0000                    * Enter Hospital ID
          CALL      FILE0000                    * Enter File Name
          MOVE      SP30,FNAMEB
.
          CLOSE     TEMP1
          PREP      TEMP1,FILENAME          * Open Extract File
.
.
          CALL      CONTQST                     * Ok to continue ?
          BRANCH    CEXIT,NEWS1000:             * yes
                          NEWS0000:             * no
                          NEWS9999              * cancel
.
NEWS1000  CALL      PROC0000                    * Process Discharge Records
.
          READ      TEMP1,ZERO;*1,ANS   * if TEMP1 is empty don't run us1 script
          IF        @OVER
            CLOSE     TEMP1,DELETE
            GOTO      NEWS9999
          ENDIF
.
.         Move Extract to Web Directory
.
NEWS2000  CLEAR     CMDLINE       
          APPEND    "ibaemr54.us1 ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    ".txt",CMDLINE
          APPEND    " ",CMDLINE
          APPEND    HOSPNUM,CMDLINE
          APPEND    " ",CMDLINE
          UNPACK    ENDDTE,DIM4,DIM2
          APPEND    DIM4,CMDLINE
          APPEND    " ",CMDLINE
          APPEND    DIM2,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID  
          MOVE      SP70,HOSPNUM
.
NEWS9999  RETURN
+
.****************************************************************************
.*                            MVEXT000                 Called by: ML0000    *
.*               Move Extract .txt to Web Directory                         *
.****************************************************************************
MVEXT000  CLEAR     CMDLINE
          APPEND    "ibaemr54.us1 ",CMDLINE
          APPEND    FILENAME,CMDLINE       
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
+
.****************************************************************************
.*                                SDAT0000             Called by: ML0000    *
.*                 Enter the start date for the new submission              *
.****************************************************************************
.
SDAT0000  PACK      SDATE,EMCNSDSA,SP70
          REP       " 0",SDATE
.
SDAT9999  RETURN
+
.**************************************************************************
.*                               EDAT0000              Called by: ML0000  *
.*                           Enter ending date                            *
.**************************************************************************
.
EDAT0000  DISPLAY   *P1:11,*EF,"Ending Date   : "
          MOVE      CDD,CDAY                     * default to current date
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          MOVE      TEN7,CCOL
          MOVE      TEN1,CVERT
          MOVE      ONE,CCANLDTE
          CALL      KEYDATE
          BRANCH    OVRCD,EDAT0000               * nothing entered
.
EDAT0001  PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          PACK      ENDDTESL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    
          REP       " 0",ENDDTE
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
          MATCH     ENDDTE,CURDATE                * after current date?
          GOTO      EDAT9000 IF LESS
.
          MATCH     ENDDTE,SDATE                  * after start date?
          GOTO      EDAT9999 IF LESS
          GOTO      EDAT9999 IF EQUAL
.
          DISPLAY   *P1:24,"Ending date must be greater than starting date ";
          CALL      HITENTER
          GOTO      EDAT0000
.
EDAT9000  DISPLAY   *P1:24,"Ending date cannot be in the future ";
          CALL      HITENTER
          GOTO      EDAT0000
.
EDAT9999  RETURN
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                            Get Extract Filename                            *
.******************************************************************************
FILE0000  MOVE      "13",CVERT
          DISPLAY   *P1:CVERT,*EL,"Extract File  : ";
FILE1000  
          UNPACK    CURDATE,CCENT,CYEAR,CMON,CDAY
          PACK      FILENAME,ANSN,ANSA,ANSE,ANSC
          KEYIN     *P17:CVERT,*EL,*V2LON,*RV,FILENAME;
.
          ENDSET    FILENAME
          APPEND    SP8,FILENAME
          RESET     FILENAME
.
          CMATCH    EXITCHAR,FILENAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
          MATCH     SP8,FILENAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
          STRIP     FILENAME
          SQUEEZE   FILENAME
          DISPLAY   *P17:CVERT,*EL,*V2LON,*+,FILENAME,".txt";
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP1,FILENAME          *Use TEMP1 from 01/07/2020
          TRAPCLR   IO
          BRANCH    OVRCD,FILE3000          * File doesnt exist
          CLOSE     TEMP1
.
FILE2000  KEYIN     *P1:24,*B,*EL,"An extraction file with this name":
                    " already exists. Overwrite it (",*V2LON,*DV,ANSY:
                    *HOFF,"/",*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      FILE9000 IF EQUAL       * Overwrite file
.
          MATCH     ANSN,ANS
          GOTO      FILE1000 IF EQUAL       * Keyin filename again
.
          BEEP
          GOTO      FILE2000
.
FILE3000  MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9000  MOVE      ZERO,EXIT
.
          MATCH     ANSY,ANS
          GOTO      FILE9600 IF EQUAL       * Overwrite file
.
          MATCH     ANSN,ANS
          GOTO      FILE9100 IF EQUAL       * Keyin filename again
          GOTO      FILE9100
.
FILE9100  MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9500  MOVE      ONE,EXIT
          GOTO      FILE9999
.
FILE9600  MOVE      TWO,EXIT
          GOTO      FILE9999
.
FILE9999  RETURN
+
.**************************************************************************
.*                               PROC0000                                 *
.*                    Loop through the discharge file                     *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,*P20:24,*V2LON,"** Processing Discharges **",*W:
                    *P20:24,*EL,"Admission No. : ";
          MOVE      ZERO,RECCNT
          MOVE      ZERO,ERRCNT
.
          PACK      FROMDATE,SDATE,SP70
          SUB       THIRTY,FROMDATE
.         
          PACK      KEY24,FROMDATE,SP70
          CALL      RSEMVIS4
PROC1000  CALL      RKEMVIS4
          BRANCH    OVRCD,PROC9000
.
          MATCH     "1",DEMVISTA            * bypass "Current"
          GOTO      PROC1000 IF EQUAL
.
          MATCH     "4",DEMVISTA            * bypass "Cancelled"
          GOTO      PROC1000 IF EQUAL
.
          MATCH     EMVIDDAT,SDATE           * check that Date is in range
          GOTO      PROC1000 IF NOT LESS
.
          MATCH     EMVIDDAT,ENDDTE
          GOTO      PROC1000 IF LESS
.
          IF        IBCNMHOS=1
            MATCH     SP70,PTHSHOSP
            GOTO      PROC9999 IF EQUAL
.
            PACK      KEY8,DEMVIADM,SP70
            CALL      RDPMVX11
            BRANCH    OVRCD,PROC1000
.
            MATCH     PMVXMHOS,PTHSHOSP
            GOTO      PROC1000 IF NOT EQUAL
.
          ENDIF
.
PROC8000  CALL      DETA0000                    * Get details for this discharge
.
          GOTO      PROC1000                    * Read next record
.
PROC9000  MOVE      ZERO,EXIT
          COMPARE   ONE,ERRORS
          IF        @EQUAL
            CALL      UND132
            PRINT     *N,"Number of errors             : ",ERRCNT
            PRINT     *1,"Number of visits extracted   : ",RECCNT
            PRINT     *N,"*** End of Report ***"
            DISPLAY   *P1:24,*EL,*V2LON,"Error Records Found. ";
            CALL      HITENTER
          ELSE
            CALL      IBACLOCK
            CALL      HEAD132
            UNPACK    EMCNSDSA,CCENT,CYEAR,CMON,CDAY 
            PRINT     *40,PRGNAM
            PRINT     *40,"From Discharge Date : ",CDAY,"/",CMON,"/",CCENT,CYEAR," To : ",ENDDTESL
            COMPARE   ONE,IBCNMHOS
            IF        @EQUAL
              PRINT     *40,"Hospital : ",PTHSNAME
            ENDIF
            PRINT     *N;     
            CALL      UND132
            PRINT     *1,"| No Errors Found",*132,"|"
            CALL      UND132
            PRINT     *1,"*** End of Report ***"            
          ENDIF
.
          DISPLAY     *P1:24,*EL,*V2LON,RECCNT," Records Found. ";
          CALL        HITENTER
.
PROC9999  RETURN
+
.**************************************************************************
.*                               DETA0000                                 *
.*                      Get the details for a discharge                   *
.**************************************************************************
.
DETA0000  CALL      VHCD0000                    * Validate hospital code
          CALL      MAST0000                    * Master Details
          CALL      EMVI0000                    * Emergency Details
          CALL      VIST0000                    * Visit Details 
          CALL      EMSY0000                    * Emergency System Details
          CALL      VALD0000                    * Validate data
          BRANCH    EXIT,DETA9999
.
          CALL      WRIT0000                    * Write to Extract File
          CALL      CLEA0000                    * Clear variables
          ADD       ONE,RECCNT
.
DETA9999  RETURN
+
.**************************************************************************
.*                               VHCD0000                                 *
.*                        Validate Hospital Code                          *
.**************************************************************************
.
VHCD0000  COMPARE   ONE,IBCNMHOS
          IF        @EQUAL
            UNPACK    PTHSHSID,DIM4
          ELSE
            UNPACK    CFILENO,DIM4
          ENDIF
.
          MOVE      DIM4,XHOSPNO
          MOVE      XHOSPNO,HOSPNUM
          RESET     DIM4
.
VHCD9999  RETURN
.**************************************************************************
.*                               MAST0000              Called by: DETA0000*
.*                      Get Master file Details                           *
.**************************************************************************
.
.
MAST0000  PACK      KEY8,EMVIURNO,SP70             * Read the master file record
          CALL      RDMAST1
          BRANCH    OVRCD,MAST9000
.
          MATCH     SP70,PSEX
          IF        !@EQUAL
            PACK      KEY5,CATG,PSEX,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XPSEX,TCINDC3                    * Sex
            ENDIF
          ENDIF
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY     * Birth Date
          PACK      XPDOB,CCENT,CYEAR,CMON,CDAY
.         
          MATCH     SP70,PCONT
          IF        !@EQUAL
            PACK      KEY5,CATC,PCONT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TCFORM6,FORM4
              PACK      XPCONT,FORM4                     * Birth Country
              REP       " 0",XPCONT
            ENDIF
          ENDIF
.
          PACK      XPSUBRB,PSUBRB                   * Suburb
.
          PACK      XPPOST,PPOST                     * Post code
.
          PACK      XPMEDI,PMEDI                     * Medicare number
.
          PACK      XMEDICOD,PTMXMCCD                * Medicare IRN
.
MAST9000  DISPLAY   *P1:24,*V2LON,SAVURNO," *** Missing Master file record ***"
.
MAST9999  RETURN
+
.**************************************************************************
.*                               EMVI0000                                 *
.*                      Get Emergency File Details                        *
.**************************************************************************
.
EMVI0000  PACK      KEY8,DEMVIADM,SP70          * Read the emergency file record
          CALL      RDEMVIS1
          BRANCH    OVRCD,EMVI9000
.
          PACK      XVISITNO,DEMVIADM          * Visit Number
.
          PACK      XURNO,EMVIURNO             * UR Number
.
          MATCH     SP70,EMVICOMP
          IF        !@EQUAL
            PACK      KEY5,CATCL,EMVICOMP,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XCOMPCOD,TCINDC7           * Hospital Insurance
            ENDIF
          ENDIF
.
          MATCH     SP70,EMVIMODE
          IF        !@EQUAL
            PACK      KEY5,CATEA,EMVIMODE,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XVIMODE,PTCDUDF4           * Mode of Arrival
            ENDIF
          ENDIF
.
          MATCH     SP70,EMVIDSTA
          IF        !@EQUAL
            PACK      KEY5,CATED,EMVIDSTA,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XVIDSTA,PTCDUDF4           * Departure Status
            ENDIF
          ENDIF
.
          MATCH     SP70,EMVIUC13
          IF        !@EQUAL
            PACK      KEY5,CATEE,EMVIUC13,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XATTTYP,THCSCOD            * Visit Type
            ENDIF
          ENDIF
.
          MATCH     SP70,EMVIUC20
          IF        !@EQUAL
            PACK      KEY5,CATEL,EMVIUC20,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XPRESCMP,PTCDUDF5          * Presenting Complaint
            ENDIF
          ENDIF
.
          MATCH     SP70,EMVIUC21
          IF        !@EQUAL
            PACK      KEY5,CATEM,EMVIUC21,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XVISRCE,PTCDUDF4           * Source of Referral
            ENDIF
          ENDIF
.
          MATCH     SP70,EMVIUC11
          IF        !@EQUAL
            PACK      KEY5,CATEC,EMVIUC11,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XDSCHREF,THCSCOD           * Departure Referral
            ENDIF
          ENDIF
.
          MATCH     SP70,EMVITRIG
          IF        !@EQUAL
            PACK      KEY5,CATAA,EMVITRIG,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XTRIGCAT,TCINDC1           * Triage Category
            ENDIF
          ENDIF
.
          UNPACK    EMVIDTIM,CHOUR,D1,CMIN
          PACK      XDEPDATM,EMVIDDAT,CHOUR,CMIN   * Departure Date/Time 
.
          PACK      XADMITS,EMVITSRC           * Referral from Hospital
.
          UNPACK    EMVITIME,CHOUR,D1,CMIN
          PACK      XVIDATE,EMVIDATE,CHOUR,CMIN   * Presentation Date/Time
.
          UNPACK    EMVINSTM,CHOUR,D1,CMIN     * Seen by Nurse
          PACK      SEENBYNS,EMVINSDT,CHOUR,CMIN
          MOVE      "999999999999",NSF12
          TYPE      SEENBYNS
          IF        @EQUAL
            MOVE      SEENBYNS,NSF12
          ENDIF
.
          UNPACK    EMVIDRTM,CHOUR,D1,CMIN     * Seen by Doctor
          PACK      SEENBYDR,EMVIDRDT,CHOUR,CMIN
          MOVE      "999999999999",DRF12
          TYPE      SEENBYDR
          IF        @EQUAL
            MOVE      SEENBYDR,DRF12
          ENDIF
.
          UNPACK    EMVIMPTM,CHOUR,D1,CMIN     * Seen by Mental Health Pract
          PACK      SEENBYMP,EMVIMPDT,CHOUR,CMIN
          MOVE      "999999999999",MPF12
          TYPE      SEENBYMP
          IF        @EQUAL
            MOVE      SEENBYMP,MPF12
          ENDIF
.
          IF        NSF12 <= DRF12 & NSF12 <= MPF12
            PACK      XSEENBY,SEENBYNS
          ENDIF
.
          IF        DRF12 <= NSF12 & DRF12 <= MPF12
            PACK      XSEENBY,SEENBYDR
          ENDIF
.
          IF        MPF12 <= NSF12 & MPF12 <= DRF12
            PACK      XSEENBY,SEENBYMP
          ENDIF
.
EMVI9000  DISPLAY   *P1:24,*V2LON,SAVURNO," *** Missing Emergency file record ***"
.
EMVI9999  RETURN
+
.**************************************************************************
.*                               VIST0000                                 *
.*                      Get Visit file Details                            *
.**************************************************************************
.
VIST0000  PACK      KEY8,DEMVIADM,SP70         * Read the visit file record
          CALL      RDPMVX11
          BRANCH    OVRCD,VIST9000
.
          MATCH     SP70,PMVXABRG
          IF        !@EQUAL
            PACK      KEY5,CATVA,PMVXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              UNPACK    THCSCOD,DIM1
              PACK      XPINDIG,DIM1              * Indigenous Status
            ENDIF
          ENDIF
.
VIST9000  DISPLAY   *P1:24,*V2LON,SAVURNO," *** Missing Visit file record ***"
.
VIST9999  RETURN
.**************************************************************************
.*                               EMSY0000                                 *
.*                      Get Emergency System Details                      *
.**************************************************************************
.
EMSY0000  PACK      KEY14,EMVIADMN,ZERO,ZERO,FIVE,SP70
          CALL      RSEMVCD1                     * position for admission no.
EMSY1500  CALL      RKEMVCD1                     * read next record
          BRANCH    OVRCD,EMSY9999               * eof - finished primary diag.
.
          MATCH     EMVCVIST,DEMVIADM            * same visit no. still ?
          GOTO      EMSY9999 IF NOT EQUAL        * no - finished prim. diag.
.
          MATCH     "000",EMVCSEQU               * primary diagnosis ?
          GOTO      EMSY1500 IF NOT EQUAL        * yes - ignore
.
          MATCH     "005",EMVCTYPE
          GOTO      EMSY9999 IF NOT EQUAL
.
          MOVE      EMVCMNCD,KEY9
          CALL      RDEMICD1
          BRANCH    OVRCD,EMSY2000
.
          PACK      XPRNCDIA,EMICVEMD
.
EMSY2000  MOVE      "E261:DIAGNOSIS CODE INVALID",ERRORMSG
          MOVE      ANSR,ERRTYPE
.          CALL      PRTERR00
.
EMSY9999  RETURN
.**************************************************************************
.*                               VALD0000                                 *
.*                          Validate mandatory fields                     *
.**************************************************************************
VALD0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,XVISITNO
          IF        @EQUAL
            MOVE      "Identifier",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XHOSPNO
          IF        @EQUAL
            MOVE      "Hospital Number",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XURNO  
          IF        @EQUAL
            MOVE      "Patient Unit Record Number",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XPSEX  
          IF        @EQUAL
            MOVE      "Sex Native",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XPDOB
          IF        @EQUAL
            MOVE      "Birth Date",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XPCONT
          IF        @EQUAL
            MOVE      "Birth Country Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XPINDIG
          IF        @EQUAL
            MOVE      "Indigenous Status Native Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XPSUBRB
          IF        @EQUAL
            MOVE      "Patient Locality Name",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XPPOST
          IF        @EQUAL
            MOVE      "Patient Postcode",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XPMEDI
          IF        @EQUAL
            MOVE      "Medicare Number",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XMEDICOD
          IF        @EQUAL
            MOVE      "Medicare IRN",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XCOMPCOD
          IF        @EQUAL
            MOVE      "Hospital Insurance Status Native Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XVIDATE
          IF        @EQUAL
            MOVE      "Presentation Date/Time",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XVIMODE
          IF        @EQUAL
            MOVE      "Arrival Mode Native Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XVIDSTA
          IF        @EQUAL
            MOVE      "Departure Status Native Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XATTTYP
          IF        @EQUAL
            MOVE      "Visit Type Native Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XPRESCMP
          IF        @EQUAL
            MOVE      "Presentation Problem Native Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XVISRCE
          IF        @EQUAL
            MOVE      "Presentation Referral Source Native Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XADMITS
          IF        @EQUAL
            MATCH     SP70,EMVIUC21
            IF        !@EQUAL
              PACK      KEY5,CATEM,EMVIUC21,SP70
              CALL      RDCODE1
              IF        OVRCD<>1
                MATCH     "1",TCINDC3
                IF        @EQUAL
                  MOVE      "Referral From Hospital Native Code",ERRDESC
                  MOVE      ONE,EXIT
                  CALL      PRINTERR
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,XDSCHREF
          IF        @EQUAL
            MOVE      "Departure Referral Native Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XTRIGCAT
          IF        @EQUAL
            MOVE      "Triage Category Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XSEENBY
          IF        @EQUAL
            MOVE      "Seen By Date/Time",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XDEPDATM
          IF        @EQUAL
            MOVE      "Departure Date/Time",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
          MATCH     SP70,XPRNCDIA
          IF        @EQUAL
            MOVE      "Principal Diagnosis Code",ERRDESC
            MOVE      ONE,EXIT
            CALL      PRINTERR
          ENDIF
.
VALD9999  RETURN
+
.**************************************************************************
.*                               PRINTERR                                 *
.*                          Print error report                            *
.**************************************************************************
.
PRINTERR  COMPARE   ZERO,ERRFND
          IF        @EQUAL
            CLOCK     TIME,CTIMEIS
            MOVE      ONE,ERRFND
            MOVE      ONE,ERRORS
            CALL      HEAD132
            UNPACK    EMCNSDSA,CCENT,CYEAR,CMON,CDAY 
            PRINT     *40,PRGNAM
            PRINT     *40,"From Discharge Date : ",CDAY,"/",CMON,"/",CCENT,CYEAR," To : ",ENDDTESL
            COMPARE   ONE,IBCNMHOS
            IF        @EQUAL
              PRINT     *40,"Hospital : ",PTHSNAME
            ENDIF
            PRINT     *N;     
            CALL      UND132
            PRINT     *1,"|","SA Health NAEC Extract Errors",*132,"|"
            CALL      UND132
            MOVE      "6",CLNO
          ENDIF
.
          COMPARE   FIFTY5,CLNO
          IF        !@LESS
            CALL      HEAD132
            UNPACK    EMCNSDSA,CCENT,CYEAR,CMON,CDAY
            PRINT     *40,PRGNAM
            PRINT     *40,"From Discharge Date : ",CDAY,"/",CMON,"/",CCENT,CYEAR," To : ",ENDDTESL
            COMPARE   ONE,IBCNMHOS
            IF        @EQUAL
              PRINT     *40,"Hospital : ",PTHSNAME
            ENDIF
            PRINT     *N;
            CALL      UND132
            PRINT     *1,"|","SA Health NAEC Extract Errors",*132,"|"
            CALL      UND132
            MOVE      "6",CLNO
          ENDIF
.
          PRINT     *1,"| Visit ",DEMVIADM," is missing a value in the mandatory field ",ERRDESC,*132,"|"
          ADD       ONE,CLNO
          ADD       ONE,ERRCNT
.
          RETURN
+
.**************************************************************************
.*                               CLEA0000              Called by: DETA0000*
.*                          Clear variables                               *
.**************************************************************************
.
CLEA0000  PACK      XVISITNO,SP70
          PACK      XHOSPNO,SP70 
          PACK      XURNO,SP70   
          PACK      XPSEX,SP70   
          PACK      XPGEND,SP70  
          PACK      XPDOB,SP70   
          PACK      XPCONT,SP70  
          PACK      XPINDIG,SP70 
          PACK      XPSUBRB,SP70 
          PACK      XPPOST,SP70  
          PACK      XINHCID,SP70 
          PACK      XINHCRC,SP70 
          PACK      XINHCNU,SP70 
          PACK      XPMEDI,SP70  
          PACK      XMEDICOD,SP70
          PACK      XCOMPCOD,SP70
          PACK      XVIDATE,SP70
          PACK      XVIMODE,SP70 
          PACK      XVIDSTA,SP70 
          PACK      XATTTYP,SP70
          PACK      XPRESCMP,SP70
          PACK      XVISRCE,SP70
          PACK      XADMITS,SP70
          PACK      XDSCHREF,SP70
          PACK      XTRIGCAT,SP70
          PACK      XSEENBY,SP70
          PACK      XDEPDATM,SP70
          PACK      XPRNCDIA,SP70
.
CLEA9999  RETURN
+
.**************************************************************************
.*                               WRIT0000              Called by: ML0000  *
.*                      Write to the extract file                         *
.**************************************************************************
.
WRIT0000  WRITE     TEMP1,SEQ;XVISITNO,TABDELM:  * Visit Number
                              XHOSPNO,TABDELM:   * Hospital Number
                              XURNO,TABDELM:     * Patient U/R Number
                              XPSEX,TABDELM:     * Patient Sex
                              XPGEND,TABDELM:    * Patient Gender
                              XPDOB,TABDELM:     * Patient Date of Birth
                              XPCONT,TABDELM:    * Patient Country of Birth  
                              XPINDIG,TABDELM:   * Indigenous Status
                              XPSUBRB,TABDELM:   * Suburb/Locality of Address
                              XPPOST,TABDELM:    * Postcode
                              XINHCID,TABDELM:   * Indiv Healthcare Identifier
                              XINHCRC,TABDELM:   * Indiv Healthcare Identifier
.                                                * Rec Status
                              XINHCNU,TABDELM:   * Indiv Healthcare Identifier
.                                                * Number
                              XPMEDI,TABDELM:    * Medicare Number
                              XMEDICOD,TABDELM:  * Medicare IRN
                              XCOMPCOD,TABDELM:  * Hospital Insurance Status 
.                                                * Code
                              XVIDATE,TABDELM:   * Arrival Date/Time
                              XVIMODE,TABDELM:   * Mode of Arrival
                              XVIDSTA,TABDELM:   * Departure Status
                              XATTTYP,TABDELM:   * Visit Type
                              XPRESCMP,TABDELM:  * Presenting Complaint
                              XVISRCE,TABDELM:   * Source of Referral
                              XADMITS,TABDELM:   * Referral from Hospital
                              XDSCHREF,TABDELM:  * Departure Referral
                              XTRIGCAT,TABDELM:  * Triage Category
                              XSEENBY,TABDELM:   * Seen By Date/Time
                              XDEPDATM,TABDELM:  * Departure Date/Time
                              XPRNCDIA,TABDELM  * Principal Diagnosis Code
.
          GOTO     WRIT9999
.
WRIT9999  RETURN
+
+
.******************************************************************************
.*                                  KHOS0000       Called by:                 *
.*                For multi-hospital, get the hospital code                   *
.******************************************************************************
.
KHOS0000  COMPARE   ONE,IBCNMHOS                 * using multi-hospital ?
          GOTO      KHOS9999 IF NOT EQUAL        * no - finished
.
          DISPLAY   *P1:12,*EL,"Hospital Id   : ";
          MOVE      TEN7,ECOL
          MOVE      "12",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND                * Not Mandatory
.
          CALL      PATHSPKY                     * get code
          BRANCH    EXIT,KHOS2000:               * nothing input
                         KHOS9500                * exitchar input
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          DISPLAY   *P17:12,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS3000  DISPLAY   *P21:12,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
.
KHOS9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       SEXDSCIO
          INC       EMRVISIO/INC
          INC       EMRICDIO/INC
          INC       EMRVCDIO/INC
          INC       IBAPOSIO/INC
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PMSVX1IO/INC            * Visit Extension File 
          INC       PMSPX2IO/INC            * PMI Extension File
          INC       PATVISIO/INC
.
          INC       PATHSPKY/PBL

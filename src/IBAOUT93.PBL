.******************************************************************************
.* System   : Outpatients System/Patient Billing System                       *
.* Program  : IBAOUT93                                                        *
.* Desc     : Invoice Pending by Clinic                                       *
.******************************************************************************
.* Date     :  21/01/1992                                                     *
.* Author   :  Graeme Williams                                                *
.* Mods     :                                                                 *
.*----------------------------------------------------------------------------*
.*        V11.02.01 03/03/2022  J.Tan           TSK 0902652                   *
.*                  Recompiled for OUTCATBI - New Patient Invoice             *
.*        V11.01.01 17/11/2021  J.Tan          TSK 0913621                    *
.*                  Recompiled for ABFOTINV - Mod for Multiple NEP Values     *
.*        V11.00.03 01/07/2020 J.Tan           TSK 0886178                    *
.*                  Recompiled for OUTCATBI - write to invoice pending details*
.*        V11.00.02 29/06/2020  J.Tan           TSK 0893767                   *
.*                  Recompiled for ABFOTINV - Changed CALCFLAG to 01/01/2021  *
.*        V11.00.01 20/04/2020 J.Tan           TSK 0890733                    *
.*                  Recompiled for ABFOTINV - ABF new 2020 calculation        *
.*        V10.15.01 11/10/2019  Peter Vela       TSK 0882906                  *
.*                  Recompiled for OUTCATBI - Fixed I*C for OUTBB1 ABF        *
.*        V10.14.01 21/05/2019  J.Tan           TSK 0857392                   *
.*                  Recompiled for ABF Invoices                               *
.*        V10.13.03 11/01/2019  Peter Vela     TSK 0867807                    *
.*                  Recompiled for OUTCATBI                                   *
.*        V10.13.02 26/10/2018  Thanh T        TSK 0859743                    *
.*                  Added BULKBILL field and removed OUTHEDFD/OUTHEDIO,       *
.*                  OUTSESFD/OUTSESIO for OUTCATBI changes                    *
.*        V10.13.01 25/10/2018  Thanh T        TSK 0859743                    *
.*                  Added OUTHEDFD/OUTHEDIO, OUTSESFD/OUTSESIO for OUTCATBI   *
.*                  changes                                                   *
.*----------------------------------------------------------------------------*
.*        V10.07.01 04/11/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.05.01 19/12/2014  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 21/12/2010  Mike Laming   CAR 233046                      *
.*                  Recompiled for OUTCATBI - Mods to Misc.Charges PATMCHFD   *
.*        V9.07.01  01/08/2006  J.Tan         CAR 103365                      *
.*                  Recompiled for PATCATBI - Johns Hopkins                   *
.*        V9.04.01  30/08/2004 S.Barcham   52835                              *
.*                  Include PMSVX1FD & IO                                     *
.******************************************************************************
.*        V9.03.03  19/12/2003 J.Tan   CAR 44093                              *
.*                  Replaced patcashf with comdepaf                           *
.*        V9.03.02  20/10/2003  J.Tan    CAR 44172                            *
.*                  Recompiled for OUTCATBI - Misc.theatre item file(pmsmitaf)*
.*        V9.03.01  17/09/2003  Lina Vo                                       *
.*                  Recompiled for OUTCATBI, PATDINVS, CMXMATRX.              *
.*        V9.02.02  07/05/2002  J.Tan                                         *
.*                  Recompiled for release 6.09 changes                       *
.*        V9.02.01  07/12/2001  Tonii                                         *
.*                  Recompiled for PATDINVS                                   *
.*        V9.02.00  13/10/2001  J.Tan                                         *
.*                  Ported from 5.09                                          *
.*        V5.09.01  22/09/2001  Tonii SRF 13075                               *
.*                  Recompiled for PATDINVS                                   *
.******************************************************************************
.*       V5.08.06   24/10/2000  Dean Cramer   SRF 6710                        *
.*                  Recomplied for OUTCATBI                                   *
.*                  Changed for health fund table code size increase to       *
.*                  8 characters                                              *
.*       V5.08.05   19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*       V5.08.04   23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*       V5.08.03   28/06/2000  Steve Downing                                 *
.*                  Recompiled for PATFIGFD/IO                                *
.*       V5.08.02   29/05/2000  Caleb Sharman  SRF 639169                     *
.*                  Recompiled for OUTCATBI                                   *
.*       V5.08.B05  02/04/2000  Davin                                         *
.*                  Fixed global recompile errors                             *
.*       V5.08.B04  16/03/2000  Steve Armstrong    5.08 Mods                  *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.*       V5.08.B03  07/03/2000  J.Tan                                         *
.*                  Recompiled for OUTCATBI - mods for GST                    *
.*       V5.07.B02  13/01/1999  Jim Stathopoulos                              *
.*                  Fixed Global Compile Errors                               *
.*       V5.07.B01  02/11/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.05.01  14/10/1997  Steve Armstrong    SRF 643393                 *
.*                  Recompiled for removal of THC invoice message             *
.******************************************************************************
.*        V5.04.01  15/06/1996  Greg Horvat      SRF 616143                   *
.*                  Recompiled for OUTCATAI - Added new invoice layout for    *
.*                  MBF - layout 17                                           *
.*                                                                            *
.* ************************************************************************** *
.*                                                                            *
.*             V5.01.02  23/03/92 J.Tan   SRF 113203                          *
.*             Recompiled for OUTCATBI (5.01.02)                              *
.*             V5.01.03  07/04/92 J.Tan   SRF 114007                          *
.*             Recompiled for OUTCATBI (5.01.03)                              *
.*             V5.01.04  08/05/92  ROD    SRF 114110                          *
.*             Make sure invoice is on pending file                           *
.*        V5.01.05  12/06/92  Graeme Williams               SRF 114865        *
.*                  Changes for 9 character misc items                        *
.*        V5.01.06  06/11/1992  Graeme Williams                               *
.*                  Recompiled for OUTCATBI   (5.01.05)                       *
.*        V5.01.07  24/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.*        V5.01.08  04/07/1994  Rob Leonard   Quote 8064                      *
.*                  Recompiled for OUTCATBI                                   *
.*        V5.02.01  02/03/1995  Greg Horvat      SRF 134196  Quote 8277       *
.*                  Recompiled for OUTCATBI                                   *
.*        V5.03.B2  24/05/1995  Paul Howells     SRF 134644                   *
.*                  Check whether using invoice pending file before writing.  *
.*        V5.03.01  01/08/1995  Greg Horvat      SRF 610897                   *
.*                  Recompiled for PATDINVS                                   *
.*        V5.03.02  17/08/1995  Paul Howells     SRF 611251                   *
.*                  Moved PTCNIPEN
.*                                                                            *
.******************************************************************************
.
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       IBACONFD/INC
          INC       IBAGSTFD/INC
          INC       IBAGEDFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCLIFD/INC
          INC       OUTCONFD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
          INC       OUTHTRFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       COMDEPFD/INC
          INC       PATDINVS/INC
          INC       PATDO1FD/INC
          INC       PATDTRFD/INC
          INC       PATFIGFD/INC
          INC       PATFINFD/INC
          INC       PATHSPFD/INC
          INC       PATINVFD/INC
          INC       PATIPEFD/INC
          INC       PATIN1FD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATMCHFD/INC
          INC       PATRE1FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATWC1FD/INC
          INC       PATWMAFD/INC
          INC       PATWVEFD/INC
          INC       PMSMTIFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
CLINDFRM  DIM       20
CLINDTO   DIM       20
CLINFROM  DIM       6
CLINTO    DIM       6
CLINPFRM  DIM       6
CLINPTO   DIM       6
CMDLINE   DIM       70
CURRDATE  DIM       8
CURRCLIN  DIM       6
BULKBILL  DIM       1
.
DIM4      DIM       4
DIM30     DIM       30
DIM40     DIM       40
.
FNAME     DIM       8
.
NACCOM    FORM      5.2
NRECS     FORM      8
NDAYCH    FORM      8
.
PCHARGE   FORM      5.2
.
SP14      DIM       14
USERID    DIM       10
WBSEUID   DIM       10
.
TEMPKEY   DIM       20        * key for temporary index file
.
VISTDATE  DIM       8
.
.
.         Web Variables (only the include files use these variables)
.
DAYFORMT  DIM       4
NJAN      INIT      "January"
NFEB      INIT      "February"
NMAR      INIT      "March"
NAPR      INIT      "April"
NMAY      INIT      "May"
NJUN      INIT      "June"
NJUL      INIT      "July"
NAUG      INIT      "August"
NSEP      INIT      "September"
NOCT      INIT      "October"
NNOV      INIT      "November"
NDEC      INIT      "December"
.
HTMLFILE  FIFO      ASCII, FIXED=250
MTHNAM3   DIM       3
MTHNAM3A  DIM       3
.
.         end web variables
.
.         Temporary index file
.
SRTKEY    DIM       30
SRTKEY1   INIT      "U1-6,15-22,7-14"
SRTKEY2   INIT      "U1-6,23-42,43-67"
SRTKEY3   INIT      "U1-6,7-14"
.
TEMPFILE  IFILE     SQL, FIXED=68, KEY=SRTKEY
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
XCLIN     DIM       6         6         1         Clinic code
XADMN     DIM       8         8         7         Outpatient number
XURNO     DIM       8         8         15        U/R number
XSNAME    DIM       20        20        23        Surname
XGNAME    DIM       25        25        43        Given name
.                        End of record  68
.
.         Program constants
.
CODECL    INIT      "CL"
.
FINISH    INIT      "Finish              "
.
ISERASE   INIT      "iserase "
.
SP12      INIT      "            "
START     INIT      "Start               "
.
TEMPRLEN  INIT      " 68 "
.
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT93"
PRGNAM    INIT      "Invoices Pending By Clinic"
VERSION   INIT      "V12.01.00"
.
. *****************************************************************************
. * ML0000     : Main line code                                               *
. *****************************************************************************
.
ML0000    CALL      INIT0000                 * program initialisation
.
ML0100    CALL      OPTN0000                 * select option
          BRANCH    OPTION,ML1000:           * U/R sequence selected
                           ML1000:           * Surname sequence selected
                           ML1000            * Outpatient no. sequence selected
          GOTO      ML9999
.
.         Get report parameters (range of clinics, and attendance date)
.
ML1000    CALL      KCLI0000                 * get the range of clinics
          BRANCH    EXIT,ML0100              * nothing input
.
          CALL      KDTE0000                 * get date of attendance
.
          CALL      CONTQST                  * ok to continue ?
          BRANCH    CEXIT,ML1200,ML1000,ML0100
.
ML1200    CALL      CRET0000                 * create temporary index file
          CALL      STPR0000                 * initialise variables for print
          CALL      LOAD0000                 * load data into temp file
          CALL      REP0000                  * generate report
          CALL      DELT0000                 * delete temporary index file
          GOTO      ML0100
.
ML9999    CHAIN     PGM
.
. *****************************************************************************
. * INIT0000   : Program initialisation                                       *
. *****************************************************************************
.
INIT0000  CALL      DISPHEAD                 * display standard heading
.
          MOVE      UID,OSTFILE
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*85,IBCNUGST
          READ      CONTROLF,TEN;*217,CAUDQ    * added for V5.01.14 - SRF 105195
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TEN5;*247,CSTDOWN
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,TWENTY;*164,PTRESDAY,*193,PTCNDCLM:
                                    *199,PTCNBCUT
          READ      CONTROLF,THIRTY1;*163,OTCNFTYP,OTCNCFEE
          READ      CONTROLF,FIFTY9;*1,CDLSTEP,*5,CREMINV:
                                   *66,CGSTPERC,CGSTITEM
          READ      CONTROLF,SEVENTY9;*248,PTCNIPEN
          READ      CONTROLF,SEVENTY9;*68,PTCNMFEE,PTCNJFEE
          READ      CONTROLF,EIGHTY8;*162,PTCFEEOF           
.
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P64:24,CFNAME
          CALL       OPOTBB11
.
          PACK      CFNAME,UID,FILBB1A2
          DISPLAY   *P64:24,CFNAME
          CALL       OPOTBB12
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILDTRO1
          DISPLAY   *P64:24,CFNAME
          OPEN      OUTDTRO1,CFNAME
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P64:24,"comdepaf";
          OPEN      COMDEPA2,"comdepaf"
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P64:24,"patfinsf";
          OPEN      PATFINS1,"patfinsf"
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
.
          IF        PTCNIPEN=0
            DISPLAY   *P64:24,"patipenf";
            OPEN      PATIPEN1,"patipenf"
          ENDIF
.
          DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P64:24,"patmchaf";
          OPEN      PATMCHG1,"patmchgf"
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
          DISPLAY   *P64:24,"patwc1af";
          OPEN      PATWC1A1,"patwc1af"
          DISPLAY   *P64:24,"patwmabf";
          OPEN      PATWMAB1,"patwmabf"
          DISPLAY   *P64:24,"patwvetf";
          OPEN      PATWVET1,"patwvetf"
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
.
. *****************************************************************************
. * OPTN0000   : Select option for report                                     *
. * Returns    : OPTION   - 0 = Exit                                          *
. *                         1 = U/R sequence                                  *
. *                         2 = Surname sequence                              *
. *                         3 = Admission number sequence                     *
. *****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = U/R No. sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Surname sequence":
                    *P1:7,*V2LON,THREE,*HOFF," = Outpatient No. sequence":
                    *P1:9,"Select option ? ";
.
OPTN1000  KEYIN     *P17:9,*DV,UNDLN2,*EL:
                    *P17:9,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
.
          DISPLAY   *P17:9,*V2LON,OPTION
.
          BRANCH    OPTION,OPTN8100,OPTN8200,OPTN8300
          BEEP
          GOTO      OPTN1000
.
OPTN8100  MOVE      "(U/R No. seq.)",CPHDROPT
          GOTO      OPTN9999
.
OPTN8200  MOVE      "(Surname seq.)",CPHDROPT
          GOTO      OPTN9999
.
OPTN8300  MOVE      "(O/P No. seq.)",CPHDROPT
          GOTO      OPTN9999
.
OPTN9999  RETURN
.
. *****************************************************************************
. * KCLI0000   : Keyin the clinic range for the report                        *
. * Returns    : EXIT     - 0 = Clinic range input                            *
. *                         1 = EXITCHAR input                                *
. *              CLINFROM - Starting clinic for report (for report)           *
. *              CLINTO   - Ending clinic for report (for report)             *
. *              CLINPFRM - Starting clinic for report (for display/print)    *
. *              CLINPTO  - Ending clinic for report (for display/print)      *
. *              CLINDFRM - Starting clinic for report (description)          *
. *              CLINDTO  - Ending clinic for report (description)            *
. *****************************************************************************
.
KCLI0000  DISPLAY   *P1:11,*EF:
                    *P1:11,"Starting clinic : ":
                    *P1:13,"Ending clinic   : ";
.
          MOVE      TEN1,CVERT               * row for keyin
          MOVE      TEN9,CCOL                * column for keyin
          CALL      CLIN0000                 * keyin a clinic code
          BRANCH    EXIT,KCLI1000:           * nothing input
                         KCLI9000            * exitchar input
.
.         Clinic code input
.
          MOVE      OCACLIN,CLINFROM         * from clinic code
          MOVE      OCACLIN,CLINPFRM         * from clinic code
          MOVE      OCADESC,CLINDFRM         * from clinic description
          GOTO      KCLI4000
.
.         Nothing input for from ward code
.
KCLI1000  MOVE      SP6,CLINFROM             * from clinic code
          MOVE      SP6,CLINPFRM             * from clinic code
          MOVE      START,CLINDFRM           * from clinic desc. ("Start")
.
.         Display ward code and description
.
KCLI4000  DISPLAY   *P19:11,*V2LON,CLINPFRM,*HOFF,*P28:11,CLINDFRM
.
.         Input the to clinic code
.
          MOVE      TEN3,CVERT               * row for keyin
          MOVE      TEN9,CCOL                * column for keyin
          CALL      CLIN0000                 * keyin a clinic code
          BRANCH    EXIT,KCLI6000:           * nothing input
                         KCLI9000            * exitchar input
.
.         Clinic code input
.
          MOVE      OCACLIN,CLINTO           * to clinic code
          MOVE      OCACLIN,CLINPTO          * to clinic code
          MOVE      OCADESC,CLINDTO          * to clinic description
          GOTO      KCLI8000
.
.         Nothing input for to clinic code
.
KCLI6000  MOVE      Z30,CLINTO               * to clinic code
          MOVE      SP6,CLINPTO              * to clinic code
          MOVE      FINISH,CLINDTO           * to clinic desc. ("Finish")
.
.         Display clinic code and description
.
KCLI8000  DISPLAY   *P19:13,*V2LON,CLINPTO,*HOFF,*P28:13,CLINDTO
.
.         Validate clinic range
.
          MATCH     CLINFROM,CLINTO
          GOTO      KCLI8900 IF LESS         * invalid clinic range
.
          MOVE      ZERO,EXIT
          GOTO      KCLI9999
.
.         Invalid clinic range
.
KCLI8900  DISPLAY   *P1:24,*EL,*B,"Invalid clinic range. ";
          CALL      HITENTER
          GOTO      KCLI0000
.
.         Exitchar input
.
KCLI9000  MOVE      ONE,EXIT
.
KCLI9999  RETURN
.
. *****************************************************************************
. * CLIN0000   : Keyin a clinic code                                          *
. * Parameters : CVERT    - Row for keyin                                     *
. *              CCOL     - Column for keyin                                  *
. * Returns    : EXIT     - 0 = Clinic code input                             *
. *                         1 = nothing input                                 *
. *                         2 = EXITCHAR input                                *
. *              OUTCLIFD record if EXIT = 0                                  *
. *****************************************************************************
.
CLIN0000  KEYIN     *PCCOL:CVERT,*DV,*EL,UNDLN6:
                    *PCCOL:CVERT,*V2LON,OCACLIN:
                    *PCCOL:CVERT,*DV,OCACLIN;
.
          PACK      OCACLIN,OCACLIN,SP10
.
          MATCH     SP6,OCACLIN              * anything input ?
          GOTO      CLIN9000 IF EQUAL        * no, return with appropriate flag
.
          CMATCH    QUEST,OCACLIN            * "?" input ?
          GOTO      CLIN2000 IF EQUAL        * yes, display available clinics
.
          CMATCH    EXITCHAR,OCACLIN         * exitchar input ?
          GOTO      CLIN9990 IF EQUAL        * yes, abort input
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,IDDD,OCACLIN,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
.
              MATCH     OCACLIN,OCACLIN
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Invalid clinic code. ";
            CALL      HITENTER
            GOTO      CLIN0000
          ENDIF
          CALL      CLDS0000                 * format clinic description
.
          MOVE      ZERO,EXIT                * valid clinic code
          GOTO      CLIN9999
.
.         Display available clinics
.
CLIN2000  DISPLAY   *PCCOL:CVERT,SP6         * clear "?" from screen
          MOVE      ZERO,HLEF                * entire screen to be saved
          CALL      GETSCR00                 * save screen
.
CLIN3000  CALL      OUTDSCLN                 * display clinics
.
.         Keyin after a "?" option
.
.
CLIN4000  KEYIN     *P1:24,"Enter the clinic code : ":
                    *P25:24,*DV,*EL,UNDLN6:
                    *P25:24,*V2LON,OCACLIN:
                    *P25:24,*DV,OCACLIN;
.
          PACK      OCACLIN,OCACLIN,SP10
.
          MATCH     SP6,OCACLIN              * anything input ?
          GOTO      CLIN8500 IF EQUAL        * no, return with appropriate flag
.
          CMATCH    QUEST,OCACLIN            * "?" input ?
          GOTO      CLIN3000 IF EQUAL        * yes, display available clinics
.
          CMATCH    EXITCHAR,OCACLIN         * exitchar input ?
          GOTO      CLIN9900 IF EQUAL        * yes, abort input
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,IDDD,OCACLIN,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
.
              MATCH     OCACLIN,OCACLIN
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Invalid clinic code. ";
            CALL      HITENTER
            GOTO      CLIN4000
          ENDIF
.
          CALL      CLDS0000                 * format clinic description
          CALL      PUTSCR00
          MOVE      ZERO,EXIT                * valid clinic code
          GOTO      CLIN9999
.
.         Nothing input
.
CLIN8500  CALL      PUTSCR00
.
CLIN9000  MOVE      ONE,EXIT                 * nothing input
          GOTO      CLIN9999
.
.         Exitchar input
.
CLIN9900  CALL      FRESCR00                 * free the saved screen
.
CLIN9990  MOVE      TWO,EXIT
.
CLIN9999  RETURN
.
. *****************************************************************************
. * KDTE0000   : Keyin an attendance date                                     *
. * Returns    : VISTDATE - attendance date                                   *
. *****************************************************************************
.
KDTE0000  DISPLAY   *P1:15,*EF:
                    *P1:15,"Attendance date : ";
.
          MOVE      TEN5,CVERT               * row for keyin
          MOVE      TEN9,CCOL                * column for keyin
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD * default date
          REP       " 0",CURRDATE
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDTE0000
.
          PACK      CPDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CPDATE
.
.         Validate date
.
          MATCH     CPDATE,CURRDATE
          GOTO      KDTE9000 IF LESS
.
          MOVE      CPDATE,VISTDATE          * set attendance date
          GOTO      KDTE9999
.
.         Invalid date
.
KDTE9000  DISPLAY   *P1:24,*EL,*B,"Date must not be in the future. ";
          CALL      HITENTER
          GOTO      KDTE0000
.
KDTE9999  RETURN
.
. *****************************************************************************
. * CRET0000   : Create a temporary index file                                *
. *****************************************************************************
.
CRET0000  MOVE      SRTKEY3,SRTKEY           * key for writing new records
.
          CALL      DELT0000                 * delete any existing file
          PACK      CMDLINE,ISBUILD,FNAME,TEMPRLEN:
                            SRTKEY1,SP1,SRTKEY2,SP1,SRTKEY3
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,FNAME
.
CRET9999  RETURN
.
. *****************************************************************************
. * DELT0000   : Delete a temporary index file                                *
. *****************************************************************************
.
DELT0000  CLOSE     TEMPFILE                 * make sure file is closed
          PACK      CMDLINE,ISERASE,FNAME
          EXECUTE   CMDLINE,TASKID
.
DELT9999  RETURN
.
. *****************************************************************************
. * STPR0000   : Initialise data for printing report                          *
. *****************************************************************************
.
STPR0000  MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
.
          CLOCK     TIME,CTIMEIS             * start time of report
          MOVE      SP6,CURRCLIN             * initialise current ward
.
          MOVE      ZERO,NRECS               * number of records
          MOVE      ZERO,NDAYCH              * number of visits charged
          MOVE      ZERO,NACCOM              * amount charged
.
STPR9999  RETURN
.
. *****************************************************************************
. * LOAD0000   : Load data into temporary index file                          *
. *****************************************************************************
.
LOAD0000  DISPLAY   *P1:24,*EL,*BLINKON,"Sorting data",*HOFF:
                    *P20:24,"O/P No.   : ";
.
.         Loop through everyone who was booked on the given date
.
          PACK      KEY16,VISTDATE,SP20
          CALL      RDSBOKB2
LOAD1000  CALL      RDKBOKB2                 * get next record
          BRANCH    OVRCD,LOAD9999           * finished loop
.
          MATCH     OBADATE,VISTDATE         * correct date ?
          GOTO      LOAD9999 IF NOT EQUAL    * no, finished loop
.
          DISPLAY   *P32:24,*V2LON,OBAOUTNO;
.
.         Check if the patient is attended
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDVISA1                  * check if attended
          BRANCH    OVRCD,LOAD1000           * not attended
.
.         Check if the ward code is in the request range
.
          MATCH     CLINFROM,PVIDOCT         * check against starting clinic
          GOTO      LOAD1000 IF LESS
.
          MATCH     PVIDOCT,CLINTO           * check against ending clinic
          GOTO      LOAD1000 IF LESS
.
.         make sure invoice no. is on invoice pending file
.         If Using the Invoice Pending File
.
          IF        PTCNIPEN = 0
            PACK      KEY8,OBAOUTNO
            CALL      RDIPEN1
            BRANCH    OVRCD,LOAD1000
          ENDIF
.
.         Get the rest of the details for this patient
.
          MOVE      PVIURNO,KEY8
          CALL      RDMAST1                  * get master file details
          BRANCH    OVRCD,LOAD1000
.
.         Set up variables for write to temp file
.
          MOVE      PVIDOCT,XCLIN
          MOVE      OBAOUTNO,XADMN
          MOVE      PURNO,XURNO
          MOVE      PSNAME,XSNAME
          MOVE      PGNAME,XGNAME
.
          PACK      TEMPKEY,XCLIN,XADMN
          CALL      WRTEMP
          GOTO      LOAD1000
.
LOAD9999  RETURN
.
. *****************************************************************************
. * REP0000    : Print the report                                             *
. *****************************************************************************
.
REP0000   DISPLAY   *P1:24,*EL,*BLINKON,"Printing data",*HOFF:
                    *P20:24,"Clinic : ";
.
.         Open the temporary file on the appropriate index
.
          CLOSE     TEMPFILE
          LOAD      SRTKEY,OPTION,SRTKEY1,SRTKEY2,SRTKEY3
          OPEN      TEMPFILE,FNAME
.
.         Loop through the temporary index file, and produce the report
.
          MOVE      SP30,TEMPKEY
          CALL      RSTEMP
REP1000   CALL      RKTEMP
          BRANCH    OVRCD,REP9000            * finished report
.
          DISPLAY   *P29:24,*V2LON,XCLIN,SP1,XSNAME;
.
.         Check for a change in clinic
.
          MATCH     XCLIN,CURRCLIN           * change of clinic ?
          GOTO      REP4000 IF EQUAL         * no, just proceed
.
          MATCH     SP6,CURRCLIN             * first clinic ?
          GOTO      REP3000 IF EQUAL         * yes, just proceed
.
          CALL      SUBT0000                 * print sub-totals
.
REP3000   MOVE      XCLIN,CURRCLIN           * set current clinic
          CALL      HEAD0000                 * start a new page
.
.         Get the data for this patient
.
REP4000   MOVE      XURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,REP1000
.
          MOVE      XADMN,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,REP1000
.
          MOVE      XADMN,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,REP1000
.
.         Calculate the amount to be invoiced
.
          MOVE      ONE,PROGFLAG             * calculate amount of invoice
          CALL      OUTTBI                   * calculate routine
.
          CALL      PRTD0000                 * print details
.
          ADD       ONE,NRECS                * one more patient
          IF        GROSSTOT <> 0.00
            ADD       ONE,NDAYCH             * visits charged
            ADD       GROSSTOT,NACCOM        * charge
          ENDIF
          GOTO      REP1000
.
.         End of report
.
REP9000   CALL      SUBT0000                 * print sub-totals
.
          PRINT    *N,"*** End of Report ***"
.
REP9999   RETURN
.
. *****************************************************************************
. * HEAD0000   : Print the report heading                                     *
. *****************************************************************************
.
HEAD0000  IF        NRECS = 0
            MOVE      ONE,CNOUNDLN
          ELSE
            MOVE      ZERO,CNOUNDLN
          ENDIF
          CALL      HEAD80
.
          UNPACK    VISTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *20,"Starting clinic  : ",CLINPFRM,SP2,CLINDFRM,*N:
                    *20,"  Ending clinic  : ",CLINPTO,SP2,CLINDTO,*N:
                    *20,"Attendance date  : ",CPCDATE,*N
.
          PACK      KEY20,IDDD,CURRCLIN,VISTDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
.
              MATCH     CURRCLIN,OCACLIN
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      CLDS0000                 * format clinic description
          ENDIF
.
          PRINT     *1,CURRCLIN,SP2,OCADESC
          CALL      UND80
          PRINT     *1,"|  U/R No.":
                    *12,"|  O/P No.":
                    *23,"| Patients Name":
                    *70,"|  Charge":
                    *80,"|"
          CALL      UND80
.
          ADD       SIX,CLNO
.
HEAD9999  RETURN
.
. *****************************************************************************
. * PRTD0000   : Print a record on the report                                 *
. *****************************************************************************
.
PRTD0000  SUB       HFTHEA,GROSSTOT          * subtract rebate portion
          SUB       HFMISC,GROSSTOT          * subtract rebate portion
.
          MOVE      GROSSTOT,PCHARGE
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"| ",XURNO:
                    *12,"| ",XADMN:
                    *23,"| ",XSNAME,XGNAME:
                    *70,"|",PCHARGE:
                    *80,"|"
          ADD       ONE,CLNO
.
PRTD9999  RETURN
.
. *****************************************************************************
. * SUBT0000   : Print the sub-totals for the current ward                    *
. *****************************************************************************
.
SUBT0000  COMPARE   "57",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          CALL      UND80
          PRINT     *1,"|":
                    *3,"Number of patients = ",NRECS:
                    *37,"Patients charged = ",NDAYCH:
                    *70,"|",NACCOM:
                    *80,"|"
          CALL      UND80
.
          MOVE      ZERO,NRECS
          MOVE      ZERO,NDAYCH
          MOVE      ZERO,NACCOM
.
SUBT9999  RETURN
.
. *****************************************************************************
. * CLDS0000   : Get the description for the current clinic id                *
. *****************************************************************************
.
CLDS0000  MATCH     SP6,OCADOCT              * do we have a doctor code ?
          GOTO      CLDS9999 IF EQUAL        * no, we must have a description
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1                  * get the doctor details
          BRANCH    OVRCD,CLDS9999           * no doctor details
.
          CALL      CONCATDR                 * format doctor (from OUTDSCLN)
          MOVE      DIM40,OCADESC
.
CLDS9999  RETURN
.
. *****************************************************************************
. * I/O routines for temporary index file                                     *
. *****************************************************************************
.
RSTEMP    READ      TEMPFILE,TEMPKEY;;
          RETURN
.
RKTEMP    MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XCLIN,XADMN,XURNO,XSNAME,XGNAME
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP    WRITE     TEMPFILE,TEMPKEY;XCLIN,XADMN,XURNO,XSNAME,XGNAME
          RETURN
.
          INC       STD001IO
          INC       OUTCATBI
          INC       OUTDSCLN
          INC       PATMCHRD                * Miscellaneous Charges read routine
          INC       TFILENAM
          INC       CLPATINP
          INC       OUTSITIO/INC
          INC       IBAGSTIO/INC
          INC       IBAGEDIO/INC
          INC       IBASEQIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDTRIO/INC
          INC       OUTCLIIO/INC
          INC       OUTHTRIO/INC
          INC       PATCODIO/INC
          INC       COMDEPIO/INC
          INC       PATDO1IO/INC
          INC       PATFIGIO/INC
          INC       PATFINIO/INC
          INC       PATHSPIO/INC
          INC       PATINPIO/INC
          INC       PATINVIO/INC
          INC       PATIN1IO/INC
          INC       PATIPEIO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       PATMCHIO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PATWC1IO/INC
          INC       PATWMAIO/INC
          INC       PATWVEIO/INC
          INC       PMSMTIIO/INC
          INC       WEBERRIO/INC
.
WRITETRN
PATCALF
TAIL
INITIVAR
PRTEOL
PROCMISC
PRTCLMS
NXTTRAN1
PRTINVTL
SINVT000
PEOL0000
ENDPRT00
PRLN0000
PRTIPROV
ENDPRT    RETURN

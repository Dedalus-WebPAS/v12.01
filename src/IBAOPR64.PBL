.******************************************************************************
.* Program        : IBAOPR64                                                  *
.* Name           : Operating Records Report                                  *
.* Date           : 09/01/91                                                  *
.******************************************************************************
.* Author         : ROD                                                       *
.* Function       : This program will print the list of operations            *
.*                  completed in a given date range for a given set           *
.*                  of clinic/surgeons                                        *
.*  Mod's    :                                                                *
.******************************************************************************
.* V12.00.01 22/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.* V11.03.02 20/04/2023  Thanh T        TSK 0909393                           *
.*           Changed RPRT0500 to setup KEY26 with OPSEHOSP                    * 
.* V11.03.01 20/03/2023  PJ Le Febour   TSK 0909393a                          *
.*           amended KEY19 to KEY22 for theatre index changes                 *
.* V11.01.02 27/04/2021  Thanh T        TSK 0895165                           *
.*           Recompiled for OPRARDFD changes                                  *
.* V11.01.01 16/02/2021  Tracey Nguyen  TSK 0888639                           *
.*           FD changed - oprpmbaf.OPRPMBA1 from KEY13 to KEY14               *
.******************************************************************************
.*        V11.00.01 11/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.12.01 12/01/2018  Richa Phogat   TSK 0825792                    *
.*                  Modified PRTB0000 to read pmshcpaf if OPCNURSE = 1        *
.*        V10.11.01 23/11/2017  Davin          TSK 0846231                    *
.*                  Recompiled for OPRPMBFD - added oppmrefn                  *
.*        V10.01.01 03/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added PMSVX1FD                                            *
.*        V10.00.01 16/08/2010  Steve Armstrong  CAR 228150                   *
.*                  Added options 3 & 4 which replace comments from OPDACOMM  *
.*                  with first 2 lines of theatre booking comments            *
.*                  (type = 009) from oprdedaf.                               *
.******************************************************************************
.*        V9.12.02  25/02/2010  Jill Parkinson CAR 215098                     *
.*                  Added N/A to count correct                                *
.*        V9.12.01  05/11/2009  Ebon Clements CAR 207604                      *
.*                  Added call to RCVT0000 - determine time in recovery       *
.*        V9.09.01  15/04/2008  Sandra Barcham   165442                       *
.*                  Fix report layout 1 cancelled patients should get next pat*
.*                  Loop through OPRDETA5 not OPRDETA1                        *
.*                  Print Operation Description                               *
.*        V9.05.01  06/04/2006  Peter Vela    CAR 96656                       *
.*                  Removed Operation Type from report OPDATYPE OTYPDESC      *
.*        V9.04.01  20/01/2004  Lina Vo       CAR 55909                       *
.*                  Fixed report layout 1                                     *
.*                  Added Operating Room Sequenced Report                     *
.*                  Moved Admission number and added DOB to layout 1          *
.*        V9.03.01  07/08/2003  Jill Habasque CAR 41815                       *
.*                  Changed matches of opdastat                               *
.*        V9.02.03  13/03/2003  Tonii  CAR 37263                              *
.*                  - MBS code not initialised, values carrying over from     *
.*                    previous patient                                        *
.*                  - Program not using time set by parameter OPCNFTIM &      *
.*                    OPCNTTIM, mods to save working variables                *
.*        V9.02.02  14/02/2003  Tonii  SRS No: 36667                          *
.*                  Mods to use new tables if a record exists in oprsrgaf     *
.*                  Program to use OPCNFTIM and OPCNTTIM to determine which   *
.*                  operation times to calculate the duration                 *
.*        V9.02.01  23/12/2002 J.Tan  SRF 19614                               *
.*                  Fixed report to use new WEB Theatre files                 *
.******************************************************************************
.*        V5.08.02  04/09/2000  Jill Habasque SRF 5404                        *
.*                  Fixed display of OPDAPOS4                                 *
.*        V5.08.01  23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund table variables to 8 chars            *
.*        V5.07.00  28/10/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.*        V5.03.01  08/05/1996 Howellsy        5.04                      *
.*           :           Added 2 more nurse codes & types.                    *
.*        V5.01.02  19/02/91 Graeme Williams                                  *
.*                  Fixed up printing of default team details                 *
.*        V5.01.03  22/05/91  Steve O'Gorman                                  *
.*                  Fixed Errors found by the New Compiler                    *
.*        V5.01.04  25/07/1991    ROD                                         *
.*                  Recompiled for OPRSESFD                                   *
.*        V5.01.05  21/11/1991    DWJ                                         *
.*                  Fix Wellington Errors - SRF 111789                        *
.*        V5.01.06  30/12/1991    Justin Coates  (Modbury Changes)            *
.*                  Added report layout 2 - Modbury                           *
.*        V5.01.07  03/06/92      ROD                                         *
.*                  Added check on rep layout to print Op Sub total           *
.*        V5.08.01  23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund table variables to 8 chars            *
.*        V5.07.00  28/10/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.*        V5.03.01  08/05/1996 Howellsy        5.04                      *
.*           :           Added 2 more nurse codes & types.                    *
.*        V5.01.02  19/02/91 Graeme Williams                                  *
.*                  Fixed up printing of default team details                 *
.*        V5.01.03  22/05/91  Steve O'Gorman                                  *
.*                  Fixed Errors found by the New Compiler                    *
.*        V5.01.04  25/07/1991    ROD                                         *
.*                  Recompiled for OPRSESFD                                   *
.*        V5.01.05  21/11/1991    DWJ                                         *
.*                  Fix Wellington Errors - SRF 111789                        *
.*        V5.01.06  30/12/1991    Justin Coates  (Modbury Changes)            *
.*                  Added report layout 2 - Modbury                           *
.*        V5.01.07  03/06/92      ROD                                         *
.*                  Added check on rep layout to print Op Sub total           *
.*                  Added parameter for printing cancelled sessions (Y/N)     *
.*        V5.01.08  29/06/92 J.Tan            SRF 114917                      *
.*                  Mods grand total patients to exclude cancelled bookings   *
.*                  for report layout two                                     *
.*        V5.01.09  20/07/92  ROD                                             *
.*                  Altered code for printing Count Correct Yes/No            *
.*                  Print Actual theatre for each case                        *
.*        V5.01.10  05/10/92  Graeme Williams              SRF 117810         *
.*                  Fix printing of 1st team if there is a 2nd team           *
.*        V5.01.11  13/01/1994  Steve Armstrong                               *
.*                  Fixed bugs for recompile                                  *
.*        V5.01.12  24/05/1994  Matt                                          *
.*                  Fixed bugs for recompile                                  *
.******************************************************************************
.
.$$$$$$
.
. For a new report format just add Print statements to the following routines:
.
.                 PAGE0000  -  new page details
.                 SUBH0000  -  sub heading
.                 PRTA0000  -  DEAFD details
.                   PDEA0000  - Operation descs Pre-op & Post-op times
.                 PRTB0000  -  Team details
.                 PRTC0000  -  End of session
.
.$$$$$$
.
          INC       STD001FD
          INC       PATCONFD/INC
          INC       PATCALAG/INC
          INC       OPRCONFD/INC
          INC       OPRARDFD/INC
          INC       OPRSRGFD/INC
          INC       OPRTSMFD/INC
          INC       PATDO1FD/INC
          INC       PATDHEAD/INC
          INC       OPRCLIFD/INC
          INC       OPRDEAFD/INC
          INC       OPRDEBFD/INC
          INC       OPRDECFD/INC
          INC       OPRDEDFD/INC
          INC       PATITMFD/INC
          INC       PATTRNFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSHCPFD/INC   * C-0825792 HCP File
          INC       PMSVX1FD/INC
          INC       PATPR1FD/INC
          INC       OPROPRFD/INC
          INC       OPRPMBFD/INC
          INC       OPRSESFD/INC
          INC       OPRNURFD/INC
          INC       PATCODFD/INC
          INC       BOKRC1FD/INC
.
ADMDATE   DIM       10
ADMTYPE   DIM       15
ANAEDESC  DIM       14
ATTDOCT   DIM       40
BJDAY     FORM      3
CCOUNTYN  DIM       3
CJDAY     FORM      3
CLINDESC  DIM       30
CLSUDESC  DIM       30
CLSUDES1  DIM       30
CLSUDES2  DIM       30
CODEA     INIT      "A "
CODEOA    INIT      "OA"
CODEOK    INIT      "OK"
CODEON    INIT      "ON"
CODEOF    INIT      "OF"
CODEOP    INIT      "OP"
CODEOS    INIT      "OS"
CODEOY    INIT      "OY"
CODESC    INIT      "SC"
CODEST    INIT      "ST"
CODETU    INIT      "TU"
CODEY6    INIT      "Y6"
CODEZ2    INIT      "Z2"
COLM1OR2  FORM      1
COMCOUNT  FORM      1
CORDDESC  DIM       30
COUNT     FORM      2
COUNTER1  FORM      2
COUNTER2  FORM      2
COUNTER3  FORM      2
COUNTER4  FORM      2
CURRDATE  DIM       8
CURTEAM   FORM      1
DCENT     DIM       2
DDAY      DIM       2
DESC1     DIM       40
DESC2     DIM       40
DESC3     DIM       40
DESC4     DIM       40
DESC5     DIM       40
DESC6     DIM       40
DESC40    DIM       40
DETSOK    FORM      1
DIM3      DIM       3
DIM42     DIM       42
DOBDATE   DIM       10
DOCCODE   DIM       6
DOCTCODE  DIM       6
DOCTDESC  DIM       20
DOCTNAME  DIM       30
DOCTTYPE  DIM       3
DURATION  FORM      4
DYEAR     DIM       2
ENCODE    DIM       6
ENDDATE   DIM       8
ENDTIME   DIM       5
ENTMPCOD  DIM       6
ENTMPDES  DIM       30
FNDTEAM   FORM      1
FORM4B    FORM      4
INITVARS  DIM       9
KNOWDESC  DIM       20
NURSCODE  DIM       6  
NURSDESC  DIM       20
NURSNAME  DIM       30
NURSTYPE  DIM       3
OPERDESC  DIM       40
OPGRDNUM  FORM      4         * grand-total for number of ops for patient
OPGRDTIM  FORM      5         * grand-total for time for patient
OPSUBNUM  FORM      4         * sub-total for number of ops for patient
OPSUBTIM  FORM      5         * sub-total for time for patient
OPT1DESC  DIM       60
OPT2DESC  DIM       60
OPT3DESC  DIM       60
OPT4DESC  DIM       60
OPTNDESC  DIM       60
OTYPDESC  DIM       20
PATSNAME  DIM       25
PATTOTAL  FORM      2
PREOPDES  DIM       18
PREOPTIM  DIM       5
PSTOPDES  DIM       18
PSTOPTIM  DIM       5
SRGFLAG   FORM      1
SAVEHOSP  DIM       3
SAVECASE  DIM       3
SAVECLIN  DIM       6
SAVEDATE  DIM       8
SAVESTAT  FORM      1
SAVETIME  DIM       5
SAVENDAT  DIM       10
SAVSTDAT  DIM       10
SESSDATE  DIM       10 
SP36      INIT      "                                   "
SP40      INIT      "                                       "
SP42      INIT      "                                         "
STCODE    DIM       6
STRTDATE  DIM       8
STRTTIME  DIM       5
STTMPCOD  DIM       6
STTMPDES  DIM       30
TEMPCODE  DIM       6
TIMESTOP  DIM       5
TIMESTRT  DIM       5
TYPEDESC  DIM       20
SESTDESC  DIM       20
SEQUENCE  INIT      " Sequence"
UNDEQL40  INIT      "========================================"
DASH12    INIT      "------------"
DASH40    INIT      "----------------------------------------"
UNKNOWNC  INIT      "Unknown Code "
Z6        INIT      "zzzzzz"
.
PRGID     INIT      "IBAOPR64"
PRGNAM    INIT      "Operating Records Report"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                               ML0000                                   *
.*                  Mainline code - Controlling logic                     *
.**************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SCRN0000                      * get main options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      CLRV0000                      * clear variables
          CALL      DISP0000                      * disp screen layout
.
          CALL      GDAT0000                      * get date ranges
          CALL      RANG0000                      * get code ranges
          CALL      CONTQST                       * ask Continue (Y/N/C)?
          BRANCH    CEXIT,ML3000,ML2000,ML1000    
.
ML3000    CALL      RPRT0000                      * do processing/print report 
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**************************************************************************
.*                               INIT0000            Called By: ML0000    *
.*                      Initialization                                    *
.**************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P1:3,*EF,*P56:24,"controlf"
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P56:24,"oprardaf"
          OPEN      OPRARDA1,"oprardaf"
          DISPLAY   *P56:24,"oprpmbaf"
          OPEN      OPRPMBA1,"oprpmbaf"
          DISPLAY   *P56:24,"oprsrgaf"
          OPEN      OPRSRGA1,"oprsrgaf"
          DISPLAY   *P56:24,"oprtsmaf"
          OPEN      OPRTSMA1,"oprtsmaf"
          DISPLAY   *P56:24,"oprcliaf"
          OPEN      OPRCLIA1,"oprcliaf"
          OPEN      OPRCLIA2,"oprcliaf"
          DISPLAY   *P56:24,"oprdetaf"
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA5,"oprdetaf"
          DISPLAY   *P56:24,"oprdetbf"
          OPEN      OPRDETB1,"oprdetbf"
          DISPLAY   *P56:24,"oprdetcf"
          OPEN      OPRDETC1,"oprdetcf"
          DISPLAY   *P56:24,"oprdedaf"
          OPEN      OPRDEDA1,"oprdedaf"
          DISPLAY   *P56:24,"patitemn"
          OPEN      PATITEM1,"patitemn"
          DISPLAY   *P56:24,"pattranf"
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
          DISPLAY   *P56:24,"opropraf"
          OPEN      OPROPRA1,"opropraf"
          DISPLAY   *P56:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P56:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P56:24,"oprnuraf"
          OPEN      OPRNURA1,"oprnuraf"
          DISPLAY   *P56:24,"oprsesaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA4,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
          DISPLAY   *P56:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          DISPLAY   *P56:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          DISPLAY   *P56:24,"pmshcpa1"
          OPEN      PMSHCPA1,"pmshcpaf"      * HCP file
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P56:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P56:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P1:24,*EL
.
          READ      CONTROLF,FORTY2;*19,OPCNFOTM,OPCNTOTM,*60,OPCNR64A:
                                        *120,OPCNPC64,*235,OPCNFTIM: 
                                        *237,OPCNTTIM
          READ      CONTROLF,FORTY;*81,OPCNODSC,OPCNCDSC,*111,OPCNCLSU:
                                   *112,OPCNCODE,*114,OPCNSESS,OPCNNOOP
          READ      CONTROLF,SIXTY8;*1,OPCNDPR1,OPCNDPR2,OPCNDPR3,OPCNDPR4:
                             OPCNDPR5,*163,OPCNDPS1,OPCNDPS2,OPCNDPS3,OPCNDPS4
          READ      CONTROLF,HUND13;*51,OPCNURSE
.
. remove trailing blanks from Clinic/Surgeon description
.
          ENDSET    OPCNCDSC
INIT5000  CMATCH    SP1,OPCNCDSC
          GOTO      INIT6000 IF NOT EQUAL
          BUMP      OPCNCDSC,-1
          GOTO      INIT5000 IF NOT EOS
.
INIT6000  LENSET    OPCNCDSC
          RESET     OPCNCDSC
.         
INIT9999  RETURN
+
.**************************************************************************
.*                               SCRN0000            CALLED BY: ML0000    *
.*                         Display main menu screen                       *
.**************************************************************************
SCRN0000  HLINE     *G37,2,52,80
.
          STRIP     OPCNCDSC
          CLEAR     OPT1DESC
          APPEND    "Date, Time & ",OPT1DESC
          APPEND    OPCNCDSC,OPT1DESC
          APPEND    SEQUENCE,OPT1DESC
          APPEND    SP2,OPT1DESC
          APPEND    "(standard comments)",OPT1DESC
          RESET     OPT1DESC
.
          CLEAR     OPT3DESC
          APPEND    "Date, Time & ",OPT3DESC
          APPEND    OPCNCDSC,OPT3DESC
          APPEND    SEQUENCE,OPT3DESC
          APPEND    SP2,OPT3DESC
          APPEND    "(theatre booking comments)",OPT3DESC
          RESET     OPT3DESC
.
          STRIP     OPCNODSC
          CLEAR     OPT2DESC
          APPEND    "Date & ",OPT2DESC
          APPEND    OPCNODSC,OPT2DESC
          APPEND    SEQUENCE,OPT2DESC
          APPEND    SP8,OPT2DESC
          APPEND    "(standard comments)",OPT2DESC
          RESET     OPT2DESC
.
          STRIP     OPCNODSC
          CLEAR     OPT4DESC
          APPEND    "Date & ",OPT4DESC
          APPEND    OPCNODSC,OPT4DESC
          APPEND    SEQUENCE,OPT4DESC
          APPEND    SP8,OPT4DESC
          APPEND    "(theatre booking comments)",OPT4DESC
          RESET     OPT4DESC
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",OPT1DESC:
                    *P1:6,*V2LON,TWO,*HOFF," = ",OPT2DESC:
                    *P1:7,*V2LON,THREE,*HOFF," = ",OPT3DESC:
                    *P1:8,*V2LON,FOUR,*HOFF," = ",OPT4DESC:
                    *P1:10,"Select Option : "
.
SCRN1000  KEYIN     *P17:10,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION                   * keyin = 0 ?
          GOTO      SCRN9000 IF EQUAL
.
          BRANCH    OPTION,SCRN2000:
                           SCRN2000:
                           SCRN2000:
                           SCRN2000
.
          BEEP 
          GOTO      SCRN1000
.
SCRN2000  DISPLAY   *P52:2,*V2LON,"- Print Report "
          MOVE      FALSE,EXIT
          GOTO      SCRN9999
.
SCRN9000  MOVE      TRUE,EXIT
.
SCRN9999  RETURN
+
.***************************************************************************
.*                                 DISP0000          Called by: ML0000     *
.*                      Display screen layout                              *
.***************************************************************************
DISP0000  DISPLAY   *P1:4,*EF:
                    *P5:4,"Starting date",*P30:4,":":
                    *P5:6,"Ending date",*P30:6,":":
                    *P5:8,"Starting ",OPCNCDSC,*P30:8,":":
                    *P5:10,"Ending ",OPCNCDSC,*P30:10,":"
DISP9999  RETURN
+
.***************************************************************************
.*                                 DVAR0000          Called by: ML0000     *
.*                      Display screen variables                           *
.***************************************************************************
DVAR0000  
. format start date
.
          UNPACK    STRTDATE,DCENT,DYEAR,DMON,DDAY
          MOVE      DCENT,CCENT
          MOVE      DYEAR,CYEAR
          MOVE      DMON,CMON
          MOVE      DDAY,CDAY
          CALL      PACDATE
          DISPLAY   *P32:4,*V2LON,CPCDATE         * display start date
          MOVE      CPCDATE,SAVSTDAT              * save formatted date
          REP       " 0",SAVSTDAT
.
. format end date
.
          UNPACK    ENDDATE,DCENT,DYEAR,DMON,DDAY
          MOVE      DCENT,CCENT
          MOVE      DYEAR,CYEAR
          MOVE      DMON,CMON
          MOVE      DDAY,CDAY
          CALL      PACDATE
          DISPLAY   *P32:6,*V2LON,CPCDATE         * display end date
          MOVE      CPCDATE,SAVENDAT              * save formatted date
          REP       " 0",SAVENDAT
.
. display starting & ending codes
.
          DISPLAY   *P32:8,*V2LON,STTMPCOD,*HOFF,*P42:8,STTMPDES:
                    *P32:10,*V2LON,ENTMPCOD,*HOFF,*P42:10,ENTMPDES
.
DVAR9999  RETURN
+
.***************************************************************************
.*                                 GDAT0000          Called by: ML0000     *
.*                         keyin a date                                    *
.***************************************************************************
GDAT0000  MOVE      FOUR,CVERT                    * set up defaults
          MOVE      "32",CCOL
          MOVE      ZERO,CCANLDTE
          MOVE      ONE,CDEFDTE
          PACK      CURRDATE,CCC,CYY,CMM,CDD      * get current date
          REP       " 0",CURRDATE
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
.
GDAT1000  CALL      KEYDATE                       * keyin a date
          BRANCH    CQUEST,GDAT1000               * don't allow for "?"
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
. check if less than current date
.
          MATCH     STRTDATE,CURRDATE
          GOTO      GDAT8500 IF LESS
.
. save formatted date
.
          PACK      SAVSTDAT,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",SAVSTDAT
. --- now keyin Ending date ---
.
GDAT5000  MOVE      "6",CVERT                    * set up defaults
          MOVE      "32",CCOL
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
.
GDAT6000  CALL      KEYDATE                       * keyin a date
          BRANCH    CQUEST,GDAT6000               * don't allow for "?"
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
. check if less than current date
.
          MATCH     ENDDATE,CURRDATE
          GOTO      GDAT9000 IF LESS
.
. save formatted date
.
          PACK      SAVENDAT,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",SAVENDAT
.
. check if start date is <= end date
.
          MATCH     ENDDATE,STRTDATE
          GOTO      GDAT9999 IF LESS
          GOTO      GDAT9999 IF EQUAL
.
GDAT8000  DISPLAY   *P1:24,*EL,*B,"Invalid Range.  ";
          CALL      HITENTER
          GOTO      GDAT0000
.
GDAT8500  DISPLAY   *P1:24,*B,*EL,"Starting date must not be into future.  ";
          CALL      HITENTER
          GOTO      GDAT0000
.
GDAT9000  DISPLAY   *P1:24,*B,*EL,"Ending date must not be into future.  ";
          CALL      HITENTER
          GOTO      GDAT5000
.
GDAT9999  RETURN
+
.**************************************************************************
.*                             RANG0000            Called by: ML0000      *
.*                        keyin ranges                                    *
.**************************************************************************
RANG0000  CALL      SCOD0000                      * keyin starting code
.
RANG2000  CALL      ECOD0000                      * keyin ending code
.
. validate range keyed in
.
          MATCH     STCODE,ENCODE
          GOTO      RANG7000 IF LESS
          GOTO      RANG9999
.
. range is invalid
.
RANG7000  DISPLAY   *P1:24,*B,"Invalid Range.  ";
          CALL      HITENTER
          DISPLAY   *P32:10,*EL                   * erase end date line
          GOTO      RANG0000
.
RANG9999  RETURN
+
.*************************************************************************
.*                              SCOD0000           Called by: RANG0000   *
.*                  get starting Code                                    *
.*************************************************************************
SCOD0000  KEYIN     *P32:8,*DV,*EL,UNDLN6:
                    *P32:8,*V2LON,STCODE:
                    *P32:8,*DV,STCODE
.
. call check routine
.
          MOVE      STCODE,TEMPCODE
          CALL      CHEK0000
          MOVE      TEMPCODE,STCODE
          BRANCH    EXIT,SCOD1000,SCOD6000,SCOD8000
.
. invalid SCOD entered
.
          GOTO      SCOD0000
.
. "?" entered so display whats there
.
SCOD1000  MOVE      ONE,CNEWDS
          BRANCH    OPCNCLSU,SCOD1100             * using clinic or surgeons?
          CALL      OPRCLIDS                      * we are using clinics
          GOTO      SCOD2000
.
SCOD1100  CALL      GETSCR00                      * we are using surgeons
          MOVE      SP10,STCODE
          CALL      PATDOCDS
          CALL      PUTSCR00
          BRANCH    EXIT,SCOD8000
          MOVE      FALSE,EXIT                    * valid input
          MOVE      DOCCODE,STCODE
          MOVE      STCODE,KEY6
          CALL      RDDOCT1                       * we are using surgeons
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CLSUDES1             * save descripiton
          MOVE      PACFNAME,CLSUDES2             * save descripiton
          GOTO      SCOD6000 
.
. now ask for SCOD while leaving displayed names on the screen
.
SCOD2000  DISPLAY   *P1:24,"Starting ",*+,OPCNCDSC," : " 
          MOVELPTR  OPCNCDSC,CCOL
          ADD       "13",CCOL
.
          KEYIN     *PCCOL:24,*DV,*EL,UNDLN6:
                    *PCCOL:24,*V2LON,STCODE:
                    *PCCOL:24,*DV,STCODE
.
. repeat the validation of the CODE
.
          MOVE      STCODE,TEMPCODE
          CALL      CHEK0000
          MOVE      TEMPCODE,STCODE
          BRANCH    EXIT,SCOD1000,SCOD7000,SCOD9000
          GOTO      SCOD2000 
.
. CODE is valid so display its description (no "?" input)
.
SCOD6000  DISPLAY   *P32:8,*V2LON,STCODE,*HOFF,*P42:8,CLSUDES1
          MOVE      STCODE,STTMPCOD               * move to temp location
          MOVE      CLSUDES1,STTMPDES             * move to temp location
          GOTO      SCOD9999
.
. CODE is valid so display its description ("?" input)
. 
SCOD7000  CALL      DISP0000                      * redisplay screen layout
          MOVE      STCODE,STTMPCOD               * move to temp location
          MOVE      CLSUDES1,STTMPDES             * move to temp location
          CALL      DVAR0000
          GOTO      SCOD9999 
.
. nothing was entered (no "?" input)
.
SCOD8000  DISPLAY   *P32:8,*V2LON,"Start"
          MOVE      "Start",STTMPCOD              * move to temp location
          MOVE      SP30,STTMPDES                 * move to temp location
          MOVE      SP6,STCODE
          GOTO      SCOD9999
.
. nothing was entered ("?" input)
.
SCOD9000  CALL      DISP0000                      * redisplay screen layout
          MOVE      "Start",STTMPCOD              * move to temp location
          MOVE      SP30,STTMPDES                 * move to temp location
          MOVE      SP6,STCODE
          CALL      DVAR0000                      * display vars
.
SCOD9999  RETURN
+
.*************************************************************************
.*                              ECOD0000           Called by: DISC0000   *
.*                  get ending CODE                                      *
.*************************************************************************
ECOD0000  KEYIN     *P32:10,*DV,*EL,UNDLN6:
                    *P32:10,*V2LON,ENCODE:
                    *P32:10,*DV,ENCODE
.
. call check routine
.
          MOVE      ENCODE,TEMPCODE
          CALL      CHEK0000
          MOVE      TEMPCODE,ENCODE
          BRANCH    EXIT,ECOD1000,ECOD6000,ECOD8000
.
. invalid CODE entered
.
          GOTO      ECOD0000
.
. "?" entered so display whats there
.
ECOD1000  MOVE      ONE,CNEWDS
          BRANCH    OPCNCLSU,ECOD1100             * using clinic or surgeons?
          CALL      OPRCLIDS                      * we are using clinics
          GOTO      ECOD2000
.
ECOD1100  CALL      GETSCR00                      * we are using surgeons
          MOVE      SP10,ENCODE
          CALL      PATDOCDS
          CALL      PUTSCR00
          BRANCH    EXIT,ECOD8000
          MOVE      FALSE,EXIT                    * valid input
          MOVE      DOCCODE,ENCODE
          MOVE      ENCODE,KEY6
          CALL      RDDOCT1                       * we are using surgeons
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CLSUDES1             * save descripiton
          MOVE      PACFNAME,CLSUDES2             * save descripiton
          GOTO      ECOD6000 
.
. now ask for CODE while leaving displayed names on the screen
.
ECOD2000  DISPLAY   *P1:24,"Ending ",*+,OPCNCDSC," : "
          MOVELPTR  OPCNCDSC,CCOL
          ADD       "11",CCOL
.
          KEYIN     *PCCOL:24,*DV,*EL,UNDLN6:
                    *PCCOL:24,*V2LON,ENCODE:
                    *PCCOL:24,*DV,ENCODE
.
. repeat the validation of the CODE
.
          MOVE      ENCODE,TEMPCODE
          CALL      CHEK0000
          MOVE      TEMPCODE,ENCODE
          BRANCH    EXIT,ECOD1000,ECOD7000,ECOD9000
          GOTO      ECOD2000 
.
. CODE is valid so display its description (no "?" input)
.
ECOD6000  DISPLAY   *P32:10,*V2LON,ENCODE,*HOFF,*P42:10,CLSUDES2
          MOVE      ENCODE,ENTMPCOD               * store in temp location
          MOVE      CLSUDES2,ENTMPDES             * move to temp location
          GOTO      ECOD9999
.
. CODE is valid so display its description ("?" input)
. 
ECOD7000  CALL      DISP0000                      * redisplay screen layout
.
          MOVE      ENCODE,ENTMPCOD               * store in temp location
          MOVE      CLSUDES2,ENTMPDES             * store in temp location
          CALL      DVAR0000                      * display variables
          GOTO      ECOD9999 
.
. nothing was entered (no "?" input)
.
ECOD8000  DISPLAY   *P32:10,*V2LON,"Finish"
          MOVE      "Finish",ENTMPCOD             * store in temp location
          MOVE      SP30,ENTMPDES                 * store in temp location
          MOVE      Z6,ENCODE
          GOTO      ECOD9999
.
. nothing was entered ("?" input)
.
ECOD9000  CALL      DISP0000                      * display screen layout
          MOVE      "Finish",ENTMPCOD             * store in temp location
          MOVE      SP30,ENTMPDES                 * store in temp location
          MOVE      Z6,ENCODE
          CALL      DVAR0000                      * display variables
.
ECOD9999  RETURN
+
.************************************************************************
.*                               CHEK0000           Called by: SCOD0000 *
.*            validation routine for Start & End Code          ECOD0000 *
.************************************************************************
CHEK0000  ENDSET    TEMPCODE
          APPEND    SP6,TEMPCODE
          RESET     TEMPCODE
.
          MATCH     SP6,TEMPCODE
          GOTO      CHEK6000 IF EQUAL
.
. check for a "?"
.    
          CMATCH    QUEST,TEMPCODE
          GOTO      CHEK8000 IF EQUAL
.
          PACK      KEY6,TEMPCODE  
          BRANCH    OPCNCLSU,CHEK1000             * using clinics or surgeons?
.
          CALL      RDOPCLI1                      * we are using clinics
          BRANCH    OVRCD,CHEK7000
          MOVE      OPCLDESC,CLSUDES1             * save descripiton
          MOVE      OPCLDESC,CLSUDES2             * save descripiton
          MOVE      TWO,EXIT                      * valid code entered
          GOTO      CHEK9999
.
CHEK1000  CALL      RDDOCT1                       * we are using surgeons
          BRANCH    OVRCD,CHEK7000
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CLSUDES1             * save descripiton
          MOVE      PACFNAME,CLSUDES2             * save descripiton
          MOVE      TWO,EXIT                      * valid code entered
          GOTO      CHEK9999
.
CHEK6000  MOVE      THREE,EXIT                    * nothing was entered
          GOTO      CHEK9999
.
CHEK7000  DISPLAY   *P1:24,*B,"Invalid Code.  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      CHEK9999
.
CHEK8000  MOVE      ONE,EXIT                      * "?" was entered
.
CHEK9999  RETURN
+
.****************************************************************************
.*                                 RPRT0000             Called by: ML0000   *
.*                          print report                                    *
.****************************************************************************
RPRT0000  CALL      IBACLOCK       
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,OPSUBNUM           * clear number of operations
          MOVE      ZERO,OPSUBTIM           * clear time
          MOVE      ONE,CNOUNDLN 
          DISPLAY   *P1:24,*EL,*P23:24,"Printing..." 
.
          BRANCH    OPTION,RPRT0010,RPRT0060
.
.         Date / Time / Doctor Sequence
.
RPRT0010  PACK      KEY22,STRTDATE,SP30
          CALL      RSOPSES7                      * position at start date 
RPRT0050  CALL      RKOPSES7                      * read next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
          MATCH     OPSEDATE,ENDDATE              * still in date range?
          GOTO      RPRT9000 IF LESS
.
          MATCH     STCODE,OPSECLIN               * > start code range?
          GOTO      RPRT0050 IF LESS
.
          MATCH     OPSECLIN,ENCODE               * < end code range?
          GOTO      RPRT0050 IF LESS
.
          BRANCH    OPCNPC64,RPRT0500             * ignore cancelled sessions?
.
          COMPARE   ZERO,OPSESTAT                 * cancelled?
          GOTO      RPRT0050 IF EQUAL
.
          GOTO      RPRT0500
.
.         Date / Theatre Room Sequence
.
RPRT0060  PACK      KEY28,SP3,STRTDATE,SP30
          CALL      RSOPSES4                     * position at start date 
RPRT0070  CALL      RKOPSES4                      * read next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
          MATCH     STRTDATE,OPSEDATE             * still in date range?
          GOTO      RPRT0070 IF LESS
.
          MATCH     OPSEDATE,ENDDATE              * still in date range?
          GOTO      RPRT0070 IF LESS
.
          MATCH     STCODE,OPSECLIN               * > start code range?
          GOTO      RPRT0070 IF LESS
.
          MATCH     OPSECLIN,ENCODE               * < end code range?
          GOTO      RPRT0070 IF LESS
.
          BRANCH    OPCNPC64,RPRT0500             * ignore cancelled sessions?
.
          COMPARE   ZERO,OPSESTAT                 * cancelled?
          GOTO      RPRT0070 IF EQUAL
.
          GOTO      RPRT0500
.
.  we have a valid session 
.
RPRT0500  CALL      PAGE0000                      * set up new page (heading)
.
. now loop through DEA to get rest of information
.
          MOVE      ZERO,PATTOTAL                 * init patient total
.
          PACK      KEY26,OPSEHOSP,OPSEDATE,OPSETIME,OPSECLIN,SP70
          CALL      RSOPDEA5
RPRT1000  CALL      RKOPDEA5
          BRANCH    OVRCD,RPRT1050                * get next session
.
          MATCH     OPSEHOSP,OPDAHOSP             * still same hospital?
          GOTO      RPRT1050 IF NOT EQUAL
.
          MATCH     OPSEDATE,OPDADATE             * still same date?
          GOTO      RPRT1050 IF NOT EQUAL
.
          MATCH     OPSETIME,OPDATIME             * still same time?
          GOTO      RPRT1050 IF NOT EQUAL
.
          MATCH     OPSECLIN,OPDACLIN             * still same clinic?
          GOTO      RPRT1050 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT                * Cancelled
          GOTO      RPRT1100 IF NOT EQUAL         * no 
.
          COMPARE   TWO,OPCNR64A                  * Report Layout 2 ?
          GOTO      RPRT1000 IF NOT EQUAL         * no
.
. report layout 2 - cancelled booking allowed but not to be counted
.
          GOTO      RPRT1200
.
.         Next Session
.
RPRT1050  BRANCH    OPTION,RPRT0050,RPRT0070
          GOTO      RPRT0050
.
RPRT1100  ADD       ONE,PATTOTAL                  * increment patient total
.
. show user something is happening
.
RPRT1200  DISPLAY   *P35:24,*HOFF,"Date:",*V2LON,OPDADATE,SP1,*HOFF,"Time:":
                    *V2LON,OPDATIME,SP1,*HOFF,"Clin:",*V2LON,OPDACLIN:
                    SP1,*HOFF,"Case:",*V2LON,OPDACASE
.
. check if we need a new page
.
          COMPARE   "50",CLNO
          GOTO      RPRT1300 IF LESS
.
          CALL      PAGE0000                      * set up new page
.
RPRT1300  CALL      DETS0000                      * get main details
          BRANCH    EXIT,RPRT1000                 * ignore this patient
.
          CALL      SUBH0000                      * print sub-heading
.
          CALL      PRTA0000                      * print the PMI & DEA details
.
          CALL      DEBC0000                      * get/print the DBC details
.
          CALL      ENDS0000                      * check if end of session
          BRANCH    EXIT,RPRT1000
.
          CALL      PEND0000                      * print end of session dets.
          GOTO      RPRT1000                      * get next case
.
RPRT9000  COMPARE   TWO,OPCNR64A
          GOTO      RPRT9100 IF NOT EQUAL         * not layout 2
.
          PRINT     *38,"Operation Grand Total : ",OPGRDNUM,SP3:
                    OPGRDTIM," Minutes "
.
RPRT9100  PRINT     *N,*1,"*** End of Report ***",*N
.
RPRT9999  RETURN
+
.****************************************************************************
.*                                 PAGE0000             Called by: RPRT0000 *
.*                     set-up for a new page                                *
.****************************************************************************
PAGE0000  CALL      HEAD132
          BRANCH    OPCNR64A,PAGE1000,PAGE2000
.
. -- Report 1  Heading --
.
PAGE1000  LOAD      OPTNDESC,OPTION,OPT1DESC,OPT2DESC,OPT3DESC,OPT4DESC
.
          PRINT     *40,OPTNDESC
          PRINT     *40,"Date Range : ",SAVSTDAT," to ",SAVENDAT
          PRINT     *40,"Code Range : ",STTMPCOD," to ",ENTMPCOD
          CALL      HDET0000                      * get/format heading details
          PRINT     *N,*10,OPCNCDSC,*26,":",*28,OPSECLIN,*39,CLINDESC:
                    *80,"Session Type   :",*97,SESTDESC
          PRINT     *10,OPCNODSC,*26,":",*28,OPSETHET,*39,OPRMDESC:
                    *80,"Session Date   :",*97,SESSDATE,*108," at ",OPSETIME
.
          COMPARE   ONE,OPCNSESS                  * sys par 1=print
          GOTO      PAGE1100 IF NOT EQUAL
.
          PRINT     *80,"Session Co-ord :",*97,CORDDESC
.
PAGE1100  MOVE      "10",CLNO
          GOTO      PAGE9999
.
.         report format two
.
PAGE2000  PRINT     *40,"Date Range : ",SAVSTDAT," to ",SAVENDAT,*N
          PRINT     *30,"Admission",*57,"Start  End    Total":
                    *N,"Patient",*32,"Date",*40,"Ward/Bed Age Sex Time   ":
                    "Time   (Mins)",*90,"Surgery Type",*115,"Anaesthetic":
                    *N,DASH40,DASH40,DASH40,DASH12
          MOVE      "9",CLNO
.
PAGE9999  RETURN
+
.*************************************************************************
.*                             HDET0000             Called by: PAGE0000  *
.*              get and format heading details                           *
.*************************************************************************
HDET0000   
. get clinic/surgeon desc
.
          MOVE      OPSECLIN,TEMPCODE
          CALL      CLSU0000                      * format/get desc
          MOVE      CLSUDESC,CLINDESC
.
. get session type
.
          MOVE      SP20,SESTDESC
          PACK      KEY5,CODETU,OPSESTYP
          CALL      RDCODE1
          BRANCH    OVRCD,HDET0200
          MOVE      TDESC,SESTDESC
.
HDET0200  MOVE      SP20,TYPEDESC
          PACK      KEY5,CODEST,OPSETYPE
          CALL      RDCODE1
          BRANCH    OVRCD,HDET1000
          MOVE      TDESC,TYPEDESC
.
. get theatre desc
.
HDET1000  PACK      OPRMDESC,SP20,SP20
          PACK      KEY6,OPSETHET
          CALL      RDOPOPR1
          COMPARE   ZERO,OVRCD
          GOTO      HDET2000 IF EQUAL
          MOVE      " << Missing Code >> ",OPRMDESC
.
. get Session co-ordinator
.
HDET2000  PACK      KEY6,OPSECORD
          CALL      RDDOCT1
          BRANCH    OVRCD,HDET2500
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CORDDESC             * save descripiton
          GOTO      HDET3000
.
HDET2500  MOVE      " << Missing Code >> ",CORDDESC
.
HDET3000  UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,SESSDATE
.
HDET9999
+
.*************************************************************************
.*                             CLSU0000             Called by: HDET0000  *
.*              get and format heading details                           *
.*              Returns : CLSUDESC                                       *
.*************************************************************************
CLSU0000  PACK      KEY6,TEMPCODE  
          BRANCH    OPCNCLSU,CLSU1000             * using clinics or surgeons?
.
          CALL      RDOPCLI1                      * we are using clinics
          BRANCH    OVRCD,CLSU7000
          MATCH     SP6,OPCLDOCT
          GOTO      CLSU0500 IF EQUAL
          PACK      KEY6,OPCLDOCT
          GOTO      CLSU1000
CLSU0500  MOVE      OPCLDESC,CLSUDESC             * save descripiton
          GOTO      CLSU9999
.
CLSU1000  CALL      RDDOCT1                       * we are using surgeons
          BRANCH    OVRCD,CLSU7000
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CLSUDESC             * save descripiton
          GOTO      CLSU9999
.
CLSU7000  MOVE      " << Missing Code >> ",CLSUDESC
.
CLSU9999  RETURN
+
.*************************************************************************
.*                             SUBH0000             Called by:           *
.*                       print sub-heading                               *
.*************************************************************************
SUBH0000  BRANCH    OPCNR64A,SUBH1000,SUBH2000
.
. Report 1  (Sub-Heading)
.
SUBH1000  PRINT     *N,*1,UNDEQL40,UNDEQL40,UNDEQL40,"============"
          PRINT     *1,"Patient",*28,"U/R No.",*37,"Adm Date":
                    *47,"Ward/Bed     DOB     Age Sex Case No. Anaesthetic":
                    *100,"Actual Thet."
.
          PRINT     *1,DASH40,DASH40,DASH40,"------------"
          ADD       FOUR,CLNO
          GOTO      SUBH9999
.
.         subheading report 2
.
SUBH2000  UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CALL      HDET0000
          MOVE      OPRMDESC,KEY26
          PRINT     *1,CPCDATE,",",OPDATIME," At ",KEY26,*57,CLINDESC:
                    *95,TYPEDESC:
                    *N,UNDEQL40,"=====",*N
          ADD       "3",CLNO
          GOTO      SUBH9999
.
SUBH9999  RETURN
+
.*************************************************************************
.*                             PRTA0000             Called by: RPRT0000  *
.*                  print main details of report                         *
.*************************************************************************
PRTA0000  BRANCH    OPCNR64A,PRTA1000,PRTA2000
.
. -- Report 1 --
.
. print PMI details
.
PRTA1000  PRINT     *1,PATSNAME,*27,PURNO,*36,ADMDATE:
                    *48,TWARD,*51,"/",TBED,*56,DOBDATE,*67,PSAGEYRS,*72,PSEX;
.
. print DEA details
.
          PRINT     *77,OPDACASE,*85,ANAEDESC,*100,OPDATHET
          PRINT     *37,OPDAADMN,*N
          ADD       THREE,CLNO
.
          PRINT     *1,"Operation Description : ";
.     
.     print Oper. desc's , Pre-op and Post-op times (system par. dependant)
.
          CALL      PDEA0000
.
.    print Comments & Known infection
.
PRTA1400  IF        OPTION = 3 | OPTION = 4
            MOVE      ZERO,COMCOUNT              * initialise comment line count
            PRINT     *N,*1,"Comments:";         * line 1
            ADD       TWO,CLNO                   * increment line count
.
.           See of there are any theatre booking comments
.
            PACK      KEY16,OPDAUNIQ,ZERO,ZERO,NINE,SP20
            CALL      RSOPDED1                   * position on unique id
PRTA1450    CALL      RKOPDED1                   * read next record
            BRANCH    OVRCD,PRTA1500             * eof - end of comments
.
            MATCH     OPDAUNIQ,OPDDUNIQ          * same unique id still ?
            GOTO      PRTA1500 IF NOT EQUAL      * no - end of comments
.
            MATCH     "009",OPDDTYPE             * theatre booking type still ?
            GOTO      PRTA1500 IF NOT EQUAL      * no - end of comments
.
            ADD       ONE,COMCOUNT               * increment comment line count
.
            PRINT     *11,OPDDDATA;              * print comment
            IF        (COMCOUNT = 1) & (SRGFLAG <> 1)
              PRINT     *85,"Known Infection",*104,":",*106,KNOWDESC
            ELSE
              PRINT     *N;
            ENDIF
.
            BRANCH    COMCOUNT,PRTA1450          * one comment line printed
            ADD       ONE,CLNO                   * increment line count
.
.           Finished printing comments
.
PRTA1500    IF        (COMCOUNT = 0) 
              IF        (SRGFLAG <> 1)
                PRINT     *85,"Known Infection",*104,":",*106,KNOWDESC
              ELSE
                PRINT     *N;
              ENDIF
            ENDIF
            GOTO      PRTA9999
          ENDIF
.
          IF        SRGFLAG=1
            PRINT     *N,*1,"Comments    :",*25,OPDACOMM
          ELSE
            PRINT     *N,*1,"Comments    :",*25,OPDACOMM:
                      *73,"Known Infection",*92,":",*94,KNOWDESC
          ENDIF
.
          ADD       TWO,CLNO
          GOTO      PRTA9999
.
. ******* Report type 2 *******
.
PRTA2000  PRINT     *1,PATSNAME,*31,ADMDATE,SP1,TWARD,"/",TBED,SP2,PSAGEYRS:
                    SP1,PSEX,*57,OPDADES1
          PRINT     *1,"( U/R ",PURNO," Adm ",OPDAADMN," )",*38,ADMTYPE:
                    *57,OPDADES2
          MOVE      SP20,TDESC
          MATCH     SP3,OPDACANC
          GOTO      PRTA2100 IF EQUAL       * no canc. reason
.
          MOVE      "Invalid Canc. Code",TDESC
          PACK      KEY5,CODESC,OPDACANC
          CALL      RDCODE1
.
PRTA2100  PRINT     TDESC,*57,OPDADES3
          PRINT     ATTDOCT
          ADD       "6",CLNO
          GOTO      PRTA9999
.
PRTA9999  RETURN
+
.*************************************************************************
.*                             PRTB0000             Called by: DEBC0000  *
.*                  print team details of report                         *
.*************************************************************************
PRTB0000  BRANCH    OPCNR64A,PRTB1000,PRTB2000
.
. -- Report 1 --
.
PRTB1000  BRANCH    DETSOK,PRTB1950    * do we have team details to print
.
..          MATCH     "1",OPDBTEAM
..          IF        !@EQUAL
..            PRINT     *N;
..          ENDIF
.
          PRINT     *N,*1,"Team :",*8,OPDBTEAM,*13,"Start :",*21,STRTTIME:
                    *30,"End :",*36,ENDTIME,*45,"Count Correct :",*61,CCOUNTYN:
                    *N
          ADD       THREE,CLNO
.
. print doctor descriptions
.
          MOVE      ONE,COUNTER3
          MOVE      ONE,COLM1OR2                  * print in colm 1 initially
.
PRTB1200  COMPARE   NINE,COUNTER3                 * no more doct types?
          GOTO      PRTB1300 IF NOT LESS
.
          LOAD      DOCTTYPE,COUNTER3,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                      OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          ADD       ONE,COUNTER3                  * increment counter
          MATCH     SP3,DOCTTYPE                  * is code blank?
          GOTO      PRTB1200 IF EQUAL
.
          COMPARE   ONE,SRGFLAG
          GOTO      PRTB1210 IF NOT EQUAL
.
          IF        COUNTER3=7 | COUNTER3=8
            PACK      KEY5,CODEOF,DOCTTYPE
            GOTO      PRTB1220
          ENDIF
.
PRTB1210  PACK      KEY5,CODEOP,DOCTTYPE
PRTB1220  CALL      RDCODE1
          BRANCH    OVRCD,PRTB1230
          MOVE      TDESC,DOCTDESC
          GOTO      PRTB1250
.
PRTB1230  PACK      DOCTDESC,UNKNOWNC,DOCTTYPE
.
PRTB1250  SUB       ONE,COUNTER3
          LOAD      DOCTCODE,COUNTER3,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                      OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          ADD       ONE,COUNTER3
.
          PACK      KEY6,DOCTCODE
          CALL      RDDOCT1
          BRANCH    OVRCD,PRTB1270
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCTNAME
          GOTO      PRTB1290
.
PRTB1270  MOVE      "< Missing Code >",DOCTNAME
.
PRTB1290  BRANCH    COLM1OR2,PRTB1293,PRTB1295
.
PRTB1293  PRINT     *1,DOCTDESC,*22,":",*24,DOCTNAME;
          ADD       ONE,CLNO
          MOVE      TWO,COLM1OR2                  * print next desc in colm 2
          GOTO      PRTB1200
.
PRTB1295  PRINT     *67,DOCTDESC,*89,":",*91,DOCTNAME
          MOVE      ONE,COLM1OR2                  * print next desc in colm 1
          GOTO      PRTB1200
.
. print nurse descriptions
.
PRTB1300  MOVE      ONE,COUNTER4
.
PRTB1310  COMPARE   SEVEN,COUNTER4                 * no more nurse types?
          GOTO      PRTB1400 IF NOT LESS
.
          LOAD      NURSTYPE,COUNTER4,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4:
                                      OPDBTYN5,OPDBTYN6
.
          ADD       ONE,COUNTER4                  * increment counter
.
          MATCH     SP3,NURSTYPE                  * is code blank?
          GOTO      PRTB1310 IF EQUAL
.
          PACK      KEY5,CODEON,NURSTYPE
          CALL      RDCODE1
          BRANCH    OVRCD,PRTB1330
          MOVE      TDESC,NURSDESC
          GOTO      PRTB1350
.
PRTB1330  PACK      NURSDESC,UNKNOWNC,NURSTYPE
.
PRTB1350  SUB       ONE,COUNTER4
          LOAD      NURSCODE,COUNTER4,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4:
                                      OPDBNUC5,OPDBNUC6
          ADD       ONE,COUNTER4
.
.         CONTROL.OPCNURSE - Use oprnuraf/pmshcpaf for OT Nurse
          MATCH     "1",OPCNURSE                 * Use pmshcpaf for OT Nurse?
          GOTO      PRTB1351 IF EQUAL            * Yes
.
          PACK      KEY6,NURSCODE
          CALL      RDOPNUR1
          BRANCH    OVRCD,PRTB1370
          MOVE      OPNRGNAM,PACGNAME
          MOVE      OPNRSNAM,PACSNAME
          MOVE      SP10,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,NURSNAME
          GOTO      PRTB1390
.
PRTB1351  PACK      KEY10,NURSCODE,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,PRTB1370
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCSNAM,PACSNAME
          MOVE      SP10,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,NURSNAME
          GOTO      PRTB1390
.
PRTB1370  MOVE      "< Missing Code >",NURSNAME
.
PRTB1390  BRANCH    COLM1OR2,PRTB1393,PRTB1395
.
PRTB1393  PRINT     *1,NURSDESC,*22,":",*24,NURSNAME;
          MOVE      TWO,COLM1OR2                  * print next desc in colm 1
          ADD       ONE,CLNO
          GOTO      PRTB1310
.
PRTB1395  PRINT     *67,NURSDESC,*89,":",*91,NURSNAME
          MOVE      ONE,COLM1OR2                  * print next desc in colm 2
          GOTO      PRTB1310
.
. print operation codes or No. of operations (Sys Par. dependant)
.
PRTB1400  COMPARE   ONE,COLM1OR2       * need to print a new line?
          GOTO      PRTB1500 IF EQUAL
.
          PRINT     *N;
.
PRTB1500  BRANCH    OPCNNOOP,PRTB1900
.
          MOVE      SP40,IDESC
          PACK      KEY17,OPDCICD1,OPDADATE,SP70
          CALL      PATITMRD
          PRINT     *1,"Operation Codes",*22,":",*24,OPDCICD1," ",IDESC
          ADD       ONE,CLNO
          MATCH     SP9,OPDCICD2
          GOTO      PRTB9999 IF EQUAL
          MOVE      SP40,IDESC
          PACK      KEY17,OPDCICD2,OPDADATE,SP70
          CALL      PATITMRD
          PRINT     *24,OPDCICD2," ",IDESC
          ADD       ONE,CLNO
          MATCH     SP9,OPDCICD3
          GOTO      PRTB9999 IF EQUAL
          MOVE      SP40,IDESC
          PACK      KEY17,OPDCICD3,OPDADATE,SP70
          CALL      PATITMRD
          PRINT     *24,OPDCICD3," ",IDESC
          MOVE      SP40,IDESC
          MATCH     SP9,OPDCICD4
          GOTO      PRTB9999 IF EQUAL
          MOVE      SP40,IDESC
          PACK      KEY17,OPDCICD4,OPDADATE,SP70
          CALL      PATITMRD
          PRINT     *24,OPDCICD4," ",IDESC
          ADD       ONE,CLNO
          MATCH     SP9,OPDCICD5
          GOTO      PRTB9999 IF EQUAL
          MOVE      SP40,IDESC
          PACK      KEY17,OPDCICD5,OPDADATE,SP70
          CALL      PATITMRD
          PRINT     *24,OPDCICD5," ",IDESC
          ADD       ONE,CLNO
          MATCH     SP9,OPDCICD6
          MOVE      SP40,IDESC
          PACK      KEY17,OPDCICD6,OPDADATE,SP70
          CALL      PATITMRD
          GOTO      PRTB9999 IF EQUAL
          PRINT     *24,OPDCICD6," ",IDESC
          ADD       ONE,CLNO
          GOTO      PRTB9999
.
PRTB1900  PRINT     *1,"No of Operations",*22,":",*24,OPDCNOOP
          ADD       ONE,CLNO
          GOTO      PRTB9999
.
. we have no team details to print
.
PRTB1950  PRINT     *1,"*** Operating Team details not on file ***"
          ADD       ONE,CLNO
          GOTO      PRTB9999
.
. ******* Report type 2 *******
.
PRTB2000  BRANCH    DETSOK,PRTB2950         * no team details
.
          IF        OPDCDURA=0
            PRINT     *N," Team : ",OPDBTEAM,*57,STRTTIME,*64,ENDTIME:
                      *90,ANAEDESC;
          ELSE
            PRINT     *N," Team : ",OPDBTEAM,*57,STRTTIME,*64,ENDTIME,SP3:
                      OPDCDURA,*90,ANAEDESC;
          ENDIF
          ADD       ONE,CLNO
          ADD       OPDCDURA,OPSUBTIM       * time
          ADD       OPDCNOOP,OPSUBNUM       * number of ops
          CALL      XXXX0000                * display all oper. personnel
          GOTO      PRTB9999
.
PRTB2950  PRINT     *1,"*** Operating Team details not on file ***":
                    *N,DASH40,DASH40,DASH40,DASH12
          ADD       ONE,CLNO
          GOTO      PRTB9999
.
PRTB9999  RETURN
+
.*************************************************************************
.*                              PEND0000            Called by: RPRT0000  *
.*                    print end of session details                       *
.*************************************************************************
PEND0000  BRANCH    OPCNR64A,PEND1000,PEND9999
          GOTO      PEND9999
.
. ******* Report 1 *******
.
PEND1000  PRINT     *N,*1,UNDEQL40,UNDEQL40,UNDEQL40,"============"
          PRINT     *22,"End of Session          Patient Total : ",PATTOTAL 
          PRINT     *1,DASH40,DASH40,DASH40,DASH12
          GOTO      PEND9999
.
PEND9999  RETURN
+
.*************************************************************************
.*                             PDEA0000             Called by: RPRT0000  *
.*                    get DEA  details of report                         *
.*************************************************************************
PDEA0000  MOVE      ONE,COUNTER1
          MOVE      ONE,COUNTER2
          MOVE      ONE,COUNTER3
.
PDEA1000  LOAD      OPERDESC,COUNTER1,OPDADES1,OPDADES2,OPDADES3,SP30,SP30,SP30
.
          BRANCH    SRGFLAG,PDEA2000
.
. check if we have no more to print
.
          ADD       ONE,COUNTER1
          CALL      MORE0000
          BRANCH    EXIT,PDEA9999
.
. get a pre-op time
.
PDEA1100  LOAD      PREOPDES,COUNTER2,OPCNDPR1,OPCNDPR2,OPCNDPR3,OPCNDPR4:
                                      OPCNDPR5
          ADD       ONE,COUNTER2                  * increment counter
.
          COMPARE   SIX,COUNTER2                  * no more Pre-op times?
          GOTO      PDEA1300 IF NOT LESS
.
          MATCH     SP20,PREOPDES                 * is description blank
          GOTO      PDEA1100 IF EQUAL
.
.     we have a desc that is not blank so get corresponding time
.
          SUB       ONE,COUNTER2
          LOAD      PREOPTIM,COUNTER2,OPDAPRE1,OPDAPRE2,OPDAPRE3,OPDAPRE4:
                                      OPDAPRE5
          ADD       ONE,COUNTER2
.
. get a Post-op time
.
PDEA1300  COMPARE   FIVE,COUNTER3                 * no more Pre-op times?
          GOTO      PDEA1500 IF NOT LESS
.
          LOAD      PSTOPDES,COUNTER3,OPCNDPS1,OPCNDPS2,OPCNDPS3,OPCNDPS4
.
          ADD       ONE,COUNTER3                  * increment counter
.
          MATCH     SP20,PREOPDES                 * is description blank
          GOTO      PDEA1300 IF EQUAL
.
.     we have a desc that is not blank so get corresponding time
.
          SUB       ONE,COUNTER3
          LOAD      PSTOPTIM,COUNTER3,OPDAPOS1,OPDAPOS2,OPDAPOS3,OPDAPOS4
          ADD       ONE,COUNTER3
.
PDEA1400  PRINT     *25,OPERDESC;
.
          PACK      DESC40,PREOPDES,PSTOPDES      * both desc blank?
          MATCH     SP40,DESC40
          GOTO      PDEA1500 IF EQUAL
.
          PRINT     *72,PREOPDES,*94,":",*96,PREOPTIM:
                    *103,PSTOPDES,*124,":",*126,PSTOPTIM
          ADD       ONE,CLNO
.
PDEA1500  MOVE      SP5,PREOPTIM                  * clear variables
          MOVE      SP5,PSTOPTIM 
          GOTO      PDEA1000
.
.         WEB Theatre
.
PDEA2000  COMPARE   SIX,COUNTER1
          GOTO      PDEA9999 IF NOT LESS
.
          PRINT     *25,OPERDESC;
          BRANCH    COUNTER1,PDEA2100,PDEA2200,PDEA2300,PDEA2400,PDEA2500
          GOTO      PDEA9999
.
PDEA2100  PRINT     *72,"Arrival Time",*90,":",*93,OPARATIM:
                    *102,"Recovery Time (Front)",*123,":",*125,OPARTIRF
          ADD       ONE,COUNTER1
          ADD       ONE,CLNO
          GOTO      PDEA1000
.
PDEA2200  PRINT     *72,"Anaesthetic Prep",*90,":",*93,OPARANAP:
                    *102,"Recovery Time (Back)",*123,":",*125,OPARTIRB
          ADD       ONE,COUNTER1
          ADD       ONE,CLNO
          GOTO      PDEA1000
.
PDEA2300  PRINT     *72,"Anaesthetic Start",*90,":",*93,OPARANAS:
                    *102,"Recovery Time (D/P)",*123,":",*125,OPARTIRD
          ADD       ONE,COUNTER1
          ADD       ONE,CLNO
          GOTO      PDEA1000
.
PDEA2400  PRINT     *72,"Time Patient Died",*90,":",*93,OPARTEXP:
                    *102,"Time Exit Theatre",*123,":",*125,OPARTETC
          ADD       ONE,COUNTER1
          ADD       ONE,CLNO
          GOTO      PDEA1000
.
PDEA2500  PRINT     *72,"Time went to ICU",*90,":",*93,OPARTICU:
                    *102,"Ready to Depart Time",*123,":",*125,OPARTIDP
          ADD       ONE,COUNTER1
          ADD       ONE,CLNO
          GOTO      PDEA1000
.
PDEA9999  RETURN
+
.*************************************************************************
.*                             MORE0000             Called by: RPRT0000  *
.*                     any more details to print                         *
.*************************************************************************
MORE0000  COMPARE   FOUR,COUNTER1
          GOTO      MORE8000 IF LESS
.
          COMPARE   SIX,COUNTER2
          GOTO      MORE8000 IF LESS
         
          COMPARE   FIVE,COUNTER3
          GOTO      MORE8000 IF LESS
.
          MOVE      ONE,EXIT
          GOTO      MORE9999
.
MORE8000  MOVE      ZERO,EXIT
.
MORE9999  RETURN
+
.*************************************************************************
.*                             DETS0000             Called by: RPRT0000  *
.*                    get main details of report                         *
.*************************************************************************
DETS0000  CALL      GPMI0000              * get PMI details to be printed
          BRANCH    EXIT,DETS9999         * Patient not found. Ignore patient
.
          MOVE      ZERO,SRGFLAG
          PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          IF        OVRCD=0
            MOVE      ONE,SRGFLAG
          ENDIF
.
. get Operation type
.
.          MATCH     SP3,OPDATYPE
.          GOTO      DETS0700 IF EQUAL
.          PACK      KEY5,CODEOY,OPDATYPE
.          CALL      RDCODE1
.          BRANCH    OVRCD,DETS0700
.          MOVE      TDESC,OTYPDESC
.          GOTO      DETS1000
.
.DETS0700  MOVE      " < Missing Code > ",OTYPDESC
.
. get anaesthetic description
.
DETS1000  MATCH     SP3,OPDAANAE
          GOTO      DETS1700 IF EQUAL
          PACK      KEY5,CODEOA,OPDAANAE
          CALL      RDCODE1
          BRANCH    OVRCD,DETS1700
          MOVE      TDESC,ANAEDESC
          GOTO      DETS2000
.
DETS1700  MOVE      " < Missing Code > ",ANAEDESC
.
. get Known infection
.
DETS2000  MOVE      SP70,KNOWDESC
          BRANCH    SRGFLAG,DETS3000
          MATCH     SP3,OPDAKNOW
          GOTO      DETS2700 IF EQUAL
          PACK      KEY5,CODEOK,OPDAKNOW
          CALL      RDCODE1
          BRANCH    OVRCD,DETS2700
          MOVE      TDESC,KNOWDESC
          GOTO      DETS3000
.
DETS2700  MOVE      " < Missing Code > ",KNOWDESC
.
. we have ALL the DEA variables and descriptions that need to be displayed
. 
DETS3000  MOVE      ZERO,EXIT
.
DETS9999  RETURN
+
.*************************************************************************
.*                             DEBC0000             Called by: RPRT0000  *
.*                  get DEB & DEC details                                *
.*************************************************************************
DEBC0000  MOVE      ZERO,FNDTEAM                 * init found team flag
.
. get DEB details (team details)
.
          MOVE      ZERO,CURTEAM
.
DEBC1000  COMPARE   NINE,CURTEAM
          GOTO      DEBC7000 IF EQUAL
.
. check if we need a new page
.
          COMPARE   "50",CLNO 
          GOTO      DEBC1100 IF LESS
.
          CALL      PAGE0000                      * set up new page
.
DEBC1100  ADD       ONE,CURTEAM
.
          BRANCH    SRGFLAG,DEBC2100              * use surgical file
.
          PACK      KEY11,OPDAUNIQ,CURTEAM
          CALL      RDOPDEB1
          BRANCH    OVRCD,DEBC4000                * no more records
.
          MOVE      ONE,FNDTEAM                  * we have found a team
.
. get DEC details
.
DEBC2000  PACK      KEY11,OPDAUNIQ,OPDBTEAM
          CALL      RDOPDEC1                      * read next record
          BRANCH    OVRCD,DEBC3000                * blank out DEC variables
.
. get start time and end time (sys par. OPCNFOTM)
.
          LOAD      STRTTIME,OPCNFOTM,OPDCOTM1,OPDCOTM2,OPDCOTM3,OPDCOTM4
          LOAD      ENDTIME,OPCNTOTM,OPDCOTM1,OPDCOTM2,OPDCOTM3,OPDCOTM4
.
          GOTO      DEBC2300
.
.         Use surgical file
.
DEBC2100  PACK      KEY11,OPDAUNIQ,CURTEAM
          CALL      RDOPSRG1
          BRANCH    OVRCD,DEBC4000          * no more record
.
          CALL      RCVT0000              * determine time in recovery
.
          LOAD      TIMESTRT,OPCNFTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:     
                                      OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:    
                                      OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:    
                                      OPARTIDP,OPARTETC
.
          LOAD      TIMESTOP,OPCNTTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:     
                                      OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                                      OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:    
                                      OPARTIDP,OPARTETC                       
.
          MOVE      TIMESTRT,STRTTIME      *** CAR 37263
          MOVE      TIMESTOP,ENDTIME
.
          CALL      DURN0000                * get operation duration
.
          CALL      GSTF0000                * get doctor and nurse details
.
.         If no team details and this is the first team, get team details
.         from session record
.
          IF        FNDTEAM=0 & CURTEAM=1
            GOTO      DEBC4000              * Get team details from session
          ENDIF
.
DEBC2200  MOVE      OPSGTMNO,OPDBTEAM
.          MOVE      OPSGTISS,STRTTIME
.          MOVE      OPSGTISE,ENDTIME
          REP       "2110",OPSGCCNT
          MOVE      OPSGCCNT,OPDCCONT
          MOVE      SP70,OPDCUSR2
          MOVE      OPSGNOOP,OPDCNOOP
          MOVE      DURATION,OPDCDURA
.
. get correct count description
.
DEBC2300  MATCH     "3",OPDCCONT
          GOTO      DEBC2400 IF NOT EQUAL
          MOVE      "N/A",CCOUNTYN
          GOTO      DEBC5000
.
DEBC2400  MATCH     "1",OPDCCONT
          GOTO      DEBC2500 IF NOT EQUAL
          MOVE      "Yes",CCOUNTYN
          GOTO      DEBC5000
.
DEBC2500  MOVE      "No ",CCOUNTYN
          GOTO      DEBC5000
.
. there was no DEC record for the team (from DEB) so blank out variables
.
DEBC3000  MOVE      ONE,COUNT   
          MOVE      ZERO,OPDCNOOP
          MOVE      SP5,STRTTIME                  * blank out display vars
          MOVE      SP3,ENDTIME
          MOVE      SP3,CCOUNTYN
.
DEBC3100  LOAD      INITVARS,COUNT,OPDCOTM1,OPDCOTM2,OPDCOTM3,OPDCOTM4:
                        OPDCCONT,OPDCICD1,OPDCICD2,OPDCICD3:
                        OPDCICD4,OPDCICD5,OPDCICD6
.
          MOVE      SP10,INITVARS
.
          STORE     INITVARS,COUNT,OPDCOTM1,OPDCOTM2,OPDCOTM3,OPDCOTM4:
                        OPDCCONT,OPDCICD1,OPDCICD2,OPDCICD3:
                        OPDCICD4,OPDCICD5,OPDCICD6
.
          ADD       ONE,COUNT
          COMPARE   "12",COUNT
          GOTO      DEBC3100 IF LESS              * init next variable
          GOTO      DEBC5000                      * now go and print
.
.         No team details found. If this is team 1, use the session default.
.         Otherwise, we have finished printing all team details.
.
DEBC4000  COMPARE   ONE,CURTEAM
          GOTO      DEBC7000 IF NOT EQUAL
.
. get team details from SESFD
.
          PACK      DIM42,OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4,OPSETYD5:
                          OPSETYD6,OPSETYD7,OPSETYD8,OPSETYN1,OPSETYN2:
                          OPSETYN3,OPSETYN4,OPSETYN5,OPSETYN6
.
          MATCH     SP42,DIM42                    * are details blank?
          GOTO      DEBC4200 IF EQUAL
.
          MOVE      OPSETYD1,OPDBTYD1
          MOVE      OPSETYD2,OPDBTYD2
          MOVE      OPSETYD3,OPDBTYD3
          MOVE      OPSETYD4,OPDBTYD4
          MOVE      OPSETYD5,OPDBTYD5
          MOVE      OPSETYD6,OPDBTYD6
          MOVE      OPSETYD7,OPDBTYD7
          MOVE      OPSETYD8,OPDBTYD8
          MOVE      OPSEDCC1,OPDBDCC1
          MOVE      OPSEDCC2,OPDBDCC2
          MOVE      OPSEDCC3,OPDBDCC3
          MOVE      OPSEDCC4,OPDBDCC4
          MOVE      OPSEDCC5,OPDBDCC5
          MOVE      OPSEDCC6,OPDBDCC6
          MOVE      OPSEDCC7,OPDBDCC7
          MOVE      OPSEDCC8,OPDBDCC8
          MOVE      OPSETYN1,OPDBTYN1
          MOVE      OPSETYN2,OPDBTYN2
          MOVE      OPSETYN3,OPDBTYN3
          MOVE      OPSETYN4,OPDBTYN4
          MOVE      OPSETYN5,OPDBTYN5
          MOVE      OPSETYN6,OPDBTYN6
          MOVE      OPSENUC1,OPDBNUC1
          MOVE      OPSENUC2,OPDBNUC2
          MOVE      OPSENUC3,OPDBNUC3
          MOVE      OPSENUC4,OPDBNUC4
          MOVE      OPSENUC5,OPDBNUC5
          MOVE      OPSENUC6,OPDBNUC6
          MOVE      ONE,OPDBTEAM
          MOVE      TWO,FNDTEAM      * we have found a team from session details
          BRANCH    SRGFLAG,DEBC2200
          GOTO      DEBC2000                      * get the DEC details
.
DEBC4200  MOVE      ONE,DETSOK                    * we dont have any details
          CALL      PRTB0000
          GOTO      DEBC1000 
.
. we have all of the details so print them
.
DEBC5000  MOVE      ZERO,DETSOK                   * we have details to print
          CALL      PRTB0000
          GOTO      DEBC1000
.
.         print the details for the end of each unique id
.
DEBC7000  COMPARE   TWO,OPCNR64A                  * only print for layout 2
          GOTO      DEBC9999 IF NOT EQUAL
.
          PRINT     *40,"Operation Sub Total : ",OPSUBNUM:
                    SP3,OPSUBTIM," Minutes"
          PRINT     DASH40,DASH40,DASH40,DASH12
          ADD       OPSUBNUM,OPGRDNUM
          ADD       OPSUBTIM,OPGRDTIM
          ADD       "2",CLNO
          MOVE      ZERO,OPSUBNUM           * clear number of ops
          MOVE      ZERO,OPSUBTIM           * clear time
.
DEBC9999  RETURN
+
.*********************************************************************
.*                  DURN0000                    Called by : DEBC2000 *
.*        Calculate a duration given two times                       *
.*        Para's  : TIMESTOP      the end time   (HH:MM)             *
.*                  TIMESTRT      the start time (HH:MM)             *
.*        Returns : DURATION      the duration (minutes)             *
.*********************************************************************
DURN0000  MOVE      ZERO,DURATION
          MATCH     TIMESTOP,TIMESTRT
          GOTO      DURN9999 IF EQUAL       * no duration
          GOTO      DURN1000 IF LESS        * times not over midnight
.
.         operation went over midnight so make end time larger
.         eg 22:00 to 01:00 becomes 22:00 to 25:00 which gives 3 hours duration
.
          UNPACK    TIMESTOP,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2
          ADD       "24",FORM2
          MOVE      FORM2,CHOUR
          PACK      TIMESTOP,CHOUR,ANS,CMIN
.
.         get end time as minutes
.
DURN1000  UNPACK    TIMESTOP,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM4A            * set up work variable
          MULT      "60",FORM4A             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ADD       FORM2,FORM4A            * the total minutes of end time
.
.         get start time as minutes
.
          UNPACK    TIMESTRT,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM4B            * set up work variable
          MULT      "60",FORM4B             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ADD       FORM2,FORM4B            * the total minutes of start time
.
.         duration = end minutes - start minutes
.
          SUB       FORM4B,FORM4A           * the duration
          MOVE      FORM4A,DURATION         * the duration
.
DURN9999  RETURN
+
.*************************************************************************
.*                             GDOC0000                                  *
.*                  get staff members                                    *
.*************************************************************************
GSTF0000  CALL      ITSM0000
          UNPACK    SP70,OPDCICD1,OPDCICD2,OPDCICD3,OPDCICD4:  *** CAR 37263
                         OPDCICD5,OPDCICD6
.
          MOVE      ZERO,FNDTEAM
          MOVE      ZERO,COUNT
          MOVE      ZERO,FORM1
          MOVE      " 0",KEY2                * staff type as doctor
          PACK      KEY35,OPDAUNIQ,CURTEAM,KEY2,SP70
          CALL      RSOPTSM1
GSTF1000  CALL      RKOPTSM1
          BRANCH    OVRCD,GSTF5000
          MATCH     OPDAUNIQ,OPTSUNID        * same unique number?
          GOTO      GSTF5000 IF NOT EQUAL
          MOVE      CURTEAM,KEY1
          MATCH     KEY1,OPTSTMNO
          GOTO      GSTF5000 IF NOT EQUAL
          MATCH     SP6,OPTSSCDE
          GOTO      GSTF1000 IF EQUAL
.
          ADD       ONE,COUNT
          BRANCH    OPTSSIND,GSTF1010,GSTF1000,GSTF1700  * Skip nurse
          GOTO      GSTF1010
.
GSTF1010  PACK      KEY14,OPTSUNID,OPTSTMNO,SP30
          CALL      RSOPPMB1
GSTF1020  CALL      RKOPPMB1
          BRANCH    OVRCD,GSTF1030
.
          MATCH     OPTSUNID,OPPMUNID
          GOTO      GSTF1030 IF NOT EQUAL
. 
          MATCH     OPTSTMNO,OPPMTMNO
          GOTO      GSTF1030 IF NOT EQUAL
.
          STORE     OPPMCMBS,OPPMCNTR,OPDCICD1,OPDCICD2,OPDCICD3,OPDCICD4:
                                      OPDCICD5,OPDCICD6
          GOTO      GSTF1020
.
GSTF1030  BRANCH    COUNT,GSTF1100,GSTF1200,GSTF1300,GSTF1400
          GOTO      GSTF5000
.
GSTF1100  MOVE      OPTSSCDE,OPDBDCC1
          MOVE      OPTSSTYP,OPDBTYD1
          MOVE      OPTSSLEV,OPDBLVD1
          GOTO      GSTF1000
GSTF1200  MOVE      OPTSSCDE,OPDBDCC2
          MOVE      OPTSSTYP,OPDBTYD2
          MOVE      OPTSSLEV,OPDBLVD2
          GOTO      GSTF1000
GSTF1300  MOVE      OPTSSCDE,OPDBDCC3
          MOVE      OPTSSTYP,OPDBTYD3
          MOVE      OPTSSLEV,OPDBLVD3
          GOTO      GSTF1000
GSTF1400  MOVE      OPTSSCDE,OPDBDCC4
          MOVE      OPTSSTYP,OPDBTYD4
          MOVE      OPTSSLEV,OPDBLVD4
          GOTO      GSTF1000
GSTF1500  MOVE      OPTSSCDE,OPDBDCC5
          MOVE      OPTSSTYP,OPDBTYD5
          MOVE      OPTSSLEV,OPDBLVD5
          GOTO      GSTF1000
GSTF1600  MOVE      OPTSSCDE,OPDBDCC6
          MOVE      OPTSSTYP,OPDBTYD6
          MOVE      OPTSSLEV,OPDBLVD6
          GOTO      GSTF1000
.
.         The following fields are for other staff 
.
GSTF1700  BRANCH    FORM1,GSTF1800,GSTF5000
          MOVE      OPTSSCDE,OPDBDCC7
          MOVE      OPTSSTYP,OPDBTYD7
          MOVE      OPTSSLEV,OPDBLVD7
          ADD       ONE,FORM1
          GOTO      GSTF1000
GSTF1800  MOVE      OPTSSCDE,OPDBDCC8
          MOVE      OPTSSTYP,OPDBTYD8
          MOVE      OPTSSLEV,OPDBLVD8
          ADD       ONE,FORM1
          GOTO      GSTF1000
.
.         Get nurse codes for the team
.
GSTF5000  IF        COUNT<>0
            MOVE      ONE,FNDTEAM               * found doctor 
          ENDIF
.
          MOVE      ZERO,COUNT
          MOVE      " 2",KEY2                * staff type as nurse
          PACK      KEY35,OPDAUNIQ,CURTEAM,KEY2,SP70
          CALL      RSOPTSM1
GSTF6000  CALL      RKOPTSM1
          BRANCH    OVRCD,GSTF8000
          MATCH     OPDAUNIQ,OPTSUNID        * same unique number?
          GOTO      GSTF8000 IF NOT EQUAL
          MOVE      CURTEAM,KEY1
          MATCH     KEY1,OPTSTMNO
          GOTO      GSTF8000 IF NOT EQUAL
          COMPARE   TWO,OPTSSIND
          GOTO      GSTF8000 IF NOT EQUAL
          MATCH     SP6,OPTSSCDE
          GOTO      GSTF6000 IF EQUAL
.
          ADD       ONE,COUNT
          BRANCH    COUNT,GSTF6100,GSTF6200,GSTF6300,GSTF6400:
                          GSTF6500,GSTF6600
          GOTO      GSTF8000
.
GSTF6100  MOVE      OPTSSCDE,OPDBNUC1
          MOVE      OPTSSTYP,OPDBTYN1
          GOTO      GSTF6000
GSTF6200  MOVE      OPTSSCDE,OPDBNUC2
          MOVE      OPTSSTYP,OPDBTYN2
          GOTO      GSTF6000
GSTF6300  MOVE      OPTSSCDE,OPDBNUC3
          MOVE      OPTSSTYP,OPDBTYN3
          GOTO      GSTF6000
GSTF6400  MOVE      OPTSSCDE,OPDBNUC4
          MOVE      OPTSSTYP,OPDBTYN4
          GOTO      GSTF6000
GSTF6500  MOVE      OPTSSCDE,OPDBNUC5
          MOVE      OPTSSTYP,OPDBTYN5
          GOTO      GSTF6000
GSTF6600  MOVE      OPTSSCDE,OPDBNUC6
          MOVE      OPTSSTYP,OPDBTYN6
          GOTO      GSTF6000
.
GSTF8000  IF        FNDTEAM=0 & COUNT<>0
            MOVE      ONE,FNDTEAM     * found nursing 
          ENDIF
GSTF9999  RETURN
+
.*************************************************************************
.*                             GPMI0000             Called by: RPRT0000  *
.*                  get PMI details from wherever                        *
.*************************************************************************
GPMI0000  MOVE      OPDAADMN,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,GPMI1000          * try bokrecfd
.
          MATCH     ZEROUR,AURNO 
          GOTO      GPMI2000 IF EQUAL       * on pram file
.
          MOVE      AURNO,PURNO             * set up UR number
          GOTO      GPMI3000
.
.         no admission record so get UR no. from BOKRC1FD
.
GPMI1000  PACK      KEY8,OPDAADMN
          CALL      RDBKREC1
          BRANCH    OVRCD,GPMI9500
.
          MATCH     ZEROUR,BKREURNO
          GOTO      GPMI2000 IF EQUAL       * on pram file
.
          MOVE      BKREURNO,PURNO
          GOTO      GPMI3000
.
.         get PMI details from PATPR1FD
.
GPMI2000  PACK      KEY8,OPDAADMN
          CALL      RDPRAM1
          BRANCH    OVRCD,GPMI9000
          MOVE      ZEROUR,PURNO
          GOTO      GPMI5000
.
.         get name from master file
.
GPMI3000  MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GPMI9000
.
. we have patient details so format them (details) 
.
GPMI5000  .
. format patients name
.
          MOVE      TWO,PACFRMT
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PATSNAME
.
. format admission date
.
          MOVE      SP10,ADMDATE            
          MOVE      OPDAADMN,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,GPMI5600          * try bokrecfd file
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ADMDATE
.
.  get patients ward and bed
.
GPMI5500 
          PACK      KEY30,OPDAADMN,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,GPMI5700
.
          MATCH     OPDAADMN,DTADMN               * same admission no.?
          GOTO      GPMI6000 IF EQUAL
          GOTO      GPMI5700
.
. read bokrc1af
.
GPMI5600  MOVE      OPDAADMN,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,GPMI5700          * no bokrec record
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ADMDATE         * exp admission date
          MOVE      BKREWARD,TWARD          * exp ward
          MOVE      BKREEBED,TBED           * exp bed
          PACK      ADATE,CCENT,CYEAR,CMON,CDAY   * set up for calcage
          GOTO      GPMI6000
.
. there is no record so blank out Ward and Bed
.
GPMI5700  MOVE      SP3,TWARD                
          MOVE      SP3,TBED
.
. calculate patients age
.
GPMI6000  MOVE      ADATE,AGEDATE
          CALL      CALCAGE                    .(Returns PSAGEYRS)
.
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DOBDATE
.
.         get attending doctor
.
          MOVE      "Invalid Attending Doctor",ATTDOCT
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GPMI6100
          MOVE      TWO,PACFRMT
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,ATTDOCT
.
.         get admission type
.
GPMI6100  PACK      KEY5,CODEA,ATYPE
          CALL      RDCODE1
          MOVE      TDESC,ADMTYPE
.
. we now have all the patient details (formatted)
. 
          MOVE      ZERO,EXIT
          GOTO      GPMI9999
.
GPMI9000  MOVE      "** Missing PMI Details **",PATSNAME
.
. blank out ALL display variables
.
          MOVE      ZEROUR,AURNO
          MOVE      ZEROVISN,OPDAADMN
          MOVE      SP8,ADMDATE
          MOVE      SP8,DOBDATE
          MOVE      SP3,TWARD
          MOVE      SP3,TBED
          MOVE      SP3,PSAGEYRS
          MOVE      SP1,PSEX
          MOVE      FALSE,EXIT
          GOTO      GPMI9999
.
GPMI9500  MOVE      TRUE,EXIT               * no PMI details. Ignore this Pat.
.
GPMI9999  RETURN
+
.****************************************************************************
.*                                 CLRV0000            Called by: ML0000    *
.*                       clear variables                                    *
.****************************************************************************
CLRV0000  MOVE      SP8,STRTDATE
          MOVE      SP8,ENDDATE
          MOVE      SP6,STTMPCOD
          MOVE      SP6,ENTMPCOD
          MOVE      SP30,STTMPDES
          MOVE      SP30,ENTMPDES
.
CLRV9999  RETURN
+
.****************************************************************************
.*                               ENDS0000             Called by: RPRT0000   *
.*     check if end of session.                                             *
.****************************************************************************
ENDS0000  .
. save our current record so we don't lose our place
.
          MOVE      OPDAHOSP,SAVEHOSP
          MOVE      OPDADATE,SAVEDATE
          MOVE      OPDATIME,SAVETIME
          MOVE      OPDACLIN,SAVECLIN
          MOVE      OPDASTAT,SAVESTAT
          MOVE      OPDACASE,SAVECASE
.
ENDS1000  CALL      RKOPDEA5
          BRANCH    OVRCD,ENDS8000                * get next case
.
          MATCH     SAVEDATE,OPDADATE             * still same date?
          GOTO      ENDS8000 IF NOT EQUAL
.
          MATCH     SAVETIME,OPDATIME             * still same time?
          GOTO      ENDS8000 IF NOT EQUAL
.
          MATCH     SAVECLIN,OPDACLIN             * still same clinic?
          GOTO      ENDS8000 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT                * booked?
          GOTO      ENDS1000 IF EQUAL
.
. we have another case for the same session so
. put record pointer on original position
.
          PACK      KEY26,SAVEHOSP,SAVEDATE,SAVETIME,SAVECLIN,SAVECASE:
                          SAVESTAT,SP70
          CALL      RDOPDEA5
          MOVE      ONE,EXIT
          GOTO      ENDS9999
.
ENDS8000  MOVE      ZERO,EXIT                     * different session
.
ENDS9999  RETURN
.
.*********************************************************************
.*                  XXXX0000                    Called by : PRTB2000 *
.*        display all surgeons/anaes/assistants                      *
.*********************************************************************
XXXX0000  MOVE      ZERO,FORM2
.
XXXX1000  ADD       ONE,FORM2
          COMPARE   NINE,FORM2
          GOTO      XXXX9000 IF NOT LESS    * finished check
.
.         get the doctor type
.
          LOAD      ACODE,FORM2,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
          MATCH     SP3,ACODE
          GOTO      XXXX1000 IF EQUAL       * no code to check
.
.         If we are using a default team from session file 
.
          COMPARE   TWO,FNDTEAM
          GOTO      XXXX1100 IF EQUAL
.
          IF        SRGFLAG=1 & (FORM2=7 | FORM2=8)
            PACK      KEY5,CODEOF,ACODE
            GOTO      XXXX1200
          ENDIF
.
XXXX1100  PACK      KEY5,CODEOP,ACODE
XXXX1200  CALL      RDCODE1
          BRANCH    OVRCD,XXXX1000          * invalid code
          MOVE      TDESC,KEY20             * save description
.
.         get the doctor code
.
          LOAD      KEY6,FORM2,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                               OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          MATCH     SP6,KEY6
          GOTO      XXXX1000 IF EQUAL       * no code to check
.
          CALL      RDDOCT1
          BRANCH    OVRCD,XXXX1000
.
          MOVE      TWO,PACFRMT
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,DOCTDESC
          MOVE      DCODE,DOCTCODE
.
.         get the supervision level if they have one
.
          MOVE      SP3,ACODE               * clear code
          MOVE      SP20,TDESC              * clear name
          LOAD      ACODE,FORM2,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                                OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
.
          MATCH     SP3,ACODE
          GOTO      XXXX2000 IF EQUAL       * no code to check
.
          MOVE      SP20,TDESC
          PACK      KEY5,CODEOS,ACODE
          CALL      RDCODE1
.
XXXX2000  PRINT     *N,KEY20," : ",DOCTCODE,SP2,DOCTDESC,SP6,ACODE,SP2,TDESC;
          ADD       ONE,CLNO
          BRANCH    SRGFLAG,XXXX1000        * using new surgical file
.
.         see if to print extra details
.
          COMPARE   THREE,FORM2
          GOTO      XXXX1000 IF NOT LESS    * extra details printed
.
          BRANCH    FORM2,XXXX3000          * printing first personnel
.
          MOVE      SP20,TDESC              * print OPDAUSR1
          MATCH     SP3,OPDAUSR1
          GOTO      XXXX1000 IF EQUAL       * no code to print
          PACK      KEY5,CODEY6,OPDAUSR1
          CALL      RDCODE1
          PRINT     *90,TDESC;
          GOTO      XXXX1000
.
XXXX3000  MOVE      SP20,TDESC
          PACK      KEY5,CODEZ2,OPDCUSR2
          CALL      RDCODE1
          MOVE      TDESC,CCITEMD
.
          PRINT     *90,"Major Surgery : ",CCITEMD," Count Correct : ",CCOUNTYN;
          GOTO      XXXX1000
.
XXXX9000  PRINT     *N,DASH40,DASH40,DASH40,DASH12
.
XXXX9999  RETURN
+
.*********************************************************************
.*                  ITSM0000                                         *
.*        Initialise staff members field                             *
.*********************************************************************
ITSM0000  UNPACK    SP70,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                         OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          UNPACK    SP70,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                         OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
          UNPACK    SP70,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                         OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
          UNPACK    SP70,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4:
                         OPDBTYN5,OPDBTYN6
          UNPACK    SP70,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4:
                         OPDBNUC5,OPDBNUC6
ITSM9999  RETURN
.
.         I/O includes
.
          INC       STD001IO
          INC       BOKRC1IO/INC
          INC       CALCAGE  
          INC       OPRCLIDS
          INC       OPRECOVT
          INC       PATITMRD
          INC       OPRARDIO/INC
          INC       OPRSRGIO/INC
          INC       OPRTSMIO/INC
          INC       OPRCLIIO/INC
          INC       OPRDEAIO/INC
          INC       OPRDEBIO/INC
          INC       OPRDECIO/INC
          INC       OPRDEDIO/INC
          INC       OPRNURIO/INC
          INC       OPROPRIO/INC
          INC       OPRPMBIO/INC
          INC       OPRSESIO/INC
          INC       PATDOCDS
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSHCPIO/INC   * C-0825792 HCP File
          INC       PMSVX1IO/INC
          INC       PATPR1IO/INC
          INC       PATTRNIO/INC
...............................................................................

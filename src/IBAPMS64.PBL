.------------------------------------------------------------------------------
. Program        :  IBAPMS64                                                
. Description    :  Upload NZ Private Procedure Fee Records            
.------------------------------------------------------------------------------
. Modifications  :                                                        
.------------------------------------------------------------------------------
. V11.03.01  05/07/2023  J.Tan         TSK 0913418                           
.            Mod to write a header record for  NZP Procedure Matrix
. V11.03.00  25/11/2022  Thanh T       TSK 0913418                           
.            New Upload porgram for NZP Procedure Fee Records  
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC            * Codes file
          INC       PATHSPFD/INC
          INC       PATFN1FD/INC
          INC       PATCMCFD/INC
.
          INC       NZPPFEFD/INC
          INC       NZPRBRFD/INC
          INC       NZPIBRFD/INC
          INC       NZPCMTFD/INC
.
. NZ Private Contract Procedure Fee Upload file
.
NZPFETXT  FILE       HL7, FIXED=175
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XZPFHOSP  DIM       3      3        1     Hospital Code (pathspaf)
XZPFCLMC  DIM       3      3        4     Claim Code (Catg CL)
XZPFCNTR  DIM       6      6        5     Contract (Health Fund)
XZPFFTAB  DIM       8      8        13    Fund Table
XZPFCPRC  DIM       9      9        21    Contract Procedure Code (patcmcaf)
XZPFEFDT  DIM       8      8        30    Effective Date (CCYYMMDD)
XZPFCONT  DIM       1      1        39    Maximum Funder Contribution 1=Yes
XZPFPPOR  FORM      14.2   17       40    Patient Co-Payment(if Contr.=No)
XZPFHPOR  FORM      14.2   17       57    Total Fixed Cost(if Contr.=No)
XZPFDES1  DIM       50     50       74    Description line 1
XZPFDES2  DIM       50     50       124   Description line 2
XZPFRBRK  DIM       1      1        174   Revenue Breakdown(avail if Contr.=No)
XZPFIBRK  DIM       1      1        175   Invoice Breakdown(avail if Contr.=No) 
.
.End of Record      176
.
. NZ Private Contract Procedure Revenue Breakdown Upload file
.
NZPRBTXT  FILE       HL7, FIXED=57
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XZRBHOSP  DIM       3      3        1     Hospital Code (pathspaf) 
XZRBCLMC  DIM       3      3        4     Claim Code (Catg CL)
XZRBCNTR  DIM       6      6        7     Contract (Health Fund)
XZRBFTAB  DIM       8      8        13    Fund Table
XZRBCPRC  DIM       9      9        21    Contract Procedure Code (patcmcaf)
XZRBEFDT  DIM       8      8        30    Effective Date (CCYYMMDD)
XZRBBRCD  DIM       3      3        38    Breakdown Code (Catg FI)
XZRBAMNT  FORM      14.2   17       41    Amount
.
.End of Record      58
.
. NZ Private Contract Procedure Invoice Breakdown Upload file
.
NZPIBTXT  FILE       HL7, FIXED=57
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XZIBHOSP  DIM       3      3        1     Hospital Code (pathspaf)
XZIBCLMC  DIM       3      3        4     Claim Code (Catg CL
XZIBCNTR  DIM       6      6        7     Contract (Health Fund)
XZIBFTAB  DIM       8      8        13    Fund Table
XZIBCPRC  DIM       9      9        21    Contract Procedure Code (patcmcaf)
XZIBEFDT  DIM       8      8        30    Effective Date (CCYYMMDD)
XZIBBRCD  DIM       3      3        38    Breakdown Code (Catg FI)
XZIBAMNT  FORM      14.2   17       41    Amount
.
.End of Record      58
.
. NZ Private Contract Procedure Matrix Upload file
.
NZPCMTXT  FILE       HL7, FIXED=33
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XZCMCLMC  DIM       3      3        1     Claim Code (Catg CL)
XZCMCNTR  DIM       6      6        4     Contract/Health Fund
XZCMRTYP  DIM       1      1        10    Record Type
XZCMCPRC  DIM       9      9        11    Contract Procedure Code (patcmcaf)
XZCMITYP  DIM       1      1        20    Item Type 0-Proc 1-Misc
XZCMFTYP  FORM      1      1        21    Field Type for Item Type=1(Misc Item)
XZCMPROC  DIM       9      9        22    Procedure or Misc Item Code
XZCMMIGP  DIM       3      3        31    Misc Item Group(Cat FI) (Item Type=1)
.
.End of Record      34
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      7
UPDCOUNT  FORM      7
ERRCOUNT  FORM      7
CMDLINE   DIM       200
WORKLINE  DIM       200
DIM2C     DIM       2
DIM2Y     DIM       2
DIM2M     DIM       2
DIM2D     DIM       2
DIM3      DIM       3
DIM4      DIM       4
DIM6      DIM       6
DIM9      DIM       9
DIM10     DIM       10
DIM14     DIM       14
DIM15     DIM       15
DIM20     DIM       20
DIM30     DIM       30
DIM100    DIM       100
FORM3     FORM      3
.
ARRCODE   DIM       3[99]
CNTCODE   FORM      2
.
CURRDATE  DIM       8             * current date
CURRCENT  FORM      2             * current century
ERRFLAG   FORM      1
ERRORMSG  DIM       50
ERRORMS1  DIM       50
FNAMEI    DIM       500
LINECNTR  FORM      7
LINECNT1  FORM      7
OUTFNAME  DIM       8
PATHNAME  DIM       150
USERID    DIM       10
VALEFDTE  DIM       8             * Validate from date
VALTDATE  DIM       8             * Validate to date
AUTOEDAT  DATE      8
ENDKEY38  DIM       38
ENDKEY48  DIM       48
CURKEY37  DIM       37
SAVKEY10  DIM       10
SAVKEY30  DIM       30
SAVKEY37  DIM       37
WRKDTE8   DIM       8
WRKDTE10  DIM       10
XDATE10   DIM       10
.
MSCORE36  FORM      3.6
TEMPDATE  DIM       8
ERORFLAG  FORM      1 
ERORDESC  DIM       40
FLDNAME   DIM       40
FLDSIZE   FORM      3
FLDDATA   DIM       100
FLDSTATS  FORM      1
TABLNAME  DIM       8
TOTAMNT   FORM      14.2
.
DINFIELD  DIM       30
DINTLEN   FORM      2
DDECLEN   FORM      2
DNEGALLW  FORM      1 
DTOTLEN   FORM      2    
WORKFLD   DIM       30
WORKFLDI  DIM       30
WORKFLDD  DIM       30 
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
XFLD0006  DIM       30
XFLD0007  DIM       30
XFLD0008  DIM       30
XFLD0009  DIM       30
XFLD0010  DIM       30
XFLD0011  DIM       30
XFLD0012  DIM       30
XFLD0013  DIM       30
XFLD0014  DIM       30
XFLD0015  DIM       30
.
XFLDX100  DIM       100
XFLDY100  DIM       100
XFLDX256  DIM       256
XFLDY256  DIM       256
.
CKEY      INIT      "Key"
CNOTEQAL  INIT      "<>"
CISBLANK  INIT      "Is Blank"
CMBBLANK  INIT      "Must be Blank"
CINVALID  INIT      "Invalid"
CMUST01   INIT      "Must be 0 or 1"
CMUST02   INIT      "Must be 0, 1 or 2"
CMUST03   INIT      "Must be Blank, 0 or 1"
CFLDLENM  INIT      "Field Length >"
CNOTEXST  INIT      "Does not exist in"
CALREXST  INIT      "Record already existed in"
CDIFTOTA  INIT      "Total Amount not equal to amount in nzppfeaf"
CDUPCODE  INIT      "Duplicate Code exist for the same key"
CCATEGRY  INIT      "Catgeory" 
.            
HYPHEN    INIT      "-"
ALPHANUM  INIT      "a~b~c~d~e~f~g~h~i~j~k~l~m~n~o~p~q~r~s~t~u~v~w~x~y~z~A~B~C~D~E~F~G~H~I~J~K~L~M~N~O~P~Q~R~S~T~U~V~W~X~Y~Z~1~2~3~4~5~6~7~8~9~0~ ~"
.
.------------------------------------------------------------------------------
PRGID     INIT      "IBAPMS64"
PRGNAM    INIT      "Upload NZ Private Contract Procedure Fee Records              "
PRGNAM1   INIT      "Upload NZ Private Contract Procedure Fee Records              "
PRGNAM2   INIT      "Upload NZ Private Contract Procedure Revenue Breakdown Records" 
PRGNAM3   INIT      "Upload NZ Private Procedure Invoice Breakdown Records         "
PRGNAM4   INIT      "Upload NZ Private Contract Procedure Matrix Records           "
.------------------------------------------------------------------------------
VERSION   INIT      "V12.01.00"
.------------------------------------------------------------------------------
. Main Module   
.------------------------------------------------------------------------------
ML0000    CALL      INIT0000                * Initailize variables & open files
          CALL      KOPT0000                * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
ML0100    BRANCH    COPTION,ML1000,ML1000,ML1000,ML1000
          GOTO      ML9999
.
ML1000    CALL      KASC0000                * Keyin file
          BRANCH    EXIT,ML9900
.
ML1100    CALL      SCRD0000                * Display screen
.
          CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000                * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      SFLD0000
          BRANCH    EXIT,ML1100
.
          CALL      PRHD0000                * Print header
.
          CALL      CTOT0000                * Clear total vars
.
          CALL      PROC0000                * with Updating file
.
          CALL      PTOT0000                * Print report totals
.
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.------------------------------------------------------------------------------
.                   Initialize Variables & Open Files   
.------------------------------------------------------------------------------
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P56:24,"Opening pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P56:24,"Opening patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P56:24,"Opening patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P64:24,"nzppfeaf";
          OPEN      NZPPFEA1,"nzppfeaf"
.
          DISPLAY   *P64:24,"nzprbraf";
          OPEN      NZPRBRA1,"nzprbraf"
.
          DISPLAY   *P64:24,"nzpibraf";
          OPEN      NZPIBRA1,"nzpibraf"
.
          DISPLAY   *P64:24,"nzpcmtaf";
          OPEN      NZPCMTA1,"nzpcmtaf"
.
          OPEN      CONTROLF,"controlf"
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      CCC,CURRCENT
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
INIT9999  RETURN
+
.------------------------------------------------------------------------------
.                   Choose Upload or Update files
.------------------------------------------------------------------------------
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:3,*EF:
                    *P1:3,*V2LON,"0",*HOFF," = Exit":
                    *P1:4,*V2LON,"1",*HOFF," = Upload NZ Private Contract":
                                 " Procedure Fee": 
                    *P1:5,*V2LON,"2",*HOFF," = Upload NZ Private Contract":
                                 " Procedure Revenue Breakdown": 
                    *P1:6,*V2LON,"3",*HOFF," = Upload NZ Private Contract":
                                 " Procedure Invoice Breakdown":
                    *P1:7,*V2LON,"4",*HOFF," = Upload NZ Private Contract":
                                 " Procedure Matrix"
.
          DISPLAY   *P1:9,"Select Option : ";
.
KOPT1000  KEYIN     *P17:9,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999,KOPT9999,KOPT9999,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.------------------------------------------------------------------------------
.                   Keyin file  
.------------------------------------------------------------------------------
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          IF        COPTION=1
            DISPLAY   *P26:14,*EF,"NZ Private Contract Procedure Fee Load File";
          ENDIF
          IF        COPTION=2
            DISPLAY   *P26:14,*EF,"NZ Private Contract Procedure Revenue":
                                  " Breakdown Load File";
          ENDIF
          IF        COPTION=3
            DISPLAY   *P26:14,*EF,"NZ Private Contract Procedure Invoice":
                                  " Breakdown Load File";
          ENDIF
          IF        COPTION=4
            DISPLAY   *P26:14,*EF,"NZ Private Contract Procedure Matrix":
                                  " Load File";
          ENDIF
.
          KEYIN     *P1:15,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          IF        COPTION=1
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      NZPFETXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=2
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      NZPRBTXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=3
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      NZPIBTXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=4
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      NZPCMTXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          CALL      KOUT0000                * Keyin output file
          BRANCH    EXIT,KASC9000
.
          CALL      EXCUS000
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.------------------------------------------------------------------------------
.                   Screen display
.------------------------------------------------------------------------------
SCRD0000  DISPLAY   *P1:17,*EF:
                    *P1:17," 1. Keyin User id      :":
                    *P1:18," 2. Keyin Path name    :";
SCRD9999  RETURN
+
.------------------------------------------------------------------------------
.                   Keyin user id 
.------------------------------------------------------------------------------
KUSR0000  KEYIN     *P26:17,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.------------------------------------------------------------------------------
.                   Keyin path name
.------------------------------------------------------------------------------
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:18,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.------------------------------------------------------------------------------
.                   Keyin output file
.------------------------------------------------------------------------------
KOUT0000  DISPLAY   *P1:16,"Keyin Output file      :";
.
          KEYIN     *P26:16,*EL,*V2LON,OUTFNAME;
          PACK      OUTFNAME,OUTFNAME,SP70
          MATCH     SP70,OUTFNAME
          GOTO      KOUT9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KOUT9999
.
KOUT9500  MOVE      ONE,EXIT
KOUT9999  RETURN
+
.------------------------------------------------------------------------------
.                   Select Field 
.------------------------------------------------------------------------------
SFLD0000  CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SFLD9999 IF EQUAL
          GOTO      SFLD1000 IF LESS
.
          MOVE      ZERO,EXIT
.
          PERFORM   CCITEM,KUSR0000:             * Keyin user id
                           KPTH0000              * Keyin pathname
          BRANCH    EXIT,SFLD9999
          GOTO      SFLD0000
.
SFLD1000  MOVE      ONE,EXIT
SFLD9999  RETURN
+
.------------------------------------------------------------------------------
.                   Print the selection details     
.------------------------------------------------------------------------------
PRHD0000  CALL      SETHDR00
          CALL      HEAD132
.
          BRANCH    COPTION,PRHD1000,PRHD2000,PRHD3000,PRHD4000
          GOTO      PRHD9999
.
PRHD1000  PRINT     *N,"Upload NZ Private Contract Procedure Fee"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"NZ Private Contract Procedure Fee Load file: ":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line  ",*10,"|Error",*62,"|Hospital Code":
                    *76,"|Contract Type",*90,"|Contract":
                    *99,"|Procedure Code",*114,"|Effective Date":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD2000  PRINT     *N,"Upload NZ Private Contract Procedure Revenue Breakdown"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"NZ Private Contract Procedure Revenue Breakdown Load":
                       " file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line  ",*10,"|Error",*62,"|Hospital Code":
                    *76,"|Contract Type",*90,"|Contract":
                    *99,"|Procedure Code",*114,"|Effect Date":
                    *126,"|Type",*132,"|"
          GOTO      PRHD9000
.
PRHD3000  PRINT     *N,"Upload NZ Private Contract Procedure Invoice Breakdown"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"NZ Private Contract Procedure Invoice Breakdown":
                       " Load file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line  ",*10,"|Error",*62,"|Hospital Code":
                    *76,"|Contract Type",*90,"|Contract":
                    *99,"|Procedure Code",*114,"|Effect Date":
                    *126,"|Type",*132,"|"
          GOTO      PRHD9000
.
PRHD4000  PRINT     *N,"Upload NZ Private Contract Procedure Matrix"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"NZ Private Contract Procedure Matrix Load file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line  ",*10,"|Error",*62,"|Contract Type":
                    *76,"|Contract",*85,"|Record Type":
                    *97,"|Procedure Code":
                    *112,"|Item Type":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD9000  CALL      UND132
          GOTO      PRHD9999
.
PRHD9999  RETURN
.
.------------------------------------------------------------------------------
. Set up Report heading Name
.------------------------------------------------------------------------------
SETHDR00  BRANCH    COPTION,SETHDR10,SETHDR20,SETHDR30,SETHDR40
          GOTO      SETHDR99
.
SETHDR10  MOVE      PRGNAM1,PRGNAM
          GOTO      SETHDR99
.
SETHDR20  MOVE      PRGNAM2,PRGNAM
          GOTO      SETHDR99
.
SETHDR30  MOVE      PRGNAM3,PRGNAM
          GOTO      SETHDR99
.
SETHDR40  MOVE      PRGNAM4,PRGNAM
          GOTO      SETHDR99
.
SETHDR99  RETURN
.
.------------------------------------------------------------------------------
.                   Generate NZP Procedure Fee tables 
.------------------------------------------------------------------------------
PROC0000  MOVE      ZERO,LINECNTR
.
          BRANCH    COPTION,PROC1000,PROC2000,PROC3000,PROC4000
          GOTO      PROC9999
.
PROC1000  CALL      CPFE0000         * Process Contract Procedure Fee
          IF        ERRCOUNT = 0 
            CALL      CPFEI000
          ENDIF     
          GOTO      PROC9999
.
PROC2000  CALL      CPRB0000         * Process Contract Proc Revenue Breakdown 
          IF        ERRCOUNT = 0
            CALL      CPRBI000
          ENDIF
          GOTO      PROC9999
.
PROC3000  CALL      CPIB0000         * Process Contract Proc Invoice Breakdown 
          IF        ERRCOUNT = 0
            CALL      CPIBI000
          ENDIF
          GOTO      PROC9999
.
PROC4000  CALL      CPMT0000         * Process Contract Proc Matrix
          IF        ERRCOUNT = 0
            CALL      CPMTI000
          ENDIF
          GOTO      PROC9999
.
PROC9999  RETURN
+
.------------------------------------------------------------------------------
. Check Contract Procedure Fee data
.------------------------------------------------------------------------------
CPFE0000  OPEN      NZPFETXT,FNAMEI
.
CPFE1000  READ      NZPFETXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLDX100:
                                 XFLDY100,XFLD0012,XFLD0013
          GOTO      CPFE9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Hospital Code 
          GOTO      CPFE2000 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Claim Code
          GOTO      CPFE2000 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * Contract 
          GOTO      CPFE2000 IF NOT EQUAL
.
          PACK      XFLD0004,XFLD0004,SP70
          MATCH     SP70,XFLD0004            * Fund Table
          GOTO      CPFE2000 IF NOT EQUAL
.
          PACK      XFLD0005,XFLD0005,SP70
          MATCH     SP70,XFLD0005            * Contract Procedure Code
          GOTO      CPFE2000 IF NOT EQUAL
.
          GOTO      CPFE1000
.
CPFE2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,CPFE1000
.
          MOVE      XFLD0001,XZPFHOSP        * Hospital Code 
          MOVE      XFLD0002,XZPFCLMC        * Claim Code
          MOVE      XFLD0003,XZPFCNTR        * Contract
          MOVE      XFLD0004,XZPFFTAB        * Fund Table
          MOVE      XFLD0005,XZPFCPRC        * Contract Procedure Code 
          MOVE      XFLD0006,XZPFEFDT        * Effective Date
.
          MOVE      "nzppfeaf",FLDNAME
          PACK      KEY37,XZPFHOSP,XZPFCLMC,XZPFCNTR,XZPFFTAB,XZPFCPRC:
                          XZPFEFDT,SP70
          CALL      RDNZPFE1
          IF        OVRCD = 0
            PACK      ERRORMSG,CALREXST,SP1,FLDNAME 
            CALL      PERR0000
          ENDIF
.
          GOTO      CPFE1000
.
CPFE9000  CLOSE     NZPFETXT
CPFE9999  RETURN
.
.------------------------------------------------------------------------------
. Insert Contract Procedure Fee data
.------------------------------------------------------------------------------
CPFEI000  OPEN      NZPFETXT,FNAMEI
          MOVE      ZERO,LINECNTR   
.
CPFEI100  READ      NZPFETXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLDX100:
                                 XFLDY100,XFLD0012,XFLD0013
          GOTO      CPFEI900 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Hospital Code
          GOTO      CPFEI200 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Claim Code
          GOTO      CPFEI200 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * Contract
          GOTO      CPFEI200 IF NOT EQUAL
.
          PACK      XFLD0004,XFLD0004,SP70
          MATCH     SP70,XFLD0004            * Fund Table 
          GOTO      CPFEI200 IF NOT EQUAL
.
          PACK      XFLD0005,XFLD0005,SP70
          MATCH     SP70,XFLD0005            * Contract Procedure Code
          GOTO      CPFEI200 IF NOT EQUAL
.
          GOTO      CPFEI100
.
CPFEI200  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          MOVE      XFLD0001,XZPFHOSP        * Hospital Code
          MOVE      XFLD0002,XZPFCLMC        * Claim Code
          MOVE      XFLD0003,XZPFCNTR        * Contract
          MOVE      XFLD0004,XZPFFTAB        * Fund Table
          MOVE      XFLD0005,XZPFCPRC        * Contract Procedure Code
          MOVE      XFLD0006,XZPFEFDT        * Effective Date
          MOVE      XFLD0007,XZPFCONT        * Maximum Funder Contribution
.
          MOVE      ZERO,XZPFPPOR
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XZPFPPOR        * Patient Co-Payment
.
          MOVE      ZERO,XZPFHPOR
          SQUEEZE   XFLD0009
          MOVE      XFLD0009,XZPFHPOR        * Total Fixed Cost
.
          MOVE      XFLDX100,XZPFDES1        * Description line 1
          MOVE      XFLDY100,XZPFDES2        * Description line 2
          MOVE      XFLD0012,XZPFRBRK        * Revenue Breakdown
          MOVE      XFLD0013,XZPFIBRK        * Invoice Breakdown
.
          CALL      CLNZPPFE                 * clear fields
          CALL      SETF0000
.
          PACK      KEY37,NZPFHOSP,NZPFCLMC,NZPFCNTR,NZPFFTAB,NZPFCPRC:
                          NZPFEFDT,SP70
.
          MOVE      USERID,NZPFCUID
          CALL      IBACLOCK
          PACK      NZPFCDAT,CCC,CYY,CMM,CDD
          REP       " 0",NZPFCDAT
          MOVE      CTIMEIS,NZPFCTIM
.
          CALL      WRNZPFE1
          ADD       ONE,ADDCOUNT             * Total records added
.
          GOTO      CPFEI100
.
CPFEI900  CLOSE     NZPFETXT
CPFEI999  RETURN
.
.------------------------------------------------------------------------------
. Check Contract Procedure Revenue Breakdown data
.------------------------------------------------------------------------------
CPRB0000  OPEN      NZPRBTXT,FNAMEI
          MOVE      SP70,SAVKEY37
          MOVE      ZERO,TOTAMNT
          CALL      ICODAR00
.
CPRB1000  READ      NZPRBTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008
          GOTO      CPRB9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Hospital Code
          GOTO      CPRB2000 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Claim Code
          GOTO      CPRB2000 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * Contract 
          GOTO      CPRB2000 IF NOT EQUAL
.
          PACK      XFLD0004,XFLD0004,SP70
          MATCH     SP70,XFLD0004            * Fund Table
          GOTO      CPRB2000 IF NOT EQUAL
.
          PACK      XFLD0005,XFLD0005,SP70
          MATCH     SP70,XFLD0005            * Contract Procedure Code 
          GOTO      CPRB2000 IF NOT EQUAL
.
          GOTO      CPRB1000
.
CPRB2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,CPRB1000
.
          MOVE      XFLD0001,XZRBHOSP        * Hospital Code 
          MOVE      XFLD0002,XZRBCLMC        * Claim Code
          MOVE      XFLD0003,XZRBCNTR        * Contract
          MOVE      XFLD0004,XZRBFTAB        * Fund Table
          MOVE      XFLD0005,XZRBCPRC        * Contract Procedure Code 
          MOVE      XFLD0006,XZRBEFDT        * Effective Date
          MOVE      XFLD0007,XZRBBRCD        * Breakdown Code
.
          MOVE      "nzppfeaf",FLDNAME
          PACK      KEY37,XZRBHOSP,XZRBCLMC,XZRBCNTR,XZRBFTAB,XZRBCPRC:
                          XZRBEFDT,SP70
          CALL      RDNZPFE1
          IF        OVRCD = 1 
            PACK      ERRORMSG,CKEY,SP1,CNOTEXST,SP1,FLDNAME
            CALL      PERR0000
            GOTO      CPRB8000
          ELSE
            COMPARE   ONE,NZPFRBRK
            IF        !@EQUAL
              MOVE      "nzppfeaf.nzpfrbrk <> 1",ERRORMSG
              CALL      PERR0000
              GOTO      CPRB8000
            ENDIF  
          ENDIF
.
          MOVE      "nzprbraf",FLDNAME
          PACK      KEY40,XZRBHOSP,XZRBCLMC,XZRBCNTR,XZRBFTAB,XZRBCPRC:
                          XZRBEFDT,XZRBBRCD,SP70
          CALL      RDNZRBR1
          IF        OVRCD = 0
            PACK      ERRORMSG,CALREXST,SP1,FLDNAME
            CALL      PERR0000
            GOTO      CPRB8000
          ENDIF
.
          PACK      CURKEY37,XZRBHOSP,XZRBCLMC,XZRBCNTR,XZRBFTAB,XZRBCPRC:
                             XZRBEFDT,SP70 
.
          MATCH     SP70,SAVKEY37
          IF        !@EQUAL
            MATCH     SAVKEY37,CURKEY37 
            IF        !@EQUAL
              CALL      CHKRBT00   
              MOVE      ZERO,TOTAMNT
              CALL      ICODAR00
            ENDIF
          ENDIF 
.
          MOVE      CURKEY37,SAVKEY37  
.
          MOVE      ZERO,XZRBAMNT
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XZRBAMNT        * Amount
          ADD       XZRBAMNT,TOTAMNT
.
          CALL      ADDCDR00
.
CPRB8000  GOTO      CPRB1000
.
CPRB9000  CLOSE     NZPRBTXT
          MATCH     SP70,SAVKEY37
          IF        !@EQUAL
            CALL      CHKRBT00
          ENDIF 
.
CPRB9999  RETURN
.
.------------------------------------------------------------------------------
. Insert Contract Procedure Revenue Breakdown data
.------------------------------------------------------------------------------
CPRBI000  OPEN      NZPRBTXT,FNAMEI
          MOVE      ZERO,LINECNTR
.
CPRBI100  READ      NZPRBTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008
          GOTO      CPRBI900 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Hospital Code
          GOTO      CPRBI200 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Claim Code
          GOTO      CPRBI200 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * Contract 
          GOTO      CPRBI200 IF NOT EQUAL
.
          PACK      XFLD0004,XFLD0004,SP70
          MATCH     SP70,XFLD0004            * Fund Table
          GOTO      CPRBI200 IF NOT EQUAL
.
          PACK      XFLD0005,XFLD0005,SP70
          MATCH     SP70,XFLD0005            * Contract Procedure Code
          GOTO      CPRBI200 IF NOT EQUAL
.
          GOTO      CPRBI100
.
CPRBI200  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          MOVE      XFLD0001,XZRBHOSP        * Hospital Code
          MOVE      XFLD0002,XZRBCLMC        * Claim Code
          MOVE      XFLD0003,XZRBCNTR        * Contract
          MOVE      XFLD0004,XZRBFTAB        * Fund Table
          MOVE      XFLD0005,XZRBCPRC        * Contract Procedure Code
          MOVE      XFLD0006,XZRBEFDT        * Effective Date
          MOVE      XFLD0007,XZRBBRCD        * Breakdown Code
.
          MOVE      ZERO,XZRBAMNT
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XZRBAMNT        * Amount
.
          CALL      CLNZPRBR                 * clear fields
          CALL      SETF0000
.
          PACK      KEY40,NZRBHOSP,NZRBCLMC,NZRBCNTR,NZRBFTAB,NZRBCPRC:
                          NZRBEFDT,NZRBBRCD,SP70
.
          CALL      WRNZRBR1
          ADD       ONE,ADDCOUNT             * Total records added

          GOTO      CPRBI100
.
CPRBI900  CLOSE     NZPRBTXT
CPRBI999  RETURN
.
.------------------------------------------------------------------------------
. Check the total revenue breakdown is equal to contract fee total
.------------------------------------------------------------------------------
CHKRBT00  MOVE      "nzppfeaf",FLDNAME
          MOVE      SAVKEY37,KEY37
          CALL      RDNZPFE1
          IF        OVRCD = 1
            PACK      ERRORMSG,CKEY,SP1,CNOTEXST,SP1,FLDNAME
            CALL      PECPRB10 
            GOTO      CHKRBT99
          ENDIF
.
          COMPARE   NZPFHPOR,TOTAMNT
          IF        !@EQUAL 
            PACK      ERRORMSG,CDIFTOTA,SP1,NZPFHPOR,SLASH,TOTAMNT
            PACK      ERRORMS1,TOTAMNT,SP1,CNOTEQAL,SP1,NZPFHPOR 
            CALL      PECPRB10
          ENDIF    
.
CHKRBT99  RETURN
.
.------------------------------------------------------------------------------
. Initialize the Code array
.------------------------------------------------------------------------------
ICODAR00  MOVE      ZERO,CNTCODE
          REPEAT
            ADD       ONE,CNTCODE
            MOVE      SP70,ARRCODE[CNTCODE]
          UNTIL       CNTCODE = 99
.
          MOVE      ZERO,CNTCODE
.
ICODAR99  RETURN
.
.------------------------------------------------------------------------------
. Add code to revenue code array  
.------------------------------------------------------------------------------
ADDCDR00  MOVE      ZERO,F2
          WHILE     F2 < CNTCODE
            ADD       ONE,F2
            MATCH     XZRBBRCD,ARRCODE[F2]
            IF        @EQUAL
              PACK      ERRORMSG,CDUPCODE,SP1,HYPHEN,SP1,XZRBBRCD
              CALL      PERR0000
              GOTO      ADDCDE09 
            ENDIF
          DO
.
          ADD       ONE,CNTCODE
          MOVE      XZRBBRCD,ARRCODE[CNTCODE]  
.
ADDCDE09  RETURN
.
.------------------------------------------------------------------------------
. Add code to invoice code array
.------------------------------------------------------------------------------
ADDCDI00  MOVE      ZERO,F2
          WHILE     F2 < CNTCODE
            ADD       ONE,F2
            MATCH     XZIBBRCD,ARRCODE[F2]
            IF        @EQUAL
              PACK      ERRORMSG,CDUPCODE,SP1,HYPHEN,SP1,XZIBBRCD
              CALL      PERR0000
              GOTO      ADDCDI09
            ENDIF
          DO
.
          ADD       ONE,CNTCODE
          MOVE      XZIBBRCD,ARRCODE[CNTCODE]
.
ADDCDI09  RETURN
.
.------------------------------------------------------------------------------
. Check Contract Procedure Invoice Breakdown data
.------------------------------------------------------------------------------
CPIB0000  OPEN      NZPIBTXT,FNAMEI
          MOVE      SP70,SAVKEY37
          MOVE      ZERO,TOTAMNT
          CALL      ICODAR00
.
CPIB1000  READ      NZPIBTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008
          GOTO      CPIB9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Hospital Code
          GOTO      CPIB2000 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Claim Code
          GOTO      CPIB2000 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * Contract
          GOTO      CPIB2000 IF NOT EQUAL
.
          PACK      XFLD0004,XFLD0004,SP70
          MATCH     SP70,XFLD0004            * Fund Table
          GOTO      CPIB2000 IF NOT EQUAL
.
          PACK      XFLD0005,XFLD0005,SP70
          MATCH     SP70,XFLD0005            * Contract Procedure Code
          GOTO      CPIB2000 IF NOT EQUAL
.
          GOTO      CPIB1000
.
CPIB2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,CPIB1000
.
          MOVE      XFLD0001,XZIBHOSP        * Hospital Code
          MOVE      XFLD0002,XZIBCLMC        * Claim Code
          MOVE      XFLD0003,XZIBCNTR        * Contract
          MOVE      XFLD0004,XZIBFTAB        * Fund Table
          MOVE      XFLD0005,XZIBCPRC        * Contract Procedure Code
          MOVE      XFLD0006,XZIBEFDT        * Effective Date
          MOVE      XFLD0007,XZIBBRCD        * Breakdown Code
.
          MOVE      "nzppfeaf",FLDNAME
          PACK      KEY37,XZIBHOSP,XZIBCLMC,XZIBCNTR,XZIBFTAB,XZIBCPRC:
                          XZIBEFDT,SP70
          CALL      RDNZPFE1
          IF        OVRCD = 1
            PACK      ERRORMSG,CKEY,SP1,CNOTEXST,SP1,FLDNAME
            CALL      PERR0000
            GOTO      CPIB8000
          ELSE
            COMPARE   ONE,NZPFIBRK
            IF        !@EQUAL
              MOVE      "nzppfeaf.nzpfibrk <> 1",ERRORMSG
              CALL      PERR0000
              GOTO      CPIB8000
            ENDIF
          ENDIF
.
          MOVE      "nzpibraf",FLDNAME
          PACK      KEY40,XZIBHOSP,XZIBCLMC,XZIBCNTR,XZIBFTAB,XZIBCPRC:
                          XZIBEFDT,XZIBBRCD,SP70
          CALL      RDNZIBR1
          IF        OVRCD = 0
            PACK      ERRORMSG,CALREXST,SP1,FLDNAME
            CALL      PERR0000
            GOTO      CPIB8000
          ENDIF
.
          PACK      CURKEY37,XZIBHOSP,XZIBCLMC,XZIBCNTR,XZIBFTAB,XZIBCPRC:
                             XZIBEFDT,SP70
.
          MATCH     SP70,SAVKEY37
          IF        !@EQUAL
            MATCH     SAVKEY37,CURKEY37
            IF        !@EQUAL
              CALL      CHKIBT00
              MOVE      ZERO,TOTAMNT
              CALL      ICODAR00
            ENDIF
          ENDIF
.
          MOVE      CURKEY37,SAVKEY37
.
          MOVE      ZERO,XZIBAMNT
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XZIBAMNT        * Amount
          ADD       XZIBAMNT,TOTAMNT
.
          CALL      ADDCDI00
.
CPIB8000  GOTO      CPIB1000
.
CPIB9000  CLOSE     NZPIBTXT
          MATCH     SP70,SAVKEY37
          IF        !@EQUAL
            CALL      CHKIBT00
          ENDIF
.
CPIB9999  RETURN
.
.------------------------------------------------------------------------------
. Insert Contract Procedure Invoice Breakdown data
.------------------------------------------------------------------------------
CPIBI000  OPEN      NZPIBTXT,FNAMEI
          MOVE      ZERO,LINECNTR
.
CPIBI100  READ      NZPIBTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008
          GOTO      CPIBI900 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Hospital Code
          GOTO      CPIBI200 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Claim Code
          GOTO      CPIBI200 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * Contract
          GOTO      CPIBI200 IF NOT EQUAL
.
          PACK      XFLD0004,XFLD0004,SP70
          MATCH     SP70,XFLD0004            * Fund Table
          GOTO      CPIBI200 IF NOT EQUAL
.
          PACK      XFLD0005,XFLD0005,SP70
          MATCH     SP70,XFLD0005            * Contract Procedure Code
          GOTO      CPIBI200 IF NOT EQUAL
.
          GOTO      CPIBI100
.
CPIBI200  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          MOVE      XFLD0001,XZIBHOSP        * Hospital Code
          MOVE      XFLD0002,XZIBCLMC        * Claim Code
          MOVE      XFLD0003,XZIBCNTR        * Contract
          MOVE      XFLD0004,XZIBFTAB        * Fund Table
          MOVE      XFLD0005,XZIBCPRC        * Contract Procedure Code
          MOVE      XFLD0006,XZIBEFDT        * Effective Date
          MOVE      XFLD0007,XZIBBRCD        * Breakdown Code
.
          MOVE      ZERO,XZIBAMNT
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XZIBAMNT        * Amount
.
          CALL      CLNZPIBR                 * clear fields
          CALL      SETF0000
.
          PACK      KEY40,NZIBHOSP,NZIBCLMC,NZIBCNTR,NZIBFTAB,NZIBCPRC:
                          NZIBEFDT,NZIBBRCD,SP70
.
          CALL      WRNZIBR1
          ADD       ONE,ADDCOUNT             * Total records added

          GOTO      CPIBI100
.
CPIBI900  CLOSE     NZPIBTXT
CPIBI999  RETURN
.
.------------------------------------------------------------------------------
. Check the total invoice breakdown is equal to contract fee total
.------------------------------------------------------------------------------
CHKIBT00  MOVE      "nzppfeaf",FLDNAME
          MOVE      SAVKEY37,KEY37
          CALL      RDNZPFE1
          IF        OVRCD = 1
            PACK      ERRORMSG,CKEY,SP1,CNOTEXST,SP1,FLDNAME
            CALL      PECPIB10
            GOTO      CHKIBT99
          ENDIF
.
          COMPARE   NZPFHPOR,TOTAMNT
          IF        !@EQUAL
            PACK      ERRORMSG,CDIFTOTA,SP1,NZPFHPOR,SLASH,TOTAMNT
            PACK      ERRORMS1,TOTAMNT,SP1,CNOTEQAL,SP1,NZPFHPOR
            CALL      PECPIB10
          ENDIF
.
CHKIBT99  RETURN
.
.------------------------------------------------------------------------------
. Check Contract Procedure Matrix data
.------------------------------------------------------------------------------
CPMT0000  OPEN      NZPCMTXT,FNAMEI
.
CPMT1000  READ      NZPCMTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008
          GOTO      CPMT9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Claim Code
          GOTO      CPMT2000 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Contract 
          GOTO      CPMT2000 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * Record Type 
          GOTO      CPMT2000 IF NOT EQUAL
.
          PACK      XFLD0004,XFLD0004,SP70
          MATCH     SP70,XFLD0004            * Contract Procedure Code
          GOTO      CPMT2000 IF NOT EQUAL
.
          PACK      XFLD0005,XFLD0005,SP70
          MATCH     SP70,XFLD0005            * Item Type
          GOTO      CPMT2000 IF NOT EQUAL
.
          GOTO      CPMT1000
.
CPMT2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,CPMT1000
.
          MOVE      XFLD0001,XZCMCLMC        * Claim Code
          MOVE      XFLD0002,XZCMCNTR        * Contract
          MOVE      XFLD0003,XZCMRTYP        * Record Type
          MOVE      XFLD0004,XZCMCPRC        * Contract Procedure Code 
          MOVE      XFLD0005,XZCMITYP        * Item Type 
.
.         Check if header record already exists
.
          MOVE      "nzpcmtaf",FLDNAME
          MOVE      ZERO,NZCMCCNT
          PACK      KEY26,XZCMCLMC,XZCMCNTR,XZCMRTYP,XZCMCPRC,XZCMITYP:
                          NZCMCCNT,SP70
          CALL      RDNZCMT1
          BRANCH    OVRCD,CPMT1000 
.
          PACK      ERRORMSG,CALREXST,SP1,FLDNAME
          CALL      PERR0000
.
          GOTO      CPMT1000
.
CPMT9000  CLOSE     NZPCMTXT
CPMT9999  RETURN
.
.------------------------------------------------------------------------------
. Insert Contract Procedure Matrix data
.------------------------------------------------------------------------------
CPMTI000  OPEN      NZPCMTXT,FNAMEI
          MOVE      ZERO,LINECNTR   
.
CPMTI100  READ      NZPCMTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008
          GOTO      CPMTI900 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Claim Code
          GOTO      CPMTI200 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Contract
          GOTO      CPMTI200 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * Record Type 
          GOTO      CPMTI200 IF NOT EQUAL
.
          PACK      XFLD0004,XFLD0004,SP70
          MATCH     SP70,XFLD0004            * Contract Procedure Code
          GOTO      CPMTI200 IF NOT EQUAL
.
          PACK      XFLD0005,XFLD0005,SP70
          MATCH     SP70,XFLD0005            * Item Type
          GOTO      CPMTI200 IF NOT EQUAL
.
          GOTO      CPMTI100
.
CPMTI200  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          UNPACK    SP70,XZCMCLMC,XZCMCNTR,XZCMRTYP,XZCMCPRC,XZCMITYP:
                         XZCMPROC,XZCMMIGP
          MOVE      ZERO,XZCMFTYP
.
          MOVE      XFLD0001,XZCMCLMC        * Claim Code
          MOVE      XFLD0002,XZCMCNTR        * Contract
          MOVE      XFLD0003,XZCMRTYP        * Record Type
          MOVE      XFLD0004,XZCMCPRC        * Contract Procedure Code
.
          CALL      CLNZPCMT                 * clear fields
          CALL      SETF0000
          MOVE      ONE,NZCMFTYP             * Field Type
.
.         Checking header records for both Procedure and Misc.
.
          MOVE      "0",NZCMITYP               * Procedure
          MOVE      ZERO,NZCMCCNT
          PACK      KEY26,NZCMCLMC,NZCMCNTR,NZCMRTYP,NZCMCPRC,NZCMITYP:
                          NZCMCCNT,SP70
          CALL      RANZCMT1
          IF        OVRCD=1
            CALL      WRNZCMT1
          ENDIF
.
          MOVE      "1",NZCMITYP               * Misc.
          PACK      KEY26,NZCMCLMC,NZCMCNTR,NZCMRTYP,NZCMCPRC,NZCMITYP:
                          NZCMCCNT,SP70
          CALL      RANZCMT1
          IF        OVRCD=1
            CALL      WRNZCMT1
          ENDIF
.
.
.         Write record from the Upload file
.
          MOVE      XFLD0005,XZCMITYP        * Item Type
          MOVE      XFLD0006,XZCMFTYP        * Field Type for Item Type
          MOVE      XFLD0007,XZCMPROC        * Procedure or Misc Item Code
          MOVE      XFLD0008,XZCMMIGP        * Misc Item Group
.
          MOVE      ZERO,F6
          PACK      KEY26,XZCMCLMC,XZCMCNTR,XZCMRTYP,XZCMCPRC,XZCMITYP,Z70
          MOVE      KEY26,KEY20
          CALL      RSNZCMT1
          CALL      RPNZCMT1
          IF        OVRCD = 1
            MOVE      ONE,F6 
            GOTO      CPMTI800
          ENDIF 
.
          PACK      DIM20,NZCMCLMC,NZCMCNTR,NZCMRTYP,NZCMCPRC,NZCMITYP,SP70
          MATCH     KEY20,DIM20
          IF        !@EQUAL
            MOVE      ONE,F6 
            GOTO      CPMTI800
          ENDIF
.
          MOVE      NZCMCCNT,F6
          ADD       ONE,F6    
.
CPMTI800  CALL      CLNZPCMT                 * clear fields
          CALL      SETF0000
          MOVE      F6,NZCMCCNT
          PACK      KEY26,NZCMCLMC,NZCMCNTR,NZCMRTYP,NZCMCPRC,NZCMITYP:
                          NZCMCCNT,SP70
          CALL      WRNZCMT1
          ADD       ONE,ADDCOUNT           * Total records added
.
          GOTO      CPMTI100
.
CPMTI900  CLOSE     NZPCMTXT
CPMTI999  RETURN
.
.------------------------------------------------------------------------------
. Set fields    
.------------------------------------------------------------------------------
SETF0000  BRANCH   COPTION,SETF1000,SETF2000,SETF30000,SETF4000
          GOTO     SETF9999
.
SETF1000  CALL     SCPFE000
          GOTO     SETF9999
.
SETF2000  CALL     SCPRB000
          GOTO     SETF9999
.
SETF3000  CALL     SCPIB000
          GOTO     SETF9999
.
SETF4000  CALL     SCPMT000
          GOTO     SETF9999
.
SETF9999  RETURN
.
.------------------------------------------------------------------------------
. Set fields - Contract Procedure Fee 
.------------------------------------------------------------------------------
SCPFE000  MOVE     XZPFHOSP,NZPFHOSP   * Hospital Code 
          MOVE     XZPFCLMC,NZPFCLMC   * Claim Code 
          MOVE     XZPFCNTR,NZPFCNTR   * Contract
          MOVE     XZPFFTAB,NZPFFTAB   * Fund Table
          MOVE     XZPFCPRC,NZPFCPRC   * Contract Procedure Code
          MOVE     XZPFEFDT,NZPFEFDT   * Effective Date (CCYYMMDD)
          MOVE     XZPFCONT,NZPFCONT   * Maximum Funder Contribution
          MOVE     XZPFPPOR,NZPFPPOR   * Patient Co-Payment
          MOVE     XZPFHPOR,NZPFHPOR   * Total Fixed Cost
          MOVE     XZPFDES1,NZPFDES1   * Description line 1 
          MOVE     XZPFDES2,NZPFDES2   * Description line 2 
          MOVE     XZPFRBRK,NZPFRBRK   * Revenue Breakdown
          MOVE     XZPFIBRK,NZPFIBRK   * Invoice Breakdown
.
SCPFE999  RETURN
.
.------------------------------------------------------------------------------
. Set fields - Contract Procedure Revenue Breakdown
.------------------------------------------------------------------------------
SCPRB000  MOVE     XZRBHOSP,NZRBHOSP   * Hospital Code
          MOVE     XZRBCLMC,NZRBCLMC   * Claim Code 
          MOVE     XZRBCNTR,NZRBCNTR   * Contract 
          MOVE     XZRBFTAB,NZRBFTAB   * Fund Table
          MOVE     XZRBCPRC,NZRBCPRC   * Contract Procedure Code
          MOVE     XZRBEFDT,NZRBEFDT   * Effective Date (CCYYMMDD)
          MOVE     XZRBBRCD,NZRBBRCD   * Breakdown Code
          MOVE     XZRBAMNT,NZRBAMNT   * Amount
.
SCPRB999  RETURN
.
.------------------------------------------------------------------------------
. Set fields - Contract Procedure Invoice Breakdown File 
.------------------------------------------------------------------------------
SCPIB000  MOVE     XZIBHOSP,NZIBHOSP   * Hospital Code
          MOVE     XZIBCLMC,NZIBCLMC   * Claim Code
          MOVE     XZIBCNTR,NZIBCNTR   * Contract
          MOVE     XZIBFTAB,NZIBFTAB   * Fund Table
          MOVE     XZIBCPRC,NZIBCPRC   * Contract Procedure Code
          MOVE     XZIBEFDT,NZIBEFDT   * Effective Date (CCYYMMDD)
          MOVE     XZIBBRCD,NZIBBRCD   * Breakdown Code
          MOVE     XZIBAMNT,NZIBAMNT   * Amount
.
SCPIB999  RETURN
.
.------------------------------------------------------------------------------
. Set fields - Contract Procedure Matrix
.------------------------------------------------------------------------------
SCPMT000  MOVE     XZCMCLMC,NZCMCLMC   * Claim Code
          MOVE     XZCMCNTR,NZCMCNTR   * Contract
          MOVE     XZCMRTYP,NZCMRTYP   * Record Type  
          MOVE     XZCMCPRC,NZCMCPRC   * Contract Procedure Code
          MOVE     XZCMITYP,NZCMITYP   * Item Type
          MOVE     XZCMFTYP,NZCMFTYP   * Field Type
          MOVE     XZCMPROC,NZCMPROC   * Procedure or Misc Item Code
          MOVE     XZCMMIGP,NZCMMIGP   * Misc Item Group
.
SCPMT999  RETURN
.
.------------------------------------------------------------------------------
.         Execute us1 command before processing
.------------------------------------------------------------------------------
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibapms64.us1 ",CMDLINE
          SQUEEZE   FNAMEI
          APPEND    FNAMEI,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    OUTFNAME,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
EXCUS999  RETURN
+
.------------------------------------------------------------------------------
. Validate input text file fields
.------------------------------------------------------------------------------
VALD0000  MOVE      ZERO,EXIT
          MOVE      ZERO,ERRFLAG
.
          BRANCH    COPTION,VALD1000,VALD2000,VALD3000,VALD4000
          GOTO      VALD9999
.
VALD1000  CALL      VCPFE000        * Val Contract Procedure Fee fields
          GOTO      VALD9999
.
VALD2000  CALL      VCPRB000        * Val Contract Procedure Revenue Breakdown
          GOTO      VALD9999
.
VALD3000  CALL      VCPIB000        * Val Contract Procedure Invoice Breakdown 
          GOTO      VALD9999
.
VALD4000  CALL      VCPMT000        * Val Contract Procedure Matrix 
          GOTO      VALD9999
.
VALD9999  RETURN
+
.------------------------------------------------------------------------------
. Validate Contract Procedure Fee fields
.------------------------------------------------------------------------------
VCPFE000  MOVE      XFLD0001,XZPFHOSP        * Hospital Code 
          MOVE      XFLD0002,XZPFCLMC        * Claim Code
          MOVE      XFLD0003,XZPFCNTR        * Contract
          MOVE      XFLD0004,XZPFFTAB        * Fund Table
          MOVE      XFLD0005,XZPFCPRC        * Contract Procedure Code 
          MOVE      XFLD0006,XZPFEFDT        * Effective Date
.
          MOVE      XZPFEFDT,WRKDTE8
          CALL      CNVDTE00
          MOVE      WRKDTE10,XDATE10 
.
VCPFE100  MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "Hospital Code is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPFE200
          ENDIF
.    
          MOVE      XFLD0001,FLDDATA
          MOVE      "Hospital Code",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPFE200
          ENDIF
. 
          MOVE      "Hospital Code",FLDNAME
          MOVE      XFLD0001,DIM3
          CALL      VHOSP000  
.
VCPFE200  MATCH     SP70,XFLD0002
          IF        @EQUAL
            MOVE      "Contract Type is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPFE250
          ENDIF
.
          MOVE      XFLD0002,FLDDATA
          MOVE      "Contract Type",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPFE250
          ENDIF
.
          MOVE      "Contract Type",FLDNAME
          MOVE      XFLD0002,DIM3
          CALL      VCLAM000
.
VCPFE250  MATCH     SP70,XFLD0003
          IF        @EQUAL
            GOTO      VCPFE260
          ENDIF
.
          MOVE      XFLD0003,FLDDATA
          MOVE      "Contract",FLDNAME
          MOVE      6,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPFE260
          ENDIF
.
          MOVE      "Contract",FLDNAME
          MOVE      XFLD0003,DIM6
          CALL      VCONT000
.
VCPFE260  MATCH     SP70,XFLD0004
          IF        @EQUAL
            GOTO      VCPFE300
          ENDIF
.
          MOVE      XFLD0004,FLDDATA
          MOVE      "Table",FLDNAME
          MOVE      8,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPFE300
          ENDIF
.
VCPFE300  MATCH     SP70,XFLD0005
          IF        @EQUAL
            MOVE      "Contract Procedure Code is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPFE350
          ENDIF
.
          MOVE      XFLD0005,FLDDATA
          MOVE      "Contract Procedure Code",FLDNAME
          MOVE      9,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPFE350
          ENDIF
.
          MOVE      "Contract Procedure Code",FLDNAME
          MOVE      XFLD0005,DIM9
          CALL      VPROC000
.
VCPFE350  MOVE      XFLD0006,DINFIELD
          MOVE      "Effective Date",FLDNAME
          CALL      CEFFFR00
.
VCPFE400  MATCH     SP70,XFLD0007
          IF        @EQUAL
            MOVE      "Funder Contribution must be 0 or 1",ERRORMSG
            CALL      PERR0000
          ELSE
            MOVE      XFLD0007,DIM1
            REP       "10",DIM1
            MATCH     "0",DIM1
            IF        !@EQUAL
              MOVE      "Funder Contribution must be 0 or 1",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     "0",XFLD0007
          IF        @EQUAL
            MATCH     SP70,XFLD0009
            IF        @EQUAL
              MOVE      "Total Fixed Cost Cannot be blank",ERRORMSG
              MOVE      "When Funder Contribution is 0",ERRORMS1
              CALL      PERR0000
            ENDIF
          ELSE
            MATCH     "1",XFLD0007
            IF        @EQUAL
              MATCH     SP70,XFLD0009
              IF        !@EQUAL
                MOVE      "Total Fixed Cost Must be blank",ERRORMSG
                MOVE      "When Funder Contribution is 1",ERRORMS1
                CALL      PERR0000
              ENDIF
            ENDIF
          ENDIF
.
VCPFE500  MATCH     SP70,XFLD0008
          IF        !@EQUAL
            MOVE      XFLD0008,DINFIELD
            MOVE      "Patient Co-Payment",FLDNAME
            CALL      C14D2N00
          ENDIF 
.
VCPFE550  MATCH     SP70,XFLD0009
          IF        !@EQUAL
            MOVE      XFLD0009,DINFIELD
            MOVE      "Total Fixed Cost",FLDNAME
            CALL      C14D2N00
          ENDIF 
.
VCPFE700  MATCH     SP70,XFLDX100
          GOTO      VCPFE750 IF EQUAL
.
          MOVE      XFLDX100,FLDDATA
          MOVE      "Description Line 1",FLDNAME
          MOVE      50,FLDSIZE
          CALL      FLDSIZ00
.
VCPFE750  MATCH     SP70,XFLDY100
          GOTO      VCPFE800 IF EQUAL
.
          MOVE      XFLDY100,FLDDATA
          MOVE      "Description Line 2",FLDNAME
          MOVE      50,FLDSIZE
          CALL      FLDSIZ00
.
VCPFE800  MATCH     SP70,XFLD0012
          IF        @EQUAL
            MOVE      "Revenue Breakdown must be 0, 1 or 2",ERRORMSG
            CALL      PERR0000
          ELSE
            MOVE      XFLD0012,DIM1
            REP       "1020",DIM1
            MATCH     "0",DIM1
            IF        !@EQUAL
              MOVE      "Revenue Breakdown must be 0, 1 or 2",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
VCPFE850  MATCH     SP70,XFLD0013
          IF        @EQUAL
            MOVE      "Invoice Breakdown must be 0 or 1",ERRORMSG
            CALL      PERR0000
          ELSE
            MOVE      XFLD0013,DIM1
            REP       "10",DIM1
            MATCH     "0",DIM1
            IF        !@EQUAL
              MOVE      "Invoice Breakdown must be 0 or 1",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
VCPF999  RETURN
.
.--------------------------------------------------------------------
. Convert date 8 (ccyymmdd) to date 10 (dd/mm/ccyy) (PATSHT6)
.--------------------------------------------------------------------
CNVDTE00  MOVE      SP70,WRKDTE10
          MATCH     SP70,WRKDTE8
          GOTO      CNVDTE99 IF EQUAL
.
          UNPACK    WRKDTE8,CCENT,CYEAR,CMON,CDAY
          PACK      WRKDTE10,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,SP70
.
CNVDTE99  RETURN
.
.------------------------------------------------------------------------------
. Validate an effective date field
.------------------------------------------------------------------------------
CEFFFR00  MATCH     SP70,DINFIELD
          IF        @EQUAL | @EOS
            PACK      ERRORMSG,FLDNAME,SP1,CISBLANK
            CALL      PERR0000
            GOTO      CEFFFR99 
          ENDIF
.
          PACK      TEMPDATE,DINFIELD,SP70
          CALL      VDAT0000
.
CEFFFR99  RETURN
.
.------------------------------------------------------------------------------
. Validate a numeric field size 14.2 
.------------------------------------------------------------------------------
C14D2N00  STRIP     DINFIELD
          TYPE      DINFIELD
          IF        !@EQUAL
            PACK      ERRORMSG,CINVALID,SP1,FLDNAME
            CALL      PERR0000
            GOTO      C14D2N09
          ENDIF
.
C14D2N02  MOVE      14,DINTLEN
          MOVE      2,DDECLEN
          MOVE      0,DNEGALLW
.
          CALL      NUMDC000
.
C14D2N09  RETURN
.
.------------------------------------------------------------------------------
. Validate a numeric with decimal field
.  
. Requires:  DINFIELD - Number/Decimal field 
.            FLDNAME  - Field Name
.            DINTLEN  - Integer length 
.            DDECLEN  - Decimal length
.            DNEGALLW - 1 (Yes), 0 (No) 
. Returns:   ERORFLAG - 1 if an error in date validation occurs.
.            ERORDESC - Description of error when an error is occurred
.------------------------------------------------------------------------------
NUMDC000  MOVE      SP70,ERORFLAG
          STRIP     DINFIELD 
          MOVE      DINFIELD,WORKFLD
          MATCH     SP70,WORKFLD               * blank number ?
          IF        @EQUAL | @EOS
            GOTO      NUMDC999
          ENDIF
.
          ASSIGN    DINTLEN+DDECLEN+1,DTOTLEN 
          MOVELPTR  WORKFLD,FORM2
          IF        FORM2 > DTOTLEN
            MOVE      "Total Length > Max Length Allowed",ERORDESC
            CALL      NERR0000
            GOTO      NUMDC999
          ENDIF
.
NUMDC200  SCAN      ".",WORKFLD
          IF        @EQUAL
            BUMP      WORKFLD
            MOVEFPTR  WORKFLD,FORM2
            MOVE      WORKFLD,WORKFLDD
            SUB       TWO,FORM2    
            MOVE      DINFIELD,WORKFLD
            SETLPTR   WORKFLD,FORM2
            MOVE      WORKFLD,WORKFLDI
          ELSE
            MOVE      WORKFLD,WORKFLDI
            MOVE      SP70,WORKFLDD
          ENDIF
.
          SQUEEZE   WORKFLDI
          MOVELPTR  WORKFLDI,FORM2
          IF        FORM2 > DINTLEN
            MOVE      "Integer Length",ERORDESC
            CALL      NERR0000
            GOTO      NUMDC999
          ENDIF
.
          SQUEEZE   WORKFLDD
          MOVELPTR  WORKFLDD,FORM2
          IF        FORM2 > DDECLEN
            MOVE      "Decimal Length",ERORDESC
            CALL      NERR0000
            GOTO      NUMDC999
          ENDIF
.
          COMPARE   ZERO,DNEGALLW
          IF        @EQUAL
            SCAN      HYPHEN,WORKFLD
            IF        @EQUAL
              MOVE      "Negative SIGN Not Allowed",ERORDESC
              CALL      NERR0000
              GOTO      NUMDC999
            ENDIF
          ENDIF
.
NUMDC999  RETURN
.
.------------------------------------------------------------------------------
. Write error message for a numeric field  
.------------------------------------------------------------------------------
NERR0000  MOVE      ONE,ERORFLAG
          STRIP     FLDNAME
          STRIP     ERORDESC
.
          PACK      DIM100,CINVALID,SP1,FLDNAME,SP1,HYPHEN,SP1,ERORDESC,SP70
          STRIP     DIM100
          MOVELPTR  DIM100,FORM3
          IF        FORM3 > 50
            PACK      ERRORMSG,CINVALID,SP1,FLDNAME
            PACK      ERRORMS1,HYPHEN,SP1,ERORDESC
          ELSE
            MOVE      DIM100,ERRORMSG
            MOVE      SP70,ERRORMS1
          ENDIF
.
          CALL      PERR0000
.
NERR9999  RETURN
.
.------------------------------------------------------------------------------
. Validate hospital code
.------------------------------------------------------------------------------
VHOSP000  MOVE      "pathspaf",TABLNAME
          MOVE      DIM3,KEY3
          CALL      RDPTHSP1
          IF        OVRCD=1
            PACK      ERRORMSG,FLDNAME,SP1,CNOTEXST,SP1,TABLNAME
            CALL      PERR0000
          ENDIF
.
VHOSP999  RETURN
.
.------------------------------------------------------------------------------
. Validate claim code
.------------------------------------------------------------------------------
VCLAM000  MOVE      "CL",DIM2
          PACK      KEY5,DIM2,DIM3,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            PACK      ERRORMSG,FLDNAME,SP1,CNOTEXST,SP1,CCATEGRY,SP1,DIM2
            CALL      PERR0000
          ENDIF
.
VCLAM999  RETURN
.
.------------------------------------------------------------------------------
. Validate misc group
.------------------------------------------------------------------------------
VMGRP000  MOVE      "FI",DIM2
          PACK      KEY5,DIM2,DIM3,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            PACK      ERRORMSG,FLDNAME,SP1,CNOTEXST,SP1,CCATEGRY,SP1,DIM2
            CALL      PERR0000
          ENDIF
.
VMGRP999  RETURN
.
.------------------------------------------------------------------------------
. Validate contract code
.------------------------------------------------------------------------------
VCONT000  MOVE      "patfn1af",TABLNAME
          PACK      KEY14,DIM6,SP70
          CALL      RDSFUND1
          CALL      RDKFUND1
          BRANCH    OVRCD,VCONT800
.
          MATCH     DIM6,HCODE
          GOTO      VCONT800 IF NOT EQUAL
.
          GOTO      VCONT999
. 
VCONT800  PACK      ERRORMSG,FLDNAME,SP1,CNOTEXST,SP1,TABLNAME
          CALL      PERR0000
.
VCONT999  RETURN
.
.------------------------------------------------------------------------------
. Validate procedure code
.------------------------------------------------------------------------------
VPROC000  MOVE      "patcmcaf",TABLNAME
          PACK      KEY9,DIM9,SP70
          CALL      RDPTCMC1
          IF        OVRCD=1
            PACK      ERRORMSG,FLDNAME,SP1,CNOTEXST,SP1,TABLNAME
            CALL      PERR0000
          ENDIF
.
VPROC999  RETURN
.
.------------------------------------------------------------------------------
. Validate maximum field length
.------------------------------------------------------------------------------
FLDSIZ00  MOVE      ZERO,FLDSTATS
          STRIP     FLDDATA
          MOVELPTR  FLDDATA,F2
          COMPARE   F2,FLDSIZE
          IF        @LESS
            PACK      ERRORMSG,FLDNAME,SP1,CFLDLENM,SP1,FLDSIZE,SP70
            CALL      PERR0000
            MOVE      ONE,FLDSTATS
          ENDIF
.
FLDSIZ99  RETURN
.
.------------------------------------------------------------------------------
. Validate Contract Procedure Revenue Breakdown fields
.------------------------------------------------------------------------------
VCPRB000  MOVE      XFLD0001,XZRBHOSP        * Hospital Code
          MOVE      XFLD0002,XZRBCLMC        * Claim Code
          MOVE      XFLD0003,XZRBCNTR        * Contract
          MOVE      XFLD0004,XZRBFTAB        * Fund Table
          MOVE      XFLD0005,XZRBCPRC        * Contract Procedure Code
          MOVE      XFLD0006,XZRBEFDT        * Effective Date
          MOVE      XFLD0007,XZRBBRCD        * Breakdown Code 
.
          MOVE      XZRBEFDT,WRKDTE8
          CALL      CNVDTE00
          MOVE      WRKDTE10,XDATE10
.
VCPRB100  MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "Hospital Code is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPRB200
          ENDIF
.
          MOVE      XFLD0001,FLDDATA
          MOVE      "Hospital Code",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPRB200
          ENDIF
.
          MOVE      "Hospital Code",FLDNAME
          MOVE      XFLD0001,DIM3
          CALL      VHOSP000
.
VCPRB200  MATCH     SP70,XFLD0002
          IF        @EQUAL
            MOVE      "Contract Type is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPRB300
          ENDIF
.
          MOVE      XFLD0002,FLDDATA
          MOVE      "Contract Type",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPRB300
          ENDIF
.
          MOVE      "Contract Type",FLDNAME
          MOVE      XFLD0002,DIM3
          CALL      VCLAM000
.
VCPRB300  MATCH     SP70,XFLD0003
          IF        @EQUAL
            GOTO      VCPRB400
          ENDIF
.
          MOVE      XFLD0003,FLDDATA
          MOVE      "Contract",FLDNAME
          MOVE      6,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPRB400
          ENDIF
.
          MOVE      "Contract",FLDNAME
          MOVE      XFLD0003,DIM6
          CALL      VCONT000
.
VCPRB400  MATCH     SP70,XFLD0004
          IF        @EQUAL
            GOTO      VCPRB500
          ENDIF
.
          MOVE      XFLD0004,FLDDATA
          MOVE      "Table",FLDNAME
          MOVE      8,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPRB500
          ENDIF

.
VCPRB500  MATCH     SP70,XFLD0005
          IF        @EQUAL
            MOVE      "Contract Procedure Code is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPRB600
          ENDIF
.
          MOVE      XFLD0005,FLDDATA
          MOVE      "Contract Procedure Code",FLDNAME
          MOVE      9,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPRB600
          ENDIF
.
          MOVE      "Contract Procedure Code",FLDNAME
          MOVE      XFLD0005,DIM9
          CALL      VPROC000
.
VCPRB600  MOVE      XFLD0006,DINFIELD
          MOVE      "Effective Date",FLDNAME
          CALL      CEFFFR00
.
VCPRB700  MATCH     SP70,XFLD0007
          IF        @EQUAL
            GOTO      VCPRB750
          ENDIF
.
          MOVE      XFLD0007,FLDDATA
          MOVE      "Revenue Breakbown Type",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPRB750
          ENDIF
.
          MOVE      "Revenue Breakbown Type",FLDNAME
          MOVE      XFLD0007,DIM3
          CALL      VMGRP000
.
VCPRB750  MATCH     SP70,XFLD0008
          IF        @EQUAL
            GOTO      VCPRB999
          ENDIF
.  
          MOVE      XFLD0008,DINFIELD
          MOVE      "Revenue Breakbown Amount",FLDNAME
          CALL      C14D2N00
.
VCPRB999  RETURN
+
.------------------------------------------------------------------------------
. Validate Contract Procedure Invoice Breakdown fields
.------------------------------------------------------------------------------
VCPIB000  MOVE      XFLD0001,XZIBHOSP        * Hospital Code
          MOVE      XFLD0002,XZIBCLMC        * Claim Code
          MOVE      XFLD0003,XZIBCNTR        * Contract
          MOVE      XFLD0004,XZIBFTAB        * Fund Table
          MOVE      XFLD0005,XZIBCPRC        * Contract Procedure Code
          MOVE      XFLD0006,XZIBEFDT        * Effective Date
          MOVE      XFLD0007,XZIBBRCD        * Breakdown Code
.
          MOVE      XZIBEFDT,WRKDTE8
          CALL      CNVDTE00
          MOVE      WRKDTE10,XDATE10
.
VCPIB100  MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "Hospital Code is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPIB200
          ENDIF
.
          MOVE      XFLD0001,FLDDATA
          MOVE      "Hospital Code",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPIB200
          ENDIF
.
          MOVE      "Hospital Code",FLDNAME
          MOVE      XFLD0001,DIM3
          CALL      VHOSP000
.
VCPIB200  MATCH     SP70,XFLD0002
          IF        @EQUAL
            MOVE      "Contract Type is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPIB300
          ENDIF
.
          MOVE      XFLD0002,FLDDATA
          MOVE      "Contract Type",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPIB300
          ENDIF
.
          MOVE      "Contract Type",FLDNAME
          MOVE      XFLD0002,DIM3
          CALL      VCLAM000
.
VCPIB300  MATCH     SP70,XFLD0003
          IF        @EQUAL
            GOTO      VCPIB400
          ENDIF
.
          MOVE      XFLD0003,FLDDATA
          MOVE      "Contract",FLDNAME
          MOVE      6,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPIB400
          ENDIF
.
          MOVE      "Contract",FLDNAME
          MOVE      XFLD0003,DIM6
          CALL      VCONT000
.
VCPIB400  MATCH     SP70,XFLD0004
          IF        @EQUAL
            GOTO      VCPIB500
          ENDIF
.
          MOVE      XFLD0004,FLDDATA
          MOVE      "Table",FLDNAME
          MOVE      8,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPIB500
          ENDIF
.
VCPIB500  MATCH     SP70,XFLD0005
          IF        @EQUAL
            MOVE      "Contract Procedure Code is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPIB600
          ENDIF
.
          MOVE      XFLD0005,FLDDATA
          MOVE      "Contract Procedure Code",FLDNAME
          MOVE      9,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPIB600
          ENDIF
.
          MOVE      "Contract Procedure Code",FLDNAME
          MOVE      XFLD0005,DIM9
          CALL      VPROC000
.
VCPIB600  MOVE      XFLD0006,DINFIELD
          MOVE      "Effective Date",FLDNAME
          CALL      CEFFFR00
.
VCPIB700  MATCH     SP70,XFLD0007
          IF        @EQUAL
            GOTO      VCPIB750
          ENDIF
.
          MOVE      XFLD0007,FLDDATA
          MOVE      "Revenue Breakbown Type",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPIB750
          ENDIF
.
          MOVE      "Revenue Breakbown Type",FLDNAME
          MOVE      XFLD0007,DIM3
          CALL      VMGRP000
.
VCPIB750  MATCH     SP70,XFLD0008
          IF        @EQUAL
            GOTO      VCPIB999
          ENDIF
.
          MOVE      XFLD0008,DINFIELD
          MOVE      "Revenue Breakbown Amount",FLDNAME
          CALL      C14D2N00
.
VCPIB999  RETURN
+
.------------------------------------------------------------------------------
. Validate Contract Procedure Matrix fields
.------------------------------------------------------------------------------
VCPMT000  MOVE      XFLD0001,XZCMCLMC        * Contract Type/Claim Code
          MOVE      XFLD0002,XZCMCNTR        * Contract 
          MOVE      XFLD0003,XZCMRTYP        * Record Type
          MOVE      XFLD0004,XZCMCPRC        * Contract Procedure Code 
          MOVE      XFLD0005,XZCMITYP        * Item Type
.
VCPMT100  MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "Contract Type is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPMT200
          ENDIF
.
          MOVE      XFLD0001,FLDDATA
          MOVE      "Contract Type",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPMT200
          ENDIF
.
          MOVE      "Contract Type",FLDNAME
          MOVE      XFLD0001,DIM3
          CALL      VCLAM000
.
VCPMT200  MATCH     SP70,XFLD0002
          IF        @EQUAL
            GOTO      VCPMT300
          ENDIF
.
          MOVE      XFLD0002,FLDDATA
          MOVE      "Contract",FLDNAME
          MOVE      6,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPMT300
          ENDIF
.
          MOVE      "Contract",FLDNAME
          MOVE      XFLD0002,DIM6
          CALL      VCONT000
.
VCPMT300  MATCH     SP70,XFLD0003
          IF        @EQUAL
            MOVE      "Record Type is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPMT400 
          ENDIF
.
          MATCH     "E",XFLD0003
          GOTO      VCPMT400 IF EQUAL
.
          MATCH     "O",XFLD0003
          GOTO      VCPMT400 IF EQUAL
.
          MATCH     "D",XFLD0003
          GOTO      VCPMT400 IF EQUAL
.
          MOVE      "Record Type must be O, D or E",ERRORMSG
          CALL      PERR0000
.
VCPMT400  MATCH     SP70,XFLD0004
          IF        @EQUAL
            MOVE      "Contract Procedure Code is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPMT500
          ENDIF
.
          MOVE      XFLD0004,FLDDATA
          MOVE      "Contract Procedure Code",FLDNAME
          MOVE      9,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPMT500
          ENDIF
.
          MOVE      "Contract Procedure Code",FLDNAME
          MOVE      XFLD0004,DIM9
          CALL      VPROC000
.
VCPMT500  MATCH     SP70,XFLD0005
          IF        @EQUAL
            MOVE      "Item Type is blank",ERRORMSG
            CALL      PERR0000
            GOTO      VCPMT600
          ENDIF
.
          MATCH     "0",XFLD0005
          GOTO      VCPMT600 IF EQUAL
.
          MATCH     "1",XFLD0005
          GOTO      VCPMT600 IF EQUAL
.
          MOVE      "Item Type must be 0 or 1",ERRORMSG
          CALL      PERR0000
.
VCPMT600  MATCH     SP70,XFLD0006
          IF        @EQUAL
            GOTO      VCPMT700
          ENDIF
.
          MATCH     "0",XFLD0006
          GOTO      VCPMT700 IF EQUAL
.
          MATCH     "1",XFLD0006
          GOTO      VCPMT700 IF EQUAL
.
          MOVE      "Field Type must be Blank, 0 or 1",ERRORMSG
          CALL      PERR0000
.
VCPMT700  MOVE      XFLD0007,FLDDATA
          MOVE      "Procedure/Misc. Item",FLDNAME
          MOVE      9,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPMT800
          ENDIF 
.
VCPMT800  MATCH     SP70,XFLD0008
          IF        @EQUAL
            GOTO      VCPMT999 
          ENDIF
.
          MOVE      XFLD0008,FLDDATA
          MOVE      "Misc. Item Group",FLDNAME
          MOVE      3,FLDSIZE
          CALL      FLDSIZ00
          IF        FLDSTATS = 1
            GOTO      VCPMT999 
          ENDIF
.
          MOVE      "Misc. Item Group",FLDNAME
          MOVE      XFLD0008,DIM3
          CALL      VMGRP000
.
VCPMT999  RETURN
.
.------------------------------------------------------------------------------
. Validate a date field
.
. Requires:  TEMPDATE - Date in format ccyymmdd
.            FLDNAME  - Date Field Name
. Returns:   ERORFLAG - 1 if an error in date validation occurs.
.            ERORDESC - Description of error when an error is occurred
.------------------------------------------------------------------------------
VDAT0000  MOVE      SP70,ERORFLAG
          MATCH     SP8,TEMPDATE                 * blank date ?
          GOTO      VDAT9999 IF EQUAL            * yes
.
. Validate that the date has been fully populated
.
          SQUEEZE   TEMPDATE
          MOVELPTR  TEMPDATE,FORM1
          IF        FORM1 <> 8
            MOVE      "Insufficient digits",ERORDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
. Validate that the date is numeric
.
VDAT0100  TYPE      TEMPDATE
          IF        !@EQUAL
            MOVE      "Not numeric",ERORDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
          UNPACK    TEMPDATE,DIM2C,DIM2Y,DIM2M,DIM2D
.
. Validate the century is >= 18 and <= current century
.
          MOVE      DIM2C,FORM2
          IF        FORM2 < 18 | FORM2 > CURRCENT
            MOVE      "Century not valid",ERORDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
. Validate the month
.
          MOVE      DIM2M,FORM2
          IF        FORM2 < 1 | FORM2 > 12
            MOVE      "Month not valid",ERORDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
. Validate the day is:
.   < 32 for January, March, May, July, August, October & December
.   < 31 for April, June, September & November
.   < 29 for February, except in a leap year where it is < 30
.
          BRANCH    FORM2,VDAT1000:              * Jan
                          VDAT3000:              * Feb
                          VDAT1000:              * Mar
                          VDAT2000:              * Apr
                          VDAT1000:              * May
                          VDAT2000:              * Jun
                          VDAT1000:              * Jul
                          VDAT1000:              * Aug
                          VDAT2000:              * Sep
                          VDAT1000:              * Oct
                          VDAT2000:              * Nov
                          VDAT1000               * Dec
.
. Validate days for month of 31 days
.
VDAT1000  MOVE      DIM2D,FORM2
          IF        FORM2 < 1 | FORM2 > 31
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
. Validate days for month of 30 days
.
VDAT2000  MOVE      DIM2D,FORM2
          IF        FORM2 < 1 | FORM2 > 30
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
. Validate days for February
. A leap year is one where:
.  1. Every year divisible by 4 is a leap year.
.  2. But every year divisible by 100 is NOT a leap year
.  3. Unless the year is also divisible by 400, then it is still a
.     leap year.
.
VDAT3000  MOVE      DIM2D,FORM2
          PACK      DIM4,DIM2C,DIM2Y
          MOVE      DIM4,FORM4
          IF        (FORM4%4) = 0
            IF        (FORM4%100) = 0
              IF        (FORM4%400) = 0
                GOTO      VDAT3200
              ENDIF
            ELSE
              GOTO      VDAT3200
            ENDIF
          ENDIF
.
. Check for normal Feb days
.
VDAT3100  IF        FORM2 < 1 | FORM2 > 28
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
. Check for leap year Feb days
.
VDAT3200  IF        FORM2 < 1 | FORM2 > 29
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
VDAT3900  MOVE      "Day not valid",ERORDESC
          CALL      NERR0000
          GOTO      VDAT9999
.
VDAT9999  RETURN
.
.------------------------------------------------------------------------------
. Print error line
.------------------------------------------------------------------------------
PERR0000  ADD       ONE,ERRCOUNT            * Total error count
          MOVE      ONE,ERRFLAG
          MOVE      ONE,EXIT
.
          BRANCH    COPTION,PERR1000,PERR2000,PERR3000,PERR4000
          GOTO      PERR9999
.
PERR1000  CALL      PECPFE00
          GOTO      PERR9999
.
PERR2000  CALL      PECPRB00
          GOTO      PERR9999
.
PERR3000  CALL      PECPIB00
          GOTO      PERR9999
.
PERR4000  CALL      PECPMT00
          GOTO      PERR9999
.
PERR9999  RETURN
.
.------------------------------------------------------------------------------
. Print error line - Contract Procedure Fee
.------------------------------------------------------------------------------
PECPFE00  PRINT     *1,"|",LINECNTR,*10,"|",ERRORMSG,*62,"|",XZPFHOSP:
                    *77,XZPFCLMC,*91,XZPFCNTR:
                    *100,XZPFCPRC,*115,XDATE10:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*10,"|",ERRORMS1,*62,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PECPFE09  RETURN
.
.------------------------------------------------------------------------------
. Print error line - Contract Procedure Revenue Breakdown
.------------------------------------------------------------------------------
PECPRB00  PRINT     *1,"|",LINECNTR,*10,"|",ERRORMSG,*62,"|",XZRBHOSP:
                    *77,XZRBCLMC,*91,XZRBCNTR:
                    *100,XZRBCPRC,*115,XDATE10:
                    *127,XZRBBRCD,*132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*10,"|",ERRORMS1,*62,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1 
.
PECPRB09  RETURN
.
.------------------------------------------------------------------------------
. Print error line - Contract Procedure Revenue Breakdown
.------------------------------------------------------------------------------
PECPRB10  ADD       ONE,ERRCOUNT            * Total error count
          MOVE      ONE,ERRFLAG
          MOVE      LINECNTR,LINECNT1
          SUB       ONE,LINECNT1
.
          UNPACK    SAVKEY37,NZPFHOSP,NZPFCLMC,NZPFCNTR,NZPFFTAB,NZPFCPRC:
                             NZPFEFDT 
.
          MOVE      NZPFEFDT,WRKDTE8
          CALL      CNVDTE00
          MOVE      WRKDTE10,XDATE10
.
          PRINT     *1,"|",LINECNT1,*10,"|",ERRORMSG,*62,"|",NZPFHOSP:
                    *77,NZPFCLMC,*91,NZPFCNTR:
                    *100,NZPFCPRC,*115,XDATE10:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*10,"|",ERRORMS1,*62,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PECPRB19  RETURN
.
.------------------------------------------------------------------------------
. Print error line - Contract Procedure Invoice Breakdown
.------------------------------------------------------------------------------
PECPIB00  PRINT     *1,"|",LINECNTR,*10,"|",ERRORMSG,*62,"|",XZIBHOSP:
                    *77,XZIBCLMC,*91,XZIBCNTR:
                    *100,XZIBCPRC,*115,XDATE10:
                    *127,XZIBBRCD,*132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*10,"|",ERRORMS1,*62,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PECPIB09  RETURN
.
.------------------------------------------------------------------------------
. Print error line - Contract Procedure Invoice Breakdown
.------------------------------------------------------------------------------
PECPIB10  ADD       ONE,ERRCOUNT            * Total error count
          MOVE      ONE,ERRFLAG
          MOVE      LINECNTR,LINECNT1
          SUB       ONE,LINECNT1
.
          UNPACK    SAVKEY37,NZPFHOSP,NZPFCLMC,NZPFCNTR,NZPFFTAB,NZPFCPRC:
                             NZPFEFDT
.
          MOVE      NZPFEFDT,WRKDTE8
          CALL      CNVDTE00
          MOVE      WRKDTE10,XDATE10
.
          PRINT     *1,"|",LINECNT1,*10,"|",ERRORMSG,*62,"|",NZPFHOSP:
                    *77,NZPFCLMC,*91,NZPFCNTR:
                    *100,NZPFCPRC,*115,XDATE10:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*10,"|",ERRORMS1,*62,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PECPRI19  RETURN
.
.------------------------------------------------------------------------------
. Print error line - Contract Procedure Matrix
.------------------------------------------------------------------------------
PECPMT00  PRINT     *1,"|",LINECNTR,*10,"|",ERRORMSG,*62,"|",XZCMCLMC:
                    *77,XZCMCNTR,*86,XZCMRTYP:
                    *98,XZCMCPRC,*113,XZCMITYP:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*10,"|",ERRORMS1,*62,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PECPMT09  RETURN
.
.------------------------------------------------------------------------------
. Pack fields with spaces
.------------------------------------------------------------------------------
PFLD0000  PACK      XFLD0001,XFLD0001,SP70
          PACK      XFLD0002,XFLD0002,SP70
          PACK      XFLD0003,XFLD0003,SP70
          PACK      XFLD0004,XFLD0004,SP70
          PACK      XFLD0005,XFLD0005,SP70
          PACK      XFLD0006,XFLD0006,SP70
          PACK      XFLD0007,XFLD0007,SP70
          PACK      XFLD0008,XFLD0008,SP70
          PACK      XFLD0009,XFLD0009,SP70
          PACK      XFLD0010,XFLD0010,SP70
          PACK      XFLD0011,XFLD0011,SP70
          PACK      XFLD0012,XFLD0012,SP70
          PACK      XFLD0013,XFLD0013,SP70
          PACK      XFLD0014,XFLD0014,SP70
          PACK      XFLD0015,XFLD0015,SP70
.
          PACK      XFLDX100,XFLDX100,SP70,SP70
          PACK      XFLDY100,XFLDY100,SP70,SP70
          PACK      XFLDX256,XFLDX256,SP70,SP70,SP70,SP70
          PACK      XFLDY256,XFLDY256,SP70,SP70,SP70,SP70
.
PFLD9999  RETURN
+
.------------------------------------------------------------------------------
. Clear total vars
.------------------------------------------------------------------------------
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ADDCOUNT
          MOVE      ZERO,UPDCOUNT
.
CTOT999   RETURN
+
.------------------------------------------------------------------------------
. Print total line
.------------------------------------------------------------------------------
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Records in file : ",LINECNTR,*132,"|"
          PRINT     *1,"| Total Errors          : ",ERRCOUNT,*132,"|"
.
          PRINT     *1,"| Total Records Added   : ",ADDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
+
.------------------------------------------------------------------------------
.
          INC       STD001IO
          INC       CLNZPPFE                 * clear fields
          INC       CLNZPRBR                 
          INC       CLNZPIBR                 
          INC       CLNZPCMT                 
.
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATFN1IO/INC
          INC       PATCMCIO/INC
          INC       NZPPFEIO/INC
          INC       NZPRBRIO/INC
          INC       NZPIBRIO/INC
          INC       NZPCMTIO/INC

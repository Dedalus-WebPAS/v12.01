.******************************************************************************
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*      PROGRAM NAME:     IBABIL88                                            *
.*                                                                            *
.*      FUNCTION:         HANDOVER REPORT 2 (DVH)                             *
.*                                                                            *
.*      DATE WRITTEN:     19/09/85                                            *
.*                                                                            *
.*      AUTHOR:           MALCOLM HAYSE (I.B.A)                               *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.******************************************************************************
.* V12.00.01 16/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V10.03.01 27/06/2013 Ebon Clements  CAR 287511                      *
.*                  Changes for WEXTN and NSEXTN length change from 4 to 20   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.09.02  16/01/2008  Peter Vela    CAR 153002                      *
.*                  Added new report format (Omit "Open Days", "Post Date"    *
.*                  and "Delvry Date"                                         *
.*        V9.09.01  10/01/2008  Jill Habasque  CAR 145292                     *
.*                  Added check for IBCNMHOS                                  *
.*        V9.07.00  13/07/2007 Ian Farnell                                    *
.*                  Ported from V9.06 to V9.07                                *
.******************************************************************************
.*        V9.06.00  19/12/2006  Janet George                                  *
.*                  Ported from V6 to V9                                      *
.******************************************************************************
.*        V6.07.B01 11/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*                     V6.05.02 11/11/1998 N Harrington                       *
.*                              Changed to use HEAD132                        *
.*                     V6.05.01 10/08/98 J.Tan  SRF 627165                    *
.*                              Fixed to check ward nursing                   *
.*                        V5.51 15.09.89 E.Phan  SRF4864                      *
.*                              Check for inactive ward/bed                   *
.*                        V5.52 26/09/89 Graeme Williams                      *
.*                              Fix nursing station options                   *
.*                              SRF 102439                                    *
.*                        V5.53 3/11/89  L.P.  SRF 102787                     *
.*                              Recompiled with new PATNOB                    *
.*                              FD & IO which has PLURARITY                   *
.*                              as part of the index.                         *
.*                        V5.54 20/06/90 Steve O'Gorman                       *
.*                              Recompiled for MATADAFD                       *
.*                              SRF 104915                                    *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
.          INC       HL7BMTFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATWR1FD/INC
          INC       PATNOBFD/INC
          INC       PATNURFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
+
.
.               WORK AREA
.
ADIAG     DIM      98
.
.
CODEM     INIT    "M "
CMATRN    FORM    1
.
PSKIP     FORM    1
DPSKIP    DIM     3
.
SAVNSTAT  DIM     3
SAVNDESC  DIM     30
SAVNSIST  DIM     30
SAVNEXTN  DIM     20
SAVWEXTN  DIM     20
SAWARD    DIM     3
REPTVIEW  FORM    1
WRDESC    DIM     20
.
SRLGTYP   DIM     3
DRLGTYP   DIM     30
.
CDESC     DIM     20
.
PHDAYS    FORM     3
.
DHHMM     DIM      5
.
MIN1      FORM     1
.
CJDAY      FORM    3           * JULIAN CURRENT DAY
AJDAY      FORM    3           * JULIAN ADMISSION DAY
.
PNAME     DIM     27         * PGNAME.PSNAME
COUNT     FORM    2
DOCNAME   DIM     12         * DOCTORNAME SHORTENED
DMSTAT    DIM     9
IBCNMHOS  FORM      1        * Multi Hospital Indicator
.
DSEX      DIM    6
DSON      INIT   "Son of"
DDAUG     INIT   "Daughter of"
.
SWARD     DIM     3
SBED      DIM     3
.
PRGID     INIT      "IBABIL88"
PRGNAM    INIT      "HANDOVER REPORT 2"
VERSION   INIT      "V12.01.00"
+
.
.         START OF PROGRAM
.
          CALL          DISPHEAD
.
          DISPLAY       *P56:24,"Opening patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"patma1af";
          OPEN          PATMA1A1,"patma1af"
          OPEN          PATMX1A1,"patmx1af"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN          PATMI1A1,"patmi1af"
          OPEN          PMSVX1A1,"pmsvx1af"
.
          DISPLAY       *P64:24,"pathspaf";
          OPEN          PATHSPA1,"pathspaf"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN          PATMI1A2,"patmi1af"
.
          DISPLAY       *P64:24,"patdo1af";
          OPEN          PATDO1A1,"patdo1af"
.
          DISPLAY       *P64:24,"patwr1af";
          OPEN          PATWR1A1,"patwr1af"
.
          DISPLAY       *P64:24,"patnobef";
          OPEN          PATNOBE1,"patnobef"
.
          DISPLAY       *P64:24,"patnursm";
          OPEN          PATNURS1,"patnursm"
.
          DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
.          READ      CONTROLF,EIGHTY;*1,HLBMDGUP,*2,HLBMDGUA,*105,HLBMDGAD:
.                                    *106,HLBMDGDS,*107,HLBMDGTR,*108,HLBMDGRG
.
          READ          CONTROLF,TEN5;*188,CMATRN
          READ      CONTROLF,ZERO;*43,IBCNMHOS
+
.
.        START OF PROGRAM
.
START   MOVE       ZERO,CPAGENO
        DISPLAY   *P1:4,*EF:
                 *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:6,*V2LON,ONE,*HOFF," = Particular Ward":
                  *P1:7,*V2LON,TWO,*HOFF," = Particular Nursing Station":
                *P1:8,*V2LON,THREE,*HOFF," = All Nursing Stations":
                 *P1:9,*V2LON,FOUR,*HOFF," = All Wards"
.
OPT1   
        KEYIN     *P1:11,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
.
BRNCH   BRANCH    OPTION OF KWARD,KNURS,ALLNS,ALLWS
        BEEP
        GOTO      OPT1
+
.
.               PRINT VIA WARD NUMBER
.
KWARD   KEYIN           *P1:12,"Enter Ward Number : ",*EL,*V2LON,SAWARD
        RESET           SAWARD
        GOTO            START IF EOS
.
        CALL            RPVW0000
.
        MOVE            ONE,PSKIP
        PACK            KEY6 WITH SAWARD,SP6
        CALL            RDWARD1
        BRANCH          OVRCD OF INVWARD
.
        BRANCH          WACTIVE,NOTACTIV     * Ignore non-active wards
.
        MOVE            WBDESC,WRDESC
        MOVE            WEXTN,SAVWEXTN
        DISPLAY         *P40:12,*V2LON,WRDESC
        MOVE            "60",CLNO
        DISPLAY         *P1:24,*EL:
                        *P20:24,"Ward :",*P40:24,"Bed :"
.
        GOTO            ALLWRDS
.
NOTACTIV  KEYIN     *P1:24,*EL,*B,"* Ward is not active *  Hit ",*V2LON:
                    "<ENTER>",*HOFF," to continue ... ",*EOFF,ANS,*P1:24,*EL;
          GOTO      KWARD
.
+
.
.             INPUT NURS CODE
.
KNURS   KEYIN           *P1:12,"Enter Station Number : ",*EL,*V2LON,SAVNSTAT
        RESET           SAVNSTAT
        GOTO            START IF EOS
.
        MOVE            SP3,NSTAT
        MOVE            SP3,NWARD
        MOVE            SP3,SAWARD
.
        PACK            KEY6 WITH SAVNSTAT,SP3
        CALL            RDNURS1
        BRANCH          OVRCD OF INVNURS
        DISPLAY         *P40:12,*V2LON,*EL,NSDESC
        MOVE            NSDESC,SAVNDESC
        MOVE            NSIST,SAVNSIST
        MOVE            NSEXTN,SAVNEXTN
.
        CALL          SEPPAGE
        CALL            RPVW0000
.
        MOVE            "60",CLNO
        DISPLAY         *P1:24,*EL:
                        *P20:24,"Ward :",*P40:24,"Bed :"
.
NEXTDSP CALL            RDKNURS1
        BRANCH          OVRCD OF ENDP
.
        MATCH           NSTAT,SAVNSTAT
        GOTO            ENDP IF NOT EQUAL
.
        PACK            KEY6 WITH NWARD,SP3
        CALL            RDWARD1
        BRANCH          OVRCD OF NEXTDSP
.
        BRANCH          WACTIVE,NEXTDSP      * Ignore non-active wards
.
        MOVE            WBDESC,WRDESC
        MOVE            WEXTN,SAVWEXTN
        BRANCH          WINPUT,ALLWRDS
        GOTO            NEXTW
.
INVNURS DISPLAY      *P40:12,*V2LON,"** Station does not Exist **",*W2
        GOTO         KNURS
.
INVWRD  DISPLAY      *P40:12,*V2LON,"** Ward does not Exist **",*W2
        GOTO         KWARD

+
.
.         ASK IF SEPERATE PAGE REQUIRED
.
SEPPAGE KEYIN         *P1:23,*EL,"Would you like each Ward ":
                             "on a seperate PAGE (":
                        *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                        ") ? ",*V2LON,ANS;
.
        MOVE          ZERO,PSKIP
        MATCH         "Y",ANS
        GOTO          ENDS1 IF EQUAL
        MOVE          ONE,PSKIP 
ENDS1   RETURN
+
.
.       ALL WARDS ON THE SYSTEM
.
ALLWS   CALL      KHOS0000
        BRANCH    EXIT,START
.
        CALL          SEPPAGE
        CALL            RPVW0000
        MOVE            "60",CLNO
        DISPLAY         *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :"
.
        MOVE            SP6,KEY6
        CALL            RDSWARD1
        MOVE            SP3,SAWARD
        GOTO            NEXTW
+
.
.       ALL NURSING STATIONS ON THE SYSTEM
.
ALLNS   CALL          SEPPAGE
        CALL            RPVW0000
        MOVE            "60",CLNO
        DISPLAY         *P1:24,*EL,"Station : ":
                        *P20:24,"Ward :",*P40:24,"Bed :"
.
        MOVE            SP6,KEY6
        MOVE            SP3,SAWARD
        CALL            RDSNURS1
.
NEXTS   CALL            RDKNURS1
        BRANCH          OVRCD OF ENDP
.
        MATCH           SP3,NWARD
        GOTO            CNTNS IF NOT EQUAL
.
        DISPLAY         *P12:24,*V2LON,NSTAT;
        MOVE            NSTAT,SAVNSTAT
        MOVE            NSDESC,SAVNDESC
        MOVE            NSIST,SAVNSIST
        MOVE            NSEXTN,SAVNEXTN
        MOVE            "60",CLNO       **** NEW PAGE EACH STATION
        GOTO            NEXTS
.
CNTNS   PACK            KEY6 WITH NWARD,SP3
        CALL            RDWARD1
        BRANCH          OVRCD OF NEXTS
.
        BRANCH          WACTIVE,NEXTS        * Ignore non-active wards
.
        MOVE            WBDESC,WRDESC
        MOVE            WEXTN,SAVWEXTN
.
        BRANCH          WINPUT,ALLWRDS
.
NEXTW   CALL            RDKWARD1
        COMPARE         ZERO,OVRCD
        GOTO            CNTNXW IF EQUAL
.
.
.     WE MUST CHECK NURSING STATION IF OPTION 2,3
.
        BRANCH          OPTION OF ENDP,NEXTDSP,NEXTS,ENDP
.
CNTNXW  IF              OPTION=2 | OPTION=3
          MATCH           NWARD,WWARD
          GOTO            NXTOP IF NOT EQUAL
        ENDIF
.
        BRANCH          WACTIVE,NXTWARD        * Ignore non-active wards
        MATCH           SP3,SAWARD
        IF        @EQUAL
            COMPARE   ONE,IBCNMHOS         * Using multi hospital
            GOTO      ALLWRDS IF NOT EQUAL
.
            PACK      PTHSHOSP,PTHSHOSP,SP70
            MATCH     SP10,PTHSHOSP        * All Hospitals
            GOTO      ALLWRDS IF EQUAL
.
            MATCH     PTWDHOSN,PTHSHOSP    * Correct hospital
            GOTO      NXTWARD IF NOT EQUAL
.
            GOTO      ALLWRDS
        ENDIF
.
        MATCH           SAWARD,WWARD
        GOTO            ENDP IF NOT EQUAL
.
ALLWRDS MATCH           SP3,WBED
        GOTO            CHKWAD IF NOT EQUAL
.
        COMPARE         ONE,WINPUT
        GOTO            NXTOP IF NOT EQUAL
.
        PACK            KEY13 WITH WWARD,SP8,SP2
        CALL            RDSNOBE1
NOBLP   CALL            RDKNOBE1
        BRANCH          OVRCD OF NXTOP
.
        MATCH           WWARD,NBWARD
        GOTO            NXTOP IF NOT EQUAL
.
        MOVE            NBADMNO,WADMNO
        MOVE            NBPLUR,WPLUR
        CALL            GETDATA
        GOTO            NOBLP
.
NXTWARD  MATCH      SP3,WBED
         GOTO       NEXTW IF NOT EQUAL
         PACK       KEY6,WWARD,ANSZ,ANSZ,ANSZ
         CALL       RDSWARD1
.
NXTOP   BRANCH          OPTION OF NEXTW,NEXTDSP,NEXTS,NEXTW
.
CHKWAD  CALL      GETDATA
        GOTO      NEXTW
.
.       Subroutine to process a record
.
GETDATA MATCH     WWARD,SWARD
        GOTO      FPRINTA IF EQUAL
.
.        WE MUST CHECK IF A WARD PER PAGE
.
        BRANCH    PSKIP OF FPRINTA
        CALL      HEAD
.
FPRINTA COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
.
        MATCH     ZEROVISN,WADMNO
        GOTO       EMPDA1 IF EQUAL
.
        CALL       DISPDA1
        RETURN
.
EMPDA1  CALL       EMPBED1
        RETURN
.
.
.
NOWARD  DISPLAY    *P20:24,*B,*V2LON,*EL:
                   "** No available data on File **",*W2
        GOTO       START
.
INVWARD DISPLAY    *P20:24,*EL,*B,*V2LON:
                   "** Ward does not Exist **",*W2
        GOTO       START
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP    COMPARE    ZERO,CPAGENO
        GOTO       NOWARD IF EQUAL
        PRINT      *N,"  ***  END OF REPORT  ***"
        DISPLAY    *P20:24,*EL,*V2LON:
                   "** Report Generation Completed **",*W2
        GOTO       START
+
.
.     PRINT ACTUAL DATA 
.
DISPDA1 PACK       ADIAG WITH SP30,SP30,SP30,SP30
.
        COMPARE    ONE,CMATRN
        GOTO       RDCURP IF NOT EQUAL
.
RDCURP  PACK       KEY9 WITH TWO,WADMNO
.
        CALL       RDMISS2
        BRANCH     OVRCD OF CHKONLVE
.
        PACK       ADIAG WITH ADIAG1,ADIAG2
        GOTO       CNTADMN   
.
.        CHECK IF PATIENT IS ON LEAVE
.
CHKONLVE
        PACK       KEY9 WITH FOUR,WADMNO
        CALL       RDMISS2
        BRANCH     OVRCD OF EMPBED1
        MOVE       "* PATIENT ON LEAVE *",ADIAG
.
CNTADMN MOVE       AURNO,KEY8
        CALL       RDMAST1
        BRANCH     OVRCD OF EMPBED1
.
BASDOC  MOVE       SP30,DOCNAME
        MOVE       ADOCTA,KEY6
        CALL       RDDOCT1
        BRANCH     OVRCD OF DSPCT1
        MOVE       DSNAME,DOCNAME
.
DSPCT1  MOVE       SP9,DMSTAT
        PACK       KEY5 WITH CODEM,PMSTAT
        CALL       RDCODE1
        BRANCH     OVRCD OF FPRINT
        MOVE       TDESC,DMSTAT
.
FPRINT  MATCH      PGNAME,DDAUG
        GOTO       FPRINTD IF EQUAL
.
        MATCH      PGNAME,DSON
        GOTO       FPRINTS IF EQUAL
.
        MOVE       PGNAME,ANS
        PACK       PNAME WITH ANS,DOT,PSNAME
        REP        ", ",PNAME
        GOTO       SPRINT
.
FPRINTD MOVE       "D of ",PGNAME
        PACK       PNAME WITH PGNAME,PSNAME
        GOTO       SPRINT
.
FPRINTS MOVE       "S of ",PGNAME
        PACK       PNAME WITH PGNAME,PSNAME
.
SPRINT   IF        REPTVIEW=ONE
           PRINT     *1,"!",WWARD,WBED,*8,"!",PURNO:
                      *17,"!",PNAME:
                      *34,"! ",PSEX,*38,"!",PSAGE:
                      *42,"!",DOCNAME:
                      *55,"!",*132,"!"
         ELSE
           PRINT     *1,"!",WWARD,WBED,*8,"!",PURNO:
                      *17,"!",PNAME:
                      *34,"! ",PSEX,*38,"!",PSAGE:
                      *42,"!",DOCNAME:
                      *55,"!",*61,"!",*73,"!",*132,"!"
         ENDIF
.
         CALL      PAGEND 
.
         PRINT     *1,"!",*17," Diagnosis :":
                   *34,ADIAG,*132,"!" 
.
         ADD       TWO,CLNO
         CALL      PAGEND
         DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED;
         MOVE      WWARD,SWARD
         RETURN
+
.
.          EMPTY BED PRINTOUT
.
EMPBED1  IF          REPTVIEW=ONE
           PRINT     *1,"!",WWARD,WBED:
                     *8,"!",*17,"!",*34,"!",*38,"!",*42,"!":
                     *55,"!",*132,"!"
         ELSE
           PRINT     *1,"!",WWARD,WBED:
                     *8,"!",*17,"!",*34,"!",*38,"!",*42,"!":
                     *55,"!",*61,"!",*73,"!",*132,"!"
         ENDIF
.
         CALL      PAGEND
         ADD       ONE,CLNO
         MOVE      WWARD,SWARD
         DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED;
         RETURN
+
.
.          LAST LINE ON THE CODE
.
PAGEND    PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
          MOVE      THREE,CLNO 
          BRANCH    OPTION OF HEAD13,HEAD11,HEAD11,HEAD13
          GOTO      HEAD13
.
HEAD11
          PRINT     *1,"SISTER : ",SAVNSIST:
                    *40,"STATION : ",SAVNSTAT,*60,SAVNDESC:
                    *95,"Extension : ",SAVNEXTN,*N
          ADD       TWO,CLNO
          GOTO      HEAD13
.
HEAD12    PRINT     " "
          ADD       ONE,CLNO
HEAD13 
.
.         PRINT     *40,"WARD    : ",WWARD,*60,WRDESC:
.                   *95,"Extension : ",SAVWEXTN,*N
.         ADD       TWO,CLNO
.
         CALL      PAGEND
.
         IF          REPTVIEW=ONE
           PRINT     *1,"! Ward",*8,"!U/R no":
                     *17,"! Patients Name":
                     *34,"!Sex",*38,"!Age":
                     *42,"!  Attending":
                     *55,"!":
                     *87,"Remarks & Date",*132,"!":
                     *N,"! /Bed",*8,"!",*17,"!":
                     *34,"!M/F",*38,"!":
                     *42,"!   Doctor":
                     *55,"!",*132,"!"
         ELSE
           PRINT     *1,"! Ward",*8,"!U/R no":
                     *17,"! Patients Name":
                     *34,"!Sex",*38,"!Age":
                     *42,"!  Attending":
                     *55,"! Oper",*61,"!Post Delvry",*73,"!":
                     *87,"Remarks & Date",*132,"!":
                     *N,"! /Bed",*8,"!",*17,"!":
                     *34,"!M/F",*38,"!":
                     *42,"!   Doctor":
                     *55,"! Days",*61,"! Date   Sex",*73,"!",*132,"!"
         ENDIF
.
         ADD       TWO,CLNO
         CALL      PAGEND
         RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
.
+
.
RPVW0000  KEYIN     *P1:23,*EL,"View: ",*V2LON,"0",*HOFF,"(Default View)":
  *V2LON," 1",*HOFF,"(Omit #"Open Days#", #"Post Date#" and #"Delvry Date#"): ":
                    *V2LON,ANS;
.
          MOVE      ZERO,REPTVIEW
          MATCH     "0",ANS
          GOTO      RPVW9999 IF EQUAL
          MOVE      ONE,REPTVIEW
.
RPVW9999  RETURN

+
.
         INC       STD001IO
.
         INC       PATCODIO/INC
         INC       PATMA1IO/INC
.          INC       HL7BMTIO/INC
.          INC       HL7BMTMA
         INC       PATMI1IO/INC
         INC       PMSVX1IO/INC
         INC       PATDO1IO/INC
         INC       PATWR1IO/INC
         INC       PATNOBIO/INC
         INC       PATNURIO/INC
         INC       PATHSPIO/INC
         INC       PATHSPKY
.
ENDIT    CHAIN      PGM
         STOP
+

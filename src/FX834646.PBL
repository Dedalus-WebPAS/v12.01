.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   FX834646                                                  *
.*               This program lists all receipts with blank receipt date   *
.***************************************************************************
.* Date      :   22/03/2017                                                *
.* Author    :   J.Tan                                                     *
.* Mods      :                                                             *
.* V10.08.00     22/03/2017 J.Tan     TSK 0834646                          *
.*               Created fixit                                             *
.***************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       RCPREGFD/INC
          INC       RCPDTEFD/INC
          INC       RCPBNKFD/INC
          INC       RCPBDTFD/INC
.
RECNUMB   FORM      6
SUSPREGI  DIM       3
PRNTFLAG  FORM      1
.
PRGID     INIT      "FX834646"
PRGNAM    INIT      "Fixit Receipts with Blank date"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      SCRN0000               * display input screen
.
          CALL      PONL0000               * Keyin print only option
          BRANCH    EXIT,ML0100
.
          CALL      KREG0000               * keyin register
          BRANCH    EXIT,ML0100
.
          CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,ML0200,ML0100,ML9999       * Yes, no, cancel
          GOTO      ML0100
.
ML0200    CALL      PROC0000               * process records for update
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"rcpregaf";
          OPEN      RCPREGA1,"rcpregaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"rcpdteaf";
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      RCPBDTA1,"rcpbdtaf"
.
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                *P1:5,*V2LON,ONE,*HOFF," = Fix Receipts with Blank Receipt date"
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                PROC0000            Called by: ML0000     *
.*              read through receipt transaction file                       *
.****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Processing Receipt : ";
.
          CALL      HEAD0000
          MOVE      ZERO,RECNUMB
          PACK      KEY15,SP70
          CALL      RSRCDTE1
PROC1000  CALL      RKRCDTE1
          BRANCH    OVRCD,PROC9000
.
          MATCH     SP70,RCDTTDAT
          GOTO      PROC1000 IF NOT EQUAL      * date is not blank
.
          DISPLAY   *P26:24,RCDTRECN;
          ADD       ONE,RECNUMB
.
          BRANCH    PRNTFLAG,PROC8000          * Print Only
.
          MOVE      RCDTCDAT,RCDTTDAT          * Use Created date as Trans.date
          REP       " 0",RCDTTDAT
          MATCH     SP70,RCDTREGI
          IF        @EQUAL
            MOVE      SUSPREGI,RCDTREGI        * Set to suspense register
            MOVE      "2",RCDTALLO             * Set to Not allocated
          ENDIF
          CALL      UPRCDTE1
.
          MOVE      RCDTRECN,KEY12
          CALL      RDRCBNK1
          IF        OVRCD=0
            MOVE      RCDTTDAT,RCBNTDAT
            CALL      UPRCBNK1                 * update banking file
          ENDIF
.
          PACK      KEY15,RCDTRECN,SP70
          CALL      RSRCBDT1
PROC7000  CALL      RKRCBDT1
          BRANCH    OVRCD,PROC8000
.
          MATCH     RCDTRECN,RCBDRECN          * Same receipt no?
          GOTO      PROC8000 IF NOT EQUAL
.
          MOVE      RCDTTDAT,RCBDTDAT
          CALL      UPRCBDT1                   * update banking detail file
          GOTO      PROC7000
.
PROC8000  PRINT     *1,RCDTRECN,*15,RCDTREFR,*35,RCDTREGI;
.
          MATCH     SP70,RCDTDPTY
          IF        !@EQUAL
            MOVE      "DP",KEY2
            PACK      KEY5,KEY2,RCDTDPTY
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Deposit Type",TDESC
            ENDIF
            PRINT     *45,RCDTDPTY," - ",TDESC;
          ENDIF
          PRINT     " "
.
          GOTO      PROC1000
.
PROC9000  IF        PRNTFLAG=1
            DISPLAY   *P1:15,*EF,"No of records found : ",RECNUMB
          ELSE
            DISPLAY   *P1:15,*EF,"No of records updated : ",RECNUMB
          ENDIF
          PRINT     *N,*N,*1,"Total number of records : ",RECNUMB
.
          DISPLAY   *P1:24,*EL,"Processing complete.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.***************************************************************************
.*                                HEAD0000                                 *
.*                     Heading                                             *
.***************************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD132                 * Print the report header
          PRINT     *N,*1,"Receipt No.",*15,"Reference",*35,"Register":
                    *45,"Deposit Type":
                    *N,*1,"-----------",*15,"----------------",*35,"--------":
                    *45,"-----------------------"
HEAD9999  RETURN
+
.***************************************************************************
.*                                SCRN0000                                 *
.*                     display the input screen                            *
.***************************************************************************
SCRN0000  DISPLAY   *P1:16," Print Only (Y/N) ":
                    *P27:16,COLON:
                    *P1:18," Suspense Register ":
                    *P27:18,COLON
SCRN9999  RETURN
+
.****************************************************************************
.*                                PONL0000                                  *
.*                        Keyin Print only option                           *
.****************************************************************************
PONL0000  MOVE      ZERO,EXIT
          MOVE      ZERO,PRNTFLAG
          KEYIN     *P29:16,*V2LON,ANS;
          APPEND    SP70,ANS
          RESET     ANS
          MATCH     SP70,ANS
          GOTO      PONL7000 IF EQUAL     * Exit
.
          REP       "yYnN",ANS
          MATCH     "Y",ANS
          GOTO      PONL9000 IF EQUAL
          MATCH     "N",ANS
          GOTO      PONL8000 IF EQUAL
          BEEP
          GOTO      PONL0000
.
PONL7000  MOVE      ONE,EXIT
          GOTO      PONL9999
.
PONL8000  DISPLAY   *P34:16,"No"
          GOTO      PONL9999
.
PONL9000  MOVE      ONE,PRNTFLAG          * Print only option
          DISPLAY   *P34:16,"Yes"
          GOTO      PONL9999
PONL9999  RETURN
+
.****************************************************************************
.*                                KREG0000                                  *
.*                        Key in suspense register                          *
.****************************************************************************
KREG0000  MOVE      ZERO,EXIT
          KEYIN     *P29:18,*EL,*V2LON,SUSPREGI
.
          ENDSET    SUSPREGI
          APPEND    SP70,SUSPREGI
          RESET     SUSPREGI
.
          MATCH     SP70,SUSPREGI
          GOTO      KREG8000 IF EQUAL
.
          PACK      KEY3,SUSPREGI,SP3
          CALL      RDREGA1
          BRANCH    OVRCD,KREG4000
.
          COMPARE   "99",REGIBACD
          GOTO      KREG5000 IF NOT EQUAL      * Not a suspense register
.
          DISPLAY   *P34:18,REGDESC
          MOVE      ZERO,EXIT
          GOTO      KREG9999
.
KREG4000  DISPLAY   *P34:18,*B,*EL,"Invalid Register ";
          CALL      HITENTER
          GOTO      KREG0000
.
KREG5000  DISPLAY   *P34:18,*B,*EL,"must be a Suspense Register  ";
          CALL      HITENTER
          GOTO      KREG0000
.
KREG8000  MOVE      ONE,EXIT
KREG9999  RETURN

. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODIO/INC
          INC       RCPREGIO/INC
          INC       RCPDTEIO/INC
          INC       RCPBDTIO/INC
          INC       RCPBNKIO/INC

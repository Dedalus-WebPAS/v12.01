.******************************************************************************
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*      PROGRAM NAME:     IBABIL75                                            *
.*                                                                            *
.*      FUNCTION:         SUPERVISOR REPORT                                   *
.*                                                                            *
.*      DATE WRITTEN:     30/07/87                                            *
.*                                                                            *
.*      AUTHOR:           J.TAN   (I.B.A)                                     *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.*                                                                            *
.*        V12.00.01 30/05/2025  Don Nguyen    TSK 0955096                     *
.*                  Alphanumeric visit number changes                         *
.*        V10.03.01 27/06/2013 Ebon Clements  CAR 287511                      *
.*                  Changes for WEXTN and NSEXTN length change from 4 to 20   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*                    V5.07.B02 27/01/1999 N Harrington                       *
.*                              507 rebounds                                  *
.*                    V5.07.B01 12/11/1998 Jim Stathopoulos                   *
.*                              507 Changes                                   *
.******************************************************************************
. *                       W2.01  15/03/88  J.CLARK         *
. *                              Include MAT files         *
. *                       W2.02 14/07/88 G.Williams (I.B.A)*
. *                             Added New File - PATNOBFD  *
. *                       V4.01 20/10/88 Graeme Williams   *
. *                             Fix up maternity interface *
. *                       V5.01 15/02/89 Graeme Williams   *
. *                             Fix up maternity for cisam *
. *                       V5.02 27/05/89 J.Tan             *
. *                             Fixed Mother Adm. to form8 *
. *                             (PATMI1FD).     SRF 100750 *
. *                       V5.03 06/06/89 J.Tan             *
. *                             Mods Locking master file   *
. *                             (PATIOMAS)     SRF 100809  *
. *                       V5.04 09.06.89 S.Barcham         *
. *                             Fixed MATADM includes      *
. *                             SRF 100916                 *
. *                       V5.05 17.06.89 S.Barcham         *
. *                             Compiled for new standby   *
. *                             SRF 101101                 *
. *                    V5.01.01 06.07.89 K.Peak            *
. *                             Changed Adm & U/R to 8 chrs*
. *                    V5.01.02 26/09/89 Graeme Williams   *
. *                             Fixed nursing station optns*
. *                             SRF 102439                 *
. *                   V5.01.121 27/11/90 Glen Hobby        *
. *                            Recompiled for changes to   *
. *                            PATMX1FD                    *
. *                   V5.01.122 11/06/93 J.Tan  SRF 121660 *
. *                             Mods to check for active   *
. *                             ward/bed                   *
. *                                                        *
. **********************************************************
.
          INC           STD001FD
.
          INC           PATCODFD/INC
          INC       PATMA1FD/INC

          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATWR1FD/INC
          INC           PATNOBFD/INC
          INC           PATNURFD/INC
          INC       PATDO1FD/INC
.
.               WORK AREA
.
ADIAG     DIM      50
.
.
CODEA     INIT    "A "
CODEDC    INIT    "DC"
CODEM     INIT    "M "
CODES     INIT    "S "
.
CMATRN    FORM    1
.
NOLINES   FORM    2
LINENUM   FORM    2
.
PSKIP     FORM    1
DPSKIP    DIM     3
.
SAVNSTAT  DIM     3
SAVNDESC  DIM     30
SAVNSIST  DIM     30
SAVNEXTN  DIM     20
SAVWEXTN  DIM     20
WRDESC    DIM     20
SAWARD    DIM     3
.
SRLGTYP   DIM     3
DRLGTYP   DIM     30
.
CDESC     DIM     20
.
PHDAYS    FORM     3
.
DHHMM     DIM      5
.
MIN1      FORM     1
.
CJDAY     FORM    3           * JULIAN CURRENT DAY
AJDAY     FORM    3           * JULIAN ADMISSION DAY
.
XDAY1     DIM     2
XMON1     DIM     2
XYEAR1    DIM     2
XCENT1    DIM     2
.
XDAY2     DIM     2
XMON2     DIM     2
XYEAR2    DIM     2
XCENT2    DIM     2
.
PNAME     DIM     27         * PGNAME.PSNAME
PSNAME20  DIM     20
PGNAME20  DIM     20
.
COUNT     FORM    2
DTITL4    DIM     4
DNAME48   DIM     48         * DOCTORNAME SHORTENED
DADIET    DIM     3
.
DSEX      DIM    6
.
SWARD     DIM     3
SBED      DIM     3
.
VERSION   INIT      "V12.01.00"
PRGID     INIT      "IBABIL75"
PRGNAM    INIT      "Supervisors Report"
.
.         START OF PROGRAM
.
          CALL          DISPHEAD
          DISPLAY       *P56:24,"Opening patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY       *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY       *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY       *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY       *P64:24,"patnobef";
          OPEN          PATNOBE1,"patnobef"
.
          DISPLAY       *P64:24,"patnursm";
          OPEN          PATNURS1,"patnursm"
.
          DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
.
          READ          CONTROLF,TEN5;*188,CMATRN
+
.
.        START OF PROGRAM
.
START   MOVE       ZERO,CPAGENO
        DISPLAY   *P1:4,*EF:
                 *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:6,*V2LON,ONE,*HOFF," = Particular Ward":
                  *P1:7,*V2LON,TWO,*HOFF," = Particular Nursing Station":
                *P1:8,*V2LON,THREE,*HOFF," = All Nursing Stations":
                *P1:9,*V2LON,FOUR,*HOFF," = All Wards"
.
OPT1   
        KEYIN     *P1:11,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
.
BRNCH   BRANCH    OPTION OF KWARD,KNURS,ALLNS,ALLWS
        BEEP
        GOTO      OPT1
+
.
.               PRINT VIA WARD NUMBER
.
KWARD   DISPLAY         *P1:12,"Enter Ward Number : "
.
        MOVE            TEN2,EVERT 
        MOVE            TWENTY1,ECOL
        CALL            PATWRDKY
        BRANCH          EXIT,START,START
.
        MOVE            WWARD,SAWARD
        BRANCH          WACTIVE,NOTACTIV       * Ignore non-active wards
.
        MOVE            WBDESC,WRDESC
        MOVE            WEXTN,SAVWEXTN
        MOVE            WBLINE,NOLINES        ** no of lines btwn BEDS
        DISPLAY         *P40:12,*V2LON,WRDESC
        MOVE            "60",CLNO
        DISPLAY         *P1:24,*EL:
                        *P20:24,"Ward :",*P40:24,"Bed :"
.
        GOTO            ALLWRDS
.
NOTACTIV
        KEYIN           *P1:24,*EL,*V2LON,*B,"** Ward is not active ** ";
        CALL            HITENTER
        GOTO            KWARD
+
.
.             INPUT NURS CODE
.
KNURS   DISPLAY         *P1:12,"Enter Station Number : "
.
        MOVE            TEN2,EVERT
        MOVE            TWENTY4,ECOL
        CALL            PATNURKY
        BRANCH          EXIT,START,START
        MOVE            NSTAT,SAVNSTAT
        SQUEEZE         SAVNSTAT
        RESET           SAVNSTAT
.
        MOVE            SP3,NSTAT
        MOVE            SP3,NWARD
        MOVE            SP3,SAWARD
.
        DISPLAY         *P40:12,*V2LON,*EL,NSDESC
        MOVE            NSDESC,SAVNDESC
        MOVE            NSIST,SAVNSIST
        MOVE            NSEXTN,SAVNEXTN
.
        CALL          SEPPAGE
.
        MOVE            "60",CLNO
        DISPLAY         *P1:24,*EL:
                        *P20:24,"Ward :",*P40:24,"Bed :"
.
NEXTDSP CALL            RDKNURS1
        BRANCH          OVRCD OF ENDP
.
        MATCH           NSTAT,SAVNSTAT
        GOTO            ENDP IF NOT EQUAL
.
        PACK            KEY6 WITH NWARD,SP3
        CALL            RDWARD1
        BRANCH          OVRCD OF NEXTDSP
        BRANCH          WACTIVE,NEXTDSP        * Ignore non-active wards
        MOVE            WBDESC,WRDESC
        MOVE            WEXTN,SAVWEXTN
        MOVE            WBLINE,NOLINES        ** no of lines btwn BEDS
        BRANCH          WINPUT,ALLWRDS
        GOTO            NEXTW
.
INVNURS DISPLAY      *P40:12,*V2LON,"* Station does not Exist *",*W2
        GOTO         KNURS
.
INVWRD  DISPLAY      *P40:12,*V2LON,"* Ward does not Exist *",*W2
        GOTO         KWARD
+
.
.         ASK IF SEPERATE PAGE REQUIRED
.
SEPPAGE KEYIN         *P1:23,"Would you like each Ward ":
                             "on a seperate PAGE (":
                        *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                        ") ? ",*V2LON,ANS;
.
        MOVE          ZERO,PSKIP
        MATCH         "Y",ANS
        GOTO          ENDS1 IF EQUAL
        MOVE          ONE,PSKIP 
ENDS1   RETURN
+
.
.       ALL WARDS ON THE SYSTEM
.
ALLWS   CALL          SEPPAGE
        MOVE            "60",CLNO
        DISPLAY         *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :"
.
        MOVE            SP6,KEY6
        CALL            RDSWARD1
        MOVE            SP3,SAWARD
        GOTO            NEXTW
+
.
.       ALL NURSING STATIONS ON THE SYSTEM
.
ALLNS   CALL          SEPPAGE
        MOVE            "60",CLNO
        DISPLAY         *P1:24,*EL,"Station : ":
                        *P20:24,"Ward :",*P40:24,"Bed :"
.
        MOVE            SP6,KEY6
        MOVE            SP3,SAWARD
        CALL            RDSNURS1
.
NEXTS   CALL            RDKNURS1
        BRANCH          OVRCD OF ENDP
.
        MATCH           SP3,NWARD
        GOTO            CNTNS IF NOT EQUAL
.
        DISPLAY         *P12:24,*V2LON,NSTAT;
        MOVE            NSTAT,SAVNSTAT
        MOVE            NSDESC,SAVNDESC
        MOVE            NSIST,SAVNSIST
        MOVE            NSEXTN,SAVNEXTN
        MOVE            "60",CLNO       ** NEW PAGE EACH STATION
        GOTO            NEXTS
.
CNTNS   PACK            KEY6 WITH NWARD,SP3
        CALL            RDWARD1
        BRANCH          OVRCD OF NEXTS
        BRANCH          WACTIVE,NEXTS          * Ignore non-active wards
        MOVE            WBDESC,WRDESC
        MOVE            WEXTN,SAVWEXTN
        MOVE            WBLINE,NOLINES        ** no of lines btwn BEDS
        BRANCH          WINPUT,ALLWRDS
        GOTO            NEXTW
.
NEXTW   CALL            RDKWARD1
        COMPARE         ZERO,OVRCD
        GOTO            CNTNXW IF EQUAL
.
.     WE MUST CHECK NURSING STATION IF OPTION 2,3
.
        BRANCH          OPTION OF ENDP,NEXTDSP,NEXTS,ENDP
.
CNTNXW  BRANCH          WACTIVE,NXTWARD        * Ignore non-active wards
        MATCH           SP3,SAWARD
        GOTO            ALLWRDS IF EQUAL
.
        MATCH           SAWARD,WWARD
        GOTO            ENDP IF NOT EQUAL
.
ALLWRDS MATCH           SP3,WBED
        GOTO            CHKWAD IF NOT EQUAL
.
        MOVE            WBLINE,NOLINES        ** no of lines btwn BEDS
.
        COMPARE         ONE,WINPUT
        GOTO            NXTOP IF NOT EQUAL
.
        PACK            KEY13 WITH WWARD,SP20
        CALL            RDSNOBE1
NOBLP   CALL            RDKNOBE1
        BRANCH          OVRCD OF NXTOP
.
        MATCH           WWARD,NBWARD
        GOTO            NXTOP IF NOT EQUAL
.
        MOVE            NBADMNO,WADMNO
        MOVE            NBPLUR,WPLUR
        CALL            GETDATA
        GOTO            NOBLP
.
NXTWARD MATCH           SP3,WBED
        GOTO            NEXTW IF NOT EQUAL
        PACK            KEY6,WWARD,ANSZ,ANSZ,ANSZ
        CALL            RDSWARD1
.
NXTOP   BRANCH          OPTION OF NEXTW,NEXTDSP,NEXTS,NEXTW
.
CHKWAD  CALL            GETDATA
        GOTO            NEXTW
.
.       Subroutine to process a record
.
GETDATA MATCH     WWARD,SWARD
        GOTO      FPRINTA IF EQUAL
.
.        WE MUST CHECK IF A WARD PER PAGE
.
        BRANCH    PSKIP OF FPRINT0
        CALL      HEAD
        GOTO      FPRINTA
.
FPRINT0 
        COMPARE   "53",CLNO
        GOTO      FPRINTA IF NOT LESS
.
.          STOP HEADING FOR OPTION 4 ALL WARDS FOR EACH WARD ON SAME PAGE
.
        COMPARE   FOUR,OPTION
        GOTO      FPRINTA IF EQUAL
.
        CALL      HEAD12
.
.      ZERO ADMISSION NUMBER INDICATES BED IS EMPTY
.
FPRINTA
        COMPARE   "53",CLNO
        CALL      HEAD IF NOT LESS
.
        MATCH           ZEROVISN,WADMNO
        GOTO            EMPDA1 IF EQUAL
.
        CALL            DISPDA1
        RETURN
.
EMPDA1  CALL            EMPBED1
        RETURN
.
NOWARD  DISPLAY         *P20:24,*B,*V2LON,*EL:
                        "* No available data on File *",*W2
        GOTO            START
.
INVWARD DISPLAY         *P20:24,*EL,*B,*V2LON:
                       "* Ward does not Exist *",*W2
        GOTO            START
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     COMPARE      ZERO,CPAGENO
         GOTO         NOWARD IF EQUAL
         PRINT        *N,"  **  END OF REPORT  **"
         DISPLAY      *P20:24,*EL,*V2LON:
                      "* Report Generation Completed *",*W2
         GOTO         START
+
.
.     PRINT ACTUAL DATA 
.
DISPDA1 PACK        ADIAG WITH SP30,SP30
.
        COMPARE    ONE,CMATRN
        GOTO       RDCURP IF NOT EQUAL
.
RDCURP  PACK       KEY9 WITH TWO,WADMNO
.
        CALL       RDMISS2
        BRANCH     OVRCD OF CHKONLVE
.
        MOVE       ADIAG1,ADIAG
        GOTO       CNTADMN
.
.        CHECK IF PATIENT IS ON LEAVE
.
CHKONLVE
        PACK            KEY7 WITH FOUR,WADMNO
        CALL            RDMISS2
        BRANCH          OVRCD OF EMPBED1
        MOVE            "** PATIENT ON LEAVE **        ",ADIAG
.
CNTADMN MOVE            AURNO,KEY8
        CALL            RDMAST1
        BRANCH          OVRCD OF EMPBED1
.
BASDOC  PACK            DNAME48 WITH SP30,SP30
        MOVE            ADOCTA,KEY6
        CALL            RDDOCT1
        BRANCH          OVRCD OF DSPCT1
        MOVE            DGNAME,ANS
        MOVE            DTITL,DTITL4
        PACK            DNAME48 WITH DTITL4,ANS,DOT,DSNAME
.
DSPCT1 
.
FPRINT  MOVE       SP3,ANS
.
        UNPACK     ADATE INTO XCENT1,XYEAR1,XMON1,XDAY1
        UNPACK     PBDATE INTO XCENT2,XYEAR2,XMON2,XDAY2
.
        MOVE       PSNAME,PSNAME20
        MOVE       PGNAME,PGNAME20
.
        COMPARE   "53",CLNO
        CALL      HEAD IF NOT LESS
.
         PRINT     *1,"!",WWARD,WBED:
                   *8,"! ",PSNAME20,ANS,PTITL,ANS,PGNAME20:
                   *57,PREG:
                   *60,"!",PSAGE:
                   *64,"!",*69,"!":
                   *72,"!",*78,"!",*81,"!",*85,"!":
                   *88,"!",*132,"!":
                   *N,"!",*8,"! ",DNAME48:
                   *60,"!",*64,"!":
                   *69,"!",*72,"!",*78,"!",*81,"!",*85,"!":
                   *88,"!",*132,"!":
                   *N,"!",*8,"! ":
                   *60,"!",*64,"!":
                   *69,"!",*72,"!",*78,"!",*81,"!",*85,"!":
                   *88,"!",*132,"!":
                   *N,"!",*8,"! ":
                   *60,"!",*64,"!":
                   *69,"!",*72,"!",*78,"!",*81,"!",*85,"!":
                   *88,"!",*132,"!":
                   *N,"!",*8,"! ":
                   *60,"!",*64,"!":
                   *69,"!",*72,"!",*78,"!",*81,"!",*85,"!":
                   *88,"!",*132,"!":
                   *N,"!",*8,"!",ADIAG:
                   *60,"!",*64,"!":
                   *69,"!",*72,"!",*78,"!",*81,"!",*85,"!":
                   *88,"!",*132,"!" 
         ADD       "6",CLNO
.
.           CHECK THE NUMBER OF LINES BTWN BEDS  (WBLINE)
.
CHKLINES MOVE      SIX,COUNT
.
         COMPARE   ZERO,NOLINES
         GOTO      CHK10 IF NOT EQUAL
.
         MOVE      ONE,NOLINES
.
CHK10    MOVE      NOLINES,LINENUM
         MULT      SEVEN,LINENUM            
.
         DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED;
.
NXTDSPC  COMPARE   COUNT,LINENUM
         GOTO      ENDDSP1 IF EQUAL
.
         CALL      PAGSEP
         ADD       ONE,COUNT
         GOTO      NXTDSPC
.
ENDDSP1  CALL      PAGEND
         DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED;
         MOVE      WWARD,SWARD
         RETURN
+
.
.          EMPTY BED PRINTOUT
.
EMPBED1 COMPARE   "53",CLNO
        CALL      HEAD IF NOT LESS
.
         PRINT     *1,"!",WWARD,WBED,*8,"!":
                   *60,"!",*64,"!":
                   *69,"!",*72,"!",*78,"!",*81,"!",*85,"!":
                   *88,"!",*132,"!"  
         ADD       ONE,CLNO
.
         MOVE      ONE,COUNT
.
EMPBED3  COMPARE   "7",COUNT
         GOTO      ENDDSP1 IF NOT LESS
.
         CALL      PAGSEP
         ADD       ONE,COUNT   
         GOTO      EMPBED3
+
.
.          LAST LINE ON THE CODE
.
PAGEND    PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
PAGSEP    PRINT    *1,"!",*8,"!":
                   *60,"!",*64,"!":
                   *69,"!",*72,"!",*78,"!",*81,"!",*85,"!":
                   *88,"!",*132,"!"
          ADD       ONE,CLNO
          RETURN
.
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          BRANCH    OPTION OF DESC1,DESC2,DESC3,DESC4
.
DESC1     PRINT     *N,*40,"( Particular Ward )",*N;
          GOTO      PRTTIM
.
DESC2     PRINT     *N,*40,"( Particular Nursing Station ),*N";
          GOTO      PRTTIM
.
DESC3     PRINT     *N,*40,"( All Nursing Stations )",*N;
          GOTO      PRTTIM
.
DESC4     PRINT     *N,*40,"( All Wards )",*N;
.
PRTTIM    MOVE      THREE,CLNO 
          BRANCH    OPTION OF HEAD13,HEAD11,HEAD11,HEAD13
          GOTO      HEAD13
.
HEAD11
          PRINT     *1,"SISTER : ",SAVNSIST:
                    *45,"STATION : ",SAVNSTAT,*60,SAVNDESC:
                    *95,"Extension : ",SAVNEXTN,*N
          ADD       TWO,CLNO
          GOTO      HEAD13
.
HEAD12    PRINT     " "
          ADD       ONE,CLNO
HEAD13 
          MOVE      WBDESC,WRDESC
          PRINT     *45,"WARD    : ",WWARD,*60,WRDESC:
                    *95,"Extension : ",SAVWEXTN,*N
          ADD       TWO,CLNO
          MOVE      SP20,WRDESC
.
         CALL      PAGEND
.
         PRINT     *1,"! Ward",*8,"! Patients Name":
                   *57,"Rel",*60,"!Age":
                   *64,"! PO ",*69,"!IV":
                   *72,"!COND.",*78,"!DT":
                   *81,"!OBS",*85,"!CD":
                   *88,"!  Comments",*132,"!":
                   *N,"! /Bed",*8,"! / Attending Doctor":
                   *60,"!",*64,"!":
                   *69,"!",*72,"!":
                   *78,"!",*81,"!":
                   *85,"!",*88,"!",*132,"!"
.
         ADD       TWO,CLNO
         CALL      PAGEND
         RETURN
+
.
         INC       PATCODIO/INC
         INC       PATMA1IO/INC
         INC       PATMI1IO/INC
         INC       PMSVX1IO/INC
         INC       PATDO1IO/INC
         INC       PATWR1IO/INC
         INC       PATNOBIO/INC
         INC       PATNURIO/INC
         INC        PATWRDKY
         INC        PATNURKY
.
         INCLUDE    STD001IO
.
ENDIT    CHAIN      PGM
         STOP

.*********************************************************** 
. * System     :    Waiting Lists                          *
. * Program    :    IBAWAT96                               *
. * Desc       :    Audit Report                           *
. **********************************************************
. * Date       :    25/11/1988                             *
. * Author     :    N.S.                                   *
. * Modific/ns :                                           *
.***********************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899      *
.*           Recompiled as WATTR1FD changes                *
.***********************************************************
.* V9.04.01  24/08/2005  Jill Habasque CAR 51002           *
.*           Changed to use WTWMECNT instead of WTWMUNIQ   *
.*        V5.08.01  03/07/2000 J Habasque                  *
.*                  Recompiled for WATAUTR1                *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*                 V5.07.B01 19/01/1999  N Harrington      *
. *                          Mods to print century         *
. *                 L.P. 10/04/89  Screen cleanup   V5.01  *
. *                 M.O. 23/05/89  Display audits to 'I'   *
. *                 V5.01.01 19/06/89 M.Ohleiter           *
. *                          Changes for new release 5.01  *
. *                          22.08.89 S.Barcham            *
. *                          Audits missing s              *
. *                 V5.01.02 02/10/89 L.P.                 *
. *                          Recompiled with new WATAUXFD  *
. *                          SRF102514                     *
. *                 V5.01.03 04/10/89 K.Peak               *
. *                          Replaced capital to lowercase *
. *                          SRF 102601                    *
. *                 V5.01.04 06/10/89 K.Peak               *
. *                          Added search_dpath for alpha  *
. *                          ports  SRF 102601             *
. *                 V5.01.05 26/10/89  Steve O'Gorman      *
. *                          Reset FILENAM after extracting*
. *                          port number  SRF 102801       *
. *                 V5.01.10 05/07/1990 Bill Dew           *
. *                          Re-write IBAWAT96 to New      *
. *                          waiting list specifications.  *
. *                 V5.01.11 03/08/90 Justin Coates        *
. *                          Added the use of WTCNTPDS     *
. *                 V5.01.12 02/11/90 Karin Peak           *
. *                          Fixed the printing & clearing *
. *                          of the audits                 *
. *        V5.01.13  24.02.92 Neeriem "I Hate Support" Dye *
. *                  Reformat print to look beautiful for  *
. *                  new fields                            *
. *       V5.04.01  17/04/1997  howellsy   RHA             *
. *                 Print the new fields                   *
. *       V5.05.01  30/03/1998  Davin  506                 *
. *                 Print fields WTWPHDPE & WTWPDRGC       *
. **********************************************************
.
          INC       STD001FD
          INC       WATCONFD/INC
          INC       WATTR1FD/INC
          INC       WATPROFD/INC
+
.*******************************************************************************
.                            VARIABLE DECLARATION
.*******************************************************************************
.
DELAFLAG  FORM      1        * delete audit while printing
.
AUDFLAG   FORM      1        * data generated flag
.
AUDWMFLG  FORM      1        * 1 - wataudwm doesn't exist
.                            * 2 - wataudwm does exist
AUDWPFLG  FORM      1        * 1 - wataudwp doesn't exist
.                            * 2 - wataudwp does exist
.
NUM37     FORM      "37"
NUM55     FORM      "55"
.
PORTID    DIM       2
.
TIME      DIM       8
TOTAUDA   FORM      6
TOTAUDC   FORM      6
TOTAUDD   FORM      6
.
USERID    DIM       4
.
PRGID     INIT      "IBAWAT96"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Audit Reports"
+
.*******************************************************************************
.                          MAINLINE  Rock & Roll
.*******************************************************************************
ML0000    CALL      INIT0000               * open files     
ML1000    CALL      OPTN0000               * select options
          BRANCH    OPTION,ML1111,ML2222,ML3333
.
          CHAIN     PGM
.
ML1111    CALL      DELA0000           * delete patient procedure audits
          CALL      PPCA0000           * Print Procedure Code Audits
          GOTO      ML1000
.
ML2222    CALL      DELA0000           * delete patient procedure audits
          CALL      PPPA0000           * Print Patient Procedure Audits
          GOTO      ML1000
.
ML3333    CALL      CAUD0000           * Delete all printed audits 
          GOTO      ML1000
+
.*************************************************************************** 
.                                 INIT0000
.***************************************************************************
.
INIT0000  CALL      DISPHEAD
.
.          DISPLAY   *P1:8,*EL;
.          CALL      HITENTER
.          DISPLAY   *P1:8,*EL;
.         
          MOVE      ZERO,AUDWPFLG
          MOVE      ZERO,AUDWMFLG
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NUM37;*43,HWAUDA,HWAUDB,HWAUDC,HWAUDD,HWAUDE:
                                       HWAUDF,HWAUDG,HWAUDH,HWAUDI:
                                   *122,WTCNTPUC,*185,WTCNTPDS
.                                       
          DISPLAY   *P64:24,"wataudwp"
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      WATAUDWP,"wataudwp"
          TRAPCLR   IO
          IF        OVRCD=1
              MOVE      ONE,AUDWPFLG
          ENDIF
.
INIT2222  DISPLAY   *P64:24,"wataudwm"
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      WATAUTR1,"watautr1"
          TRAPCLR   IO
          IF        OVRCD=1
              MOVE      ONE,AUDWMFLG
          ENDIF
.
INIT9999  RETURN
+
.**************************************************************************
.                                 OPTN0000
.**************************************************************************
.
OPTN0000  MOVE      ZERO,AUDFLAG       * no data generated
.
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ",WTCNTPDS," Code File":
                    *P1:6,*V2LON,"2",*HOFF," = Patient ",WTCNTPDS," File":
                    *P1:7,*V2LON,"3",*HOFF," = Clear All Audit Files":
                    *P1:9,"Select Option : _"
.
OPTN1111  KEYIN      *P17:9,*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9999 IF EQUAL
          BRANCH     OPTION,OPTN3333,OPTN3333,OPTN9999
          BEEP
          GOTO      OPTN1111
.
OPTN3333  LOAD      EXIT,OPTION,HWAUDA,HWAUDB,HWAUDC,HWAUDD,HWAUDE
          COMPARE   ZERO,EXIT
          GOTO      OPTN9999 IF EQUAL
          BRANCH    OPTION,OPTN4100,OPTN4200
.
OPTN4100  DISPLAY   *P1:24,*B,*EL,WTCNTPDS," Code audits are switched OFF.  ";
          GOTO      OPTN5555
.
OPTN4200  DISPLAY   *P1:24,*B,*EL:
                    "Patient ",WTCNTPDS," audits are switched OFF.  ";
.
OPTN5555  CALL      HITENTER
          GOTO      OPTN0000
OPTN9999  RETURN
+
.****************************************************************************
.                                 DELA0000
.    Check if operator wishes to delete audit as they are printed.
.****************************************************************************
.
DELA0000  DISPLAY   *P1:24,"Print and Clear audit file (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") : ";
          KEYIN     ANS
          REPLACE   UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      DELA1111 IF NOT EQUAL
          MOVE      ONE,DELAFLAG       * set delete audit after printing
          GOTO      DELA9999
.
DELA1111  MATCH     ANSN,ANS
          GOTO      DELA0000 IF NOT EQUAL
          MOVE      ZERO,DELAFLAG      * save audit after printing
DELA9999  RETURN
+
.****************************************************************************
.                                 PPCA0000
.    Print Procedure code Audits.
.    IF DELAFLAG is one then delete audit after printing
.****************************************************************************
.
PPCA0000  IF        AUDWPFLG=1
              DISPLAY   *P1:24,*EL,"Procedure Code Audit File ":
                        "Does Not Exist. ";
              CALL      HITENTER
              GOTO      PPCA9999
          ENDIF   
.
          DISPLAY   *P1:24,*EL,"Audit : ",*P60:24:
                    *V2LON,*BLINKON,"*** Printing ***"
          MOVE      ZERO,TOTAUDA       * zero the Add audit counter
          MOVE      ZERO,TOTAUDC       * zero the change audit counter
          MOVE      ZERO,TOTAUDD       * zero the deleted audit counter
          MOVE      ZERO,CPAGENO       * zero the page counter
          CALL      HEAD0000
.
          PACK      KEY19,SP20
          CALL      ASPROA1            * position fptr in audit
PPCA2222  CALL      AKPROA1            * Read audit record
          BRANCH    OVRCD,PPCA9000
.
          COMPARE   ONE,WTWPAUDS       * test audit status printed yes or no
          GOTO      PPCA2222 IF NOT EQUAL
.
          PACK      KEY23,WTWPAUDD,WTWPAUDT,WTWPAUDP,WTWPAUDR,WTWPAUDO
          DISPLAY   *P9:24,KEY23
          CALL      PAUD0000           * Print audit header
.
          PRINT     WPCODE,"~",WPDESC,"~",WPTIME,"~",WPESTLS,"~",WPGRP,"~":
                    WPRHAT,"~",WPCHET,"~",WTWPHDPE,"~",WTWPDRGC:
                    *132,"|"
          MOVE      WTWPAUDR,ANS       * pass audit type to ADDA
          CALL      ADDA0000           * add up audit type counters
.
          MOVE      ONE,AUDFLAG        * data generated
.
          PACK      KEY19,WTWPAUDD,WTWPAUDT,WTWPAUDP,WTWPAUDR
          CALL      ADPROA1            * delete the existing audit
          BRANCH    DELAFLAG,PPCA2222
          MOVE      TWO,WTWPAUDS       * set the record status to printed
          CALL      AWPROA1            
          GOTO      PPCA2222
.
PPCA9000  CALL      PTOT0000           * print totals
.
PPCA9999  RETURN
+
.****************************************************************************
.    Add upp Add, change, delete audit types
.****************************************************************************
.
ADDA0000  REPLACE   "A1B2C3D4",ANS
          MOVE      ANS,EXIT
          BRANCH    EXIT,ADDA1111,ADDA9999,ADDA2222,ADDA3333
.
ADDA1111  ADD       ONE,TOTAUDA        * increment add audit
          GOTO      ADDA9999
.
ADDA2222  ADD       ONE,TOTAUDC        * increment change audit
          GOTO      ADDA9999
.
ADDA3333  ADD       ONE,TOTAUDD        * increment delete audit
ADDA9999  RETURN
+
.****************************************************************************
.    Print totals
.****************************************************************************
.
PTOT0000  
.         Check if any data has been generated
.
          BRANCH    AUDFLAG,PTOT1000
          DISPLAY   *P1:24,*EL,*V2LON,*P40:24,"** No data selected **",*W2;
.
PTOT1000  CALL      LINE0000
          PRINT     *N,"Total Additions Printed : ",TOTAUDA:
                    *N,"Total Changes   Printed : ",TOTAUDC:
                    *N,"Total Deletions Printed : ",TOTAUDD
          PRINT     *N,*N,"*** END OF REPORT ***"
PTOT9999  RETURN
+
.****************************************************************************
.
PAUD0000  COMPARE   CLNO,NUM55
          CALL      HEAD0000 IF LESS
.
          UNPACK    KEY23,CCENT,CYEAR,CMON,CDAY,TIME,PORTID,ANS,USERID
          CALL      PACDATE
          REP      " 0",CPCDATE
.
          PRINT     "|",CPCDATE,"| ",TIME:
                    " | ",PORTID," |  ",ANS," | ",USERID," | ";
          ADD       ONE,CLNO
.
PAUD9999  RETURN
+
.****************************************************************************
.                                 HEAD0000
.***************************************************************************
.
HEAD0000  CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          BRANCH    OPTION,HEAD1111,HEAD3333
.
HEAD1111  PRINT     WTCNTPDS," Master file"
          GOTO      HEAD6666
.
HEAD3333  PRINT     "Patient ",WTCNTPDS," file"
HEAD6666  CALL      LINE0000
          PRINT     "|   Date   |   Time   |Port|Type| User | Audit Data":
                    *132,"|"
          CALL      LINE0000
          ADD       FIVE,CLNO
HEAD9999  RETURN
+
.****************************************************************************
.
LINE0000  PRINT     "*---------------------------------------":
                    "---------------------------------------------":
                    "----------------------------------------------*"
LINE9999  RETURN
+
.****************************************************************************
.                                 PPPA0000
.    Print Patient procedure audits
.    IF DELAFLAG is one then delete audit after printing
.****************************************************************************
.
PPPA0000  IF        AUDWMFLG=1
              DISPLAY   *P1:24,*EL,"Patient Procedure Audit File ":
                        "Does Not Exist. ";
              CALL      HITENTER
              GOTO      PPPA9999
          ENDIF   
.
          DISPLAY   *P1:24,*EL,"Audit : ",*P60:24:
                    *V2LON,*BLINKON,"*** Printing ***"
          MOVE      ZERO,TOTAUDA       * zero the Add audit counter
          MOVE      ZERO,TOTAUDC       * zero the change audit counter
          MOVE      ZERO,TOTAUDD       * zero the deleted audit counter
          MOVE      ZERO,CPAGENO       * zero the page counter
          CALL      HEAD0000
.
          PACK      KEY19,SP20
          CALL      ASTREA1            * position fptr in audit
.
PPPA2222  CALL      AKTREA1            * Read audit record
          BRANCH    OVRCD,PPPA9000
.
          COMPARE   ONE,WTWMAUDS       * test audit status printed yes or no
          GOTO      PPPA2222 IF NOT EQUAL
.
          PACK      KEY23,WTWMAUDD,WTWMAUDT,WTWMAUDP,WTWMAUDR,WTWMAUDO
          DISPLAY   *P9:24,KEY23
          CALL      PAUD0000           * Print audit header
.
          UNPACK    WMREASON,KEY11,KEY29
          PRINT     WMURNO,"~",WMCODE,"~",DWMCNT,"~",WMTIME,"~",WMDESC:
                    "~",KEY11:
                    *132,"|":
                    *N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"| ":
                    KEY29,"~",DWMSTAT,"~",WMDATE1:
                    "~",WMDATE2,"~",WMDATE3,"~",WMDATE4,"~",WMREMOVE:
                    "~",WMUNIT,"~",WMDOCTOR,"~",WMSTAY,"~":
                    *132,"|":
                    *N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"| ":
                    "~",WMPTY,"~",WMNOTICE,"~",WMPTYPE,"~",WMPCAT:
                    "~",WMUDEF1,"~",WMUDEF2,"~",WMUDEF3,"~",WMUDEF4:
                    "~",WMBOOK,"~",WMKEYDT,"~",WTWMPORT,"~",WTWMREAD:
                    "~",WTWMDEDA,"~",WTWMECNT,"~",WTWMSENT,"~",WTWMCLSS,"~":
                    *132,"|":
                    *N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"| ":
                    WTWMDTAD,"~",WTWMACAT,"~",WTWMGADD,"~",WTWMDABD,"~":
                    WTWMEATY,"~",WTWMADMC,"~",WTWMECRA,"~",WTWMGPFA,"~":
                    *132,"|":
                    *N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"| ":
                    WTWMRFGP,"~",WTWMGPPC:
                    "~",WMUDEF5,"~",WMUDEF6,"~",WMUDEF7,"~",WMUDEF8:
                    "~",WTWMRANK:
                    *132,"|"
          ADD       "4",CLNO         * increment the printed line counter
.
          MOVE      WTWMAUDR,ANS       * pass audit type to ADDA
          CALL      ADDA0000           * add up audit type counters
.
          MOVE      ONE,AUDFLAG        * data generated
.
          PACK      KEY19,WTWMAUDD,WTWMAUDT,WTWMAUDP,WTWMAUDR
          CALL      ADTREA1            * delete the existing audit
          BRANCH    DELAFLAG,PPPA2222
          MOVE      TWO,WTWMAUDS       * set the record status to printed
          CALL      AWTREA1            
          GOTO      PPPA2222           * read another record
.
PPPA9000  CALL      PTOT0000           * print totals
.
PPPA9999  RETURN
+
.****************************************************************************
.                                 CAUD0000
.    Clear all printed audits
.****************************************************************************
.
CAUD0000  DISPLAY   *P1:23,"This option will clear all previously printed ":
                    "audits from file.":
                    *P1:24,"Press (",*V2LON,"Enter",*HOFF,") to continue or (":
                    *V2LON,"E",*HOFF,")xit : ";
          KEYIN     *V2LON,ANS
          REPLACE   UPPLOW,ANS
          MATCH     ANSE,ANS
          GOTO      CAUD9999 IF EQUAL
          DISPLAY   *P1:23,*EF
.
CAUD0300  IF        AUDWPFLG=1
              DISPLAY    *P1:23,*EF,*P1:24,"Procedure Code Audit File ":
                         "Does Not Exist. ";
              CALL       HITENTER
              DISPLAY    *P1:24,*EL
          ELSE
              GOTO       CAUD1000
          ENDIF
.
CAUD0500  IF        AUDWMFLG=1
              DISPLAY    *P1:23,*EF,*P1:24,"Patient Procedure Audit File ":
                         "Does Not Exist. ";
              CALL       HITENTER
              DISPLAY    *P1:24,*EL
              GOTO       CAUD9999
          ELSE
              GOTO       CAUD2000
          ENDIF
.
          DISPLAY   *P1:23,*EF,*P60:24,*V2LON,"*** Clearing ***"
.
.
.----- delete watproaf audits -----
.
CAUD1000  PACK      KEY19,SP20
          CALL      ASPROA1            * position fptr in audit
CAUD1111  CALL      AKPROA1            * Read audit record
          BRANCH    OVRCD,CAUD0500
.
          COMPARE   ONE,WTWPAUDS       * test audit status printed yes or no
          GOTO      CAUD1111 IF EQUAL
          PACK      KEY19,WTWPAUDD,WTWPAUDT,WTWPAUDP,WTWPAUDR
          CALL      ADPROA1
          MOVE      ONE,AUDFLAG        * data generated
          GOTO      CAUD1111
.
.----- delete wattr1af audits -----
.
CAUD2000  PACK      KEY19,SP20
          CALL      ASTREA1            * position fptr in audit
CAUD2222  CALL      AKTREA1            * Read audit record
          BRANCH    OVRCD,CAUD9000
.
          COMPARE   ONE,WTWMAUDS       * test audit status printed yes or no
          GOTO      CAUD2222 IF EQUAL 
          PACK      KEY19,WTWMAUDD,WTWMAUDT,WTWMAUDP,WTWMAUDR
          CALL      ADTREA1
          MOVE      ONE,AUDFLAG        * data generated
          GOTO      CAUD2222
.
.         Check if any data has been generated
.
CAUD9000  BRANCH    AUDFLAG,CAUD9999
          DISPLAY   *P1:24,*EL,*V2LON,*P40:24,"** No data selected **",*W2;
.
CAUD9999  RETURN
+
.****************************************************************************
.
         INC        STD001IO     
         INC       WATTR1IO/INC
         INC       WATPROIO/INC

.******************************************************************************
.* System         : Waiting Lists                                             *
.* Program        : IBAWAT75.PBL                                              *
.* Name           : Waiting List Planned Report                               *
.******************************************************************************
.* Date           : 31/03/1994                                                *
.* Author         : Paul Howells                                              *
.* Function       : To print all waiting list planned procedures as at a date.*
.* Modifications  :                                                           * 
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*       V10.11.01  07/09/2017  Thanh T.          TSK 0821710                 *
.*                  Removed PATCOMFD/IO                                       *
.*       V10.05.01  01/08/2015  Ania P            CAR 261630                  *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*                  V5.01.02  20/05/94  Paul Howells    SRF440873             *
.*                  Added Do Not Admit Before Date.                           *
.******************************************************************************
.
.         FD includes 
.
          INC       STD001FD                * standard variables
          INC       BOKRC1FD/INC
          INC       PATCODFD/INC            * codes file
          INC       PATCONFD/INC            * codes file
          INC       PATDO1FD/INC            * codes file
          INC       PATMA1FD/INC            * codes file
          INC       PATMI1FD/INC            * codes file
          INC       PMSVX1FD/INC            * codes file
          INC       PATDSCFD/INC            * codes file
          INC       WATMESFD/INC
          INC       WATPROFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATTR1FD/INC
          INC       WATCATFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.         program variables
.
ASATDATE  DIM       8
ASATDTE   DIM       8
COUNT     FORM      2
CMDLINE   DIM       70
CURRDATE  DIM       8
DATE10A   DIM       10
DATE10B   DIM       10
DATE10C   DIM       10
DESC18    DIM       18
DESC44    DIM       44
DIM3      DIM       3
DIM30     DIM       30
DOCCODE   DIM       6
DOCNAME   DIM       30
FHDR      INIT      "wat71a"
FSTAT     FORM      1
GOTONE    FORM      1
PATNAME   DIM       35
PATSEX    DIM       7
PATTYPE   DIM       20
PRASATD   DIM       10
PRDLIST   DIM       8
PRPLAFR   DIM       8
PRDNAB    DIM       8
.
.Temp file definition
.
.TEMPFILE  IFILE     SQL, FIXED=106, KEY="U1-8,9-16,17-25,26-27,28-29"
TEMPFILE  IFILE     SQL, FIXED=85, KEY="U69-76,9-16,17-25,26-27"
.
XXDLIST   DIM       8       1     * date on list
XXURNO    DIM       8       9     * UR Number
XXPROC    DIM       9      17     * Procedure
XXPCNT    DIM       2      26     * Procedure count
XXGNAME   DIM       19     28     * Given name
XXSNAME   DIM       19     47     * Surname
XXLTYPE   DIM       3      66     * List type
XXPLANFR  DIM       8      69     * Planned - effective date
XXDNAB    DIM       8      77     * Do Not Admit Before
.         End of record    85 
.
.
.         program constants
.
CATWN     INIT      "WN"
CATCC     INIT      "CC"
UNDL50    INIT      "--------------------------------------------------"
UNDL10    INIT      "----------"
UNDL13    INIT      "-------------"
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBAWAT75"
PRGNAM    INIT      "PROCEDURE PLANNED REPORT"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.*********************************************************************
.         Mainline Code / Controlling Logic
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000
          BRANCH    EXIT,ML9999             * exit selected
.
ML3000    CALL      KCON0000                * keyin consultant
          BRANCH    EXIT,ML1000             * nothing input
.
          CALL      KDAT0000                * keyin as at date
          BRANCH    EXIT,ML3000             * nothing input
.
          CALL      CONTQST                 * Ok to continue?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
.
ML4000    CALL      LOAD0000                * load data into temp file
          CALL      REPT0000                * print report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.********************************************************************* 
.         Initialization
.*********************************************************************
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
.
          MOVE      ONE,CNEWDS
          DISPLAY   *P50:24,"Opening"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"     * codes file
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"     * codes file
          OPEN      PATMX1A1,"patmx1af"     * codes file
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"     * codes file
          OPEN      PATDO1A2,"patdo1af"     * codes file
          DISPLAY   *P58:24,"watmesaf"
          OPEN      WATMESA1,"watmesaf"     * codes file
          DISPLAY   *P58:24,"watproaf"
          OPEN      WATPROA1,"watproaf"     * codes file
          DISPLAY   *P58:24,"watseaaf"
          OPEN      WATSEAA1,"watseaaf"     * codes file
          DISPLAY   *P58:24,"watseiaf"
          OPEN      WATSEIA1,"watseiaf"     * codes file
          DISPLAY   *P58:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"     * codes file
          DISPLAY   *P58:24,"watcataf";
          OPEN      WATCATA2,"watcataf"
.
          DISPLAY   *P58:24,*EL,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P58:24,*EL,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P58:24,*EL,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY;*238,PTCNURCO
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.*********************************************************************
.         See if to produce listing or not
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Produce report":
                    *P1:7,"Select option : "
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL       * exit
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.*********************************************************************
.*  KCON0000  :  Keyin Consultant                                    *
.*********************************************************************
KCON0000  DISPLAY   *P1:10,*EF,"Consultant : ",*P1:12,"As at Date : "
.
          MOVE      "14",ECOL
          MOVE      "10",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      PATDOCKY
          BRANCH    EXIT,KCON9999,KCON0000        * blank,exitchar
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          DISPLAY   *P23:10,PACFNAME
          MOVE      PACFNAME,DOCNAME
          MOVE      ZERO,EXIT
.
KCON9999  RETURN
+
.**************************************************************************
.*  KDAT0000  :   Keyin as at date                                        *
.**************************************************************************
KDAT0000  UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "14",CCOL
          MOVE      TEN2,CVERT
          MOVE      CCC,CCENT
          MOVE      ZERO,CDEFDTE
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT9000                * anything input
.
          PACK      ASATDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ASATDATE
          CALL      PACDATE
          MOVE      CPCDATE,PRASATD
.
          MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT
.
KDAT9999  RETURN
+
.*********************************************************************
.* LOAD0000  :  Load data into a temp file                           *
.*********************************************************************
LOAD0000  CALL      CRET0000                      * create temp file
.
          MOVE      ZERO,GOTONE
          MOVE      SP30,KEY29
          CALL      RSWTCAT2
LOAD1000  CALL      RKWTCAT2
          BRANCH    OVRCD,LOAD9999          * end of report
.
          MATCH     WTCAEDAT,ASATDATE
          GOTO      LOAD1000 IF LESS
.
          COMPARE   ZERO,GOTONE
          GOTO      LOAD2000 IF EQUAL
.
          MATCH     XXURNO,WTCAURNO
          GOTO      LOAD3000 IF NOT EQUAL
.
          MATCH     XXPROC,WTCAPROC
          GOTO      LOAD3000 IF NOT EQUAL
.
          MATCH     XXPCNT,DWTCAPCN
          GOTO      LOAD3000 IF NOT EQUAL
.
LOAD2000  MATCH     CATCC,WTCACATF
          GOTO      LOAD3000 IF NOT EQUAL
.
          MOVE      ZERO,GOTONE
.
          PACK      KEY5,ANSC,ANSC,WTCACODT,SP5
          CALL      RDCODE1
          BRANCH    OVRCD,LOAD1000
          MATCH     ANSP,TCINDC3
          GOTO      LOAD1000 IF NOT EQUAL        * not a planned procedure
.
          MOVE      ONE,GOTONE
          MOVE      WTCAURNO,XXURNO
          MOVE      WTCAPROC,XXPROC
          MOVE      WTCAPCNT,XXPCNT
          MOVE      WTCAEDAT,XXPLANFR
          GOTO      LOAD1000

LOAD3000  COMPARE   ZERO,GOTONE
          GOTO      LOAD1000 IF EQUAL
.
          MOVE      ZERO,GOTONE
.
          PACK      KEY19,XXURNO,XXPROC,XXPCNT
          CALL      RDTREA1                          * get list type
          BRANCH    OVRCD,LOAD1000
.
          MOVE      ASATDATE,ASATDTE
          CALL      CHKSTAT
.
          COMPARE   ONE,FSTAT
          GOTO      LOAD1000 IF NOT EQUAL         * unscheduled pats only
.
          MATCH     WMDOCTOR,DOCCODE              * same doctor code
          GOTO      LOAD1000 IF NOT EQUAL
.
.         does the user have security clearance?
.
LOAD4000  PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          IF        OVRCD=1
            PACK      KEY13,DOCCODE,WMUNIT,PASSCODE
            CALL      RDWTSEI1
            BRANCH    OVRCD,LOAD1000
          ENDIF
.
          DISPLAY   *P1:24,*V2LON,*EL,WTCAURNO,SP1,WTCAPROC,SP1,WTCAPCNT
.
          PACK      KEY8,WTCAURNO           * get patient name
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD1000
.
. post a record to the temp file
.
          MOVE      WMDATE1,XXDLIST
          MOVE      PGNAME,XXGNAME
          MOVE      PSNAME,XXSNAME
          MOVE      WMUNIT,XXLTYPE
          MOVE      WTWMDABD,XXDNAB
.
          PACK      KEY27,XXPLANFR,XXURNO,XXPROC,XXPCNT
          CALL      WRTEMP1
.
          GOTO      LOAD1000
.
LOAD9999  RETURN
+
.*********************************************************************
.         Produce the report
.*********************************************************************
REPT0000  CALL      IBACLOCK
          MOVE      ZERO,CPAGENO            * clear page number
          CALL      HEAD0000                * print header
.
          MOVE      SP30,KEY27
          CALL      RSTEMP1
REPT1000  CALL      RKTEMP1
          BRANCH    OVRCD,REPT9000          * end of report
.
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          CALL      FDAT0000                * format data for printing
.
          PRINT     *1,"| ",XXURNO,*12,XXSNAME,*32,"| ",XXLTYPE,*39,"| ":
                    DESC44,*87,"| ",PRDLIST,*98,"| ",PRPLAFR,*110,"| ":
                    PRDNAB,*123,"|":
                    *N,*1,"|",*12,XXGNAME,*32,"|",*39,"|",*87,"|",*98,"| ":
                    *110,"|",*123,"|"
.
          ADD       TWO,CLNO
.
          CALL      PCOM0000                      * print W/L comments
          PRINT     *1,UNDL50,UNDL50,UNDL10,UNDL13
          GOTO      REPT1000
.
REPT9000  PRINT     " ",*N:
                    "NOTE: (i) The report will print in Date Planned Effective":                    " date order":
                    *N,*N,"*** End of Report ***",*N
          CALL      KILL0000                      * erase temp file
.
REPT9999  RETURN
+
.*********************************************************************
.*  FDAT0000  :  Format data for printing                            *
.*********************************************************************
FDAT0000  PACK      WPDESC,SP30,SP30,SP10
          PACK      KEY9,XXPROC
          CALL      RDPROA1
          MOVE      WPDESC,DESC44
.
          UNPACK    XXDLIST,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,PRDLIST
.
          UNPACK    XXPLANFR,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,PRPLAFR
.
          UNPACK    XXDNAB,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,PRDNAB
.
FDAT9999  RETURN
+
.*********************************************************************
.*  PCOM0000  :  Print W/L comments                                  *
.*********************************************************************
PCOM0000  PACK      KEY21,XXURNO,XXPROC,XXPCNT,SP3
          CALL      RDSMESA1
PCOM1000  CALL      RDKMESA1
          BRANCH    OVRCD,PCOM9999
.
          PACK      KEY19,WAURNO,WAPROC,WACNT
          MATCH     KEY19,KEY21
          GOTO      PCOM9999 IF NOT EQUAL
.
          PRINT     *1,"|",*32,"|",*39,"| ",WATEXT,*110,"|"
          ADD       ONE,CLNO
          GOTO      PCOM1000
.
PCOM9999  RETURN
+
.*********************************************************************
.         Print the page header
.*********************************************************************
HEAD0000  CALL      HEAD132
          PRINT     *40,"Consultant: ",DOCCODE,SP3,DOCNAME:
                    *N,*40,"As at date: ",PRASATD,*N
.
          PRINT     *1,UNDL50,UNDL50,UNDL10,UNDL13
          PRINT     *1,"| U/R No.  Surname",*32,"| List | Procedure ":
                    "Description",*87,"| Date",*98,"| Date Plan",*110,"| ":
                    "Do Not Adm |":
                    *N,"|          Given Name",*32,"| Type | Comments":
                    *87,"| on List",*98,"| Effect.",*110,"|   Before   |"
          PRINT     *1,UNDL50,UNDL50,UNDL10,UNDL13
                   
          MOVE      "8",CLNO
HEAD9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temporary file                                    *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*  CRET0000  :  create temporary file                                   *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 85 U69-76,9-16,17-25,26-27",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TFILNAME
CRET9999  RETURN
+
.*************************************************************************
.*  Temp file IO's                                                       *
.*************************************************************************
RSTEMP1   RESET     KEY27
          READ      TEMPFILE,KEY27;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXDLIST,XXURNO,XXPROC,XXPCNT,XXGNAME:
                             XXSNAME,XXLTYPE,XXPLANFR,XXDNAB
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY27
          WRITE     TEMPFILE,KEY27;XXDLIST,XXURNO,XXPROC,XXPCNT,XXGNAME:
                             XXSNAME,XXLTYPE,XXPLANFR,XXDNAB
          RETURN
.
.
          INC       STD001IO                * standard routine
          INC       BOKRC1IO/INC
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * codes file
          INC       PATMA1IO/INC            * codes file
          INC       PATMI1IO/INC            * codes file
          INC       PMSVX1IO/INC            * codes file
          INC       PATDSCIO/INC            * codes file
          INC       WATPROIO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WATTR1IO/INC
          INC       WATMESIO/INC
          INC       WATCATIO/INC
          INC       PATDOCKY
          INC       CHKSTAT
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC

.*****************************************************************************
.*    Program      : IBAMEH60                                                *
.*    Description  : MH Statistic by Department                              *
.*                                                                           *
.*    Author       : ROD    (15/01/92)                                       *
.*    Function     : List stats generated by stat update program by Dept.    *
.*                                                                           *
.*    Modifications:                                                         *
.*       V10.05.01  01/01/2015  Ania P                      CAR 261630       *
.*                  Removed use of PORT for temp file naming                 *
.*        V9.02.00  26/10/2002  Lina Vo     SRF 22733                        *
.*                  Ported to V9                                             *
.*****************************************************************************
.*        V5.03.01  21/11/95  Whirlpool   srf 640988                         *
.*                  Fixed to use category "A " instead of "AC"               *
.*                  for department as per rest of system !                   *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATDEPFD/INC
          INC       PATDRGFD/INC
          INC       MEHDPSFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
AVELOS    FORM      4.1
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       80
CPTDATE   DIM       8
CURRDATE  DIM       8
DAYSINP   FORM      2
ERRMESS   INIT      ":Missing code"
FNAME     DIM       8
GRNABEDS  FORM      4
GRNAVLOS  FORM      4.1
GRNBDSL   FORM      7
GRNBDXSL  FORM      7
GRNCADM   FORM      6
GRNDEAD   FORM      6
GRNDSCH   FORM      6
GRNIADM   FORM      6
GRNLVE    FORM      6
GRNOISL   FORM      3.2
GRNOXSL   FORM      3.2
GRNRETS   FORM      6
GRNSADM   FORM      6
GRNTOOUT  FORM      6
GRNTOTIN  FORM      6
GRNTRANO  FORM      6
NEWWARDC  DIM       3
OCCINSL   FORM      3.2
OCCEXSL   FORM      3.2
PRTFDTE   DIM       10
PRTTDTE   DIM       10
REQPER    DIM       6
SEPBDAYS  FORM      8
TMPAVLOS  FORM      4.1
TMPISL    FORM      3.2
TMPXSL    FORM      3.2
TOTALIN   FORM      6
TOTALOUT  FORM      6
TOTBEXSL  FORM      7
TOTBINSL  FORM      7
TOTLVE    FORM      6
TOTRETS   FORM      6
TOTSEPBD  FORM      8
TRANSOUT  FORM      6
.
PRGID     INIT      "IBAMEH60"
PRGNAM    INIT      "Mh Statistics By Department"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      GPER0000                      * Get Period
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000    * Yes, No, Cancel
.
ML5000    CALL      RPRT0000                      * Print Report
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"patdepaf"
          OPEN      PATDEPA1,"patdepaf"
.
          DISPLAY   *P60:24,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"mehdpsaf"
          OPEN      MEHDPSA1,"mehdpsaf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAME
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Department"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.*  GPER0000  :  Get Period for report                                      *
.****************************************************************************
GPER0000  DISPLAY   *P1:9,"Enter the report period :"
.
. Set up KEYPER parametres and default period
.
GPER0050  DISPLAY   *P27:9,*EF
          MOVE      NINE,CVERT
          MOVE      TWENTY7,CCOL
          MOVE      TWENTY4,EVERT
          MOVE      ONE,ECOL 
          MOVE      ZERO,CHIGHLT   
          
.         PACK      CURRDATE,CCC,CYY,CMM,CDD
.         REP       " 0",CURRDATE
.         CALL      GPERD                         * get default period
.         BRANCH    EXIT,GPER0100
.
.         PACK      KEY6,DRGCYR,DRGCNUM
.         CALL      RDSDRGA3
.         CALL      RDPDRGA3
.         BRANCH    OVRCD,GPER0100
.
.         PACK      REQPER,DRGCYR,DRGCNUM
          UNPACK    SP6,CCENT,CYEAR,CMON
          MOVE      CCC,CCENT
          CALL      KEYPER                        * keyin the period
          BRANCH    CQUEST,GPER0050
          BRANCH    OVRCD,GPER9000
          GOTO      GPER1000
.
. period not on file error
.
GPER0100  DISPLAY   *P1:24,*V2LON,"Period not on file.  ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
          GOTO      GPER9999
.
GPER1000  PACK      KEY6,CCENT,CYEAR,CMON
          CALL      RDDRGA3                      * valid REQPER?
          BRANCH    OVRCD,GPER3000
          PACK      REQPER,DRGYR,DRGNUM
.
. check if period keyed in is less than current period
.
          REP       " 0",DRGTDTE
          MATCH     CURRDATE,DRGTDTE
          GOTO      GPER1500 IF LESS              * Ok.
          GOTO      GPER8000                      * error
.
. set up to and from dates to print
.
GPER1500  UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTFDTE
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTTDTE
          CALL      DINP0000                      * get no. days in period
.
. display long desciption and from and to dates
.
          DISPLAY   *P38:9,*V2LON,DRGLDSC:
                    *P38:10,PRTFDTE," to ",PRTTDTE
          MOVE      ZERO,EXIT
          GOTO      GPER9999
.
GPER3000  DISPLAY   *P1:24,*B,*V2LON,"Invalid Period.  ";
          CALL      HITENTER
          GOTO      GPER0050
.
. date keyed in was >= current period
.
GPER8000  DISPLAY   *P1:24,*EL,*B,"Must be less than current period.  ";
          CALL      HITENTER
          GOTO      GPER0050
.
. no REQPER entered or cancel so exit = true
.
GPER9000  MOVE      TRUE,EXIT
.
GPER9999  RETURN          
+
.***************************************************************************
.*  DINP0000  :  Calculate no. days in period                              *
.***************************************************************************
DINP0000  MOVE      DRGFDTE,CDYSFDTE
          MOVE      DRGTDTE,CDYSTDTE
.
          CALL      CALCDAYS
.
          MOVE      CDYSDAYS,DAYSINP
.
DINP9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  DISPLAY   *P30:24,"Printing...";
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      TEN,CLNO                      * line counter
          MOVE      "~~~",NEWWARDC                * init to non blank
          CALL      IDAT0000                      * init data
.
          CALL      HEAD132                       * Print 3 line header
          PRINT     *40,"Period : ",DRGNUM,"/",DRGYR,SP2,"(",PRTFDTE," to ":
                    PRTTDTE,")",*N
          CALL      UND132
          CALL      PTHD0000                      * print table headings
          CALL      UND132
.
. Now loop thru temp file and print data
.
          PACK      KEY9,REQPER,SP10
          CALL      RSMHDPS1
RPRT1000  CALL      RKMHDPS1
          BRANCH    OVRCD,RPRT8000                * no more records
.
          MATCH     REQPER,MHDPDATE               * still same period?
          GOTO      RPRT8000 IF NOT EQUAL
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT2000 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          PRINT     *40,"Period : ",DRGNUM,"/",DRGYR,SP2,"(",PRTFDTE," to ":
                    PRTTDTE,")",*N
          CALL      UND132
          CALL      PTHD0000                      * print table headings
          CALL      UND132
          MOVE      TEN,CLNO
.
. get details for printing
.
RPRT2000  PACK      TDESC,MHDPDEPT,ERRMESS         * default error desc
          PACK      KEY5,ANSA,SP1,MHDPDEPT        * get dept desc
          CALL      RDCODE1
.
          ASSIGN    (MHDPRETH + MHDPRETT + MHDPRETA),TOTRETS
          ASSIGN    (MHDPADMI + MHDPADMS + MHDPADMC + TOTRETS),TOTALIN
          ASSIGN    (MHDPLVEH + MHDPLVET + MHDPLVEA),TOTLVE
          ASSIGN    (TOTLVE + MHDPDSCH + MHDPDEAD),TOTALOUT
          ASSIGN    (MHDPINBD+MHDPHLBD+MHDPTLBD+MHDPAWBD+MHDPSLBD),TOTBINSL
          ASSIGN    (MHDPINBD+MHDPHLBD+MHDPTLBD+MHDPAWBD),TOTBEXSL
          MOVE      ZERO,OCCINSL
          MOVE      ZERO,OCCEXSL
          IF        !((MHDPBEDS*DAYSINP) = 0)
            ASSIGN    (TOTBINSL/(MHDPBEDS*DAYSINP))*100,OCCINSL
            ASSIGN    (TOTBEXSL/(MHDPBEDS*DAYSINP))*100,OCCEXSL
          ENDIF
          MOVE      ZERO,AVELOS
          IF        !((TOTALOUT+MHDPTRNO) = 0)
            ASSIGN    (MHDPSEPB+MHDPSSLB)/(TOTALOUT+MHDPTRNO),AVELOS
          ENDIF
.
. add up data for GRAND totals
.
          ADD       MHDPBEDS,GRNABEDS             * avail beds grand total
          ADD       MHDPADMI,GRNIADM              * inf admiss. grand total
          ADD       MHDPADMS,GRNSADM              * special admiss. grand total
          ADD       MHDPADMC,GRNCADM              * committed admiss. grand tot
          ADD       TOTRETS,GRNRETS               * returns grand total
          ADD       TOTALIN,GRNTOTIN              * total in grand total
          ADD       TOTLVE,GRNLVE                 * leave grand total
          ADD       MHDPDSCH,GRNDSCH              * discharges grand total
          ADD       MHDPDEAD,GRNDEAD              * deaths grand total
          ADD       TOTALOUT,GRNTOOUT             * total out grand total
          ADD       TOTBINSL,GRNBDSL              * bed days inc S/L grand total
          ADD       TOTBEXSL,GRNBDXSL             * bed days exc S/L grand total
          ASSIGN    (TOTSEPBD+MHDPSEPB+MHDPSSLB),TOTSEPBD
          ADD       MHDPTRNO,GRNTRANO             * grand total transfers out
.
. print details
.
          PRINT     *1,"| ",TDESC,*24,"|",MHDPBEDS,*30,"|",MHDPADMI,"|":
                    MHDPADMS,"|",MHDPADMC,"|",TOTRETS,"|",TOTALIN,"|",TOTLVE:
                    "|",MHDPDSCH,"|",MHDPDEAD,"|",TOTALOUT,"|",TOTBINSL:
                    "|",TOTBEXSL,"|",OCCINSL," |",OCCEXSL," |",AVELOS,"|"
.
          ADD       ONE,CLNO
          GOTO      RPRT1000
.
. we have finished printing data so print end of report details
.
RPRT8000  CALL      PEQL0000                      * print =========='s
          CALL      PGRN0000                      * print grand totals
          CALL      PEQL0000                      * print =========='s
          CALL      PSUM0000                      * print summary details
.
RPRT9999  RETURN
+
.**************************************************************************
.*  PEQL0000  :  Print a 132 line of equal signs                          *
.**************************************************************************
PEQL0000  PRINT     *1,"*================================":
                       "=================================":
                       "=================================":
                       "================================*"
.
PEQL9999  RETURN
+
.**************************************************************************
.*  PGRN0000  :  Print GRAND totals                                       *
.**************************************************************************
PGRN0000
.
. calc % fields
. do the following check because if div by 0 '999.99' appears in field
.
          MOVE      ZERO,GRNOISL
          MOVE      ZERO,GRNOXSL
          IF        !((GRNABEDS*DAYSINP) = 0)
            ASSIGN    (GRNBDSL/(GRNABEDS*DAYSINP)*100),GRNOISL  * Occu % Inc SL
            ASSIGN    (GRNBDXSL/(GRNABEDS*DAYSINP)*100),GRNOXSL * Occu % Exc SL
          ENDIF
.
. calc Ave LOS (grand total)
.
          MOVE      ZERO,GRNAVLOS
          IF        !((GRNTOOUT+GRNTRANO) = 0)
            ASSIGN    (TOTSEPBD/(GRNTOOUT+GRNTRANO)),GRNAVLOS    * Ave LOS
          ENDIF
.
. print Grand totals
.
          PRINT     *1,"| GRAND TOTALS",*24,"|",GRNABEDS,*30,"|",GRNIADM,"|":
                    GRNSADM,"|",GRNCADM,"|",GRNRETS,"|",GRNTOTIN,"|",GRNLVE:
                    "|",GRNDSCH,"|",GRNDEAD,"|",GRNTOOUT,"|",GRNBDSL:
                    "|",GRNBDXSL,"|",GRNOISL," |",GRNOXSL," |",GRNAVLOS,"|"
.
PGRN9999  RETURN
+
.**************************************************************************
.*  PSUM0000  :  Print Summary details                                    *
.**************************************************************************
PSUM0000  PRINT     *N,*N,*17,"Bed Days   *   100.00",*74,"Separation Days ":
                    "(including Short Leave)":
                    *N,*1,"Occupancy %  =  ---------------------------------":
                    *57,"Average Stay  =  ---------------------------------":
                    "------":
                    *N,*17,"Available Beds * # Days in Period",*82,"Number ":
                    "of Separations":
                    *N,*N,*1,"Separations = Discharges + Deaths + Holiday ":
                    "Leave + Trial Leave + AWOL + Transfers Out"
.
          PRINT     *N,"***  End of Report ***"
.
PSUM9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
PTHD0000  PRINT     *1,"|",*24,"|Avail|---- Admissions ----|Replce| Total|":
                    "    To| Disch|      | Total|Pat Day|Pat Day|Occup %":
                    "|Occup %| Aver |",*N:
                    *1,"| Department",*24,"| Beds|   Inf|  Spec|Commit| Leave|":
                    "    In| Leave|x Dths|Deaths|   Out|Inc S/L|Exc S/L|":
                    "Inc S/L|Exc S/L| Stay |"
.
PTHD9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  initialise vars                                         *
.*************************************************************************
IDAT0000  MOVE      ZERO,GRNABEDS                 * avail beds grand total
          MOVE      ZERO,GRNIADM                  * inf admiss. grand total
          MOVE      ZERO,GRNSADM                  * special admiss. grand total
          MOVE      ZERO,GRNCADM                  * committed admiss. grand tot
          MOVE      ZERO,GRNRETS                  * returns grand total
          MOVE      ZERO,GRNTOTIN                 * total in grand total
          MOVE      ZERO,GRNLVE                   * leave grand total
          MOVE      ZERO,GRNDSCH                  * discharges grand total
          MOVE      ZERO,GRNDEAD                  * deaths grand total
          MOVE      ZERO,GRNTOOUT                 * total out grand total
          MOVE      ZERO,GRNBDSL                  * bed days inc S/L grand total
          MOVE      ZERO,GRNBDXSL                 * bed days exc S/L grand total
          MOVE      ZERO,TOTSEPBD                 * total separation bed days
          MOVE      ZERO,GRNTRANO                 * total transfers out
.
IDAT9999  RETURN
.
.
          INC       STD001IO
          INC       CALCDAYS
          INC       PATCOMN3
          INC       PATCODIO/INC
          INC       PATDRGIO/INC
          INC       PATDEPIO/INC
          INC       MEHDPSIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.

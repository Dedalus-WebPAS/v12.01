.*****************************************************************************
.*    Program      : IBAMEH57                                                *
.*    Description  : Patients Movement Report                                *
.*                                                                           *
.*    Author       : ROD    (16/12/91)                                       *
.*    Function     : Report ALL MH patient movements for a given date.       *
.*    Modifications:                                                         *
.*****************************************************************************
.*        V12.00.01 30/05/2025 Jacob Jackson     TSK 0955096                 *
.*                  Alphanumeric changes                                     *
.*        V10.05.01 01/01/2015  Ania P                      CAR 261630       *
.*                  Removed use of PORT for temp file naming                 *
.*        V10.01.01 10/02/2011  Jill Parkinson CAR 233088                    *
.*                  Added pmsvx1af                                           *
.*        V9.02.00  26/10/2002  Lina Vo     SRF 22733                        *
.*                  Ported to V9                                             *
.*****************************************************************************
.*        V5.08.02  08/12/2000  Caleb Sharman                                *
.*                  Mods to date display in printout                         *
.*        V5.08.01  28/08/2000  Caleb Sharman                                *
.*                  Changed Health Fund variables to be 8 chars              *
.*                   V5.07.02   12/08/1999  Steve Downing                    * 
.*                              Redundant date calculation routine removed   * 
.*                   V5.07.00   28/10/1998  Davin                            *
.*                              Mods for 507                                 *
.*                   V5.01.002  13/02/92  ROD                                *
.*                              Return to menu if nothing input for date     *
.*                   V5.01.003  23/02/92  Graeme Williams                    *
.*                              Don't start a new page for each section.     *
.*                   V5.01.004  17/06/92  ROD                                *
.*                              Re-arrange code to make report run faster    *
.*                   V5.01.005  24/07/92  ROD                                *
.*                              Use Legal status at current date             *
.*                   V5.01.006  29/07/92  ROD              SRF 116179        *
.*                              Use leave type from "R" tran record          *
.*                   V5.01.007  07/08/92  ROD                                *
.*                              Change to use day file                       *
.*                   V5.01.008  25/05/93  Gabrielle                          *
.*                              Changed to print correct section summary     *
.*                              checked why results are different between    *
.*                              MEH57 & MEH58. Could not see why..MEH57 is   *
.*                              printing less patients                       *
.*        V5.04.01  07/11/1996  Howellsy                                     *
.*                  Added Review Time & Legal Status Time.                   *
.*****************************************************************************
.
          INC       STD001FD
          INC       MEHVI1FD/INC
          INC       MEHLEGFD/INC
          INC       PATTRNFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDAYFD/INC
          INC       PATDSCFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
BJDAY     FORM      3
CJDAY     FORM      3
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       80
CMONDAY   DIM       4
CNTYR     DIM       4
CURRDATE  DIM       8
DAYFILE   DIM       8
DAYS      INIT      " Days"
DRDATE    DIM       10
DSADMD    DIM       8
DSDDAT    DIM       8
ENDDATE   DIM       8
FCNTYR    FORM      4
FNDDATA   FORM      1
LVETMP    DIM       3
NDAYS     FORM      2
NEWWARD   DIM       3
NOPATS    FORM      4
NOPATSD   INIT      "Number of patient"
OLDSECTN  FORM      1
RDATE     DIM       8
REPS1L    INIT      "PATIENTS ADMITTED"
REPS2L    INIT      "PATIENTS TRANSFERRED"
REPS3L    INIT      "PATIENTS GOING ON LEAVE"
REPS4L    INIT      "PATIENTS RETURNING FROM LEAVE"
REPS5L    INIT      "PATIENTS DISCHARGED (EXCLUDING DEATHS)"
REPS6L    INIT      "PATIENT DEATHS"
REPSECT   DIM       38
SAVKEY30  DIM       30
SAVLTYP   DIM       3
SAVWARD   DIM       3
SAVOMON   FORM      2
SECTSUM   DIM       39
STRTCENT  FORM      2
STRTDAYS  FORM      3
STRTLEAP  FORM      1
STRTYR    FORM      2
SUMM1     INIT      "s admitted = "
SUMM2     INIT      "s transferred = "
SUMM3     INIT      "s on leave = "
SUMM4     INIT      "s returning = "
SUMM5     INIT      "s discharged = "
SUMM6     INIT      " deaths = "
SUMMSEC   DIM       16
TMPLVD    DIM       8
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
.
. Temp file vars
.
MEHTMPA1  IFILE     SQL, FIXED=108, KEY="U1-1,2-35,36-43"
DXXSECTN  DIM       1        1     Report Section No. (1-6)
XXNAME    DIM       34       2     Patients Name
XXURNO    DIM       8       36     U/R No.
XXLSC     DIM       3       44     Legal status code
XXSEX     DIM       1       47     Sex
XXAGE     FORM      3       48     Age
XXRELIG   DIM       3       51     Religion
XXADATE   DIM       8       54     Admission date
XXDDATE   DIM       8       62     Discharge date
XXLSTAY   FORM      5       70     Length of stay
XXADOCT   DIM       22      74     Attending doctor
XXWARDT   DIM       3       96     Ward transfer (to)
XXWARDF   DIM       3       99     Ward transfer (from)
XXHOSPT   DIM       3      102     Hospital code (to)
XXHOSPF   DIM       3      105     Hospital code (from)
.             rec length   108
XXSECTN   FORM      1      redefine for key form field
.
PRGID     INIT      "IBAMEH57"
PRGNAM    INIT      "Patient Movements Report"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code   (1 of the all time great mainlines)           *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
          CALL      CRET0000                      * create temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      DATE0000                      * Get Date of movements    
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000    * Yes, No, Cancel
.
ML5000    CALL      LOAD0000                      * load data into temp file
          BRANCH    FNDDATA,ML2000                * no data on file
.
          CALL      RPRT0000                      * Print Report
          CALL      CRET0000                      * erase temp file
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"mehlegaf"
          OPEN      MEHLEGA1,"mehlegaf"
.
          DISPLAY   *P60:24,"mehvi1af"
          OPEN      MEHVI1A1,"mehvi1af"
.
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          CALL      TFILENAM
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Surname"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* DATE0000  :  Get the Report date                                         *
.****************************************************************************
DATE0000  DISPLAY   *P1:24,*EL,*P1:9,"Date of Movements : "
.
DATE1000  MOVE      NINE,CVERT
          MOVE      "21",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP10,CYEAR,CMON,CDAY
          MOVE      ONE,CDEFDTE
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE1000
          BRANCH    OVRCD,DATE9000
.
          PACK      RDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",RDATE
.
          MATCH     CURRDATE,RDATE
          GOTO      DATE8000 IF LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date must be less than current date.  ";
          CALL      HITENTER
          GOTO      DATE1000
.
DATE8000  CALL      PACDATE
          MOVE      CPCDATE,DRDATE                 * display var
.
. now open the day file for the keyed in date
.
          PACK      CMONDAY,CMON,CDAY
          REP       " 0",CMONDAY
        
          PACK      DAYFILE,FILDAYA1,CCENT,CYEAR
          REP       " 0",DAYFILE
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATDAYA1,DAYFILE
          TRAPCLR   IO
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Day file not set up.  ";
            CALL      HITENTER
            DISPLAY   *P1:24,*EL
            GOTO      DATE9000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9000  MOVE      ONE,EXIT
.
DATE9999  RETURN
+
.****************************************************************************
.*  LOAD0000  :  Load data into temp file                                   *
.****************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading ... [        ]";
.
          MOVE      ONE,FNDDATA                   * assume no data found
          PACK      KEY12,CMONDAY,SP20
          CALL      RSPTDAY1
LOAD0500  CALL      RKPTDAY1
          BRANCH    OVRCD,LOAD9500
.
          MATCH     PTDYDATE,CMONDAY              * same date?
          GOTO      LOAD9500 IF NOT EQUAL
.
          PACK      KEY8,PTDYADMN                 * MH patient
          CALL      RDMHVIS1
          BRANCH    OVRCD,LOAD0500
.
          DISPLAY   *P43:24,MHVIADMN              * display on screen
.
. get Admission and Master details
.
          PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD0500
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD0500
.
          MOVE      ZERO,XXSECTN                   * initialise section no.
LOAD0600  ADD       ONE,XXSECTN                    * increment section no.
.
        WHILE     XXSECTN < 7
.
          CALL      IDAT0000                      * init temp vars
.
. now loop thru transfer file (check movement type, date & get LOS & Wards)
.
          PACK      KEY30,MHVIADMN,SP30
          CALL      RDSTRAN2
LOAD0700  CALL      RDKTRAN2
          BRANCH    OVRCD,LOAD0600
.
          MATCH     MHVIADMN,TADMN                * same admission no.?
          GOTO      LOAD0600 IF NOT EQUAL
.
          MATCH     RDATE,TDATE                   * same date?
          GOTO      LOAD0700 IF NOT EQUAL
.
.
. *** check if Report No. 1 - Patients Admitted ***
.
.
          MOVE      SP3,XXHOSPF
          IF        XXSECTN = 1
            MATCH    ANSA,TMOVE                   * Admission?
            GOTO     LOAD0700 IF NOT EQUAL
.
            MOVE     TWARD,XXWARDT                * ward to
            MOVE     SP3,XXWARDF                  * ward from
            MOVE     AUSR2,XXHOSPF                * hosp from
            MOVE     SP3,XXHOSPT                  * hosp to
            MOVE     ZERO,XXLSTAY                 * length of stay
            GOTO     LOAD8000                     * go and set up data
          ENDIF    * {Report No. 1}
.
.
. *** check if Report No. 2 - Patients Transferred ***
.
.
          IF        XXSECTN = 2
            MATCH    ANSC,TMOVE                   * Change record?
            GOTO     LOAD0700 IF NOT EQUAL
.
.         now get previous ward patient was in
.
            PACK     SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
            CALL     RDPTRAN2
            IF       OVRCD = 1
              GOTO     LOAD0500
            ELSE
              MATCH    MHVIADMN,TADMN             * same admission no.?
              GOTO     LOAD0500 IF NOT EQUAL
.
              MOVE     TWARD,SAVWARD              * save ward
            ENDIF
.
.         now get last change record on specified date and check if ward changed
.
            MOVE     SAVKEY30,KEY30               * restore 1st change record
            CALL     RDTRAN2
.
            MOVE     TWARD,NEWWARD                * save the new ward
.
LOAD0800    CALL     RDKTRAN2
            BRANCH   OVRCD,LOAD0850
.
            MATCH    MHVIADMN,TADMN               * same admission no.?
            GOTO     LOAD0850 IF NOT EQUAL
.
            MATCH    RDATE,TDATE                  * same date?
            GOTO     LOAD0850 IF NOT EQUAL
.
            MATCH    ANSC,TMOVE                   * Change record
            GOTO     LOAD0800 IF NOT EQUAL
.
            MOVE     TWARD,NEWWARD                * save the new ward
            GOTO     LOAD0800                     * try for another change rec
.
LOAD0850    MATCH    SAVWARD,NEWWARD              * different wards?
            GOTO     LOAD0500 IF EQUAL
.
            MOVE     SAVWARD,XXWARDF              * ward from
            MOVE     NEWWARD,XXWARDT              * ward to
.
            MOVE     SAVKEY30,KEY30               * restore 1st change record
            CALL     RDTRAN2
            CALL     LSTY0000                     * get length of stay
.
            GOTO     LOAD8000                     * go and set up data
          ENDIF    * {Report No. 2}
.
.
. *** check if Report No. 3 - Patients Going on Leave ***
.
.
          IF        XXSECTN = 3
            MATCH    ANSL,TMOVE                   * Leave record?
            GOTO     LOAD0700 IF NOT EQUAL
.
            CALL     LSTY0000                     * get length of stay
.
            MOVE     TWARD,XXWARDF                * ward from
            MOVE     SP3,XXWARDT                  * ward to
.
            PACK     SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
            CALL     RDKTRAN2                     * get next rec ('R' return)
            BRANCH   OVRCD,LOAD8000
.
            MATCH    MHVIADMN,TADMN               * same admission no.?
            GOTO     LOAD8000 IF NOT EQUAL
.
            MATCH    RDATE,TDATE                  * returned on same date?
            GOTO     LOAD8000 IF NOT EQUAL        * ignore
.
            MOVE     SAVKEY30,KEY30               * restore record
            CALL     RDTRAN2
.
            GOTO     LOAD0700                     * go and set up data
          ENDIF    * {Report No. 3}
.
.
. *** check if Report No. 4 - Patients Returning from Leave ***
.
.
          IF        XXSECTN = 4
            MATCH    ANSR,TMOVE                   * Return from leave record?
            GOTO     LOAD0700 IF NOT EQUAL
.
            CALL     LSTY0000                     * get length of stay
.
            MOVE     TWARD,XXWARDT                * ward to
            MOVE     SP3,XXWARDF                  * ward from
.
            PACK     SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED    * save key
            CALL     RDPTRAN2                     * get previous rec ('L' Leave)
            BRANCH   OVRCD,LOAD4000
.
            MATCH    MHVIADMN,TADMN               * same admission no.?
            GOTO     LOAD4000 IF NOT EQUAL
.
            MATCH    RDATE,TDATE                  * left for leave on same date?
            GOTO     LOAD4000 IF EQUAL            * ignore
.
            PACK     KEY8,MHVIADMN
            CALL     RDDSCH1
            IF       OVRCD = 0
              MATCH     RDATE,DDATE               * discharged on return date?
              GOTO      LOAD4000 IF EQUAL
            ENDIF
. 
            GOTO     LOAD8000                     * go and set up data
.
LOAD4000    MOVE     SAVKEY30,KEY30               * restore position in file
            CALL     RDTRAN2
.
          ENDIF    * {Report No. 4}
.
.
. *** check if Report No. 5 - Patients Discharged (Excluding Deaths) ***
.
.
          IF        XXSECTN = 5
            MATCH    ANSD,TMOVE                   * Discharge record?
            GOTO     LOAD0700 IF NOT EQUAL
.
            IF       PCEASE = 1
              GOTO     LOAD0700
            ENDIF
.
            MOVE     TWARD,XXWARDF                * ward from
            MOVE     SP3,XXWARDT                  * ward to
            CALL     LSTY0000                     * get length of stay
            GOTO     LOAD8000                     * go and set up data
          ENDIF    * {Report No. 5}
.
.
. *** check if Report No. 6 - Patient Deaths ***
.
.
          IF        XXSECTN = 6
            MATCH    ANSD,TMOVE                   * Discharge record?
            GOTO     LOAD0700 IF NOT EQUAL
.
            IF       PCEASE = 0
              GOTO     LOAD0700
            ENDIF
.
            MOVE     TWARD,XXWARDF                * ward from
            MOVE     SP3,XXWARDT                  * ward to
            CALL     LSTY0000                     * get length of stay
            GOTO     LOAD8000                     * go and set up data
          ENDIF    * {Report No. 6}
.
. we have a valid record so set up data for temp file
.
LOAD8000  MOVE      PURNO,XXURNO
.
          STRIP     PSNAME                        * patients name
          PACK      XXNAME,PSNAME,COMMA,SP1,PGNAME,SP30
          ENDSET    XXNAME
          RESET     XXNAME,29
          APPEND    PTITL,XXNAME
          RESET     XXNAME
.
. Get Legal status at keyed date
.
          MOVE      SP3,XXLSC
          PACK      KEY21,MHVIADMN,ZED20
          CALL      RSMHLEG1
LOAD8200  CALL      RPMHLEG1
          BRANCH    OVRCD,LOAD8500
.
          MATCH     MHVIADMN,MHLEADMN             * same admission no.?
          GOTO      LOAD8500 IF NOT EQUAL
.
          MATCH     MHLEDATE,RDATE                * <= report date?
          GOTO      LOAD8200 IF LESS
.
          MOVE      MHLESTAT,XXLSC                * legal status
.
LOAD8500  MOVE      PSEX,XXSEX                    * sex
          MOVE      PSAGE,XXAGE                   * age
          MOVE      PREG,XXRELIG                  * religion
          MOVE      ADATE,XXADATE                 * admission date
.
.      get discharge date (& DUSD1 = Hospital code to)
.
          MOVE      SP8,DDATE
          MOVE      SP3,DUSD1
          PACK      KEY8,MHVIADMN
          CALL      RDDSCH1
          MOVE      DDATE,XXDDATE                 * discharge date
          MOVE      DUSD1,XXHOSPT                 * hospital code to
.
.      get attending doctor
.
          PACK      XXADOCT,SP20,SP2
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1
          IF        OVRCD=0
                      PACK    XXADOCT,DSNAME,SP20
                      STRIP   XXADOCT
                      ENDSET  XXADOCT
                      APPEND  SP1,XXADOCT
                      MOVE    DGNAME,ANS
                      APPEND  ANS,XXADOCT
                      RESET   XXADOCT
          ENDIF
.
. write to temp file
.
LOAD9000  PACK      KEY43,XXSECTN,XXNAME,XXURNO
          CALL      WRTEMP1
          ADD       ONE,XXSECTN                   * do next section of report
          MOVE      ZERO,FNDDATA                  * we have found some data
          DISPLAY   *P43:24,*V2LON,MHVIADMN       * display on screen
.
        DO   .{end while}
.
          GOTO      LOAD0500                      * get next patient
.
LOAD9500  IF        FNDDATA=1
            DISPLAY   *P1:24,*EL,*B,"No data found.  ";
            CALL      HITENTER
          ENDIF
.
LOAD9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      NINE,OLDSECTN                 * init to dummy figure
          MOVE      "60",CLNO                     * force new page
.
. Now loop thru temp file and print data
.
          PACK      KEY43,SP30,SP20
          CALL      RSTEMP1
RPRT1000  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT7000                * no more records
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT1500 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          PRINT     *40,"Report Date : ",DRDATE,*N
          ADD       TWO,CLNO
          CALL      PTHD0000                      * print table headings
          GOTO      RPRT2000
.
. if we have a new report section print new headings
.
RPRT1500  IF        XXSECTN <> OLDSECTN
            CALL      UND132                      * print summary for this sect
.            LOAD      SUMMSEC,XXSECTN,SUMM1,SUMM2,SUMM3,SUMM4,SUMM5,SUMM6
            LOAD      SUMMSEC,OLDSECTN,SUMM1,SUMM2,SUMM3,SUMM4,SUMM5,SUMM6
            PACK      SECTSUM,NOPATSD,SUMMSEC,NOPATS
            PRINT     *1,SECTSUM
            MOVE      ZERO,NOPATS                 * init no. pats found
            CALL      PTHD0000                    * print table headings
          ENDIF
.
RPRT2000  UNPACK    XXADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,DSADMD                 * display var (Admiss Date)
.
          UNPACK    XXDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,DSDDAT               * display var (Discharge Date)
.
. print details
.
          PRINT     *1,"|",XXURNO,*11,"| ",XXNAME,*47,"| ",XXLSC:
                    *53,"| ",XXSEX," |",XXAGE,"|",XXRELIG,"|",DSADMD,"|":
                    DSDDAT,"|",XXLSTAY,"|",XXADOCT,"| ",XXWARDT,"| ",XXWARDF:
                    "| ",XXHOSPT,"| ",XXHOSPF,"|"
.
          ADD       ONE,CLNO
          ADD       ONE,NOPATS                    * increment no. pats found
          MOVE      XXSECTN,OLDSECTN              * save section of report
          GOTO      RPRT1000
.
. we have finished printing patient data so print end of report details
.
RPRT7000  CALL      UND132
          LOAD      SUMMSEC,XXSECTN,SUMM1,SUMM2,SUMM3,SUMM4,SUMM5,SUMM6
          PACK      SECTSUM,NOPATSD,SUMMSEC,NOPATS
          PRINT     *1,SECTSUM
.
          PRINT     *N,"*** End of Report ***"
.
RPRT9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Headings                                     *
.**************************************************************************
PTHD0000  LOAD      REPSECT,XXSECTN,REPS1L,REPS2L,REPS3L,REPS4L,REPS5L,REPS6L
.
          PRINT     *N,*40,REPSECT
.
          CALL      UND132
          PRINT     *1,"|",*11,"|",*47,"|Legal|   |   |   |",*74,"| Trans/":
                    *83,"|",*89,"|",*112,"|Ward|Ward|Hosp|Hosp|":
                    *N,*1,"| U/R No. | Patient Name",*42,"Title":
                    *47,"|Stat.|Sex|Age|Rel|":
                    "Adm Date|Dsc Date| LOS |Attending",*112,"| To |From|":
                    " To |From|"
          CALL      UND132
          ADD       FOUR,CLNO
.
PTHD9999  RETURN
+
.**************************************************************************
.*  LSTY0000  :  Calculate Length of Stay  (include Short Term Leave)     *
.**************************************************************************
LSTY0000  PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED    * save key
          MOVE      ZERO,XXLSTAY                  * init length of stay
.
. find last admission date before specified date
.
          PACK      KEY30,MHVIADMN,ZED20,ZED20
          CALL      RDSTRAN2
LSTY1000  CALL      RDPTRAN2
          BRANCH    OVRCD,LSTY9000
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LSTY9000 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE                    * admission?
          GOTO      LSTY1000 IF NOT EQUAL
.
          MATCH     TDATE,RDATE                   * must be <= specified date
          GOTO      LSTY1000 IF LESS
.
          PACK      CDYSFDTE,TDATE                * set up FROM date
          REP       " 0",CDYSFDTE
.
. now get TO date
.
LSTY2000  CALL      RDKTRAN2                      * read next record
          BRANCH    OVRCD,LSTY6000                * no more records
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LSTY6000 IF NOT EQUAL
.
          MATCH     ANSC,TMOVE                    * Change record?
          GOTO      LSTY2000 IF EQUAL             * ignore
.
          MATCH     RDATE,TDATE                   * >= specified date?
          GOTO      LSTY6000 IF NOT LESS
.
          MATCH     ANSD,TMOVE                    * Discharge?
          GOTO      LSTY5500 IF EQUAL
.
          MATCH     ANSL,TMOVE                    * Leave record?
          GOTO      LSTY4000 IF NOT EQUAL
.
. we have a leave record so get type of leave (ignore Short Term Leave)
.
          MOVE      PTTRLTYP,SAVLTYP              * use lve type from "L" rec
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED    * save key
          CALL      RDKTRAN2                      * get "R" record
          BRANCH    OVRCD,LSTY2500
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LSTY2500 IF NOT EQUAL
.
          MOVE      PTTRLTYP,SAVLTYP              * use lve type from "R" rec
.
LSTY2500  PACK      KEY5,ANST,ANSL,SAVLTYP        * check if Short Term leave
          CALL      RDCODE1
          BRANCH    OVRCD,LSTY2000
          MATCH     SP1,TCINDC1                   * Short Term Leave?
          GOTO      LSTY2000 IF EQUAL
.
          CALL      RDTRAN2                       * restore key
.
. patient is on Long Term Leave so calc days between FROM (Admiss or last
. Return) date and Leave date
. 
LSTY3000  PACK      CDYSTDTE,TDATE                * Leave date = TO date
          REP       " 0",CDYSTDTE
          MATCH     CDYSFDTE,CDYSTDTE             * To date = From date
          GOTO      LSTY2000 IF EQUAL
          CALL      CALCDAYS
          ADD       CDYSDAYS,XXLSTAY              * increment length of stay
          SUB       TWO,XXLSTAY                   * not inclusive
          GOTO      LSTY2000
.
. Return from Leave record
. we have a Return from Long Term Leave
.
LSTY4000  PACK      CDYSFDTE,TDATE                * Return date = FROM date
          REP       " 0",CDYSFDTE
          GOTO      LSTY2000
.
. patient has been discharged
.
LSTY5500  PACK      CDYSTDTE,TDATE                * Discharge date = TO date
          REP       " 0",CDYSTDTE
          CALL      CALCDAYS
          ADD       CDYSDAYS,XXLSTAY              * Length of stay
          SUB       ONE,XXLSTAY                   * not inclusive
          GOTO      LSTY9000
.
. patient has not been discharged so use specified date as the TO date
.
LSTY6000  MOVE      RDATE,CDYSTDTE                * to date
          CALL      CALCDAYS
          ADD       CDYSDAYS,XXLSTAY              * Length of stay
          SUB       ONE,XXLSTAY                   * not inclusive
.
. restore key
.
LSTY9000  MOVE      SAVKEY30,KEY30
          CALL      RDTRAN2
.
LSTY9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  initialise vars                                         *
.*************************************************************************
IDAT0000  UNPACK    SP70,XXNAME,XXURNO,XXLSC,XXSEX,XXRELIG,XXADATE,XXDDATE
          UNPACK    SP70,XXADOCT,XXWARDT,XXWARDF,XXHOSPF,XXHOSPT
          MOVE      ZERO,XXAGE
          MOVE      ZERO,XXLSTAY
.
IDAT9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 108 U1-1,2-35,36-43",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,TFILNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RSTEMP1   RESET     KEY43
          READ      MEHTMPA1,KEY43;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;DXXSECTN,XXNAME,XXURNO,XXLSC,XXSEX,XXAGE,XXRELIG:
                             XXADATE,XXDDATE,XXLSTAY,XXADOCT,XXWARDT,XXWARDF:
                             XXHOSPT,XXHOSPF
          GOTO      OVERCOND IF OVER
          MOVE      DXXSECTN,XXSECTN
          RETURN
.
WRTEMP1   RESET     KEY43
          MOVE      XXSECTN,DXXSECTN
          WRITE     MEHTMPA1,KEY43;DXXSECTN,XXNAME,XXURNO,XXLSC,XXSEX,XXAGE:
                             XXRELIG,XXADATE,XXDDATE,XXLSTAY,XXADOCT,XXWARDT:
                             XXWARDF,XXHOSPT,XXHOSPF
          RETURN
.
.
          INC       STD001IO
          INC       MEHVI1IO/INC
          INC       MEHLEGIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATTRNIO/INC
          INC       PATDAYIO/INC
          INC       CALCDAYS
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

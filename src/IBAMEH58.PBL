.*****************************************************************************
.*    Program      : IBAMEH58                                                *
.*    Description  : HSS PRS924 - 24 HOUR REPORT                             *
.*                                                                           *
.*    Author       : ROD    (17/12/91)                                       *
.*    Function     : List ALL MH admissions,going on leave,returns from      *
.*                   leave,discharges,deaths and changes of legal status     *
.*                   for a day.                                              *
.*                                                                           *
.*    Modifications:                                                         *
.*       V12.00.01  16/05/2025  J.Tan          TSK 0955096                   *
.*                  Added alpanumeric visit number generation                *
.*       V10.05.01  01/01/2015  Ania P                      CAR 261630       *
.*                  Removed use of PORT for temp file naming                 *
.*       V10.01.01  10/02/2011  Jill Parkinson CAR 233088                    *
.*                  Added pmsvx1af                                           *
.*        V9.02.00  26/10/2002  Lina Vo     SRF 22733                        *
.*                  Ported to V9                                             *
.*****************************************************************************
.*        V5.08.01  23/08/2000  Caleb Sharman                                *
.*                  Changed Health Fund table variables to 8 chars           *
.*        V5.08.B01 30/12/1999  Steve Armstrong                              *
.*                  Fixed global recompile bugs                              *
.*****************************************************************************
.*        V5.07.02  24/08/1999  Steve Downing                                *
.*                  Changed error message for date input                     *
.*        V5.07.01  12/08/1999  Steve Downing                                *
.*                  Redundant date calculation routine removed               *
.*        V5.07.00  28/10/1998  Davin                                        *
.*                  Mods for 507                                             *
.*        V5.04.02  10/01/1997  Steve Armstrong  MEH Changes                 *
.*                  Mods to cater for new CMH MHVISTAT statuses              *
.*        V5.04.01  07/11/1996  Howellsy                                     *
.*                  Added Review Time & Legal Status Time.                   *
.*****************************************************************************
.*                   V5.01.002  30/06/92  ROD                                *
.*                              Fixed getting a change in legal status/rerote*
.*                   V5.01.003  06/07/92  ROD                                *
.*                              Use Admission date not last trial lve return *
.*                              24/07/92  ROD                                *
.*                              Use legal status at keyed date               *
.*                   V5.01.004  14/01/94  Sandra Barcham                     *
.*                              fix global recompile                         *
.*        V5.01.005 26/05/1994 Ian Rutt                                      *
.*                  Fixed Global Recompile Bugs                              * 
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       MEHLEGFD/INC
          INC       MEHVI1FD/INC
          INC       PATTRNFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDAYFD/INC
          INC       PATDHEAD/INC
          INC       PATDSCFD/INC
          INC       PATONLFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATCALAG/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
BJDAY     FORM      3
CJDAY     FORM      3
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       80
CMONDAY   DIM       4
CNTYR     DIM       4
CURRDATE  DIM       8
DAYFILE   DIM       8
DAYS      INIT      " Days"
DRDATE    DIM       10
DSADATE   DIM       10
DSDDATE   DIM       10
DSLSDAT   DIM       10
DSLVDAT   DIM       10
DSRECDAT  DIM       10
DSRETND   DIM       10
ENDDATE   DIM       8
FCNTYR    FORM      4
LVETMP    DIM       3
NDAYS     FORM      2
NEWWARD   DIM       3
NODAYFIL  FORM      1
NOPATS    FORM      4
NOPATSD   INIT      "Number of patient"
RDATE     DIM       8
REPNO     FORM      1
REPS1L    INIT      "PATIENTS ADMITTED"
REPS2L    INIT      "PATIENTS ON LEAVE"
REPS3L    INIT      "PATIENTS RETURNED FROM LEAVE"
REPS4L    INIT      "PATIENTS DISCHARGED"
REPS5L    INIT      "PATIENTS DIED"
REPS6L    INIT      "CHANGE OF LEGAL STATUS"
REPSECT   DIM       38
SAVKEY30  DIM       30
SAVWARD   DIM       3
SAVOMON   FORM      2
SAVTDATE  DIM       8
SECTSUM   DIM       39
STRTCENT  FORM      2
STRTDAYS  FORM      3
STRTLEAP  FORM      1
STRTYR    FORM      2
SUMM1     INIT      "s admitted = "
SUMM2     INIT      "s transferred = "
SUMM3     INIT      "s on leave = "
SUMM4     INIT      "s returning = "
SUMM5     INIT      "s discharged = "
SUMM6     INIT      " deaths = "
SUMMSEC   DIM       16
TMPLVD    DIM       8
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
.
. Temp file vars
.
MEHTMPA1  IFILE     SQL, FIXED=109, KEY="U1-8,107-108"
XXURNO    DIM       8        1     U/R No.
XXNAME    DIM       34       9     Patients Name
XXLSC     DIM       2       43     Legal status code
XXSEX     DIM       1       45     Sex
XXAGE     FORM      3       46     Age
XXADATE   DIM       8       49     Admission date
XXDDATE   DIM       8       57     Discharge date
XXHOSPT   DIM       3       65     Hospital code (to)
XXHOSPF   DIM       3       68     Hospital code (from)
XXLVETYP  DIM       4       71     Leave type
XXLVEDT   DIM       6       75     Leave date
XXLSBEF   DIM       2       81     Legal Status Before
XXRECD    DIM       8       83     Reception date
XXRETN    DIM       8       91     Expected return (Review date)
XXDATLSC  DIM       8       99     Date of legal status change
DXXUNIQ   DIM       2       107
.             rec length    109
XXUNIQ    FORM      2
.
.
PRGID     INIT      "IBAMEH58"
PRGNAM    INIT      "Hss Prs924 - 24 Hour Report"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code   (1 of the all time great mainlines)           *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
          CALL      CRET0000                      * create temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      DATE0000                      * Get Date of movements    
          BRANCH    EXIT,ML1000                   * Exit selected
.
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000    * Yes, No, Cancel
.
ML5000    CALL      RPRT0000                      * Print Report
          CALL      CRET0000                      * erase temp file
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"mehlegaf"
          OPEN      MEHLEGA1,"mehlegaf"
          OPEN      MEHLEGA2,"mehlegaf"
.
          DISPLAY   *P60:24,"mehvi1af"
          OPEN      MEHVI1A1,"mehvi1af"
          OPEN      MEHVI1A2,"mehvi1af"
.
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P60:24,"patonlvf"
          OPEN      PATONLV1,"patonlvf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          CALL      TFILENAM 
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By U/R Number"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* DATE0000  :  Get the Report date                                         *
.****************************************************************************
DATE0000  DISPLAY   *P1:24,*EL,*P1:9,"Enter the report date : "
.
DATE1000  MOVE      NINE,CVERT
          MOVE      "25",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP10,CYEAR,CMON,CDAY
          MOVE      ONE,CDEFDTE
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE1000
          BRANCH    OVRCD,DATE9000
.
          PACK      RDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",RDATE
.
          MATCH     CURRDATE,RDATE
          GOTO      DATE8000 IF LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date must be before today's date.  ";
          CALL      HITENTER
          GOTO      DATE1000
.
DATE8000  CALL      PACDATE
          MOVE      CPCDATE,DRDATE                 * display var
.
. open day file
.
          PACK      DAYFILE,FILDAYA1,CCENT,CYEAR
          REP       " 0",DAYFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATDAYA1,DAYFILE
          TRAPCLR   IO
          PACK      CMONDAY,CMON,CDAY
          REP       " 0",CMONDAY
          MOVE      OVRCD,NODAYFIL
.
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9000  MOVE      ONE,EXIT
.
DATE9999  RETURN
+
.****************************************************************************
.*  ADMS0000  :  Get all admissions on specified date                       *
.****************************************************************************
ADMS0000  PACK      KEY16,RDATE,SP20
          CALL      RDSMISS3
ADMS1000  CALL      RDKMISS3
          BRANCH    OVRCD,ADMS9999
.
          DISPLAY   *P43:24,AADMNO;
.
          MATCH     RDATE,ADATE                   * same date?
          GOTO      ADMS9999 IF NOT EQUAL
.
          PACK      KEY8,AADMNO                   * check if MH patient
          CALL      RDMHVIS1
          BRANCH    OVRCD,ADMS1000
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,ADMS1000
.
          CALL      IDAT0000                      * init temp variables
.
          MOVE      SP4,THCSCOD
          PACK      KEY5,ANSU,FOUR,AUSR4
          CALL      RDCODE1
          MOVE      THCSCOD,XXHOSPF               * hosp from
.
          CALL      FRMD0000                      * format data for display
          CALL      WTMP0000                      * write a record to temp file
          GOTO      ADMS1000                      * get next admission
.
ADMS9999  RETURN
+
.****************************************************************************
.*  LEAV0000  :  Get all MH patients who went on leave on specified date    *
.****************************************************************************
LEAV0000  BRANCH    NODAYFIL,LEAV9999             * day file not set up
.
          PACK      KEY12,CMONDAY,SP20
          CALL      RSPTDAY1
LEAV1000  CALL      RKPTDAY1
          BRANCH    OVRCD,LEAV9999
.
          DISPLAY   *P43:24,PTDYADMN;
.
          MATCH     CMONDAY,PTDYDATE              * same date?
          GOTO      LEAV9999 IF NOT EQUAL
.
          PACK      KEY8,PTDYADMN                 * check if MH patient?
          CALL      RDMHVIS1
          BRANCH    OVRCD,LEAV1000
.
          PACK      KEY30,MHVIADMN,RDATE,SP30
          CALL      RDSTRAN2
LEAV2000  CALL      RDKTRAN2
          BRANCH    OVRCD,LEAV1000
.
          MATCH     MHVIADMN,TADMN                * same admission no.?
          GOTO      LEAV1000 IF NOT EQUAL
.
          MATCH     RDATE,TDATE                   * same date?
          GOTO      LEAV1000 IF NOT EQUAL
.
          MATCH     ANSL,TMOVE                    * Leave record?
          GOTO      LEAV2000 IF NOT EQUAL
.
          CALL      IDAT0000                      * init temp variables
.
          MOVE      SP1,TCINDC1                   * get type of leave
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          MATCH     SP1,TCINDC1                   * short term leave?
          GOTO      LEAV1000 IF EQUAL             * ignore
          MOVE      THCSCOD,XXLVETYP              * leave type
          MOVE      TDATE,XXLVEDT                 * leave date
.
          MOVE      SP8,OERDATE                   * get expected return date
          PACK      KEY14,TWARD,TBED,TADMN
          CALL      RDONLV1
          UNPACK    OERDATE,CCENT,CYEAR,CMON,CDAY
          PACK      XXRETN,CCENT,CYEAR,CMON,CDAY
          REP       " 0",XXRETN
.
          CALL      RDKTRAN2                      * get next rec ('R' return)
          BRANCH    OVRCD,LEAV8000
.
          MATCH     MHVIADMN,TADMN                * same admission no.?
          GOTO      LEAV8000 IF NOT EQUAL
.
          MATCH     RDATE,TDATE                   * returned on same date?
          GOTO      LEAV1000 IF EQUAL             * ignore
.
. get Admission and Master details
.
LEAV8000  PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,LEAV1000
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LEAV1000
.
          CALL      FRMD0000                    * format data for display
          CALL      WTMP0000                    * write a record to temp file
          GOTO      LEAV1000                    * get next discharge
.
LEAV9999  RETURN
+
.***************************************************************************
.*  RTNS0000  :  Get all MH returns from leave on specified date           *
.***************************************************************************
RTNS0000  BRANCH    NODAYFIL,RTNS9999             * day file not set up
.
          PACK      KEY12,CMONDAY,SP20
          CALL      RSPTDAY1
RTNS1000  CALL      RKPTDAY1
          BRANCH    OVRCD,RTNS9999
.
          DISPLAY   *P43:24,PTDYADMN;
.
          MATCH     CMONDAY,PTDYDATE              * same date?
          GOTO      RTNS9999 IF NOT EQUAL
.
          PACK      KEY8,PTDYADMN                 * check if MH patient?
          CALL      RDMHVIS1
          BRANCH    OVRCD,RTNS1000
.
          PACK      KEY30,MHVIADMN,RDATE,SP30
          CALL      RDSTRAN2
RTNS2000  CALL      RDKTRAN2
          BRANCH    OVRCD,RTNS1000
.
          MATCH     MHVIADMN,TADMN                * same admission no.?
          GOTO      RTNS1000 IF NOT EQUAL
.
          MATCH     RDATE,TDATE                   * same date?
          GOTO      RTNS1000 IF NOT EQUAL
.
          MATCH     ANSR,TMOVE                    * Return from leave record?
          GOTO      RTNS2000 IF NOT EQUAL
.
          CALL      IDAT0000                    * init temp variables
.
          MOVE      TDATE,XXRETN                  * return date
.
          PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
          CALL      RDPTRAN2                      * get previous rec ('L' Leave)
          BRANCH    OVRCD,RTNS1000
.
          MATCH     MHVIADMN,TADMN                * same admission no.?
          GOTO      RTNS1000 IF NOT EQUAL
.
          MOVE      TDATE,XXLVEDT                * leave date
.
          MOVE      SP1,TCINDC1                  * get type of leave
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          MATCH     SP1,TCINDC1
          GOTO      RTNS1000 IF EQUAL
          MOVE      THCSCOD,XXLVETYP
.
          MATCH     RDATE,TDATE                  * left for leave on same date?
          GOTO      RTNS1000 IF EQUAL            * ignore
.
          PACK      KEY8,MHVIADMN
          CALL      RDDSCH1
          IF        OVRCD = 0
            MATCH     RDATE,DDATE               * discharged on return date?
            GOTO      RTNS1000 IF EQUAL
          ENDIF
.
. get Admission and Master details
.
          PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,RTNS1000
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,RTNS1000
.
          CALL      FRMD0000                    * format data for display
          CALL      WTMP0000                    * write a record to temp file
          GOTO      RTNS1000                    * get next discharge
.
RTNS9999  RETURN
+
.****************************************************************************
.*  DSCH0000  :  Get all discharges on specified date (exclude deaths)      *
.****************************************************************************
DSCH0000  PACK      KEY16,RDATE,SP20
          CALL      RDSDSCH2
DSCH1000  CALL      RDKDSCH2
          BRANCH    OVRCD,DSCH9999
.
          DISPLAY   *P43:24,DADMNO;
.
          MATCH     RDATE,DDATE                   * same date?
          GOTO      DSCH9999 IF NOT EQUAL
.
          PACK      KEY8,DADMNO                   * check if MH patient
          CALL      RDMHVIS1
          BRANCH    OVRCD,DSCH1000
.
. get Admission and Master details
.
          PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,DSCH1000
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,DSCH1000
.
          IF        PCEASE = 1
            GOTO      DSCH1000                    * patient not dead
          ENDIF
.
          CALL      IDAT0000                      * init temp file vars
.
          MOVE      SP4,XXLVETYP                  * init leave type
          MOVE      SP6,XXLVEDT                   * leave date
          MOVE      DDATE,XXDDATE                 * discharge date
.
.        check if patient was 'discharged while on leave' (returned from leave
.        and then discharged) and also if on holiday or A.W.O.L. patient
.
          PACK      KEY30,MHVIADMN,RDATE,SP30
          CALL      RDSTRAN2
DSCH4000  CALL      RDKTRAN2
          BRANCH    OVRCD,DSCH8300
.
          MATCH     MHVIADMN,TADMN               * same admission no.?
          GOTO      DSCH8300 IF NOT EQUAL
.
          MATCH     RDATE,TDATE                  * same transfer date?
          GOTO      DSCH8300 IF NOT EQUAL
.
          MATCH     ANSR,TMOVE                   * return from leave?
          GOTO      DSCH4000 IF NOT EQUAL
.
.        discharged while on leave.   get the leave record (need the leave type)
.
          CALL      RDPTRAN2
          BRANCH    OVRCD,DSCH8300
.
          MATCH     MHVIADMN,TADMN               * same admission no.?
          GOTO      DSCH8300 IF NOT EQUAL
.
          MOVE      TDATE,XXLVEDT                * leave date
          MOVE      SP1,TCINDC1
          MOVE      SP4,THCSCOD                  * get leave type
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          MOVE      THCSCOD,XXLVETYP             * leave type
          MATCH     ANSH,TCINDC1
          GOTO      DSCH8100 IF EQUAL
          MATCH     ANSA,TCINDC1
          GOTO      DSCH8200 IF NOT EQUAL
DSCH8100  MOVE      TDATE,XXDDATE                * new discharge date
.
DSCH8200  MOVE      SP4,THCSCOD
          PACK      KEY5,ANSD,ONE,DUSD1
          CALL      RDCODE1
          MOVE      THCSCOD,XXHOSPT

DSCH8300  CALL      FRMD0000                    * format data for display
          CALL      WTMP0000                    * write a record to temp file
          GOTO      DSCH1000                    * get next discharge
.
DSCH9999  RETURN
+
.****************************************************************************
.*  DEAD0000  :  Get all deaths on specified date                           *
.****************************************************************************
DEAD0000  PACK      KEY16,RDATE,SP20
          CALL      RDSDSCH2
DEAD1000  CALL      RDKDSCH2
          BRANCH    OVRCD,DEAD9999
.
          DISPLAY   *P43:24,DADMNO;
.
          MATCH     RDATE,DDATE                   * same date?
          GOTO      DEAD9999 IF NOT EQUAL
.
          PACK      KEY8,DADMNO                   * check if MH patient
          CALL      RDMHVIS1
          BRANCH    OVRCD,DEAD1000
.
. get Admission and Master details
.
          PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,DEAD1000
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,DEAD1000
.
          IF        PCEASE = 0
            GOTO      DEAD1000                    * patient not dead
          ENDIF
.
          CALL      IDAT0000                      * init temp file vars
.
          MOVE     SP4,XXLVETYP                   * init leave type
          MOVE     SP6,XXLVEDT                    * leave date
          MOVE     DDATE,XXDDATE                  * discharge date
.
.        check if patient was 'discharged while on leave' (returned from leave
.        and then discharged) and also if on holiday or A.W.O.L. patient
.
          PACK      KEY30,MHVIADMN,RDATE,SP30
          CALL      RDSTRAN2
DEAD4000  CALL      RDKTRAN2
          BRANCH    OVRCD,DEAD8000
.
          MATCH     MHVIADMN,TADMN               * same admission no.?
          GOTO      DEAD8000 IF NOT EQUAL
.
          MATCH     RDATE,TDATE                  * same transfer date?
          GOTO      DEAD8000 IF NOT EQUAL
.
          MATCH     ANSR,TMOVE                   * return from leave?
          GOTO      DEAD4000 IF NOT EQUAL
.
.        discharged while on leave.   get the leave record (need the leave type)
.
          CALL      RDPTRAN2
          BRANCH    OVRCD,DEAD8000
.
          MATCH     MHVIADMN,TADMN               * same admission no.?
          GOTO      DEAD8000 IF NOT EQUAL
.
          MOVE      SP4,THCSCOD                  * get leave type
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          MOVE      THCSCOD,XXLVETYP             * leave type
          MOVE      TDATE,XXLVEDT               * Leave date
.
DEAD8000  CALL      FRMD0000                    * format data for display
          CALL      WTMP0000                    * write a record to temp file
          GOTO      DEAD1000                    * get next discharge
.
DEAD9999  RETURN
+
.****************************************************************************
.*  CHLS0000  :  Load data for Changes in legal status                      *
.****************************************************************************
CHLS0000  MOVE      ONE,XXUNIQ                    * init unique counter
.
          PACK      KEY8,SP8
          CALL      RSMHVIS1
CHLS1000  CALL      RKMHVIS1                      * get next record
          BRANCH    OVRCD,CHLS9999                * no more records
.
          COMPARE   MHVISTAT,SIX                  * ignore cmh records
          GOTO      CHLS1000 IF LESS
.
          DISPLAY   *P43:24,MHVIADMN;
.
          CALL      IDAT0000                    * init temp variables
.
.      get HDP equivalent of New legal status
.
          PACK      KEY21,MHVIADMN,RDATE,ZED20
          CALL      RSMHLEG1
CHLS1500  CALL      RPMHLEG1
          BRANCH    OVRCD,CHLS1000
.
          MATCH     MHVIADMN,MHLEADMN
          GOTO      CHLS1000 IF NOT EQUAL
.
          MATCH     RDATE,MHLEDATE
          GOTO      CHLS1000 IF NOT EQUAL
.
          MATCH     ANSR,MHLEFLAG           * review status change only ?
          GOTO      CHLS1500 IF EQUAL       * yes - ignore
.
          MATCH     ANSN,MHLEFLAG           * no status change ?
          GOTO      CHLS1500 IF EQUAL       * yes - ignore
.
          MOVE      SP4,THCSCOD
          PACK      KEY5,ANSL,ANSS,MHLESTAT
          CALL      RDCODE1
          BRANCH    OVRCD,CHLS1000
          MOVE      THCSCOD,XXLSC               * New Legal Status HDP equiv
          UNPACK    MHLEDATE,CCENT,CYEAR,CMON,CDAY
          PACK      XXDATLSC,CCENT,CYEAR,CMON,CDAY * date of Legal Status change
          REP       " 0",XXDATLSC
.
. check for a change in legal status (HDP equiv)
.
CHLS2000  CALL      RPMHLEG1
          BRANCH    OVRCD,CHLS1000
.
          MATCH     MHVIADMN,MHLEADMN           * same admission number?
          GOTO      CHLS1000 IF NOT EQUAL
.
          MATCH     ANSR,MHLEFLAG           * review status change only ?
          GOTO      CHLS2000 IF EQUAL       * yes - ignore
.
          MATCH     ANSN,MHLEFLAG           * no status change ?
          GOTO      CHLS2000 IF EQUAL       * yes - ignore
.
          MOVE      SP4,THCSCOD
          PACK      KEY5,ANSL,ANSS,MHLESTAT
          CALL      RDCODE1
          BRANCH    OVRCD,CHLS1000
          MOVE      THCSCOD,XXLSBEF             * Old Legal Status HDP equiv
.
          MATCH     XXLSBEF,XXLSC               * same HDP equiv (Old vs New)?
          GOTO      CHLS3000 IF EQUAL
.
          UNPACK    MHVIRODT,CCENT,CYEAR,CMON,CDAY
          PACK      XXRECD,CCENT,CYEAR,CMON,CDAY      * reception date
.
. get Admission and Master details
.
          PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,CHLS1000
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,CHLS1000
.
          CALL      FRMD0000                    * format data for display
          CALL      WTMP0000                    * write a record to temp file
.
CHLS3000  ADD       ONE,XXUNIQ
.
          MATCH     RDATE,MHLEDATE
          GOTO      CHLS1000 IF NOT EQUAL
.
          UNPACK    MHLEDATE,CCENT,CYEAR,CMON,CDAY
          PACK      XXDATLSC,CCENT,CYEAR,CMON,CDAY * date of Legal Status change
          REP       " 0",XXDATLSC
          MOVE      XXLSBEF,XXLSC   
          GOTO      CHLS2000                    * get next MH patient
.
CHLS9999  RETURN
+
.****************************************************************************
.* FRMD0000  :  Format data for display                                     *
.****************************************************************************
FRMD0000  MOVE      PURNO,XXURNO
          MOVE      PSEX,XXSEX                    * sex
.
          STRIP     PSNAME                        * patients name
          PACK      XXNAME,PSNAME,COMMA,SP1,PGNAME,SP30
          ENDSET    XXNAME
          RESET     XXNAME
.
          CALL      LADM0000                      * get last admission date
.
.       get patients age at admission or last trial leave date
.
.      patients age at report date
.
          MOVE      XXADATE,AGEDATE
          CALL      CALCAGE
          MOVE      PSAGEYRS,XXAGE
.
. get the legal status  (if discharged/died get last legal status)
.
          IF        (REPNO=4) | (REPNO=5)
            MOVE      SP4,XXLSC
            PACK      KEY21,MHVIADMN,ZED20
            CALL      RSMHLEG1
            CALL      RPMHLEG1
            BRANCH    OVRCD,FRMD9999
.
            MATCH     MHVIADMN,MHLEADMN           * same admission no.?
            GOTO      FRMD9999 IF NOT EQUAL
.
            MOVE      MHLESTAT,XXLSC              * last legal status for pat.
.
          ELSE
            IF        ! (REPNO=6)
.
. Get Legal status (HDP equivalent) at current date
.
              MOVE      SP4,THCSCOD
              PACK      KEY21,MHVIADMN,RDATE,ZED20
              CALL      RSMHLEG1
FRMD6000      CALL      RPMHLEG1
              BRANCH    OVRCD,FRMD6500
.
              MATCH     MHVIADMN,MHLEADMN             * same admission no.?
              GOTO      FRMD6500 IF NOT EQUAL
.
...              MATCH     MHLEDATE,RDATE               * <= report date?
...              GOTO      FRMD6000 IF LESS

              PACK      KEY5,ANSL,ANSS,MHLESTAT
              CALL      RDCODE1
FRMD6500      MOVE      THCSCOD,XXLSC
            ENDIF
          ENDIF
.
FRMD9999  RETURN
+
.****************************************************************************
.* WTMP0000  :  Write a record to the temp file                             *
.****************************************************************************
WTMP0000  PACK      KEY10,XXURNO,XXUNIQ
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
            DISPLAY   *P43:24,*V2LON,XXURNO;
          ENDIF
.
WTMP9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      ONE,REPNO                     * start at report no. 1
.
          CALL      HEAD132                       * Print 3 line header
          PRINT     *40,"Report Date : ",DRDATE,*N
          MOVE      TEN,CLNO                      * line counter
.
        WHILE     REPNO < 7
.
          DISPLAY   *P1:24,*EL,*P20:24,"Rep : ",REPNO:
                    *P30:24,"Loading ... [        ]";
.
          PERFORM   REPNO,ADMS0000,LEAV0000,RTNS0000,DSCH0000,DEAD0000,CHLS0000
.
          CALL      PTHD0000                      * print table headings
          ADD       TWO,CLNO
.
. Now loop thru temp file and print data
.
          PACK      KEY10,SP30,SP20
          CALL      RSTEMP1
RPRT1000  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT7000                * no more records
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT2000 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          PRINT     *40,"Report Date : ",DRDATE,*N
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
. print details
.
RPRT2000  CALL      PLIN0000                      * print a line of details
.
          ADD       ONE,CLNO
          ADD       ONE,NOPATS
          GOTO      RPRT1000
.
. we have finished printing patient data so print end of report details
.
RPRT7000  PRINT     *N
          CALL      CRET0000                      * clear temp file
          ADD       ONE,REPNO                     * increment report number
          ADD       ONE,CLNO
.
        DO   .{end while}
.
          PRINT     *N,"*** End of Report ***"
.
RPRT9999  RETURN
+
.**************************************************************************
.*  PLIN0000  :  Print a line of data                                     *
.**************************************************************************
PLIN0000  UNPACK    XXADATE,CCENT,CYEAR,CMON,CDAY * format admission date
          CALL      PACDATE
          MOVE      CPCDATE,DSADATE
.
          UNPACK    XXDATLSC,CCENT,CYEAR,CMON,CDAY   * format Legal Status date
          CALL      PACDATE
          MOVE      CPCDATE,DSLSDAT
.
          UNPACK    XXRECD,CCENT,CYEAR,CMON,CDAY     * format Reception date
          CALL      PACDATE
          MOVE      CPCDATE,DSRECDAT
.
          UNPACK    XXDDATE,CCENT,CYEAR,CMON,CDAY    * format Discharge date
          CALL      PACDATE
          MOVE      CPCDATE,DSDDATE
.
          UNPACK    XXLVEDT,CCENT,CYEAR,CMON,CDAY    * format Leave date
          CALL      PACDATE
          MOVE      CPCDATE,DSLVDAT
.
          UNPACK    XXRETN,CCENT,CYEAR,CMON,CDAY     * format return date
          CALL      PACDATE
          MOVE      CPCDATE,DSRETND
.
          BRANCH    REPNO,PLIN1000,PLIN2000,PLIN3000,PLIN4000,PLIN5000,PLIN6000
.
PLIN1000  PRINT     *1,XXURNO,*11,XXNAME,*55,XXLSC,*64,XXSEX,*68,XXAGE:
                    *73,DSADATE,*90,XXHOSPF
          GOTO      PLIN9999
.
PLIN2000  PRINT     *1,XXURNO,*11,XXNAME,*49,XXLVETYP,*56,XXLSC,*64,XXSEX:
                    *68,XXAGE,*73,DSADATE,SP2,DSLVDAT,SP2,DSRETND
          GOTO      PLIN9999
.
PLIN3000  PRINT     *1,XXURNO,*11,XXNAME,*49,XXLVETYP,*56,XXLSC,*64,XXSEX:
                    *68,XXAGE,*73,DSADATE,SP2,DSLVDAT,SP2,DSRETND
          GOTO      PLIN9999
.
PLIN4000  PRINT     *1,XXURNO,*11,XXNAME,*49,XXLVETYP,*56,XXLSC,*64,XXSEX:
                    *68,XXAGE,*73,DSADATE,SP2,DSLVDAT,SP2,DSDDATE,SP5,XXHOSPT
          GOTO      PLIN9999
.
PLIN5000  PRINT     *1,XXURNO,*11,XXNAME,*49,XXLVETYP,*56,XXLSC,*64,XXSEX:
                    *68,XXAGE,*73,DSADATE,SP2,DSLVDAT,SP2,DSDDATE
          GOTO      PLIN9999
.
PLIN6000  PRINT     *1,XXURNO,*11,XXNAME,*50,XXLSBEF,SP4,XXLSC,*64,XXSEX:
                    *68,XXAGE,*73,DSADATE,SP2,DSLSDAT,SP2,DSRECDAT
          GOTO      PLIN9999
.
PLIN9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Headings                                     *
.**************************************************************************
PTHD0000  LOAD      REPSECT,REPNO,REPS1L,REPS2L,REPS3L,REPS4L,REPS5L,REPS6L
.
          PRINT     *40,REPSECT,*N
.
          BRANCH    REPNO,PTHD1000,PTHD2000,PTHD3000,PTHD4000,PTHD5000,PTHD6000
.
PTHD1000  PRINT     *1," U/R No.  Patient Name",*48,"Leave  Legal   Sex  ":
                    "Age  Admitted",*90,"From Hospital":
                    *N,*49,"Type  Status"
          GOTO      PTHD9999
.
PTHD2000  PRINT     *1," U/R No.  Patient Name",*48,"Leave  Legal   Sex  ":
                    "Age  Admitted  On Leave  Exp Return":
                    *N,*49,"Type  Status",*93,"Review Date"
          GOTO      PTHD9999
.
PTHD3000  PRINT     *1," U/R No.  Patient Name",*48,"Leave  Legal   Sex  ":
                    "Age  Admitted  On Leave  Return":
                    *N,*49,"Type  Status"
          GOTO      PTHD9999
.
PTHD4000  PRINT     *1," U/R No.  Patient Name",*48,"Leave  Legal   Sex  ":
                    "Age  Admitted  On Leave  Disch Date   To Hospital":
                    *N,*49,"Type  Status",*93,"Trans Date"
          GOTO      PTHD9999
.
PTHD5000  PRINT     *1," U/R No.  Patient Name",*48,"Leave  Legal   Sex  ":
                    "Age  Admitted  On Leave  Died":
                    *N,*49,"Type  Status"
          GOTO      PTHD9999
.
PTHD6000  PRINT     *1," U/R No.  Patient Name",*48,"Status Status  Sex  ":
                    "Age  Admitted  Change    Reception":
                    *N,*48,"Before After",*85,"Date",*95,"Date"
.
PTHD9999  RETURN
+
.*************************************************************************
.*  LADM0000  :  Get last admission (Return from leave or Admission)     *
.*************************************************************************
LADM0000  PACK      XXADATE,ADATE              * admission date
          REP       " 0",XXADATE
          GOTO      LADM9999
.
.------------------------------------------------------------------------------
. the following code was required for the original specs
.------------------------------------------------------------------------------
.
.          IF        REPNO <> 1 
.            PACK      KEY30,AADMNO,ZED20,ZED20
.            CALL      RDSTRAN2
.LADM1000    CALL      RDPTRAN2
.            BRANCH    OVRCD,LADM9999
..
.            MATCH     MHVIADMN,TADMN              * same admission number?
.            GOTO      LADM9999 IF NOT EQUAL
..
.            MATCH     ANSR,TMOVE                  * Return from Leave?
.            GOTO      LADM1000 IF NOT EQUAL
..
.            PACK      SAVTDATE,TDATE              * save date
.            REP       " 0",SAVTDATE
..
.. we have a return record so search back for a Leave record with ind1 = "T"
..
.LADM2000    CALL      RDPTRAN2
.            BRANCH    OVRCD,LADM9999
..
.            MATCH     MHVIADMN,TADMN              * same admission number?
.            GOTO      LADM9999 IF NOT EQUAL
..
.            MATCH     ANSL,TMOVE                  * Return from Leave?
.            GOTO      LADM2000 IF NOT EQUAL
..
.            PACK      KEY5,ANST,ANSL,PTTRLTYP
.            CALL      RDCODE1
.            BRANCH    OVRCD,LADM1000
..
.            MATCH     ANST,TCINDC1                * Trial Leave?
.            GOTO      LADM1000 IF NOT EQUAL
..
.            MOVE      SAVTDATE,XXADATE            * new admission date
.          ENDIF
.
LADM9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  initialise vars                                         *
.*************************************************************************
IDAT0000  UNPACK    SP70,XXNAME,XXURNO,XXLSC,XXSEX,XXADATE,XXDDATE
          UNPACK    SP70,XXHOSPF,XXHOSPT,XXLVETYP,XXLVEDT,XXLSBEF,XXRECD:
                         XXRETN,XXDATLSC
          MOVE      ZERO,XXAGE
          MOVE      ZERO,XXUNIQ
.
IDAT9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 109 U1-8,107-108",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,TFILNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RSTEMP1   RESET     KEY10
          READ      MEHTMPA1,KEY10;;
          RETURN
.
RATEMP1   RESET     KEY10                    * existing read ?
          MOVE      ZERO,OVRCD
          READ      MEHTMPA1,KEY10;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;XXURNO,XXNAME,XXLSC,XXSEX,XXAGE,XXADATE:
                             XXDDATE,XXHOSPT,XXHOSPF,XXLVETYP,XXLVEDT:
                             XXLSBEF,XXRECD,XXRETN,XXDATLSC,DXXUNIQ
          GOTO      OVERCOND IF OVER
          MOVE      DXXUNIQ,XXUNIQ
          RETURN
.
WRTEMP1   RESET     KEY10
          MOVE      XXUNIQ,DXXUNIQ
          WRITE     MEHTMPA1,KEY10;XXURNO,XXNAME,XXLSC,XXSEX,XXAGE,XXADATE:
                             XXDDATE,XXHOSPT,XXHOSPF,XXLVETYP,XXLVEDT:
                             XXLSBEF,XXRECD,XXRETN,XXDATLSC,DXXUNIQ
          RETURN
.
.
          INC       STD001IO
          INC       MEHVI1IO/INC
          INC       PATDAYIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       MEHLEGIO/INC
          INC       PATTRNIO/INC
          INC       PATONLIO/INC
          INC       CALCAGE
          INC       CALCDAYS
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

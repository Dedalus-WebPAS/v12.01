.***************************************************************************
.* System    :   Emergency                                                 *
.* Program   :   EMRLOADP                                                  *
.* Desc      :   ICD9CMA Emergency Procedure Code Upload                   *
.***************************************************************************
.* Date      :   02/01/1998                                                *
.* Author    :   Steve Armstrong  (from 6.04)                              *
.* Function  :   To Load ICD9 operation codes from a text file into the    *
.*               emergency procedures file (emrproaf).                     *
.* Modifications:                                                          *
.***************************************************************************
.*  V10.06.01  07/08/2015  Ebon Clements         CAR 320281                *
.*             Changed update file to match EMRPROFD layout                *
.***************************************************************************
.
          INC       STD001FD
.
.
          INC       EMRPROFD/INC
.
ICD9OPR1  FILE      ASCII, FIXED=256
.
. ICD9 text file format
.
.Name     Type     Size     Start
.----     ----     ----     -----
CODE      DIM       9        1
DESC1     DIM       70       10
HDPCODE   DIM       6        80
.
.End of record               85 
.
.
. Local Variables
. ---------------
.
COUNTR    FORM      8
FNAME1    DIM       8
.
.
PRGID     INIT      "EMRLOADP"
PRGNAM    INIT      "Emergency Procedure Code Upload"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * load ICD9CMA codes
.
ML1000    CALL      FNAM0000               * get icd9cma text file name
          BRANCH    EXIT,ML0100            * nothing input, get next option
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      OPRC0000               * load operation codes
          GOTO      ML0100                 * get next menu option
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialization                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"emrproaf";
          OPEN      EMRPROA1,"emrproaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*    Returns:  EXIT    =       (0)  Valid option selected                  *
.*                              (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run ICD9 Operation Code Upload"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes - finished
.
          BRANCH    COPTION,OPTN9000             * run upload selected ?
.
          BEEP                                   * invalid keyin
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                         FNAM0000                    Called by: ML0000    *
.*             Get ICD9CMA text file name                                   *
.* Returns:  EXIT 0 = filename entered                                      *
.*                1 = nothing entered                                       *
.****************************************************************************
.
FNAM0000  MOVE      SP8,FNAME1
          KEYIN     *P1:10,*EL,"Keyin Operation ICD9 text file : ":
                    *P34:10,*V2LON,FNAME1:
                    *P34:10,*DV,FNAME1
.
          RESET     FNAME1                       * anything entered ?
          GOTO      FNAM9500 IF EOS              * no - finished
.
.         Open the ICD9 text file
.
          MOVE      ZERO,OVRCD
          TRAP      FNAM5000 IF IO
          OPEN      ICD9OPR1,FNAME1
          TRAPCLR   IO
          GOTO      FNAM9000
.
.         Error on file open
.
FNAM5000  NORETURN
          DISPLAY   *P1:24,*EL,*B,"File ",FNAME1," not found.  ";
          CALL      HITENTER
          GOTO      FNAM0000
.
FNAM9000  MOVE      ZERO,EXIT
          GOTO      FNAM9999
.
FNAM9500  MOVE      ONE,EXIT
.
FNAM9999  RETURN
+
.****************************************************************************
.*                           OPRC0000                  Called by: ML0000    *
.*            Load operation codes into emrproaf                            *
.****************************************************************************
.
OPRC0000  DISPLAY   *P1:24,*EL,"ICD9 Code: ";
          MOVE      ZERO,COUNTR                  * initialise counter
.
OPRC1000  READ      ICD9OPR1,SEQ;CODE,DESC1,HDPCODE
          GOTO      OPRC9000 IF OVER             * eof - finished
.
          DISPLAY   *P12:24,*V2LON,CODE;
          ADD       ONE,COUNTR                   * increment counter
.
          PACK      KEY9,CODE,SP10
          CALL      RDEMPRO1                     * procedure already on file ?
          BRANCH    OVRCD,OPRC2000               * no - write new record
.
          MOVE      DESC1,EMPRDESC               * yes - update record
          MOVE      HDPCODE,EMPRHDPE       
.
          CALL      UPEMPRO1
          GOTO      OPRC1000                     * get next record
 
OPRC2000  CALL      CLRV0000                     * initialise operation record
          MOVE      DESC1,EMPRDESC
          MOVE      CODE,EMPRCODE
          MOVE      HDPCODE,EMPRHDPE       
.
          CALL      WREMPRO1
          GOTO      OPRC1000
.
OPRC9000  DISPLAY   *P1:24,*EL,"Upload Completed.  ",COUNTR," records loaded. ";
          CALL      HITENTER
.
OPRC9999  RETURN
+
.****************************************************************************
.*                            CLRV0000                 Called by: OPRC0000  *
.*                 Initialise Operation record fields                       *
.****************************************************************************
.
CLRV0000  MOVE      SP9,EMPRCODE
          PACK      EMPRDESC,SP30,SP30,SP10
          MOVE      SP6,EMPRHDPE
          PACK      EMPRSPAR,SP10,SP4
.
CLRV9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       EMRPROIO/INC

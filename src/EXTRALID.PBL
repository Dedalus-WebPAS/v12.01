.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   EXTRALID                                                    *
.* Desc      :   Patient Alternate Id Data Extraction Program                *
.*****************************************************************************
.* Date      :   06/03/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will extract the patient alternate id's into a *
.*               pipe delimited file in a format that will feed into         *
.*               CONVALID.                                                   *
.* Mods:                                                                     *
.*        V10.04.00 06/03/2014  Steve Armstrong  CAR 296128                  *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSAIDFD/INC
.
.
ALTIDEXT  FILE      ASCII,FIXED=4096
.
.         Patient Alternate Id upload file layout - convalid.txt
.         (as per CONVALID.PBL)
.
.Name     Type    Length Description
.----     ----    ------ -----------
ALIDURNO  DIM       8    U/R Number                        (pmaiurno)
ALIDTYPE  DIM       3    Alternate Id Type (Cat AI)        (pmaitype)
ALIDALID  DIM       20   Alternate Id                      (pmaialid)
ALIDDISU  DIM       1    Display instead of U/R            (pmaidisu)
.                         0 or blank = No
.                         1 = Yes
ALIDHOSP  DIM       3    Hospital Code                     (pmaihosp)
ALIDRDAT  DIM       8    Registration Date (ccyymmdd)      (pmairdat)
ALIDCRDT  DIM       8    Date Record Created (ccyymmdd)    (pmaicrdt)
ALIDCRTM  DIM       8    Time Record Created (hh:mm:ss)    (pmaicrtm)
ALIDCRUD  DIM       10   User Who Created Record           (pmaicrud)
ALIDUPDT  DIM       8    Date Record Updated (ccyymmdd)    (pmaiupdt)
ALIDUPTM  DIM       8    Time Record Updated (hh:mm:ss)    (pmaiuptm)
ALIDUPUD  DIM       10   User Who Updated Record           (pmaiupud)
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
EXTCOUNT  FORM      10
RECCOUNT  FORM      10
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
PRGID     INIT      "EXTRALID"
PRGNAM    INIT      "Patient Alternate ID Extraction"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * Alternate Id extract
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      CFIL0000               * create file
          BRANCH    EXIT,MAIN0100          * file exists
.
          CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsaidaf";
          OPEN      PMSAIDA1,"pmsaidaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data extraction                     *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Patient Alternate Id Data ":
                    "Extraction"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run extraction
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                                  CFIL0000       Called by: MAIN0000       *
.*                         Create the extract file                           *
.*****************************************************************************
.
CFIL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ALTIDEXT,"convalid"     * extract file exists ?
          TRAPCLR   IO
          BRANCH    OVRCD,CFIL5000          * no - create it
.
CFIL1000  KEYIN     *P1:23,*B,*EF,"The extract file convalid.txt already ":
                    "exists.":
                    *P1:24,"Overwrite it (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,")?":
                    *P21:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS                * "Y" entered ?
          GOTO      CFIL2000 IF EQUAL       * yes - continue
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      CFIL9100 IF EQUAL       * no
.
          BEEP
          GOTO      CFIL1000
.
.         Delete the existing file
.
CFIL2000  CLOSE     ALTIDEXT,DELETE
.
.         Create a new extract file
.
CFIL5000  PREP      ALTIDEXT,"convalid"
.
          MOVE      ZERO,EXIT
          GOTO      CFIL9999
.
CFIL9100  MOVE      ONE,EXIT
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through the Doctor's file and extract each record             *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          MOVE      ZERO,EXTCOUNT                * initialise extract count
          DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      SP70,KEY31
          CALL      RSPMAID1                     * position at start of file
PROC0500  CALL      RKPMAID1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          ADD       ONE,RECCOUNT                 * increment record counter
.
          DISPLAY   *P13:24,*V2LON,RECCOUNT
.
          MOVE      PMAIURNO,ALIDURNO
          SQUEEZE   ALIDURNO
          MOVE      PMAITYPE,ALIDTYPE
          SQUEEZE   ALIDTYPE
          MOVE      PMAIALID,ALIDALID
          STRIP     ALIDALID
          MOVE      PMAIDISU,ALIDDISU
          STRIP     ALIDDISU
          MOVE      PMAIHOSP,ALIDHOSP
          SQUEEZE   ALIDHOSP
          MOVE      PMAIRDAT,ALIDRDAT
          SQUEEZE   ALIDRDAT
          MOVE      PMAICRDT,ALIDCRDT
          SQUEEZE   ALIDCRDT
          MOVE      PMAICRTM,ALIDCRTM
          SQUEEZE   ALIDCRTM
          MOVE      PMAICRUD,ALIDCRUD
          SQUEEZE   ALIDCRUD
          MOVE      PMAIUPDT,ALIDUPDT
          SQUEEZE   ALIDUPDT
          MOVE      PMAIUPTM,ALIDUPTM
          SQUEEZE   ALIDUPTM
          MOVE      PMAIUPUD,ALIDUPUD
          SQUEEZE   ALIDUPUD
.
          WRITE     ALTIDEXT,SEQ;*+,ALIDURNO,PIPE,ALIDTYPE,PIPE,ALIDALID,PIPE:
                                    ALIDDISU,PIPE,ALIDHOSP,PIPE,ALIDRDAT,PIPE:
                                    ALIDCRDT,PIPE,ALIDCRTM,PIPE,ALIDCRUD,PIPE:
                                    ALIDUPDT,PIPE,ALIDUPTM,PIPE,ALIDUPUD,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:23,*EF,"Extraction complete.":
                    *P1:24,*V2LON,EXTCOUNT,*HOFF," records extracted.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSAIDIO/INC

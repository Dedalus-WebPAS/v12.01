.***************************************************************************
.* System    :   Private Practice System                                   *
.* Program   :   IBAPRI27                                                  *
.* Desc      :   Journals Report                                           *
.***************************************************************************
.* Date      :   23/07/91                                                  *
.* Author    :   Stephen Armstrong                                         *
.* Function  :   Print a list of journal transactions for a given range    *
.*               of dates and journal codes codes                          *
.* Mods      :                                                             *
.*        V10.13.01 26/07/2018  J.Tan          TSK 0848506                    *
.*                  Changed PP Doctor from DIM6 to DIM10                      *
.*        V9.11.01  16/10/2008  J.Tan          CAR 178415                     *
.*                  Mods to check for Medical practice access file            *
.*        V9.04.02  19/08/2005  J.Tan          CAR 62750                      *
.*                  Removed redefined amount variables                        *
.*        V9.04.01  10/11/2004  Mike Laming  CAR 37608                        *
.*                  Add ability to select a single Medical Practice for report*
.*                  New routines SMPR0000 & CHKP0000, changes at PROC1500     *
.*        V9.03.02  02/03/2004  Lina Vo     CAR 47921                         *
.*                  Removed use of PRIDBTFD & IO.                             *
.*        V9.03.01  19/02/2004  Lina Vo     CAR 44138                         *
.*                  Added extra Medical Practice column to report             *
.*                  Changed format of Name to use PACNAME.                    *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 03/04/2000  Caleb Sharman                              *
.*                  Mods print on seperates pages the journals where the   *
.*                  variable THCSCOD = 'G'                                 *
.*        V5.07.01  09/06/1999  Greg Horvat                                *
.*                  Recompiled for PRIINVIO - Fixed problem with direct read*
.*              V5.07.B01 05/11/1998  Jim Stathopoulos                     *
.*                        507 Changes                                      *
.***************************************************************************
.*               V6.00.01 Steve Armstrong 31/08/92                         *
.*                        Updated to use patdrgaf file for start of fin.   *
.*                        year date                                        *
.*               V6.00.02 07/10/92  Steve Armstrong                        *
.*                        Modified for alphanumeric debtor number          *
.*               V6.00.03 12/10/92  Steve Armstrong                        *
.*                        Fixed bug with display of U/R details            *
.*               V6.00.04 27/09/93  DIG                                    *
.*                        Changed so that journals are only printed if     *
.*                        there is information against them for the given  *
.*                        parameters.                                      *
.*               V6.00.05 01/10/1993  Steve Armstrong    SRF  124940       *
.*                        Fixed bug in default end date                    *
.***************************************************************************
.
.$$$$$
.         Journals Report Program (IBAPRI27)
.         ----------------------------------
.
.         ML0000
.         Open files and initialisation
.         prijrnlf
.         pridebtf
.         priinvof
.         pridtraf
.         patma1af
.         patcodes
.         patdrgaf
.         controlf
.
.         ML0100
.         Get required option
.         If zero is input, exit program
.         If print journal report selected, proceed to display input screen
.
.         ML1000
.         Display input screen
.
.         ML2000
.         Get the starting date for the search
.         Get the end date for the search
.         Check that date range valid
.              If not, return to get start date again
.
.         ML3000
.         Get starting journal code for the search
.             If ? input, display journal codes on file
.             If nothing entered, default to "start"
.             If exitchar entered, return to get next option
.         Get end journal code for the search
.             If ? input, display journal codes on file
.             If nothing entered, default to "finish"
.             If exitchar entered, return to get next option
.         Validate code range
.             If not valid, return to get starting journal code
.
.         Confirm continuation with printout
.             If (Y)es entered, proceed to processing
.             If (N)o entered, return to get starting date for search
.             If (C)ancel entered, return to get next option
.
.         ML8000
.         Search through files for data matching criteria and print
.         When print complete, return to get next option
.
.         ML9999
.         Chain back to the calling program
.
.$$$$$
.
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PRIJRNFD/INC                 * journal file
          INC       PRIDTRFD/INC                 * debtor transaction file
..          INC       PRIDBTFD/INC                 * debtor file
          INC       PRIINVFD/INC                 * invoice file
          INC       PRICONFD/INC                 * control file
          INC       PRIPRAFD/INC                 * practice file       *I-37608
          INC       PRIALSFD/INC                 * Medical Prac.all user access
          INC       PRISECFD/INC                 * Medical Prac.security access
.
          INC       PATMA1FD/INC                 * patient master file
          INC       PATCODFD/INC                 * codes file
          INC       PATDRGFD/INC                 * date range file
.
. CONSTANTS DEFINITION
. --------------------
.
CODEJ     INIT      "J "
PIPE      INIT      "|"
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CODE      DIM       2                            * for PATCODKY
CURRDAT   DIM       8                            * current date
.
.
ECODE     DIM       3                            * end code
EDATE     DIM       8                            * end date
.
FORM8P2   FORM      8.2
FEDATE    DIM       10                           * formatted end date
FMTDEBT   DIM       9                            * formatted debtor #
FSDATE    DIM       10                           * formatted start date
FULLNAM   DIM       34                           * formatted patient name
.
GRANTOT   FORM      9.2                          * grand total
GSTFLAG   FORM      1                            * GST journal search
GSTTOT    FORM      9.2                          * gst total
.
JCODE     DIM       3                            * journal code
JDESC     DIM       30                           * journal description
JTOTAL    FORM      8.2                          * journal total
.
PRNTTOTL  FORM      1                                                  *I-37608
.
SCODE     DIM       3                            * start code
SDATE     DIM       8                            * start date
SHDESC    DIM       20                           * short journal description
SELPRAC   DIM       6                                                  *I-37608
.
USERID    DIM       10
.
PRGID     INIT      "IBAPRI27"
PRGNAM    INIT      "Journals Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000                     * initialisation and open files
.
ML0100    CALL      OPTN0000                     * select options
          BRANCH    EXIT,ML9999                  * EXIT = 1 if 0 chosen in menu
.
ML1000    CALL      SCR0000                      * display screen format
.
ML2000    CALL      SDAT0000                     * get start date
.
          CALL      EDAT0000                     * get end date
          BRANCH    EXIT,ML2000                  * invalid date range
.
ML3000    CALL      SJNL0000                     * get start journal
          BRANCH    EXIT,ML0100                  * exitchar entered
.
          CALL      EJNL0000                     * get end journal
          BRANCH    EXIT,ML3000:                 * invalid journal range
                         ML0100                  * exitchar entered
.
ML4000    CALL      SMPR0000                     * get medical prac.   *I-37608
          BRANCH    EXIT,ML0100                  * exitchar entered    *I-37608
.
          CALL      CONTQST                      * OK to continue ?
          BRANCH    CEXIT,ML8000:                * yes
                          ML1000:                * no
                          ML0100                 * cancel
.
ML8000    CALL      PROC0000                     * process report
          GOTO      ML0100                       * get next option
.
ML9999    CHAIN     PGM                          * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,"Opening ":
                    *P64:24,"prijrnlf";
          OPEN      PRIJRNL1,"prijrnlf"          * journal file
.
          DISPLAY   *P64:24,"pridtraf";
          OPEN      PRIDTRA1,"pridtraf"          * debtor transaction file
.
..          DISPLAY   *P64:24,"pridebtf";
..          OPEN      PRIDEBT1,"pridebtf"          * debtor file
.
          DISPLAY   *P64:24,"priinvof";
          OPEN      PRIINVO1,"priinvof"          * invoice file
.
          DISPLAY   *P64:24,"pripracf";                                *I-37608
          OPEN      PRIPRAC1,"pripracf"          * practice file       *I-37608
.
          OPEN      PRIALSA1,"prialsaf"
          OPEN      PRISECA1,"prisecaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"          * codes file
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA2,"patdrgaf"          * date range file
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"          * control file
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.*              COPTION = 1  Journals report                                *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Journals Report"
.
OPTN1000  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL            * exit
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000                     * invalid
.
OPTN9000  KEYIN     *P1:11,*EL,"User Id : ",USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      OPTN1000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                 SCR0000             Called by: ML0000    *
.*                        display entry screen labels                       *
.****************************************************************************
.
SCR0000   DISPLAY   *P1:9,"Starting Date    :":
                    *P1:11,"Ending Date      :":
                    *P1:13,"Starting Journal :":
                    *P1:15,"Ending Journal   :":
                    *P1:17,"Select A Medical Practice :"               *I-37608
.
SCR9999   RETURN
+
.****************************************************************************
.*                                 SDAT0000            Called by: ML0000    *
.*                       get report starting date                           *
.****************************************************************************
.
SDAT0000  DISPLAY   *P20:9,*EL,*P20:11,*EL,*P20:13,*EL,*P20:15,*EL,*P1:24,*EL
.
          CALL      IBACLOCK                     * get current date 
.
          PACK      CURRDAT,CCC,CYY,CMM,CDD
          REP       " 0",CURRDAT
          UNPACK    SP10,CCENT,CYEAR,CMON,CDAY
.
          PACK      KEY14,CURRDAT,ZED20          * get start of financial year
          CALL      RDSDRGA2
SDAT0500  CALL      RDPDRGA2
          BRANCH    OVRCD,SDAT1000
.
          MOVE      DRGNUM,FORM2
          IF        FORM2 = 1
             UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          ELSE
             GOTO   SDAT0500
          ENDIF
.
SDAT1000  MOVE      TWENTY,CCOL
          MOVE      NINE,CVERT
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYDATE                      * get date
          BRANCH    OVRCD,SDAT0000
          PACK      SDATE,CCENT,CYEAR,CMON,CDAY  * save date
.
SDAT9999  RETURN
+
.****************************************************************************
.*                                 EDAT0000            Called by: ML0000    *
.*                        get report ending date                            *
.*    RETURNS:   EXIT  0 = valid date                                       *
.*                     1 = invalid date                                     *
.****************************************************************************
.
EDAT0000  CALL      IBACLOCK                     * get today's date
          PACK      CURRDAT,CCC,CYY,CMM,CDD
          REP       " 0",CURRDAT
          UNPACK    CURRDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      TWENTY,CCOL
          MOVE      TEN1,CVERT
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYDATE                      * get date
          BRANCH    OVRCD,EDAT0000
          PACK      EDATE,CCENT,CYEAR,CMON,CDAY  * save date
.
.         Validate date range
.
          MATCH     SDATE,EDATE                  * end date < start date ?
          GOTO      EDAT9000 IF NOT LESS         * no
.
          DISPLAY   *P1:24,*EL,*B,"Start date must not be greater than end ":
                    "date. ";
          CALL      HITENTER
          GOTO      EDAT9500
.
EDAT9000  MOVE      ZERO,EXIT
          GOTO      EDAT9999
.
EDAT9500  MOVE      ONE,EXIT
.
EDAT9999  RETURN
+
.****************************************************************************
.*                                 SJNL0000            Called by: ML0000    *
.*                   get report starting journal code                       *
.*    RETURNS:  EXIT   0 = valid code                                       *
.*                     1 = exitchar entered                                 *
.****************************************************************************
.
SJNL0000  MOVE      TEN3,EVERT
          MOVE      TWENTY,ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      CODEJ,CODE
.
          CALL      PATCODKY                     * get code
          BRANCH    EXIT,SJNL5000:               * nothing entered
                         SJNL9500                * exitchar entered
.
          GOTO      SJNL9000                     * valid code
.
SJNL5000  DISPLAY   *P20:13,*V2LON,"Start"       * default display
          MOVE      SP3,SCODE
          MOVE      ZERO,EXIT
          GOTO      SJNL9999
.
SJNL9000  MOVE      ACODE,SCODE                  * save code
          DISPLAY   *P20:13,*V2LON,ACODE,*HOFF,*P25:13,TDESC
          MOVE      ZERO,EXIT
          GOTO      SJNL9999
.
SJNL9500  MOVE      ONE,EXIT
.
SJNL9999  RETURN
+
.****************************************************************************
.*                                 EJNL0000            Called by: ML0000    *
.*                    get report ending journal code                        *
.*    RETURNS:       EXIT  0 = valid date                                   *
.*                         1 = invalid date range                           *
.*                         2 = exitchar entered                             *
.****************************************************************************
.
EJNL0000  MOVE      TEN5,EVERT
          MOVE      TWENTY,ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      CODEJ,CODE
.
          CALL      PATCODKY                     * get code
          BRANCH    EXIT,EJNL5000:               * nothing entered
                         EJNL9700                * exitchar entered
.
          GOTO      EJNL7000                     * valid code
.
EJNL5000  MOVE      "zzz",ECODE
          DISPLAY   *P20:15,*V2LON,"Finish"      * default display
          MOVE      ZERO,EXIT
          GOTO      EJNL9999
.
.         Validate journal code range
.
EJNL7000  MOVE      ACODE,ECODE
          MATCH     SCODE,ECODE                  * end code < start code ?
          GOTO      EJNL9000 IF NOT LESS         * no
.
          DISPLAY   *P1:24,*EL,*B,"Start code must not be greater than end ":
                    "code. ";
          CALL      HITENTER
          DISPLAY   *P20:13,*EL,*P20:15,*EL
          GOTO      EJNL9500
.
EJNL9000  DISPLAY   *P20:15,*V2LON,ACODE,*HOFF,*P25:15,TDESC
          MOVE      ZERO,EXIT
          GOTO      EJNL9999
.
EJNL9500  MOVE      ONE,EXIT
          GOTO      EJNL9999
.
EJNL9700  MOVE      TWO,EXIT
.
EJNL9999  RETURN
+
.******************************************** Start of Addition        *I-37608
.****************************************************************************
.*                                 SMPR0000            Called by: ML0000    *
.*                   get report selected medical practice                   *
.*    RETURNS:  EXIT   0 = valid code                                       *
.*                     1 = exitchar entered                                 *
.****************************************************************************
.
SMPR0000  MOVE      ZERO,EXIT
.
          DISPLAY   *P29:17,UNDLN6;
          KEYIN     *P29:17,*V2LON,SELPRAC;
.
SMPR2000  CALL      CHKP0000                * get code
          BRANCH    EXIT,SMPR5000:          * nothing/exitchar entered
                         SMPR6000:          * "?" entered
                         SMPR9000:          * valid entry
                         SMPR5000           * blank entry

          GOTO      SMPR0000                * valid code
.
SMPR5000  DISPLAY   *P29:17,*V2LON,"All  "  * default display
          MOVE      SP6,SELPRAC
          MOVE      ZERO,EXIT
          GOTO      SMPR9999
.
.                                           * ------ ? entered ------
.
SMPR6000  MOVE      ZERO,HLEF
          CALL      GETSCR00                * save the screen
.
.                                           * display all medical practices
SMPR6200  CALL      PRIPRADS                * list M.P.'s on file
          BRANCH    OVRCD,SMPR6600
.                                           * keyin the medical practice
SMPR6400  KEYIN     *P1:24,*EL,"From Medical Practice : ",*DV,UNDLN6:
                    *P25:24,*V2LON,SELPRAC;
.
SMPR6600  CALL      PUTSCR00
          GOTO      SMPR2000                * go back - try again
.
.                                           * display selected code
.
SMPR9000  MOVE      PRPRCODE,SELPRAC             * save code
          DISPLAY   *P29:17,*V2LON,SELPRAC,*HOFF,*P36:17,PRPRDESC
          MOVE      ZERO,EXIT
          GOTO      SMPR9999
.
SMPR9500  MOVE      ONE,EXIT
.
SMPR9999  RETURN
+
.**************************************************************************
.*                                  CHKP0000           Called by: SMPR0000*
.*                         See if anything entered                EMPR0000*
.*         Exit - 0 = invalid entry                                       *
.*                1 = nothing/exitchar entered                            *
.*                2 = ? entered                                           *
.*                3 = valid medical practice
.**************************************************************************
CHKP0000  ENDSET    SELPRAC
          APPEND    SP6,SELPRAC
          RESET     SELPRAC
.
          MATCH     SP6,SELPRAC             * anything entered ?
          GOTO      CHKP9500 IF EQUAL       * no
.
          MATCH     EXITCHAR,SELPRAC        * exitchar entered ?
          GOTO      CHKP7000 IF EQUAL       * yes
.
          MATCH     QUEST,SELPRAC           * ? entered ?
          GOTO      CHKP8000 IF EQUAL       * yes
.
.         Medical Practice code entered
.
          MOVE      SELPRAC,KEY6
          CALL      RDPRPR1                  * read the medical practice file
.
          COMPARE   ZERO,OVRCD               * see if on file
          GOTO      CHKP9000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Medical Practice not on file. ";
          CALL      HITENTER
.
          MOVE      ZERO,EXIT
.
          GOTO      CHKP9999
.
.------ Exitchar was entered ------
.
CHKP7000  MOVE      ONE,EXIT
.
          GOTO      CHKP9999
.
.------ ? was entered ------
.
CHKP8000  MOVE      TWO,EXIT
.
          GOTO      CHKP9999
.
.------ we have a valid practice ------
.
CHKP9000  MOVE      THREE,EXIT     
.
          GOTO      CHKP9999
.
.------ no medical practice was entered ------
.
CHKP9500  MOVE      FOUR,EXIT
.
CHKP9999  RETURN
.********************************************   End of Addition        *I-37608
+
.****************************************************************************
.*                                 PROC0000            Called by: ML0000    *
.*                         process journal report                           *
.****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,*P10:24,"Debtor No. :",*P34:24,"Invoice No. :";
.
          MOVE      ZERO,CPAGENO                 * initialise page number
          MOVE      ZERO,CNOUNDLN
          MOVE      ZERO,CLNO                    * initialise line count
          MOVE      ZERO,GRANTOT
          MOVE      ZERO,GSTTOT
          MOVE      ZERO,GSTFLAG
          UNPACK    SDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                      * format start date
          MOVE      CPCDATE,FSDATE
          UNPACK    EDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                      * format end date
          MOVE      CPCDATE,FEDATE
          CALL      IBACLOCK
.
          MATCH     SP3,SCODE                    * starting code blank ?
          GOTO      PROC0500 IF EQUAL            * yes
.
.         See if the starting code entered is an existing code
.
          PACK      KEY5,CODEJ,SCODE             * start code exists ?
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      PROC1500 IF EQUAL            * yes
.
.         Start code doesn't exist, so position in file, then loop through
.         the codes file for each journal type
.
PROC0500  PACK      KEY5,CODEJ,SCODE
          CALL      RDSCODE1                     * position in file
PROC1000  CALL      RDKCODE1                     * read next record
          BRANCH    OVRCD,PROC9500               * end of file
.
          IF        GSTFLAG = 1
            MATCH     THCSCOD,ANSG
            GOTO      PROC1000 IF NOT EQUAL
          ELSE
            MATCH     THCSCOD,ANSG
            GOTO      PROC1000 IF EQUAL
          ENDIF
. 
PROC1200  MATCH     TCODE,CODEJ                  * same category still ?
          GOTO      PROC9500 IF NOT EQUAL        * no - end of print
.
          MATCH     ACODE,ECODE                  * end code < record code ?
          GOTO      PROC9500 IF LESS             * yes - end of print
.
PROC1500  MOVE      ACODE,JCODE                  * save current journal code
          MOVE      TDESC,JDESC                  * & description
.
          CALL      CHCK0000                     * check if data to print
          BRANCH    EXIT,PROC1000
.
.....     CALL      HEAD0000                     * print page header    D-37608
          MOVE      "99",CLNO                    * force new page      *I-37608
          MOVE      ZERO,PRNTTOTL                                      *I-37608
.
          MOVE      ZERO,JTOTAL                  * initialise total for this
.                                                  journal code
.
.         Read in journal entries matching the current code whose dates
.         fall within the specified range
.
          PACK      KEY35,JCODE,SDATE,SP30
          CALL      RSPRJR1                      * position in file
PROC2000  CALL      RKPRJR1                      * read next record
          BRANCH    OVRCD,PROC8000               * end of file
.
          MATCH     JCODE,PRJRCODE               * same journal code ?
          GOTO      PROC8000 IF NOT EQUAL        * no - get next journal code
.
          MATCH     PRJRDATE,EDATE               * end date < journal date ?
          GOTO      PROC8000 IF LESS             * yes - get next journal code
.
          DISPLAY   *P22:24,*V2LON,PRJRDEBT,*P48:24,PRJRINVN;
.
.         Valid record found, so get unique identifier from the invoice record
.
          MOVE      PRJRINVN,KEY8
          CALL      RDPRIN1                      * read record
          BRANCH    OVRCD,PROC9000               * not found
.
          MATCH     SP6,SELPRAC
          IF        @EQUAL
            MOVE      USERID,KEY10
            CALL      RDPRALS1
            COMPARE   ZERO,OVRCD
            GOTO      PROC2500 IF EQUAL       * got all medical practice access
            PACK      KEY16,PRINPRAC,USERID
            CALL      RDPRSEC1
            BRANCH    OVRCD,PROC2000          * no access
          ELSE
            MATCH     PRINPRAC,SELPRAC        * is this correct Priv.Practice?
            GOTO      PROC2000 IF NOT EQUAL
          ENDIF
.
PROC2500  MOVE      ONE,PRNTTOTL
.********************************************   End of Addition        *I-37608
.
.         Now get the corresponding DTR record
.
          PACK      KEY22,PRINUNIQ,PRJRINVN,PRJRTRAN
          CALL      RDPRDT1
          BRANCH    OVRCD,PROC9100
.
.         Check that the DTR record has the correct journal indicators
.
          MATCH     PRDTTTYP,PRJRCODE            * should be same journal code
          GOTO      PROC2000 IF NOT EQUAL        * oops - bug somewhere
.
          COMPARE   PRDTRTYP,THREE               * should be journal entry
          GOTO      PROC2000 IF NOT EQUAL        * oops - bug somewhere
.
.         Get patient's full name
.
          MOVE      "Patient unknown                             ",FULLNAM
          BRANCH    PRJRSCAN,PROC3000            * PMI debtor
.
          PACK      FMTDEBT,SP1,PRJRDEBT
..          MOVE      PRJRDEBT,KEY8
..          CALL      RDPRDB1                      * read debtor record
..          BRANCH    OVRCD,PROC4000               * not found
.
..          MOVE      TWO,PACFRMT
..          MOVE      PRDBSNAM,PACSNAME           * format PMI name
..          MOVE      PRDBGNAM,PACGNAME
..          MOVE      PRDBTITL,PACTITLE
..          CALL      PACNAME
..          MOVE      PACFNAME,FULLNAM
          GOTO      PROC4000
.
PROC3000  PACK      FMTDEBT,ANSP,PRJRDEBT
          MOVE      PRJRDEBT,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1                      * read PMI record
          BRANCH    OVRCD,PROC4000               * not found
.
          MOVE      TWO,PACFRMT
          MOVE      PSNAME,PACSNAME           * format PMI name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,FULLNAM
.
PROC4000  COMPARE   FIFTY7,CLNO                  * page full ?
          GOTO      PROC5000 IF LESS             * no
.
          CALL      HEAD0000                     * print new page header
.
PROC5000  CALL      DISP0000                     * valid record so print
          GOTO      PROC2000                     * get next record
.
.         Display totals for this journal type before proceeding
.         to the next journal type
.
PROC8000  COMPARE   ONE,PRNTTOTL                 * need a total?       *I-37608
          IF        @EQUAL
            CALL      LINE0000
            PRINT     *1,PIPE,*66,"TOTAL",*72,PIPE,*74,JTOTAL,*132,PIPE
          ENDIF                                                        *I-37608
.
          IF        GSTFLAG = 0
            ADD       JTOTAL,GRANTOT
          ENDIF
.
          IF        GSTFLAG = 1
            ADD       JTOTAL,GSTTOT
          ENDIF
.
          GOTO      PROC1000
.
.         Invoice not found
.
PROC9000  DISPLAY   *P1:23,*EL,*B,"Invoice ",*V2LON,PRJRINVBN,*HOFF:
                    " not found. ";
          GOTO      PROC2000
.
.         DTR record not found
.
PROC9100  DISPLAY   *P1:23,*EL,*B,"Transaction ",*V2LON,PRJRTRAN,*HOFF:
                    " for invoice ",*V2LON,PRJRINVN,*HOFF," not found. ";
          GOTO      PROC2000
.
.         End of print
.
PROC9500  IF        CPAGENO=0
            CALL      HEAD0000                     * print new page header
          ENDIF
          IF        GSTFLAG = 0   
            CALL      LINE0000
            PRINT     *1,PIPE,*58,"JOURNAL TOTAL",*72,PIPE,GRANTOT,*132,PIPE
            MOVE      ONE,GSTFLAG
            GOTO      PROC0500
          ENDIF
.
          IF        GSTFLAG = 1
            ADD       GSTTOT,GRANTOT
            CALL      LINE0000
            PRINT     *1,PIPE,*54,"GST JOURNAL TOTAL",*72,PIPE,GSTTOT,*132,PIPE
          ENDIF
.
          CALL      LINE0000
          PRINT     *1,PIPE,*60,"GRAND TOTAL",*72,PIPE,GRANTOT,*132,PIPE        
          CALL      LINE0000
          DISPLAY   *P1:24,*EL,*V2LON,"** Report Generated **  ";
          PRINT     *N,*1,"*** End of Report ***"
.
PROC9999  RETURN
+
.****************************************************************************
.*                            CHCK0000                                      *
.*       Check to see if there is any data to print for this journal code   *
.****************************************************************************
CHCK0000  MOVE      FALSE,EXIT
          PACK      KEY35,JCODE,SDATE,SP30
          CALL      RSPRJR1                      * position in file
.
          CALL      RKPRJR1                      * read next record
          BRANCH    OVRCD,CHCK9000               * end of file
.
          MATCH     JCODE,PRJRCODE               * same journal code ?
          GOTO      CHCK9000 IF NOT EQUAL        * no - get next journal code
.
          MATCH     PRJRDATE,EDATE               * end date < journal date ?
          GOTO      CHCK9000 IF LESS             * yes - get next journal code
.
          GOTO      CHCK9999
.
.------ set the exit flag ------
.
CHCK9000  MOVE      TRUE,EXIT
.
CHCK9999  RETURN
+
.****************************************************************************
.*                            DISP0000                 Called by: PROC0000  *
.*                valid record so display it                                *
.****************************************************************************
.
DISP0000  UNPACK    PRJRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      PRDTDESC,SHDESC
          IF        GSTFLAG = 1
            ADD       PRDTGSTM,PRDTAMNT
          ENDIF
.
          MOVE      PRDTAMNT,FORM8P2
          PRINT     *1,PIPE,*3,FULLNAM,*37,PIPE,*40,FMTDEBT,*49,PIPE:
                    *51,PRINPRAC,*59,PIPE:
                    *61,CPCDATE,*72,PIPE,*74,FORM8P2,*86,PIPE,*88,SHDESC:
                    *109,PIPE,*111,PRJRINVN,*120,PIPE,*122,PRDTBATC,*132,PIPE
.
          ADD       PRDTAMNT,JTOTAL              * increment journal total
          ADD       ONE,CLNO                     * increment line count
.
DISP9999  RETURN
+
.****************************************************************************
.*                            HEAD0000                 Called by: PROC0000  *
.*                       print page heading                                 *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          PRINT     *40,"FROM ",FSDATE,*58,"TO ",FEDATE,*N,*N:
                    *40,"FOR JOURNAL TYPE ",JCODE," - ",JDESC          *C-37608
.
.******************************************** Start of Addition        *I-37608
          MATCH     SP6,SELPRAC
          IF        @EQUAL
            PRINT     *40,"ALL PRIVATE PRACTICE"
          ELSE
            PRINT     *40,"FOR PRIVATE PRACTICE ",SELPRAC," - ",PRPRDESC
          ENDIF
.********************************************   End of Addition        *I-37608
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Patient Name",*37,PIPE," Debtor No",*49,PIPE:
                    " Med. ",*59,PIPE:
                    " Date of",*72,PIPE," Amount",*86,PIPE," Description":
                    *109,PIPE," Invoice",*120,PIPE," Batch",*132,PIPE,*N:
                    *1,PIPE,*37,PIPE,*49,PIPE," Prac. ",*59,PIPE:
                    " Journal",*72,PIPE,*86,PIPE:
                    *109,PIPE," Number",*120,PIPE," Number",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      NINE,CLNO                    * set line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: PROC0000  *
.*                      draw line across page                     HEAD0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
          ADD       ONE,CLNO
.
LINE9999  RETURN
+
. =========================================================================
.     I/O includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCODKY
          INC       PRIPRADS                                           *I-37608
.
          INC       PRIJRNIO/INC                 * journals file
          INC       PRIDTRIO/INC                 * debtors transaction file
..          INC       PRIDBTIO/INC                 * debtor file
          INC       PRIINVIO/INC                 * invoice file
          INC       PRIPRAIO/INC                 * practice file       *I-37608
          INC       PRIALSIO/INC                 * Medical Prac.all user access
          INC       PRISECIO/INC                 * Medical Prac.security access
.
          INC       PATMA1IO/INC                 * patient master file
          INC       PATCODIO/INC                 * codes file
          INC       PATDRGIO/INC                 * date range file

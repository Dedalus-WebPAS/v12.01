.*****************************************************************************
.*    Program      : FXPATTRN                                                *
.*    Description  : Checking Claim code in pattranf file                    *
.*                                                                           *
.*    Author       : J.Tan                                                   *
.*    Date         : 16/03/2013                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
.
.
COUNT     FORM      8
RECNUM    FORM      8
.
PRGNAM    INIT      "Checking Claim in pattranf "
PRGID     INIT      "FXPATTRN"
VERSION   INIT      "V12.01.00"
.*****************************************************************************
.
. Start of Program
.
          CALL      INIT0000                      * init and open files
.
MAIN0000  CALL      OPTN0000
          BRANCH    EXIT,MAIN9999
          BRANCH    OPTION,MAIN1000
          GOTO      MAIN1000
.
MAIN1000  CALL      PROC0000
          GOTO      MAIN0000
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATTRAN2,"pattranf"
.
INIT9999  RETURN
.
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run - listing only":
                    *P1:6,*V2LON,"2",*HOFF," = Run - fixit":
                    *P1:8,"Select Option :";
.         
OPTN1000  KEYIN     *P17:8,*DV,UNDLN2,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                               PROC0000                                     *
.******************************************************************************
PROC0000  CALL      HEAD0000
          MOVE      "2",KEY1
          PACK      KEY9,KEY1,SP70                * Start from Current Admission
          CALL      RDSMISS2
PROC1000  CALL      RDKMISS2
          BRANCH    OVRCD,PROC9999                * eof - finished
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2                      * position at start of file
          CALL      RDPTRAN2                      * read next record
          BRANCH    OVRCD,PROC1000                * eof - finished
          MATCH     DAADMNO,DTADMN
          GOTO      PROC1000 IF NOT EQUAL
.
          ADD       ONE,RECNUM                    * increment record counter
.
          IF        (RECNUM%1000) = 0 | (RECNUM%1000) = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MATCH      ACLAIM,PTTRCLTY
          GOTO       PROC1000 IF EQUAL
.
          PRINT      *2,AADMNO,*22,ACLAIM,*42,PTTRCLTY
.
          IF        OPTION=2
            MOVE      ACLAIM,PTTRCLTY              * set claim code
            CALL      UPTRAN1
          ENDIF
          GOTO      PROC1000
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               HEAD0000                                    *
.*****************************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD132
          CALL      LINE0000
          PRINT     *1,"|",*3,"Adm.Number":
                    *20,"|",*22,"Adm.Claim":
                    *40,"|",*42,"Tran.Claim"
          CALL      LINE0000
          MOVE      SEVEN,CLNO
HEAD9999  RETURN
+
.*****************************************************************************
.*                            LINE0000                 Called by: PROC0000   *
.*                      Draw line across page                     HEAD0000   *
.*****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
.
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       STD001IO

.******************************************************************************
.* System         : Waiting Lists                                             *
.* Program        : IBAWAT03.PBL                                              *
.* Function       : Input Outcome of Admission                                *
.******************************************************************************
.* Author         : ROD & LOFTY ARE GAY   09/02/93                            *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.00.01 16/05/2025  J.Tan          TSK 0955096                           *
.*           Added alpanumeric visit number generation                        *
.* V11.02.01 10/02/2022  Thanh T       TSK 0889899                            *
.*           Recompiled as WATTR1FD changes                                   *
.* V10.02.01 07/07/2011  Steve Armstrong  CAR 240722                          *
.*           Added RDPMPX21 prior to calling PMIHEAD                          *
.******************************************************************************
.* V9.03.01  13/08/2003  Sandra Barcham    42274                              *
.*           Defined WATHI002 with tilda's to put current date into           *
.*           WTHIEDAT to stop I * D                                           *
.******************************************************************************
.* V9.02.02  27/02/2003  Jill Habasque SRF 34660                              *
.*           Added WTCNBRSR                                                   *
.* V9.02.01  13/02/2003  Lina Vo      SRF 22709                               *
.*           Removed open of outpreaf - not used                              *
.******************************************************************************
.*        V5.08.06  08/12/2000  Ebon Clements                                 *
.*                  Fixed 509  QA problems                                    *
.*        V5.08.05  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.04  27/07/2000  Jill Habasque                                 *
.*                  Recompiled for WLHISSUB                                   *
.*        V5.08.03  17/07/2000  Jill Habasque                                 *
.*                  Recompiled for WLHISSUB                                   *
.*        V5.08.02  29/06/2000  Jill Habasque                                 *
.*                  Recompiled for WATHISFD                                   *
.*        V5.08.01  08/06/2000  Tonii Tang                                    *
.*                  Booking Status Code set to "05" (deferred booking) if     *
.*                  Waiting List Status changes to 1                          *
.******************************************************************************
.*        V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                   Fixed global recompile bugs                              *
.******************************************************************************
.*        V5.07.01  12/01/1999  Jill Habasque                                 *
.*                  Changed to display century                                *
. *       V5.04.02  15/04/1997  howellsy   RHA                                *
. *                 Write to W/L History                                      *
.*        V5.04.01  03/10/1996  Howellsy          EOC & ACC                   *
.*                  Generate Booking No when placed on the Waiting List       *
.******************************************************************************
.*                  V5.01.02  16/03/93  ROD                                   *
.*                            Added security access on a treatment record     *
.*                  V5.01.03  26/03/93  WHIRLPOOL WORLDS BEST SURFER          *
.*                            Fixed lofty bug                                 *
.*                  V5.01.04  22/04/93 DIG                                    *
.*                            Re-Compiled for changes to PATALERT.            *
.*                  V5.01.05  11/05/93  Steve O'Gorman                        *
.*                            Changed "Date to Return to List" to             *
.*                            "New Waiting List Date".                        *
.*                  V5.01.06  26/08/93  ROD                                   *
.*                            Re-Compiled for NZHIS                           *
.*                  V5.01.07  13/01/1994  Steve Armstrong                     *
.*                            Fixed bugs for global recompile                 *
.*                  V5.01.08  17/03/94  ROD           SRF 440709              *
.*                            Dont create new wattr1af rec, update existing 1 *
.*                  V5.01.09  25/03/94  Paul           SRF 440786              *
.*                            Keyin WMDATE2 with performed date.              *
.*                  V5.01.10  20/05/94  Paul Howells  SRF 440870              *
.*                            Fixed error concerning proc security.           *
.*                  V5.01.11  06/09/94  Paul Howells  SRF 440870              *
.*                            Read watpnpaf for not performed patients        *
.*                  V5.03.01  04/04/95  Max White     SRF 441238              *
.*                            Only allow outcoming for admissions.            *
.*                  V5.03.02  18/05/95  Max White     SRF 441295              *
.*                            After selecting a procedure for a given U/R No. *
.*                            ensure that any procedure not performed details *
.*                            displayed are for the correct booking.          *
.******************************************************************************
.
.         FD's required
.
          INC       STD001FD/PBL
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       BOKRC1FD/INC            * booking file
          INC       OUTPREFD/INC            * OP pre-attendance file
          INC       PATALRFD/INC
          INC       PATCODFD/INC
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATDHEAD/INC
          INC       PATMA1FD/INC            * patient master file
          INC       PATDSCFD/INC            * admission file
          INC       PATMI1FD/INC            * admission file
          INC       PATNIDFD/INC
          INC       PATPR1FD/INC            * pre-admission file
          INC       PATGSRFD/INC            * surname file
          INC       PATVISFD/INC            * surname file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       WATCONFD/INC
          INC       WATPNPFD/INC            * procedures not performed
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATTR1FD/INC
          INC       PATCALAG/INC
          INC       WATHISFD/INC
          INC       WATNBRFD/INC
          INC       WATPROFD/INC
          INC       WEBSECFD/INC
          INC       NZHISIFD/INC                 * Include for NZHIS vars
.
ADDFLAG   DIM       1
ADDWLFLG  FORM      1
BOOKNO    DIM       8
BJDAY     FORM      4
CJDAY     FORM      4
CNTITEM   FORM      2
CODE      DIM       2
.
CODEOUTC  DIM       3
CODEREAS  DIM       3
CODECOMM  DIM       50
CODEDNPF  DIM       8
.
MAXITEMS  FORM      "20"
ITEMS     DIM       19[20]    * displayed procedures
.
CALLPOSN  FORM      1
DIM25     DIM       25
DIM30     DIM       30
FORM3     FORM      3
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
NEWCNT    FORM      2         * new procedure count
NMPNUMB   DIM       20
OUTINDIC  DIM       1
.
PROCCODE  DIM       9
PROCCNT   FORM      2
PROCDESC  DIM       50
.
SCNDGNAM  DIM       40
STAT1     INIT      "Not Performed"
STAT2     INIT      "Scheduled    "
STAT3     INIT      "Pre-admitted "
STAT4     INIT      "Admitted     "
STAT5     INIT      "Discharged   "
STAT6     INIT      "Removed      "
STAT7     INIT      "Performed    "
STAT8     INIT      "Not Performed"
STATUS13  DIM       13
TODAY     DIM       8
UNDLN50   INIT      "_________________________":
                    "_________________________"
URNUMBER  DIM       8
WATHI002  INIT      "~~~~~~~~"
WMDAT18   DIM       10
WMDAT28   DIM       10
Z8        INIT      "zzzzzzzz"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGNAM    INIT      "Input Outcome of Admission"
PRGID     INIT      "IBAWAT03"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*  ML0000  :  Mainline code                                         *
.*********************************************************************
ML0000    CALL      INIT0000                      * initialisation
          BRANCH    EXIT,ML9999                   * not using new W/L Treat file
.
ML1000    CALL      EURN0000                      * keyin U/R Number
          BRANCH    EXIT,ML9999                   * nothing input
.
ML2000    CALL      GPRO0000                      * get Procedure code
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      SAVHIS00                      * save history vars
.
          CALL      DISP0000                      * display screen
          CALL      INPT0000                      * insert fields
          BRANCH    EXIT,ML2000                   * nothing input
          CALL      CHNG0000                      * change fields
          BRANCH    EXIT,ML2000                   * nothing input
.
          CALL      POST0000                      * Post details
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*  INIT0000  :  Initialisation                                      *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"          * files to open
          READ      CONTROLF,THIRTY7;*211,WTCNWLSC
.
          IF        WTCNWLSC=1
            DISPLAY   *P58:24,"watseaaf";
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P58:24,"watseiaf";
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
.
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P58:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"
          DISPLAY   *P58:24,"patalrtf"
          OPEN      PATALRT1,"patalrtf"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          DISPLAY   *P58:24,"watpnpaf"
          OPEN      WATPNPA1,"watpnpaf"
          OPEN      WATPNPA2,"watpnpaf"
.
          READ      CONTROLF,FIFTY9;*231,PTCNRGNS
          READ      CONTROLF,THIRTY7;*216,WTCNDRNP
          OPEN      PATGSRN1,"patgsrnf"     * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          MOVE      ONE,CNEWDS
          MOVE      ZERO,EXIT
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
INIT9999  RETURN
+
.*********************************************************************
.*  EURN0000  : Enter the UR number                                  *
.*********************************************************************
EURN0000  DISPLAY   *P1:4,*EF,"U/R Number : "
          MOVE      "14",CCOL
          MOVE      "4",CVERT
.
          CALL      KURN
          BRANCH    EXIT,EURN9999           * nothing entered
          BRANCH    OVRCD,EURN0000          * invalid
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,EURN0000
.
          MOVE      PURNO,URNUMBER
          CALL      PMIHEAD
.
EURN9999  RETURN
+
REDISPS   DISPLAY   *P1:4,*EF,"U/R Number : "
GETSVAR   RETURN
+
.*********************************************************************
.*  INPT0000  :  Input details                                       *
.*********************************************************************
INPT0000  MOVE      ZERO,EXIT
          MATCH     SP3,WTNPOUTC            * go straight to change mode?
          GOTO      INPT9999 IF NOT EQUAL
.
          CALL      OUTC0000                * keyin outcome
          BRANCH    EXIT,INPT9000           * EXITCHAR
.
          CALL      PDTE0000                * keyin performed date 
          BRANCH    EXIT,INPT9000           * EXITCHAR
.
          CALL      REAS0000                * keyin reason
          BRANCH    EXIT,INPT9000           * EXITCHAR
.
          CALL      COMM0000                * keyin comments
.
          CALL      DATE0000                * enter date returned to list
          GOTO      INPT9999
.
INPT9000  PACK      KEY19,URNUMBER,PROCCODE,PROCCNT
          CALL      RDTREA1
          CALL      UPTREA1                 * unlock treatment file
.
INPT9999  RETURN
+
.*********************************************************************
.*  CHNG0000  :  Change details                                      *
.*********************************************************************
CHNG0000  MOVE      ZERO,EXIT
          CALL      MAINQST                       * Select item,post,cancel?
          COMPARE   ZERO,CCITEM
          GOTO      CHNG9500 IF EQUAL             * Post
          GOTO      CHNG9000 IF LESS              * Cancel
.
          BRANCH    CCITEM,CHNG1000,CHNG1500,CHNG2000,CHNG3000,CHNG4000
          BEEP
          GOTO      CHNG0000
.
CHNG1000  CALL      OUTC0000                * keyin outcome
          BRANCH    EXIT,CHNG9000,CHNG2000  * EXITCHAR
          GOTO      CHNG1500
.
CHNG1500  CALL      PDTE0000                * keyin performed date 
          BRANCH    EXIT,CHNG9000           * EXITCHAR
          GOTO      CHNG0000                * enter the date now
.
CHNG2000  CALL      REAS0000                * keyin reason
          BRANCH    EXIT,CHNG9000           * EXITCHAR
          GOTO      CHNG4000                * enter the date now
.
CHNG3000  CALL      COMM0000                * keyin comments
          GOTO      CHNG0000
.
CHNG4000  CALL      DATE0000                * enter date if not performed
          GOTO      CHNG0000
.
CHNG9000  MOVE      ONE,EXIT
          PACK      KEY19,URNUMBER,PROCCODE,PROCCNT
          CALL      RDTREA1
          CALL      UPTREA1                 * unlock treatment file
          GOTO      CHNG9999
.
.         check if procedure performed
.
CHNG9500  MATCH     ANSP,OUTINDIC
          GOTO      CHNG9999 IF NOT EQUAL   * not performed
.
.         if procedure performed, patient must be admitted
.
          MOVE      BOOKNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,CHNG9600          * not admitted
          BRANCH    ASTAT,CHNG9600          * not admitted
          GOTO      CHNG9999
.
CHNG9600  DISPLAY   *P1:24,*B,"Patient must be admitted for the procedure ":
                    "to be performed.  ",*EL;
          CALL      HITENTER
          GOTO      CHNG0000
.
CHNG9999  RETURN
+
.*********************************************************************
.*  DISP0000  :  Display screen                                      *
.*********************************************************************
DISP0000  CALL      PMIHEAD
          DISPLAY   *P1:5,"Booking No   : ",*V2LON,BOOKNO,*HOFF:
                    *P5:7,"Procedure Code  : ",*V2LON,PROCCODE,"/",PROCCNT:
                    *HOFF,SP5,PROCDESC
          DISPLAY   *P1:09,*V2LON," 1",*HOFF,". Outcome                : ":
                    *P1:10,*V2LON," 2",*HOFF,". Performed Date         : ":
                    *P1:11,*V2LON," 3",*HOFF,". Reason not performed   : ":
                    *P1:12,*V2LON," 4",*HOFF,". Comments               : ":
                    *P1:13,*V2LON," 5",*HOFF,". New Waiting List Date  : "
.
. if details have already been input for this proc, display details
.
          MATCH     SP3,CODEOUTC                  * insert or change mode?
          GOTO      DISP9999 IF EQUAL
.
          MOVE      "Invalid Code",TDESC
          MOVE      SP1,TCINDC1                   * assume performed
          PACK      KEY5,ANSW,ANSO,CODEOUTC
          CALL      RDCODE1
          MOVE      TCINDC1,OUTINDIC              * save indicator
          DISPLAY   *P30:9,*V2LON,CODEOUTC,*HOFF,SP4,TDESC
.
. display reason only if procedure was not performed
.
          MATCH     "P",OUTINDIC
          GOTO      DISP4000 IF EQUAL       * procedure performed
.
          MOVE      "Invalid Code",TDESC
          PACK      KEY5,ANSW,ANSP,CODEREAS
          CALL      RDCODE1
          DISPLAY   *P30:11,*V2LON,CODEREAS,*HOFF,SP4,TDESC
.
. display comments field
.
DISP4000  UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P30:10,*V2LON,CPCDATE
          DISPLAY   *P30:12,*V2LON,CODECOMM
          UNPACK    CODEDNPF,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P30:13,*V2LON,CPCDATE
.
DISP9999  RETURN
+
.*********************************************************************
.*  OUTC0000  :  Keyin Outcome                                       *
.*********************************************************************
OUTC0000  MOVE      "WO",CODE
          MOVE      CODEOUTC,CKYIDEF3
          MOVE      "9",EVERT
          MOVE      "30",ECOL
          MOVE      ONE,CKYIMAND
.
          CALL      PATCODKY
          IF        EXIT=2
            CALL      PMIHEAD
            MOVE      ONE,EXIT                    * EXITCHAR
            GOTO      OUTC9999
          ENDIF
.
          MOVE      TWO,EXIT                * set to keyin reason if not perf.
          MATCH     "P",TCINDC1             * performed (proc)?
          IF        @EQUAL
            UNPACK    SP20,CODEREAS,CODEDNPF
            DISPLAY   *P30:11,*EL
            DISPLAY   *P30:13,*EL
            MOVE      ZERO,EXIT               * performed
          ELSE
            DISPLAY   *P30:10,*EL
          ENDIF
.
          MOVE      ACODE,CODEOUTC
          MOVE      TCINDC1,OUTINDIC        * save indicator
          DISPLAY   *P30:EVERT,*V2LON,ACODE,*HOFF,SP4,TDESC
.
OUTC9999  RETURN
+
.*********************************************************************
.*  REAS0000  :  Keyin Reason                                        *
.*********************************************************************
REAS0000  MATCH     "P",OUTINDIC            * Procedure Performed?
          GOTO      COMM9999 IF EQUAL       * Yes
.
          MOVE      "WP",CODE
          MOVE      CODEREAS,CKYIDEF3
          MOVE      "11",EVERT
          MOVE      "30",ECOL
          MOVE      ZERO,CKYIMAND
.
          CALL      PATCODKY
          IF        EXIT=2
            CALL      PMIHEAD
            MOVE      ONE,EXIT                * EXITCHAR
            GOTO      REAS9999
          ENDIF
.
          MOVE      ACODE,CODEREAS
          DISPLAY   *P30:EVERT,*V2LON,ACODE,*HOFF,SP4,TDESC
          MOVE      ZERO,EXIT
.
REAS9999  RETURN
+
.*********************************************************************
.*  COMM0000  :  Keyin Comments                                      *
.*********************************************************************
COMM0000  KEYIN     *P30:12,*V2LON,*RV,CODECOMM:
                    *P30:12,*DV,CODECOMM
          PACK      CODECOMM,CODECOMM,SP30,SP20
.
COMM9999  RETURN
+
.*********************************************************************
.         enter the date procedure was performed 
.*********************************************************************
PDTE0000  MOVE      ZERO,EXIT 
          MATCH     "P",OUTINDIC            * Procedure Performed?
          GOTO      PDTE9000 IF NOT EQUAL       * NO 
.
          MOVE      BOOKNO,KEY8           * returned to list on same date
          CALL      RDBKREC1                
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
.
          MOVE      "30",CCOL
          MOVE      "10",CVERT
          CALL      KEYDATE
          BRANCH    CQUEST,PDTE0000
          BRANCH    OVRCD,PDTE0000
.
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          MATCH     KEY8,TODAY
          GOTO      PDTE4000 IF NOT LESS    * date is valid
.
          DISPLAY   *P1:24,*B,"Date can't be in the future.  ",*EL;
          CALL      HITENTER
          GOTO      PDTE0000
.
PDTE4000  MATCH     WMDATE1,KEY8
          GOTO      PDTE8000 IF LESS
. 
          MOVE      KEY8,WMDATE2
          GOTO      PDTE9999
.
PDTE8000  DISPLAY   *P1:24,*B,"Performed Date can't be before Date on List. ";
          CALL      HITENTER
          GOTO      PDTE0000
.
PDTE9000  DISPLAY   *P30:10,*EL;   
          MOVE      SP8,WMDATE2
.
PDTE9999  RETURN
+
.*********************************************************************
.         enter the date procedure is to be returned to the list on
.         if the procedure is not performed
.*********************************************************************
DATE0000  MATCH     "P",OUTINDIC            * Procedure Performed?
          GOTO      DATE9000 IF EQUAL       * Yes
.
          UNPACK    CODEDNPF,CCENT,CYEAR,CMON,CDAY
          MATCH     SP8,CODEDNPF
          IF        @EQUAL
            UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          ENDIF
.
          MOVE      "30",CCOL
          MOVE      "13",CVERT
          CALL      KEYDATE
          BRANCH    CQUEST,DATE0000
          BRANCH    OVRCD,DATE0000
.
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          MATCH     KEY8,TODAY
          GOTO      DATE4000 IF NOT LESS    * date is valid
.
          DISPLAY   *P1:24,*B,"Date can't be in the future.  ",*EL;
          CALL      HITENTER
          GOTO      DATE0000
.
DATE4000  MOVE      KEY8,CODEDNPF
          GOTO      DATE9999
.
DATE9000  DISPLAY   *P30:13,*EL;   
          MOVE      SP8,CODEDNPF
.
DATE9999  RETURN
+
.*********************************************************************
.*  POST0000  :  Post the details                                    *
.*********************************************************************
POST0000  DISPLAY   *P1:24,*EL,*P35:24,"Posting...";
.
          MATCH     ANSP,OUTINDIC
          IF        @EQUAL
            MOVE      SEVEN,WMSTAT            * set as performed
            MOVE      BOOKNO,WMBOOK
          ELSE
            PACK      KEY19,WMURNO,WMCODE,WMCNT
            CALL      GENBOKNO           * generate booking number
            MOVE      ONE,WMSTAT            * set as not-performed
            MOVE      WTCNDRNP,WMREMOVE       * set removal reason
            MOVE      "05",WTWMBSCD           * defer booking
            PACK      WMDATE4,CCC,CYY,CMM,CDD
            REP       " 0",WMDATE4            * date cancelled/removed
          ENDIF
          CALL      UPTREA1
          CALL      PPNP0000                * post/delete watpnpaf if needed
.
          CALL      WRTHIS00                * write history
.
POST9999  RETURN
+
.
.         I/O includes needed
.
          INC       STD001IO/PBL
          INC       BOKRC1IO/INC            * booking file
          INC       PATCODKY
          INC       OUTPREIO/INC            * OP pre-attendance file
          INC       PATALERT
          INC       PATALRIO/INC
          INC       PATCOMN1/PBL            * holds KURN
          INC       PATCODIO/INC
          INC       PATMA1IO/INC            * patient master file
          INC       PATDSCIO/INC            * admission file
          INC       PATMI1IO/INC            * admission file
          INC       PATNIDIO/INC
          INC       PATPR1IO/INC            * pre-admission file
          INC       PATGSRIO/INC            * surname file
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATSNDX/PBL             * ? for surnames only
          INC       PATSNX2/PBL             * ? for surnames/given names
          INC       PMIHEAD
          INC       WATTR1IO/INC
          INC       WATPNPIO/INC            * procedures not performed
          INC       CALCAGE
          INC       GENBOKNO           * generate booking number
          INC       WEBSECIO/INC
          INC       WATHISIO/INC
          INC       WATNBRIO/INC
          INC       WATPROIO/INC
          INC       WLHISSUB    
..............................................................................
.*********************************************************************
.         post to watpnpaf
.*********************************************************************
PPNP0000  MATCH     ANSP,OUTINDIC
          GOTO      PPNP8000 IF EQUAL       * procedure performed
.
. write to patpnpaf
.
          MOVE      BOOKNO,WTNPBOOK
          MOVE      WTNPBOOK,KEY8
          CALL      RDWTPNP1
.
          MOVE      WMURNO,WTNPURNO         * ur number
          MOVE      WMCODE,WTNPCODE         * procedure code
          MOVE      WMCNT,WTNPCNT           * procedure count
          MOVE      CODEOUTC,WTNPOUTC       * outcome
          MOVE      CODEREAS,WTNPREAS       * reason
          MOVE      CODECOMM,WTNPCOMM       * reason
          MOVE      CODEDNPF,WTNPDRET       * new date-on-list
          PACK      WTNPDCHG,CCC,CYY,CMM,CDD
          REP       " 0",WTNPDCHG           * date status changed
          MOVE      SP2,WTNPNCNT            * clear new count
.
          IF        OVRCD = ONE
            CALL      WRWTPNP1
            CALL      RDWTPNP1
          ELSE
            CALL      UPWTPNP1
          ENDIF
.
          MATCH     CODEDNPF,WMDATE1    * see if returned on list on same date
          IF        @EQUAL
            MOVE      WTNPBOOK,KEY8           * returned to list on same date
            CALL      RDBKREC1                * clear certain bokrec fields
            IF        OVRCD = ZERO
              UNPACK    SP30,BKREPROC,BKRERFGP,BKREGPPC
              MOVE      ZERO,BKRECNT
              CALL      UPBKREC1
            ENDIF
          ELSE
            MOVE      CODEDNPF,WMDATE1     * set new date on list
          ENDIF
.
          MOVE      ONE,WMSTAT              * reset the status to un-sched.
          CALL      UPTREA1                 * update original record
          MOVE      SP2,WTNPNCNT            * clear new count variable
.
          GOTO      PPNP9999
.
. delete from watpnpaf if we need
.
PPNP8000  MOVE      BOOKNO,WTNPBOOK
          MOVE      WTNPBOOK,KEY8
          CALL      RDWTPNP1
          BRANCH    OVRCD,PPNP9999
          CALL      DEWTPNP1
. 
PPNP9999  RETURN
+
.*********************************************************************
.         get the latest procedure count for the patient
.*********************************************************************
GLPC0000  MOVE      ZERO,NEWCNT
          MOVE      "zz",KEY2
          PACK      KEY19,WTNPURNO,WTNPCODE,KEY2
          CALL      RDSTREA1
          CALL      RDPTREA1
          BRANCH    OVRCD,GLPC5000
          PACK      KEY17,WMURNO,WMCODE
          MATCH     KEY17,KEY19
          GOTO      GLPC5000 IF NOT EQUAL
          MOVE      WMCNT,NEWCNT
GLPC5000  ADD       ONE,NEWCNT
GLPC9999  RETURN
+
.*********************************************************************
.*                  GPRO0000                    Called by : xxxx0000 *
.*        Display items from the wattr1af file                       *
.*********************************************************************
GPRO0000  CALL      CLRA0000                * clear storage variables
          MOVE      ONE,CPAGENO             * set page
          MOVE      ZERO,CNTITEM            * clear count
          MOVE      "7",CVERT               * starting line
          DISPLAY   *P1:5,SP20,SP3,*P1:7,*EF
          DISPLAY   *ULON,*V2LON:
                    *P1:CVERT,"Lne":
                    *P5:CVERT,"Code     ":
                    *P15:CVERT,"Description               ":
                    *P42:CVERT,"Status       ":
                    *P56:CVERT,"On List   ":
                    *P67:CVERT,"Review    ":
                    *P78:CVERT,"Pty";
.
          PACK      KEY19,URNUMBER,SP20
          CALL      RDSTREA1
.
. ******* loop over file *******
.
GPRO1000  CALL      RDKTREA1
          BRANCH    OVRCD,GPRO5000          * end of file
.
          MATCH     URNUMBER,WMURNO
          GOTO      GPRO5000 IF NOT EQUAL   * diff. patient
.
. include procedures not performed
.
          IF        WMSTAT = 1
            PACK      KEY27,WMURNO,WMCODE,WMCNT,Z8
            CALL      RSWTPNP2
            CALL      RPWTPNP2
            BRANCH    OVRCD,GPRO1000              * ignore unscheduled
.
            MATCH     WMURNO,WTNPURNO
            GOTO      GPRO1000 IF NOT EQUAL
.
            MATCH     WMCODE,WTNPCODE
            GOTO      GPRO1000 IF NOT EQUAL
.
            COMPARE   WMCNT,WTNPCNT
            GOTO      GPRO1000 IF NOT EQUAL
          ENDIF
.
. check if user has security access to this record
.
          COMPARE   ONE,WTCNWLSC                  * using security?
          GOTO      GPRO2000 IF NOT EQUAL
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          COMPARE   ZERO,OVRCD                    * ALL Access
          GOTO      GPRO2000 IF EQUAL
          PACK      KEY13,PASSCODE,WMDOCTOR,WMUNIT
          CALL      RDWTSEI2
.
          BRANCH    OVRCD,GPRO1000                * no access
.
. ******* increment counters *******
.
GPRO2000  ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      GPRO4000 IF NOT LESS    * need a new page
          ADD       ONE,CNTITEM             * add to item counter
.
. ******* display data and store key *******
.
GPRO3000  UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,WMDAT18
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,WMDAT28
          LOAD      STATUS13,WMSTAT,STAT1,STAT2,STAT3,STAT4:
                                    STAT5,STAT6,STAT7,STAT8
          MOVE      WMDESC,DIM25
          DISPLAY   *P1:CVERT,*V2LON,CNTITEM,*HOFF,DOT,*P5:CVERT,WMCODE:
                    *P15:CVERT,DIM25,*P42:CVERT,STATUS13,*P56:CVERT,WMDAT18:
                    *P67:CVERT,WMDAT28,*P78:CVERT,WMPTY;
          PACK      ITEMS[CNTITEM],WMURNO,WMCODE,WMCNT
          GOTO      GPRO1000
.
. ******* new screen needed *******
.
GPRO4000  BRANCH    CPAGENO,GPRO4500
.
.         Middle pages : (N)ext & (P)revious
.
GPRO4100  MOVE      ONE,CALLPOSN            * set position
          CALL      KEYA0000
          BRANCH    EXIT,GPRO7000,GPRO0000,GPRO9100
.                        Next     First    Exit
          GOTO      GPRO6000
.
.         first page : (N)ext
.
GPRO4500  MOVE      TWO,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,GPRO7000,GPRO5900,GPRO9100
.                        Next     First    Exit
          GOTO      GPRO6000
.
. ******* no more data to display *******
.
GPRO5000  COMPARE   ZERO,CNTITEM
          GOTO      GPRO8900 IF EQUAL       * nothing on file
.
          BRANCH    CPAGENO,GPRO5500
.
.         last page : (P)revious
.
GPRO5100  MOVE      THREE,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,GPRO5900,GPRO0000,GPRO9100
.                        Next     First    Exit
          GOTO      GPRO6000
.
.         first page :
.
GPRO5500  MOVE      FOUR,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,GPRO5900,GPRO5900,GPRO9100
.                        Next     First    Exit
          GOTO      GPRO6000
.
GPRO5900  BEEP   
GPRO5910  BRANCH    CALLPOSN,GPRO4100,GPRO4500,GPRO5100,GPRO5500
.
. ******* item on screen selected *******
.
GPRO6000  MOVE      ITEMS[FORM2],KEY19
          CALL      RLWTTRE1                  * read and lock this record 
          BRANCH    OVRCD,GPRO5900,GPRO8800
.
          MOVE      WMCODE,PROCCODE
          MOVE      WMCNT,PROCCNT
          MOVE      WMDESC,PROCDESC
          MOVE      WMBOOK,BOOKNO
.
.         patient must be admitted
.
          MOVE      BOOKNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,GPRO8700          * not admitted
          BRANCH    ASTAT,GPRO8700          * not admitted
....
....         get procedure not performed details for this ur/procedure/count
....
...          PACK      KEY27,WMURNO,WMCODE,WMCNT,Z8
...          CALL      RSWTPNP2
...          CALL      RPWTPNP2
...          BRANCH    OVRCD,GPRO6500  
....
...          MATCH     WMURNO,WTNPURNO
...          GOTO      GPRO6500 IF NOT EQUAL
....
...          MATCH     WMCODE,WTNPCODE
...          GOTO      GPRO6500 IF NOT EQUAL
....
...          COMPARE   WMCNT,WTNPCNT
...          GOTO      GPRO6500 IF NOT EQUAL
....
...          MOVE      WTNPBOOK,BOOKNO
...          GOTO      GPRO6800
.
.         get procedure not performed details for this booking
.
          MOVE      BOOKNO,KEY8
          CALL      RDWTPNP1
          BRANCH    OVRCD,GPRO6500
.
          GOTO      GPRO6800
.
GPRO6500  UNPACK    SP20,WTNPOUTC,WTNPREAS,WTNPDRET
          PACK      WTNPCOMM,SP20,SP20,SP20
          MOVE      ZEROVISN,WTNPBOOK
.
GPRO6800  MOVE      WTNPDRET,CODEDNPF
          MOVE      WTNPOUTC,CODEOUTC
          MOVE      WTNPREAS,CODEREAS
          MOVE      WTNPCOMM,CODECOMM
.
          MOVE      ZERO,EXIT
          GOTO      GPRO9999
.
. ******* set up for a new page *******
.
GPRO7000  CALL      CLRA0000                * clear storage variables
          ADD       ONE,CPAGENO             * add to page counter
          MOVE      ONE,CNTITEM             * reset item counter
          MOVE      "8",CVERT               * set row
          DISPLAY   *P1:CVERT,*EF           * clear screen
          GOTO      GPRO3000
.
GPRO8000  DISPLAY   *P1:24,*EL,*B:
                    "Procedure Not Performed Record Missing. ";
          CALL      HITENTER
          GOTO      GPRO5910
.
GPRO8700  MOVE      ITEMS[FORM2],KEY19
          CALL      UPTREA1                 * unlock treatment file
          DISPLAY   *P1:24,*B,"Patient must be admitted.  ",*EL;
          CALL      HITENTER
          GOTO      GPRO5910
.
GPRO8800  DISPLAY   *P1:24,*EL,*B:
                    "Patient Treatment record is busy on Terminal ":
                    *V2LON,WTWMPORT,*HOFF,".  ";
          CALL      HITENTER
          GOTO      GPRO5910
.
GPRO8900  DISPLAY   *P1:24,*B,"No procedures found.  ",*EL;
          CALL      HITENTER
.
. ******* set exit flags *******
.
GPRO9100  MOVE      ONE,EXIT                * exit selected
.
GPRO9999  RETURN
+
.*********************************************************************
.*        Keyin response for line 24 question                        *
.*********************************************************************
KEYA0000  DISPLAY   *P1:24,*EL,"Select item, (":
                    *V2LON,ANSE,*HOFF,")xit";
          MOVE      "19",CCOL
          IF        CALLPOSN = ONE | CALLPOSN = TWO
            DISPLAY   ", (",*V2LON,"N",*HOFF,")ext";
            ADD       "8",CCOL
          ENDIF
          IF        CALLPOSN = ONE | CALLPOSN = THREE
            DISPLAY   ", (",*V2LON,"F",*HOFF,")irst";
            ADD       "9",CCOL
          ENDIF
          DISPLAY   " ? ",*EL;
          ADD       FOUR,CCOL
.
KEYA1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      KEYA5000 IF EQUAL       * number entered
.
.         letter entered
.
          MATCH     SP1,DIM2
          GOTO      KEYA1500 IF NOT EQUAL   * invalid entry
.
          UNPACK    DIM2,ANS,ANS            * get required letter in ANS
          REP       UPPLOW,ANS              * get as uppercase
          REP       "N1F2E3",ANS            * turn valid letters into number
          MOVE      ZERO,EXIT               * clear exit flag
          MOVE      ANS,EXIT                * set exit flag
          BRANCH    EXIT,KEYA9999,KEYA9999,KEYA9999
.
KEYA1500  BEEP
          GOTO      KEYA1000
.
.         number entered
.
KEYA5000  MOVE      ZERO,EXIT               * set exit flag
          MOVE      DIM2,FORM2              * get as a number
.
          COMPARE   FORM2,ZERO
          GOTO      KEYA1500 IF NOT LESS    * negative entered
.
          COMPARE   FORM2,CNTITEM
          GOTO      KEYA1500 IF LESS        * number too large
.
KEYA9999  RETURN
+
.*********************************************************************
.*                  CLRA0000                    Called by :          *
.*        Clear the storage variables                                *
.*********************************************************************
CLRA0000  MOVE      ONE,FORM2
          WHILE     FORM2 <= MAXITEMS
            MOVE      SP30,ITEMS[FORM2]
            ADD       ONE,FORM2
          DO
CLRA9999  RETURN
.
.
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       NZCOMPIO/INC
          INC       GENANVIS
AGECHK
CLPATMAS  RETURN
.
+

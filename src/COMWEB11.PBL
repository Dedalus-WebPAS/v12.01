.----------------------------------------------------------------------
. Program       : COMWEB11
.
. Function      : Common Maintainance Reports
.
. Modifications : 
.----------------------------------------------------------------------
. V11.01.06 21.09.2021  DA Sarkies               TSK 0905305
.           Removed the word flag for the list display on report 3           
. V11.01.05 20.09.2021  DA Sarkies               TSK 0905305
.           Changed ECDG Subcategory to ECDG Subcode. Changed size of dFrames
.           to prevent wrapping.
. V11.01.04 13.09.2021  DA Sarkies               TSK 0905305
.           Changed descruption to ECDG Subcategory description
.           Added MOVE ZERO,STARTNUM to CLRPAR00
. V11.01.03 08.09.2021  DA Sarkies               TSK 0905305
.           Changed code from 0 to 1 and vice versa for inactive/active value 
. V11.01.02 01.09.2021  DA Sarkies               TSK 0905305
.           Added code to pass space into DISPTDAT variable to ECDG complexity
.           Switched value that produced the active value on the list screen
. V11.01.01 20/08/2021  Thanh T                  TSK 0905305
.           Renamed EMRACHVL to EMRACIVL
.           Renamed EMRICHVL to EMRACAVL and EMRACCVL to be
.           consistent with table name
. V11.01.00 20/05/2021  DA Sarkies               TSK 0905305
.           Created Web Server
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       PATCONFD/INC
.
          INC       EMRACAFD/INC
          INC       EMRACATD/INC
.
          INC       EMRACCFD/INC
          INC       EMRACCTD/INC
.
          INC       EMRACIFD/INC
          INC       EMRACITD/INC
.
NUMBRONE  FORM      1
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
F3P6      FORM      3.6
STARTKEY  DIM       14
DISPFDAT  DIM       11
DISPTDAT  DIM       11
DISPAFLG  DIM       3
ENDKEY38  DIM       38
ENDKEY48  DIM       48
AUTOEDAT  DATE      8
VALFDATE  DIM       8
VALTDATE  DIM       8
SAVKEY38  DIM       38
SAVKEY30  DIM       30
SAVKEY10  DIM       10
ALLOWINF  DIM       3
MAXSCORE  DIM       10
EMACTEMP  DIM       40       
.----------------------------------------------------------------------
PRGID     INIT      "COMWEB11"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "AECC Maintainance"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      AECC0000        * AECC Coefficients for Age Group
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      AEAC0000        * AECC Coefficients for Complexity
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      AECI0000        * AECC Coefficients for Intercept
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      EMRACAA1,"emracaaf"
	  OPEN      EMRACCA1,"emraccaf"
          OPEN      EMRACIA1,"emraciaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATCODE3,"patcodes"
          OPEN      PATCODE4,"patcodes"
.
INIT9999  RETURN
.
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,NORECORD
          MOVE      ZERO,STARTNUM
          MOVE      SP70,STARTKEY
.
          CALL      CPEMAA00
          CALL      CPEMAC00
          CALL      CPEMAI00
.
CLRPAR99  RETURN
.
.------------------------------------------------------------
. Set Parameters - this is where the code starts from when
. called from the HTML page
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emaca",QRYNAME
          IF        @EQUAL
            CALL      SPEMRA00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emacc",QRYNAME
          IF        @EQUAL
            CALL      SPEMAC00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emaci",QRYNAME
          IF        @EQUAL
            CALL      SPEMAI00
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.
.------------------------------------------------------------
. New Report Number - determines which function to call
.------------------------------------------------------------
AECC0000  MATCH     SP70,UPDTTYPE           * Display
          GOTO      AECC7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      AECC1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      AECC2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      AECC3000 IF EQUAL
.
          GOTO      AECC9000
.
.         ************ ADD *************
.
AECC1000  PACK      KEY38,EMACA001,EMACA002,SP70
          MOVE      EMACA002,VALFDATE       * Validate From date
          MOVE      EMACA003,VALTDATE       * Validate To date
          MOVE      SP70,ENDKEY38           * Auto end date key
          CALL      RIVL0000                * Check for date overlap
          BRANCH    EXIT,AECC9300
.
          CALL      CLEMRACA                * Clear file vars
.
          PACK      KEY38,EMACA001,EMACA002,SP70
          CALL      RAEMACA1                * Check if already exists
          COMPARE   ONE,OVRCD
          GOTO      AECC9100 IF NOT EQUAL
.
          PACK      EMAACDAT,CCC,CYY,CMM,CDD
          REP       " 0",EMAACDAT           * Date created
          CLOCK     TIME,EMAACTIM           * Time created
          MOVE      USERID,EMAACUID         * Created By
.
          CALL      MVEMAA00                * Move cgi to file vars
.
          CALL      WREMACA1                * Write Record
.
          MATCH     SP70,ENDKEY38                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMAAFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY38,ENDKEY38,SP70
            CALL      RDEMACA1
            IF        OVRCD=0
              MATCH     SP70,EMAATDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMAATDAT      * set end date
                CALL      UPEMACA1
              ENDIF
            ENDIF
          ENDIF
.

          MOVE      ZERO,EXIT
          GOTO      AECC9999
.
.         ************ UPDATE **********
.
AECC2000  PACK      KEY38,EMACA001,EMACA002,SP70 * Update record key
          MOVE      EMACA002,VALFDATE            * Validate from date
          MOVE      EMACA003,VALTDATE            * Validate to date
          CALL      RIVL0000                     * Check for date overlap
          BRANCH    EXIT,AECC9300
.
          PACK      KEY38,EMACA001,EMACA002,SP70        *Builds the key
          CALL      RDEMACA1                            *Checks entry exists
          BRANCH    OVRCD,AECC9200                      *Error if not
.
          CALL      MVEMAA00                            
.
          PACK      EMAAUDAT,CCC,CYY,CMM,CDD            *Saves user details
          REP       " 0",EMAAUDAT
          CLOCK     TIME,EMAAUTIM
          MOVE      USERID,EMAAUUID
.
          CALL      UPEMACA1                           *Updates entry
.
          MOVE      ZERO,EXIT
          GOTO      AECC9999
.
.         ************ DELETE **********
.
AECC3000  PACK      KEY38,EMACA001,EMACA002,SP70       *Builds Key
          CALL      RDEMACA1                           *Checks existance
          BRANCH    OVRCD,AECC9200
.
          CALL      DEEMACA1                           *Deletes record
.
          MOVE      ZERO,EXIT
          GOTO      AECC9999
.
.         ************ DISPLAY *********
.
AECC7000  PACK      KEY38,EMACA001,EMACA002,SP70
          CALL      RDEMACA1
          IF        OVRCD=1
            CALL      CLEMRACA
          ENDIF 
.
.         Read your file here for add/update page display 
.         Call clear of file vars if record not found
.
          CLEAR     WEBTITLE
          MOVE      "AECC Co-Efficient Codes",WEBTITLE
          RESET     WEBTITLE
.
.         ************ Calls functions to build the page ********
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,AECC9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      AECC9999
.

.         ************ ERROR MESSAGE SUBROUTINE  *********
.
AECC9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AECC9999
.
AECC9100  MOVE      "AECC Co-Efficient Code already exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AECC9999
.
AECC9200  MOVE      "AECC Co-Efficient Code does not exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AECC9999
.
AECC9300  MOVE      "Invalid Effective From/To Date (Overlap).",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AECC9999
.
AECC9999  RETURN
.
.------------------------------------------------------------
. New Report Number - determines which function to call
.------------------------------------------------------------
AEAC0000  MATCH     SP70,UPDTTYPE           * Display
          GOTO      AEAC7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      AEAC1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      AEAC2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      AEAC3000 IF EQUAL
.
          GOTO      AEAC9000
.
.         ************ ADD *************
.
AEAC1000  PACK      KEY48,EMACC001,EMACC005,EMACC002,SP70
          MOVE      EMACC002,VALFDATE       * Validate From date
          MOVE      EMACC003,VALTDATE       * Validate To date
          MOVE      SP70,ENDKEY48           * Auto end date key
          CALL      RRVL0000                * Check for date overlap
          BRANCH    EXIT,AEAC9300
.
          CALL      CLEMRACC                * Clear file vars
.
          PACK      KEY48,EMACC001,EMACC005,EMACC002,SP70
          CALL      RAEMACC1                * Check if already exists
          COMPARE   ONE,OVRCD
          GOTO      AEAC9100 IF NOT EQUAL
.
          PACK      EMACCDAT,CCC,CYY,CMM,CDD
          REP       " 0",EMACCDAT           * Date created
          CLOCK     TIME,EMACCTIM           * Time created
          MOVE      USERID,EMACCUID         * Created By
.
          CALL      MVEMAC00                * Move cgi to file vars
.
          CALL      WREMACC1                * Write Record
.
          MATCH     SP70,ENDKEY48                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMACFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY48,ENDKEY48,SP70
            CALL      RDEMACC1
            IF        OVRCD=0
              MATCH     SP70,EMACTDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMACTDAT      * set end date
                REP       " 0",EMACTDAT
                MOVE      USERID,EMACUUID
                CALL      IBACLOCK
                PACK      EMACUDAT,CCC,CYY,CMM,CDD
                REP       " 0",EMACUDAT
                MOVE      CTIMEIS,EMACUTIM
                CALL      UPEMACC1
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      AEAC9999
.
.         ************ UPDATE **********
.
AEAC2000  PACK      KEY38,EMACC001,EMACC002,SP70 * Update record key
          MOVE      EMACC002,VALFDATE            * Validate from date
          MOVE      EMACC003,VALTDATE            * Validate to date
          CALL      RRVL0000                     * Check for date overlap
          BRANCH    EXIT,AEAC9300
.
          PACK      KEY48,EMACC001,EMACC005,EMACC002,SP70        *Builds the key
          CALL      RDEMACC1                            *Checks entry exists
          BRANCH    OVRCD,AEAC9200                      *Error if not
.
          CALL      MVEMAC00                            
.
          PACK      EMACUDAT,CCC,CYY,CMM,CDD            *Saves user details
          REP       " 0",EMACUDAT
          CLOCK     TIME,EMACUTIM
          MOVE      USERID,EMACUUID
.
          CALL      UPEMACC1                           *Updates entry
.
          MOVE      ZERO,EXIT
          GOTO      AEAC9999
.
.         ************ DELETE **********
.
AEAC3000  PACK      KEY48,EMACC001,EMACC005,EMACC002,SP70       *Builds Key
          CALL      RDEMACC1                           *Checks existance
          BRANCH    OVRCD,AECC9200
.
          CALL      DEEMACC1                           *Deletes record
.
          MOVE      ZERO,EXIT
          GOTO      AEAC9999
.
.         ************ DISPLAY *********
.
AEAC7000  PACK      KEY48,EMACC001,EMACC005,EMACC002,SP70
          CALL      RDEMACC1
          IF        OVRCD=1
            CALL      CLEMRACC
          ENDIF 
.
.         Read your file here for add/update page display 
.         Call clear of file vars if record not found
.
          CLEAR     WEBTITLE
          MOVE      "AECC Co-Efficient for ECDG Complexes",WEBTITLE
          RESET     WEBTITLE
.
.         ************ Calls functions to build the page ********
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,AEAC9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      AEAC9999
.

.         ************ ERROR MESSAGE SUBROUTINE  *********
.
AEAC9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AEAC9999
.
AEAC9100  MOVE      "AECC Co-Efficient for ECDG already exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AEAC9999
.
AEAC9200  MOVE      "AECC Co-Efficient for ECDG does not exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AEAC9999
.
AEAC9300  MOVE      "Invalid Effective From/To Date (Overlap).",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AEAC9999
.

AEAC9999  RETURN
.
.------------------------------------------------------------
. AECC Coefficients for Intercept
.------------------------------------------------------------
AECI0000  MATCH     SP70,UPDTTYPE           * Display
          GOTO      AECI7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      AECI1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      AECI2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      AECI3000 IF EQUAL
.
          GOTO      AECI9000
.
.         ************ ADD *************
.
AECI1000  PACK      KEY38,EMACI001,EMACI002,SP70 * Add record key
          MOVE      EMACI002,VALFDATE            * Validate from date
          MOVE      EMACI003,VALTDATE            * Validate to date
          MOVE      SP70,ENDKEY38                * Auto end date key
          CALL      RAVL0000                     * Check for date overlap
          BRANCH    EXIT,AECI9300
.
          CALL      CLEMRACI                     * Clear File vars
.
          PACK      KEY38,EMACI001,EMACI002,SP70
          CALL      RAEMACI1                     * Check if already exists
          COMPARE   ONE,OVRCD
          GOTO      AECI9100 IF NOT EQUAL
.
          PACK      EMRADATC,CCC,CYY,CMM,CDD
          REP       " 0",EMRADATC                * Date created
          CLOCK     TIME,EMRATIMC                * Time created
          MOVE      USERID,EMRAUSRC              * Created By
.
          CALL      MVEMAI00                     * Move cgi to file vars
.
          CALL      WREMACI1                     * Write Record
.
          MATCH     SP70,ENDKEY38                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMRAFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY38,ENDKEY38,SP70
            CALL      RDEMACI1
            IF        OVRCD=0
              MATCH     SP70,EMRATDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMRATDAT      * set end date
                CALL      UPEMACI1
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      AECI9999
.
.         ************ UPDATE **********
.
AECI2000  PACK      KEY38,EMACI001,EMACI002,SP70 * Update record key
          MOVE      EMACI002,VALFDATE            * Validate from date
          MOVE      EMACI003,VALTDATE            * Validate to date
          CALL      RAVL0000                     * Check for date overlap
          BRANCH    EXIT,AECI9300
.
          PACK      KEY38,EMACI001,EMACI002,SP70 * Builds the key
          CALL      RDEMACI1                     * Checks entry exists
          BRANCH    OVRCD,AECI9200               * Error if not
.
          CALL      MVEMAI00
.
          PACK      EMRADATU,CCC,CYY,CMM,CDD     * Saves user details
          REP       " 0",EMRADATU
          CLOCK     TIME,EMRATIMU
          MOVE      USERID,EMRAUSRU
.
          CALL      UPEMACI1                     * Updates entry
.
          MOVE      ZERO,EXIT
          GOTO      AECI9999
.
.         ************ DELETE **********
.
AECI3000  PACK      KEY38,EMACI001,EMACI002,SP70 * Builds the Key
          CALL      RDEMACI1
          BRANCH    OVRCD,AECI9200
.
          CALL      DEEMACI1
.
          MOVE      ZERO,EXIT
          GOTO      AECI9999
.
.         *********** DISPLAY **********
.
AECI7000  PACK      KEY38,EMACI001,EMACI002,SP70
          CALL      RDEMACI1
          IF        OVRCD=1
            CALL      CLEMRACI
          ENDIF
.    
          CLEAR     WEBTITLE
          APPEND    "AECC Coefficients for Intercept and ECDG ",WEBTITLE
          APPEND    "Subcategories",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00
          BRANCH    EXIT,AECI9999
          CALL      WEBBOD00
          CALL      WEBEND00
.
          MOVE      ONE,EXIT
          GOTO      AECI9999
.
AECI9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AECI9999
.
AECI9100  MOVE      "AECC Co-Efficient Code already exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AECI9999
.
AECI9200  MOVE      "AECC Co-Efficient Code does not exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AECI9999
.
AECI9300  MOVE      "Invalid Effective From/To Date (Overlap).",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AECI9999
.
AECI9999  RETURN
.
.------------------------------------------------------------
. Process Fields - Count from here 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      EMAA0000             * EMRACATM tags
          GOTO      PROFLD99 
.
PROFLD12  CALL      GTAG0000             * General tags
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22
          GOTO      PROFLD99
.
PROFLD21  CALL      EMAC0000             * EMRACCTM tags
          GOTO      PROFLD99
.
PROFLD22  CALL      GTAG0000             * General tags
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32
          GOTO      PROFLD99
.
PROFLD31  CALL      EMAI0000
          GOTO      PROFLD99
.
PROFLD32  CALL      GTAG0000
          GOTO      PROFLD99
.
PROFLD99  RETURN

.------------------------------------------------------------------------
. General Tags
. ------------------------------------------------------------------------
GTAG0000  BRANCH    FLDITMNO,GTAG0010,GTAG0020,GTAG0030,GTAG0040,GTAG0050:
                    GTAG0060,GTAG0070,GTAG0080,GTAG0090,GTAG0100

          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      GTAG9999
.
GTAG0010  CALL      LNTAB000                             * AECC Co-efficients
          GOTO      GTAG9999
.
GTAG0020  CALL      NEXT0000                             * Next Page
          GOTO      GTAG9999
.
GTAG0030  CALL      PREV0000                             * Prev Page
          GOTO      GTAG9999
.
GTAG0040  CALL      EACCTB00                             * AECC Co-Eff for ECDG
          GOTO      GTAG9999
.
GTAG0050  CALL      EACITB00
          GOTO      GTAG9999
.
GTAG0060  
          GOTO      GTAG9999
.
GTAG0070  
          GOTO      GTAG9999
.
GTAG0080  
          GOTO      GTAG9999
.
GTAG0090  
          GOTO      GTAG9999
.
GTAG0100  
          GOTO      GTAG9999
.
GTAG9999  RETURN

.------------------------------------------------------------
. Link to next page
.------------------------------------------------------------

NEXT0000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3     *Sets the start position
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2

.         Builds the link
          WRITE     HTMLFILE;"comweb11.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
                                 
NEXT9999  RETURN

.------------------------------------------------------------
. Link to previous page
.------------------------------------------------------------

PREV0000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3     *Sets the start position
          IF        F3<0
            MOVE      ZERO,F3                *Sets to zero if less
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2

.         Builds the link
          WRITE     HTMLFILE;"comweb11.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;

PREV9999  RETURN

.------------------------------------------------------------
. Table of OECC Coefficients
.------------------------------------------------------------
.
LNTAB000  IF        NORECORD=0
            MOVE      "20",NORECORD         *Sets number of records to display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          CALL      LNHED000
          PACK      KEY14,SP70
.
          MATCH     SP70,STARTKEY          *Sets start for goto button
          IF          @EQUAL
            PACK      KEY38,SP70
          ELSE
            PACK      KEY38,STARTKEY,SP70
          ENDIF
.
          CALL      RSEMACA1                  *Position on start
LNTAB100  CALL      RKEMACA1                  *Read next
          BRANCH    OVRCD,LNTAB999
.
          ADD       ONE,RECNUMB               *Calls function to build
          IF        STARTNUM>=RECNUMB         
            GOTO      LNTAB100                *HTML entry
          ENDIF
.
          CALL      LNROW000
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      LNTAB100 IF NOT EQUAL
.
LNTAB999  RETURN

.------------------------------------------------------------
. Table of OECC Coefficients for ECDG
.------------------------------------------------------------
.
EACCTB00  IF        NORECORD=0
            MOVE      "20",NORECORD         *Sets number of records to display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          CALL      EACCHD00
          PACK      KEY14,SP70
.
          MATCH     SP70,STARTKEY          *Sets start for goto button
          IF        @EQUAL
            PACK      KEY48,SP70
          ELSE
            PACK      KEY48,STARTKEY,SP70
          ENDIF
.
          CALL      RSEMACC1                  *Position on start
EACCTB10  CALL      RKEMACC1                  *Read next
          BRANCH    OVRCD,EACCTB99
.
          ADD       ONE,RECNUMB               *Calls function to build
          IF        STARTNUM>=RECNUMB         
            GOTO      EACCTB10                *HTML entry
          ENDIF
.
          CALL      EACCRW00
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      EACCTB10 IF NOT EQUAL
.
EACCTB99  RETURN
.
.------------------------------------------------------------------------
. AECC Coefficients for Intercept & ECDG Subcategories
.------------------------------------------------------------------------
EACITB00  IF        NORECORD=0
            MOVE      "20",NORECORD
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          CALL      EACILH00                      * Generate Table Header
.
          MATCH     SP70,STARTKEY
          IF        @EQUAL
            PACK      KEY38,SP70
          ELSE
            PACK      KEY38,STARTKEY,SP70
          ENDIF
.
          CALL      RSEMACI1
EACITB01  CALL      RKEMACI1     
          BRANCH    OVRCD,EACITB99
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      EACITB01
          ENDIF
.
          CALL      EACILD00                      * Generate Table Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      EACITB01 IF NOT EQUAL
.
EACITB99  RETURN
.
.------------------------------------------------------------
. AECC Coeefficient table headings 
.------------------------------------------------------------
LNHED000  WRITE     HTMLFILE;"<tr>";
.
          WRITE     HTMLFILE;"<td class=HeadingCell>ECDG Code</td>":
                             "<td class=HeadingCell>Age 0 to 4 Years</td>":
			     "<td class=HeadingCell>Age 5 to 9 Years</td>":
                             "<td class=HeadingCell>Age 10 to 14 Years</td>":
                             "<td class=HeadingCell>Age 15 to 69 Years</td>":
                             "<td class=HeadingCell>Age 70 to 74 Years</td>":
                             "<td class=HeadingCell>Age 75 to 79 Years</td>":
                             "<td class=HeadingCell>Age 80 to 84 Years</td>":
                             "<td class=HeadingCell>Age 85+ Years</td>":
                             "<td class=HeadingCell>Date Effective From</td>":
                             "<td class=HeadingCell>Date Effective To</td>":
                             "<td class=HeadingCell>Active</td>":
                             "</tr>"
.
LNHED999  RETURN
.
.------------------------------------------------------------
. AECC Coeefficient for ECDG table headings 
.------------------------------------------------------------
EACCHD00  WRITE     HTMLFILE;"<tr>";
.
          WRITE     HTMLFILE;"<td class=HeadingCell>ECDG Code</td>":
                             "<td class=HeadingCell>ECDG Description</td>":
                             "<td class=HeadingCell>AECC Class</td>":
			     "<td class=HeadingCell>Minimum Score</td>":
                             "<td class=HeadingCell>Maximum Score</td>":
                             "<td class=HeadingCell>Date Effective From</td>":
                             "<td class=HeadingCell>Date Effective To</td>":
                             "<td class=HeadingCell>Active</td>":
                             "<td class=HeadingCell>Allow Infinity</td>":
                             "</tr>"
.
EACCHD99  RETURN
.
.------------------------------------------------------------
. AECC Coefficiens for Intercept and ECDG Subcategories Table Headings
.------------------------------------------------------------
EACILH00  WRITE     HTMLFILE;"<tr>";
.
          WRITE     HTMLFILE;"<td class=HeadingCell>ECDG Subcode</td>":
                             "<td class=HeadingCell>ECDG Subcode":
                             " Description</td>":
                             "<td class=HeadingCell>ECDG Code</td>":
                             "<td class=HeadingCell>Coefficient for ":
                             "Intercept</td>":
                             "<td class=HeadingCell>Coefficient for ":
                             "Intercept ECDG Subgroup</td>":
                             "<td class=HeadingCell>Date Effective From</td>":
                             "<td class=HeadingCell>Date Effective To</td>":
                             "<td class=HeadingCell>Active</td>" 
          WRITE     HTMLFILE;"</tr>";
.
EACILH99  RETURN 
.
.------------------------------------------------------------
. AECC Coefficient Table Rows
.------------------------------------------------------------

LNROW000  UNPACK    EMAAFDAT,CYEAR,CCENT,CMON,CDAY     *Date Massage Routine
          MOVE      " ",SP1
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPFDAT,CDAY,SP1,MTHNAM3,SP1,CYEAR,CCENT

          MATCH     SP70,EMAATDAT                      *Checks if empty
          IF        !@EQUAL
            UNPACK    EMAATDAT,CYEAR,CCENT,CMON,CDAY
            MOVE      " ",SP1
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPTDAT,CDAY,SP1,MTHNAM3,SP1,CYEAR,CCENT 
          ELSE
            MOVE      SP70,DISPTDAT
          ENDIF
 
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:UpdRecord":
                             "(#"comweb11.pbl":
                             "?reportno=1":
                             "&template=003":
                             "&emaca001=",EMAACODE:
                             "&emaca002=",DISPFDAT,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "class=Icon hspace=4></a>":
                             "&nbsp; ",EMAACODE,"</td>";
          MOVE      SP1,D3
          MATCH     "1",EMAAAFLG
          IF        @EQUAL
            MOVE      "No",D3
          ELSE
            MOVE      "Yes",D3
          ENDIF
          WRITE     HTMLFILE;"<td nowrap>",EMAAAG04,"</td>":
                             "<td nowrap>",EMAAAG59,"</td>":
			     "<td nowrap>",EMAAAG14,"</td>":
                             "<td nowrap>",EMAAAG69,"</td>":
                             "<td nowrap>",EMAAAG74,"</td>":
                             "<td nowrap>",EMAAAG79,"</td>":
                             "<td nowrap>",EMAAAG84,"</td>":
                             "<td nowrap>",EMAAAG85,"</td>":
                             "<td nowrap>",DISPFDAT,"</td>":
                             "<td nowrap>",DISPTDAT,"</td>":
                             "<td nowrap>",D3,"</td>":
                             "</tr>"
          MOVE      SP1,D3

LNROW999  RETURN
.
.------------------------------------------------------------
. AECC Coefficient Table Rows
.------------------------------------------------------------

EACCRW00  UNPACK    EMACFDAT,CYEAR,CCENT,CMON,CDAY     *Date Massage Routine
          MOVE      " ",SP1
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPFDAT,CDAY,SP1,MTHNAM3,SP1,CYEAR,CCENT

          MATCH     SP70,EMACTDAT                      *Checks if empty
          IF        !@EQUAL
            UNPACK    EMACTDAT,CYEAR,CCENT,CMON,CDAY
            MOVE      " ",SP1
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPTDAT,CDAY,SP1,MTHNAM3,SP1,CYEAR,CCENT 
          ELSE
            MOVE      SP70,DISPTDAT
          ENDIF
 
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:UpdRecord":
                             "(#"comweb11.pbl":
                             "?reportno=2":
                             "&template=003":
                             "&emacc001=",EMACCODE:
                             "&emacc005=",EMACMINS:
                             "&emacc002=",DISPFDAT,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "class=Icon hspace=4></a>":
                             "&nbsp; ",EMACCODE,"</td>";
          MOVE      SP1,D3
          MATCH     "1",EMACAFLG
          IF        @EQUAL
            MOVE      "No",D3
          ELSE
            MOVE      "Yes",D3
          ENDIF
          MOVE      SP1,ALLOWINF
          MOVE      SP10,MAXSCORE
          MATCH     "1",EMACMXSI
          IF        @EQUAL
            MOVE      "Yes",ALLOWINF
            MOVE      "Infinity",MAXSCORE
          ELSE
            MOVE      "No",ALLOWINF
            MOVE      EMACMAXS,MAXSCORE
          ENDIF
          WRITE     HTMLFILE;"<td nowrap>",EMACDESC,"</td>":
                             "<td nowrap>",EMACCLAS,"</td>":
                             "<td nowrap>",EMACMINS,"</td>":
                             "<td nowrap>",MAXSCORE,"</td>":
                             "<td nowrap>",DISPFDAT,"</td>":
                             "<td nowrap>",DISPTDAT,"</td>":
                             "<td nowrap>",D3,"</td>":
                             "<td nowrap>",ALLOWINF,"</td>":
                             "</tr>"
          MOVE      SP1,D3

EACCRW99  RETURN
.
.------------------------------------------------------------------------
. Table Data Rows - ECDG Subcategories Codes Data
.------------------------------------------------------------------------
.
.         -------------------------------
.           Display Effective From Date
.         -------------------------------
.
EACILD00  UNPACK    EMRAFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPFDAT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
.         -----------------------------
.           Display Effective To Date
.         -----------------------------
.
          MATCH     SP70,EMRATDAT 
          IF        !@EQUAL
            UNPACK    EMRATDAT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPTDAT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            MOVE      "",DISPTDAT
          ENDIF
.
.         ----------------------------------
.           Display Active / Inactive Flag
.         ----------------------------------
.
          MATCH     "0",EMRAAFLG
          IF        @EQUAL
            MOVE      "Yes",DISPAFLG
          ELSE
            MOVE      "No",DISPAFLG
          ENDIF
.
.         --------------------------------------------
.           Write the HTML File to Output Table Data
.         --------------------------------------------
.
          WRITE     HTMLFILE;"<tr>"
.
          WRITE     HTMLFILE;"<td nowrap><a href='javascript:UpdRecord":
                             "(#"comweb11.pbl":
                             "?reportno=03":
                             "&template=003":
                             "&emaci001=",EMRASUBV:
                             "&emaci002=",DISPFDAT:
                             "#")'>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "class=Icon hspace=4></a>&nbsp;":
                             EMRASUBV,"</td>":
                             "<td nowrap>",EMRASUBD,"</td>":
                             "<td nowrap>",EMRACODE,"</td>":
                             "<td nowrap>",EMRACINT,"</td>":
                             "<td nowrap>",EMRACITS,"</td>":
                             "<td nowrap>",DISPFDAT,"</td>":
                             "<td nowrap>",DISPTDAT,"</td>":
                             "<td nowrap>",DISPAFLG,"</td>":
                             "</tr>"
.
EACILD99  RETURN
.               
.------------------------------------------------------------
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       CLEMRACA
          INC       CLEMRACC
          INC       CLEMRACI
          INC       DAYOFWEK
          INC       EMRACATM
          INC       EMRACCTM
          INC       EMRACITM
          INC       EMRACIVL 
          INC       EMRACAVL 
          INC       EMRACCVL 
.
          INC       EMRACAIO/INC
          INC       EMRACCIO/INC
          INC       EMRACIIO/INC

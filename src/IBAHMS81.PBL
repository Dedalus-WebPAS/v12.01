.******************************************************************************
.* System         : Patient Management System                                 *
.* Program        : IBAHMS81.PBL                                              *
.* Name           : HoNOS Details Report                                      *
.******************************************************************************
.* Date           : 03/03/2008                                                *
.* Author         : J.Tan                                                     *
.* Function       : HoNOS details for patient discharged within the period and*
.*                  range of Claim codes                                      *
.* Modifications  :                                                           *
.*                                                                            *
.*        V10.05.01 26/08/2014  Ania P         CAR 303210                     *
.*                  Recompiled for MEHHONFD                                   *
.*        V10.04.01 13/06/2014 Steve Armstrong  CAR 301639                    *
.*                  Moved call to TFILENAM into INIT0000                      *
.*        V10.03.01 24/12/2012 Patrick Adair  CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.10.01 24/07/08 J.Tan    CAR 170392                               *
.*                 Mods to update MH Honos sent status (MHHNSENT)             *
.******************************************************************************
+
          INC       STD001FD
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC            * Codes file
          INC       PATMA1FD/INC            * Master file
          INC       PATMI1FD/INC            * Admission file
          INC       PMSVX1FD/INC            * Admission file
          INC       PATDSCFD/INC            * Discharge file
          INC       PATDRGFD/INC            * Date Range file
          INC       PATTRNFD/INC            * Transfer file
          INC       MEHHONFD/INC            * MH HONOS file
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
. ----- Program Variables -----
.
ADMSCORE  DIM       12
DSCSCORE  DIM       12
CALDAYS   FORM      8
CURDATE   DIM       8
CMDLINE   DIM       80
CPTDATE   DIM       8
ENDATE8   DIM       8   
FNAMEI    DIM       8
FORM6     FORM      6
FROMDATE  DIM       8
GROUPCLM  FORM      1                       * Group each claim
.
STDATE8   DIM       8   
SAVCLAIM  DIM       3
SAVURNUM  DIM       8
SAVADMNO  DIM       8
STRCLAIM  DIM       3
ENDCLAIM  DIM       3
TODATE    DIM       8
TMBDAYS   FORM      8
TMPTLOS   FORM      8
.
NBRDAYS   FORM      8
PATNAME   DIM       25
.
UKEY1     INIT      " 103 U1-3,20-39,4-11"
TEMPFIL1  IFILE     SQL, FIXED=103, KEY="U1-3,20-39,4-11"
.
.Name     Type  Length  Physical Start Description
.----     ----  ------  -------- ----- -----------
XXCLAIM   DIM       3      3        1  Claim Code
XXADMNO   DIM       8      8        4  Admission Number
XXURNUM   DIM       8      8       12  U/R Number
XXPSNAM   DIM      20     20       20  Surname
XXPGNAM   DIM      25     25       40  Given Name
XXBDAYS   FORM      8      5       65  Bed days
XXPTLOS   FORM      8      5       70  Length of Stay
XXPTITL   DIM       4      4       75  Patient title
XXADS01   DIM       1      1       79  Admission score 1
XXADS02   DIM       1      1       80  Admission score 2
XXADS03   DIM       1      1       81  Admission score 3
XXADS04   DIM       1      1       82  Admission score 4
XXADS05   DIM       1      1       83  Admission score 5
XXADS06   DIM       1      1       84  Admission score 6
XXADS07   DIM       1      1       85  Admission score 7
XXADS08   DIM       1      1       86  Admission score 8
XXADS09   DIM       1      1       87  Admission score 9
XXADS10   DIM       1      1       88  Admission score 10
XXADS11   DIM       1      1       89  Admission score 11
XXADS12   DIM       1      1       90  Admission score 12
XXDSS01   DIM       1      1       91  Discharge score 1
XXDSS02   DIM       1      1       92  Discharge score 2
XXDSS03   DIM       1      1       93  Discharge score 3
XXDSS04   DIM       1      1       94  Discharge score 4
XXDSS05   DIM       1      1       95  Discharge score 5
XXDSS06   DIM       1      1       96  Discharge score 6
XXDSS07   DIM       1      1       97  Discharge score 7
XXDSS08   DIM       1      1       98  Discharge score 8
XXDSS09   DIM       1      1       99  Discharge score 9
XXDSS10   DIM       1      1      100  Discharge score 10
XXDSS11   DIM       1      1      101  Discharge score 11
XXDSS12   DIM       1      1      102  Discharge score 12
.EOR                              103
.
. ----- Program Constants -----
.
PRGID     INIT      "IBAHMS81"
PRGNAM    INIT      "HoNOS Details Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      MNTH0000                * Keyin the Month range
          BRANCH    EXIT,ML1000
.
ML3000    CALL      KCLM0000                * Keyin range of claim code
          BRANCH    EXIT,ML2000
.
          CALL      GCLM0000                * Group by Claim code
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
          GOTO      ML1000
.
ML4000    CALL      PROC0000                * Process the files
          CALL      PRNT0000                * Print report
          CALL      KILL0000                * Kill tempfile
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000             Called by: ML0000   *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"mehhonaf";
          OPEN      MEHHONA1,"mehhonaf"
.
          DISPLAY   *P60:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE 
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEI
.
.         Get the current financial period
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,INIT3000
.
          GOTO      INIT9999
.
INIT3000  KEYIN     *P1:24,*EL,*V2LON,*B:
                    "** Error: Current Period not in Period File. ":
                    "Hit <ENTER> to exit ",*EOFF,ANS;
          MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          MOVE      ZERO,CPAGENO            * Reset the page no
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Date Range":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.**********************************************************************
.*                           MNTH0000                                 *
.*           Enter the starting month for the report                  *
.**********************************************************************
MNTH0000  DISPLAY   *P1:9,*EF,"Enter the Starting Month : ":
                    *P1:11,"          Ending Month   : "; 
          MOVE      TWENTY8,CCOL        
          MOVE      NINE,CVERT          
          MOVE      ZERO,CHIGHLT        
          MOVE      CCC,CCENT           
          REP       " 0",CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
.
          CALL      KEYPER                  * keyin the period code
          BRANCH    OVRCD,MNTH9000
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
.
          CALL      RDDRGA3                 * read the period file
          BRANCH    OVRCD,MNTH8000
.
          DISPLAY   *P40:9,DRGLDSC;
          PACK      FROMDATE,DRGCNUM,SLASH,DRGCYR
          MOVE      DRGFDTE,STDATE8
.
          MOVE      TWENTY8,CCOL
          MOVE      TEN1,CVERT
          MOVE      ZERO,CHIGHLT
.
          UNPACK    DRGCYR,CCENT,CYEAR      * set up default date
          REP       " 0",DRGYR
          UNPACK    DRGCNUM,CMON
          CALL      KEYPER                  * keyin the period code
          BRANCH    OVRCD,MNTH2100
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
.
          CALL      RDDRGA3                 * read the period file
          BRANCH    OVRCD,MNTH6000
.
          DISPLAY   *P28:11,*EL,*V2LON,DRGCNUM,*HOFF,SLASH,*V2LON,DRGCYR:
                    *P40:11,*HOFF,DRGLDSC;
.
.------ we have the ending month so validate and display it ------
.
MNTH2100  PACK      TODATE,DRGCNUM,SLASH,DRGCYR
.
          MATCH     STDATE8,DRGTDTE         * Make sure the todate is not
          GOTO      MNTH5000 IF LESS        * before the fromdate
.
          MATCH     DRGTDTE,CURDATE         * Make sure range does not go into
          GOTO      MNTH7000 IF LESS        * the future
.
          MATCH     DRGTDTE,CURDATE
          GOTO      MNTH4000 IF NOT EQUAL
.
.         If current period subtract one from the ending date
.
          MOVE      CCC,CC
          MOVE      CYY,YY
          MOVE      CMM,MM
          MOVE      CDD,DD
          CALL      DMYCON
.
          SUB       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      DRGTDTE,CC,YY,MM,DD
          REP       " 0",DRGTDTE
.
MNTH4000  MOVE      DRGTDTE,ENDATE8         * set end date
.
          MOVE      FALSE,EXIT
          GOTO      MNTH9999
.
.------ the date range will go into the future ------
.
MNTH5000  DISPLAY   *P1:24,*EL,*B,"The To Date cannot be less than the ":
                    "From date - Hit <",*V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      MNTH0000
.
.------ invalid period entered ------
.
MNTH6000  DISPLAY   *P1:24,*EL,*B,"Invalid Ending Month - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      MNTH0000
.
.------ the date range will go into the future ------
.
MNTH7000  DISPLAY   *P1:24,*EL,*B,"The Date Range cannot go into the future ":
                           "- Hit <",*V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      MNTH0000
.
.------ invalid period entered ------
.
MNTH8000  DISPLAY   *P1:24,*EL,*B,"Invalid Starting Month - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      MNTH0000
.
.------ no period entered ------
.
MNTH9000  MOVE      TRUE,EXIT
.
MNTH9999  RETURN
+
.**********************************************************************
.*                             KCLM0000                               *
.*                keyin range of claim codes                          *
.**********************************************************************
KCLM0000  MOVE       ZERO,EXIT
          MOVE       SP20,STRCLAIM
          MOVE       SP20,ENDCLAIM
.
          DISPLAY    *P1:4,*EF,"From Claim Code   :":
                         *P1:5,"To   Claim Code   :";
.
          MOVE      "4",EVERT
          MOVE      "20",ECOL               * keyin position
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM9000,KCLM2000
          MOVE      ACODE,STRCLAIM
          GOTO      KCLM3000
.
KCLM2000  DISPLAY   *P24:4,*EL,"Start"
.
KCLM3000  MOVE      "5",EVERT
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM0000,KCLM4000
          MOVE      ACODE,ENDCLAIM
          GOTO      KCLM9999
.
KCLM4000  DISPLAY   *P24:5,*EL,"End"
          GOTO      KCLM9999
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.******************************************************************************
.*                                  GCLM0000              Called by: ML0000   *
.*                              Group by Claim Code                           *
.******************************************************************************
GCLM0000  MOVE      ZERO,GROUPCLM
          DISPLAY   *P1:12,*EF,"Group by Claim Code (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ";
.
GCLM1000  MOVE      ANSN,ANS
          KEYIN     *P44:12,*V2LON,*RV,ANS;
.
          RESET     ANS
          REP       UPPLOW,ANS
.
          MATCH     SP1,ANS
          GOTO      GCLM2000 IF EQUAL       * Spaces, group by claim code
.
          MATCH     ANSN,ANS
          GOTO      GCLM2000 IF EQUAL       * Dont group by claim code
.
          MATCH     ANSY,ANS
          GOTO      GCLM3000 IF EQUAL       * group by claim code
.
          BEEP
          GOTO      GCLM1000
.
GCLM2000  DISPLAY   *P44:12,*EL,*V2LON,"No";
          MOVE      ZERO,GROUPCLM
          GOTO      GCLM9000
.
GCLM3000  DISPLAY   *P44:12,*EL,*V2LON,"Yes";
          MOVE      ONE,GROUPCLM
.
GCLM9000  MOVE      ZERO,EXIT
          GOTO      GCLM9999
.
GCLM9500  MOVE      ONE,EXIT
GCLM9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Process Discharge file                        *
.******************************************************************************
PROC0000  CALL      INDZD000
.
          PACK      KEY16,STDATE8,SP70
          CALL      RDSDSCH2                * Position on & read next record
PROC1000  CALL      RDKDSCH2                * file record
          BRANCH    OVRCD,PROC9999
.
          MATCH     DDATE,ENDATE8
          GOTO      PROC9999 IF LESS
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PROC1000        * missing admission record
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000        * missing master record
.
          CALL      RCLM0000              * Check range of Claim code
          BRANCH    EXIT,PROC1000
.
          CALL      CHON0000              * Check HONOS records
          BRANCH    EXIT,PROC1000
.
          CALL      CDAY0000              * Calculate bed days and LOS
          CALL      WRIT0000              * Write record to tempfile
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
.******************************************************************************
.         Calculate bed days
.******************************************************************************
CDAY0000  MOVE      ZERO,NBRDAYS
          MOVE      ZERO,TMBDAYS
          MOVE      ZERO,TMPTLOS
          PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CDAY9999
.
.         Public Patient includes leave days
.         Others (Private/Compensable) not include leave days
.
          MOVE      ZERO,NBRDAYS
          MATCH     "P",TCINDC9              * public
          GOTO      CDAY4000 IF NOT EQUAL
.
          MOVE      STDATE8,FROMDATE
          MOVE      ENDATE8,TODATE
          MATCH     ADATE,STDATE8
          IF        @LESS
            MOVE      ADATE,FROMDATE
          ENDIF
          MATCH     ENDATE8,DDATE
          IF        @LESS
            MOVE      DDATE,TODATE
          ENDIF
          DAYSEP    FROMDATE,TODATE,NBRDAYS
          MOVE      NBRDAYS,TMBDAYS          * Bed days
.
          MOVE      ZERO,NBRDAYS
          DAYSEP    ADATE,DDATE,NBRDAYS      * include leave days
          MOVE      NBRDAYS,TMPTLOS          * Length of Stay
          GOTO      CDAY9999
.
.         Private exclude leave day
.
CDAY4000  MOVE      STDATE8,FROMDATE
          MOVE      ENDATE8,TODATE
          CALL      NHOSPDAY
          MOVE      NBRDAYS,TMBDAYS         * Bed days
.
          MOVE      ZERO,NBRDAYS
          MOVE      ADATE,FROMDATE
          MOVE      DDATE,TODATE
          CALL      NHOSPDAY                * exclude leave days
          MOVE      NBRDAYS,TMPTLOS         * Length of Stay
.
CDAY9999  RETURN
+
.******************************************************************************
.         Write record to tempfile
.******************************************************************************
WRIT0000  CALL      ITEMP000
          PACK      KEY31,ACLAIM,PSNAME,AADMNO,SP70
          CALL      RDTEMP1
          IF        OVRCD=1
            MOVE      ACLAIM,XXCLAIM
            MOVE      AURNO,XXURNUM
            MOVE      AADMNO,XXADMNO
            MOVE      PSNAME,XXPSNAM
            MOVE      PGNAME,XXPGNAM
            UNPACK    ADMSCORE,XXADS01,XXADS02,XXADS03,XXADS04:
                               XXADS05,XXADS06,XXADS07,XXADS08:
                               XXADS09,XXADS10,XXADS11,XXADS12
            UNPACK    DSCSCORE,XXDSS01,XXDSS02,XXDSS03,XXDSS04:
                               XXDSS05,XXDSS06,XXDSS07,XXDSS08:
                               XXDSS09,XXDSS10,XXDSS11,XXDSS12
            MOVE      TMBDAYS,XXBDAYS
            MOVE      TMPTLOS,XXPTLOS
            CALL      WRTEMP1
          ENDIF
WRIT9999  RETURN
+
.******************************************************************************
.         Initalise fields for tempfile
.******************************************************************************
ITEMP000  MOVE      SP70,XXCLAIM       * Claim Code
          MOVE      SP70,XXADMNO       * Admission Number
          MOVE      SP70,XXURNUM       * U/R Number
          MOVE      SP70,XXPSNAM       * Surname
          MOVE      SP70,XXPGNAM       * Given Name
          MOVE      ZERO,XXBDAYS       * Bed days
          MOVE      ZERO,XXPTLOS       * Length of Stay
          UNPACK    SP70,XXADS01,XXADS02,XXADS03,XXADS04,XXADS05,XXADS06
          UNPACK    SP70,XXADS07,XXADS08,XXADS09,XXADS10,XXADS11,XXADS12
          UNPACK    SP70,XXDSS01,XXDSS02,XXDSS03,XXDSS04,XXDSS05,XXDSS06
          UNPACK    SP70,XXDSS07,XXDSS08,XXDSS09,XXDSS10,XXDSS11,XXDSS12
ITEMP999  RETURN
+
.******************************************************************************
.         Check HONOS records
.******************************************************************************
CHON0000  MOVE      SP70,ADMSCORE
          MOVE      SP70,DSCSCORE
          MOVE      ZERO,FORM1
          MOVE      ZERO,EXIT
          PACK      KEY16,AADMNO,SP70
          CALL      RSMHHON1
CHON2000  CALL      RKMHHON1
          BRANCH    OVRCD,CHON9000
          MATCH     MHHNVISN,DAADMNO
          GOTO      CHON9000 IF NOT EQUAL
.
          MOVE      "hp",KEY2
          MATCH     SP70,MHHNREAS
          GOTO      CHON2000 IF EQUAL
.
          PACK      KEY5,KEY2,MHHNREAS,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHON2000          * invalid reason for collection code
.
          MATCH     "03",THCSCOD
          GOTO      CHON6000 IF NOT EQUAL
.
.         Admission score
.
          MATCH     SP70,ADMSCORE
          GOTO      CHON2000 IF NOT EQUAL
.
          PACK      ADMSCORE,MHHNADQ1,MHHNADQ2,MHHNADQ3,MHHNADQ4:
                             MHHNADQ5,MHHNADQ6,MHHNADQ7,MHHNADQ8:
                             MHHNADQ9,MHHNAQ10,MHHNAQ11,MHHNAQ12
          ADD       ONE,FORM1
          MOVE      "1",MHHNSENT                * Sent
          CALL      UPMHHON1
.
          MATCH     SP70,DSCSCORE
          GOTO      CHON9999 IF NOT EQUAL     * found discharge score already
          GOTO      CHON2000
.
CHON6000  MATCH     "08",THCSCOD
          GOTO      CHON7000 IF EQUAL        * Discharge score
          MATCH     "09",THCSCOD
          GOTO      CHON2000 IF NOT EQUAL
.
CHON7000  MATCH     SP70,DSCSCORE
          GOTO      CHON2000 IF NOT EQUAL
.
          PACK      DSCSCORE,MHHNADQ1,MHHNADQ2,MHHNADQ3,MHHNADQ4:
                             MHHNADQ5,MHHNADQ6,MHHNADQ7,MHHNADQ8:
                             MHHNADQ9,MHHNAQ10,MHHNAQ11,MHHNAQ12
          ADD       ONE,FORM1
          MOVE      "1",MHHNSENT                * Sent
          CALL      UPMHHON1
.
          MATCH     SP70,ADMSCORE
          GOTO      CHON2000 IF EQUAL
.
CHON9000  COMPARE   ZERO,FORM1
          GOTO      CHON9999 IF NOT EQUAL       * Found HONOS record
          MOVE      ONE,EXIT
CHON9999  RETURN
+
.******************************************************************************
.         Check Range of Claim codes
.******************************************************************************
RCLM0000  MATCH     SP20,STRCLAIM
          IF        !@EQUAL
            MATCH     STRCLAIM,ACLAIM         * Check with Start claim code
            GOTO      RCLM9000 IF LESS
          ENDIF
.
          MATCH     SP20,ENDCLAIM
          IF        !@EQUAL
            MATCH     ACLAIM,ENDCLAIM         * Check with End of Claim code
            GOTO      RCLM9000 IF LESS
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      RCLM9999
.
RCLM9000  MOVE      ONE,EXIT
RCLM9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000                                  *
.*                             Print Record                                   *
.******************************************************************************
PRNT0000  MOVE      "60",CLNO
.
          MOVE      SP70,SAVURNUM
          MOVE      SP70,KEY31
          CALL      RSTEMP1
PRNT1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRNT9000
.
          MOVE      XXPSNAM,PACSNAME
          MOVE      XXPGNAM,PACGNAME
          MOVE      XXPTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * Patients name
.
          IF        GROUPCLM=1
            MATCH     SAVCLAIM,XXCLAIM
            IF        !@EQUAL
              MOVE      XXCLAIM,SAVCLAIM
              CALL      HEAD0000
            ENDIF
          ENDIF
.
          COMPARE   "58",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MATCH     XXURNUM,SAVURNUM
          IF        @EQUAL
            PRINT     *1,"| ";
          ELSE
            PRINT     *1,"|",XXURNUM;
            MOVE      XXURNUM,SAVURNUM
          ENDIF
.
          MOVE      XXBDAYS,FORM6
          PRINT     *10,"|",XXADMNO,*19,"|",PATNAME:
                    *45,"|",FORM6;
.
          MOVE      XXPTLOS,FORM6
          PRINT     *52,"|",FORM6:
                    *60,"|",XXADS01,*63,"|",XXDSS01:
                    *66,"|",XXADS02,*69,"|",XXDSS02:
                    *72,"|",XXADS03,*75,"|",XXDSS03:
                    *78,"|",XXADS04,*81,"|",XXDSS04:
                    *84,"|",XXADS05,*87,"|",XXDSS05:
                    *90,"|",XXADS06,*93,"|",XXDSS06:
                    *96,"|",XXADS07,*99,"|",XXDSS07:
                    *102,"|",XXADS08,*105,"|",XXDSS08:
                    *108,"|",XXADS09,*111,"|",XXDSS09:
                    *114,"|",XXADS10,*117,"|",XXDSS10:
                    *120,"|",XXADS11,*123,"|",XXDSS11:
                    *126,"|",XXADS12,*129,"|",XXDSS12,*132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
          GOTO      PRNT1000
.
PRNT9000  MOVE      SP70,SAVCLAIM
          COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL
          CALL      UND132
PRNT9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000                                  *
.*                             Print Report Header                            *
.******************************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD132                 * Print report header
.
          UNPACK    STDATE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"Date Range From : ",CPCDATE;
.
          UNPACK    ENDATE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *85,"End Date   : ",CPCDATE
.
          MOVE      "Start               ",TDESC
          MATCH     SP70,STRCLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,STRCLAIM
            CALL      RDCODE1
          ENDIF
          PRINT     *40,"Claim Type From : ",TDESC;
.
          MOVE      "Finish              ",TDESC
          MATCH     SP70,ENDCLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,ENDCLAIM
            CALL      RDCODE1
          ENDIF
          PRINT     *85,"Claim Type To   : ",TDESC
.
          MOVE      "No ",KEY3
          IF        GROUPCLM=1
            MOVE      "Yes",KEY3
          ENDIF
          PRINT     *40,"Group by Claim Type: ",KEY3
.
          IF        GROUPCLM=1
            PRINT     *1,"Claim Code: ";
            MATCH     SP70,SAVCLAIM
            IF        !@EQUAL
              PACK      KEY5,ANSC,ANSL,SAVCLAIM,SP70
              CALL      RDCODE1
              PRINT     *13,SAVCLAIM," - ",TDESC
            ELSE
              PRINT     *N;
            ENDIF
            ADD       ONE,CLNO
          ENDIF
.
          CALL      UND132
          PRINT     *1,"| U/R No.",*10,"| Adm.No",*19,"| Patient Name":
                    *45,"| Bed",*52,"| TLOS ":
                    *60,"|  Q1",*66,"|  Q2",*72,"|  Q3",*78,"|  Q4":
                    *84,"|  Q5",*90,"|  Q6",*96,"|  Q7",*102,"| Q8":
                    *108,"|  Q9",*114,"| Q10":
                    *120,"| Q11",*126,"| Q12",*132,"|"
.
          PRINT     *1,"*",*10,"|",*19,"|":
                    *45,"| Days",*52,"|":
                    *60,"|-----",*66,"|-----",*72,"|-----",*78,"|-----":
                    *84,"|-----",*90,"|-----",*96,"|-----",*102,"|-----":
                    *108,"|-----",*114,"|-----":
                    *120,"|-----",*126,"|-----",*132,"*"
.
          PRINT     *1,"|",*10,"|",*19,"|":
                    *45,"|",*52,"|":
                    *60,"|A",*63,"|D",*66,"|A",*69,"|D",*72,"|A",*75,"|D":
                    *78,"|A",*81,"|D":
                    *84,"|A",*87,"|D",*90,"|A",*93,"|D",*96,"|A",*99,"|D":
                    *102,"|A",*105,"|D":
                    *108,"|A",*111,"|D",*114,"|A",*117,"|D":
                    *120,"|A",*123,"|D",*126,"|A",*129,"|D",*132,"|"
          CALL      UND132
          ADD       "5",CLNO
.
HEAD9999  RETURN
+
.******************************************************************************
.         Create an indexed file based on port
.******************************************************************************
.
.         Check if previously exists..if so delete it
.
INDZD000  CALL      KILL0000
.         
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    UKEY1,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.             Open Temp Index file
.
          TRAP      INDZD800 IF IO
          OPEN      TEMPFIL1,FNAMEI
          TRAPCLR   IO
.
          GOTO      INDZD999
.
INDZD800  DISPLAY   *P1:24,*EL,*B,"** Temporary Index File ",*V2LON,FNAMEI:
                               *HOFF," not found **   ";
          PRINT     *10,"** Temporary Index File ",FNAMEI," not found ** "
          TRAPCLR   IO
          GOTO      INDZD999
.
INDZD999  RETURN
.
.******************************************************************************
.*                                  KILL0000              Called by: ML0000   *
.*                             Delete The Tempfile                            *
.******************************************************************************
KILL0000  CLOSE     TEMPFIL1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.******************************************************************************
.*                                 KCOD0000              Called By : ML0000   *
.*                         Keyin codes                                        *
.******************************************************************************
KCOD0000  KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,ACODE:
                    *PECOL:EVERT,*DV,ACODE
          ENDSET    ACODE
          APPEND    SP3,ACODE
          RESET     ACODE
.
          MATCH     SP3,ACODE
          GOTO      KCOD8000 IF EQUAL       * nothing
.
          PACK      KEY5,ANSC,ANSL,ACODE
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      KCOD1000 IF EQUAL
          MOVE      ONE,EXIT
          GOTO      KCOD9999
.
KCOD1000  DISPLAY   *P30:EVERT,*V2LON,TDESC;
          MOVE      ZERO,EXIT
          GOTO      KCOD9999
.
KCOD8000  MOVE      TWO,EXIT
KCOD9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY31
          READ      TEMPFIL1,KEY31;XXCLAIM,XXADMNO,XXURNUM:
                             XXPSNAM,XXPGNAM,XXBDAYS,XXPTLOS,XXPTITL:
                             XXADS01,XXADS02,XXADS03,XXADS04,XXADS05,XXADS06:
                             XXADS07,XXADS08,XXADS09,XXADS10,XXADS11,XXADS12:
                             XXDSS01,XXDSS02,XXDSS03,XXDSS04,XXDSS05,XXDSS06:
                             XXDSS07,XXDSS08,XXDSS09,XXDSS10,XXDSS11,XXDSS12
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   RESET     KEY31
          READ      TEMPFIL1,KEY31;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFIL1;XXCLAIM,XXADMNO,XXURNUM:
                             XXPSNAM,XXPGNAM,XXBDAYS,XXPTLOS,XXPTITL:
                             XXADS01,XXADS02,XXADS03,XXADS04,XXADS05,XXADS06:
                             XXADS07,XXADS08,XXADS09,XXADS10,XXADS11,XXADS12:
                             XXDSS01,XXDSS02,XXDSS03,XXDSS04,XXDSS05,XXDSS06:
                             XXDSS07,XXDSS08,XXDSS09,XXDSS10,XXDSS11,XXDSS12
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY31
          WRITE     TEMPFIL1,KEY31;XXCLAIM,XXADMNO,XXURNUM:
                             XXPSNAM,XXPGNAM,XXBDAYS,XXPTLOS,XXPTITL:
                             XXADS01,XXADS02,XXADS03,XXADS04,XXADS05,XXADS06:
                             XXADS07,XXADS08,XXADS09,XXADS10,XXADS11,XXADS12:
                             XXDSS01,XXDSS02,XXDSS03,XXDSS04,XXDSS05,XXDSS06:
                             XXDSS07,XXDSS08,XXDSS09,XXDSS10,XXDSS11,XXDSS12
          RETURN
.
UPTEMP1   MOVE      ZERO,OVRCD
          UPDATE    TEMPFIL1;XXCLAIM,XXADMNO,XXURNUM:
                             XXPSNAM,XXPGNAM,XXBDAYS,XXPTLOS,XXPTITL:
                             XXADS01,XXADS02,XXADS03,XXADS04,XXADS05,XXADS06:
                             XXADS07,XXADS08,XXADS09,XXADS10,XXADS11,XXADS12:
                             XXDSS01,XXDSS02,XXDSS03,XXDSS04,XXDSS05,XXDSS06:
                             XXDSS07,XXDSS08,XXDSS09,XXDSS10,XXDSS11,XXDSS12
          RETURN
+
.***************************************************************************
.
          INC       STD001IO
          INC       NHOSPDAY
          INC       PATCOMN3
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC            * Codes file
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC            * Admission file
          INC       PMSVX1IO/INC            * Admission file
          INC       PATDSCIO/INC            * Discharge file
          INC       PATDRGIO/INC            * Date Range file
          INC       MEHHONIO/INC            * MH HONOS
          INC       PATTRNIO/INC            * Transfer file
          INC       WEBERRIO/INC                 * Web Server Error Logging
+
.


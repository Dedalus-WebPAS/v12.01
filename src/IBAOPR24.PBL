.**********************************************************************
.* System   : Operating Room System                                   *
.* Program  : IBAOPR24                                                *
.* Desc     : Clinic Enquiry                                          *
.**********************************************************************
.* Date     :  14/12/89                                               *
.* Author   :  K.Peak                                                 *
.* Mods.    :  14/03/90  Bill Dew
.              make this program work.  IBAOPR24 is now a real program 16/03/90
.*        V5.00     18/12/1990    Justin Coates                       *
.*                  Converted to Version 5                            *
.*        V5.01.01  21/01/91  ROD                                     *
.*                  Converted to Release 5.01                         *
.*        V5.01.02  25/11/91 J.Tan      SRF 110721                    *
.*                  Fixed displaying statistics                       *********
.*        V5.01.03  24/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.
.$$$$$
.    Operating Room System Clinic File Enquiry (IBAOPR24 )
.    -----------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPRCLIA1
.               PATDO1A1
.               PATCODE1
.               CONTROLF
.
.    - Clear Input Variables
.
.    - Display Screen Format
.
.    - Input Clinic Code
.            - Read OPRCLIA1, check if exists
.
.    - Display Data
.            - Description
.            - Doctor     
.            - Doctor Type 
.
.    - END
.$$$$$
.
.         FD includes needed
.
          INC       STD001FD
          INC       OPRCLIFD/INC           * Clinic File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       PATCODFD/INC           * patient codes file
          INC       PATDO1FD/INC           * doctor file
          INC       PATDRGFD/INC
          INC       OPRCOUFD/INC           * clinic operation Usage
.
.         program variables
.
.
BOOKCASE  FORM      6
BOOKOPS   FORM      6
BOOKTIME  FORM      6
CLSUDESC  DIM       20
CODE      DIM       5
CODEDT    INIT      "DT"
CODEYG    INIT      "YG"
.
DIM3      DIM       3
DOCDESC   DIM       22        * doctor desc for clinic
DOCCODE   DIM       6
DOCTCLIN  DIM       6         * doctor/clinic code entered
EXTRCASE  FORM      6
EXTROPS   FORM      6
EXTRTIME  FORM      6
.
FORM6     FORM      6
INDEX     FORM      2
.
LNO       FORM      2
.
OPTION1   FORM      2
.
PAGENO    FORM      3
PREVDATE  DIM       6
PTCNDCQS  FORM      1
.
SCRNFLAG  FORM      1
TOTLCASE  FORM      6
TOTLOPS   FORM      6
TOTLTIME  FORM      6
TYPDESC   DIM       30
.
UVERT1    FORM      2
.
VAR       FORM      2
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
.
PRGNAM    DIM       30
PRGID     INIT      "IBAOPR24"
PRGNAM1   INIT      "Enquiry"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format 
.
          CALL      ENTR0000               * enter clinic code
          BRANCH    EXIT,ML9999
.
          CALL      DISP0000               * display details
.
ML0500    CALL      SFLD0000               * select choice of exit
          BRANCH    OPTION1,ML1000,ML3000,ML2000,ML0100,ML9999
          GOTO      ML0100
.
ML1000    CALL      BASC0000               * display basic details
          GOTO      ML0500
.
ML2000    CALL      PRNT0000               * print enquiry
          CALL      PSST0000           * Print Surgeon STatistics
          CALL      PRNT2000
          GOTO      ML0500
.
ML3000    CALL      STAT0000               * display statistics
          GOTO      ML0500
ML9999
          CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,FORTY;*96,OPCNCDSC,*111,OPCNCLSU
 MOVE "0",OPCNCLSU
.
          ENDSET    OPCNCDSC
.
INIT1000  CMATCH    SP1,OPCNCDSC
          GOTO      INIT2000 IF NOT EQUAL
.
          BUMP      OPCNCDSC,-1
          GOTO      INIT1000 IF NOT EOS
.
INIT2000  LENSET    OPCNCDSC
          RESET     OPCNCDSC
.
          PACK      PRGNAM,OPCNCDSC,SP1,PRGNAM1
          RESET     PRGNAM
          STRIP     PRGNAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening oprcliaf";
          OPEN      OPRCLIA1,"oprcliaf"
.
          DISPLAY   *P64:24,"oprcliaf";
          OPEN      OPRCLIA2,"oprcliaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"oprcouaf";
          OPEN      OPRCOUA1,"oprcouaf"
.
INIT9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000   MOVE      ZERO,PAGENO
          MOVE      SP6,DOCTCLIN            * code entered
.
. ----- Clear the Clinic File Variables -----
.
          MOVE      SP6,OPCLCLIN
          PACK      OPCLDESC,SP20,SP20
          MOVE      SP6,OPCLDOCT
          MOVE      SP3,OPCLCAT
.
. ----- Clear the Doctor File Variables -----
.
          MOVE      SP6,DCODE
          MOVE      SP9,DTITL
          PACK      DGNAME,SP20,SP20
          PACK      DSNAME,SP20,SP20
          MOVE      SP3,DRTYPE 
.
CLR9999   RETURN
+
.**********************************************************************
.*                            SCRN0000                                *
.*                     Main Data Screen Display                       *
.**********************************************************************
.
SCR0000   BRANCH    OPCNCLSU,SCR1000               * 0= Clinic, 1=Surgeon
.
          HLINE     *G37,2,47,80
          DISPLAY   *P1:3,*EF:
                    *P5:4,*+,OPCNCDSC,*-," Code",*P29:4," : ":
                    *P5:6,"Description              : ":
                    *P5:7,"Doctor                   : ":
                    *P5:8,"Doctor Type              : ";
          GOTO      SCR9999
.
SCR1000   DISPLAY   *P1:3,*EF:
                    *P5:4,*+,OPCNCDSC,*-," Code",*P29:4," : ":
                    *P5:6,"Title                    : ":
                    *P5:7,"Surname                  : ":
                    *P5:8,"Given Name(s)            : ":
                    *P5:9,"Doctor Type              : ";
.
SCR9999   RETURN
+
.**********************************************************************
.*                           ENTR00000                                *
.*                  Read Clinic File for Record                       *
.**********************************************************************
.
ENTR0000  KEYIN     *P32:4,*DV,UNDLN6,*EL:
                    *P32:4,*V2LON,DOCTCLIN;
.
          ENDSET    DOCTCLIN
          APPEND    SP6,DOCTCLIN
          RESET     DOCTCLIN
.
          DISPLAY   *P32:4,*V2LON,DOCTCLIN;
.
          MATCH     SP6,DOCTCLIN
          GOTO      ENTR9500 IF EQUAL
.
          MATCH     QUEST,DOCTCLIN
          GOTO      ENTR0500 IF NOT EQUAL
.
.         ? entered
.
          CALL      QSUR0000                * do ? routine
          BRANCH    EXIT,ENTR9500           * nothing entered
.
          CALL      SCR0000                 * redisplay screen
          DISPLAY   *P32:4,*V2LON,DOCTCLIN
.
          CALL      DISP0000                * display details
          BRANCH    EXIT,ENTR9999           * error
.
          GOTO      ENTR8000
.
.         validate what was entered
.
ENTR0500  BRANCH    OPCNCLSU,ENTR1000              * 0= Clinic, 1=Surgeon
.
          PACK      KEY6,DOCTCLIN           * validate on clinic file
          CALL      RDOPCLI1
          BRANCH    OVRCD,ENTR9000
          GOTO      ENTR9999
.
ENTR1000  PACK      KEY6,DOCTCLIN           * validate on doctor file
          CALL      RDDOCT1 
          BRANCH    OVRCD,ENTR9000
          GOTO      ENTR9999
.
ENTR8000  MOVE      FALSE,EXIT              * a valid answer
          MOVE      DOCTCLIN,OPCLCLIN       * copy what was entered
          GOTO      ENTR9999
.
ENTR9000  DISPLAY   *P1:24,"Record Not on File - Hit <",*V2LON,"ENTER":
                           *HOFF,"> to Continue";
          KEYIN     ANS
          DISPLAY   *P1:24,*EL;
          GOTO      ENTR0000
.
ENTR9500  MOVE      TRUE,EXIT                    * nothing entered
.
ENTR9999  RETURN
+
.*************************************************************************
.*                               QSUR0000                                *
.*               Question mark handling routine for surgeon or clinic    *
.*                     called by : ENTR0000                              *
.*************************************************************************
QSUR0000  BRANCH    OPCNCLSU,QSUR2000
.
.         ? for clinics
.
          CALL      OPRCLIDS
.
QSUR0500  DISPLAY   *P1:24,*+,OPCNCDSC," code : ",*EL;
.
QSUR1000  MOVELPTR  OPCNCDSC,CCOL
          ADD       "9",CCOL
          KEYIN     *PCCOL:24,*DV,UNDLN6:
                    *PCCOL:24,*V2LON,DOCTCLIN:
                    *PCCOL:24,*DV,DOCTCLIN
.
          ENDSET    DOCTCLIN
          APPEND    SP6,DOCTCLIN
          RESET     DOCTCLIN
.
          MATCH     SP6,DOCTCLIN
          GOTO      QSUR9500 IF EQUAL
.
          MATCH     QUEST,DOCTCLIN
          GOTO      QSUR0000 IF EQUAL
.
          PACK      KEY6,DOCTCLIN
          CALL      RDOPCLI1
          BRANCH    OVRCD,QSUR9000
          MOVE      FALSE,EXIT
          GOTO      QSUR9999
.
.         ? for surgeons
.
QSUR2000  MOVE      ONE,CNEWDS
          MOVE      DCODE,KEY6
          CALL      PATDOCDS
          BRANCH    EXIT,QSUR9500
.
. ------- doctor selected from ? --------------------------------
.
          MOVE      FALSE,EXIT                    * valid input
          MOVE      DOCCODE,DCODE
          PACK      KEY6,DCODE
          CALL      RDDOCT1
          CALL      PUTSCR00                      * restore screen
          MOVE      DCODE,DOCTCLIN
          MOVE      FALSE,EXIT
          GOTO      QSUR9999
.
.         common error message
.
QSUR9000  DISPLAY   *P1:24,*B,*+,OPCNCDSC,"code not on file. ";
          CALL      HITENTER
          BRANCH    OPCNCLSU,QSUR2000
          GOTO      QSUR0500
.
QSUR9500  MOVE      TRUE,EXIT
.
QSUR9999  RETURN
+
.**************************************************************************
.*                               DISP0000                                 *
.*                   Display Data from the Clinic File                    *
.**************************************************************************
.
DISP0000  DISPLAY   *P47:2,*V2LON,"- Basic Details "
          BRANCH    OPCNCLSU,DISP4000              * 0= Clinic, 1=Surgeon
.
.         using clinic details
.
          MOVE      DOCTCLIN,KEY6           * value entered
          CALL      RDOPCLI1                * read clinic file
.
.         read the doct file to see if a doctor for the clinic is set up
.
          MOVE      SP30,DOCDESC            * clear desc
          MOVE      OPCLDOCT,KEY6           * clinic doct as key
          CALL      RDDOCT1                 * read doctor file
          BRANCH    OVRCD,DISP1000          * invalid doc code
.
          MOVE      SP1,ANS
          MOVE      DGNAME,ANS
          PACK      DOCDESC,ANS,DOT,DSNAME  * set up doc name
.
.         get the doctor type
.
DISP1000  MOVE      SP30,TYPDESC
.
          MATCH     SP3,OPCLCAT
          GOTO      DISP2000 IF EQUAL       * no code to display
.
          PACK      KEY5,CODEYG,OPCLCAT
          CALL      RDCODE1
          BRANCH    OVRCD,DISP2000
          MOVE      TDESC,TYPDESC
.
DISP2000  DISPLAY   *P32:6,*V2LON,OPCLDESC:
                    *P32:7,OPCLDOCT,*HOFF,*P41:7,DOCDESC:
                    *P32:8,*V2LON,OPCLCAT,*HOFF,*P41:8,TYPDESC;
          GOTO      DISP9999
.
.         Using Surgeon Codes
.
DISP4000  MOVE      DOCTCLIN,KEY6           * the doctor entered
          CALL      RDDOCT1
.
.         get the doctor type
.
          MOVE      SP30,TYPDESC
          MATCH     SP3,DRTYPE 
          GOTO      DISP5000 IF EQUAL       * no code to display
.
          PACK      KEY5,CODEDT,DRTYPE
          CALL      RDCODE1
          BRANCH    OVRCD,DISP5000
          MOVE      TDESC,TYPDESC
.
DISP5000  DISPLAY   *P32:6,*V2LON,DTITL:
                    *P32:7,DSNAME:
                    *P32:8,DGNAME:
                    *P32:9,*V2LON,DRTYPE,*HOFF,*P41:9,TYPDESC;
.
DISP9999  RETURN
+
.*******************************************************************************
.    Lets zero the stats vars
CLRV0000
          MOVE      ZERO,TOTLCASE
          MOVE      ZERO,TOTLOPS
          MOVE      ZERO,TOTLTIME
          MOVE      ZERO,EXTRCASE
          MOVE      ZERO,EXTROPS
          MOVE      ZERO,EXTRTIME
          MOVE      ZERO,BOOKCASE
          MOVE      ZERO,BOOKOPS
          MOVE      ZERO,BOOKTIME
CLRV9999  RETURN
.**************************************************************************
.*                               STAT0000                                 *
.*                          Display Statistics                            *
.    This routine was updated by Bill Dew to extract data from the oprcouaf.
.    Once a change in the date was detected the tallied information was printed.
.**************************************************************************
.
STAT0000  BRANCH    OPCNCLSU,STAT1000              * 0= Clinic, 1=Surgeon
.
          HLINE     *G37,2,47,80
          DISPLAY   *P1:3,*EF:
                    *P47:2,*V2LON,"- Statistics ",*HOFF:
                    *P5:4,*EL,*+,OPCNCDSC,*-," Code":
                    *P29:4," : ",*V2LON,DOCTCLIN:
                    *P5:5,*HOFF,"Description              : ",*V2LON,OPCLDESC
          GOTO      STAT1500
.
STAT1000  DISPLAY   *P1:3,*EF:
                    *P47:2,*V2LON,"- Statistics ",*HOFF:
                    *P5:4,*+,OPCNCDSC,*-," Code",*P29:4," : ",*V2LON,DOCTCLIN:
                    *P5:5,*HOFF,"Surname                  : ",*V2LON,DSNAME
STAT1500  DISPLAY   *HOFF,*P18:7,"BOOKED",*P42:7,"EXTRA",*P65:7,"TOTAL":
                    *ULON,*P1:8,"Period ",*HOFF:
                    *ULON,*P11:8,"Cases   Ops    Time ",*HOFF:
                    *ULON,*P34:8,"Cases   Ops    Time ",*HOFF:
                    *ULON,*P57:8,"Cases   Ops    Time ",*HOFF
.
STAT1111  MOVE      ZERO,INDEX
          MOVE      ZERO,SCRNFLAG
          MOVE      "000000",PREVDATE
          CALL      CLRV0000           * zero the stats vars
          MOVE      EIGHT,CVERT        * set the screen line counter
          PACK      KEY21,Z20,Z20      * set to read from the end
STAT2000  CALL      RSOPCOU1           * read the clinic operation usage file
STAT2222  CALL      RPOPCOU1           * file has full key to access oprcsuaf
          BRANCH    OVRCD,STAT7777
.
          MATCH     OPCODATE,PREVDATE
          GOTO      STAT3333 IF NOT EQUAL
.
STAT3000  MOVE      OPCODATE,PREVDATE
          MATCH     OPCLCLIN,OPCODOCT
          GOTO      STAT5555 IF NOT EQUAL
.
          MOVE      ONE,SCRNFLAG
          CALL      ABET0000           * Add up date for Booked Extra Total
          GOTO      STAT2222
.
.         Different date is found
.
STAT3333  MATCH     "000000",PREVDATE
          GOTO      STAT3000 IF EQUAL
.
          COMPARE   ZERO,SCRNFLAG
          GOTO      STAT3000 IF EQUAL
.
.         Need to display data from previous period
.
          ADD       ONE,INDEX
          ADD       ONE,CVERT          * increment the line counter
          COMPARE   TWENTY3,CVERT      * test for full screen
          GOTO      STAT8888 IF EQUAL
.
          MOVE      ZERO,SCRNFLAG
          CALL      SDET0000           * display details
          CALL      CLRV0000           * clear the stats vars
          GOTO      STAT3000
.
.         Different doctor is found, check if any previous data to be displayed
.
STAT5555  COMPARE   ZERO,SCRNFLAG      * Anything to be displayed ?
          GOTO      STAT2222 IF EQUAL  * No.
.
          ADD       ONE,INDEX
          ADD       ONE,CVERT          * increment the line counter
          COMPARE   TWENTY3,CVERT      * test for full screen
          GOTO      STAT8888 IF EQUAL
.
          MOVE      ZERO,SCRNFLAG
          CALL      SDET0000           * display details
          CALL      CLRV0000           * clear the stats vars
          MOVE      OPCODATE,PREVDATE
          GOTO      STAT2222
.
STAT7777  COMPARE   ZERO,SCRNFLAG
          GOTO      STAT9999 IF EQUAL  * nothing to display
          ADD       ONE,CVERT          * increment the line counter
          MOVE      ZERO,SCRNFLAG
          CALL      SDET0000
          GOTO      STAT9999
.
STAT8888  COMPARE   ZERO,INDEX
          GOTO      STAT9999 IF EQUAL
          DISPLAY   *P1:24,*EL,"(",*V2LON,"F",*HOFF,")irst page, (":
                    *V2LON,"N",*HOFF,")ext page, (",*V2LON,"E",*HOFF,")xit : ";
          KEYIN     ANS                * keyin End page option
          REP       UPPLOW,ANS
          REPLACE   "F1N2E3f1n2e3",ANS
          MOVE      ANS,EXIT
          BRANCH    EXIT,STAT0000,STAT9000,STAT9999
          BEEP                         * give'em beeps if not F N or E
          GOTO      STAT8888
.
STAT9000  MOVE      SEVEN,CVERT        * set the starting row
          DISPLAY   *P1:CVERT,*EF
          MOVE      ZERO,SCRNFLAG
          CALL      SDET0000           * display details
          CALL      CLRV0000           * clear the stats vars
          MOVE      OPCODATE,PREVDATE
          GOTO      STAT3000
.
STAT9999  RETURN
.*******************************************************************************
.    Show details 
SDET0000  MOVE      PREVDATE,KEY6
          CALL      RDDRGA1
          BRANCH    OVRCD,SDET9000
.
          UNPACK    DRGCYR,CCENT,CYEAR 
          MOVE      DRGCNUM,CMON
          DISPLAY   *P1:CVERT,CMON,SLASH,CCENT,CYEAR,*P20:CVERT:
                    *P11:CVERT,BOOKCASE,SP1,BOOKOPS,SP1,BOOKTIME,SP3:
                    EXTRCASE,SP1,EXTROPS,SP1,EXTRTIME,SP3:
                    TOTLCASE,SP1,TOTLOPS,SP1,TOTLTIME
          GOTO      SDET9999
.
SDET9000  KEYIN     *P1:24,*EL,*V2LON,*B:
                    "** Error: ",*DV,PREVDATE," not found in Period file.":
                    " Hit <RETURN> ",*EOFF,ANS,*P1:24,*EL;
SDET9999  RETURN
.*******************************************************************************
ABET0000
          MATCH     ANSB,OPCOTYPE      * test for booked session
          GOTO      ABET2222 IF NOT EQUAL
          ADD       OPCONOPR,BOOKOPS
          ADD       OPCONCAS,BOOKCASE
          ADD       OPCOTIME,BOOKTIME
          GOTO      ABET4444           * tally up totals
ABET2222
          MATCH     ANSE,OPCOTYPE      * test for extra session
          GOTO      ABET9999 IF NOT EQUAL
          ADD       OPCONOPR,EXTROPS
          ADD       OPCOTIME,EXTRTIME
          ADD       OPCONCAS,EXTRCASE
ABET4444                               * add up totals of booked and extra
          ADD       OPCONOPR,TOTLOPS
          ADD       OPCOTIME,TOTLTIME
          ADD       OPCONCAS,TOTLCASE
ABET9999  RETURN
.**************************************************************************
.                                 PSST0000
.    The routine is the same as STAT but prints outputs instead to screen
.**************************************************************************
PSST0000
          PRINT     *N,*N,*18,"BOOKED",*42,"EXTRA",*65,"TOTAL":
                    *N,"Period ",*11,"Cases   Ops    Time ":
                    *34,"Cases   Ops    Time ",*57,"Cases   Ops    Time ":
                    *N,"---------",*11,"--------------------":
                    *34,"--------------------",*57,"--------------------"
.
PSST1111  MOVE      ZERO,INDEX
          MOVE      ZERO,SCRNFLAG
          MOVE      "000000",PREVDATE
          CALL      CLRV0000           * zero the stats vars
          PACK      KEY21,Z20,Z20      * set to read from the end
PSST2000  CALL      RSOPCOU1           * read the clinic operation usage file
PSST2222  CALL      RPOPCOU1           * file has full key to access oprcsuaf
          BRANCH    OVRCD,PSST7777
.
          MATCH     OPCODATE,PREVDATE
          GOTO      PSST3333 IF NOT EQUAL
.
PSST3000  MOVE      OPCODATE,PREVDATE
          MATCH     OPCLCLIN,OPCODOCT
          GOTO      PSST5555 IF NOT EQUAL
.
          MOVE      ONE,SCRNFLAG
          CALL      ABET0000           * Add up date for Booked Extra Total
          GOTO      PSST2222
.
.         Different date is found
.
PSST3333  MATCH     "000000",PREVDATE
          GOTO      PSST3000 IF EQUAL
.
          COMPARE   ZERO,SCRNFLAG
          GOTO      PSST3000 IF EQUAL
.
.         Need to print data from previous period
.
          ADD       ONE,INDEX
.
          COMPARE   TEN4,INDEX         * print only 14 months
          GOTO      PSST9999 IF EQUAL
          MOVE      ZERO,SCRNFLAG
          CALL      PDET0000           * print details
          CALL      CLRV0000           * clear the stats vars
          GOTO      PSST3000
.
.         Different doctor is found, check if any previous data to be printed
.
PSST5555  COMPARE   ZERO,SCRNFLAG      * Anything to be printed ?
          GOTO      PSST2222 IF EQUAL  * No.
.
          ADD       ONE,INDEX
.
          COMPARE   TEN4,INDEX         * print only 14 months
          GOTO      PSST9999 IF EQUAL
          MOVE      ZERO,SCRNFLAG
          CALL      PDET0000           * print details
          CALL      CLRV0000           * clear the stats vars
          MOVE      OPCODATE,PREVDATE
          GOTO      PSST2222
.
PSST7777  COMPARE   ZERO,SCRNFLAG
          GOTO      PSST9999 IF EQUAL  * nothing to print
          MOVE      ZERO,SCRNFLAG
          CALL      PDET0000
.
PSST9999  RETURN
.*******************************************************************************
.    Show details 
PDET0000
          MOVE      PREVDATE,KEY6
          CALL      RDDRGA1
          UNPACK    DRGCYR,CCENT,CYEAR 
          MOVE      DRGCNUM,CMON
          PRINT     *1,CMON,SLASH,CCENT,CYEAR:
                    *11,BOOKCASE,SP1,BOOKOPS,SP1,BOOKTIME,SP3:
                    EXTRCASE,SP1,EXTROPS,SP1,EXTRTIME,SP3:
                    TOTLCASE,SP1,TOTLOPS,SP1,TOTLTIME
PDET9999  RETURN
.*******************************************************************************
.*                         SFLD0000                                       *
.*                     Select choice of exit                              *
.**************************************************************************
.
SFLD0000  MOVE      ZERO,OPTION1
.
          KEYIN     *P1:24,*EL," (",*V2LON,"B",*HOFF,")asic Details, (":
                               *V2LON,"S",*HOFF,")tatistics, (":
                               *V2LON,"P",*HOFF,")rint, (":
                               *V2LON,"N",*HOFF,")ext, (":
                               *V2LON,"E",*HOFF,")xit ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    "B",ANS
          GOTO      SFLD1000 IF NOT EQUAL
          MOVE      ONE,OPTION1
          GOTO      SFLD9999
.
SFLD1000  CMATCH    "S",ANS
          GOTO      SFLD2000 IF NOT EQUAL
          MOVE      TWO,OPTION1
          GOTO      SFLD9999
.
SFLD2000  CMATCH    "P",ANS
          GOTO      SFLD3000 IF NOT EQUAL
          MOVE      THREE,OPTION1
          GOTO      SFLD9999
.
SFLD3000  CMATCH    "N",ANS
          GOTO      SFLD4000 IF NOT EQUAL
          MOVE      FOUR,OPTION1
          GOTO      SFLD9999
.
SFLD4000  CMATCH    "E",ANS
          GOTO      SFLD5000 IF NOT EQUAL
          MOVE      FIVE,OPTION1
          GOTO      SFLD9999
.
SFLD5000  BEEP  
          GOTO      SFLD0000
.
SFLD9999  RETURN
+
.**************************************************************************
.*                         BASC0000                                       *
.*                     Display Basic Details                              *
.**************************************************************************
.
BASC0000  CALL      SCR0000                 * display screen
          DISPLAY   *P47:2,*V2LON,"- Basic details ":
                    *P32:4,*V2LON,DOCTCLIN;
          CALL      DISP0000                * display details
.
BASC9999  RETURN
+
.**************************************************************************
.*                  PRNT0000                    Called by : ML2000        *
.*        Print the Basic Details                                         *
.**************************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Printing Enquiry **"
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          CALL      HEAD80
.
          PRINT     *N,*20,*+,OPCNCDSC,*-," Code",*35,": ",DOCTCLIN,*N 
.
          BRANCH    OPCNCLSU,PRNT1000              * 0= Clinic, 1=Surgeon
.
.         using clinics
.
          PRINT     *20,"Description    : ",OPCLDESC,*N:
                    *20,"Doctor         : ",OPCLDOCT,*50,DOCDESC,*N:
                    *20,"Doctor Type    : ",OPCLCAT,*50,TYPDESC,*N;
          GOTO      PRNT9999
.
.         using doctor code
.
PRNT1000  PRINT     *20,"Title          : ",DTITL,*N:
                    *20,"Surname        : ",DSNAME,*N:
                    *20,"Given Name(s)  : ",DGNAME,*N:
                    *20,"Doctor Type    : ",DRTYPE,*50,TYPDESC,*N;
          GOTO      PRNT9999
.
PRNT2000  PRINT     *N,*N,"** END OF ENQUIRY **"
          DISPLAY   *P1:24,*EL,*V2LON:
                    "** Report Generation Completed ** ";
          CALL      HITENTER
.
PRNT9999  RETURN
+
.
.         I/O includes needed
.
          INC       STD001IO
          INC       PATCODKY                * patient codes file routine
          INC       PATDRGIO/INC
          INC       PATDOCDS                * patient codes file routine
          INC       PATDOCKY                * patient codes file routine
          INC       OPRCLIDS                * patient codes file routine
          INC       PATCODIO/INC            * patient i/o for codes
          INC       PATDO1IO/INC            * doctor i/o 
          INC       OPRCLIIO/INC            * clinic i/o
          INC       OPRCOUIO/INC           * clinic operation Usage
+

.*****************************************************************************
.*    Program      : IBAWAT64                                                *
.*    Description  : Valid Waiting List Master Listing                       *
.*                                                                           *
.*    Author       : ROD     (15/12/92)                                      *
.*                                                                           *
.*    Modifications: V5.01.02  15/12/92  ROD                                 *
.*                             Change Status from codes field to Form 1      *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       WATVWLFD/INC
.
CONSNAME  DIM       25
DESC25    DIM       25
DESC37    DIM       37
DIM3      DIM       3
DOCCODE   DIM       6
ENDCODE   DIM       6
LTYPDSC   DIM       15
PENDESC   DIM       25
PENDCODE  DIM       6
PSTDCODE  DIM       6
PSTDESC   DIM       25
PTCNDCQS  FORM      1
SAVCONS   DIM       6
STATDSC   DIM       15
STDCODE   DIM       6
.
PRGID     INIT      "IBAWAT64"
PRGNAM    INIT      "Valid Waiting List Master Listing"
VERSION   INIT      "V12.01.00"
+
.****************************************************************************
.*  ML0000  :  Mainline code                                                *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit
.
ML2000    CALL      KCON0000                      * Keyin Consultant code range
          BRANCH    EXIT,ML1000                   * EXITCHAR
.
          CALL      CONTQST                       * Ok to Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.
ML5000    CALL      RPRT0000                      * Print report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.*  INIT0000  :  Initialisation                                             *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"watvwlaf"
          OPEN      WATVWLA1,"watvwlaf"
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P60:24,*EL,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.****************************************************************************
.*  MENU0000  :  Main Menu                                                  *
.****************************************************************************
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = by Consultant":
                    *P1:7,"Select Option : "
.
MENU1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BRANCH    MOPTION,MENU8000
.
          BEEP
          GOTO      MENU1000
.
MENU8000  MOVE      ZERO,EXIT
          GOTO      MENU9999
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.****************************************************************************
.*  KCON0000  :  Keyin range of Cancer codes                                *
.****************************************************************************
KCON0000  DISPLAY   *P1:9,*EF,"Start Consultant Code : ":
                    *P1:10,"End   Consultant Code : "
.
          MOVE      "25",ECOL                     * set up parameters
          MOVE      "9",EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
.
          CALL      PATDOCKY
          BRANCH    EXIT,KCON2000,KCON9000        * nothing, EXITCHAR input
.
          MOVE      DOCCODE,STDCODE               * start doctor code
          MOVE      DOCCODE,PSTDCODE              * start doctor code (print)
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
          MOVE      PACFNAME,PSTDESC              * desc for doctor
          MOVE      PACFNAME,DESC25
          DISPLAY   *P36:9,PACFNAME               * display Description
          GOTO      KCON5000
.
. nothing input for start consultant code
.
KCON2000  MOVE      SP6,STDCODE                   * start consultant code
          MOVE      "Start",PSTDCODE              * print var
          PACK      PSTDESC,SP30,SP30             * desc for print
          DISPLAY   *P25:9,*V2LON,"Start"
.
. Keyin Ending Doctor code
.
KCON5000  MOVE      "25",ECOL                     * set up parameters
          MOVE      "10",EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
.
          CALL      PATDOCKY
          BRANCH    EXIT,KCON7000,KCON9000        * nothing, EXITCHAR input
.
          MOVE      DOCCODE,ENDCODE               * end consultant type
.
          MATCH     STDCODE,ENDCODE               * valid range?
          GOTO      KCON6000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      KCON0000

KCON6000  MOVE      DOCCODE,ENDCODE               * end consultant code (print)
          MOVE      DOCCODE,PENDCODE              * code for print
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
          MOVE      PACFNAME,DESC25
          DISPLAY   *P36:10,DESC25                * display Doctor name
          MOVE      PACFNAME,PENDESC
          MOVE      ZERO,EXIT
          GOTO      KCON9999
.
. nothing input for end consultant code
.
KCON7000  MOVE      "zzzzzz",ENDCODE              * end consultant code
          MOVE      "Finish",PENDCODE             * print var
          PACK      PENDESC,SP30,SP30
          DISPLAY   *P25:10,*V2LON,"Finish"
          MOVE      ZERO,EXIT
          GOTO      KCON9999
.
KCON9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KCON9999  RETURN
+
.****************************************************************************
.*  RPRT0000  :  Print report from temp file                                *
.****************************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO                     * force new page
          MOVE      SP6,SAVCONS
.
          PACK      KEY9,STDCODE,SP10
          CALL      RSWTVWL1
RPRT0500  CALL      RKWTVWL1                      * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
          MATCH     WTVWCONS,ENDCODE              * in range?
          GOTO      RPRT9000 IF LESS
.
. check if we need a new page
.
          COMPARE   "55",CLNO
          GOTO      RPRT5000 IF LESS
.
. we need a new page
.
          CALL      HEAD132
          PRINT     *40,"Consultant From : ",PSTDCODE,SP3,PSTDESC:
                    *N,*40,"Consultant To   : ",PENDCODE,SP3,PENDESC,*N
          CALL      PTAB0000                      * print headings
          MOVE      TEN,CLNO
.
RPRT5000  CALL      GDAT0000                      * get data for printing
.
          MATCH     SP6,SAVCONS                   * 1st consultant?
          IF        @EQUAL
            PRINT     *1,"| ",WTVWCONS,SP1,CONSNAME;
            GOTO      RPRT5100
          ENDIF
.
          MATCH     WTVWCONS,SAVCONS              * same consultant?
          IF        !@EQUAL
            PRINT     *1,"|",*36,"|",*58,"|",*98,"|     |     |",*132,"|":
                      *N,*1,"| ",WTVWCONS,SP1,CONSNAME;
          ELSE
            PRINT     *1,"| ";
          ENDIF
.
. print a line of data
.
RPRT5100  PRINT     *36,"| ",WTVWLTYP,SP1,LTYPDSC:
                    *58,"| ",DESC37,*98,"| ",WTVWWTEL,*104,"| ",WTVWWTDC:
                    *110,"| ",WTVWSTAT,SP1,STATDSC,*132,"|"
.
          ADD       ONE,CLNO                      * increment line counter
          MOVE      WTVWCONS,SAVCONS              * save consultant printed
          GOTO      RPRT0500
.
RPRT9000  CALL      UND132                        * print underlines
          PRINT     *N,*1,"*** End of report ***" 
.
RPRT9999  RETURN
+
.****************************************************************************
.*  GDAT0000  :  Get data for printing                                      *
.****************************************************************************
GDAT0000  MOVE      "<Missing code>",TDESC
          PACK      KEY5,ANSW,ANSU,WTVWLTYP
          CALL      RDCODE1
          MOVE      TDESC,LTYPDSC
.
          MOVE      SP20,STATDSC
          IF        WTVWSTAT=0
            MOVE      "Active  ",STATDSC
          ENDIF
          IF        WTVWSTAT=1
            MOVE      "Inactive",STATDSC
          ENDIF
.
          MOVE      SP30,CONSNAME
          PACK      KEY6,WTVWCONS
          CALL      RDDOCT1
          BRANCH    OVRCD,GDAT2000
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CONSNAME
.
GDAT2000  MOVE      WTVWDESC,DESC37
.
GDAT9999  RETURN
+
.****************************************************************************
.*  PTAB0000  :  Print Table Heading                                        *
.****************************************************************************
PTAB0000  CALL      UND132
          PRINT     *1,"|",*36,"|",*58,"|",*98,"|WTime|WTime|",*132,"|":
                    *N,*1,"| Consultant",*36,"| List Type",*58,"| Description":
                    *98,"|Elec.| D.C.| Status",*132,"|"
          CALL      UND132
.
PTAB9999  RETURN
+
.
          INC       STD001IO
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       WATVWLIO/INC

.******************************************************************************
.*    Program      : IBAWAT47  -  PATIENTS WAITING OVER GUARANTEE DATE        *
.*    Author       : ROD                                                      *
.*    Date         : 01/02/94                                                 *
.*    Modifications:                                                          *
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*                   V5.07.B02 02/12/1998  Davin                              *
.*                             Mods for 507 (CPDATE)                          *
.******************************************************************************
.*                   V5.01.02  03/02/94  ROD                                  *
.*                             Make sure suspension end date is not past      *
.*                             the as at date of report                       *
.*                   V5.01.03  14/02/94  ROD                                  *
.*                             Make report diff format                        *
.*                   V5.01.04  08/04/94  Paul Howells                         *
.*                             Use status as at date                          *
.*                             Exclude planned suspended pats at date         *
.******************************************************************************
.
          INC       STD001FD
          INC       WATCONFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATTR1FD/INC
          INC       WATSUSFD/INC
          INC       WATPROFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       WATCATFD/INC
          INC       BOKRC1FD/INC
          INC       PATDSCFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
ASATDATE  DIM       8
ASATDTE   DIM       8
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       70
CODE      DIM       2
CONSDESC  DIM       20
DATEEND   DIM       8         * end date
DATEFROM  DIM       8         * start date
DIM3      DIM       3
DOCCODE   DIM       6
DURATION  FORM      4         * duration
EKYSPEC   DIM       3
ENDCODE   DIM       6
ENDESCOD  DIM       40
ENDESTYP  DIM       30
FDDATE    DIM       10
FHDR      INIT      "wat47a"
FNAME     DIM       8
FORM3     FORM      3
FROMDATE  DIM       8
FSTAT     FORM      1
PATNAME   DIM       24
PRDLIST   DIM       10
PRTCID    DIM       10
PROCDESC  DIM       36
SAVCONS   DIM       6
SAVSPEC   DIM       3
SCHED     INIT      "Scheduled"
PREADD    INIT      "Pre-Admitted"
PTCNDCQS  FORM      1
UNSCHED   INIT      "Unscheduled"
SCTITLE   DIM       10
SKYSPEC   DIM       3
STDCODE   DIM       6
STDESCOD  DIM       40
STDESTYP  DIM       30
STRTCENT  FORM      2
STRTDAYS  FORM      4         * START DATE variables
STRTLEAP  FORM      1
STRTYR    FORM      2
SUSPDAYS  FORM      4
TODATE    DIM       8
TODAY     DIM       8
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
PREVDAY   DIM       8
CODECC    INIT      "CC"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
PLAN      DIM       8[20][20]
INDEX1    FORM      2
INDEX2    FORM      2
WKINDEX1  FORM      2
WKINDEX2  FORM      2
LASTPLAN  DIM       1
.
.
. Temp file definition
TEMPFILE  IFILE     SQL, FIXED=64, KEY="U1-3,4-9,10-13,14-21,22-30,31-32"
XXSPEC    DIM       3      1
XXCONS    DIM       6      4
XXDIMW    DIM       4     10   * make days wait in descending order
XXURNO    DIM       8     14
XXPROC    DIM       9     22
DXXCNT    DIM       2     31
XXDWAIT   FORM      4     33
XXDLIST   DIM       8     36
XXSTATUS  DIM       12    44   * WMSTAT= 1,2 or 3
XXTCIDAT  DIM       8     56   * To Come In Date
.         End of record   64
XXCNT     FORM      2
.
PRGID     INIT      "IBAWAT47"
PRGNAM    INIT      "PATIENTS WAITING OVER GUARANTEE DATE"
VERSION   INIT      "V12.01.00"
+
.***********************************************************************
.*  ML0000  :  Mainline code                                           *
.***********************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * menu
          BRANCH    EXIT,ML9999                   * Exit selected
.
          CALL      PROC0000                      * keyin fields
          BRANCH    EXIT,ML1000                   * Cancel/Exitchar
.
ML5000    CALL      LOAD0000                      * load data into temp file
          BRANCH    EXIT,ML1000                   * No data found
.
          CALL      RPRT0000                      * print report (from temp fil)
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.***********************************************************************
.*  INIT0000  :  Initialisation                                        *
.***********************************************************************
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
.
          DISPLAY   *P55:24,"controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,THIRTY7;*211,WTCNWLSC
.
          DISPLAY   *P55:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
          DISPLAY   *P55:24,"watsusaf"
          OPEN      WATSUSA1,"watsusaf"
          DISPLAY   *P55:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          DISPLAY   *P55:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P55:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P55:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P55:24,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
          DISPLAY   *P55:24,"watcataf";
          OPEN      WATCATA1,"watcataf"
          OPEN      WATCATA2,"watcataf"
.
          IF        WTCNWLSC = ONE
            DISPLAY   *P55:24,"watseaaf"
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P55:24,"watseiaf"
            OPEN      WATSEIA1,"watseiaf"
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
          DISPLAY   *P55:24,*EL,"Opening bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P55:24,*EL,"Opening patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P55:24,*EL,"Opening patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.***********************************************************************
.*  MENU0000  :  Main Menu                                             *
.***********************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Report Inpatients":
                    *P1:6,*V2LON,"2",*HOFF," = Report Day Cases":
                    *P1:8,"Select option ? "
.
MENU1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION;
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9000,MENU9100
          IF        MOPTION<>0
            BEEP
            GOTO      MENU1000
          ENDIF
          MOVE      ONE,EXIT                      * Exit
          GOTO      MENU9999
.
MENU9000  MOVE      "Inpatients",SCTITLE
          MOVE      " - Inpatients",CPHDROPT
          GOTO      MENU9999
.
MENU9100  MOVE      "Day Cases",SCTITLE
          MOVE      " - Day Cases",CPHDROPT
.
MENU9999  RETURN
+
.**************************************************************************
.*  PROC0000  :   process keyin fields                                    *
.**************************************************************************
PROC0000  CALL      DSCR0000                      * display screen
.
PROC1000  CALL      SPEC0000                      * keyin specialty range
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
.
          CALL      CONS0000                      * keyin consultant range
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
.
          CALL      KDAT0000                      * keyin as at date
.
. ask Ok to Continue?
.
          CALL      CONTQST
          BRANCH    CEXIT,PROC8000,PROC0000,PROC9000
.
PROC8000  MOVE      ZERO,EXIT                     * all is Ok. so continue
          GOTO      PROC9999
.
PROC9000  MOVE      ONE,EXIT                      * return to menu
.
PROC9999  RETURN
+
.**************************************************************************
.*  DSCR0000  :   Display screen layout                                   *
.**************************************************************************
DSCR0000  DISPLAY   *P60:2,*V2LON,"- ",SCTITLE,*HOFF:
                    *P1:11,*EF:
                    *P1:11,"Specialty from  :":
                    *P1:12,"          to    :":
                    *P1:14,"Consultant from :":
                    *P1:15,"           to   :":
                    *P1:17,"As at date      :"
DSCR9999  RETURN
+
.****************************************************************************
.*  SPEC0000  :  Keyin range of Specialty's                                 *
.****************************************************************************
SPEC0000  MOVE      "19",ECOL                     * set up parameters
          MOVE      "11",EVERT
          MOVE      "A ",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          DISPLAY   *P19:11,*EL,*P19:12,*EL;
.
          CALL      PATCODKY
          BRANCH    EXIT,SPEC2000,SPEC9000        * nothing, EXITCHAR input
.
          MOVE      ACODE,SKYSPEC                 * start specialty
          PACK      STDESTYP,ACODE,SP5,TDESC
          DISPLAY   *P31:11,TDESC                 * display description
          GOTO      SPEC5000
.
. nothing input for start specialty
.
SPEC2000  MOVE      SP3,SKYSPEC                   * start specialty type
          MOVE      "Start",STDESTYP              * print var
          DISPLAY   *P19:11,*V2LON,"Start"
.
. Keyin Ending Specialty
.
SPEC5000  MOVE      "19",ECOL                     * set up parameters
          MOVE      "12",EVERT
          MOVE      "A ",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
.
          CALL      PATCODKY
          BRANCH    EXIT,SPEC7000,SPEC9000        * nothing, EXITCHAR input
.
          MOVE      ACODE,EKYSPEC                 * end specialty
.
          MATCH     SKYSPEC,EKYSPEC               * valid range?
          GOTO      SPEC6000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      SPEC0000
.
SPEC6000  MOVE      ACODE,ENDESTYP                * end specialty print
          DISPLAY   *P31:12,TDESC                 * display description
          PACK      ENDESTYP,ACODE,SP5,TDESC
          MOVE      ZERO,EXIT
          GOTO      SPEC9999
.
. nothing input for end specialty type
.
SPEC7000  MOVE      "zzz",EKYSPEC                 * end specialty
          MOVE      "Finish",ENDESTYP             * print var
          DISPLAY   *P19:12,*V2LON,"Finish"
          MOVE      ZERO,EXIT
          GOTO      SPEC9999
.
SPEC9000  MOVE      ONE,EXIT                      * EXITCHAR
.
SPEC9999  RETURN
+
.****************************************************************************
.*  CONS0000  :  Keyin range of Doctor codes                                *
.****************************************************************************
CONS0000  MOVE      "19",ECOL                     * set up parameters
          MOVE      "14",EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
          DISPLAY   *P19:14,*EL,*P19:15,*EL;
.
          CALL      PATDOCKY
          BRANCH    EXIT,CONS2000,CONS9000        * nothing, EXITCHAR input
.
          MOVE      DOCCODE,STDCODE               * start doctor code
          MOVE      DOCCODE,KEY6
          CALL      FDNM0000                      * format doctor name
          DISPLAY   *P31:14,PACFNAME              * display Doctor name
          PACK      STDESCOD,DOCCODE,SP2,PACFNAME
          GOTO      CONS5000
.
. nothing input for start doctor code
.
CONS2000  MOVE      SP6,STDCODE                   * start doctor code
          MOVE      "Start",STDESCOD              * print var
          DISPLAY   *P19:14,*V2LON,"Start"
.
. Keyin Ending Doctor code
.
CONS5000  MOVE      "19",ECOL                     * set up parameters
          MOVE      "15",EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
.
          CALL      PATDOCKY
          BRANCH    EXIT,CONS7000,CONS9000        * nothing, EXITCHAR input
.
          MOVE      DOCCODE,ENDCODE               * end doctor type
.
          MATCH     STDCODE,ENDCODE               * valid range?
          GOTO      CONS6000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      CONS0000
.
CONS6000  MOVE      ENDCODE,KEY6
          CALL      FDNM0000                      * format doctor name
          DISPLAY   *P31:15,PACFNAME              * display Doctor name
          PACK      ENDESCOD,DOCCODE,SP2,PACFNAME
          MOVE      ZERO,EXIT
          GOTO      CONS9999
.
. nothing input for end doctor code
.
CONS7000  MOVE      "zzzzzz",ENDCODE              * end doctor code
          MOVE      "Finish",ENDESCOD             * print var
          DISPLAY   *P19:15,*V2LON,"Finish"
          MOVE      ZERO,EXIT
          GOTO      CONS9999
.
CONS9000  MOVE      ONE,EXIT                      * EXITCHAR
.
CONS9999  RETURN
+
.**************************************************************************
.*  FDNM0000  :  Format doctor name                                       *
.*               Requires: KEY6 - Doctor name                             *
.**************************************************************************
FDNM0000  MOVE      "<<Missing Doctor>>",CONSDESC
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      SP4,PACTITLE
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,CONSDESC
          ENDIF
.
FDNM9999  RETURN
+
.**************************************************************************
.*  KDAT0000  :   Keyin as at date                                        *
.**************************************************************************
KDAT0000  PACK      TODAY,CCC,CYY,CMM,CDD         * get todays date
          REP       " 0",TODAY
.
. keyin from date
.
          MOVE      "19",CCOL
          MOVE      TEN7,CVERT
          MOVE      ZERO,CDEFDTE
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT9000                * anything input
.
          PACK      ASATDATE,CCENT,CYEAR,CMON,CDAY
          PACK      FDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    * display var
          REP       " 0",ASATDATE 
          REP       " 0",FDDATE 
.
          MOVE      ZERO,EXIT                     * Valid range
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT                      * nothing input
.
KDAT9999  RETURN
+
.***********************************************************************
.*  LOAD0000  :  Load data into temp file                              *
.***********************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading...";
.
          CALL      CRET0000                      * create temp file
.
. calculate the date the patient must have been on the list
.
          MOVE      ASATDATE,DATEFROM
          IF        MOPTION=1
            MOVE      "-365",DURATION
          ELSE
            MOVE      "-183",DURATION
          ENDIF
.
          CALL      CEDT0000                      * calculate end date
.
. get all patients on the list before the date calculated (above)
.
          PACK      KEY27,DATEEND,ZED20
          CALL      RDSTREA3
LOAD1000  CALL      RDPTREA3
          BRANCH    OVRCD,LOAD9000
.
          DISPLAY   *P60:24,WMURNO
.
          MATCH     STDCODE,WMDOCTOR
          GOTO      LOAD1000 IF LESS
          MATCH     WMDOCTOR,ENDCODE
          GOTO      LOAD1000 IF LESS
.
          MATCH     SP3,WMPCAT                    * ignore if no specialty
          GOTO      LOAD1000 IF EQUAL
.
          MATCH     SKYSPEC,WMPCAT
          GOTO      LOAD1000 IF LESS
          MATCH     WMPCAT,EKYSPEC
          GOTO      LOAD1000 IF LESS
.
          MOVE      ASATDATE,ASATDTE
          CALL      CHKSTAT              * check status at date
.
          COMPARE   FOUR,FSTAT
          GOTO      LOAD1000 IF NOT LESS
.
. check if we have the correct admission class
.
          PACK      KEY5,ANSP,SP1,WTWMCLSS
          CALL      RDCODE1
          BRANCH    OVRCD,LOAD1000
.
          IF        MOPTION=2
            MATCH     "2",TCINDC2                 * must be a day case
            GOTO      LOAD1000 IF NOT EQUAL
          ELSE
            MATCH     "2",TCINDC2                 * must NOT be a day case
            GOTO      LOAD1000 IF EQUAL
          ENDIF
.
. check security
.
          CALL      WATCKSEC
          BRANCH    EXIT,LOAD1000
.
. we have a valid patient so remove the suspension & planned period
.
          CALL      CSUS0000                      * calculate suspension period
          BRANCH    EXIT,LOAD1000                 * suspended in period
.
          MOVE      WMDATE1,CDYSFDTE
          MOVE      ASATDATE,CDYSTDTE
          CALL      CALCDAYS
.
          SUB       SUSPDAYS,CDYSDAYS
          IF        MOPTION=1
            COMPARE   "365",CDYSDAYS
            GOTO      LOAD1000 IF LESS
          ELSE
            COMPARE   "183",CDYSDAYS
            GOTO      LOAD1000 IF LESS
          ENDIF
.
. set up variables for write
.
          MOVE      WMPCAT,XXSPEC
          MOVE      WMDOCTOR,XXCONS
          MOVE      WMURNO,XXURNO
          MOVE      WMCODE,XXPROC
          MOVE      WMCNT,XXCNT
          MOVE      CDYSDAYS,XXDWAIT
          MOVE      WMDATE1,XXDLIST
          MOVE      WMDATE3,XXTCIDAT
. the stuff below is to make waiting period print in descending order    SUPER!
          MOVE      XXDWAIT,XXDIMW
          REP       "9a8b7c6d5e4f3g2h1i0j k",XXDIMW
.
          LOAD      XXSTATUS,FSTAT,UNSCHED,SCHED,PREADD
.
          PACK      KEY36,XXSPEC,XXCONS,XXDIMW,XXURNO,XXPROC,XXCNT
          CALL      WRTEMP1
          GOTO      LOAD1000
.
. check if any data was found
.
LOAD9000  MOVE      ZERO,EXIT
          PACK      KEY36,SP30
          CALL      RSTEMP1
          CALL      RKTEMP1
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"No data found.  ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
.
LOAD9999  RETURN
+
.***********************************************************************
.*  CSUS0000  :  Calculate period suspended & planned for              *
.*               Returns: SUSPDAYS                                     *
.***********************************************************************
CSUS0000  MOVE      ZERO,SUSPDAYS
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP20
          CALL      RSWTSUS1
CSUS1000  CALL      RKWTSUS1
          BRANCH    OVRCD,CSUS5000
.
          PACK      DIM19,WTSUURNO,WTSUCODE,WTSUCNT
          MATCH     DIM19,KEY21
          GOTO      CSUS5000 IF NOT EQUAL
.
. check if suspended as at the period run date
.
          MATCH     ASATDATE,WTSUFDAT
          IF        @LESS | @EQUAL
            MATCH     WTSUTDAT,ASATDATE
            IF        @LESS | @EQUAL
              MOVE      ONE,EXIT                  * ignore this patient
              GOTO      CSUS9999
            ENDIF
          ENDIF
.
. if end suspension date is past the as at date of report then calculate the
. suspension period only up to the as at date.
.
          MATCH     WTSUTDAT,ASATDATE
          IF        @LESS
            MOVE      ASATDATE,WTSUTDAT
            MATCH     WTSUFDAT,WTSUTDAT * after change, make sure to > from date
            GOTO      CSUS1000 IF LESS
          ENDIF
.
          MOVE      WTSUFDAT,CDYSFDTE
          MOVE      WTSUTDAT,CDYSTDTE
          CALL      CALCDAYS
          ADD       CDYSDAYS,SUSPDAYS
          GOTO      CSUS1000
.
. check period of planned
.
CSUS5000  MATCH     SP3,WTWMEATY
          GOTO      CSUS9500 IF EQUAL
.
.******** check if planned patient as at date ********
.
          MOVE      ZERO,INDEX1
          MOVE      ZERO,INDEX2
          MOVE      ANSN,LASTPLAN
          MOVE      SP8,TODATE
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANSC,ANSC,SP10
          CALL      RSWTCAT2
CSUS6000  CALL      RKWTCAT2
          BRANCH    OVRCD,CSUS7000
.
          MATCH     WMURNO,WTCAURNO
          GOTO      CSUS7000 IF NOT EQUAL
.
          MATCH     WMCODE,WTCAPROC
          GOTO      CSUS7000 IF NOT EQUAL
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      CSUS7000 IF NOT EQUAL
.
          MATCH     CODECC,WTCACATF
          GOTO      CSUS7000 IF NOT EQUAL
.
          MATCH     WTCAEDAT,ASATDATE 
          GOTO      CSUS7000 IF LESS 
.
          PACK      KEY5,ANSC,ANSC,WTCACODT,SP5
          CALL      RDCODE1
          BRANCH    OVRCD,CSUS6000
          MATCH     ANSP,TCINDC3
          IF        @EQUAL
              MATCH     LASTPLAN,ANSN
              IF        @EQUAL
                ADD       ONE,INDEX1
                MOVE      WTCAEDAT,PLAN[INDEX1][1]
                MOVE      ANSY,LASTPLAN
              ENDIF
              GOTO      CSUS6000 
          ENDIF
.
          MATCH     ANSN,LASTPLAN
          GOTO      CSUS6000 IF EQUAL
.
          MOVE      ANSN,LASTPLAN
          ADD       ONE,INDEX2
          MOVE      WTCAEDAT,PLAN[INDEX1][2]
          GOTO      CSUS6000
.
CSUS7000  MOVE      ZERO,WKINDEX1
          MOVE      ZERO,WKINDEX2
.
CSUS8000  COMPARE   WKINDEX1,INDEX1
          GOTO      CSUS9000 IF EQUAL
.  
          COMPARE   ZERO,INDEX2
          IF        @EQUAL
            MOVE      ONE,EXIT
            GOTO      CSUS9999
          ENDIF
.
          ADD       ONE,WKINDEX1
          COMPARE   WKINDEX2,INDEX2
          IF        @EQUAL
            MOVE      ONE,EXIT
            GOTO      CSUS9999
          ENDIF
.
          ADD       ONE,WKINDEX2
          MOVE      PLAN[WKINDEX1][1],CDYSFDTE
          MOVE      PLAN[WKINDEX1][2],CDYSTDTE
          CALL      CALCDAYS
          ADD       CDYSDAYS,SUSPDAYS
          IF        SUSPDAYS > 0
            SUB       ONE,SUSPDAYS          * do not count to date
          ENDIF
          GOTO      CSUS8000
.         
CSUS9000  COMPARE   ZERO,INDEX1
          GOTO      CSUS9500 IF EQUAL
.
          MATCH     ASATDATE,CDYSFDTE
          IF        @LESS | @EQUAL
            MATCH     CDYSTDTE,ASATDATE
            IF        @LESS | @EQUAL
              MOVE      ONE,EXIT
              GOTO      CSUS9999
            ENDIF
          ENDIF
.
CSUS9500  MOVE      ZERO,EXIT
CSUS9999  RETURN
+
.***********************************************************************
.*  RPRT0000  :  Print Report                                          *
.***********************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,CNOUNDLN
          MOVE      SP3,SAVSPEC
          MOVE      "60",CLNO                     * force new page
.
          PACK      KEY36,SP30
          CALL      RSTEMP1  
RPRT0500  CALL      RKTEMP1                       * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
. check if we need a new page
.
          COMPARE   "55",CLNO
          GOTO      RPRT5000 IF LESS
.
. we need a new page
.
          CALL      PAGE0000
.
. check if we have a new specialty or a new consultant
.
RPRT5000  MATCH     XXSPEC,SAVSPEC
          GOTO      RPRT5200 IF NOT EQUAL
          MATCH     XXCONS,SAVCONS
          GOTO      RPRT6000 IF EQUAL
.
. print a new table heading
.
RPRT5200  MATCH     SP3,SAVSPEC
          IF        !@EQUAL
            CALL      UND132
            PRINT     *1
          ENDIF
.
          MOVE      "<<Missing>>",TDESC
          PACK      KEY5,ANSA,SP1,XXSPEC
          CALL      RDCODE1
          PRINT     *1,"Specialty  : ",XXSPEC,SP5,TDESC
          MOVE      SP6,SAVCONS
.
          MOVE      XXCONS,KEY6
          CALL      FDNM0000                      * format doctor name
          PRINT     *1,"Consultant : ",XXCONS,SP2,CONSDESC
          CALL      TABL0000
          ADD       FIVE,CLNO
.
. now print a line
.
RPRT6000  CALL      FRMD0000                      * format data for printing
.
. print a line
.
          PRINT     *1,"| ",PATNAME,*28,"| ",XXURNO,*39,"|",PRDLIST:
                    *50,"| ",XXPROC,SP1,PROCDESC,*99,"| ",XXSTATUS,*114:
                    "|",PRTCID,*125,"| ",XXDWAIT,*132,"|"
.
          MOVE      XXSPEC,SAVSPEC
          MOVE      XXCONS,SAVCONS
          ADD       ONE,CLNO
          GOTO      RPRT0500
.
. no more records
.
RPRT9000  CALL      UND132
          PRINT     *N,*1,"NOTE:(i)   Waiting period excludes time on ":
                    "Suspension and time Planned.":
                    *N,"     (ii)  Patients who are unscheduled,scheduled and ":
                    "Pre-Admitted As at the Report date are included in this ":
                    "report.":
                    *N,"     (iii) Patients who are Suspended or Planned As ":
                    "at the Report date are excluded from this report.":
                    *N,"     (iv)  Utilises Date On List for Patient Selection"
          PRINT     *N,*1,"*** End of report ***"
.
          CALL      KILL0000                      * erase temp file
.
RPRT9999  RETURN
+
.*************************************************************************
.*  FRMD0000  :  format data for printing                                *
.*************************************************************************
FRMD0000  MOVE      "<<Missing Master Rec>>",PATNAME
          PACK      KEY8,XXURNO
          CALL      RDMAST1
          IF        OVRCD=0
            MOVE      SP4,PACTITLE
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATNAME
          ENDIF
.
          MOVE      XXCONS,KEY6
          CALL      FDNM0000                      * format doctor name
.
          MOVE      "<<Missing Procedure>>",WPDESC
          PACK      KEY9,XXPROC
          CALL      RDPROA1
          MOVE      WPDESC,PROCDESC
.
          UNPACK    XXDLIST,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDLIST
.
          UNPACK    XXTCIDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTCID
.
FRMD9999  RETURN
+
.*************************************************************************
.*  PAGE0000  :  Print a new page                                        *
.*************************************************************************
PAGE0000  CALL      HEAD132
.
          PRINT     *40,"Specialty  from : ",STDESTYP,*N:
                    *40,"           to   : ",ENDESTYP,*N:
                    *40,"Consultant from : ",STDESCOD,*N:
                    *40,"           to   : ",ENDESCOD,*N:
                    *40,"     As at Date : ",FDDATE,*N
.
          MOVE      "9",CLNO
          MOVE      SP3,SAVSPEC
          MOVE      SP3,SAVCONS
.
PAGE9999  RETURN
+
.*************************************************************************
.* TABL0000  :  print table heading                                      *
.*************************************************************************
TABL0000  CALL      UND132
          PRINT     *1,"| Patient Name",*28,"|  U/R No.",*39,"| List Date":
                    "| Procedure",*99,"| Status",*114,"| TCI Date",*125:
                    "| Wait",*132,"|"
          CALL      UND132
TABL9999  RETURN
+
.*************************************************************************
.*  Temp file IO's                                                       *
.*************************************************************************
RSTEMP1   RESET     KEY36
          READ      TEMPFILE,KEY36;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY36
          READ      TEMPFILE,KEY36;XXSPEC,XXCONS,XXDIMW,XXURNO,XXPROC,DXXCNT:
                                   XXDWAIT,XXDLIST,XXSTATUS,XXTCIDAT
          GOTO      OVERCOND IF OVER
          MOVE      DXXCNT,XXCNT
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXSPEC,XXCONS,XXDIMW,XXURNO,XXPROC,DXXCNT:
                             XXDWAIT,XXDLIST,XXSTATUS,XXTCIDAT
          GOTO      OVERCOND IF OVER
          MOVE      DXXCNT,XXCNT
          RETURN
.
WRTEMP1   RESET     KEY36
          MOVE      XXCNT,DXXCNT
          WRITE     TEMPFILE,KEY36;XXSPEC,XXCONS,XXDIMW,XXURNO,XXPROC,DXXCNT:
                                  XXDWAIT,XXDLIST,XXSTATUS,XXTCIDAT
          RETURN
.
UPTEMP1   MOVE      XXCNT,DXXCNT
          UPDATE    TEMPFILE;XXSPEC,XXCONS,XXDIMW,XXURNO,XXPROC,DXXCNT:
                             XXDWAIT,XXDLIST,XXSTATUS,XXTCIDAT
          RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temporary file                                    *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*  CRET0000  :  create temporary file                                   *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 64 U1-3,4-9,10-13,14-21,22-30,31-32",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TFILNAME
.
CRET9999  RETURN
.
.*************************************************************************
.                   CEDT0000
.         Calculate an end date given a start date and duration 
.         Para's  : DATEFROM      the starting date
.                   DURATION      the number of days
.         Returns : DATEEND       the end date
.*************************************************************************
.
.         turn the starting date into julian days
.
CEDT0000  UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          MOVE      CDAY,DD                 * set up start day
          MOVE      CMON,MM                 *              month
          MOVE      CYEAR,YY                *              year
          MOVE      CCENT,CC                *              century
.
          CALL      DMYCON                  * get julian day
          MOVE      JULDAY,STRTDAYS         * get current julian day
          MOVE      JULYR,STRTYR            * get current julian year
          MOVE      JULCC,STRTCENT          * get current julian century
          MOVE      LEAPYR,STRTLEAP         * get leap year flag (1=leap)
.
.         add the number of days
.
          ADD       DURATION,STRTDAYS       * get the total days for the year
.
. if strtdays is < 0 then we need to get the previous year
.
          IF        STRTDAYS<0
            SUB       ONE,STRTYR
            MOVE      STRTCENT,CC
            MOVE      STRTYR,YY
            CALL      DMLEAPYR
            ASSIGN    (365+STRTDAYS+LEAPYR),STRTDAYS
          ENDIF
.
.         convert back to calender date
.
          MOVE      STRTDAYS,FORM3   * since strtdays is form 4 and jkwday dim3
          MOVE      FORM3,JWKDAY
          MOVE      STRTYR,JWKYER           * work year
          MOVE      STRTCENT,JWKCC          * work century
.
          CALL      JULCON                  * convert to DD,MM,YY,CC
.
.         check for change in centuries
.
          COMPARE   STRTYR,YY
          GOTO      CEDT5000 IF NOT LESS    * ending year >= starting year
.
.         need a change in centuries
.
          ADD       ONE,STRTCENT            * add a century
.
.         set up end date
.
CEDT5000  PACK      DATEEND,STRTCENT,YY,MM,DD
          REP       " 0",DATEEND
.
CEDT9999  RETURN
+
.
          INC       STD001IO
          INC       PATCODIO/INC
          INC       WATTR1IO/INC
          INC       WATSUSIO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WATPROIO/INC
          INC       PATMA1IO/INC
          INC       PATDO1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       WATCATIO/INC
          INC       BOKRC1IO/INC
          INC       PATDSCIO/INC
          INC       CALCDAYS
          INC       PATCODKY
          INC       PATDOCKY
          INC       WATCKSEC
          INC       CHKSTAT
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
.

.***************************************************************************
.* System    :   Private Practice                                          *
.* Program   :   IBAPRI92                                                  *
.* Desc      :   End Of Year Reset                                         *
.***************************************************************************
.* Date      :   08/10/91                                                  *
.* Author    :   Stephen Armstrong                                         *
.* Function  :   Allows the user to input a carry forward balance for a    *
.*               particular item in the financial summary file for the     *
.*               current financial year                                    *
.* Mods.     :                                                             *
.*        V10.13.01 06/08/2018  Richa Phogat  TSK 0848506                  *
.*                  Extended keylength for RDPRFN1                         *
.*        V9.12.01  08/07/2009  Mike Laming   CAR 199944                   *
.*                  Added Medical Practice selection Option and correct    *
.*                  read of "prifinsf"                                     *
.*        V9.11.01  08/01/2009 J.Tan      CAR 178415                       *
.*                  Added Medical Practice to prifinsf                     *
.*        V9.02.01  09/02/2002  J.Tan                                      *
.*                  Mods for doctor income percentage                      *
.*        V5.07.01  16/10/1999  D.Di Paola                                 *
.*                  Recompiled for PRIITMFD                                *
.*        V5.05.01  08/10/1997  Steve Armstrong         WWH Mods           *
.*                  Mods for change from GETITEM to KYPRIITM               *
.*        V5.05.01  08/10/1997  Steve Armstrong         WWH Mods           *
.*                  Mods for change from GETITEM to KYPRIITM               *
.*        V5.05.01  08/10/1997  Steve Armstrong         WWH Mods           *
.*                  Mods for change from GETITEM to KYPRIITM               *
.***************************************************************************
.*               V6.01 26/03/92 Steve Armstrong                            *
.*               Recompiled for changes to PRIITMDS                        *
.*               V6.00.02 31/08/92 Steve Armstrong                         *
.*                        Removed PRCNSDAT as not in use                   *
.***************************************************************************
.
.$$$$$
.         End of Year Reset Program (IBAPRI92)
.         ------------------------------------
.
.         ML0000
.         Open files and initialisation
.         prifinsf
.         priitemf
.         controlf
.         patdrgaf
.
.         ML0100
.         Get user option
.         If zero selected, exit program
.
.         ML1000                                                      *C-199944
.         Get the Practice Code                                       *I-199944
.
.         Get item type (MBS or AMA)
.         
.         Get Item Code
.         If nothing/exitchar entered, return to get next option
.         If "?" input, display items on file
.         If valid code input, proceed to get forward balance
.
.         Get forward balance
.
.         Confirm reset
.         If (Y)es input, continue with reset
.         If (N)o input, return to get next item type
.         If (C)ancel input, return to get next option
.
.         ML2000
.         Update financial summary file
.
.         Return to get next option
.
.         ML9999
.         Chain back to calling program
.
.$$$$$
.
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PRIFINFD/INC                 * financial summary file
          INC       PRIITMFD/INC                 * item file
          INC       PRICONFD/INC                 * control file
          INC       PRIPRAFD/INC                 * Practices table    *I-199944
.
          INC       PATCOMM/INC
          INC       PATDRGFD/INC                 * date range file
.
.
. CONSTANTS DEFINITION
. --------------------
.
AMA       INIT      "AMA"
MBS       INIT      "MBS"
NINE8     FORM      "99999999"
.
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
MPRAC     DIM       6                            * for Practice Code
IFLAG     FORM      2                            * for KYPRIITM
ITEMNUMB  DIM       9                            * for KYPRIITM
ITMDESC   DIM       30                           * for PRIITMDS
ITMFLAG   FORM      1                            * for PRIITMDS
.
PATHFLAG  FORM      1                            * for PRIITMDS
RSTORSCN  FORM      1                                                 *I-199944
.
SERDATE   DIM       8                            * for PRIITMDS
SHTDESC   DIM       23                           * for PRIITMDS
SUBITEM   DIM       3                            * for KYPRIITM
.
TOTAMNT   FORM      7.2
.
.
PRGID     INIT      "IBAPRI92"
PRGNAM    INIT      "End Of Year Reset"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000
.
ML1000    CALL      PRAC0000               * get the Practice
          BRANCH    EXIT,ML0100                  
.
ML1200    CALL      TYPE0000               * get item type
.
ML1400    CALL      PROC0000               * get misc. item code
          BRANCH    EXIT,ML1000            * nothing/exitchar input
.
          CALL      GETB0000               * get balance
.
          CALL      POSTQST                * confirm reset
          BRANCH    CEXIT,ML2000:          * (Y)es
                          ML1000:          * (N)o
                          ML0100           * (C)ancel
.
ML2000    CALL      UPD0000                * update financial summary file
.
          GOTO      ML0100                 * get next option
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"prifinsf";
          OPEN      PRIFINS1,"prifinsf"          * financial summary file
.
          DISPLAY   *P64:24,"pripracf";          * priv.practice table I-199944
          OPEN      PRIPRAC1,"pripracf"
.
          DISPLAY   *P64:24,"priitemf";
          OPEN      PRIITEM1,"priitemf"
          OPEN      PRIITEM2,"priitemf"          * item file
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"          * date range file
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"          * control file
          READ      CONTROLF,THIRTY4;*199,PRCNAFEE
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.*              COPTION = 1  Enter carry forward balance                    *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Enter Carry Forward Balance"
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL            * exit
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000                     * invalid option
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                             TYPE0000                Called by: ML0000    *
.*                        get the item type                                 *
.****************************************************************************
.
TYPE0000  KEYIN     *P1:11,*EF,*P1:24,"(",*V2LON,*DV,ANSM,*HOFF,")BS or (":
                    *V2LON,*DV,ANSA,*HOFF,")MA":
                    *P1:11,"Item Type : ",*DV,ANSM:
                    *P13:11,*V2LON,ANS
.
          RESET     ANS
          GOTO      TYPE1000 IF EOS              * nothing input
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSM,ANS                     * M entered ?
          GOTO      TYPE1000 IF EQUAL            * yes
.
          MATCH     ANSA,ANS                     * A entered ?
          GOTO      TYPE2000 IF EQUAL            * yes
.
          BEEP
          GOTO      TYPE0000
.
TYPE1000  MOVE      ZERO,IFLAG                   * set MBS flag
          DISPLAY   *P13:11,*EF,*V2LON,MBS
          GOTO      TYPE9999
.
TYPE2000  MOVE      ONE,IFLAG                    * set AMA flag
          DISPLAY   *P13:11,*EF,*V2LON,AMA
.
TYPE9999  RETURN
+
.****************************************************************************
.*                             PROC0000                Called by: ML0000    *
.*                   get carry forward item code                            *
.****************************************************************************
PROC0000  MOVE      ZERO,RSTORSCN
.
PROC1000  DISPLAY   *P1:13,*EL,"Enter Carry Forward item code : "
.
          MOVE      THIRTY3,CCOL                 * set screen position
          MOVE      TEN3,CVERT 
          CALL      KYPRIITM                     * get an item #
          BRANCH    EXIT,PROC9000:               * exitchar entered
                         PROC4000:               * ? entered
                         PROC9000                * nothing entered
.
          CALL      CHKI0000                     * see if item on file
          BRANCH    EXIT,PROC1000
.
          GOTO      PROC8000
.
PROC4000  MOVE      ZERO,HLEF
          CALL      GETSCR00                     * save screen
          MOVE      ONE,RSTORSCN
.
PROC4200  MOVE      ONE,ITMFLAG
          MOVE      ONE,PATHFLAG
          CALL      PRIITMDS                     * list codes on file
.
PROC4500  DISPLAY   *P1:24,*EL,"Item Number: "
          MOVE      TEN4,CCOL
          MOVE      TWENTY4,CVERT
          CALL      KYPRIITM                     * get item number
          BRANCH    EXIT,PROC9000:               * exitchar entered
                         PROC4200:               * ? entered
                         PROC9000                * nothing entered
.
          CALL      CHKI0000                     * item on file ?
          BRANCH    EXIT,PROC4500                * no
.
          CALL      PUTSCR00                     * restore screen
          DISPLAY   *P33:13,*V2LON,ITEMNUMB
.
          MATCH     SP3,SUBITEM                  * subitem number ?
          GOTO      PROC8000 IF EQUAL            * no
.
          DISPLAY   *P42:13,"(",*V2LON,SUBITEM,*HOFF,")"
PROC8000  DISPLAY   *P50:13,PRITDESC             * item found
          MOVE      ZERO,EXIT
          GOTO      PROC9999
.
PROC9000  IF        RSTORSCN = 1
            CALL      PUTSCR00
          ENDIF     
          MOVE      ONE,EXIT
.
PROC9999  RETURN
+
.***************************************************************************
.*                             CHKI0000               Called by: PROC0000  *
.*                    check if item on file                                *
.* Returns:          EXIT    0 = valid item                                *
.*                           1 = invalid item                              *
.***************************************************************************
.
CHKI0000  PACK      KEY22,IFLAG,ITEMNUMB,SUBITEM,SP30
          CALL      RSPRIT1                      * position in file
          CALL      RKPRIT1                      * read next record
          BRANCH    OVRCD,CHKI5000               * end of file
.
          COMPARE   PRITFLAG,IFLAG               * same item type ?
          GOTO      CHKI5000 IF NOT EQUAL        * no
.
          MATCH     ITEMNUMB,PRITITMN            * same item number ?
          GOTO      CHKI5000 IF NOT EQUAL        * no
.
          MATCH     SUBITEM,PRITSUBN             * same subitem number ?
          GOTO      CHKI5000 IF NOT EQUAL        * no
.
          MOVE      ANSA,PRFNTYPE
          IF        PRCNAFEE=1
            MOVE      SP70,KEY10
            PACK      PRFNCODE,ANSM,KEY10,PRITSETC,PRITFLAG,PRITITMN:
                               PRITSUBN,SP30
          ELSE
            PACK      PRFNCODE,ANSM,PRITSETC,PRITFLAG,PRITITMN,PRITSUBN,SP30
          ENDIF
          GOTO      CHKI9000
.
CHKI5000  DISPLAY   *P1:24,*EL,*B,"Item not on file.  ";
          CALL      HITENTER
          GOTO      CHKI9500
.
CHKI9000  MOVE      ZERO,EXIT
          GOTO      CHKI9999
.
CHKI9500  MOVE      ONE,EXIT
.
CHKI9999  RETURN
+
.****************************************************************************
.*                                GETB0000             Called by: ML0000    *
.*                      get carry forward balance                           *
.****************************************************************************
.
GETB0000  KEYIN     *P1:15,*EL,"Enter new carry forward value : ",*V2LON,"$":
                    *P34:15,*V2LON,TOTAMNT:
                    *P34:15,*DV,TOTAMNT
.
GETB9999  RETURN
+
.****************************************************************************
.*                              UPD0000                Called by: ML0000    *
.*                    update financial summary file                         *
.****************************************************************************
.
UPD0000   DISPLAY   *P1:24,*EL,*P50:24,*V2LON,"** Posting Data **";
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD      * get current fin. year
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,UPD9500
.
          MOVE      DRGYR,PRFNYEAR          * Key Field - PRFNTYPE & PRFNCODE
.                                           * were set in PROC000
.
          PACK      PRFNMPRA,MPRAC,SP10     * Private Practice Code   *I-199944
.
UPD1000   PACK      KEY39,PRFNYEAR,PRFNTYPE,PRFNCODE,PRFNMPRA,SP30
          CALL      RDPRFN1                 * position on record
          IF        OVRCD = 1
            CALL      CLPFIN00              * clear other variables
            MOVE      TOTAMNT,PRFNPR01
            CALL      WRPRFN1
            GOTO      UPD9999
          ENDIF
.
          MOVE      TOTAMNT,PRFNPR01
          CALL      UPPRFN1                      * update existing record
.
          GOTO      UPD9999
.
UPD9500   DISPLAY   *P1:24,*EL,*B,"Current year not on period file.  ";
          CALL      HITENTER
          GOTO      UPD0000
.
UPD9999   RETURN
+
.****************************************************************************
.*                              CLPFIN00               Called by: UPD000    *
.*                 clear financial summary fields                           *
.****************************************************************************
CLPFIN00  MOVE      ZERO,PRFNPERA
          MOVE      ZERO,PRFNPR01
          MOVE      ZERO,PRFNPR02
          MOVE      ZERO,PRFNPR03
          MOVE      ZERO,PRFNPR04
          MOVE      ZERO,PRFNPR05
          MOVE      ZERO,PRFNPR06
          MOVE      ZERO,PRFNPR07
          MOVE      ZERO,PRFNPR08
          MOVE      ZERO,PRFNPR09
          MOVE      ZERO,PRFNPR10
          MOVE      ZERO,PRFNPR11
          MOVE      ZERO,PRFNPR12
          MOVE      ZERO,PRFNPR13
          MOVE      ZERO,PRFNLAST
          MOVE      SP30,PRFNSPAR
CLPFIN99  RETURN
+
. ******************************************* pirated from IBAPRI32   *I-199944
.****************************************************************************
.*                              PRAC0000               Called by: ML0000    *
.*                 get the starting medical practice                        *
.****************************************************************************
PRAC0000  MOVE      ZERO,EXIT
          MOVE      SP6,MPRAC
          DISPLAY   *P1:9,*EF,*P1:9,"Private Practice : ",*V2LON,MPRAC
PRAC1000  KEYIN     *P21:9,*DV,UNDLN6,*P21:9,*V2LON,MPRAC
.
          DISPLAY   *P1:24,*EL;
.
          CALL      CHEK0000                 * see what was entered
          BRANCH    EXIT,PRAC9900:           * exitchar entered
                         PRAC4000:           * ? entered
                         PRAC9000:           * valid M.P.
                         PRAC8000            * nothing entered
.
          GOTO      PRAC0000                 * invalid M.P.
.
PRAC4000  MOVE      ZERO,HLEF
          CALL      GETSCR00
PRAC4200  CALL      PRIPRADS                 * list M.P.'s on file
.
PRAC4500  KEYIN     *P1:24,*EL,"Medical Practice : ",*DV,UNDLN6:
                    *P20:24,*V2LON,MPRAC
.
          CALL      CHEK0000                 * see what was entered
          BRANCH    EXIT,PRAC9800:           * exitchar entered
                         PRAC4200:           * ? entered
                         PRAC8900:           * valid M.P. number
                         PRAC7900            * nothing entered
.
          GOTO      PRAC4500                 * invalid M.P.
.
PRAC7900  CALL      PUTSCR00                 * restore screen
PRAC8000  DISPLAY   *P1:24,"A valid code must be entered";
          GOTO      PRAC1000
.
PRAC8900  CALL      PUTSCR00                 * restore screen
PRAC9000  DISPLAY   *P21:9,*EL,*V2LON,MPRAC:
                    *P40:9,*HOFF,PRPRDESC
.
PRAC9500  MOVE      ZERO,EXIT
          GOTO      PRAC9999
.
PRAC9800  CALL      FRESCR00
PRAC9900  MOVE      ONE,EXIT
.
PRAC9999  RETURN
+
.**************************************************************************
.*                                  CHEK0000           Called by: PRAC0000*
.*                         See if anything entered                        *
.*         Exit - 0 = invalid entry                                       *
.*                1 = nothing/exitchar entered                            *
.*                2 = ? entered                                           *
.*                3 = valid medical practice
.**************************************************************************
.
CHEK0000  ENDSET    MPRAC
          APPEND    SP6,MPRAC
          RESET     MPRAC
.
          MATCH     SP6,MPRAC               * anything entered ?
          GOTO      CHEK9500 IF EQUAL       * no
.
          MATCH     EXITCHAR,MPRAC          * exitchar entered ?
          GOTO      CHEK7000 IF EQUAL       * yes
.
          MATCH     QUEST,MPRAC             * ? entered ?
          GOTO      CHEK8000 IF EQUAL       * yes
.
.         Medical Practice code entered
.
          MOVE      MPRAC,KEY6
          CALL      RDPRPR1                  * code on file ?
.
          COMPARE   ZERO,OVRCD               * yes
          GOTO      CHEK9000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Medical Practice not on file. ";
          CALL      HITENTER
.
          MOVE      ZERO,EXIT               * invalid entry
          GOTO      CHEK9999
.
CHEK7000  MOVE      ONE,EXIT                * nothing input
          GOTO      CHEK9999
.
CHEK8000  MOVE      TWO,EXIT                * ? input
          GOTO      CHEK9999
.
CHEK9000  MOVE      THREE,EXIT              * valid practice
          GOTO      CHEK9999
.
CHEK9500  MOVE      FOUR,EXIT               * nothing entered
.
CHEK9999  RETURN
+
. =========================================================================
.  I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PRIPRADS                                          *I-199944
.
          INC       KYPRIITM
          INC       PRIITMDS
          INC       PATCOMN3                     * for GPERD
.
          INC       PRIFINIO/INC                 * financial summary file
          INC       PRIITMIO/INC                 * item file
          INC       PRIPRAIO/INC                 * Practices table    *I-199944
.
          INC       PATDRGIO/INC                 * date range file

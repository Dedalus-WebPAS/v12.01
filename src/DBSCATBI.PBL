.        DBSCATBI : Display and Print Discharge Billing Statement
.
.        NOTE : CMXFLAG  0-charge patcat, 1-Casemix , 2-user to fix matrix
.               PCFLAG   0-Casemix, 1-revert to patcat, 2-patcat & casemix
.                        3-revert to patcat from high boundary or critical care
.
.        VERSION  :
.
.         V11.04.01 11/06/2024  J.Tan          TSK 0946701
.                   Fixed calculating items(amt x quantity) greater than FORM6P2
.         V11.03.01 12/09/2023  J.Tan          TSK 0935891
.                   Added ACCMDESC for displaying longer bed fees desc.
.         V10.14.01 28/06/2019  J.Tan          TSK 0857392
.                   Mod for ABF invoice
.         V10.13.01 07/01/2019  J.Tan            TSK 0868242
.                   Mod to initialise PTITTCER
.         V10.07.04 10/03/2016  J.Tan            CAR 0322614
.                   Fixed printing lumpsum description
.         V10.07.03 07/03/2016  J.Tan            CAR 0814361
.                   Changed bed days variables to BPDAYS, BSDAYS, BCDAYS
.         V10.05.01 23/09/2014  J.Tan        CAR 253233
.                   Mods to calculate days of CCU/ICU/SCN/HDU
.         V10.04.02 03/02/2014  J.Tan        CAR 265430
.                   Mods to display Pat.total(Disc.Billing amount) on inv.header
.         V10.03.02 25/09/2013 J.Tan           CAR 281515
.                   Mods checking Catg FI Ind8=1 to exclude item
.         V10.03.01 12/12/2012 J.Tan           CAR 237130
.                   Mods to calculate total of PHP & PHX Pharmacy Items
.         V9.12.02  30/07/2009  J.Tan             CAR 200712
.                   Fixed Cabrini Discharged billing statement
.         V9.12.01  11/05/2009  J.Tan             CAR 125842
.                   Added parameter to recalculate zero amount of Misc.item
.         V9.11.01  25/02/2009  J.Tan    CAR 190567
.                   Mods printing DBS for Cabrini
.         V9.11.00  23/10/2008  J.Tan    CAR 181263
.                   Created new routine to print Discharge Billing Statement
.
.        This include displays and prints the Accommodation, MBS Item and
.        Miscellaneous charges.
.
.---------------------------------------------------------------------------
.
.        IF PROGFLAG = 2  THEN DISPLAY DISCHARGE BILLING STATEMENT
.
.        IF PROGFLAG = 6  THEN PRINT DISCHARGE BILLING STATEMENT (NO UPDATE)
.
.--------------------------------------------------------------------------
.
.*******************************************************************************
.          Display/write accommodation record for Discharge Billing Statement
.*******************************************************************************
DBAC0000  PACK      ACCMDESC,ACCMDESC,SP70
          MATCH     SP70,ACCMDESC
          IF        @EQUAL
            PACK      ACCMDESC,TDDESC,SP70    * longer var for Accom.description
          ENDIF
.
          MATCH    ANSD,TMOVE
          IF       @EQUAL
            IF        CMXFLAG=1 & MVBFLAG=1
              UNPACK    DDATE,TCENT1,TYEAR1,TMON1,TDAY1  * use ddate for invoice
            ENDIF
          ENDIF
          MOVE      LSTRATE,XLSTRATE
          MOVE      LINEGST,XLINEGST
          ADD       LINEGST,LINETOT
          MOVE      LINETOT,XLINETOT
          ADD       LINETOT,TDBSAMNT
          BRANCH    PROGFLAG OF DBAC1000,DBAC2000,DBAC9999,DBAC9999,DBAC9999:
                                DBAC5000
.
DBAC1000  CALL      SCPAY0000             * Set co-payment for private/shared
          GOTO      DBAC9999
.
.
.         Display accommodation record
.
DBAC2000  COMPARE   ONE,DCFLAG
          GOTO      DBAC2500 IF NOT EQUAL
.
          COMPARE   ZERO,NODAYS
          GOTO      DBAC8000 IF EQUAL          * no additional charge
.
          MOVE      FMON1,FORM2
          LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                  NSEP,NOCT,NNOV,NDEC
          MOVE      TMON1,FORM2
          LOAD      MTHNAM3A,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL:
                              NAUG,NSEP,NOCT,NNOV,NDEC
.
          PACK      KEY4,FCENT1,FYEAR1
          REP       " 0",KEY4
          WRITE     HTMLFILE;"<tr><td>",FDAY1,SP1,MTHNAM3,SP1,KEY4:
                               " to ";
.
          PACK      KEY4,TCENT1,TYEAR1
          REP       " 0",KEY4
          WRITE     HTMLFILE;TDAY1,SP1,MTHNAM3A,SP1,KEY4;
.
          WRITE     HTMLFILE;"</td><td>",ACCMDESC;
.
          WRITE     HTMLFILE;"</td><td>&nbsp;":
                                   "</td><td align=right>",LSTRATE:
                                   "</td>&nbsp;":
                                   "</td><td align=right>",LINETOT:
                                   "</td></tr>"
          CALL      SCPAY0000             * Set co-payment for private/shared
          GOTO      DBAC8000
.
DBAC2500  COMPARE   ONE,CMXFLAG
          GOTO      DBAC2800 IF NOT EQUAL
.
          MATCH     ADATE,DDATE
          IF        @EQUAL
            IF        LSTRATE = 0
              GOTO      DBAC8000          * daycase without daily charge
            ENDIF
          ENDIF
.
DBAC2800  MOVE      FMON1,FORM2
          LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                  NSEP,NOCT,NNOV,NDEC
          MOVE      TMON1,FORM2
          LOAD      MTHNAM3A,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                     NSEP,NOCT,NNOV,NDEC
.
          IF        NODAYS=1
            MOVE      "DAY",DAYFORMT
          ELSE
            MOVE      "DAYS",DAYFORMT
          ENDIF
.
          PACK      KEY4,FCENT1,FYEAR1
          REP       " 0",KEY4
          WRITE     HTMLFILE;"<tr><td>",FDAY1,SP1,MTHNAM3,SP1,KEY4;
.
          PACK      KEY4,TCENT1,TYEAR1
          REP       " 0",KEY4
          WRITE     HTMLFILE;" to ":
                                 TDAY1,SP1,MTHNAM3A,SP1,KEY4;
          WRITE     HTMLFILE;"</td><td>",NODAYS,SP1,DAYFORMT,SP1,ACCMDESC;
.
          WRITE     HTMLFILE;"</td><td>&nbsp;":
                             "</td><td align=right>",LSTRATE:
                             "</td><td>&nbsp;":
                             "</td><td align=right>",LINETOT:
                             "</td></tr>"
          CALL      SCPAY000              * Set co-payment for private/shared
          GOTO      DBAC8000
.
.         Print the accommodation line
.
DBAC5000  COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          CALL      SCPAY000              * Set co-payment for private/shared
          MOVE      FDAY1,CDAY
          MOVE      FMON1,CMON
          MOVE      FCENT1,CCENT
          MOVE      FYEAR1,CYEAR
          CALL      PACDATE
.
          PRINT     *1,CPDATE;
.
          MOVE      TDAY1,CDAY
          MOVE      TMON1,CMON
          MOVE      TCENT1,CCENT
          MOVE      TYEAR1,CYEAR
          CALL      PACDATE
.
          COMPARE   "25",PTCNINVL
          GOTO      DBAC6000 IF EQUAL      * Cabrini
.
          MOVE      NODAYS,FORM3
          PRINT     *10,CPDATE:
                    *19,FORM3;
.
          MOVE      XLSTRATE,FORM62
          PRINT     *23,TDDESC:
                    *53,FORM62;
.
          PRINT     *71,XLINETOT;
          GOTO      DBAC7000
.
DBAC6000  PRINT     *10,CPDATE:
                    *19,TDDESC;
.
          MOVE      XLSTRATE,FORM52
          PRINT     *50,FORM52;
.
          PRINT     *59,NODAYS;
.
          MOVE      XLINEGST,FORM52
          PRINT     *64,FORM52;
.
          MOVE      XLINETOT,FORM52
          PRINT     *73,FORM52;
.
DBAC7000  ADD       XLINETOT,TOTAMNT
          CALL      PEOL0000
          CALL      CKTL0000
.
DBAC8000  CALL      MTDNFD00                * Move to date to next from date
DBAC9999  RETURN
+
.*******************************************************************************
.        Setup the co-payment for private or shared
.*******************************************************************************
SCPAY000 MOVE       "BT",KEY2
         PACK       KEY5,KEY2,STRBTYP,SP70
         CALL       RDCODE1
         BRANCH     OVRCD,SCPAY999
.
         IF         NODAYS<>0
           ADD        NODAYS,DYCHRGED
         ENDIF
.
         UNPACK     PTCDNHCP,KEY1
         MATCH      "1",KEY1
         IF         @EQUAL
           MOVE       PTELPPRI,FORM12P2
           IF         DCFLAG<>1 | NODAYS<>0
             MULT       NODAYS,FORM12P2
             ADD        NODAYS,BPDAYS
           ENDIF
           ADD        FORM12P2,CPAYPRIV        * Co-payment for Private
           GOTO       SCPAY999
         ENDIF
.
         MATCH      "2",KEY1
         IF         @EQUAL
           MOVE       PTELPSHA,FORM12P2
           IF         DCFLAG<>1 | NODAYS<>0
             MULT       NODAYS,FORM12P2
             ADD        NODAYS,BSDAYS
           ENDIF
           ADD        FORM12P2,CPAYSHRD        * Co-payment for Shared
           GOTO       SCPAY999
         ENDIF
.
         MATCH      "3",KEY1
         IF         @EQUAL
           MOVE       PTELPCCU,FORM12P2
           IF         DCFLAG<>1 | NODAYS<>0
             MULT       NODAYS,FORM12P2
             ADD        NODAYS,BCDAYS
           ENDIF
           ADD        FORM12P2,CPAYCCUH        * Co-payment for CCU,ICU,SCN,HDU
           GOTO       SCPAY999
         ENDIF
.
SCPAY999 RETURN
+
.*******************************************************************************
.        Search for any miscellaneous items for this invoice
.*******************************************************************************
GDBM0000 CALL      CTMP0000             * create tempfile to sort items
         CALL      PT2P0000             * create misc item file 
.
         MOVE      ZERO,TOTMISC
         MOVE      ZERO,TOTTHEA
         MOVE      ZERO,TOTOTHR
         MOVE      ZERO,PHXIAMNT        * Total of PHX Pharmacy items
         MOVE      ZERO,PHPIAMNT        * Total of PHP Pharmacy items
.
         MOVE      SP8,TREF
         REP       " 0",TREF
         MOVE      SP10,DIM9         * Initialise misc.item code
         MOVE      ZERO,COUNTA
.
         MOVE      ZERO,FORM6
         MOVE      SP20,CURSESS
         PACK      KEY39,SP70
.
         CALL      RDSSORT1
GDBM2300 CALL      RDKSORT1
         BRANCH    OVRCD,GDBM2330
.
         MOVE      PINVNO,TREF
.
         PACK      KEY14,AADMNO,ZTRNN
         CALL      RDPMMTI1
         BRANCH    OVRCD,GDBM2300
         CALL      MTDT0000                  * move fields
.
.        Check if item to be excluded from display/calculation
.
         MATCH     SP70,TMISGRP
         IF        !@EQUAL
           MOVE      "FI",KEY2
           PACK      KEY5,KEY2,TMISGRP
           CALL      RDCODE1
           IF        OVRCD=0
             MATCH     "1",TCINDC8
             GOTO      GDBM2300 IF EQUAL   * exclude item
           ENDIF
         ENDIF
.
         MATCH     "3",ZRECTYP
         GOTO      GDBM2328 IF NOT EQUAL   * not miscellaneous item
.
.        Process Miscellaneous Items (DTRs)
.
         COMPARE   ONE,CMXFLAG
         GOTO      GDBM2320 IF NOT EQUAL   * not casepayment
.
.        accummulate no of items to see if this item tobe charged for casemix
.
         MATCH     DIM9,ZMISC
         IF        @EQUAL
           ADD       ONE,COUNTA
         ELSE
           MOVE      ONE,COUNTA
           MOVE      ZMISC,DIM9          * save the new item code
         ENDIF
.
GDBM2320 CALL      GMIS0000                * recalculate misc.
         BRANCH    OVRCD OF GDBM2328
         MATCH     "Y",PTMCKREB
         GOTO      GDBM2328 IF EQUAL       * manual change for rebate
.
         COMPARE   ONE,CMXFLAG
         GOTO      GDBM2323 IF NOT EQUAL   * not casemix, just charge item
.
         COMPARE   ZERO,PTMCCFCY           * No charge on all items
         GOTO      GDBM2325 IF EQUAL
.
         COMPARE   PTMCCFCY,COUNTA         * Charge from item number ?
         GOTO      GDBM2325 IF LESS
.
.        Check for front end deductable item
.
GDBM2323 MOVE      MPATPOR,FORM12P2
         ADD       MHFPOR,FORM12P2
         COMPARE   ZERO,FORM12P2
         GOTO      GDBM2327 IF EQUAL    * front end deductable - don't change
.
.        Check if this item was entered manually. 
.                   
         IF        MPATPOR=0 & MHFPOR=0           
.                   
.        Check with parameter if Zero amount of item to be recalculated
.                   
           MATCH     "1",PTCNCALM
           GOTO      GDBM2324 IF EQUAL
           GOTO      GDBM2327           *  do not change the amount
         ENDIF
.                   
GDBM2324 COMPARE   TWO,MNINV
         GOTO      GDBM2327 IF EQUAL    *  split invoices - don't change amount
.
         MOVE      MPATPOR,TPATAMTP
         MOVE      MHFPOR,TREBATE
         GOTO      GDBM2327
.
GDBM2325 MOVE      ZERO,TPATAMTP
         MOVE      ZERO,TREBATE
.
GDBM2327 MOVE      TPATAMTP,TPATAMTT
         ADD       TREBATE,TPATAMTT
         IF        TSERVS<>0
           MULT      TSERVS,TPATAMTT
         ENDIF
.
         GOTO      GDBM2329
.
.        Make sure total of patient and rebate portion equal to tatal (tpatamtt)
.
GDBM2328 COMPARE   ZERO,TSERVS
         GOTO      GDBM2329 IF EQUAL
         MOVE      TPATAMTP,FORM12P2
         ADD       TREBATE,FORM12P2
         MULT      TSERVS,FORM12P2
         COMPARE   TPATAMTT,FORM12P2
         GOTO      GDBM2329 IF EQUAL
         DIV       TSERVS,TPATAMTP
         DIV       TSERVS,TREBATE
.
.        End of Miscellaneous Items (DTRs)  - Type "3" Process
.
GDBM2329 MOVE      ZERO,OVRCD
         MOVE      DTRECTYP,TMPRECTY
         PACK      TMPDTRDT,TFCENT,TFYEAR,TFMONTH,TFDAY
         REP       " 0",TMPDTRDT
         IF        TRECTYPE=2
.....      MOVE      ZUNIQ,TMPZUNIQ                                     D-46224
           ADD       ONE,FORM6              * use input seq from 1st   *I-46224
           MOVE      FORM6,TMPZUNIQ         * work file - SORTFILE     *I-46224
         ELSE
           MOVE      DTTRANSN1,TMPZUNIQ
         ENDIF
         MOVE      DTTRANSN1,TMPTRNSC
         MOVE      TREF,TMPREFNC
         MOVE      TPATAMTP,TMPAMNTP
         MOVE      TREBATE,TMPREBAT
         PACK      KEY15,TMPRECTY,TMPDTRDT,TMPZUNIQ
         CALL      WRSRTM1
.
         MATCH      "1",PTCNGPHR
         IF         @EQUAL
           CALL       GPHR0000           * Group Pharmacy Items
         ENDIF
.
         GOTO      GDBM2300
.
. **************************************************************************
.*       End of loop to write to 2nd Tempfile - Read Tempfile back
. **************************************************************************
.        Write a mechv billing record if required. We want this to appear as the
.        first misc. charge on the invoice (directly under accommodation).
.
GDBM2330 IF        CMXFLAG=1 & MVBFLAG=1
           MOVE      "1",TMPRECTY           * want this to be first record
           MOVE      MVBDATE,TMPDTRDT
           MOVE      "     +",TMPZUNIQ       * dummy transaction number
           PACK      KEY15,TMPRECTY,TMPDTRDT,TMPZUNIQ
           CALL      WRSRTM1
         ENDIF
.
         MOVE     ZERO,MBSCOUNT
         PACK     KEY15,SP70
         CALL     RDSSRTM1
GDBM2335 CALL     RDKSRTM1
         BRANCH   OVRCD,GDBM9999
.
         MOVE     SP70,PTITTCER
         MATCH    "     +",TMPZUNIQ
         IF       @EQUAL
           CALL     PMVBR000              * populate dtr fields for mv billing
           CALL     MTDT0000              * move var from pmsmti to dtr
           GOTO     GDBM2340
         ENDIF
.
         PACK      KEY14,AADMNO,TMPTRNSC
         CALL      RDPMMTI1
         BRANCH    OVRCD,GDBM2335
         MOVE      PMMITRAN,SAVTRNNO
         CALL      MTDT0000             * move fields
         IF        TRECTYPE=2
           ADD       ONE,MBSCOUNT
         ENDIF
.
         MOVE      TMPAMNTP,TPATAMTP
         MOVE      TMPREBAT,TREBATE
         MOVE      TMPAMNTP,TPATAMTT
         ADD       TMPREBAT,TPATAMTT
         MULT      TSERVS,TPATAMTT
.
         MOVE      TMPAMNTP,PMMIAMTP
         MOVE      TMPREBAT,PMMIRBAT
         MOVE      TMPAMNTP,PMMIAMTT
         ADD       TMPREBAT,PMMIAMTT
         MULT      TSERVS,PMMIAMTT
.
. If we are backdating invoices then check if we have passed the specified
. date
.
GDBM2340 IF       BDTEFLAG = 1
           MOVE     TYEAR,CYEAR
           MOVE     TMON,CMON
           MOVE     TDAY,CDAY
           MOVE     TCENT,CCENT
           PACK     KEY8,CCENT,CYEAR,CMON,CDAY
           REP      " 0",KEY8

           MOVE     TFDAY,CDAY
           MOVE     TFMONTH,CMON
           MOVE     TFYEAR,CYEAR
           MOVE     TFCENT,CCENT
           PACK     CKYIDEF8,CCENT,CYEAR,CMON,CDAY
           REP       " 0",CKYIDEF8
.
           MATCH    CKYIDEF8,KEY8
           GOTO     GDBM2335 IF LESS        * Return to read next DTR  
         ENDIF
.
.        If it has already been printed, then ignore it
.
         BRANCH    TINVPRT,GDBM2335         * Return to read next DTR  
.
.        Check if two invoices are wanted
.
         LOAD      NUMINVS,TNINVS,NUMINVS,TWO                          *C-90226
.
         IF        TRECTYPE=2
           MOVE      ONE,CBFLAG
           CALL      GETIT00           * re-calculate theatre fees
           MOVE      ZERO,CBFLAG
         ENDIF
.
.        Display ABF invoice:
.         â€“ display the Actual amount of Patient only items( ie items with zero HF portion)
.         -  display zero amount for Items with HF portion

.        Raising ABF invoice will NOT print Patient Only items
.
         IF        ABFFLAG=1
           IF        TMPREBAT<>0
             MOVE      ZERO,TREBATE
             MOVE      ZERO,TPATAMTP
             MOVE      ZERO,TPATAMTT
             MOVE      ZERO,PMMIRBAT
             MOVE      ZERO,PMMIAMTP
             MOVE      ZERO,PMMIAMTT
           ELSE
             COMPARE   THREE,PROGFLAG
             GOTO      GDBM2335 IF EQUAL   * Print inv - skip Patient only item
           ENDIF
         ENDIF
.
         MOVE      TPATAMTP,LINETOT
         MULT      TSERVS,LINETOT
.
.    Process this miscellaneous item
.
         MOVE      ZERO,LINEGST
         IF        IBCNUGST=2 & PTDTGSTA=1
           CALL      IGST0000               * Get Item GST rate
           MOVE      LINETOT,LINEGST
           MULT      IBGEAMNT,LINEGST     * GST amount
           DIV       "100.00",LINEGST     * Divide by 100 to get wanted fig.
         ENDIF
         ADD       LINEGST,LINETOT
         MOVE      SP9,PRTITCOD
.
.        If this is a theatre charge, do any theatre charge processing
.
.        COMPARE   TWO,TRECTYPE
.        CALL      PROCTHEA IF EQUAL
.
.        Check "Hospital ID', if MBS Code should be printed on invoice
.
         IF        TRECTYPE=2
           MOVE      TITEMNO,PRTITCOD     * Print MBS item
           IF        IBCNMHOS=1
             MATCH     "1",PTHSPMBS
             IF        !@EQUAL
               MOVE      SP10,PRTITCOD
             ENDIF
           ENDIF
         ENDIF
.
.        If this is a miscellaneous charge, do any miscellaneous processing
.
         IF        TRECTYPE=3
           MOVE      SP10,PRTITCOD
           CALL      PROCMISC
           IF        PTCNPMIS=1
             MOVE      TITEMNO,PRTITCOD     * Print miscellaneous item
           ENDIF
         ENDIF
.
         MOVE      LINETOT,XLINETOT
         ADD       LINETOT,TDBSAMNT
.
         BRANCH    PROGFLAG,GDBM2335,GDBM2600,GDBM9999,GDBM9999,GDBM9999:
                            GDBM2800
.
.        Display miscellaneous item
.
GDBM2600 PACK      DIM30,TDDESC,SP20
.
         MOVE      SP70,DIM20
         MATCH     SP70,PTITTCER
         IF        !@EQUAL & !@EOS
           PACK      KEY5,CATCR,PTITTCER,SP70
           CALL      RDCODE1
           IF        OVRCD=0
.            IF        TCFORM6=1 | TCFORM6=2
             MATCH     "D",TCINDC1
             IF        @EQUAL
               MOVE      TDESC,DIM20
               MOVE      SP70,PTITTCER
               MOVE      ONE,CERTINDC
             ENDIF
           ENDIF
         ENDIF
.
GDBM2670 MOVE      TFMONTH,FORM2
         LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                      NSEP,NOCT,NNOV,NDEC
         PACK      KEY4,TFCENT,TFYEAR
         REP       " 0",KEY4
.
         MOVE      "0",KEY1              * Set 'No' to Change item desc.
         IF        TRECTYPE=3
           COMPARE   ONE,MINDIC
           IF        @EQUAL
             MOVE      "1",KEY1          * Set 'Yes' to Change item desc.
           ENDIF
         ENDIF
.
         MOVE      "0",ANS               * Set 'No' to Change item amount
         IF        TRECTYPE=3
           MATCH     "Y",PTMCKREB
           IF        @EQUAL
             MOVE      "1",ANS           * Set 'Yes' to Change item amount
           ENDIF
         ENDIF
.
         MATCH     "1",PMMIMVBR
         IF        @EQUAL
           WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                              "<td>",MVDAYS,"x ",DIM30,"</td>":
                              "<td>",TITEMNO,"</td>";
         ELSE
           WRITE     HTMLFILE;"<tr><td>",TFDAY,SP1,MTHNAM3,SP1,KEY4,"</td>";
           MATCH     SP70,DIM20
           IF        @EQUAL | @EOS
             WRITE     HTMLFILE;"<td>",DIM30,"</td>";
           ELSE
             STRIP     DIM20
             WRITE     HTMLFILE;"<td>",DIM30:
             " (<font color=red>",DIM20,"</font>)</td>";
           ENDIF
           WRITE     HTMLFILE;"<td><img src=../images/EditIcon.gif ":
                              " class=ListIcon onclick='DeleteItem(#"":
                              AURNO,"#",#"":
                              AADMNO,"#",#"":
                              PMMITRAN,"#",#"":
                              KEY1,"#",#"",ANS,"#")'>":
                              TITEMNO,"</td>";
         ENDIF
.
         WRITE     HTMLFILE;"<td align=right>",TPATAMTP,"</td>":
                            "<td align=right>&nbsp;</td>":
                            "<td align=right>",LINETOT,"</td>":
                            "</tr>"
.
         GOTO      GDBM2335                 * read next DTR 
.
.        Print miscellaneous item
.
GDBM2800 CALL      CKTL0000     * check if tail needs to be printed
.
         MOVE      TFDAY,CDAY
         MOVE      TFMONTH,CMON
         MOVE      TFCENT,CCENT
         MOVE      TFYEAR,CYEAR
         CALL      PACDATE
.
         COMPARE   MAXLINE,COUNT
         CALL      TAIL IF NOT LESS
.
         MOVE      LINETOT,XLINETOT
         PACK      KEY20,TDDESC,SP20
.
         COMPARE   "25",PTCNINVL      * Cabrini invoice?
         GOTO      GDBM7000 IF EQUAL
.
         MATCH     "1",PMMIMVBR       * want days not date for mv billing
         IF        @EQUAL
           PRINT     *18,MVDAYS;
         ELSE
           PRINT     *1,CPDATE;
         ENDIF
         PRINT     *23,KEY20,*44,PRTITCOD;
         MOVE      PMMIAMTP,FORM62
         PRINT     *53,FORM62;
         PRINT     *71,XLINETOT;
         GOTO      GDBM8000
.
GDBM7000 MATCH     "1",PMMIMVBR       * want days not date for mv billing
         IF        @EQUAL
           PRINT     *1,MVDAYS;
         ELSE
           PRINT     *1,CPDATE;
         ENDIF
         PRINT     *10,PRTITCOD,*19,KEY20;
         MOVE      PMMIAMTP,FORM52
         PRINT     *50,FORM52;
.
         MOVE      ONE,F4
         IF        PMMISERV<>0
           MOVE      PMMISERV,F4
         ENDIF
         PRINT     *59,F4;
         MOVE      LINEGST,FORM52
         PRINT     *64,FORM52;
         MOVE      XLINETOT,FORM52
         PRINT     *73,FORM52;
.
GDBM8000 CALL      PEOL0000
         GOTO      GDBM2335      * Read next DTR
.
GDBM9999 RETURN
+
.*******************************************************************************
.         Group Pharmacy items for Discharge billing statement
.*******************************************************************************
GPHR0000  COMPARE   ONE,DBSFLAG
          GOTO      GPHR9999 IF NOT EQUAL
.
          MATCH     SP70,PMMIMGRP
          GOTO      GPHR9999 IF EQUAL
.
          MOVE      "FI",KEY2
          PACK      KEY5,KEY2,PMMIMGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,GPHR9999
.
          MATCH     "P",TCINDC5
          IF        @EQUAL
            MOVE      TPATAMTP,FORM12P2
            MULT      TSERVS,FORM12P2
            ADD       FORM12P2,PHPIAMNT      * Total of PHP Phamacy items
          ELSE
            MATCH     "X",TCINDC5
            IF        @EQUAL
              MOVE      TPATAMTP,FORM12P2
              MULT      TSERVS,FORM12P2
              ADD       FORM12P2,PHXIAMNT    * Total of PHX Phamacy items
            ENDIF
          ENDIF
.
GPHR9999  RETURN
+
.*******************************************************************************
.         Generate the lumpsum description
.*******************************************************************************
LSDB0000  CALL      GTAB0000         *get table type
          MOVE      SP30,TEDESC
          PACK      TDDESC,SP20,SP20
          PACK      PTDTDES2,SP20,SP20
          MOVE      PTTRLSPT,SPTTRLSP
          MOVE      PTTRLSRB,SPTTRLSR
.
.         If patient discharged and it's a daycase, get desc. from daycase rate
.
          COMPARE   ZERO,DCFLAG
          GOTO      LSDB0070 IF EQUAL    * overnight
.
. ------  sameday ------
.
          CALL      DLUM0000         * Get desc. for daycase lumpsum amount
          MOVE      LSDESC1,SAVDES1
          MOVE      LSDESC2,SAVDES2
          MOVE      KEY27,SKEY27     * save the key
          IF        NOFEE=0
            MOVE      PTLSLPAT,PTTRLSPT
            MOVE      PTTRLSPT,SPTTRLSP
          ENDIF
.
          MOVE      PTLSNINV,NUMINVS
          MOVE      PTLSNINV,TNINVS
          GOTO      LSDB0900
.
. ------ overnight lumpsum ------
.
LSDB0070  CALL      OLUM0000         * Get desc. & cut off for lumpsum amount
          MOVE      LSDESC1,SAVDES1
          MOVE      LSDESC2,SAVDES2
          MOVE      KEY27,SKEY27      * save the key
          IF        NOFEE=0
            MOVE      PTHLLPAT,PTTRLSPT
            MOVE      PTTRLSPT,SPTTRLSP
            MOVE      SPTTRLSP,LINETOT    * patient lumpsum
          ENDIF
.
          MOVE      PTHLNINV,NUMINVS
          MOVE      PTHLNINV,TNINVS
.
LSDB0900  PACK      PTDTLSD1,SAVDES1,SP20
          PACK      PTDTLSD2,SAVDES2,SP20
.
          PACK      TDDESC,SAVDES1,SP20,SP20
.
          MOVE      PTTRLSPT,LINETOT    * patient lumpsum
          MOVE      ZERO,SPTTRLSR
          IF        IBCNUGST=2
            MOVE      LINETOT,GSTWORK
            MULT      GSTPCEN,GSTWORK
            DIV       "100.00",GSTWORK
            ADD       GSTWORK,LINETOT
          ENDIF
          ADD       LINETOT,TDBSAMNT
.
          BRANCH    PROGFLAG,LSDB9000   * workout to be invoiced only
.
          COMPARE   TWO,PROGFLAG
          GOTO      LSDB5000 IF NOT EQUAL    * print invoice
.
.         Display next invoice
.
          MOVE      FMON1,FORM2
          LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                NSEP,NOCT,NNOV,NDEC
          PACK      KEY4,FCENT1,FYEAR1
          REP       " 0",KEY4
          WRITE     HTMLFILE;"<tr><td>",FDAY1,SP1,MTHNAM3,SP1,KEY4:
                             "</td>";
.
          WRITE     HTMLFILE;"<td>",TDDESC,"</td>";
.
          WRITE     HTMLFILE;"<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>",LINETOT,"</td>":
                             "</tr>"
          GOTO      LSDB9000
.
.         Check if we are printing invoice or update patfins only
.
LSDB5000  MOVE      LINETOT,XLINETOT
.
. ------  Print invoice ------
.
          MOVE      FDAY1,CDAY
          MOVE      FMON1,CMON
          MOVE      FCENT1,CCENT
          MOVE      FYEAR1,CYEAR
          CALL      PACDATE
.
          MOVE      FDAY1,CDAY
          MOVE      FMON1,CMON
          MOVE      FCENT1,CCENT
          MOVE      FYEAR1,CYEAR
          CALL      PACDATE
.
          MOVE      XLINETOT,FORM52
          IF        PTCNINVL=25
            MOVE      XLINETOT,FORM52
            PRINT     *1,CPDATE:
                      *19,TDDESC:
                      *73,FORM52;
          ELSE
            PRINT     *1,CPDATE:
                      *23,TDDESC:
                      *71,XLINETOT;
          ENDIF
          CALL      PEOL0000
          CALL      CKTL0000     * check if tail needs to be printed
.
LSDB9000  MOVE      ZERO,EXIT
LSDB9999  RETURN
+

.-----------------------------------------------------------------------------
.   Program       : makedcs
.   Function      : Create Site Transfer File <program>.dcs
.   Modifications :   
.
.   V9.09.01 17.04.2008 Sandra Barcham     166599
.            Changed from compress to gzip
.   V6.00.04 12.05.93 B.G.Ackland
.            Fix Multi File Transfer
.   V6.00.03 12.05.93 B.G.Ackland
.            Fix File Layout for Screen Master
.            Transfer Linked Program ID Details
.-----------------------------------------------------------------------------
          INC       IBACOMM/INC
          INC       IBAKEYS/INC
          INC       SCRSBGFD/INC
          INC       SCRSLTFD/INC
          INC       SCRPSTFD/INC
          INC       SCRFLDFD/INC
          INC       SCRMASFD/INC
          INC       SCRPRGFD/INC
.=========================
.Local Variable Definition
.-------------------------
SAVKEY10  DIM       10
PROGNAME  DIM       8
RECTYPE   FORM      1
EXTENS    INIT      "/dcs"
DCS       INIT      ".dcs"
FILE      DIM       12
COMPRESS  INIT      "gzip "
TRANFILE  FILE      ASCII, FIXED=250
.
PRGID     INIT      "makedcsg"
PRGNAM    INIT      "Create dcs Screen Transfer File"
VERSION   INIT      "V12.01.00"
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
ML0000
          CALL      INIT0000
          KEYIN     *EOFF,PROGNAME
          MOVE      ZERO,RECTYPE
.
. Record Type 1 - Program Masterfile
. 
          PACK      FILE,PROGNAME,EXTENS
          SQUEEZE   FILE
          DISPLAY   *P1:1,*HON,*ES,SP20,SP20,SP20,SP20
          DISPLAY   *P1:1,*HON,PRGID,"/",VERSION,SP2,"Creating ",*+,FILE,".gz"
          PREP      TRANFILE,FILE
.
          MOVE      PROGNAME,KEY8
          CALL      RDSCPR1
          BRANCH    OVRCD,ML9000
.
          CALL      MPRG0000   * Add Details for Initial Program
.
. Check for any Linked Screens
. 
          PACK      KEY10,PROGNAME,SP20
          CALL      RSSCMA1
ML1000    CALL      RKSCMA1
          BRANCH    OVRCD,ML1100
          MATCH     SCMAPID,PROGNAME
          GOTO      ML1100 IF NOT EQUAL
          MATCH     SP20,SCMAFPR
          GOTO      ML1000 IF EQUAL
          MATCH     PROGNAME,SCMAFPR
          GOTO      ML1000 IF EQUAL
.
          PACK      SAVKEY10,SCMAPID,SCMASID
          MOVE      SCMAFPR,KEY8
          CALL      RDSCPR1
          BRANCH    OVRCD,ML1000
.
          DISPLAY   *P50:1,*HON," Adding ",SCPRPID
          CALL      MPRG0000   * Add Details for Initial Program
.         
          MOVE      SAVKEY10,KEY10
          CALL      RDSCMA1
          GOTO      ML1000
.
ML1100    CLOSE     TRANFILE
          PACK      KEY13,PROGNAME,DCS
          SQUEEZE   KEY13
          PACK      KEY50,COMPRESS,KEY13
          EXECUTE   KEY50,KEY8
ML9000
          SHUTDOWN  "" 
.
. Record Type 3 - Field Masterfile 
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  
          OPEN      SCRSBGA1,"scrsbgaf"
          OPEN      SCRPSTA1,"scrpstaf"
          OPEN      SCRPSTA2,"scrpstaf"
          OPEN      SCRSLTA1,"scrsltaf"
          OPEN      SCRFLDA1,"scrfldaf"
          OPEN      SCRMASA1,"scrmasaf"
          OPEN      SCRPRGA1,"scrprgaf"
INIT9999  
          RETURN
.
          INC       SCRSBGIO/INC
          INC       SCRSLTIO/INC
          INC       SCRPSTIO/INC
          INC       SCRFLDIO/INC
          INC       SCRMASIO/INC
          INC       SCRPRGIO/INC
          INC       IBAOVRIO/INC
.
HITENTER  KEYIN     ANS
          RETURN
.
RDTRFL1   READ      TRANFILE,SEQ;SCFLPID,SCFLSID,SCFLFNO,SCFLDES:
                                  SCFLTYP,SCFLFLD,SCFLCAL,SCFLWPF,SCFLFSC:
                                  SCFLDEC,SCFLMIN,SCFLMAX,SCFLFLN,SCFLFTY:
                                  SCFLKTY,SCFLMAN,SCFLFEX,SCFLDAF,SCFLMUL:
                                  SCFLKEY,SCFLMTY,SCFLSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRFL1   WRITE     TRANFILE,SEQ;SCFLPID,SCFLSID,SCFLFNO,SCFLDES:
                                  SCFLTYP,SCFLFLD,SCFLCAL,SCFLWPF,SCFLFSC:
                                  SCFLDEC,SCFLMIN,SCFLMAX,SCFLFLN,SCFLFTY:
                                  SCFLKTY,SCFLMAN,SCFLFEX,SCFLDAF,SCFLMUL:
                                  SCFLKEY,SCFLMTY,SCFLSPA
          RETURN
.
RDTRMA1   READ      TRANFILE,SEQ;SCMAPID,SCMASID,SCMADES,SCMATOP:
                                 SCMALEF,SCMABOT,SCMARIG,SCMACBF,SCMACAF:
                                 SCMATYP,SCMASEX,SCMAFSC,SCMACLR,SCMAFPR,SCMASPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRMA1   WRITE     TRANFILE,SEQ;SCMAPID,SCMASID,SCMADES,SCMATOP:
                                 SCMALEF,SCMABOT,SCMARIG,SCMACBF,SCMACAF:
                                 SCMATYP,SCMASEX,SCMAFSC,SCMACLR,SCMAFPR,SCMASPA
          RETURN
.
RDTRPR1   READ      TRANFILE,SEQ;SCPRPID,SCPRNAM,SCPRNAT,SCPRCBF:
                                 SCPRCAF,SCPRISC,SCPRSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRPR1   WRITE     TRANFILE,SEQ;SCPRPID,SCPRNAM,SCPRNAT,SCPRCBF:
                                 SCPRCAF,SCPRISC,SCPRSPA
          RETURN
.
RDTRPS1   READ      TRANFILE,SEQ;SCPSPID,SCPSSID,SCPSROW,SCPSCOL:
                                 SCPSITM,SCPSLEN,SCPSMAN,SCPSATT,SCPSNOR:
                                 SCPSMTY,SCPSFMT,SCPSSPA
          GOTO      OVERCOND IF OVER
          MATCH     "A",SCPSATT
          IF        @EQUAL
            CMOVE     0x0A,SCPSATT
          ENDIF
          RETURN
.
WRTRPS1   
          CMATCH    0x0A,SCPSATT
          IF        @EQUAL
            CMOVE     "A",SCPSATT
          ENDIF
          WRITE     TRANFILE,SEQ;SCPSPID,SCPSSID,SCPSROW,SCPSCOL:
                                 SCPSITM,SCPSLEN,SCPSMAN,SCPSATT,SCPSNOR:
                                 SCPSMTY,SCPSFMT,SCPSSPA
          RETURN
.
RDTRSB1   READ      TRANFILE,SEQ;SCSBPID,SCSBSID,SCSBLIN,SCSBDAT:
                                 SCSBATT,SCSBSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRSB1   WRITE     TRANFILE,SEQ;SCSBPID,SCSBSID,SCSBLIN,SCSBDAT:
                                 SCSBATT,SCSBSPA
          RETURN
.
RDTRSL1   READ      TRANFILE,SEQ;SCSLPID,SCSLSID,SCSLSEL,SCSLITM:
                                 SCSLROW,SCSLCOL,SCSLDES,SCSLSEC,SCSLSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRSL1   WRITE     TRANFILE,SEQ;SCSLPID,SCSLSID,SCSLSEL,SCSLITM:
                                 SCSLROW,SCSLCOL,SCSLDES,SCSLSEC,SCSLSPA
          RETURN
.
RDTRTR1   READ      TRANFILE,SEQ;RECTYPE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRTR1   WRITE     TRANFILE,SEQ;RECTYPE
          RETURN
.
.----------------------------------------------------------------------
. Transfer Program identified by the scrprgaf record being read
.----------------------------------------------------------------------
. Record Type 1 - Program Masterfile
.------------------------------------
MPRG0000  MOVE      ONE,RECTYPE
          CALL      WRTRTR1
          CALL      WRTRPR1
.
. Record Type 2 - Screen Masterfile 
. 
          MOVE      TWO,RECTYPE
          PACK      KEY10,SCPRPID,SP20
          CALL      RSSCMA1
MPRG1000  CALL      RKSCMA1
          BRANCH    OVRCD,MPRG1100
          MATCH     SCMAPID,SCPRPID
          GOTO      MPRG1100 IF NOT EQUAL
          CALL      WRTRTR1
          CALL      WRTRMA1
          GOTO      MPRG1000
.
. Record Type 3 - Field Masterfile 
. 
MPRG1100  MOVE      THREE,RECTYPE
          PACK      KEY15,SCPRPID,SP20
          CALL      RSSCFL1
MPRG2000  CALL      RKSCFL1
          BRANCH    OVRCD,MPRG2100
          MATCH     SCFLPID,SCPRPID
          GOTO      MPRG2100 IF NOT EQUAL
          CALL      WRTRTR1
          CALL      WRTRFL1
          GOTO      MPRG2000
.
. Record Type 4 - Screen Background
. 
MPRG2100  MOVE      FOUR,RECTYPE
          PACK      KEY12,SCPRPID,SP20
          CALL      RSSCSB1
MPRG3000  CALL      RKSCSB1
          BRANCH    OVRCD,MPRG3100
          MATCH     SCSBPID,SCPRPID
          GOTO      MPRG3100 IF NOT EQUAL
          CALL      WRTRTR1
          CALL      WRTRSB1
          GOTO      MPRG3000
.
. Record Type 5 - Screen Positions
. 
MPRG3100  MOVE      FIVE,RECTYPE
          PACK      KEY15,SCPRPID,SP20
          CALL      RSSCPS1
MPRG4000  CALL      RKSCPS1
          BRANCH    OVRCD,MPRG4100
          MATCH     SCPSPID,SCPRPID
          GOTO      MPRG4100 IF NOT EQUAL
          CALL      WRTRTR1
          CALL      WRTRPS1
          GOTO      MPRG4000
.
. Record Type 6 - Screen Selections
. 
MPRG4100  MOVE      SIX,RECTYPE
          PACK      KEY13,SCPRPID,SP20
          CALL      RSSCSL1
MPRG5000  CALL      RKSCSL1
          BRANCH    OVRCD,MPRG5100
          MATCH     SCSLPID,SCPRPID
          GOTO      MPRG5100 IF NOT EQUAL
          CALL      WRTRTR1
          CALL      WRTRSL1
          GOTO      MPRG5000
.
MPRG5100   
MPRG9999  RETURN

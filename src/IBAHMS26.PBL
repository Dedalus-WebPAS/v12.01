.******************************************************************************
.* System         : Patient Management      Release 5                         *
.* Program        : IBAHMS26.PBL                                              *
.* Name           : Patient Statistics Report                                 *
.******************************************************************************
.* Date           : 21/05/1992                                                *
.* Author         : Steve O'Gorman  (Little Stinky)                           *
.* Function       : List the number of days patients have been in a given set *
.*                  of step down ranges for each admission type and health fnd*
.*                  Also num. of admiss, disc, trans, sep. bed days & ALOS    *
.* Modifications  :                                                           *
.*        V12.00.01 15.05.2025  DA Sarkies     TSK 0955096                    *
.*                  Updated for Alpha-Numeric Visit Number                    *
.*        V10.10.01 04/07/2017  Ania P        TSK 0841458                     *
.*                  Urgent rollback of PORT change                            *
.*                  04/07/2017  Ania P        TSK 0841584                     *
.*                  Urgent rollback of PORT change                            *
.*        V10.09.01 20/02/2017  Ania P        CAR 261630                      *
.*                  Removed use of PORT                                       *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.03.00  30/07/2004  Lina Vo        CAR 52049                      *
.*                  Ported from R6.12.                                        *
.*        V6.06.02  14/01/2000  Caleb Sharman                                 *
.*                  Mods in CDAY0000 to ignore record if start date is blank  *
.*        V6.06.01  16/09/1999  Steve Downing                                 *
.*                  Mods to use DAYSEP                                        *
.*        V6.06.B01 04/03/1999  Steve Armstrong                               *
.*                  Fixed EXTA3000 to use full date when incrementing by 1 day*
.*                  Also changed STRTYR from FROM2 to FORM4                   *
.******************************************************************************
.*        V6.05.02  08/12/1998 Jill Habasque                                  *
.*                  Removed CCENTRY                                           *
.*        V6.05.01  17/11/1998  Nicole Harrington                             *
.*                  Changed to use century in call to DMYCON and associated   *
.*                  compares                                                  *
.******************************************************************************
.*                  V5.01  21/05/1992  Steve O'Gorman                         *
.*                         Converted from IBAPMS29                            *
.*                  V5.02  17/07/92    ROD                                    *
.*                         Fixed totals and Sep. Bed day calc.    SRF 114443  *
.*                  V5.03  21/08/92    ROD                                    *
.*                         Fixed Sep. Bed day for discharged pats             *
.*        V6.00.01  25/11/1992    Graeme Williams                             *
.*                  Use KEY14 for health fund table, not KEY10                *
.*        V6.03.01  11/10/1995  Greg Horvat      SRF 611811                   *
.*                  Changed to use CALCDAYS instead of CALC when calculating  *
.*                  the difference between two dates                          *
.*        V6.03.02  27/10/1995  Greg Horvat      SRF 612064 612205 612206     *
.*                  Recompiled for changes to NHOSPDAY                        *
.*                                                                            *
.******************************************************************************
.
.$$$$$
.         IBAHMS26.PBL        Patient Statistics Report
.         ============        =========================
.         OPTN0000 - Whether to print report or not
.         DATE0000 - Enter the date range for the report
.         EXTA0000 - Extract admissions in the date range from PATDAYFD
.         EXTB0000 - Extract all data about patients from PATTRNFD
.         REPT0000 - Produce the report
.
.$$$$$
.
.         FD's required
.
          INC       STD001FD                * standard variables
          INC       PATCODFD/INC            * codes file
          INC       PATCONFD/INC            * control file
          INC       PATDAYFD/INC            * day file
          INC       PATDSCFD/INC            * discharge file
          INC       PATDTRFD/INC            * patient debtors transaction file
          INC       PATFN1FD/INC            * health fund file
          INC       PATMI1FD/INC            * admission file
          INC       PMSVX1FD/INC            * admission file
          INC       PATTRNFD/INC            * transfer file
.
.         program variables
.
ACTTODAT  DIM       8
CALCDATE  DIM       8         * end date to calculate days for
CALDAYS   FORM      4
CCYY      DIM       4         * century and year
CDFRDATE  DIM       8         * starting date : ccyymmdd
CDTODATE  DIM       8         * ending   date : ccyymmdd
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       50        * command line for execute command
CURRDATE  DIM       8         * current date
CURRTYPE  DIM       3         * current admission type
CNDMX1    FORM      6
CNDMX2    FORM      6
CNDMX3    FORM      6
CNDMX4    FORM      6
CNDMX5    FORM      6
.
DAYCASE   FORM      1         * 1 = a day case patient
DAYSPERD  FORM      6         * number of days for type in period
DAYSTYPE  FORM      6         * number of days for type
DCENT     DIM       2
DIM4      DIM       4         * date as mmdd
DIM8      DIM       8         * date as ccyymmdd
DIFFDAY   FORM      6         * difference in total days and days for period
DXFRDATE  DIM       10        * starting date : dd/mm/ccyy
DXTODATE  DIM       10        * ending   date : dd/mm/ccyy
DYEAR     DIM       2
.
ENDYR     FORM      4
.
FORM6     FORM      6         * working variable for days b/w two dates
FORM8     FORM      8         * form version of admission number
FRDATE8   DIM       8         * from date as dim 8
FROMDATE  DIM       8
.
JYEAR     FORM      2
.
MMDD      DIM       4         * month and day
.
NBRDAYS   FORM      4
.
PRNTFUND  FORM      1         * = 1 means fund printed
PERDDATE  DIM       8         * starting date of the period ccyymmdd
RECFLG    FORM      1
DISCH FORM "1"
SAVFUND   DIM       6
SAVCODE   DIM       3
SAVATYPE  DIM       3         * Save variable for admission type
SAVTMOVE  DIM       1
SEPTYPE   DIM       3
STRTDAY   FORM      3         * julian day of start date
STRTYR    FORM      4         * julian year of start date
STRTLEAP  FORM      1         * if a leap year for start date
OYEAR     FORM      "365"
.
TODATE    DIM       8
TODATE8   DIM       8         * to date as dim 8
TYPEDATE  DIM       8         * starting date of current admission type yymmdd
TYPETOTL  FORM      6         * the total for a type
.
WDATE1    DIM       8
WDATE2    DIM       8
.
.         temp file definitions
.
TEMPA     IFILE     SQL, FIXED=9, KEY="U1-8"
.
.Name     Type      Length  Physical Start  Name
.-------  ----      ------  -------- -----  -----------------------------------
XADMISS   DIM       8            8       1  Admission Number
. End of Record                          9
.
TEMPB     IFILE     SQL, FIXED=54, KEY="U1-6,7-9"
.
.Name     Type      Length  Physical Start  Name
.-------  ----      ------  -------- -----  -----------------------------------
XHFUND    DIM       6            6       1  Health fund code 
.                                             "zzzzzz" for Grand Totals
XCODE     DIM       3            3       7  Admission type
.                                             "zzz" for Fund totals
XDAYCAS   FORM      6            4      10  Number of day cases
XRANGE1   FORM      6            4      14  Number of days in range 1
XRANGE2   FORM      6            4      18  Number of days in range 2
XRANGE3   FORM      6            4      22  Number of days in range 3
XRANGE4   FORM      6            4      26  Number of days in range 4
XRANGE5   FORM      6            4      30  Number of days in range 5
XRANGE6   FORM      6            4      34  Number of days greater than range 5
. -----   Extra fields added for IBAHMS26  -----
XADMS     FORM      6            4      38  Number of Admissions
XDISC     FORM      6            4      42  Number of Discharges
XSBDY     FORM      6            4      46  Number of Separation Bed Days
XTOUT     FORM      6            4      50  Number of Transfers Out
. End of Record                         54  
.
SXDAYCAS  FORM      6            4      10  Number of day cases
SXRANGE1  FORM      6            4      14  Number of days in range 1
SXRANGE2  FORM      6            4      18  Number of days in range 2
SXRANGE3  FORM      6            4      22  Number of days in range 3
SXRANGE4  FORM      6            4      26  Number of days in range 4
SXRANGE5  FORM      6            4      30  Number of days in range 5
SXRANGE6  FORM      6            4      34  Number of days greater than range 5
. -----   Extra fields added for IBAHMS26  -----
SXADMS    FORM      6            4      38  Number of Admissions
SXDISC    FORM      6            4      42  Number of Discharges
SXSBDY    FORM      6            4      46  Number of Separation Bed Days
SXTOUT    FORM      6            4      50  Number of Transfers Out
.
.         program constants
.
CATA      INIT      "A "      * admission type
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
.
KEYA      INIT      " 9 U1-8"
KEYB      INIT      " 54 U1-6,7-9"
.
MISSING   INIT      "Missing Code : "
PRFIXA    INIT      "hms26a"
PRFIXB    INIT      "hms26b"
.
XALOS      FORM     4.1                     * Average Length of Stay
.
ZED3      INIT      "zzz"
ZED6      INIT      "zzzzzz"
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
ZERO8     INIT      "0000    "
.
PRGID     INIT      "IBAHMS26"
PRGNAM    INIT      "PATIENT STATISTICS REPORT"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000                * initialization
.
ML1000    CALL      OPTN0000                * Print report ??
          BRANCH    EXIT,ML9999             * exit
.
ML2000    CALL      DATE0000                * enter date range
          BRANCH    EXIT,ML1000             * nothing entered
.
ML3000    CALL      EXTA0000                * extract all the patients to temp A
          CALL      EXTB0000                * extract data to temp file B
.
ML4000    CALL      REPT0000                * produce the report
.
          CALL      DTPA0000                * delete temp file A
ML5000    CALL      DTPB0000                * delete temp file B
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD                * display program details
          CALL      IBACLOCK
          DISPLAY   *P50:24,"Opening ",*EL;
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"     * admission file
          OPEN      PMSVX1A1,"pmsvx1af"     * admission file
          DISPLAY   *P58:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"     * discharge file
          DISPLAY   *P58:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"     * transfer file
          DISPLAY   *P58:24,"patfn1af"
          OPEN      PATFN1A1,"patfn1af"     * health fund file
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"     * codes file
.
          MOVE      "1",CCANLDTE            * can cancel default
          MOVE      "1",CDEFDTE             * enter once to accept default
          MOVE      "0",CHIGHLT             * use highlighting
          MOVE      "1",CNEWDS              * new ? option
          MOVE      "0",CNOUNDLN            * print underlines at End Of Page
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        Print report or not                                        *
.*        Returns : EXIT = 1      exit selected                      *
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*P1:5,"1",*HOFF:
                    *P2:4," = Exit":
                    *P2:5," = Print Report":
                    *P1:7,"Select Option : "
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          BRANCH    MOPTION,OPTN8000
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9000 IF EQUAL       * exit
.
          BEEP
          GOTO      OPTN1000
.
OPTN8000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  DATE0000                    Called by : ML2000   *
.*        Enter the date ranges                                      *
.*        Returns : CDFRDATE      from date                          *
.*                : CDTODATE      to   date                          *
.*********************************************************************
DATE0000  DISPLAY   *P1:8,*EF:
                    *P1:9,"Starting Date : ":
                    *P1:10,"Ending   Date : "
.
DATE1000  UNPACK    SP20,CDFRDATE,CDTODATE  * clear codes
          UNPACK    SP20,DXFRDATE,DXTODATE  * clear descriptions
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE           * get current date
.
          UNPACK    SP6,CMON,CDAY,CYEAR
          MOVE      CCC,CCENT               * have current century
.
          DISPLAY   *P17:9,*EL,*P17:10,*EL
.
.         enter the from date
.
          MOVE      "9",CVERT               * row
          MOVE      "17",CCOL               * column
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE1000         * ? not allowed
          BRANCH    OVRCD,DATE9100          * exit
.
          PACK      CDFRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDFRDATE
          UNPACK    CDFRDATE,FRDATE8
.
          CALL      PACDATE
          MOVE      CPCDATE,DXFRDATE        * formatted date
.
          MATCH     CURRDATE,CDFRDATE
          GOTO      DATE2000 IF LESS        * entered < current -> OK
.
          DISPLAY   *P1:24,*B,"Date must be in the past.  ",*EL;
          CALL      HITENTER 
          GOTO      DATE1000
.
.         enter the to date
.
DATE2000  MOVE      "10",CVERT              * row
          MOVE      "17",CCOL               * column
          UNPACK    CDFRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE2000         * ? not allowed
          BRANCH    OVRCD,DATE1000          * re-enter from date
.
          PACK      CDTODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDFRDATE
          UNPACK    CDTODATE,TODATE8
          MOVE      TODATE8,ACTTODAT
.
          CALL      PACDATE
          MOVE      CPCDATE,DXTODATE        * formatted date
.
          MATCH     CURRDATE,CDTODATE
          GOTO      DATE3000 IF LESS        * entered < current -> OK
.
          DISPLAY   *P1:24,*B,"Date must be in the past.  ",*EL;
          CALL      HITENTER 
          GOTO      DATE2000
.
DATE3000  MATCH     CDFRDATE,CDTODATE
          GOTO      DATE3100 IF NOT LESS    * ending >= starting -> OK
.
          DISPLAY   *P1:24,*B,"Ending date must be after Start date.  ",*EL;
          CALL      HITENTER 
          GOTO      DATE1000
.
.         Add one to the TODATE. This is because initially the program did not
.         include the end date, so it looks at all the data upto but including
.         the TODATE
.
DATE31000 UNPACK    TODATE8,XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,DD
          MOVE      XMON,MM
          MOVE      XYEAR,YY
          MOVE      XCENT,CC
          CALL      DMYCON
.
          ADD       ONE,JULDAY
          MOVE      JULCC,JWKCC
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          CALL      JULCON
.
          PACK      TODATE8,CC,YY,MM,DD
          REP       " 0",TODATE8
.
.         OK to Continue (Y/N/C) ?
.
DATE4000  CALL      CONTQST
          BRANCH    CEXIT,DATE9000,DATE0000,DATE9100
.                         Yes      No       Cancel
.
DATE9000  MOVE      ZERO,EXIT               * continue
          GOTO      DATE9999
.
DATE9100  MOVE      ONE,EXIT                * exit selected
.
DATE9999  RETURN
+
.*********************************************************************
.*                  EXTA0000                    Called by : ML3000   *
.*        Loop over day file for the entered date and write the      *
.*        Admission numbers to temp file A                           *
.*********************************************************************
EXTA0000  CALL      CTPA0000                * create temp file
          UNPACK    CDFRDATE,CCYY,MMDD      * get ccyy and mmdd of start date
.
.         open the current file
.
EXTA0500  PACK      CFNAME,FILDAYA1,CCYY    * open starting file
          REP       " 0",CFNAME
          DISPLAY   *P58:24,CFNAME,*EL;
.
          CLOSE     PATDAYA1                * close old file
          MOVE      ZERO,OVRCD              * clear flag
          TRAP      OVERCOND IF IO          * call overcond if no file
          OPEN      PATDAYA1,CFNAME         * starting file
          TRAPCLR   IO                      * clear the trap
          BRANCH    OVRCD,EXTA3000          * get next years file
.
          DISPLAY   *P1:24,*EL,"Extracting Admission No : ";
          PACK      KEY12,MMDD,SP8
.
          CALL      RSPTDAY1                * positional read
.
EXTA1000  CALL      RKPTDAY1                * next read
          BRANCH    OVRCD,EXTA3000          * end of current file
.
          UNPACK    PTDYDATE,XMON,XDAY
          DISPLAY   *P27:24,XADMISS,*P55:24,XDAY,SLASH,XMON,*EL;
.
          UNPACK    CDTODATE,KEY4,DIM4      * get mmdd into DIM4
.
          MATCH     KEY4,CCYY               * check the from year and to year ?
          GOTO      EXTA2000 IF LESS
          GOTO      EXTA3000 IF NOT EQUAL
.
          MATCH     PTDYDATE,DIM4
          GOTO      EXTA9999 IF LESS        * after end date
.
EXTA2000  MOVE      PTDYADMN,KEY8           * admission number is key
          CALL      RATEMPA1
          COMPARE   ZERO,OVRCD
          GOTO      EXTA1000 IF EQUAL       * already exists
.
          MOVE      PTDYADMN,XADMISS        * set up admission number
          MOVE      XADMISS,KEY8
          DISPLAY   *P27:24,*V2LON,XADMISS,*EL;
          CALL      WRTEMPA1
          GOTO      EXTA1000
.
. ------- increment year by one and get open next day file -------
.
EXTA3000  CALL      IBACLOCK                * get current century and year
          PACK      KEY4,CCC,CYY            * get current century and year
          REP       " 0",KEY4
.
          MOVE      CCYY,FORM4              * convert year to form field
          ADD       ONE,FORM4               * increment by 1 year
          MOVE      FORM4,CCYY              * convert year back to dim field
          REP       " 0",CCYY
.
          MATCH     CCYY,KEY4
          GOTO      EXTA9999 IF LESS        * will be opening into the future
.
          MOVE      SP4,MMDD                * set to start of file
          GOTO      EXTA0500                * try and open it
.
EXTA9999  RETURN
+
.*********************************************************************
.*                  EXTB0000                    Called by : ML3000   *
.*        Loop over temp file A and extract valid data to temp fileB *
.*********************************************************************
EXTB0000  CALL      CTPB0000                * create temp file B
.
. -------------------------------------
. ------- loop over temp file A -------
. -------------------------------------
.
          DISPLAY   *P1:24,"Processing Admission No : ":
                    *P40:24,"Admission type : ",*EL;
          MOVE      SP8,KEY8                * clear key
          CALL      RSTEMPA1                * positional read
.
EXTB1000  CALL      RKTEMPA1                * next read
          BRANCH    OVRCD,EXTB9999          * finished temp file
        
.
          DISPLAY   *P27:24,XADMISS         * read in admission number
.
          MOVE      XADMISS,KEY8            * admission number
          CALL      RDMISS1                 * read admission file
          BRANCH    OVRCD,EXTB1000          * invalid admission number
.
          REP       " 0",ADATE              * zero fill admission date
          MATCH     TODATE8,ADATE
          GOTO      EXTB1000 IF NOT LESS    * admitted after period
.
          MOVE      SP8,DDATE               * clear discharge date
          CALL      RDDSCH1                 * read discharge file
          BRANCH    OVRCD,EXTB1100          * no discharge record
.
          MATCH     FRDATE8,DDATE
          GOTO      EXTB1000 IF LESS        * discharged before period
.
EXTB1100  DISPLAY   *P27:24,*V2LON,XADMISS  * valid admission number
.
.         set up for transfer file
.
          MOVE      ZERO,DAYCASE            * set to not a daycase
          MOVE      ZERO,DAYSPERD           * clear period total
          MOVE      ZERO,DAYSTYPE           * clear type days
          MOVE      SP1,CURRTYPE            * clear current admission type
          MOVE      SP10,TYPEDATE           * clear starting type date
          MOVE      SP10,PERDDATE           * clear starting for the period
          MOVE      SP8,CALCDATE            * clear ending type date
          MOVE      ZERO,RECFLG
          UNPACK    XADMISS,KEY8            * get as a dim8
.
          PROC      TRANS000                * Process this transfer record
.
          PACK      KEY30,KEY8,SP30         * start at visit number
          CALL      RDSTRAN2                * positional read
.
. -------------------------------------------------------
. ------- loop over PATTRNFD for the visit number -------
. -------------------------------------------------------
.
EXTB2000  CALL      RDKTRAN2                * next read
          BRANCH    OVRCD,EXTB3600          * finished for visit number
.
          MOVE      XADMISS,DIM8VISN        * get as a form
          MATCH     DIM8VISN,TADMN
          GOTO      EXTB3600 IF NOT EQUAL   * different visit
.
          DISPLAY   *P57:24,*V2LON,TATYPE   * display admission type
.
.         If this is an admission or return record, then just save the details
.
          MOVE      TMOVE,STMOVE
          MATCH     "A",TMOVE
          GOTO      EXTB2200 IF EQUAL       * admission record
.
          MATCH     "R",TMOVE
          GOTO      EXTB2200 IF EQUAL       * return record
.
          MATCH     "C",TMOVE
          GOTO      EXTB2100 IF EQUAL
.
          MATCH     "L",TMOVE
          GOTO      EXTB2100 IF EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      EXTB2010 IF EQUAL       * day case patient
.
          MATCH     FRDATE8,TDATE
          GOTO      EXTB1000 IF LESS
.
          MATCH     TODATE8,TDATE
          GOTO      EXTB3600 IF LESS
          MOVE      TODATE8,TDATE
          GOTO      EXTB3600
.
.         Day case
.
EXTB2010  MOVE      ONE,DAYSTYPE
          MOVE      ONE,DAYSPERD
          MOVE      ONE,DAYCASE
          MOVE      TATYPE,CURRTYPE
          CALL      POST1000
          GOTO      EXTB1000
.
.         If the admission type has not changed, then ignore the record
.
EXTB2100  CMATCH    "L",TMOVE 
          GOTO      EXTB2105 IF NOT EQUAL       * must be change record
.
          MATCH     TODATE8,TDATE
          GOTO      EXTB2109 IF NOT LESS
.
          MOVE      TDATE,CALCDATE
          CALL      BDAY0000
          MOVE      ONE,RECFLG
          GOTO      EXTB2000
.
EXTB2105  MATCH     CURRTYPE,TATYPE
          GOTO      EXTB2000 IF EQUAL       * same admission type
.
.         A change record
.
EXTB2108  MOVE      ZERO,RECFLG
          MATCH     TODATE8,TDATE
          GOTO      EXTB2110 IF LESS
.
EXTB2109  MOVE      TODATE8,CALCDATE
          CALL      POST0000
          GOTO      EXTB1000
.
EXTB2110  MOVE      TDATE,CALCDATE
          CALL      POST0000
          MOVE      TATYPE,CURRTYPE         * set current type
.
.         Start of period - Save the admission type
.
EXTB2200  MATCH     TODATE8,TDATE
          GOTO      EXTB3700 IF NOT LESS
.
.         Is this starting date less than the start date of the period ?
.
          MOVE      TDATE,TYPEDATE          * Start date for last adm.type chg.
          MATCH     FRDATE8,TDATE
          GOTO      EXTB2210 IF NOT LESS
          MOVE      FRDATE8,PERDDATE        * Start date for the period
          MATCH     SP8,PERDDATE
          IF        @EQUAL
            DISPLAY   *P1:24,DAADMNO,SP10
            CALL      HITENTER
          ENDIF
          GOTO      EXTB2220
.
EXTB2210  MOVE      TDATE,PERDDATE
.
EXTB2220  MATCH     "R",TMOVE
          GOTO      EXTB2225 IF NOT EQUAL
.
          MATCH     CURRTYPE,TATYPE
          GOTO      EXTB2000 IF EQUAL
.
          COMPARE   ONE,RECFLG
          CALL      POST1000 IF EQUAL
          MOVE      ZERO,RECFLG
.
EXTB2225  MOVE      TATYPE,CURRTYPE         * set current type
.
          MOVE      ZERO,DAYSTYPE           * init number of days of this type
          MOVE      ZERO,DAYSPERD           * init days in period
          GOTO      EXTB2000
.
.         End of this admissions records in transfer file. No discharge record
.
EXTB3600  MATCH     "L",STMOVE
          GOTO      EXTB3700 IF EQUAL
.
          MATCH     SP1,STMOVE
          GOTO      EXTB3700 IF EQUAL
.
          MATCH     "D",STMOVE
          GOTO      EXTB3610 IF NOT EQUAL
.
          MOVE      TDATE,CALCDATE
          GOTO      EXTB3620
.
EXTB3610  MOVE      TODATE8,CALCDATE
EXTB3620  CALL      POST0000
          GOTO      EXTB1000
.
EXTB3700  COMPARE   ONE,RECFLG
          CALL      POST1000 IF EQUAL
          MOVE      ZERO,RECFLG
          GOTO      EXTB1000
.
EXTB9999  RETURN
.
+
.*********************************************************************
.*                  REPT0000                    Called by : ML4000   *
.*        Produce the report                                         *
.*********************************************************************
REPT0000  DISPLAY   *P1:24,"Producing the report now - ",*V2LON,*BLINKON:
                    "Please wait",*EL;
          MOVE      ZERO,CPAGENO            * clear page counter
          CALL      HEAD0000                * display header
          MOVE      ZERO,PRNTFUND           * fund not printed
.
          MOVE      SP9,KEY9                * start of file
          CALL      RDTEMPB1                * exact read
          COMPARE   ZERO,OVRCD
          GOTO      REPT1100 IF EQUAL       * valid read
.
. -----------------------------------
. ------- loop over temp file -------
. -----------------------------------
.
REPT1000  CALL      RKTEMPB1                * next read
          BRANCH    OVRCD,REPT9999          * end of file
.
REPT1100  MATCH     ZED6,XHFUND        
          GOTO      REPT5000 IF EQUAL       * print grand totals
.
          MATCH     ZED3,XCODE         
          GOTO      REPT4000 IF EQUAL       * print health fund totals
.
. ------- print for health fund and banding code -------
.
REPT3000  COMPARE   "55",CLNO
          GOTO      REPT3100 IF LESS        * will fit on page
.
          CALL      HEAD0000                * print new page
.  
REPT3100  BRANCH    PRNTFUND,REPT3300       * fund has been printed
          MOVE      "No Health Fund",HFNAME
          MATCH     SP6,XHFUND
          GOTO      REPT3200 IF EQUAL
          MOVE      "Invalid Health Fund",HFNAME
          PACK      KEY14,XHFUND,ZERO8
          CALL      RDFUND1
.
REPT3200  PRINT     *1,"| ",HFNAME,"|",*83,"|",*92,"|",*100,"|":
                    *108,"|",*116,"|",*124,"|",*132,"|"
          ADD       ONE,CLNO
          MOVE      ONE,PRNTFUND            * signal fund printed
.
REPT3300  MOVE      "No Admission Type",TDESC 
          MATCH     SP3,XCODE
          GOTO      REPT3400 IF EQUAL       * no admission type
.
          PACK      TDESC,MISSING,XCODE,SP20
          PACK      KEY5,CATA,XCODE         * get type desc
          CALL      RDCODE1
.
REPT3400  MOVE      XDAYCAS,TYPETOTL
          ADD       XRANGE1,TYPETOTL
          ADD       XRANGE2,TYPETOTL
          ADD       XRANGE3,TYPETOTL
          ADD       XRANGE4,TYPETOTL
          ADD       XRANGE5,TYPETOTL
          ADD       XRANGE6,TYPETOTL         * the total
.
.                                            *     Average Length of Stay =
.                                            *
          MOVE      XTOUT,FORM6              *       Seperation Bed Days
          ADD       XDISC,FORM6              * --------------------------------
          MOVE      ZERO,XALOS
          COMPARE   ZERO,FORM6
          GOTO      REPT3700 IF EQUAL
          MOVE      XSBDY,XALOS              *     Average Length of Stay =
          DIV       FORM6,XALOS              * (No. Discharges + Transfers Out)
.
REPT3700  PRINT     *1,"|",*8,TDESC,*33,"|",XDAYCAS,SP1,XRANGE1,SP1,XRANGE2:
                    SP1,XRANGE3,SP1,XRANGE4,SP1,XRANGE5,SP1,XRANGE6:
                    " | ",TYPETOTL:
                    *92,"|",XADMS:
                    *100,"|",XDISC:
                    *108,"|",XSBDY:
                    *116,"|",XTOUT:
                    *124,"|",XALOS:
                    *132,"|"
.
          ADD       ONE,CLNO
          GOTO      REPT1000
.
. ------- print the total for a health fund -------
.
REPT4000  COMPARE   "58",CLNO
          GOTO      REPT4100 IF LESS        * dont need a new page
.
          CALL      HEAD0000                * print new page
.
REPT4100  MOVE      XDAYCAS,TYPETOTL
          ADD       XRANGE1,TYPETOTL
          ADD       XRANGE2,TYPETOTL
          ADD       XRANGE3,TYPETOTL
          ADD       XRANGE4,TYPETOTL
          ADD       XRANGE5,TYPETOTL
          ADD       XRANGE6,TYPETOTL         * the total
.
. calc average length of stay
.
          MOVE      XTOUT,FORM6
          ADD       XDISC,FORM6
          MOVE      ZERO,XALOS
          COMPARE   ZERO,FORM6
          GOTO      REPT4200 IF EQUAL
          MOVE      XSBDY,XALOS
          DIV       FORM6,XALOS
.
REPT4200  PRINT     *1,"*----------------------------------------------":
                       "--------------------------------------":
                       "----------------------------------------------*":
                    *N,"|      Total for Health Fund",*33,"|",XDAYCAS,SP1:
                    XRANGE1,SP1,XRANGE2,SP1,XRANGE3,SP1,XRANGE4,SP1,XRANGE5:
                    SP1,XRANGE6:
                    " | ",TYPETOTL," |",XADMS," |",XDISC," |",XSBDY," |":
                    XTOUT," |",XALOS," |":
                    *N,"*----------------------------------------------":
                       "--------------------------------------":
                       "----------------------------------------------*"
.
          ADD       "3",CLNO
          MOVE      ZERO,PRNTFUND           * signal fund not printed
          GOTO      REPT1000
.
. ------- print the grand totals for the bands -------
.
REPT5000  COMPARE   "55",CLNO
          GOTO      REPT5100 IF LESS        * will fit on page
.
          CALL      HEAD0000                * print new page
.  
REPT5100  BRANCH    PRNTFUND,REPT5200       * heading been printed
.
          PRINT     *1,"| Grand Totals",*33,"|",*83,"|",*92,"|",*132,"|"
          ADD       ONE,CLNO
          MOVE      ONE,PRNTFUND            * signal fund printed
.
REPT5200  MATCH     ZED3,XCODE         
          GOTO      REPT3300 IF NOT EQUAL   * print band totals
.
. ------- print overall grand totals -------
.
          MOVE      XDAYCAS,TYPETOTL
          ADD       XRANGE1,TYPETOTL
          ADD       XRANGE2,TYPETOTL
          ADD       XRANGE3,TYPETOTL
          ADD       XRANGE4,TYPETOTL
          ADD       XRANGE5,TYPETOTL
          ADD       XRANGE6,TYPETOTL         * the total
.
. calc average length of stay
.
          MOVE      XTOUT,FORM6
          ADD       XDISC,FORM6
          MOVE      ZERO,XALOS
          COMPARE   ZERO,FORM6
          GOTO      REPT6000 IF EQUAL
          MOVE      XSBDY,XALOS
          DIV       FORM6,XALOS
.
REPT6000  PRINT     *1,"*----------------------------------------------":
                       "--------------------------------------":
                       "----------------------------------------------*":
                    *N,"|      Grand Totals",*33,"|",XDAYCAS,SP1:
                    XRANGE1,SP1,XRANGE2,SP1,XRANGE3,SP1,XRANGE4,SP1,XRANGE5:
                    SP1,XRANGE6:
                    " | ",TYPETOTL," |",XADMS," |",XDISC," |",XSBDY," |":
                    XTOUT," |",XALOS," |":
                    *N,"*----------------------------------------------":
                       "--------------------------------------":
                       "----------------------------------------------*",*N,*N
.
          COMPARE   "55",CLNO
          GOTO      REPT7000 IF LESS
.
          CALL      HEAD132
.
REPT7000  PRINT     *1,"No. of Adm's = Number of Admissions in date range",*N:
                    *1,"No. of Disc. = Number if Discharges in date range",*N:
                    *1,"Sep. Bd Dys  = No. bed days a patient has had a ":
                    "particular admission type",*N:
                    *1,"Tran. Out    = No. of times there is a change in ":
                    "admission type within the date range",*N:
                    *1,"ALOS         = Separation Bed days / (Discharges + ":
                    "Transfers Out)":
                    *N,*N,"*** End of Report ***",*N
.
REPT9999  RETURN
+
.*********************************************************************
.*                  HEAD0000                    Called by : REPT0000 *
.*        Print a new page heading for the report                    *
.*********************************************************************
.
HEAD0000  CALL      HEAD132                 * report page heading
          READ      CONTROLF,HUND03;*21,PTCNDMX1,PTCNDMX2,PTCNDMX3,PTCNDMX4:
                                    *29,PTCNDMX5
          PRINT     *20,"From ",DXFRDATE," to ",DXTODATE:
                    *N,*N,"*----------------------------------------------":
                       "----------------------------------------------":
                       "--------------------------------------*":
                    *N,"|",*33,"|--------------- Stepdown Periods ":
                    "--------------- |  Total | No of | No of | Sep.  |":
                    " Tran. | ALOS  |":
                    *N,"| Health Fund/Admission Type":
                    *33,"|   Day   1-",PTCNDMX1,SP2;
          ADD       ONE,PTCNDMX1
          PRINT     PTCNDMX1,"-",PTCNDMX2,SP2;
          ADD       ONE,PTCNDMX2
          PRINT     PTCNDMX2,"-",PTCNDMX3,SP2;
          ADD       ONE,PTCNDMX3
          PRINT     PTCNDMX3,"-",PTCNDMX4,SP2;
          ADD       ONE,PTCNDMX4
          PRINT     PTCNDMX4,"-",PTCNDMX5,SP4;
          ADD       ONE,PTCNDMX5
          PRINT     PTCNDMX5,"+",*83,"|   Days | Adm's | Disc. | Bd Dys|":
                    " Out   |       |":
                    *N,"*----------------------------------------------":
                       "--------------------------------------":
                       "----------------------------------------------*"
          MOVE      "10",CLNO               * set line number

HEAD9999  RETURN
+
.*********************************************************************
.*                  BDAY0000                    Called by : POST0000 *
.*        Calculate no of days for the periods and                   *
.*                  no of days since admission date                  *
.*********************************************************************
BDAY0000  MOVE      ZERO,EXIT
          MATCH     TYPEDATE,CALCDATE
          GOTO      BDAY1000 IF LESS
.
          MOVE      TYPEDATE,DIM8            * Starting date
          CALL      CDAY0000                 * Calculate no of days for adm.
          ADD       FORM6,DAYSTYPE           * add to current days value
.
BDAY1000  MATCH     PERDDATE,CALCDATE
          GOTO      BDAY1100 IF LESS
.
          MOVE      PERDDATE,DIM8            * Starting date
          CALL      CDAY0000                 * Calculate no of days for periods
          ADD       FORM6,DAYSPERD           * add to the days in the period
.
BDAY1100  COMPARE   ZERO,DAYSTYPE
          GOTO      BDAY2000 IF NOT EQUAL
.
          COMPARE   ZERO,DAYSPERD
          GOTO      BDAY2000 IF NOT EQUAL
          MOVE      ONE,EXIT
.
BDAY2000  RETURN
.
.*********************************************************************
.*                  CDAY0000                    Called by : BDAY0000 *
.*        Calculate the duration between two dates                   *
.*        Para's  : TYPEDATE      the starting date                  *
.*                  CALCDATE      the ending   date                  *
.*                  DAYSTYPE      the current number of days         *
.*                  DAYSPERD      the number of days in the period   *
.*        Returns : DAYSTYPE      the number of days for adm type    *
.*                  DAYSPERD      the number of days in the period   *
.*********************************************************************
CDAY0000  MOVE      ZERO,FORM6              * clear working days variable
.
          MATCH     DIM8,SP8
          GOTO      CDAY9999 IF EQUAL
.
          DAYSEP    DIM8,CALCDATE,FORM6
.
CDAY9999  RETURN
+
.*********************************************************************
.*                  POST0000                    Called by : EXTB4000 *
.*        Update temp file B with required combination of data       *
.*        Para's  : DAYSTYPE      days for the admission type        *
.*                  DAYSPERD      days for the period                *
.*                  TYPEDATE      starting date for the period       *
.*                  CALCDATE      ending   date for the period       *
.*                  CURRTYPE      current admission type             *
.*********************************************************************
POST0000  CALL      BDAY0000                * calculate number of days
          BRANCH    EXIT,POST9999
.
POST1000 
          MOVE      DAYSTYPE,DIFFDAY        * total days for admission type
          SUB       DAYSPERD,DIFFDAY        * get stepdown factor
          CALL      CADB0000                * Calculate days in ranges
.
POST2000  PACK      KEY9,AFUNDH,CURRTYPE    * fund and type
          CALL      UPDB0000                * update for fund and type total
.
          PACK      KEY9,AFUNDH,ZED3        * fund and zzz
          CALL      UPDB0000                * update for health fund totals
.
          PACK      KEY9,ZED6,CURRTYPE      * zzz and type
          CALL      UPDB0000                * update for type totals
.
          PACK      KEY9,ZED6,ZED3          * zzzzzz and zzz
          CALL      UPDB0000                * update for grand totals

.
POST9999  RETURN
+
.*********************************************************************
.*                  CADB0000                    Called by : POST0000 *
.*        Updates values on temp file B                              *
.*        Para's  : KEY9          health fund and admission type     *
.*                  DIFFDAYS      stepdown factor                    *
.*                  DAYSPERD      days in the period                 *
.*********************************************************************
CADB0000  MOVE      ZERO,SXDAYCAS           * clear Number of day cases
          MOVE      ZERO,SXRANGE1           * clear Number of days in range 1
          MOVE      ZERO,SXRANGE2           * clear Number of days in range 2
          MOVE      ZERO,SXRANGE3           * clear Number of days in range 3
          MOVE      ZERO,SXRANGE4           * clear Number of days in range 4
          MOVE      ZERO,SXRANGE5           * clear Number of days in range 5
          MOVE      ZERO,SXRANGE6           * clear Number of days in range 6
          READ      CONTROLF,HUND03;*21,PTCNDMX1,PTCNDMX2,PTCNDMX3,PTCNDMX4:
                                    *29,PTCNDMX5
.
          BRANCH    DAYCASE,CADB8900        * a day case patient
.
.         Move to FORM3 to include -ve sign if any
.
          MOVE      PTCNDMX1,CNDMX1
          MOVE      PTCNDMX2,CNDMX2
          MOVE      PTCNDMX3,CNDMX3
          MOVE      PTCNDMX4,CNDMX4
          MOVE      PTCNDMX5,CNDMX5
.
          SUB       DIFFDAY,CNDMX1          * new range 1
          SUB       DIFFDAY,CNDMX2          * new range 2
          SUB       DIFFDAY,CNDMX3          * new range 3
          SUB       DIFFDAY,CNDMX4          * new range 4
          SUB       DIFFDAY,CNDMX5          * new range 5
.
.         now work out where to store the data
.         see if days in period is in range 1
.
CADB3000  COMPARE   CNDMX1,ZERO
          GOTO      CADB4000 IF NOT LESS    * range 1 <= 0
.
          COMPARE   DAYSPERD,CNDMX1
          GOTO      CADB3100 IF LESS        * days is after range 1
.
.         days in period is in range 1
.
          ADD       DAYSPERD,SXRANGE1       * add number to range 1 days
          GOTO      CADB9999
.
.         days in period is after range 1
.
CADB3100  ADD       CNDMX1,SXRANGE1       * add to number in range 1
          SUB       CNDMX1,DAYSPERD       * get number of days left to add
          SUB       CNDMX1,CNDMX2         * get new range 2
          SUB       CNDMX1,CNDMX3         * get new range 3
          SUB       CNDMX1,CNDMX4         * get new range 4
          SUB       CNDMX1,CNDMX5         * get new range 5
.
.         see if days in period is in range 2
.
CADB4000  COMPARE   CNDMX2,ZERO
          GOTO      CADB5000 IF NOT LESS    * range 2 <= 0
.
          COMPARE   DAYSPERD,CNDMX2
          GOTO      CADB4100 IF LESS        * days is after range 2
.
.         days in period is in range 2
.
          ADD       DAYSPERD,SXRANGE2       * add number to range 2 days
          GOTO      CADB9999
.
.         days in period is after range 2
.
CADB4100  ADD       CNDMX2,SXRANGE2       * add to number in range 2
          SUB       CNDMX2,DAYSPERD       * get number of days left to add
          SUB       CNDMX2,CNDMX3         * get new range 3
          SUB       CNDMX2,CNDMX4         * get new range 4
          SUB       CNDMX2,CNDMX5         * get new range 5
.
.         see if days in period is in range 3
.
CADB5000  COMPARE   CNDMX3,ZERO
          GOTO      CADB6000 IF NOT LESS    * range 3 <= 0
.
          COMPARE   DAYSPERD,CNDMX3
          GOTO      CADB5100 IF LESS        * days is after range 3
.
.         days in period is in range 3
.
          ADD       DAYSPERD,SXRANGE3       * add number to range 3 days
          GOTO      CADB9999
.
.         days in period is after range 3
.
CADB5100  ADD       CNDMX3,SXRANGE3       * add to number in range 3
          SUB       CNDMX3,DAYSPERD       * get number of days left to add
          SUB       CNDMX3,CNDMX4         * get new range 4
          SUB       CNDMX3,CNDMX5         * get new range 5
.
.         see if days in period is in range 4
.
CADB6000  COMPARE   CNDMX4,ZERO
          GOTO      CADB7000 IF NOT LESS    * range 4 <= 0
.
          COMPARE   DAYSPERD,CNDMX4
          GOTO      CADB6100 IF LESS        * days is after range 4
.
.         days in period is in range 4
.
          ADD       DAYSPERD,SXRANGE4        * add number to range 4 days
          GOTO      CADB9999
.
.         days in period is after range 4
.
CADB6100  ADD       CNDMX4,SXRANGE4       * add to number in range 4
          SUB       CNDMX4,DAYSPERD       * get number of days left to add
          SUB       CNDMX4,CNDMX5         * get new range 5
.
.         see if days in period is in range 5
.
CADB7000  COMPARE   CNDMX5,ZERO
          GOTO      CADB8000 IF NOT LESS    * range 5 <= 0
.
          COMPARE   DAYSPERD,CNDMX5
          GOTO      CADB7100 IF LESS        * days is after range 5
.
.         days in period is in range 5
.
          ADD       DAYSPERD,SXRANGE5       * add number to range 5 days
          GOTO      CADB9999
.
.         days in period is after range 5
.
CADB7100  ADD       CNDMX5,SXRANGE5       * add to number in range 5
          SUB       CNDMX5,DAYSPERD       * get number of days left to add
.
.         days in period is in range 6
.
CADB8000  ADD       DAYSPERD,SXRANGE6       * update days in range 6
          GOTO      CADB9999
.
.         Day case patient
.
CADB8900  ADD       ONE,SXDAYCAS             * add to daycase amount
CADB9999  RETURN
.
.*********************************************************************
.*                  UPDB0000                    Called by : POST0000 *
.*         Update/write to tempfile                                  *
.*********************************************************************[
.
UPDB0000  UNPACK    KEY9,XHFUND,XCODE       * get fund and type
          CALL      RDTEMPB1                * read temp file
          BRANCH    OVRCD,UPDB9500          * write a record
.
.         update current values
.
          ADD       SXDAYCAS,XDAYCAS
          ADD       SXRANGE1,XRANGE1
          ADD       SXRANGE2,XRANGE2
          ADD       SXRANGE3,XRANGE3
          ADD       SXRANGE4,XRANGE4
          ADD       SXRANGE5,XRANGE5
          ADD       SXRANGE6,XRANGE6
          CALL      UPTEMPB1              
          GOTO      UPDB9999
.
.         write a new record
.
UPDB9500  MOVE      SXDAYCAS,XDAYCAS
          MOVE      SXRANGE1,XRANGE1
          MOVE      SXRANGE2,XRANGE2
          MOVE      SXRANGE3,XRANGE3
          MOVE      SXRANGE4,XRANGE4
          MOVE      SXRANGE5,XRANGE5
          MOVE      SXRANGE6,XRANGE6
.
          MOVE      ZERO,XADMS
          MOVE      ZERO,XTOUT
          MOVE      ZERO,XDISC
          MOVE      ZERO,XSBDY
          CALL      WRTEMPB1                * write a record
.
UPDB9999  RETURN
+
.*********************************************************************
.*                  DTPA0000                    Called by : EXTA0000 *
.*        Delete temp file A                                         *
.*********************************************************************
DTPA0000  PACK      CFNAME,PRFIXA,PORT
          REP       " 0",CFNAME
.
          CLOSE     TEMPA
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAME
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTPA9999  RETURN
+
.*********************************************************************
.*                  CTPA0000                    Called by : EXTA0000 *
.*        Create temp file A                                         *
.*********************************************************************
CTPA0000  CALL      DTPA0000                * delete it first
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAME,KEYA,SP30
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPA,CFNAME
.
CTPA9999  RETURN
+
.*********************************************************************
.*                  DTPB0000                    Called by : EXTB0000 *
.*        Delete temp file B                                         *
.*********************************************************************
DTPB0000  PACK      CFNAME,PRFIXB,PORT
          REP       " 0",CFNAME
.
          CLOSE     TEMPB
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAME
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTPB9999  RETURN
+
.*********************************************************************
.*                  CTPB0000                    Called by : EXTB0000 *
.*        Create temp file B                                         *
.*********************************************************************
CTPB0000  CALL      DTPB0000                * delete it first
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAME,KEYB,SP30
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPB,CFNAME
.
CTPB9999  RETURN
+
.*********************************************************************
.*        I/O routines for Temp file A                               *
.*********************************************************************
RATEMPA1  MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMPA,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMPA1  RESET     KEY8
          READ      TEMPA,KEY8;;
          RETURN
.
RDTEMPA1  MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMPA,KEY8;XADMISS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMPA1  MOVE      ZERO,OVRCD
          READKS    TEMPA;XADMISS
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMPA1  MOVE      ZERO,OVRCD
          READKP    TEMPA;XADMISS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMPA1  RESET     KEY8
          WRITE     TEMPA,KEY8;XADMISS
          RETURN
.
UPTEMPA1  UPDATE    TEMPA;XADMISS
          RETURN
+
.*********************************************************************
.*        I/O routines for Temp file B                               *
.*********************************************************************
RATEMPB1  MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPB,KEY9;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMPB1  RESET     KEY9
          READ      TEMPB,KEY9;;
          RETURN
.
RDTEMPB1  MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPB,KEY9;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                               XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                               XADMS,XDISC,XSBDY,XTOUT
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMPB1  MOVE      ZERO,OVRCD
          READKS    TEMPB;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                          XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                          XADMS,XDISC,XSBDY,XTOUT
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMPB1  MOVE      ZERO,OVRCD
          READKP    TEMPB;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                          XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                          XADMS,XDISC,XSBDY,XTOUT
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMPB1  RESET     KEY9
          WRITE     TEMPB,KEY9;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                               XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                               XADMS,XDISC,XSBDY,XTOUT
          RETURN
.
UPTEMPB1  UPDATE    TEMPB;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                          XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                          XADMS,XDISC,XSBDY,XTOUT
          RETURN
+
.-------------------------------------------------------------------------------
. -----   Process this transfer record for adms. disch. trans. sep bed days ----
.
.*******************************************************************************
.*          TRANS000 - Procedure to extract all admission information          *
.*                                                                             *
.*******************************************************************************
.
          DEFPROC   TRANS000
TEMPB     IFILE     SQL, FIXED=54, KEY="U1-6,7-9"
.
.Name     Type      Length  Physical Start  Name
.-------  ----      ------  -------- -----  -----------------------------------
XHFUND    DIM       6            6       1  Health fund code 
.                                             "zzzzzz" for Grand Totals
XCODE     DIM       3            3       7  Admission type
.                                             "zzz" for Fund totals
XDAYCAS   FORM      6            4      10  Number of day cases
XRANGE1   FORM      6            4      14  Number of days in range 1
XRANGE2   FORM      6            4      18  Number of days in range 2
XRANGE3   FORM      6            4      22  Number of days in range 3
XRANGE4   FORM      6            4      26  Number of days in range 4
XRANGE5   FORM      6            4      30  Number of days in range 5
XRANGE6   FORM      6            4      34  Number of days greater than range 5
. -----   Extra fields added for IBAHMS26  -----
XADMS     FORM      6            4      38  Number of Admissions
XDISC     FORM      6            4      42  Number of Discharges
XSBDY     FORM      6            4      46  Number of Separation Bed Days
XTOUT     FORM      6            4      50  Number of Transfers Out
.
SXDAYCAS  FORM      6
SXRANGE1  FORM      6
SXRANGE2  FORM      6
SXRANGE3  FORM      6
SXRANGE4  FORM      6
SXRANGE5  FORM      6
SXRANGE6  FORM      6
.
          INC       PATTRNFD/INC
.
          OPEN      PATTRAN2,"pattranf"
          PACK      CFNAME,PRFIXB,PORT
          REP       " 0",CFNAME
          OPEN      TEMPB,CFNAME
.
          MOVE      ZERO,SXADMS
          MOVE      ZERO,SXDISC
          MOVE      ZERO,SXSBDY
          MOVE      ZERO,SXTOUT
.
. -----   Check if Admitted in the date range  -----
.
          PACK      KEY30,KEY8,ADATE,SP30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
.
          MOVE      XADMISS,DIM8VISN        * get as a form
          MATCH     DIM8VISN,TADMN
          GOTO      TRANS100 IF NOT EQUAL   * different visit
.
          MATCH     ANSA,TMOVE              * Check for an admission record
          GOTO      TRANS100 IF NOT EQUAL
.
          MATCH     FRDATE8,TDATE           * Check if after from date
          GOTO      TRANS100 IF LESS
.
          MATCH     TODATE8,TDATE           * Check if before end date
          GOTO      TRANS100 IF NOT LESS
.
          MOVE      ONE,SXADMS
.
          PACK      KEY9,AFUNDH,TATYPE
          MOVE      TATYPE,SAVCODE
          CALL      WRIT0000
.
. -----   Check if Discharged in the date range  -----
.
TRANS100  PACK      KEY30,KEY8,DDATE,SP30
          CALL      RDSTRAN2
TRANS110  CALL      RDKTRAN2
          BRANCH    OVRCD,TRANS200
.
          MOVE      XADMISS,DIM8VISN        * get as a form
          MATCH     DIM8VISN,TADMN
          GOTO      TRANS200 IF NOT EQUAL   * different visit
.
          MATCH     ANSD,TMOVE              * Check for a discharge record
          GOTO      TRANS110 IF NOT EQUAL   * Maybe Admitted on the same day
.
          MATCH     FRDATE8,TDATE           * Check if after to date
          GOTO      TRANS200 IF LESS
.
          MATCH     TODATE8,TDATE           * Check if before end date
          GOTO      TRANS200 IF NOT LESS
.
          MOVE      ONE,SXDISC
.
          PACK      KEY9,AFUNDH,TATYPE
          MOVE      TATYPE,SAVCODE
          CALL      WRIT0000
.
TRANS200  CALL      SBDY0000                * Calculate Seperation Bed Days
.
TRANS300  CALL      TOUT0000                * Determine transfers out
.
TRANS999  GOTO      ENDPROC
.
.*******************************************************************************
.*          SBDY0000 - Calculate Seperation Bed Day for a Date Range           *
.*                                                                             *
.*******************************************************************************
.
SBDY0000  CALL      GFNSH000                * Get Finish of separation period
          BRANCH    EXIT,SBDY9999
.
SBDY1000  CALL      GSTRT000                * Get Start of Separation Period
          BRANCH    EXIT,SBDY9999
.
          CALL      NHOSPDAY                * Calculate Bed Days
.
          MOVE      NBRDAYS,SXSBDY          * Set up the data
.
          PACK      KEY9,AFUNDH,SEPTYPE
          MOVE      SEPTYPE,SAVCODE
          CALL      WRIT0000                * Update the temp file
.
          MATCH     FRDATE8,TDATE          * Last seperation bed day adm type
          GOTO      SBDY9999 IF LESS
.
          MOVE      TDATE,TODATE           * Save the current record as the
          MOVE      TATYPE,SAVATYPE         * finish of the next seperation
          GOTO      SBDY1000                * period, then get next start date
.
SBDY9999  RETURN
.
.*******************************************************************************
.*          GFNSH000 - Get Finish of Separation Period                         *
.*                                                                             *
.*******************************************************************************
.
GFNSH000  MOVE      FALSE,EXIT
.
          PACK      KEY30,KEY8,ACTTODAT,ZEDS
          CALL      RDSTRAN2
.
GFNSH100  CALL      RDPTRAN2
          BRANCH    OVRCD,GFNSH900
.
GFNSH200  MOVE      XADMISS,DIM8VISN        * get as a form
          MATCH     DIM8VISN,TADMN
          GOTO      GFNSH900 IF NOT EQUAL   * different visit
.
          MATCH     FRDATE8,TDATE           * Check if in the date range
          GOTO      GFNSH900 IF LESS
.
          MATCH     ANSA,TMOVE              * An admission record cannot be the
          GOTO      GFNSH900 IF EQUAL       * end of a seperation range
.
          MATCH     ANSR,TMOVE              * A return from leave record cannot
          GOTO      GFNSH100 IF EQUAL       * be the end of a seperation range
.
          MOVE      TDATE,TODATE            * Save the transfer date
          MOVE      TATYPE,SAVATYPE         * Save the admission type
          MOVE      TMOVE,SAVTMOVE          * save movement type
          GOTO      GFNSH999
.
GFNSH900  MOVE      TRUE,EXIT
.
GFNSH999  RETURN
.
.*******************************************************************************
.*          GSTRT000 - Get Start of Separation Period                          *
.*                                                                             *
.*******************************************************************************
.
GSTRT000  MOVE      FALSE,EXIT
.
GSTRT100  CALL      RDPTRAN2
          BRANCH    OVRCD,GSTRT900
.
          MATCH     DIM8VISN,TADMN
          GOTO      GSTRT900 IF NOT EQUAL   * different visit
.
          MATCH     ANSD,SAVTMOVE           * was previous record a discharge?
          GOTO      GSTRT500 IF EQUAL       * ignore check on Admission type
          
          MATCH     SAVATYPE,TATYPE
          GOTO      GSTRT100 IF EQUAL
.
GSTRT500  MOVE      TATYPE,SEPTYPE          * separation type
          MOVE      TDATE,FROMDATE
          GOTO      GSTRT999
.
GSTRT900  MOVE      TRUE,EXIT
.
GSTRT999  RETURN
.
.*******************************************************************************
.*          WRIT0000 - Write/Update the Temp File Variables                    *
.*                                                                             *
.*******************************************************************************
.
WRIT0000  CALL      UPDT0000                * update for fund and type total
.
          PACK      KEY9,XHFUND,ZED3        * fund and zzz
          CALL      UPDT0000                * update for health fund totals
.
          MOVE      SAVCODE,XCODE           * restore code since prev "zzz"
          PACK      KEY9,ZED6,XCODE         * zzzzzz and type
          CALL      UPDT0000                * update for type totals
.
          PACK      KEY9,ZED6,ZED3          * zzzzzz and zzz
          CALL      UPDT0000                * update for grand totals
.
          MOVE      ZERO,SXADMS             * Clear the fields
          MOVE      ZERO,SXDISC
          MOVE      ZERO,SXSBDY
          MOVE      ZERO,SXTOUT
.
          RETURN
.
.*******************************************************************************
.*          UPDT0000 - Update the Temp File Variables                          *
.*                                                                             *
.*******************************************************************************
.
UPDT0000  UNPACK    KEY9,XHFUND,XCODE       * get fund and type
          CALL      RDTEMPB1                * read temp file
          BRANCH    OVRCD,UPDT5000          * write a record
.
          ADD       SXADMS,XADMS            * Number of Admissions
          ADD       SXDISC,XDISC            * Number of Discharges
          ADD       SXSBDY,XSBDY            * Number of Seperation Bed Days
          ADD       SXTOUT,XTOUT            * Number of Transfers out
.
          CALL      UPTEMPB1              
          GOTO      UPDT9999
.
UPDT5000  MOVE      SXADMS,XADMS            * Number of Admissions
          MOVE      SXDISC,XDISC            * Number of Discharges
          MOVE      SXSBDY,XSBDY            * Number of Seperation Bed Days
          MOVE      SXTOUT,XTOUT            * Number of Transfers out
.
          MOVE      ZERO,XDAYCAS
          MOVE      ZERO,XRANGE1
          MOVE      ZERO,XRANGE2
          MOVE      ZERO,XRANGE3
          MOVE      ZERO,XRANGE4
          MOVE      ZERO,XRANGE5
          MOVE      ZERO,XRANGE6
.
          CALL      WRTEMPB1              
.
UPDT9999  RETURN
.
.*******************************************************************************
.*          TOUT0000 - Calculate Transfers out of adm. types                   *
.*                                                                             *
.*******************************************************************************
.
TOUT0000  PACK      KEY30,KEY8,FRDATE8,ZEDS
          CALL      RDSTRAN2
          CALL      RDPTRAN2                * Get 1st tran record before todate
.
          MOVE      XADMISS,DIM8VISN        * get as a form
          MATCH     DIM8VISN,TADMN
          GOTO      TOUT1000 IF NOT EQUAL   * not admitted before the range
.
          MOVE      TATYPE,SAVATYPE         * Save The Admission Type
          GOTO      TOUT2000
.
TOUT1000  CALL      RDKTRAN2                * Get 1st tran record if it is after
          BRANCH    OVRCD,TOUT9999          * the fromdate
.
          MOVE      XADMISS,DIM8VISN        * get as a form
          MATCH     DIM8VISN,TADMN
          GOTO      TOUT9999 IF NOT EQUAL
.
          MATCH     TDATE,TODATE8           * Record out of range
          GOTO      TOUT9999 IF LESS
.
          MOVE      TATYPE,SAVATYPE
.
TOUT2000  CALL      RDKTRAN2
          BRANCH    OVRCD,TOUT9999
.
          MOVE      XADMISS,DIM8VISN        * get as a form
          MATCH     DIM8VISN,TADMN
          GOTO      TOUT9999 IF NOT EQUAL
.
          MATCH     TDATE,TODATE8           * Record out of range
          GOTO      TOUT9999 IF LESS
.
          MATCH     TATYPE,SAVATYPE
          GOTO      TOUT2000 IF EQUAL
.
          MOVE      ONE,SXTOUT
.
          MOVE      SAVATYPE,SAVCODE
          PACK      KEY9,AFUNDH,SAVATYPE
          CALL      WRIT0000
.
          MOVE      TATYPE,SAVATYPE
          GOTO      TOUT2000
.
TOUT9999  RETURN
.
.
          INC       IBAOVRIO/INC
          INC       PATTRNIO/INC
RATEMPB1  MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPB,KEY9;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMPB1  RESET     KEY9
          READ      TEMPB,KEY9;;
          RETURN
.
RDTEMPB1  MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPB,KEY9;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                               XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                               XADMS,XDISC,XSBDY,XTOUT
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMPB1  MOVE      ZERO,OVRCD
          READKS    TEMPB;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                          XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                          XADMS,XDISC,XSBDY,XTOUT
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMPB1  MOVE      ZERO,OVRCD
          READKP    TEMPB;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                          XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                          XADMS,XDISC,XSBDY,XTOUT
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMPB1  RESET     KEY9
          WRITE     TEMPB,KEY9;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                               XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                               XADMS,XDISC,XSBDY,XTOUT
          RETURN
.
UPTEMPB1  UPDATE    TEMPB;XHFUND,XCODE,XDAYCAS,XRANGE1,XRANGE2:
                          XRANGE3,XRANGE4,XRANGE5,XRANGE6:
                          XADMS,XDISC,XSBDY,XTOUT
          RETURN
.
ENDPROC   
          ENDPROC
.
.-------------------------------------------------------------------------------
.
.
.
.         I/O includes needed
.
          INC       STD001IO                * standard routines
          INC       PATDAYIO/INC            * day file
          INC       PATCODIO/INC            * codes file
          INC       PATDSCIO/INC            * discharge file
          INC       PATFN1IO/INC            * health fund file
          INC       PATMI1IO/INC            * admission file
          INC       PMSVX1IO/INC            * admission file
          INC       PATTRNIO/INC            * transfer file
.
          INC       CALCDAYS
          INC       NHOSPDAY
................................................................................

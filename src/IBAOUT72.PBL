.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT72.PBL                                              *
.* Name           : Print Extra Screen Details                                *
.******************************************************************************
.* Date           : 26/06/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To print the Word processor details entered in the        *
.*                  Outpatient extra booking screen                           *
.* Modifications  :                                                           * 
.******************************************************************************
.*        V12.00.01 23/05/2025  Bella Turco      TSK 0955096                  *
.*                  Alphanumeric changes                                      *
.*        V11.02.01 09/02/2022  Thanh T          TSK 0905641                  *
.*                  Recompiled as OUTMA1FD changes                            *
.*        V10.07.01 30/10/2015  Ebon Clements    CAR 268970                   *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.02.01 07/07/2011  Steve Armstrong  CAR 240722                   *
.*                  Added RDPMPX21 prior to calling PMIHEAD                   *
.******************************************************************************
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.******************************************************************************
.*        V9.03.01  11/09/2003  Sandra Barcham    43010                       *
.*                  Changed open OUTPREA1 to NOT use site prefix              *
.******************************************************************************
.*        V9.02.01  31/01/2003  Tracey Nguyen SRF 22709                       *
.*                  Changed open OUTPREA1 to use site prefix                  *
.*        V9.02.00  Ported from r5.10                                         *
.******************************************************************************
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 15/03/2000  Steve Armstrong   5.08 Mods                   *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.*        V5.01.02  14/09/92 J.Tan   SRF 117075                               *
.*                  Fixed I*C on patcodes                                     *
.*        V5.01.03  30/09/92 J.Tan   SRF 117614                               *
.*                  Fixed to read pmi detail for clinic details option        *
.*        V5.01.04  02/03/93 ROD                                              *
.*                  recompiled for new outma1af                               *
.*        V5.01.05  22/04/93 DIG                                              *
.*                  Re-Compiled for changes to PATALERT.                      *
.*        V5.01.06  26/08/93 ROD                                              *
.*                  recompiled for NZHIS                                      *
.*        V5.01.07  09/12/1993    Justin Coates  Quote 7838  SRF 126069       *
.*                  added All for U/R option                                  *
.*        V5.01.08  13/01/1994    Sandra Barcham                              *
.*                  fix global recompile errors                               *
.*        V5.01.09  27/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.******************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       BOKRC1FD/INC            * booking file
          INC       OUTBOAFD/INC            * booking A
          INC       OUTCLIFD/INC            * booking A
          INC       OUTCONFD/INC            * system parameters
          INC       OUTCTYFD/INC            * clinic type file
          INC       OUTMA1FD/INC            * clinic master fil
          INC       OUTPREFD/INC            * OP pre-attendance file
          INC       OUTXWPFD/INC            * extra screen WP file
          INC       PATALRFD/INC            * alerts file
          INC       PATCALAG/INC
          INC       PATCODFD/INC            * codes file
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATDHEAD/INC            * variables for header
          INC       PATDO1FD/INC            * doctor file
          INC       PMSHCPFD/INC            * National HCP file
          INC       PATMA1FD/INC            * patient master file
          INC       PATMI1FD/INC            * admission file
          INC       PATNIDFD/INC            * national id file
          INC       PATPR1FD/INC            * pre-admission file
          INC       PATGSRFD/INC            * surname file
          INC       PATVISFD/INC            * visit file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC            * visit extension file
.
.         program variables
.
BOOKING   DIM       36[15]    * displayed bookings
BJDAY     FORM      3
CJDAY     FORM      3
CNTBOOK   FORM      2
CALLPOSN  FORM      1
DDAY      DIM       9
DIM40     DIM       40
COMMENT   DIM       50        * session comments
DSESST    DIM       20        * session type
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
NMPNUMB   DIM       20
NOCOMM    FORM      1         * 1=no comments for patient
NORMREVS  FORM      1         * print sequence 2 = reverse date, 1 = forward
PATNAME   DIM       40        * patients name
SCNDGNAM  DIM       40
SHCTYP    DIM       20
VSESS     DIM       25        * session details
VSITE     DIM       6         * site   code
VCLIN     DIM       6         * clinic code
VDATE     DIM       8         * clinic date
VTIME     DIM       5         * clinic time
VCTYP     DIM       6         * clinic type
VDAY      FORM      1         * clinic day 
VSESST    DIM       3         * session type
.
.         program constants
.
CATCS     INIT      "CS"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT72"
PRGNAM    INIT      "Print Extra Screen Details"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.                   ML0000
.         MAINLINE CODE / CONTROLLING LOGIC
.*********************************************************************
ML0000    CALL      INIT0000                * initilization
.
ML1000    CALL      OPTN0000
          BRANCH    MOPTION,ML2000,ML3000,ML4000
          GOTO      ML9999
.
. ******* by U/R number *******
.
ML2000    CALL      BYUR0000
          GOTO      ML1000
.
. ******* by Clinic details *****
.
ML3000    CALL      CLIN0000
          GOTO      ML1000
.
. ******* by U/R number for all visits *******
.
ML4000    CALL      UALL0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.                   INIT0000                    Called by : ML0000
.         Initialization
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"          * files to open
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P58:24,"outpreaf";
          OPEN      OUTPREA1,"outpreaf"
.
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
          DISPLAY   *P58:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          PACK      CFNAME,UID,FILXWPA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTXWPA1,CFNAME
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTBOKA1,CFNAME
          OPEN      OUTBOKA3,CFNAME
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
          PACK      CFNAME,UID,FILCTYA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCTYA1,CFNAME
          PACK      CFNAME,UID,FILMA1A1
          DISPLAY   *P58:24,CFNAME
          CALL       OPOTMA11
.
          READ      CONTROLF,THIRTY1;*213,OTCNAXCU
.
.         see which surname search files to open
.
          OPEN      PATGSRN1,"patgsrnf"     * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
          MOVE      ONE,CNEWDS
          MOVE      IDDD,VSITE              * set site
.
INIT9999  RETURN
+
.*********************************************************************
.                   OPTN0000                    Called by : ML1000
.         enter the option
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF,". Exit":
                    *P1:5,*V2LON,"1",*HOFF,". U/R Number":
                    *P1:6,*V2LON,"2",*HOFF,". Clinic Details";
          MOVE      EIGHT,CVERT
          IF        OTCNAXCU = ONE
            DISPLAY   *P1:7,*V2LON,"3",*HOFF,". All comments for U/R Number"
            ADD       ONE,CVERT
          ENDIF
          DISPLAY   *P1:CVERT,"Select option : "
.
OPTN1000  KEYIN     *P17:CVERT,*V2LON,MOPTION
          BRANCH    MOPTION,OPTN9999,OPTN9999,OPTN5000
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL
OPTN1500  BEEP
          GOTO      OPTN1000
.
OPTN5000  COMPARE   ONE,OTCNAXCU
          GOTO      OPTN1500 IF NOT EQUAL   * option not available
.
OPTN9999  RETURN
+
.*********************************************************************
.                   BYUR0000                    Called by : ML2000
.         get the visit by UR number
.*********************************************************************
BYUR0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"U/R Number   : "
          MOVE      "4",CVERT
          MOVE      "16",CCOL
          CALL      KURN
          BRANCH    EXIT,BYUR9999           * exit
          BRANCH    OVRCD,BYUR9000          * invalid UR 
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,BYUR9000
.
BYUR1000  CALL      PMIHEAD
          CALL      DBOA0000                * display all outpatient visits
          BRANCH    EXIT,BYUR0000           * exit
.
          CALL      OUTHEAD
          CALL      DCOM0000                * display the comments
          CALL      CONTQST
          BRANCH    CEXIT,BYUR2000,BYUR1000,BYUR0000
.                         Yes      No       Cancel
.
BYUR2000  CALL      PDET0000                * print details
          GOTO      BYUR1000
.
BYUR9000  DISPLAY   *P1:24,*B,"Invalid U/R number.  ",*EL;
          CALL      HITENTER
          GOTO      BYUR0000
.
BYUR9999  RETURN
+
.*********************************************************************
.                   CLIN0000                    Called by : ML3000
.         get the patient by entering clinic details
.*********************************************************************
CLIN0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"Clinic : ":
                    *P5:5,"Date   : ":
                    *P5:6,"Time   : "
.
          CALL      KCLI0000                * enter clinic id
          BRANCH    EXIT,CLIN9999           * exit
.
          CALL      KDAT0000                * enter clinic date
          CALL      KTIM0000                * enter clinic time
          BRANCH    OVRCD,CLIN0000
.
CLIN1000  CALL      HDRC0000                * display clinic header
          BRANCH    EXIT,CLIN0000           * invalid details
.
          CALL      DSES0000                * display the session
          BRANCH    EXIT,CLIN0000           * exit
.
          CALL      OUTHEAD
          CALL      DCOM0000                * display the comments
          CALL      CONTQST
          BRANCH    CEXIT,CLIN2000,CLIN1000,CLIN0000
.                         Yes      No       Cancel
.
CLIN2000  CALL      PDET0000                * print details
          GOTO      CLIN1000
.
CLIN9999  RETURN
+
.*********************************************************************
.                   UALL0000                    Called by : ML4000
.         print comments for all visits for a U/R Number
.*********************************************************************
UALL0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"U/R Number   : "
          MOVE      "4",CVERT
          MOVE      "16",CCOL
          CALL      KURN
          BRANCH    EXIT,UALL9999           * exit
          BRANCH    OVRCD,UALL9000          * invalid UR 
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,UALL9000
.
UALL1000  CALL      PMIHEAD
          KEYIN     *P1:7,"Reverse visit date sequence (":
                    *V2LON,*DV,ANSY,*HOFF,"/",*V2LON,*DV,ANSN,*HOFF:
                    ") : ",*V2LON,ANS
          REP       UPPLOW,ANS
          MOVE      TWO,NORMREVS            * set to reverse
          MATCH     ANSY,ANS
          GOTO      UALL2000 IF EQUAL
          MOVE      ONE,NORMREVS            * set to normal
          MATCH     ANSN,ANS
          GOTO      UALL2000 IF EQUAL
          BEEP
          GOTO      UALL1000
.
.         now print the details
.
UALL2000  MOVE      ZERO,CPAGENO
          MOVE      "70",CLNO               * set line number for new page
          CALL      IBACLOCK
.
          IF        NORMREVS = ONE
            PACK      KEY36,PURNO,SP30
          ELSE
            PACK      KEY36,PURNO,Z30
          ENDIF
          CALL      RDSBOKA3
.
UALL5000  PERFORM   NORMREVS,RDKBOKA3,RDPBOKA3
          BRANCH    OVRCD,UALL7000          * finished for patient
.
          MATCH     PURNO,OBAURNO
          GOTO      UALL7000 IF NOT EQUAL   * finished for patient
.
          MOVE      OBACLIN,PVIDOCT         * set doctor
          MOVE      OBADATE,PVIDATE         * set date
          MOVE      OBAOUTNO,PVIBILL        * set op number
          MOVE      OBASITE,PVISITE         * set site
          CALL      OUTHEAD
.
          CALL      PDET0000
          GOTO      UALL5000
.
UALL7000  PRINT     *N,"*** End of Report ***",*N
          GOTO      UALL9999
.
UALL9000  DISPLAY   *P1:24,*B,"Invalid U/R number.  ",*EL;
          CALL      HITENTER
          GOTO      UALL0000
.
UALL9999  RETURN
+
.*********************************************************************
.*                  DBOA0000                    Called by : BYUR1000 *
.*        Display all bookings for the UR number entered             *
.*********************************************************************
DBOA0000  CALL      CLRA0000                * clear storage variables
          MOVE      ONE,CPAGENO             * set page
          MOVE      "7",CVERT               * starting line
          MOVE      ZERO,CNTBOOK            * clear count
          DISPLAY   *P1:7,*EF,*V2LON,*ULON:
                    *P5:7,"O/P No. ":
                    *P14:7,"Clinic                                 ":
                    *P54:7,"   Date   ":
                    *P65:7,"Time "
.
          PACK      KEY36,PURNO,Z30
          CALL      RDSBOKA3
.
. ******* loop over file *******
.
DBOA1000  CALL      RDPBOKA3
          BRANCH    OVRCD,DBOA5000          * end of file
.
          MATCH     OBAURNO,PURNO
          GOTO      DBOA5000 IF NOT EQUAL   * different patient
.
          MATCH     IDDD,OBASITE
          GOTO      DBOA1000 IF NOT EQUAL   * wrong site
.
          MOVE      ONE,FORM4
          PACK      KEY12,OBAOUTNO,FORM4
          CALL      RDOTXWP1
          BRANCH    OVRCD,DBOA1000          * no extra details
.
. ******* increment counters *******
.
DBOA2000  ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      DBOA4000 IF NOT LESS    * need a new page
          ADD       ONE,CNTBOOK             * add to item counter
.
. ******* display data and store key *******
.
DBOA3000  UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      KEY20,OBASITE,OBACLIN,OBADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OBASITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OBACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          BRANCH    OVRCD,DBOA3100
.
          MATCH     SP6,OCADOCT
          IF        !@EQUAL
            MOVE      OCADOCT,KEY6
            CALL      RDDOCT1
            IF        OVRCD = ZERO
              MOVE      DSNAME,PACSNAME
              MOVE      DGNAME,PACGNAME
              MOVE      DTITL,PACTITLE
              MOVE      TWO,PACFRMT
              CALL      PACNAME
              MOVE      PACFNAME,OCADESC
            ENDIF
          ENDIF
.
DBOA3100  DISPLAY   *P1:CVERT,*V2LON,CNTBOOK,*HOFF,". ",OBAOUTNO,SP1:
                    KEY6," - ",OCADESC,SP1,CPCDATE,SP1,OBASTRT
          PACK      KEY36,OBAURNO,OBADATE,OBASTRT,OBASLOT,OBASITE,OBACLIN
          MOVE      KEY36,BOOKING[CNTBOOK]
          GOTO      DBOA1000
.
. ******* new screen needed *******
.
DBOA4000  BRANCH    CPAGENO,DBOA4500
.
.         Middle pages : (N)ext & (P)revious
.
DBOA4100  MOVE      ONE,CALLPOSN            * set position
          CALL      KEYA0000
          BRANCH    EXIT,DBOA7000,DBOA8000,DBOA9100
.                        Next     Previous Exit
          GOTO      DBOA6000
.
.         first page : (N)ext
.
DBOA4500  MOVE      TWO,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DBOA7000,DBOA5900,DBOA9100
.                        Next     Previous Exit
          GOTO      DBOA6000
.
. ******* no more data to display *******
.
DBOA5000  COMPARE   ZERO,CNTBOOK
          GOTO      DBOA8900 IF EQUAL       * nothing on file
.
          BRANCH    CPAGENO,DBOA5500
.
.         last page : (P)revious
.
DBOA5100  MOVE      THREE,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DBOA5900,DBOA8000,DBOA9100
.                        Next     Previous Exit
          GOTO      DBOA6000
.
.         first page :
.
DBOA5500  MOVE      FOUR,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DBOA5900,DBOA5900,DBOA9100
.                        Next     Previous Exit
          GOTO      DBOA6000
.
DBOA5900  BEEP   
          BRANCH    CALLPOSN,DBOA4100,DBOA4500,DBOA5100,DBOA5500
.
. ******* item on screen selected *******
.
DBOA6000  MOVE      BOOKING[FORM2],KEY36
          CALL      RDBOKA3
          BRANCH    OVRCD,DBOA5900          * invalid details
.
          MOVE      ONE,FORM4
          PACK      KEY12,OBAOUTNO,FORM4
          CALL      RDOTXWP1
          BRANCH    OVRCD,DBOA5900          * invalid details
.
          MOVE      OBACLIN,PVIDOCT         * set doctor
          MOVE      OBADATE,PVIDATE         * set date
          MOVE      OBAOUTNO,PVIBILL        * set op number
          MOVE      OBASITE,PVISITE         * set site
.
          MOVE      ZERO,EXIT
          GOTO      DBOA9999
.
. ******* set up for a new page *******
.
DBOA7000  MOVE      BOOKING[15],KEY36
          CALL      RDBOKA3
          CALL      RDPBOKA3
          BRANCH    OVRCD,DBOA5900          * no next screen
.
          CALL      CLRA0000                * clear storage variables
          ADD       ONE,CPAGENO             * add to page counter
          MOVE      ONE,CNTBOOK             * reset item counter
          MOVE      "8",CVERT               * set row
          DISPLAY   *P1:CVERT,*EF           * clear screen
          GOTO      DBOA3000
.
. ******* set up for previous screen *******
.
DBOA8000  MOVE      BOOKING[1],KEY36
          CALL      RDBOKA3
          MOVE      ZERO,FORM2
.
DBOA8100  ADD       ONE,FORM2
          COMPARE   "16",FORM2
          GOTO      DBOA8200 IF NOT LESS    * finished reads
.
DBOA8150  CALL      RDKBOKA3
          BRANCH    OVRCD,DBOA8200          * finished reads
.
          MATCH     IDDD,OBASITE
          GOTO      DBOA8150 IF NOT EQUAL   * incorrect site
.
          MOVE      ONE,FORM4
          PACK      KEY12,OBAOUTNO,FORM4
          CALL      RDOTXWP1
          BRANCH    OVRCD,DBOA8150          * no extra details
          GOTO      DBOA8100
.
DBOA8200  CALL      CLRA0000                * clear storage variables
          SUB       ONE,CPAGENO             * sub from page counter
          MOVE      ONE,CNTBOOK             * reset item counter
          MOVE      "8",CVERT               * set row
          DISPLAY   *P1:CVERT,*EF           * clear screen
          GOTO      DBOA3000
.
DBOA8900  DISPLAY   *P1:24,*B,"No Bookings found.  ",*EL;
          CALL      HITENTER
.
. ******* set exit flags *******
.
DBOA9100  MOVE      ONE,EXIT                * exit selected
.
DBOA9999  RETURN
+
.*********************************************************************
.                   KCLI0000                    Called by : CLIN0000 
.         enter the clinic id 
.*********************************************************************
KCLI0000  MOVE      ZERO,CKYIMAND           * not mandatry
          MOVE      SP6,CKYIDEF6            * no default
          MOVE      FOUR,EVERT              * set row
          MOVE      "14",ECOL               * set column
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLI9100,KCLI9100,KCLI9100
.
          MOVE      OCACLIN,VCLIN           * set clinic code
          DISPLAY   *P30:4,OCADESC
          GOTO      KCLI9999
.
KCLI9100  MOVE      ONE,EXIT
.
KCLI9999  RETURN
+
.*********************************************************************
.                   KDAT0000                    Called by : CLIN0000 
.         enter the clinic date
.*********************************************************************
KDAT0000  MOVE      CCC,CCENT
          UNPACK    SP6,CDAY,CMON,CYEAR
          MOVE      ONE,CDEFDTE
          MOVE      "14",CCOL
          MOVE      "5",CVERT
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00                * save the screen
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT2000         * ? entered
          BRANCH    OVRCD,KDAT0000          * enter a date
.
          CALL      FRESCR00                * free the screen
          PACK      VDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",VDATE
          GOTO      KDAT9999
.
KDAT2000  CALL      OUTDSMAS
          CALL      PUTSCR00
          GOTO      KDAT0000
.
KDAT9999  RETURN
+
.*********************************************************************
.                   KTIM0000                    Called by : CLIN0000 
.         enter the clinic time
.*********************************************************************
KTIM0000  UNPACK    SP4,CHOUR,CMIN
          MOVE      "14",CCOL
          MOVE      "6",CVERT
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00                * save the screen
          CALL      KEYTIME
          BRANCH    CQUEST,KTIM2000         * ? entered
          BRANCH    OVRCD,KTIM9999          * exit
.
          CALL      FRESCR00                * free the screen
          PACK      VTIME,CHOUR,COLON,CMIN
          REP       " 0",VTIME
          MOVE      ZERO,OVRCD
          GOTO      KTIM9999
.
KTIM2000  CALL      OUTDSMAS
          CALL      PUTSCR00
          GOTO      KTIM0000
.
KTIM9999  RETURN
+
.*********************************************************************
.                   HDRC0000                    Called by : CLIN1000 
.         get the session details and display header
.*********************************************************************
HDRC0000  PACK      VSESS,VSITE,VCLIN,VDATE,VTIME
          PACK      KEY28,VSESS,SP6
          CALL      RDSBOKA1
          CALL      RDKBOKA1
          BRANCH    OVRCD,HDRC9000
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,VSESS
          GOTO      HDRC9000 IF NOT EQUAL   * incorrect session
.
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,HDRC9100          * no master record
.
          MOVE      OMATYP,VCTYP            * Save clinic type
          MOVE      OMADAY,VDAY             * Save day of week
.
.         now get the details to display
.
          MOVE      OBASESST,VSESST     * Save the session status
          MOVE      SP20,DSESST
          MATCH     SP3,VSESST
          IF        !@EQUAL
            PACK      KEY5,CATCS,OBASESST
            CALL      RDCODE1
            IF        OVRCD = ZERO
              MOVE      TDESC,DSESST
            ENDIF
          ENDIF
.
          CLEAR     COMMENT                 * set up comments
          MOVE      OMACOM1,COMMENT
          STRIP     COMMENT
          ENDSET    COMMENT
          APPEND    SP1,COMMENT
          APPEND    OMACOM2,COMMENT
          RESET     COMMENT
.
          MOVE      "Unknown Clinic Type",OCTDESC
          PACK      KEY12,VSITE,VCTYP
          CALL      RDCTYA1                 * get clinic type description
.
          MOVE      SP10,DDAY
          LOAD      DDAY,VDAY,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          UNPACK    VDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:3,*EF:
                    *P1:4,"Clinic Type :":
                    *P1:5,"Clinic Id   :":
                    *P54:4,"Clinic Date :":
                    *P54:5,"Day :":
                    *P70:5,"@":
                    *P15:4,*V2LON,VCTYP,*P22:4,OCTDESC:
                    *P15:5,VCLIN,*P22:5,OCADESC:
                    *P68:4,CPCDATE:
                    *P60:5,DDAY,*P72:5,VTIME:
                    *P1:6,COMMENT,*P60:6,*V2LON,*BLINKON,DSESST
.
          MOVE      ZERO,EXIT
          GOTO      HDRC9999
.
.         Session not found
.
HDRC9000  DISPLAY   *P1:24,*B,*P1:24,"Session details not found.  ",*EL;
          GOTO      HDRC9990
.
HDRC9100  DISPLAY   *P1:24,*B,*P1:24,"Session Master not found.  ",*EL;
.
HDRC9990  CALL      HITENTER
          MOVE      ONE,EXIT
.
HDRC9999  RETURN
+
.*********************************************************************
.                   DSES0000                    Called by : CLIN1000 
.         display the session details
.*********************************************************************
DSES0000  CALL      CLRA0000                * clear storage variables
          MOVE      ONE,CPAGENO             * set page
          MOVE      "8",CVERT               * starting line
          MOVE      ZERO,CNTBOOK            * clear count
          DISPLAY   *P1:8,*EF,*V2LON,*ULON:
                    *P5:8,"Slot":
                    *P10:8,"Time ":
                    *P16:8,"O/P No. ":
                    *P25:8,"U/R No. ":
                    *P34:8,"Patients Name                    "
.
          PACK      KEY28,VSESS,SP3
          CALL      RDSBOKA1
.
. ******* loop over file *******
.
DSES1000  CALL      RDKBOKA1
          BRANCH    OVRCD,DSES5000          * end of file
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,VSESS
          GOTO      DSES5000 IF NOT EQUAL   * incorrect session
.
          MOVE      ONE,FORM4
          PACK      KEY12,OBAOUTNO,FORM4
          CALL      RDOTXWP1
          BRANCH    OVRCD,DSES1000          * no extra details
.
. ******* increment counters *******
.
DSES2000  ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      DSES4000 IF NOT LESS    * need a new page
          ADD       ONE,CNTBOOK             * add to item counter
.
. ******* display data and store key *******
.
DSES3000  PACK      PATNAME,SP30,SP20
          MOVE      ONE,OVRCD
          MATCH     ZEROUR,OBAURNO
          IF        @EQUAL
            MATCH     ZEROVISN,OBAOUTNO
            IF        !@EQUAL
              MOVE      "Missing PMI details",PATNAME
              MOVE      OBAOUTNO,KEY8
              CALL      RDPRAM1
            ENDIF
          ELSE
            MOVE      "Missing PMI details",PATNAME
            MOVE      OBAURNO,KEY8
            CALL      RDMAST1
            CALL      RDPMPX21
          ENDIF
          IF        OVRCD = ZERO
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATNAME
          ENDIF
          DISPLAY   *P1:CVERT,*V2LON,CNTBOOK,*HOFF,".  ",OBASLOT,SP1:
                    OBATIME,SP1,OBAOUTNO,SP1,OBAURNO,SP1,PATNAME
          PACK      BOOKING[CNTBOOK],VSESS,OBASLOT,SP30
          GOTO      DSES1000
.
. ******* new screen needed *******
.
DSES4000  BRANCH    CPAGENO,DSES4500
.
.         Middle pages : (N)ext & (P)revious
.
DSES4100  MOVE      ONE,CALLPOSN            * set position
          CALL      KEYA0000
          BRANCH    EXIT,DSES7000,DSES8000,DSES9100
.                        Next     Previous Exit
          GOTO      DSES6000
.
.         first page : (N)ext
.
DSES4500  MOVE      TWO,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSES7000,DSES5900,DSES9100
.                        Next     Previous Exit
          GOTO      DSES6000
.
. ******* no more data to display *******
.
DSES5000  COMPARE   ZERO,CNTBOOK
          GOTO      DSES8900 IF EQUAL       * nothing on file
.
          BRANCH    CPAGENO,DSES5500
.
.         last page : (P)revious
.
DSES5100  MOVE      THREE,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSES5900,DSES8000,DSES9100
.                        Next     Previous Exit
          GOTO      DSES6000
.
.         first page :
.
DSES5500  MOVE      FOUR,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSES5900,DSES5900,DSES9100
.                        Next     Previous Exit
          GOTO      DSES6000
.
DSES5900  BEEP   
          BRANCH    CALLPOSN,DSES4100,DSES4500,DSES5100,DSES5500
.
. ******* item on screen selected *******
.
DSES6000  MOVE      BOOKING[FORM2],KEY28
          CALL      RDBOKA1
          BRANCH    OVRCD,DSES5900          * invalid details
.
          MATCH     ZEROUR,OBAURNO
          IF        @EQUAL
            MATCH     ZEROVISN,OBAOUTNO
            IF        !@EQUAL
              MOVE      "Missing PMI details",PATNAME
              MOVE      OBAOUTNO,KEY8
              CALL      RDPRAM1
            ENDIF
          ELSE
            MOVE      "Missing PMI details",PATNAME
            MOVE      OBAURNO,KEY8
            CALL      RDMAST1
          ENDIF
.
          MOVE      ONE,FORM4
          PACK      KEY12,OBAOUTNO,FORM4
          CALL      RDOTXWP1
          BRANCH    OVRCD,DSES5900          * invalid details
.
          MOVE      OBACLIN,PVIDOCT         * set doctor
          MOVE      OBADATE,PVIDATE         * set date
          MOVE      OBAOUTNO,PVIBILL        * set op number
          MOVE      OBASITE,PVISITE         * set site
.
          MOVE      ZERO,EXIT
          GOTO      DSES9999
.
. ******* set up for a new page *******
.
DSES7000  MOVE      BOOKING[14],KEY28
          CALL      RDBOKA1
          CALL      RDKBOKA1
          BRANCH    OVRCD,DSES5900          * no next screen
.
          CALL      CLRA0000                * clear storage variables
          ADD       ONE,CPAGENO             * add to page counter
          MOVE      ONE,CNTBOOK             * reset item counter
          MOVE      "9",CVERT               * set row
          DISPLAY   *P1:CVERT,*EF           * clear screen
          GOTO      DSES3000
.
. ******* set up for previous screen *******
.
DSES8000  MOVE      BOOKING[1],KEY28
          CALL      RDBOKA1
          MOVE      ZERO,FORM2
.
DSES8100  ADD       ONE,FORM2
          COMPARE   "15",FORM2
          GOTO      DSES8200 IF NOT LESS    * finished reads
.
DSES8150  CALL      RDPBOKA1
          BRANCH    OVRCD,DSES8200          * finished reads
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,VSESS
          GOTO      DSES8200 IF NOT EQUAL   * incorrect session
.
          MOVE      ONE,FORM4
          PACK      KEY12,OBAOUTNO,FORM4
          CALL      RDOTXWP1
          BRANCH    OVRCD,DSES8150          * no extra details
          GOTO      DSES8100
.
DSES8200  CALL      CLRA0000                * clear storage variables
          SUB       ONE,CPAGENO             * sub from page counter
          MOVE      ONE,CNTBOOK             * reset item counter
          MOVE      "9",CVERT               * set row
          DISPLAY   *P1:CVERT,*EF           * clear screen
          GOTO      DSES3000
.
DSES8900  DISPLAY   *P1:24,*B,"No Bookings found.  ",*EL;
          CALL      HITENTER
.
. ******* set exit flags *******
.
DSES9100  MOVE      ONE,EXIT                * exit selected
.
DSES9999  RETURN
+
.*********************************************************************
.                   PDET0000                    Called by : BYUR2000 
.         print the details
.*********************************************************************
PDET0000  MOVE      ONE,NOCOMM              * set as no comments
          PACK      KEY12,OBAOUTNO,SP4
          CALL      RSOTXWP1
.
PDET1000  CALL      RKOTXWP1
          BRANCH    OVRCD,PDET9000          * end of file
.
          MATCH     OBAOUTNO,OTXWOUTN
          GOTO      PDET9000 IF NOT EQUAL   * different patient
.
          IF        CLNO >= 55
            CALL      HEAD80
          ENDIF
          IF        NOCOMM = ONE
            PRINT     *1,"U/R Number   : ",PURNO,SP1,*26,"Name : ",PATHDNAM
            PRINT     *1,"Outpatient No: ",PVIBILL,*26,"Sex  : ",PATHDSEX:
                      *42,"Born : ",PATHDDOB,*61,"Age : ",PSAGE
            PRINT     *1,PATHDADM,*26,"Site : ",PVISITE,*42,"Clin : ",PATHDDOC
            PRINT     *N;
            CALL      UNLN0000
            ADD       THREE,CLNO
          ENDIF
. 
          PRINT     *3,"| ",OTXWDESC," |"
          ADD       ONE,CLNO
          MOVE      ZERO,NOCOMM             * set as having comments
          GOTO      PDET1000
.
PDET9000  BRANCH    NOCOMM,PDET9999         * no comments printed
          CALL      UNLN0000
          PRINT     *N,"*** No more comments for this Episode ***",*N
PDET9999  RETURN
+
.*********************************************************************
.                   DCOM0000                    Called by : xxxx0000 
.         display a screen of the comments to the user
.*********************************************************************
DCOM0000  BOX       16,5,8,76,22
          MOVE      ZERO,OVRCD
          MOVE      NINE,CVERT
          PACK      KEY12,OBAOUTNO,SP4
          CALL      RSOTXWP1
DCOM1000  CALL      RKOTXWP1
          BRANCH    OVRCD,DCOM9000
.
          MATCH     OBAOUTNO,OTXWOUTN
          GOTO      DCOM9000 IF NOT EQUAL
.
          IF        CVERT <= 21
            DISPLAY   *P6:CVERT,OTXWDESC
            ADD       ONE,CVERT
            GOTO      DCOM1000
          ENDIF
.
DCOM9000  MOVE      ZERO,CPAGENO
.
DCOM9999  RETURN
+
.*********************************************************************
.         print underlines
.*********************************************************************
UNLN0000  PRINT     *3,"*------------------------------------":
                       "------------------------------------*"
          ADD       ONE,CLNO
UNLN9999  RETURN
.
.*********************************************************************
.*                  KEYA0000                    Called by : DBOA0000 *
.*        Keyin response for line 24 question                        *
.*        Para's  : CALLPOSN      which line 24 prompt               *
.*        Returns : EXIT = 0      item selected (FORM2)              *
.*                  EXIT = 1      Next screen selected               *
.*                  EXIT = 2      Previous screen selected           *
.*                  EXIT = 3      Exit entered                       *
.*********************************************************************
KEYA0000  DISPLAY   *P1:24,*EL,"Select booking";
          MOVE      "14",CCOL
          IF        ((CALLPOSN = ONE) | (CALLPOSN = TWO))
            DISPLAY   ", (",*V2LON,"N",*HOFF,")ext";
            ADD       "8",CCOL
          ENDIF
          IF        ((CALLPOSN = ONE) | (CALLPOSN = THREE))
            DISPLAY   ", (",*V2LON,"P",*HOFF,")revious";
            ADD       "12",CCOL
          ENDIF
          DISPLAY   " ? ",*EL;
          ADD       FOUR,CCOL
.
KEYA1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      KEYA5000 IF EQUAL       * number entered
.
          RESET     DIM2
          GOTO      KEYA4300 IF EOS         * nothing entered
.
.         letter entered
.
          MATCH     SP1,DIM2
          GOTO      KEYA1500 IF NOT EQUAL   * invalid entry
.
          UNPACK    DIM2,ANS,ANS            * get required letter in ANS
          REP       UPPLOW,ANS              * get as uppercase
          REP       "N1P2E3",ANS            * turn valid letters into number
          MOVE      ZERO,EXIT               * clear exit flag
          MOVE      ANS,EXIT                * set exit flag
          BRANCH    EXIT,KEYA9999,KEYA9999,KEYA1500
.
KEYA1500  BEEP
          GOTO      KEYA1000
.
KEYA4300  MOVE      THREE,EXIT
          GOTO      KEYA9999
.
.         number entered
.
KEYA5000  MOVE      ZERO,EXIT               * set exit flag
          MOVE      DIM2,FORM2              * get as a number
.
          COMPARE   FORM2,ZERO
          GOTO      KEYA1500 IF NOT LESS    * negative entered
.
          COMPARE   FORM2,CNTBOOK
          GOTO      KEYA1500 IF LESS        * number too large
.
KEYA9999  RETURN
+
.*********************************************************************
.*                  CLRA0000                    Called by : DBOA0000 *
.*        Clear the storage variables                                *
.*********************************************************************
CLRA0000  MOVE      ONE,FORM2
          WHILE     FORM2 < 16 
            PACK      BOOKING[FORM2],SP20,SP20
            ADD       ONE,FORM2
          DO
CLRA9999  RETURN
+
REDISPS   DISPLAY   *P1:4,*EF,"U/R Number   : "
GETSVAR   RETURN
+
.
.         I/O includes needed
.
          INC       STD001IO/PBL
          INC       BOKRC1IO/INC            * booking file
          INC       CALCAGE
          INC       KYOUTCLI/PBL            * keyin clinic id
          INC       OUTDSCLN/PBL            * ? for clinics
          INC       OUTDSMAS/PBL            * ? for dates/times
          INC       OUTHEAD/PBL             * display Outpatient details
          INC       OUTBOAIO/INC            * booking A
          INC       OUTCLIIO/INC            * booking A
          INC       OUTCTYIO/INC            * clinic type file
          INC       OUTMA1IO/INC            * master file
          INC       OUTPREIO/INC            * OP pre-attendance file
          INC       OUTXWPIO/INC            * extra screen WP file
          INC       PATALERT/PBL            * alerts
          INC       PATALRIO/INC            * alerts file
          INC       PATCOMN1/PBL            * holds KURN
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctor file
          INC       PMSHCPIO/INC            * National HCP file
          INC       PATMA1IO/INC            * master file
          INC       PATMI1IO/INC            * admission file
          INC       PATNIDIO/INC            * National id file
          INC       PATPR1IO/INC            * pre-admission file
          INC       PATGSRIO/INC            * surname file
          INC       PATVISIO/INC            * visit file
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC            * visit extension file
          INC       PATSNDX/PBL             * ? for surnames only
          INC       PATSNX2/PBL             * ? for surnames/given names
          INC       PMIHEAD/PBL             * display PMI details
          INC       NZCOMPIO/INC
.
AGECHK
CLPATMAS  RETURN
..............................................................................

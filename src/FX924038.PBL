.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX924038.PBL                                              *
.* Desc      :   Write PRS2 patpeiaf records for selected discharges       *
.***************************************************************************
.* Date      :   14/09/2022                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will write patpeiaf records for discharges   *
.*           :   in the selected date range. 01/07/2022 until today        *
.* Modifications  :                                                        *
.*        V11.02.01 19/09/2022  Ebon Clements   TSK 0924038                *
.*                  Added print of discharges processed                    *
.*        V11.02.00 14/09/2022  Ebon Clements   TSK 0924038                *
.*                  Created program based on MRTWEB02                      *
.***************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PATPEIFD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CURRDATE  DIM       8
ENDDDATE  DIM       8
HDPEQUIV  DIM       2
RECCOUNT  FORM      8
PRTODATE  DIM       10       * Display date from
PRFRDATE  DIM       10       * Display date to
PRNTDATE  DIM       10       * Print Date
STRTDATE  DIM       8
.
PRGID     INIT      "FX924039"
PRGNAM    INIT      "Write patpeiaf PRS2 records for discharges"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      DATE0000               * Keyin the date range
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patpeiaf";
          OPEN      PATPEIO1,"patpeiaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000    MOVE       FALSE,EXIT
            DISPLAY    *P1:10,*EF,"Discharge Date From :":
                           *P1:11,"Discharge Date To   :";
.
            MOVE      "10",CVERT
            MOVE      "23",CCOL
            CALL      IBACLOCK
            PACK      CURRDATE,CCC,CYY,CMM,CDD
            REP       " 0",CURRDATE
            MOVE      CCC,CCENT
            MOVE      CYY,CYEAR
            MOVE      CMM,CMON
            MOVE      CDD,CDAY

            CALL      KEYDATE                          * enter the from date
            PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",STRTDATE                    * from date
.
            MATCH     "20220701",STRTDATE
            IF        @LESS
              DISPLAY    *P1:24,*EL,*B,"Fix can't be run before 1 July 2022":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MOVE       "11",CVERT
            MOVE       "23",CCOL
            CALL      KEYDATE                          * enter the from date
            PACK      ENDDDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",ENDDDATE                    * from date
.
            MATCH     ENDDDATE,CURRDATE
            IF        @LESS
              DISPLAY    *P1:24,*EL,*B,"Fix can't be run for a future date":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MATCH      STRTDATE,ENDDDATE
            GOTO       DATE9999 IF NOT LESS
.
            DISPLAY    *P1:24,*EL,*B,"2nd Range Must Be Greater Than 1st ":
                       "Range - Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       DATE0000
.
DATE9999    RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      HEAD0000                * Print heading
.
          MOVE      ZERO,RECCOUNT           * Number of discharged checked
.
          PACK      KEY16,STRTDATE,SP70 
          CALL      RDSDSCH2
PROC1000  CALL      RDKDSCH2                * Discharge from date
          BRANCH    OVRCD,PROC9000
.
          MATCH     DDATE,ENDDDATE          * Exit aftre discharge to date
          GOTO      PROC9000 IF LESS
.
          PACK      KEY8,DADMNO,SP70
          CALL      RDMISS1                 * Valid admission
          BRANCH    OVRCD,PROC1000
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * need a new page
.
          CALL      PRNT0000                * Print line
          ADD       ONE,RECCOUNT            * Number of discharged checked
.
          MOVE      "12",HDPEQUIV
          CALL      WPEI0000                * Write patpeiaf record
.
          GOTO      PROC1000
.
PROC9000  CALL      UND80
.
          PRINT     *1,"| Total Discharges Processed :",RECCOUNT,*80,"|"
           
          CALL      UND80
          PRINT     *N,*N,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Discharges Processed : ",RECCOUNT,SP10;
          CALL      HITENTER
.
PROC9999  RETURN
.
.******************************************************************************
.*                                 WPEI0000                                   *
.*                        Write A Record To patpeiaf                          *
.******************************************************************************
WPEI0000  READ      CONTROLF,EIGHTY8;*116,CHCSDTE,*124,CHCSST,*132,CHCSED
.
          COMPARE   THREE,PTCNHDPS
          GOTO      WPEI9999 IF NOT EQUAL   * Not in Victoria
.
          COMPARE   THREE,ASTAT
          GOTO      WPEI9000 IF NOT EQUAL   * Not discharged
.
          MATCH     CHCSDTE,DDATE
          GOTO      WPEI9000 IF LESS        * Discharge date < start HDP date
.
          MATCH     CHCSST,ADATE
          GOTO      WPEI9000 IF NOT LESS    * Adm date >= PRS2 extract start dte
.
          PACK      KEY16,SP8,AADMNO,TWO
          CALL      RDPTPEI1                * Read a PRS2 inc/exc file record
          COMPARE   ONE,OVRCD
          GOTO      WPEI9000 IF NOT EQUAL   * Record exists, dont write
.
          PACK      KEY16,CHCSST,AADMNO,TWO
          CALL      RDPTPEI1                * Read a PRS2 inc/exc file record
          COMPARE   ONE,OVRCD
          GOTO      WPEI9000 IF NOT EQUAL   * Record exists, dont write
.
          MOVE      SP8,PTPESTDT
          MOVE      AADMNO,PTPEADMN
          MOVE      TWO,PTPESTAT
          CALL      IBACLOCK                * Get the current date
          PACK      PTPEDATE,CCC,CYY,CMM,CDD
          REP       " 0",PTPEDATE
          MOVE      SP70,PTPEOPER
.
          MOVE      SP3,PTPERESN
          PACK      KEY5,ANSR,ANSN,SP3
          CALL      RDSCODE1                * Position on & read a codes file
WPEI1000  CALL      RDKCODE1                  record
          BRANCH    OVRCD,WPEI2000
.
          MATCH     "RN",TCODE
          GOTO      WPEI2000 IF NOT EQUAL   * Different category
.
          MATCH     HDPEQUIV,THCSCOD
          GOTO      WPEI1000 IF NOT EQUAL   * Different HDP equivalent
.
          MOVE      ACODE,PTPERESN
.
WPEI2000  MOVE      PASSCODE,PTPEOPER
          PACK      KEY16,PTPESTDT,PTPEADMN,SP70
          CALL      WRPTPEI1                * Write a PRS2 inc/exc file record
.
WPEI9000  MOVE      ZERO,EXIT
WPEI9999  RETURN
+
.*********************************************************************
. Print heading            
.*********************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD80
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE         * formatted start date
.
          UNPACK    ENDDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted start date

          PRINT     *20,"From : ",PRFRDATE," to ",PRTODATE
          ADD       ONE,CLNO
.
          CALL      UND80
.
          PRINT     *1,"| Disc. Date ",*14,"| Visit ":
                    *25,"| U/R",*80,"|"
.
          ADD       ONE,CLNO
.
          CALL      UND80
.
HEAD9999  RETURN
+
.*********************************************************************
. Print line
.*********************************************************************
PRNT0000  UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRNTDATE         * formatted discharge date
.
          PRINT     *1,"| ",PRNTDATE,*14,"| ",DADMNO:
                    *25,"| ",DURNO,*80,"|"
PRNT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PATPEIIO/INC
          INC       PMSVX1IO/INC

.------------------------------------------------------------------------------
.* Program        : IBAPMS63.PBL                                              
.* Name           : QuickSnap Extract and Import                       
.------------------------------------------------------------------------------
.* Modifications  :                                
.------------------------------------------------------------------------------
. V12.00.01 16/05/2025  J.Tan          TSK 0955096
.           Added alpanumeric visit number generation
. V11.04.04 04/04/2024  J.Tan          TSK 0945062
.           Removed checking Coding date (PTEDDTCD) against Functional
.           Assessment date Discharge (PTRDFDDT)
. V11.04.03 08/03/2024  J.Tan          TSK 0931613
.           Mod Upload AN-SNAP for event which are NOT Palliative Care visits
. V11.04.02 15/01/2024  J.Tan          TSK 0931613
.           Fixed Checking date format (VDAT0000)
. V11.04.01 10/01/2024  J.Tan          TSK 0931613
.           Mod Upload to check for valid Sub Acute Palliative care
. V11.03.05 16/11/2023  J.Tan          TSK 0931613
.           Added validation for AN-SNAP Classification code
. V11.03.04 10/10/2023  Thanh T        TSK 0931613
.           Fixed issues with Mod for ABF Sub-Acute Palliative phase of Care 
. V11.03.03 28/09/2023  J.Tan          TSK 0931613
.           Mod for ABF Sub-Acute Palliative phase of Care
. V11.03.02 22/11/2022  Thanh T        TSK 0906597
.           Added checking for All hospital option
. V11.03.01 21/11/2022  Thanh T        TSK 0906597
.           Fixed issue with snap code not update correctly
. V11.02.00 06/10/2022  Thanh T        TSK 0906597
.           Initial version
.------------------------------------------------------------------------------
.
          INC       STD001FD/PBL
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       PATCONFD/INC
          INC       PATCODFD/INC 
          INC       PATHSPFD/INC
          INC       PATDSCFD/INC            * Discharge File
          INC       PATECDFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATMI1FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATRDEFD/INC
          INC       PMSSNPFD/INC
          INC       PMSFOCFD/INC
          INC       PATCHCFD/INC
          INC       PATABFFD/INC
          INC       PATFIMFD/INC
          INC       PATTRNFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. ------- program variables -------
.
NOFREAD   FORM      6
NOFEXTR   FORM      6
.
CMDLINE   DIM       80
CHCCOUNT  FORM      3
.
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
DIM6      DIM       6
.
ERRFLAG   FORM      1
ERRCOUNT  FORM      8
ERORDESC  DIM       60
ERRORMSG  DIM       45
FILENAME  DIM       8
FNAMEI    DIM       500
FROMDATE  DIM       10
TODATE    DIM       10
HOSPCODE  DIM       3
IBCNMHOS  FORM      1
PREVADMN  DIM       8
SAVADMNO  DIM       8
STRTDATE  DIM       8
ENDDATE   DIM       8 
USERID    DIM       10
WRKDTE8   DIM       8
WRKDTE10  DIM       10
PRDCCLSS  DIM       3
.
CCUDF6    DIM       1
CCASC6    DIM       6
.
FILENO    DIM       2
.
BEDTYPE   DIM       3
CDYSYEAR  FORM      2
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
SVADDATE  DIM       8
NUMLEV    FORM      2
.
LINECNTR  FORM      8
OUTFNAME  DIM       8
PATHNAME  DIM       150
UPDCOUNT  FORM      8
.
WORKHITH  FORM      5
WORKICLS  FORM      4
WORKLDAY  FORM      5
WORKPSYC  FORM      5
WORKQUAL  FORM      5
ADMPFLAG  FORM      1
ADMTYPE   DIM       3
DEPTYPE   DIM       3
PSYCFLAG  FORM      1
EPSDSDAT  DIM       8
EPSDEDAT  DIM       8
EPSCCLSS  DIM       3
PHSESDAT  DIM       8
PHSEEDAT  DIM       8
PHCARE    FORM      1
.
PMFOCFLD  DIM       5
FRTPHASE  FORM      1
.
HYPHEN    INIT      "-"
ZED30     INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
XFLD0006  DIM       30
XFLD0007  DIM       30
XFLD0008  DIM       30
XFLD0009  DIM       30
XFLD0010  DIM       30
XFLD0011  DIM       30
XFLD0012  DIM       30
XFLD0013  DIM       30
XFLD0014  DIM       30
XFLD0015  DIM       30
XFLD0016  DIM       30
XFLD0017  DIM       30
XFLD0018  DIM       30
XFLD0019  DIM       30
XFLD0020  DIM       30
XFLD0021  DIM       30
XFLD0022  DIM       30
XFLD0023  DIM       30
XFLD0024  DIM       30
XFLD0025  DIM       30
XFLD0026  DIM       30
XFLD0027  DIM       30
XFLD0028  DIM       30
XFLD0029  DIM       30
XFLD0030  DIM       30
XFLD0031  DIM       30
XFLD0032  DIM       30
XFLD0033  DIM       30
XFLD0034  DIM       30
XFLD0035  DIM       30
XFLD0036  DIM       30
XFLD0037  DIM       30
XFLD0038  DIM       30
XFLD0039  DIM       30
XFLD0040  DIM       30
XFLD0041  DIM       30
XFLD0042  DIM       30
.
XADMNO    DIM       8
XEPISODE  DIM       2
XFLDVER1  DIM       1
XFLDVERS  DIM       2
XFLDCODE  DIM       3
.
. Sub-Acute Inpatient events Extract File
.
EXTSUBAF  FILE      ASCII, FIXED=142      
.
. Field   Type    Size        Description
. -----   ----    ----        ----------------
XRECIND   DIM       11   1    Record Identifier
XCASTYPE  DIM       6    12   Case Type
XEPSTYPE  DIM       1    18   Episode Type
XASSONLY  DIM       1    19   Assessment Only
XPHSTYPE  DIM       4    20   Phase Type
XFSTEPIS  DIM       1    24   First phase
XPSTRDTE  DIM      10    25   Palliactive Care Phase Start Date
XPENDDTE  DIM      10    35   Palliactive Care Phase End Date
XIMPCODE  DIM       7    45   Impairment Code
XDOB      DIM      10    52   Date Of Birth
XEADMDTE  DIM      10    62   Episode Admission Date
XEDSCDTE  DIM      10    72   Episode Discharge Date
XLEVDAYS  DIM       5    82   Leave Days
XRUGDAIL  DIM       2    87   RUG-ADL Total
XPCPSS    DIM       1    89   PCPSS Total
XFIMEAT   FORM      2    90   FIM Eating
XFIMGRM   FORM      2    92   FIM Grooming
XFIMBTH   FORM      2    94   FIM Bathing
XFIMUPB   FORM      2    96   FIM Dressing Upper Body
XFIMLWB   FORM      2    98   FIM Dressing Lower Body
XFIMTLT   FORM      2    100  FIM Toileting
XFIMBLM   FORM      2    102  FIM Bladder Management
XFIMBWM   FORM      2    104  FIM Bowel Management
XFIMTBD   FORM      2    106  FIM Transfer to Bed/Chair/Wheelchair
XFIMTTL   FORM      2    108  FIM Transfer to Toilet
XFIMTBT   FORM      2    110  FIM Transfer to Bath/Shower
XFIMLOC   FORM      2    112  FIM Locomotion
XFIMSTR   FORM      2    114  FIM Stairs
XFIMCOM   FORM      2    116  FIM Comprehension
XFIMEXP   FORM      2    118  FIM Expression
XFIMSCI   FORM      2    120  FIM Social Interaction
XFIMPRB   FORM      2    122  FIM Problem Solving
XFIMMEM   FORM      2    124  FIM Memory
XHONOVR   DIM       1    126  HoNOS Overactive
XHONPRB   DIM       1    127  HoNOS ADL
XHONTOT   DIM       1    128  HoNOS Total
XFOCCARE  DIM      10    129  Focus of Care
XREHPRGT  DIM       1    139  Rehab Program Type
XDELDEM   DIM       1    140  Delirium or Dementia
XGEMCLIN  DIM       1    141  GEM Clinic Type
XAAGETYP  DIM       1    142  Age Type
.
.EOR                     143
.
. Sub-Acute Inpatient events Extract File
.
EXTSUBAI  FILE      HL7, FIXED=148
.
. Field   Type    Size        Description
. -----   ----    ----        ----------------
.XRECIND   DIM       11   1    Record Identifier
.XCASTYPE  DIM       6    12   Case Type
.XEPSTYPE  DIM       1    18   Episode Type
.XASSONLY  DIM       1    19   Assessment Only
.XPHSTYPE  DIM       4    20   Phase Type
.XFSTEPIS  DIM       1    24   First phase
.XPSTRDTE  DIM      10    25   Palliactive Care Phase Start Date
.XPENDDTE  DIM      10    35   Palliactive Care Phase End Date
.XIMPCODE  DIM       7    45   Impairment Code
.XDOB      DIM      10    52   Date Of Birth
.XEADMDTE  DIM      10    62   Episode Admission Date
.XEDSCDTE  DIM      10    72   Episode Discharge Date
.XLEVDAYS  DIM       5    82   Leave Days
.XRUGDAIL  DIM       2    87   RUG-ADL Total
.XPCPSS    DIM       1    89   PCPSS Total
.XFIMEAT   FORM      2    90   FIM Eating
.XFIMGRM   FORM      2    92   FIM Grooming
.XFIMBTH   FORM      2    94   FIM Bathing
.XFIMUPB   FORM      2    96   FIM Dressing Upper Body
.XFIMLWB   FORM      2    98   FIM Dressing Lower Body
.XFIMTLT   FORM      2    100  FIM Toileting
.XFIMBLM   FORM      2    102  FIM Bladder Management
.XFIMBWM   FORM      2    104  FIM Bowel Management
.XFIMTBD   FORM      2    106  FIM Transfer to Bed/Chair/Wheelchair
.XFIMTTL   FORM      2    108  FIM Transfer to Toilet
.XFIMTBT   FORM      2    110  FIM Transfer to Bath/Shower
.XFIMLOC   FORM      2    112  FIM Locomotion
.XFIMSTR   FORM      2    114  FIM Stairs
.XFIMCOM   FORM      2    116  FIM Comprehension
.XFIMEXP   FORM      2    118  FIM E.Xpression
.XFIMSCI   FORM      2    120  FIM Social Interaction
.XFIMPRB   FORM      2    122  FIM Problem Solving
.XFIMMEM   FORM      2    124  FIM Memory
.XHONOVR   DIM       1    126  HoNOS Overactive
.XHONPRB   DIM       1    127  HoNOS ADL
.XHONTOT   DIM       1    128  HoNOS Total
.XFOCCARE  DIM      10    129  Focus of Care
.XREHPRGT  DIM       1    139  Rehab Program Type
.XDELDEM   DIM       1    140  Delirium or Dementia
.XGEMCLIN  DIM       1    141  GEM Clinic Type
.XAAGETYP  DIM       1    142  Age Type
.XANSNAP   DIM       6    143  AN-SNAP Clssification
.
.EOR                     149
.
.--------------------------------------------------------------------------
PRGID     INIT      "IBAPMS63"
PRGNAM    INIT      "QuickSnap Extract and Import"
VERSION   INIT      "V12.01.00"
.--------------------------------------------------------------------------
.         mainline code
.--------------------------------------------------------------------------
ML0000    CALL      INIT0000                * initilisation
.
ML0500    CALL      OPTN0000                * Get Report Option 
          BRANCH    OPTION,ML1000,ML2000
.
          GOTO      ML9999
.
ML1000    CALL      DTRNG000                * get date range
          BRANCH    EXIT,ML0500
          CALL      CHKMH000
          BRANCH    EXIT,ML0500,ML0500      * check multi hospital
.
ML1100    CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML1200,ML1100,ML0500
.                         Yes    No     Cancel
.
ML1200    CALL      NDIS0000                * extract by Discharge date
          CALL      MVEXC000 
.
          GOTO      ML9999 
.
ML2000    CALL      KASC0000                * Keyin file 
          BRANCH    EXIT,ML0500
.
ML2100    CALL      SCRD0000                * Display screen
.
          CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000                * Keyin pathname
          BRANCH    EXIT,ML0000
.
ML2200    CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML2300,ML2200,ML0500
.                         Yes    No     Cancel
.
ML2300    CALL      UPLOD000 
          GOTO      ML9999
.
ML9999    MOVE      ZERO,EXIT
          CHAIN     PGM
.
.--------------------------------------------------------------------------
. initilisation
.--------------------------------------------------------------------------
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"          * files to open
.
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P58:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PATMI1A4,"patmi1af"
.
          DISPLAY   *P58:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P58:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P58:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patfimaf";
          OPEN      PATFIMA1,"patfimaf"
.
          DISPLAY   *P64:24,"patrdeaf";
          OPEN      PATRDEA1,"patrdeaf"
.
          DISPLAY   *P64:24,"pmssnpaf";
          OPEN      PMSSNPA1,"pmssnpaf"
.
          DISPLAY   *P64:24,"pmsfocaf";
          OPEN      PMSFOCA1,"pmsfocaf"
.
          DISPLAY   *P64:24,"patchcof";
          OPEN      PATCHCO2,"patchcof"
.
          DISPLAY   *P64:24,"patabfaf";
          OPEN      PATABFA1,"patabfaf"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA1,"patecdaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.         
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          READ      CONTROLF,TEN;*37,CMOPRT,*54,CFILENO
.
          MOVE      CFILENO,DIM6            * should be 2-digit num.  230673
          SQUEEZE   DIM6
          MOVE      DIM6,FILENO
.
          CLOCK     TIME,CTIMEIS
.
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,NOFREAD
          MOVE      ZERO,NOFEXTR
.
INIT9999  RETURN
.
.--------------------------------------------------------------------------
. Main options  
.--------------------------------------------------------------------------
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Extract Sub-Acute Inpatient":
                                            " events":
                    *P1:6,*V2LON," 2",*HOFF," = Upload AN-SNAP Classification"
OPTN1000  KEYIN     *P1:8," Select Option : ",*V2LON,*P18:8,OPTION;
.
          BRANCH    OPTION,OPTN9999,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
.
.--------------------------------------------------------------------------
. Upload file
.--------------------------------------------------------------------------
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P1:10,*EF,"AN-SNAP Classification Load File";
.
          KEYIN     *P1:11,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:11,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EXTSUBAI,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      KOUT0000                * Keyin output file
          BRANCH    EXIT,KASC9000
.
          CALL      EXCUS000
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
.
.--------------------------------------------------------------------------
. Keyin output file
.--------------------------------------------------------------------------
KOUT0000  DISPLAY   *P1:12,"Keyin Output file      :";
.
          KEYIN     *P26:12,*EL,*V2LON,OUTFNAME;
          PACK      OUTFNAME,OUTFNAME,SP70
          MATCH     SP70,OUTFNAME
          GOTO      KOUT9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KOUT9999
.
KOUT9500  MOVE      ONE,EXIT
KOUT9999  RETURN
.
.--------------------------------------------------------------------------
. Screen display
.--------------------------------------------------------------------------
SCRD0000  DISPLAY   *P1:13," 1. Keyin User id      :":
                    *P1:14," 2. Keyin Path name    :";
SCRD9999  RETURN
.
.--------------------------------------------------------------------------
. Keyin user id
.--------------------------------------------------------------------------
KUSR0000  KEYIN     *P26:13,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.--------------------------------------------------------------------------
. Keyin path name  
.--------------------------------------------------------------------------
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:14,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.--------------------------------------------------------------------------
. Select Field  
.--------------------------------------------------------------------------
SFLD0000  CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SFLD9999 IF EQUAL
          GOTO      SFLD1000 IF LESS
.
          MOVE      ZERO,EXIT
.
          PERFORM   CCITEM,KUSR0000:             * Keyin user id
                           KPTH0000              * Keyin pathname
          BRANCH    EXIT,SFLD9999
          GOTO      SFLD0000
.
SFLD1000  MOVE      ONE,EXIT
SFLD9999  RETURN
.
.--------------------------------------------------------------------------
. Get the from and to dates.
.--------------------------------------------------------------------------
DTRNG000  MOVE      ZERO,EXIT
          DISPLAY   *P1:10,*EF,*P2:10,"From Date : ":
                    *P2:11,"To Date : ";
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
DTRNG100  MOVE      TEN4,CCOL
          MOVE      TEN,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,DTRNG100
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE
          PACK      FROMDATE,CPCDATE
          REP       " 0",FROMDATE
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
DTRNG200  MOVE      TEN4,CCOL
          MOVE      TEN1,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,DTRNG200
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE
          PACK      TODATE,CPCDATE
          REP       " 0",TODATE
.
.                                           * set up extract file name 
          UNPACK    STRTDATE,DIM4,KEY4
          PACK      FILENAME,KEY4,CMON,CDAY,SP10
          REP       " 0",FILENAME
.
.          DISPLAY   *P1:12,"This Extract will have a file name of '":
.                    *V2LON,FILENAME,".txt'",*HOFF
.
DTRNG999  RETURN
+
.--------------------------------------------------------------------------
. Process extracting
.--------------------------------------------------------------------------
NDIS0000  MOVE      ZERO,EXIT
.
          CALL      FILE0000 
          BRANCH    EXIT,NDIS9999 
.
          CLOSE     EXTSUBAF
          PREP      EXTSUBAF,FILENAME
.
          CALL      WRITA000                * Write column heading  
.
          MOVE      SP8,PREVADMN
          PACK      KEY16,STRTDATE,SP10
          CALL      RDSDSCH2
NDIS1000  CALL      RDKDSCH2                * Loop over discharges
          BRANCH    OVRCD,NDIS8000
.
          MATCH     DDATE,ENDDATE           * In date range
          GOTO      NDIS8000 IF LESS
.
          PACK      KEY8,DADMNO,SP70   
          MATCH     PREVADMN,KEY8    
          GOTO      NDIS1000 IF EQUAL
          MOVE      KEY8,PREVADMN
.
          PACK      KEY8,DADMNO,SP10
          CALL      RDMISS1                 * read admission details
          BRANCH    OVRCD,NDIS1000
          CALL      RDVISA1
          BRANCH    OVRCD,NDIS1000
.
          PACK      KEY8,AURNO,SP10
          CALL      RDMAST1                 * read PMI Details
          BRANCH    OVRCD,NDIS1000
.
          CALL      CLPMSPX2
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
.
          PACK      KEY8,DADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,NDIS1000
          IF        IBCNMHOS=1
            MATCH     SP70,HOSPCODE
            IF        !@EQUAL
              MATCH     HOSPCODE,PMVXMHOS
              GOTO      NDIS1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     SP70,ACARE
          GOTO      NDIS1000 IF EQUAL
.
          MOVE      ZERO,PHCARE
          MOVE      ACARE,KEY3
          CALL      CCARE0000             * Check care class
.
          MATCH     "X",DIM1
          GOTO      NDIS1000 IF NOT EQUAL
.
          MATCH     "3",CCUDF6
          IF        @EQUAL
            MATCH     "8",THCSCOD
            IF        @EQUAL
               MOVE     ONE,PHCARE           *  Palliative phase of care
            ENDIF
          ENDIF
.
          MOVE      ADATE,EPSDSDAT           * Episode Start date
          MOVE      DDATE,EPSDEDAT           * Episode End date
.
          MOVE      ADATE,PHSESDAT           * Phase Start date
          MOVE      DDATE,PHSEEDAT           * Phase End date
.
          MOVE      ZERO,CHCCOUNT
          MOVE      ONE,FRTPHASE             * First phase in episode
          MOVE      ACARE,PRDCCLSS
.
          MOVE      SP70,PMFOCFLD
          PACK      KEY10,DDADMNO,SP70
          CALL      RSPTRDE1 
NDIS7000  CALL      RKPTRDE1
          BRANCH    OVRCD,NDIS1000
.
          MATCH     DDADMNO,DPTRDADM
          GOTO      NDIS1000 IF NOT EQUAL 
.
          COMPARE   ONE,PHCARE
          GOTO      NDIS7600 IF NOT EQUAL    * Not a palliative care
.
          MOVE      ONE,FRTPHASE             * First phase in episode
          CALL      NEPS0000                 * get End of episode care date
.
.         Extract each Phase of Palliative Care from pmsfocaf file
.
          MOVE      DDATE,PHSEEDAT           * Phase End date
          PACK      KEY18,DPTRDADM,DPTRDEPN,SP70
          CALL      RSPMFOC1
          CALL      RKPMFOC1
          BRANCH    OVRCD,NDIS7600
.
          MATCH     DPTRDADM,PMFOVISN
          GOTO      NDIS7600 IF NOT EQUAL    * different admission
          MATCH     DPTRDEPN,PMFOEPNO
          GOTO      NDIS7600 IF NOT EQUAL    * different episode
.
          MOVE      PMFOPCDT,PHSEEDAT        * Phase of care end date
          PACK      PMFOCFLD,PMFOPHOC,PMFORAPC  * save phase care changes
.
NDIS7600  CALL      INITA000
.
          MOVE      PRDCCLSS,KEY3
          CALL      CCARE0000             * Check care class
.
          CALL      EXTRC000
          CALL      WRIT0000   
.
          COMPARE   ONE,PHCARE
          GOTO      NDIS1000 IF NOT EQUAL    * Not palliative care
.
          MATCH     SP70,PMFOCFLD
          GOTO      NDIS7000 IF EQUAL        * No phase of care record
.
          MOVE      ZERO,FRTPHASE            * NOT a first phase in episode
          UNPACK    PMFOCFLD,PTRDPOCA,PTRDRGAA
          MOVE      SP70,PMFOCFLD
.
          MOVE      PHSEEDAT,PHSESDAT
.
          CALL      RKPMFOC1
          BRANCH    OVRCD,NDIS7800           * no more phase of care change
.
          MATCH     DPTRDADM,PMFOVISN
          GOTO      NDIS7800 IF NOT EQUAL    * different admission
          MATCH     DPTRDEPN,PMFOEPNO
          GOTO      NDIS7800 IF NOT EQUAL
.
          MATCH     PMFOPCDT,DDATE
          GOTO      NDIS7800 IF EQUAL        * changes on Discharge date
.
          MOVE      PMFOPCDT,PHSEEDAT        * Phase of care end date
.
          PACK      PMFOCFLD,PMFOPHOC,PMFORAPC * Phase of Care & RUG
          GOTO      NDIS7600
.
.         Use the Episode End date as the Phase end date
.
NDIS7800  MOVE      EPSDEDAT,PHSEEDAT        * Phase end date
          GOTO      NDIS7600
.
NDIS8000  CLOSE     EXTSUBAF
.
NDIS9999  RETURN
+
.--------------------------------------------------------------------------
. Check Care class 
.--------------------------------------------------------------------------
CCARE0000 MOVE      SP70,CCUDF6  
          MOVE      SP70,CCASC6  
          PACK      KEY5,ANSC,ANSC,KEY3,SP70 
          CALL      RDCODE1                
          IF        OVRCD=1
            MOVE      SP70,PTCDUDF6
            MOVE      SP70,PTCDASC6
            MOVE      SP70,THCSCOD
          ENDIF
          MOVE      PTCDUDF6,CCUDF6 
          MOVE      PTCDASC6,CCASC6
.
          MOVE      CCUDF6,DIM1              * only 2,3,4,5,6  are valid
          REP       "X 2X3X4X5X6X",DIM1 
CCARE9999 RETURN
+
.--------------------------------------------------------------------------
. Get end of episode from Change care class file
.--------------------------------------------------------------------------
NEPS0000  COMPARE   ZERO,CHCCOUNT
          GOTO      NEPS1000 IF NOT EQUAL  * get next care class change
.
          PACK      KEY26,DPTRDADM,DPTRDEPN,SP70
          CALL      RDSCHCO2
NEPS1000  CALL      RDKCHCO2
          BRANCH    OVRCD,NEPS8000
.
          MATCH     DDADMNO,DCHADMN
          GOTO      NEPS8000 IF NOT EQUAL
.
          MATCH     DPTRDEPN,CHEPISNO     * Check episode number
          GOTO      NEPS2000 IF NOT EQUAL
.
          MATCH     "CC",CHCATG           * care class change ?
          GOTO      NEPS1000 IF NOT EQUAL
.
          PACK      KEY5,CHCATG,CHCODE
          CALL      RDCODE1
          BRANCH    OVRCD,NEPS1000
.
          MOVE      ZERO,FORM1
          SQUEEZE   PTCDUDF6
          MOVE      PTCDUDF6,FORM1
          COMPARE   THREE,FORM1
          GOTO      NEPS1000 IF NOT EQUAL     * NOT Palliative care
.
          MOVE      SP70,KEY2
          UNPACK    THCSCOD,KEY2
          MATCH     "8",KEY2
          GOTO      NEPS1000 IF NOT EQUAL    * NOT Palliative phase of Care
.
          MOVE      CHCODE,PRDCCLSS
          MOVE      CHDATE,EPSDEDAT       * set End date of episode care
          ADD       ONE,CHCCOUNT
          GOTO      NEPS9999
.
NEPS2000  MOVE      CHDATE,EPSDSDAT       * start of new episode
          MOVE      CHDATE,PHSESDAT       * start of new phase
          GOTO      NEPS1000
.
NEPS8000  MOVE      DDATE,EPSDEDAT        * use discharged date
          MOVE      ACARE,PRDCCLSS
.
NEPS9999  RETURN
+
.--------------------------------------------------------------------------
. Write extract record heading
.--------------------------------------------------------------------------
WRITA000  WRITE     EXTSUBAF,SEQ;"RecordIdentifier",COMMA:                * 1
                                 "CaseType",COMMA:                        * 2
                                 "EpisodeType",COMMA:                     * 3
                                 "AssessmentOnly",COMMA:                  * 4
                                 "PhaseType",COMMA:                       * 5
                                 "Flag_FirstPhaseInEpisode",COMMA:        * 6
                                 "PhaseDateStart",COMMA:                  * 7
                                 "PhaseDateEnd",COMMA:                    * 8
                                 "ImpairmentCode",COMMA:                  * 9
                                 "DateOfBirth",COMMA:                     * 10
                                 "StartDate",COMMA:                       * 11
                                 "EndDate",COMMA:                         * 12
                                 "LeaveDays",COMMA:                       * 13
                                 "RUG_ADL",COMMA:                         * 14
                                 "PCPSS",COMMA:                           * 15
                                 "FIM_01",COMMA:                          * 16
                                 "FIM_02",COMMA:                          * 17
                                 "FIM_03",COMMA:                          * 18
                                 "FIM_04",COMMA:                          * 19
                                 "FIM_05",COMMA:                          * 20
                                 "FIM_06",COMMA:                          * 21
                                 "FIM_07",COMMA:                          * 22
                                 "FIM_08",COMMA:                          * 23
                                 "FIM_09",COMMA:                          * 24
                                 "FIM_10",COMMA:                          * 25
                                 "FIM_11",COMMA:                          * 26
                                 "FIM_12",COMMA:                          * 27
                                 "FIM_13",COMMA:                          * 28
                                 "FIM_14",COMMA:                          * 29 
                                 "FIM_15",COMMA:                          * 30
                                 "FIM_16",COMMA:                          * 31
                                 "FIM_17",COMMA:                          * 32
                                 "FIM_18",COMMA:                          * 33
                                 "HoNOS_Overactive",COMMA:                * 34
                                 "HoNOS_ADL",COMMA:                       * 35
                                 "HoNOS_Total",COMMA:                     * 36
                                 "FocusOfCare",COMMA:                     * 37
                                 "RehabProgramType",COMMA:                * 38
                                 "Flag_DeliriumOrDementia",COMMA:         * 39
                                 "GemClinicType",COMMA:                   * 40
                                 "AgeType",COMMA                          * 41
.
WRITA999  RETURN
.
.--------------------------------------------------------------------------
. Write extract record
.--------------------------------------------------------------------------
WRIT0000  WRITE     EXTSUBAF,SEQ;XRECIND,COMMA,XCASTYPE,COMMA:           * 2
                                 XEPSTYPE,COMMA,XASSONLY,COMMA:          * 4
                                 XPHSTYPE,COMMA,XFSTEPIS,COMMA:          * 6
                                 XPSTRDTE,COMMA,XPENDDTE,COMMA:          * 8
                                 XIMPCODE,COMMA,XDOB,COMMA:              * 10
                                 XEADMDTE,COMMA,XEDSCDTE,COMMA:          * 12
                                 XLEVDAYS,COMMA,XRUGDAIL,COMMA:          * 14
                                 XPCPSS,COMMA:                           * 15
                                 XFIMEAT,COMMA,XFIMGRM,COMMA:            * 17
                                 XFIMBTH,COMMA,XFIMUPB,COMMA:            * 19
                                 XFIMLWB,COMMA,XFIMTLT,COMMA:            * 21
                                 XFIMBLM,COMMA,XFIMBWM,COMMA:            * 23
                                 XFIMTBD,COMMA,XFIMTTL,COMMA:            * 25
                                 XFIMTBT,COMMA,XFIMLOC,COMMA:            * 27
                                 XFIMSTR,COMMA,XFIMCOM,COMMA:            * 29
                                 XFIMEXP,COMMA,XFIMSCI,COMMA:            * 31
                                 XFIMPRB,COMMA,XFIMMEM,COMMA:            * 33
                                 XHONOVR,COMMA,XHONPRB,COMMA:            * 35
                                 XHONTOT,COMMA,XFOCCARE,COMMA:           * 37
                                 XREHPRGT,COMMA,XDELDEM,COMMA:           * 39
                                 XGEMCLIN,COMMA,XAAGETYP,COMMA           * 41
.
WRIT9999  RETURN
.
.--------------------------------------------------------------------------
. Initailise extract file varaibles
.--------------------------------------------------------------------------
INITA000  PACK      XRECIND,SP70
          UNPACK    SP70,XCASTYPE,XEPSTYPE,XASSONLY,XPHSTYPE,XFSTEPIS
          UNPACK    SP70,XPSTRDTE,XPENDDTE,XIMPCODE,XDOB,XEADMDTE
          UNPACK    SP70,XEDSCDTE,XLEVDAYS,XRUGDAIL,XPCPSS
          UNPACK    SP70,XHONOVR,XHONPRB,XHONTOT,XFOCCARE,XREHPRGT
          UNPACK    SP70,XDELDEM,XGEMCLIN,XAAGETYP
          MOVE      ZERO,XFIMEAT
          MOVE      ZERO,XFIMGRM
          MOVE      ZERO,XFIMBTH
          MOVE      ZERO,XFIMUPB
          MOVE      ZERO,XFIMLWB
          MOVE      ZERO,XFIMTLT
          MOVE      ZERO,XFIMBLM
          MOVE      ZERO,XFIMBWM
          MOVE      ZERO,XFIMTBD
          MOVE      ZERO,XFIMTTL
          MOVE      ZERO,XFIMTBT
          MOVE      ZERO,XFIMLOC
          MOVE      ZERO,XFIMSTR
          MOVE      ZERO,XFIMCOM
          MOVE      ZERO,XFIMEXP
          MOVE      ZERO,XFIMSCI
          MOVE      ZERO,XFIMPRB
          MOVE      ZERO,XFIMMEM
.
INITA999  RETURN    
.
.--------------------------------------------------------------------------
. Extract details
.--------------------------------------------------------------------------
EXTRC000  PACK      XRECIND,DPTRDADM,HYPHEN,DPTRDEPN  . Record Identifier (1)
          MOVE      CCASC6,XCASTYPE              . Case Type (2)
.
          MATCH     PHSESDAT,PHSEEDAT            . Episode Type (3)
          IF        @EQUAL
            MOVE      "2",XEPSTYPE
          ELSE
            MOVE      "1",XEPSTYPE
          ENDIF
.
          MATCH     SP70,PTRDPOCA                . Phase Type (5)
          IF        !@EQUAL
            MOVE      "pu",DIM2 
            PACK      KEY5,DIM2,PTRDPOCA,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      SP70,THCSCOD
            ENDIF
            MOVE        THCSCOD,XPHSTYPE
          ENDIF
.
          COMPARE   ONE,FRTPHASE                 . First Phase Episode (6)
          IF        @EQUAL                 
            MOVE      "1",XFSTEPIS
          ELSE
            MOVE      "0",XFSTEPIS
          ENDIF
.
          MOVE      "0",XASSONLY   
          MATCH     "3",CCUDF6 
          IF        @EQUAL
            MOVE      PHSESDAT,WRKDTE8           . Pal Care Start Date (7)
            CALL      CNVDTE00
            MOVE      WRKDTE10,XPSTRDTE
            MOVE      PHSEEDAT,WRKDTE8           . Pal Care End   Date (8)
            CALL      CNVDTE00
            MOVE      WRKDTE10,XPENDDTE
          ELSE
            MOVE      "0",XPCPSS                 . PCPSS Total (15)   
            CALL      FIMSCR00                   . FIM (16-33)
          ENDIF          
.
          MOVE      PTRDIMPR,XIMPCODE            . Impairment Code (9)
.
          MOVE      PBDATE,WRKDTE8               . Date Of Birth (10)
          CALL      CNVDTE00
          MOVE      WRKDTE10,XDOB
.
          MATCH     "3",CCUDF6
          IF        @EQUAL
            MOVE      XPSTRDTE,XEADMDTE          . Episode Admission Date (11)
            MOVE      XPENDDTE,XEDSCDTE          . Episode Discharge Date (12)
          ELSE
            MOVE      EPSDSDAT,WRKDTE8           . Episode Admission Date (11) 
            CALL      CNVDTE00
            MOVE      WRKDTE10,XEADMDTE
.
            MOVE      EPSDEDAT,WRKDTE8           . Episode Discharge Date (12)
            CALL      CNVDTE00
            MOVE      WRKDTE10,XEDSCDTE
          ENDIF
.
          MOVE      PTRDRGAA,XRUGDAIL            . RUG-ADL Total (14)
.
          MOVE      ZERO,XHONOVR                 . HoNOS Overactive (34)
          MOVE      ZERO,XHONPRB                 . HoNOS ADL (35)
          MOVE      ZERO,XHONTOT                 . HoNOS Total (36)
.
          MATCH     "3",CCUDF6
          IF        @EQUAL
            MOVE      PTRDFDDT,WRKDTE8           
            CALL      CNVDTE00
            MOVE      WRKDTE10,XFOCCARE          . Focus of Care (37)
          ELSE
            MOVE      "0",XFOCCARE               . Focus of Care (37)
            MOVE      "0",XREHPRGT               . Rehab Program Type (38)
            MOVE      "0",XGEMCLIN               . GEM Clinic Type (40)
          ENDIF
.
          CALL      DELDEM00                     . Delirium or Dementia (39)
.
          MOVE      "9",XAAGETYP                 . Age Type (41)
.
          CALL      CLOS0000
.
EXTRC999  RETURN
.
.--------------------------------------------------------------------
. Convert date 8 (ccyymmdd) to date 10 (dd/mm/ccyy)
.--------------------------------------------------------------------
CNVDTE00  MOVE      SP70,WRKDTE10
          MATCH     SP70,WRKDTE8
          GOTO      CNVDTE99 IF EQUAL
.
          UNPACK    WRKDTE8,CCENT,CYEAR,CMON,CDAY
          PACK      WRKDTE10,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,SP70
          REP       " 0",WRKDTE10
.
CNVDTE99  RETURN
.
.-----------------------------------------------------------------------------
. Get Delirium or Dementia flag
.-----------------------------------------------------------------------------
DELDEM00  MOVE      "0",XDELDEM
          PACK      KEY13,DPTRDADM,DPTRDEPN,Z70
          CALL      RSPTECD1
DELDEM20  CALL      RPPTECD1
          BRANCH    OVRCD,DELDEM99
.
          MATCH     DPTRDADM,DPTEDADM        * same Admission ?
          GOTO      DELDEM99 IF NOT EQUAL
          MATCH     DPTRDEPN,DPTEDEPN        * same Episode ?
          GOTO      DELDEM99 IF NOT EQUAL
.
          MATCH     SP70,PTRDFADT
          IF        !@EQUAL
            MATCH     PTRDFADT,PTEDDTCD
            GOTO      DELDEM20 IF LESS
          ENDIF
.
.         MATCH     SP70,PTRDFDDT
.         IF        !@EQUAL
.           MATCH     PTEDDTCD,PTRDFDDT
.           GOTO      DELDEM20 IF LESS
.         ENDIF
.
.         Checking the following ICD10 disease codes from site
.         in addition to the existing list of codes
.
          MATCH     "F00.00",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F00.01",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F00.10",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F00.11",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F00.20",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F00.21",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F00.90",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F00.91",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F01.01",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.10",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.11",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.20",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.21",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.30",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.31",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.80",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.81",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.90",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F01.91",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F02.00",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.01",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.10",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.11",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.20",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.21",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.30",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.31",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.40",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.41",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.80",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F02.81",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F03.00",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
          MATCH     "F03.01",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
._____________________________________________________________________________
.
          MATCH     "F05.9",PTEDCODE
          GOTO      DELDEM80 IF EQUAL        
.
          MATCH     "F05.8",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F05.1",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F05.0",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F03",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F02.8",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F02.4",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F02.3",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F02.2",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F02.1",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F02.0",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F01.9",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F01.8",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F01.3",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F01.2",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F01.1",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F01.0",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F00.9",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F00.2",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F00.1",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          MATCH     "F00.0",PTEDCODE
          GOTO      DELDEM80 IF EQUAL
.
          GOTO      DELDEM20 
.
DELDEM80  MOVE      "1",XDELDEM
          GOTO      DELDEM99
.
DELDEM99  RETURN
.
.-----------------------------------------------------------------------------
. Get the FIM array
.-----------------------------------------------------------------------------
FIMSCR00  PACK      KEY24,DAADMNO,PTRDFADT,SP70
          CALL      RSPTFIM1
FIMSCR20  CALL      RKPTFIM1
          BRANCH    OVRCD,FIMSCR99
.
          MATCH     DAADMNO,PTFIVISN
          GOTO      FIMSCR99 IF NOT EQUAL
.
          MATCH     SP70,PTRDFDDT
          IF        !@EQUAL
            MATCH     PTFIDATE,PTRDFDDT
            GOTO      FIMSCR99 IF LESS
          ENDIF
.
          MATCH     "1",PTFIDELT
          GOTO      FIMSCR20 IF EQUAL
.
          MATCH     "A",PTFITYPE                 * No Adm FIM exists
          GOTO      FIMSCR20 IF NOT EQUAL
.
FIMSCR40  MATCH     "1",PTFIASES                 . Assessment Only (4)
          IF        @EQUAL
            MOVE      "1",XASSONLY
          ENDIF
.
FIMSCR50  MOVE      PTFIEATS,XFIMEAT
          MOVE      PTFIGRMS,XFIMGRM
          MOVE      PTFIBTHS,XFIMBTH
          MOVE      PTFIUPBS,XFIMUPB
          MOVE      PTFILWBS,XFIMLWB
          MOVE      PTFITLTS,XFIMTLT
          MOVE      PTFIBLMS,XFIMBLM
          MOVE      PTFIBWMS,XFIMBWM
          MOVE      PTFITBDS,XFIMTBD
          MOVE      PTFITTLS,XFIMTTL
          MOVE      PTFITBTS,XFIMTBT
          MOVE      PTFILOCS,XFIMLOC
          MOVE      PTFISTRS,XFIMSTR
          MOVE      PTFICOMS,XFIMCOM
          MOVE      PTFIEXPS,XFIMEXP
          MOVE      PTFISCIS,XFIMSCI
          MOVE      PTFIPRBS,XFIMPRB
          MOVE      PTFIMEMS,XFIMMEM
.
FIMSCR99  RETURN
+
.--------------------------------------------------------------------------
. Get Extract File Name
.--------------------------------------------------------------------------
FILE0000  MOVE      "QSNAP",DIM5
.
          CALL      IBACLOCK
          PACK      FILENAME,DIM5,SP70
          ENDSET    FILENAME
          APPEND    SP8,FILENAME
          RESET     FILENAME
.
          MATCH     EXITCHAR,FILENAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
          MATCH     SP70,FILENAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
          STRIP     FILENAME
          SQUEEZE   FILENAME
.
FILE9000  MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9500  MOVE      ONE,EXIT
FILE9999  RETURN
.
.--------------------------------------------------------------------------
. Move Extract .csv file to Web Directory
.--------------------------------------------------------------------------
MVEXC000  CLEAR     CMDLINE
          APPEND    "ibapms63.us1 ",CMDLINE
          APPEND    FILENO,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    FILENAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXC999  RETURN
.
.--------------------------------------------------------------------------
. Check and input multi hospital
.--------------------------------------------------------------------------
CHKMH000  IF        IBCNMHOS = 1
            DISPLAY   *P2:13,*EF,"Hospital ID: ";
            MOVE      SP70,HOSPCODE
            MOVE      TEN5,ECOL
            MOVE      TEN3,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
            CALL      PATHSPKY
            BRANCH    EXIT,CHKMH800,CHKMH950
            MOVE      KEY3,HOSPCODE
            DISPLAY   *P22:13,*EL,PTHSNAME,*W1;
            GOTO      CHKMH900
          ENDIF
.
CHKMH800  MOVE      "All Hospitals",PTHSNAME
          DISPLAY   *P22:13,*EL,PTHSNAME,*W1;
          MOVE      SP70,HOSPCODE
.
CHKMH900  MOVE      ZERO,EXIT
          GOTO      CHKMH999
.
CHKMH950  MOVE      ONE,EXIT
CHKMH999  RETURN
.
.--------------------------------------------------------------------------
. Calculate The LOS For This Patient
.--------------------------------------------------------------------------
CLOS0000  MOVE      ZERO,XLEVDAYS
          MOVE      ZERO,WORKHITH           * Initialise HITH care days
          MOVE      ZERO,WORKICLS           * Initialise ICU LOS
          MOVE      ZERO,WORKLDAY           * Initialise leave days
          MOVE      ZERO,WORKPSYC           * Initialise psychiatric days
          MOVE      ZERO,PSYCFLAG           * Initialise psych flag
          MOVE      ZERO,ADMPFLAG
          MOVE      ZERO,WORKQUAL           * Initialise qualified days
          MOVE      ACLSSFT,DEPTYPE
.
. ----- Daycase -----
.
          MATCH     ADATE,DDATE
          IF        @EQUAL
            PACK      KEY30,AADMNO,SP30
            CALL      RDSTRAN2              * Position on & read the transfer
            CALL      RDKTRAN2                file
            IF        OVRCD = 0
              MATCH     AADMNO,TADMN
              IF        @EQUAL
                MOVE      TRBTYP,BEDTYPE
                CALL      CPSY0000            * Check for psychiatric care
                IF        EXIT=1
                  MOVE      ONE,WORKPSYC
                  MOVE      ZERO,EXIT
                ENDIF
              ENDIF
            ENDIF
.
            PACK      KEY30,AADMNO,SP30
            CALL      RDSTRAN2              * Position on & read the transfer
CLOS0500    CALL      RDKTRAN2                file
            IF        OVRCD = 0
              MATCH     AADMNO,TADMN
              GOTO      CLOS0600 IF NOT EQUAL
.
              CMATCH    ANSR,TMOVE
              GOTO      CLOS0550 IF EQUAL
.
              CMATCH    ANSC,TMOVE
              GOTO      CLOS0550 IF EQUAL
.
              GOTO      CLOS0500
.
CLOS0550      MOVE      TRBTYP,BEDTYPE
              CALL      CPSY0000            * Check for psychiatric care
              IF        EXIT=1
                MOVE      ONE,WORKPSYC
                MOVE      ZERO,EXIT
              ENDIF
.
            ENDIF
.
CLOS0600    PACK      KEY30,AADMNO,ZED30
            CALL      RDSTRAN2              * Position on & read the transfer
            CALL      RDPTRAN2                file
            IF        OVRCD=0
              MATCH     AADMNO,TADMN
              GOTO      CLOS9000 IF NOT EQUAL
.
              MOVE      TATYPE,ADMTYPE
              MOVE      TDEPT,DEPTYPE
              MOVE      TRBTYP,BEDTYPE
              CALL      CHKC0000            * Check a stay in a ICU
              IF        EXIT=1
                MOVE      ONE,WORKICLS
                MOVE      ZERO,EXIT
              ENDIF
              CALL      CPSY0000            * Check for psychiatric care
              IF        EXIT=1
                MOVE      ONE,WORKPSYC
                MOVE      ZERO,EXIT
              ENDIF
              CALL      CQNB0000            * Check for qualified care
              IF        EXIT=1
                MOVE      ONE,WORKQUAL
                MOVE      ZERO,EXIT
              ENDIF
              CALL      CHIHA000            * Check for hospital in the home
              IF        EXIT=1
                MOVE      ONE,WORKHITH
                MOVE      ZERO,EXIT
              ENDIF
            ENDIF
            GOTO      CLOS9000
          ENDIF
.
. ----- Non Daycase -----
.
          PACK      KEY30,AADMNO,SP20
          CALL      RDSTRAN2                * Position on & read the transfer
CLOS1000  CALL      RDKTRAN2                  file
          BRANCH    OVRCD,CLOS7000
.
          MATCH     AADMNO,TADMN
          GOTO      CLOS7000 IF NOT EQUAL
.
          CMATCH    ANSA,TMOVE
          GOTO      CLOS2000 IF EQUAL       * Admission record
.
          CMATCH    ANSL,TMOVE
          GOTO      CLOS3000 IF EQUAL       * Leave record
.
          CMATCH    ANSR,TMOVE
          GOTO      CLOS4000 IF EQUAL       * Return from leave record
.
          CMATCH    ANSC,TMOVE
          GOTO      CLOS5000 IF EQUAL       * Change record
.
          CMATCH    ANSD,TMOVE
          GOTO      CLOS6000 IF EQUAL       * Discharge record
.
          GOTO      CLOS5000                * Assume change record
.
. ----- Admission Record -----
.
CLOS2000  CALL      LSAV0000                * Save details
          GOTO      CLOS1000
.
. ----- Leave Record -----
.
CLOS3000  CALL      LEND0000                * Process end of a period
          GOTO      CLOS1000
.
. ----- Return Record -----
.
CLOS4000  CALL      LDAY0000                * Calculate the leave days
          CALL      LSAV0000                * Save details
          GOTO      CLOS1000
.
. ----- Change Record -----
.
CLOS5000  CALL      LEND0000                * Process end of a period
          CALL      LSAV0000                * Save new details
          GOTO      CLOS1000
.
. ----- Discharge Record -----
.
CLOS6000  CALL      LEND0000                * Process end of a period
          GOTO      CLOS9000
.
. ----- Error -----
.
CLOS7000  MOVE      DDATE,TDATE             * Assume discharge date
          CALL      LEND0000                * Process end of a period
          GOTO      CLOS9999
.
. ----- Finished -----
.
CLOS9000  MOVE      WORKLDAY,XLEVDAYS            . Leave Days (13)
.
CLOS9999  RETURN
.
.--------------------------------------------------------------------------
. Save Start Of Period Details For Length Of Stay Calculations
.--------------------------------------------------------------------------
LSAV0000  PACK      CDYSFDTE,TDATE
          MOVE      TATYPE,STATYPE
          MOVE      TRBTYP,STRBTYP
.
          MATCH     "A",TMOVE
          IF        @EQUAL
            PACK      SVADDATE,TDATE
.
            MATCH     SP3,TRBTYP
            GOTO      LSAV9999 IF EQUAL        * Dont allow blank codes
.
            PACK      KEY5,ANSB,ANST,TRBTYP    * first check for any psych days
            CALL      RDCODE1
            BRANCH    OVRCD,LSAV9999
.
            MATCH     ANSA,TCINDC3
            IF        @EQUAL
              MOVE      ONE,ADMPFLAG           * patient has psych care
            ENDIF
            MATCH     ANSD,TCINDC3
            IF        @EQUAL
              MOVE      ONE,ADMPFLAG           * patient has psych care
            ENDIF
          ENDIF
.
LSAV9999  RETURN
+
.--------------------------------------------------------------------------
. Process End Of Period Details For Length Of Stay
.--------------------------------------------------------------------------
LEND0000  PACK      CDYSTDTE,TDATE
.
LEND1000  CALL      CALCDAYS                * Calculate diff between dates
          SUB       ONE,CDYSDAYS            * We dont want inclusive dates
          MOVE      STATYPE,ADMTYPE
          MOVE      TDEPT,DEPTYPE
          MOVE      STRBTYP,BEDTYPE
          CALL      CHKC0000                * Check if stay in a ICU
          IF        EXIT=1
            ADD       CDYSDAYS,WORKICLS
            MOVE      ZERO,EXIT
          ENDIF
.
          CALL      CPSY0000                * Check for psychiatric care
          IF        EXIT=1
            ADD       CDYSDAYS,WORKPSYC
            IF        WORKPSYC = 0
              CMATCH    ANSC,TMOVE          * Check if it's not a change rec
              IF        !@EQUAL
                CMATCH    ANSL,TMOVE        * Check if it's not a leave rec
                IF        !@EQUAL
                  ADD       ONE,WORKPSYC    * at least 1 psych day
                ENDIF
              ELSE
                IF        PSYCFLAG=0
                  MOVE      ZERO,WORKPSYC
                ENDIF
              ENDIF
            ENDIF
            MOVE      ZERO,EXIT
          ELSE
            CMATCH    ANSC,TMOVE
            IF        @EQUAL
              IF        PSYCFLAG=1
                MATCH     TDATE,SVADDATE
                IF        @EQUAL
                  MOVE      ZERO,WORKPSYC
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          CMATCH    ANSD,TMOVE
          IF        @EQUAL
            IF        ADMPFLAG = 1
              IF        WORKPSYC = 0
                MOVE      ONE,WORKPSYC
              ENDIF
            ENDIF
          ENDIF
.
          CALL      CQNB0000                * Check for qualified new born
          IF        EXIT=1
            ADD       CDYSDAYS,WORKQUAL
            MOVE      ZERO,EXIT
          ENDIF
.
          CALL      CHIHA000                * Check for hospital in the home
          IF        EXIT=1
            ADD       CDYSDAYS,WORKHITH
            MOVE      ZERO,EXIT
          ENDIF
.
LEND9000  MOVE      TDATE,STDATE            * Save transfer date for leave days
LEND9999  RETURN
+
.--------------------------------------------------------------------------
. Calculate The Leave Days
.--------------------------------------------------------------------------
LDAY0000  PACK      CDYSFDTE,STDATE
          PACK      CDYSTDTE,TDATE
          MATCH     CDYSTDTE,CDYSFDTE
          GOTO      LDAY9999 IF NOT LESS
.
          CALL      CALCDAYS                * Calculate diff between dates
          SUB       ONE,CDYSDAYS            * We dont want inclusive dates
          ADD       CDYSDAYS,WORKLDAY       * Total leave days
.
          COMPARE   ZERO,WORKLDAY
          GOTO      LDAY9999 IF EQUAL       * not a leave period - CAR 291926
.
          COMPARE   NINTY9,NUMLEV
          GOTO      LDAY9999 IF EQUAL       * don't increase for more than 99
.
          ADD       ONE,NUMLEV
.
LDAY9999  RETURN
+
.--------------------------------------------------------------------------
. Check For A Stay In A Critical Care Unit
.--------------------------------------------------------------------------
CHKC0000  MOVE      ZERO,EXIT               * Default normal/HDU bed stay
          IF        PTCNHCCC=1
            MATCH     SP3,BEDTYPE
            GOTO      CHKC9999 IF EQUAL     * Dont allow blank codes
.
            PACK      KEY5,ANSB,ANST,BEDTYPE   * Using bed type for CCU/ICU
            CALL      RDCODE1
            BRANCH    OVRCD,CHKC9999
.
            MATCH     ANSI,TCINDC1
            GOTO      CHKC9999 IF NOT EQUAL * Not an CCU/ICU stay
          ELSE
            MATCH     SP3,ADMTYPE
            GOTO      CHKC9999 IF EQUAL     * Dont allow blank codes
.
            PACK      KEY5,ANSA,SP1,ADMTYPE    * Using admin type for CCU/ICU
            CALL      RDCODE1
            BRANCH    OVRCD,CHKC9999
.
            COMPARE   ONE,TCFORM6
            GOTO      CHKC9999 IF NOT EQUAL * Not an CCU/ICU stay
          ENDIF
          MOVE      ONE,EXIT                * CCU/ICU stay (not HDU)
CHKC9999  RETURN
+
.--------------------------------------------------------------------------
. Check For Psychiatric care (use category BT)
.--------------------------------------------------------------------------
CPSY0000  MOVE      ZERO,EXIT                * Default normal/HDU bed stay
          MATCH     SP3,TRBTYP
          GOTO      CPSY9999 IF EQUAL        * Dont allow blank codes
.
          PACK      KEY5,ANSB,ANST,TRBTYP    * first check for any psych days
          CALL      RDCODE1
          BRANCH    OVRCD,CPSY9999
.
          MATCH     ANSA,TCINDC3
          IF        @EQUAL
            MOVE      ONE,PSYCFLAG           * patient has psych care
          ENDIF
          MATCH     ANSD,TCINDC3
          IF        @EQUAL
            MOVE      ONE,PSYCFLAG           * patient has psych care
          ENDIF
.
CPSY8000  MATCH     SP3,BEDTYPE
          GOTO      CPSY9999 IF EQUAL        * Dont allow blank codes
.
          PACK      KEY5,ANSB,ANST,BEDTYPE   * now check for period psych days
          CALL      RDCODE1
          BRANCH    OVRCD,CPSY9999
.
          MATCH     ANSA,TCINDC3
          GOTO      CPSY9000 IF EQUAL        * psychiatric care
.
          MATCH     ANSD,TCINDC3
          GOTO      CPSY9999 IF NOT EQUAL    * Not psychiatric care
.
CPSY9000  MOVE      ONE,EXIT
CPSY9999  RETURN
+
.--------------------------------------------------------------------------
. Check For Qualified new born
.--------------------------------------------------------------------------
CQNB0000  MOVE      ZERO,EXIT
          MATCH     SP3,ADMTYPE
          GOTO      CQNB9999 IF EQUAL     * Dont allow blank codes
.
          PACK      KEY5,ANSA,SP1,ADMTYPE    * Using admin type for qualified
          CALL      RDCODE1
          BRANCH    OVRCD,CQNB9999
.
          MATCH     "Q",THCSCOD
          GOTO      CQNB9000 IF EQUAL       * Qualified
.
          GOTO      CQNB9999                * Not qualified
.
CQNB9000  MOVE      ONE,EXIT
CQNB9999  RETURN
+
.--------------------------------------------------------------------------
. Check For Hospital In The Home
.--------------------------------------------------------------------------
CHIHA000  MATCH     SP3,BEDTYPE
          GOTO      CHIHA999 IF EQUAL        * Dont allow blank codes
.
          PACK      KEY5,ANSB,ANST,BEDTYPE   * Using bed type
          CALL      RDCODE1
          BRANCH    OVRCD,CHIHA999
.
          MATCH     ANSH,TCINDC1
          GOTO      CHIHA999 IF NOT EQUAL    * Not hospital in the home
.
          MOVE      ONE,EXIT
.
CHIHA999  RETURN
.
.--------------------------------------------------------------------------
. Uploading data
.--------------------------------------------------------------------------
UPLOD000  CALL      PRHD0000                * Print header
          CALL      CTOT0000                * Clear total vars
.
          CALL      PROC0000                * with Updating file
          CALL      PTOT0000                * Print report totals
.
UPLOD999  RETURN
.
.--------------------------------------------------------------------------
. Process uploading  
.--------------------------------------------------------------------------
PROC0000  OPEN      EXTSUBAI,FNAMEI
          MOVE      SP70,SAVADMNO
.
PROC1000  READ      EXTSUBAI,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011,XFLD0012,XFLD0013,XFLD0014,XFLD0015:
                                 XFLD0016,XFLD0017,XFLD0018,XFLD0019,XFLD0020:
                                 XFLD0021,XFLD0022,XFLD0023,XFLD0024,XFLD0025:
                                 XFLD0026,XFLD0027,XFLD0028,XFLD0029,XFLD0030:
                                 XFLD0031,XFLD0032,XFLD0033,XFLD0034,XFLD0035:
                                 XFLD0036,XFLD0037,XFLD0038,XFLD0039,XFLD0040:
                                 XFLD0041,XFLD0042
          GOTO      PROC9000 IF OVER
.
          ADD       ONE,LINECNTR            * Text file line counter for error
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Admission/Episode 
          GOTO      PROC1000 IF EQUAL
.
          CALL      PFLD0000                * pack fields with spaces
.
          PACK      XRECIND,XFLD0001,SP70
          UNPACK    XRECIND,XADMNO,DIM1,XEPISODE
          MOVE      XADMNO,KEY8
          TYPE      KEY8
          GOTO      PROC1000 IF NOT EQUAL * Not valid numeric
.
          CALL      VALD0000              * Validate fields
          BRANCH    ERRFLAG,PROC1000
.
          COMPARE   ONE,PHCARE
          GOTO      PROC2000 IF NOT EQUAL * Not sub-acute
.
          UNPACK    XFLD0007,KEY10     * Start date
          CALL      VDAT0000           * Check date format
          PACK      XPSTRDTE,KEY10
.
          UNPACK    XFLD0008,KEY10     * End date
          CALL      VDAT0000           * Check date format
          PACK      XPENDDTE,KEY10
.
.          MOVE      SP70,D2
.          MOVE      ZERO,F2
.          UNPACK    XFLD0042,XFLDCODE,KEY1,D2
.          SQUEEZE   D2
.          MOVE      D2,F2
.          PACK      XFLDVERS,F2,SP70
.
          MATCH     XADMNO,SAVADMNO
          IF        !@EQUAL
            MOVE      XADMNO,KEY8
            CALL      DPAB0000              * Deleting existing patabfaf records
            MOVE      XADMNO,SAVADMNO
          ENDIF
.
PROC2000  PACK      KEY10,XADMNO,XEPISODE,SP70
          CALL      RDPTRDE1
          BRANCH    OVRCD,PROC1000
.
          MOVE      XFLDCODE,PTRDSNAP
          MOVE      XFLDVERS,PTRDVERS    
          CALL      UPPTRDE1
.
          ADD       ONE,UPDCOUNT       * Total records updated
.
          COMPARE   ONE,PHCARE
          GOTO      PROC1000 IF NOT EQUAL
.
.         Update/Write the AN-SNAP value to ABF file
.
          MOVE      DPTRDADM,PTABADMN
          UNPACK    XPSTRDTE,XDAY,KEY1,XMON,D1,XCENT,XYEAR
          PACK      PTABENCT,XCENT,XYEAR,XMON,XDAY,SP70   * Start date
          REP       " 0",PTABENCT
.
          UNPACK    XPENDDTE,XDAY,KEY1,XMON,D1,XCENT,XYEAR
          PACK      KEY8,XCENT,XYEAR,XMON,XDAY,SP70   * End date
          REP       " 0",KEY8
.
.         Pack ABF Description with
.         AN-SNAP Code, Version no ,Episode number and Episode End date
.
          MOVE      SP70,PTABINVN
          MOVE      "070",PTABADJT          * Type of adjustment (AN-SNAP)
          PACK      KEY27,PTABADMN,PTABENCT,PTABINVN,PTABADJT,SP70
          CALL      RDPTABF1
          IF        OVRCD=0
            PACK      PTABCDES,PTRDSNAP,SLASH,PTRDVERS:
                               SP1,XEPISODE,SP1,KEY8,SP70
            MOVE      "7",PTABABFT              * Palliative Phase of Care
.
            PACK      PTABUDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTABUDAT
            CLOCK     TIME,PTABUTIM
            MOVE      "QUICKSNAP",PTABUUID
            CALL      UPPTABF1
          ELSE
            CALL      CLPATABF
            MOVE      "7",PTABABFT              * Palliative Phase of Care
            UNPACK    KEY27,PTABADMN,PTABENCT,PTABINVN,PTABADJT
            PACK      PTABCDES,PTRDSNAP,SLASH,PTRDVERS:
                               SP1,XEPISODE,SP1,KEY8,SP70
.
            PACK      PTABCDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTABCDAT
            CLOCK     TIME,PTABCTIM
            MOVE      "QUICKSNAP",PTABCUID
.
            CALL      RAPTABF1
            IF        OVRCD=1
              MOVE      ZERO,OVRCD
              TRAP      OVERCOND IF IO
              CALL      WRPTABF1
              TRAPCLR   IO
            ENDIF
          ENDIF
.
          GOTO      PROC1000
.
PROC9000  CLOSE     EXTSUBAI
PROC9999  RETURN
+
.--------------------------------------------------------------------------
. Validate the input date
. date can be in format of d/m/ccyy or dd/m/ccyy or d/mm/ccyy or dd/mm/ccyy
. return date dd/mm/ccyy
.--------------------------------------------------------------------------
VDAT0000  UNPACK    SP70,XDAY,XMON,XCENT,XYEAR,KEY2,KEY4
          CLEAR     KEY2
          CLEAR     KEY4
          MATCH     SP70,KEY10
          GOTO      VDAT9000 IF EQUAL
          MOVE      ZERO,F1
.
VDAT1000  CMATCH    SLASH,KEY10
          GOTO      VDAT1200 IF EQUAL
.
          MOVE      KEY10,KEY1
          APPEND    KEY1,KEY2
.
          BUMP      KEY10,1
          GOTO      VDAT1000 IF NOT EOS
.
VDAT1200  APPEND    SP10,KEY2
          RESET     KEY2
          SQUEEZE   KEY2
          MOVE      ZERO,F2
          MOVE      KEY2,F2
.
          ADD       ONE,F1
          STORE     F2,F1,XDAY,XMON  * Day/Month
          MOVE      SP70,KEY2
          CLEAR     KEY2
.
          BUMP      KEY10,1
          GOTO      VDAT9000 IF EOS
.
          COMPARE   TWO,F1
          GOTO      VDAT2000 IF EQUAL
.
          GOTO      VDAT1000
.
VDAT2000  APPEND    KEY10,KEY4       * must be Century and Year
          RESET     KEY4
          GOTO      VDAT9000
.
.
VDAT9000  PACK      KEY10,XDAY,SLASH,XMON,SLASH,KEY4,SP70
          REP       " 0",KEY10
VDAT9999  RETURN
+
.--------------------------------------------------------------------------
. Validate input text file fields
.--------------------------------------------------------------------------
VALD0000  MOVE      ZERO,EXIT
          MOVE      ZERO,ERRFLAG
          MOVE      ZERO,PHCARE
.
          PACK      KEY8,XADMNO,SP10
          CALL      RDMISS1                 * read admission details
          IF        OVRCD=1
            MOVE      "Invalid Visit number.",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
            GOTO      VALD9999
          ENDIF
.
.        Check for Sub Acute Palliative care
.
          MOVE      "CC",KEY2
          PACK      KEY5,KEY2,ACARE
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      ZERO,FORM1
            SQUEEZE   PTCDUDF6
            MOVE      PTCDUDF6,FORM1
            IF        FORM1=3
              MOVE      SP70,KEY2
              UNPACK    THCSCOD,KEY2
              MATCH     "8",KEY2
              IF        @EQUAL
                MOVE      ONE,PHCARE        * Flag for Sub-Acute
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "Record Identifier is blank",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ELSE
            PACK      KEY10,XADMNO,XEPISODE,SP70
            CALL      RDPTRDE1
            IF        OVRCD=1
              MOVE      "Invalid Record Identifier",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MATCH     SP70,XFLD0042            * AN-SNAP Code
          IF        @EQUAL
            MOVE      "AN-SNAP Code is blank",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
            GOTO      VALD9999
          ENDIF
.
          CMATCH    SP1,XFLD0042
          IF        @EQUAL
            UNPACK    XFLD0042,XFLDVERS,XFLDCODE
          ELSE
            UNPACK    XFLD0042,XFLDVER1,XFLDCODE
            PACK      XFLDVERS,SP1,XFLDVER1,SP70
          ENDIF
.
          PACK      KEY5,XFLDCODE,XFLDVERS,SP70
          CALL      RDPMSNP1
          IF        OVRCD=1
            CLEAR     ERRORMSG
            APPEND    "Invalid AN-SNAP Class.Code: ",ERRORMSG
            APPEND    KEY5,ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
.
            CALL      PERR0000
            MOVE      ONE,EXIT
          ENDIF
.
VALD9999  RETURN
.
.*******************************************************************************
.         Deleting existing ABF Patient details (patabfaf)
.*******************************************************************************
DPAB0000  PACK      KEY27,KEY8,SP70
          CALL      RSPTABF1
          CALL      RKPTABF1
          BRANCH    OVRCD,DPAB9999
.
          MATCH     KEY8,PTABADMN       * Same admission no ?
          GOTO      DPAB9999 IF NOT EQUAL
.
          MATCH     SP70,PTABINVN
          GOTO      DPAB9999 IF NOT EQUAL  * Invoice must be blank
.
          PACK      KEY27,PTABADMN,PTABENCT,PTABINVN,PTABADJT
          CALL      DEPTABF1
          GOTO      DPAB0000
.
DPAB9999  RETURN
+
.--------------------------------------------------------------------------
. Print error line
.--------------------------------------------------------------------------
PERR0000  ADD       ONE,ERRCOUNT            * Total error count
          MOVE      ONE,ERRFLAG
.
PERR8100  PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG:
                    *61,"|",XRECIND,*132,"|"
.
PERR9999  RETURN
+
.--------------------------------------------------------------------------
. Print Heading for the report
.--------------------------------------------------------------------------
PRHD0000  CALL      HEAD132 
.
          PRINT     *N,"Upload AN-SNAP Classification for AN-SNAP":
                       " Classification"
.
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS
.
          PRINT     *N,"AN-SNAP Classification Load file : ":
                    *N,"     ",FNAMEI
          CALL      UND132
.
          PRINT     *1,"| Line No.",*12,"|Error Messages":
                    *61,"|Record Identifier",*132,"|"
.
          CALL      UND132
.
PRHD9999  RETURN
.
.--------------------------------------------------------------------------
. Clear total vars
.--------------------------------------------------------------------------
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,UPDCOUNT
.
CTOT999   RETURN
+
.--------------------------------------------------------------------------
. Print total line
.--------------------------------------------------------------------------
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Record Read    : ",LINECNTR,*132,"|"
          PRINT     *1,"| Total Record Errors  : ",ERRCOUNT,*132,"|"
.
          PRINT     *1,"| Total Record Updated : ",UPDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
+
.--------------------------------------------------------------------------
. Pack fields with spaces
.--------------------------------------------------------------------------
PFLD0000  PACK      XFLD0001,XFLD0001,SP70
          PACK      XFLD0002,XFLD0002,SP70
          PACK      XFLD0003,XFLD0003,SP70
          PACK      XFLD0004,XFLD0004,SP70
          PACK      XFLD0005,XFLD0005,SP70
          PACK      XFLD0006,XFLD0006,SP70
          PACK      XFLD0007,XFLD0007,SP70
          PACK      XFLD0008,XFLD0008,SP70
          PACK      XFLD0009,XFLD0009,SP70
          PACK      XFLD0010,XFLD0010,SP70
          PACK      XFLD0011,XFLD0011,SP70
          PACK      XFLD0012,XFLD0012,SP70
          PACK      XFLD0013,XFLD0013,SP70
          PACK      XFLD0014,XFLD0014,SP70
          PACK      XFLD0015,XFLD0015,SP70
          PACK      XFLD0016,XFLD0016,SP70
          PACK      XFLD0017,XFLD0017,SP70
          PACK      XFLD0018,XFLD0018,SP70
          PACK      XFLD0019,XFLD0019,SP70
          PACK      XFLD0020,XFLD0020,SP70
          PACK      XFLD0021,XFLD0021,SP70
          PACK      XFLD0022,XFLD0022,SP70
          PACK      XFLD0023,XFLD0023,SP70
          PACK      XFLD0024,XFLD0024,SP70
          PACK      XFLD0025,XFLD0025,SP70
          PACK      XFLD0026,XFLD0026,SP70
          PACK      XFLD0027,XFLD0027,SP70
          PACK      XFLD0028,XFLD0028,SP70
          PACK      XFLD0029,XFLD0029,SP70
          PACK      XFLD0030,XFLD0030,SP70
          PACK      XFLD0031,XFLD0031,SP70
          PACK      XFLD0032,XFLD0032,SP70
          PACK      XFLD0033,XFLD0033,SP70
          PACK      XFLD0034,XFLD0034,SP70
          PACK      XFLD0035,XFLD0035,SP70
          PACK      XFLD0036,XFLD0036,SP70
          PACK      XFLD0037,XFLD0037,SP70
          PACK      XFLD0038,XFLD0038,SP70
          PACK      XFLD0039,XFLD0039,SP70
          PACK      XFLD0040,XFLD0040,SP70
          PACK      XFLD0041,XFLD0041,SP70
          PACK      XFLD0042,XFLD0042,SP70
.
PFLD9999  RETURN
+
.--------------------------------------------------------------------------
. Execute us1 command before processing
.--------------------------------------------------------------------------
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibapms63.us2 ",CMDLINE
          SQUEEZE   FNAMEI
          APPEND    FNAMEI,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    OUTFNAME,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
.
.--------------------------------------------------------------------------
          INC       STD001IO/PBL
.
          INC       CLPATMAS
          INC       CLPMSPX2
          INC       CLPATABF
          INC       PATHSPKY
          INC       TFILENAM                * Generate Temp File Name
          INC       CALCDAYS
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       PATCODIO/INC 
          INC       PATHSPIO/INC
          INC       PATDSCIO/INC            * Discharge File
          INC       PATECDIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PATRDEIO/INC
          INC       PMSSNPIO/INC
          INC       PMSFOCIO/INC
          INC       PATCHCIO/INC
          INC       PATABFIO/INC
          INC       PATFIMIO/INC
          INC       PATTRNIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

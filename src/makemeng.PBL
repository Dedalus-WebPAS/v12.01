.-----------------------------------------------------------------------------
.   Program       : makemeng
.   Function      : Create Site Transfer File <menuname>.men.gz
.   Modifications :   
.
.   V9.09.01 17.04.2008 Sandra Barcham     166599
.            Changed from compress to gzip
.   V6.00.01 13.05.93 B.G.Ackland
.            New Program
.
.-----------------------------------------------------------------------------
          INC       IBACOMM/INC
          INC       IBAKEYS/INC
          INC       MENUSEFD/INC
.=========================
.Local Variable Definition
.-------------------------
SAVKEY10  DIM       10
PROGNAME  DIM       8
RECTYPE   FORM      1
EXTENS    INIT      "/men"
MENU      INIT      ".men"
FILE      DIM       12
COMPRESS  INIT      "gzip "
TRANFILE  FILE      ASCII, FIXED=250
.
PRGID     INIT      "makemeng"
PRGNAM    INIT      "Create Menu Generator <filename>.men.gz"
VERSION   INIT      "V12.01.00"
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
ML0000    CALL      INIT0000
          BRANCH    OVRCD,ML9000
          KEYIN     *EOFF,PROGNAME
.
          PACK      FILE,PROGNAME,EXTENS
          SQUEEZE   FILE
          DISPLAY   *P1:1,*HON,*ES,SP20,SP20,SP20,SP20
          DISPLAY   *P1:1,*HON,PRGID,"/",VERSION,SP2,"Creating ",*+,FILE,".gz"
          PREP      TRANFILE,FILE
.
          PACK      KEY15,PROGNAME,SP20
          CALL      RSMEUS1
ML1000    CALL      RKMEUS1
          BRANCH    OVRCD,ML1100
          MATCH     MEUSMENU,PROGNAME
          GOTO      ML1100 IF NOT EQUAL
.
          CALL      WRTRFL1 * Write Record
.
          GOTO      ML1000
.
ML1100    CLOSE     TRANFILE
          PACK      KEY13,PROGNAME,MENU
          SQUEEZE   KEY13
          PACK      KEY50,COMPRESS,KEY13
          EXECUTE   KEY50,KEY8
          GOTO      ML9999
.
ML9000    DISPLAY   *P1:1,*B,*ES,"Unable to Open menuserf.",*W2
.
ML9999    SHUTDOWN  "" 
.
. Record Type 3 - Field Masterfile 
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  MOVE      "180",MRECLEN
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      MENUSER1,"menuserf"
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      "181",MRECLEN
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      MENUSER1,"menuserf"
            TRAPCLR   IO
          ENDIF
          IF        OVRCD=1
            DISPLAY   "Can't Open menuserf"
            STOP
          ENDIF
          RETURN
.
HITENTER  KEYIN     ANS
          RETURN
.
RDTRFL1   MOVE      ZERO,OVRCD
          READ      TRANFILE,SEQ;MEUSMENU,MEUSK2,MEUSK3,MEUSK4,MEUSD1:
                                 MEUSD2
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRFL1   WRITE     TRANFILE,SEQ;MEUSMENU,MEUSK2,MEUSK3,MEUSK4,MEUSD1:
                                 MEUSD2
          RETURN
.
          INC       MENUSEIO/INC
          INC       IBAOVRIO/INC

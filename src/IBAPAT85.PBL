.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAPAT85                                             *
.*                                                                            *
.*     FUNCTION:         PATIENT MASTERFILE LISTING                           *
.*                                                                            *
.*     DATE WRITTEN:     18/09/1986                                           *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*        V12.00.02 03/06/2025  J.Tan          TSK 0955096                    *
.*                  Added Alphanumeric visit number changes                   *
.*        V12.00.01 02/06/2025  J.Tan          TSK 0955096                    *
.*                  Added Alphanumeric visit number changes                   *
.*        V11.03.01 03/02/2023  PJ Le Febour   Task 0928557                   *
.*                  amended compare CLNO with FIFTY instead  of 55            *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.02.02 13/07/2011  Steve Armstrong   CAR 240722                  *
.*                  Further mods to cater for second given name as            *
.*                  part of PATGSRFD keys                                     *
.*        V10.02.01 22/06/2011  Steve Armstrong   CAR 240722                  *
.*                  Mods to cater for changes to PATGSRFD:                    *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.            *
.*                       - PTGSGNM2 added.                                    *
.*                       - all indexes changed in length.                     *
.******************************************************************************
.*        V9.04.01  22/12/2004  Davin         CAR 55344                       *
.*                  Fixed display of sex description (removed 1 space)        *
.******************************************************************************
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.******************************************************************************
.*        V9.02.02  01/11/2001  Greg Horvat                                   *
.*                  Recompiled for ACTCOMN1 - Added xtra option to the        *
.*                  parameter PNSWRACE                                        *
.*        V9.02.01  08/09/2001  Sandra Barcham                                *
.*                  Added DOB & SEX to patgsrnf                               *
.******************************************************************************
.*        V5.10.B01 27/03/2001  Glenn Saunders                                *
.*                  Remove access to PTCNSNDX and associated processing.      *
.*                  Remove all references to PATSUR file (old surname file).  *
.******************************************************************************
.*        V5.08.04  10.10.2000 Sandra Barcham                                 *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.03  23/11/2000  J.Tan                                         *
.*                  Fixed to initialise count variable                        *
.*        V5.08.02  29/08/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.******************************************************************************
.*        V5.07.01  18/05/1999  Caleb Sharman                                 *
.*                  Print Re-alignment                                        *
.*        V5.07.B02 28/01/1999  Jim Stathopoulos                              *
.*                  Report Modifications                                      *
.*                  09/11/1998  Glenn Berry      5.07 changes
.******************************************************************************
.*                    V5.01.01  17/07/89 Graeme Williams                      *
.*                              Convert to U/R as DIM 8                       *
.*                    V5.01.02  30/08/89 Graeme Williams                      *
.*                              New PATCOMN1 routine                          *
.*                    V5.01.03  20/03/90 J.Tan             SRF 104379         *
.*                              Fixed for CTYPEUR=2 AANNNN                    *
.*                    V5.01.04  23/07/90 Paul Duncan                          *
.*                              Implemented new surname file                  *
.*                    V5.01.122 25/01/91 Peter Eddey                          *
.*                              Included BOKRC1FD & recompiled                *
.*                    V5.01.123 06/08/91 i chung           SRF 109853         *
.*                              Fixed incorrect surnames appearing in surname *
.*                              range specified                               *
.*                    V5.01.124 06/12/91 i chung           SRF 111928         *
.*                              Replaced CPAGENO (FORM3) with PAGENO (FORM4)  *
.*                    V5.01.125 26/08/93  ROD                                 *
.*                              recompiled for NZHIS                          *
.*                    V5.01.126 25/05/1994 Ian Rutt                           *
.*                              Fixed Global Recompile Bugs                   * 
.*                    V5.01.127 31/08/1994  ROD       SRF 127550              *
.*                              added new type U/R                            * 
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       PATCONFD/INC 
          INC       PATCOMM/INC
.
          INC       BOKRC1FD/INC
          INC       OUTPREFD/INC
.
          INC       PATCALAG/INC
          INC       PATCODFD/INC            * System Codes file
          INC       PATDSCFD/INC
          INC       PATDHEAD/INC
          INC       PATGSRFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATVISFD/INC            * Patient Visits file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
+
.
. ------ <WORK VARIABLES> ------
.
AFIRST    DIM       30
ALAST     DIM       30
.
BIRDATE   DIM       10
BJDAY     FORM      3
CJDAY     FORM      3
.
CDESC     DIM       20
CODE      DIM       2
COUNT     FORM      8
.
DCOUNT    DIM       8
DDSTAT    DIM       18
DFIRST    DIM       8
DLAST     DIM       8
DPSTAT    DIM       19
DSCDATE   DIM       10
DSEX      DIM       8                                         * was 6  *C-49016
.
FIRST     DIM       8
FORM3     FORM      3
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
.
LAST      DIM       8
NMPNUMB   DIM       20
PADMNO    DIM       8
PAGENO    FORM      4
PGNAME16  DIM       16
PSNAME17  DIM       17
.
SCNDGNAM  DIM       40
SPURNO    DIM       8
STMAST    DIM       2
.
. ------ CONSTANTS> ------
.
CODECL    INIT      "CL"
CODED     INIT      "D "
CODEDD    INIT      "DD"
CODEHD    INIT      "HD"
CODEUNK   INIT      "UNK"
.
DFINISH   INIT      "Finish"
DSTART    INIT      "Start"
.
.
STAT0     INIT      "U/R IS VALID       "
STAT1     INIT      "U/R IS CANCELLED   "
STAT2     INIT      "REFER TO U/R       "
STAT3     INIT      "NOT YET ALLOCATED  "
STATMIS   INIT      "RECORD NOT ON FILE "
.
TILDA30   INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
.
PRGID     INIT      "IBAPAT85"
PRGNAM    INIT      "Patient Master Index Listing"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FIFTY9;*34,CTYPEUR
.
OPENGSR   DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
+
.
.        START OF PROGRAM
.
START     MOVE      ZERO,PAGENO
          MOVE      "60",CLNO
.
          BRANCH    CTYPEUR,ALPHURS,ALPHURS,ALPHURS
.
.         Numeric U/R Numbers.  This means we have an option for Missing U/R's
.
NUMRURS   DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Code Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Patient Surname Sequence":
                    *P1:7,*V2LON,THREE,*HOFF," = Missing U/R Numbers":
                    *P1:9,"Select Option :";
.
REKSTRT   KEYIN     *P17:9,*EL,*DV,UNDLN1,*P17:9,*V2LON,MOPTION;
.
          COMPARE   ZERO TO MOPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    MOPTION OF MASTNO,NAMENO,MASTMNO
          BEEP
          GOTO      REKSTRT
.
NOMAST    MATCH     "IBARSH99",PGM
          IF        @EQUAL
            CALL      PAGEND
            PRINT     *N,*N,"***  No Patients on file ***"
          ELSE
            DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** No Patients on file **",*W3;
          ENDIF
          GOTO      START
.
.         Alphanumeric U/R Numbers.  This means no option for Missing U/R's
.
ALPHURS   DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Code Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Patient Surname Sequence":
                    *P1:8,"Select Option :";
.
REKSTR1   KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,MOPTION;
.
          COMPARE   ZERO TO MOPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    MOPTION OF MASTNO,NAMENO
          BEEP
          GOTO      REKSTR1
+
.
.------ Redisplay for Surname search ------
.
REDISPS   COMPARE   THREE,MOPTION
          GOTO      REDISPS0 IF EQUAL
          DISPLAY   *P1:3,*EF,*P1:5,*V2LON,"*****    CODE SEQUENCE    *****";
          GOTO      REDISPS1
.
REDISPS0  DISPLAY   *P1:3,*EF,*P1:5,*V2LON,"*****  MISSING U/R NUMBERS  *****";
.
REDISPS1  COMPARE   SEVEN,FORM2
          GOTO      REDISPS2 IF NOT EQUAL
          DISPLAY   *P1:7,"Starting U/R Number : ";
          RETURN
.
REDISPS2  DISPLAY   *P1:7,"Starting U/R Number : ",*V2LON,DFIRST,*HOFF:
                    *P1:9,"Ending   U/R Number : ";
          RETURN
+
.
.        Print via Master file - Code Sequence
.
MASTMNO   DISPLAY   *P1:3,*EF,*P1:5,*V2LON,"*****  MISSING U/R NUMBERS  *****";
          GOTO      MASTN0
.
MASTNO    DISPLAY   *P1:3,*EF,*P1:5,*V2LON,"*****    CODE SEQUENCE    *****";
.
MASTN0    MOVE      TWENTY3,CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ZERO,SRCHSYS
          DISPLAY   *P1:7,"Starting U/R Number : ";
.
MASTN0A   MOVE      SP8,FIRST
          MOVE      SP8,DFIRST
          MOVE      SEVEN,CVERT
          MOVE      SEVEN,FORM2
.
          CALL      KURN                      * Keyin Starting U/R Number
          BRANCH    OVRCD,MASTN0B
          MOVE      PURNO,FIRST
          MOVE      PURNO,DFIRST
          LOAD      DFIRST,EXIT,DSTART
          LOAD      FIRST,EXIT,SP8
          DISPLAY   *P23:7,*EL,*V2LON,DFIRST;
          GOTO      MASTN1
.
MASTN0B   DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid U/R Number **    ";
          CALL      HITENTER
          MOVE      ZERO,OVRCD
          GOTO      MASTN0A
.
MASTN1    DISPLAY   *P1:9,"Ending   U/R Number : ";
.
MASTN1A   MOVE      SP8,LAST
          MOVE      SP8,DLAST
          MOVE      NINE,CVERT
          MOVE      NINE,FORM2
.
          CALL      KURN                      * Keyin Ending U/R Number
          BRANCH    OVRCD,MASTN1B
MASTN11   MOVE      PURNO,LAST
          MOVE      PURNO,DLAST
          LOAD      DLAST,EXIT,DFINISH
          LOAD      LAST,EXIT,TILDA30
          DISPLAY   *P23:9,*EL,*V2LON,DLAST;
          GOTO      MASTN1C
.
MASTN1B   MATCH     "IBARSH99",PGM
          IF        @EQUAL
            MATCH     PURNO,TILDA30
            IF        @EQUAL
              MOVE      ONE,EXIT
              GOTO      MASTN11
            ENDIF
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid U/R Number **    ";
          CALL      HITENTER
          MOVE      ZERO,OVRCD
          GOTO      MASTN1A
.
MASTN1C   MATCH     LAST,TILDA30
          GOTO      MASTN2 IF EQUAL
.
          MATCH     SP8,FIRST
          GOTO      MASTN2 IF EQUAL
          MATCH     FIRST,LAST
          GOTO      MASTN2 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending U/R No. must be >= Starting U/R No. **    ";
          CALL      HITENTER
          GOTO      MASTN1A
.
MASTN2    CALL      CONTQST
          BRANCH    CEXIT,MASTN4,MASTN3,START
          BEEP
          GOTO      MASTN2
.
MASTN3    DISPLAY   *P23:7,*EF;
          GOTO      MASTN0A
.
MASTN4    DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Listing",*HOFF:
                    *V2LON," **",*HOFF,*P45:24,"Patient U/R :";
.
          MOVE      ZERO,COUNT
          MOVE      FIRST,COUNT
          MOVE      FIRST,KEY8 
          CALL      RDMAST1
          BRANCH    OVRCD,NEXTN
          GOTO      HAVUR
.
NEXTN     CALL      RDKMAST1
          BRANCH    OVRCD OF ENDP
.
HAVUR     MOVE      PURNO,SPURNO              * save to check if Last U/R exists
.
          MATCH     LAST,PURNO
          GOTO      NEXTN2 IF LESS 
          MOVE      LAST,PURNO
.
NEXTN2    BRANCH    CTYPEUR,GOTUR,GOTUR,GOTUR
.
.        Numeric U/R.  Check if its a missing U/R.
.
          MOVE      COUNT,DCOUNT
          MATCH     DCOUNT,LAST
          GOTO      ENDP IF LESS
.
          MATCH     PURNO,DCOUNT
          GOTO      GOTUR IF EQUAL
          GOTO      GOTIT IF NOT LESS
.
NOTLUR    BRANCH    CTYPEUR,ENDP,ENDP,ENDP
.
          CALL      DISPNO
          DISPLAY   *P59:24,*V2LON,COUNT;
          ADD       ONE,COUNT
          GOTO      NEXTN2
.
GOTIT     MOVE      PURNO,DCOUNT
.
GOTUR     MATCH     LAST,PURNO
          GOTO      NOTCHK IF NOT EQUAL
.
.        If on last PURNO check if it is from Master file or not
.
          MATCH     LAST,SPURNO
          GOTO      NOTLUR IF NOT EQUAL
.
NOTCHK    CALL      DISPDA
          DISPLAY   *P59:24,*V2LON,PURNO;
          ADD       ONE,COUNT
          GOTO      NEXTN
.
.        Print via Surname sort key
.
NAMENO    DISPLAY   *P1:3,*EF,*P1:5,*V2LON:
                    "*****   SURNAME SEQUENCE    *****",*HOFF:
                    *P1:7,"Starting Surname : ":
                    *P1:9,"Ending   Surname : ";
.
MASTA0    KEYIN     *P20:7,*EL,*P20:9,*EF:
                    *P20:7,*V2LON,AFIRST;
          RESET     AFIRST
          GOTO      MASTA1 IF NOT EOS
          MOVE      SP70,AFIRST
          DISPLAY   *P20:7,*V2LON,DSTART;
.
MASTA1    KEYIN     *P20:9,*EL,*V2LON,ALAST;
          RESET     ALAST
          GOTO      MASTA2 IF NOT EOS
.
          MOVE      TILDA30,ALAST
          DISPLAY   *P20:9,*V2LON,DFINISH;
          GOTO      MASTA3
.
MASTA2    MATCH     AFIRST,SP70
          GOTO      MASTA3 IF EQUAL
          MATCH     AFIRST,ALAST
          GOTO      MASTA3 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Surname must be >= Starting Surname **    ";
          CALL      HITENTER
          GOTO      MASTA1
.
MASTA3    CALL      CONTQST
          BRANCH    CEXIT,MASTA4,MASTA0,START
          BEEP
          GOTO      MASTA3
+
.         Generate Listing
.
MASTA4    MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Listing",*HOFF:
                    *V2LON," **",*HOFF,*P31:24,"Surname :",*P65:24,"U/R :";
.
OTHSNDX   PACK      KEY98,AFIRST,SP70,SP70
          CALL      RDPTGSR2
          COMPARE   ZERO,OVRCD
          GOTO      OTHSNX2 IF EQUAL
          CALL      RSPTGSR2
.
OTHSNX1   CALL      RKPTGSR2
          BRANCH    OVRCD OF ENDP
.
OTHSNX2   MATCH     ALAST,GSRSNAM
          GOTO      NEXTA2  IF EQUAL
          GOTO      ENDP IF NOT LESS
.
NEXTA2    MOVE      GSRURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,NEXTA3
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,NEXTA3
.
          MATCH     GSRSNAM,PTMASNAM
          GOTO      NEXTA3 IF NOT EQUAL     * Ignore Aliases
.
          MATCH     GSRGNAM,PMPXFNAM
          GOTO      NEXTA3 IF NOT EQUAL     * Ignore Aliases
.
          MATCH     PTGSGNM2,PMPXSNAM
          GOTO      NEXTA3 IF NOT EQUAL     * Ignore Aliases
.
          MATCH     GSRDOB,PBDATE
          GOTO      NEXTA3 IF NOT EQUAL     * Ignore Aliases
.
          MATCH     GSRSEX,PSEX
          GOTO      NEXTA3 IF NOT EQUAL     * Ignore Aliases
.
          DISPLAY   *P41:24,*V2LON,PSNAME,*P71:24,PURNO;
          CALL      DISPDA
.
NEXTA3    GOTO      OTHSNX1                 * go back for next record
+
.        OVER condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,PAGENO
          GOTO      NOMAST IF EQUAL
          CALL      PAGEND
          PRINT     *N,*N,"***  End of Listing  ***"
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Listing Generation Completed **",*W2;
          GOTO      START
+
.
.        Print actual Data 
.
DISPDA    BRANCH    PSTAT OF PRNTP,PRNTP,PRNTP
.
          COMPARE   THREE,MOPTION           * Don't print PSTAT=0
          RETURN    IF EQUAL
.
PRNTP     COMPARE   FIFTY,CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      STAT0,DPSTAT
          LOAD      DPSTAT FROM PSTAT OF STAT1,STAT2,STAT3
.
          COMPARE   TWO,PSTAT
          GOTO      CNTPRT IF NOT EQUAL
.
          RESET     DPSTAT,13
          APPEND    PADMNO,DPSTAT
          RESET     DPSTAT
.
CNTPRT    UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BIRDATE
.
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.* ******************************************   End of Changes         *C-49016
.
.        Get latest Discharge information
.   
DSEXCON   MOVE      ZEROVISN,PADMNO            * PADMNO is used as a save
          MOVE      SP20,DDSTAT
          MOVE      SP20,DSCDATE
          MOVE      PURNO,PVIURNO
          PACK      KEY24 WITH PVIURNO,SP20
          CALL      RDSVISA2
.
CNXTAD1   CALL      RDKVISA2
          BRANCH    OVRCD OF NOAD
.
          MATCH     PURNO,PVIURNO
          GOTO      NOAD IF NOT EQUAL
.
.         Check if Inpatient
.
          COMPARE   THREE,PVITYPE
          GOTO      CNXTAD1 IF NOT EQUAL
.
.         Read the discharge file
.
          MOVE      PVIBILL,DADMNO
          MOVE      DADMNO,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD OF CNXTAD1
.
          MOVE      DADMNO,PADMNO
          GOTO      CNXTAD1
.
NOAD      MATCH     ZEROVISN,PADMNO
          GOTO      NODSC IF EQUAL
.
          MOVE      PADMNO,KEY8
          CALL      RDDSCH1
.
          PACK      KEY5 WITH CODED,DSTAT
          CALL      RDCODE1
          MOVE      TDESC,DDSTAT
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSCDATE
.
NODSC     MOVE      PSNAME,PSNAME17
          MOVE      PGNAME,PGNAME16
.
.         COMPARE   TWO,MOPTION
.         RETURN    IF EQUAL
.
          COMPARE   FIFTY,CLNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"|",PURNO,*10,"| ",PTITL,*18,PSNAME17,*35,PGNAME16:
                    *51,"|",DSEX,*60,"| ",BIRDATE,*72,"| ",DSCDATE:
                    *84,"| ",DDSTAT,*108,"| ",DPSTAT,*132,"|"
          ADD       ONE,CLNO
          RETURN
+
.
.        In Number Sequence - print all U/R s
.
DISPNO    COMPARE   FIFTY,CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      STATMIS,DPSTAT
.
          PRINT     *1,"|",DCOUNT,*10,"|",*51,"|",*60,"|":
                    *72,"|",*84,"|",*108,"| ",DPSTAT,*132,"|"
          ADD       ONE,CLNO
          RETURN
+
.
.        Last Line on the Page
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------":
                    "-----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.        Headings for output
.
HEAD      ADD       ONE,PAGENO
.
          COMPARE   ONE,PAGENO
          CALL      PAGEND IF NOT EQUAL
          CALL      IBACLOCK                               . pjl20230131
          CLOCK     TIME,CTIMEIS
.
          PRINT     *F,PRGID,*40,CNAME,*116,"DATE : ",CDATE:
                    *N,VERSION,*40,PRGNAM;
.
          BRANCH    MOPTION OF PRNTC,PRNTA,PRNTM
.
PRNTA     PRINT     "     (Surname Sequence)";
          GOTO      PRTCONT
.
PRNTC     PRINT     "     (Code Sequence)";
          GOTO      PRTCONT
.
PRNTM     PRINT     "     (Missing U/R Numbers)";
.
PRTCONT   PRINT     *116,"TIME :   ",CTIMEIS,*N:
                    *116,"PAGE : ",*130,PAGENO,*N
.
          BRANCH    MOPTION OF PRNTRN,PRNTRA,PRNTRN
.
PRNTRA    PRINT     *40,"Starting Surname : ";
          MATCH     AFIRST,SP70
          GOTO      PRNTRA1 IF EQUAL
          PRINT     AFIRST;
          GOTO      PRNTRA2
.
PRNTRA1   PRINT     DSTART;
.
PRNTRA2   PRINT     "  Ending Surname : ";
          MATCH     TILDA30,ALAST
          GOTO      PRNTRA3 IF EQUAL
          PRINT     ALAST,*N
          GOTO      PRTON
.
PRNTRA3   PRINT     DFINISH,*N
          GOTO      PRTON
.
PRNTRN    PRINT     *40,"Starting U/R Number : ",DFIRST:
                        "  Ending U/R Number : ",DLAST,*N
.
PRTON     CALL      PAGEND
.
          PRINT     *1,"| U/R ",*10,"| Title",*18,"Surname",*35,"Given Names":
                    *51,"| Sex",*60,"| Birth",*72,"| Discharge":
                    *84,"| Discharge Status",*108,"| Patient Status",*132,"|":
                    *N,"| Number",*10,"|",*51,"|",*60,"| Date",*72,"|   Date":
                    *84,"|",*108,"|",*132,"|"
.
          CALL      PAGEND
          MOVE      TEN1,CLNO
          RETURN
+
GETSVAR   RETURN                  * label needed by surname search routine *
+
.
ENDIT     CHAIN     PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCAGE
          INC       SEXDSCIO
          INC       PATCOMN1
          INC       PATSNDX
          INC       PATSNX2
.
          INC       BOKRC1IO/INC
          INC       OUTPREIO/INC
.
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATGSRIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       NZCOMPIO/INC
AGECHK
CLPATMAS  RETURN

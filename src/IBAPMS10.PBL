.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   IBAPMS10                                                    *
.* Desc      :   Generate Single Patient OPV request for Eclipse             *
.*****************************************************************************
.* Date      :   28/07/2010                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will allow the user to keyin a valid U/R       *
.*               number, then create a file with a corresponding OPV         *
.*               request, ready for submission to the Server Adaptor.        *
.* Modifications:                                                            * 
.*               V10.05.01  12/11/2014 Peter Vela CAR 294157                 *
.*                          New Program                                      *
.*****************************************************************************
.
.>>>>>>   Add extract error writing to file
.
.>>>>>>   Resolve extract file naming convention and change code accordingly
.>>>>>>   Might also need to add filename to pmsopvaf so there is a link to
.>>>>>>   the physical file
.
.>>>>>>   Need to change PMSOVRFD to be written to by this program along
.>>>>>>   with any errors (in a related new FD).  Should there be a status
.>>>>>>   to indicate where the extract is at as well.
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       BOKRC1FD/INC
          INC       NHIMASFD/INC
          INC       NZHISIFD/INC
          INC       OUTPREFD/INC
          INC       PATALRFD/INC
          INC       PATCALAG/INC
          INC       PATCOMM/INC
          INC       PATCONFD/INC
          INC       PATCRTFD/INC
          INC       PATDHEAD/INC
          INC       PATGSRFD/INC
          INC       PATLCNFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PMSOVRFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.>>>>>    INC       PMSXXXFD/INC
.
.
OPVXFILE  FILE      ASCII,FIXED=150
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
BJDAY     FORM      3
.
CJDAY     FORM      3
CMDLINE   DIM       80
COUNT     FORM      2
CURRDATE  DIM       8                  * current date (ccyymmdd)
.
ERORDESC  DIM       40   *****>>>>> same length as FD field length ****
FILENAME  DIM       8
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
.
FORM3     FORM      3
.
LONGFNAM  DIM       12
.
NMPNUMB   DIM       20
.
SCNDGNAM  DIM       40
.
. ----- Eclipse Data Variables -----
.
. HDR segment       Len    Pos
.-------------      ---    ---
.~        DIM       1        1
.HDR      DIM       3        2
HDRSEG01  DIM       8        5    * Location Id
HDRSEG02  DIM       10      13    * Certificate Id
HDRSEG03  DIM       60      23    * Sender Id
HDRSEG04  DIM       1       83    * Test/Production Flag (T/P)
. End of Segment            84
.
. OPV segment       Len    Pos
.-------------      ---    ---
.~        DIM       1        1
.OPV      DIM       3        2
OPVSEG01  DIM       8        5    * EarliestDateOfService
OPVSEG02  DIM       3       13    * OPVTypeCde
OPVSEG03  DIM       40      16    * PatientFirstName
OPVSEG04  DIM       40      56    * PatientFamilyName
OPVSEG05  DIM       8       96    * PatientDateOfBirth
OPVSEG06  DIM       8      104    * ServicingProviderNum
OPVSEG07  DIM       10     112    * PatientMedicareCardNum
OPVSEG08  DIM       1      122    * PatientReferenceNum
OPVSEG09  DIM       8      123    * PatientURNum
. End of Segment           131
.
.
. PROGRAM CONSTANTS
. -----------------
SP40      INIT      "                                        "
SP60      INIT      "                              ":
                    "                              "
TILDA01   INIT      "~"
TXT       INIT      ".txt"
.
.
PRGID     INIT      "IBAPMS10"
PRGNAM    INIT      "Generate Single Patient OPV Request"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
          BRANCH    EXIT,MAIN9999          * not using EDI
.
MAIN0500  CALL      GTUR0000               * keyin the U/R number
          BRANCH    EXIT,MAIN9999          * nothing entered
.
          CALL      PROC0000               * process
          BRANCH    EXIT,MAIN0500          * error
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN1000:        * yes
                          MAIN0500:        * no
                          MAIN9999         * cancel
.
MAIN1000  CALL      EXTR0000               * extract data into file
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.* Returns:    EXIT  0 = ok to continue                                      *
.*                   1 = not using eclipse                                   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmsovraf";
          OPEN      PMSOVRA1,"pmsovraf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patlcnaf";
          OPEN      PATLCNA1,"patlcnaf"
.
          DISPLAY   *P64:24,"patcrtaf";
          OPEN      PATCRTA1,"patcrtaf"
.
.>>>>>    DISPLAY   *P64:24,"pmsxxxaf";
.>>>>>    OPEN      PMSXXXA1,"pmsxxxaf"
.
          DISPLAY   *P64:24,"patpr1af";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*115,PTCNUEDI      * Using EDI
          READ      CONTROLF,HUND20;*1,PTCNOPVO:       * OPV outbound path
                                    *121,PTCNUTPO      * OPV test or prod'n
.
          COMPARE   ONE,PTCNUEDI
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Hospital not using Eclipse.  ";
            CALL      HITENTER
            GOTO      INIT9100
          ENDIF
.
          MOVE      "eclovsrv",FILENAME
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9100  MOVE      ONE,EXIT
.
INIT9999  RETURN
+
.*****************************************************************************
.*                               GTUR0000              Called by: MAIN0000   *
.*                Keyin the patient U/R number                               *
.* Returns:  EXIT   0 = Valid U/R entered                                    *
.*                  1 = Nothing entered                                      *
.*****************************************************************************
.
GTUR0000  DISPLAY   *P1:4,*EF,"U/R Number     :"
          MOVE      TEN8,CCOL
          MOVE      FOUR,CVERT
          MOVE      ZERO,SRCHSYS
          MOVE      THREE,SRCHOPT
          CALL      KURN                         * get U/R
          BRANCH    EXIT,GTUR9100                * nothing entered
          BRANCH    OVRCD,GTUR9700               * U/R does not exist
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,GTUR9800
.
          MOVE      ZERO,EXIT
          GOTO      GTUR9999
.
GTUR9100  MOVE      ONE,EXIT
          GOTO      GTUR9999
.
GTUR9700  DISPLAY   *P1:24,*EL,*B,"U/R number not on file.  ";
          GOTO      GTUR9900
.
GTUR9800  DISPLAY   *P1:24,*EL,*B,"pmxpx2af record not on file.  ";
GTUR9900  CALL      HITENTER
          GOTO      GTUR0000
.
GTUR9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Load the OPV variables with the relevant PMI data                  *
.* Returns: EXIT   0 = Ok to continue                                        *
.*                 1 = error(s) encountered                                  *
.*****************************************************************************
.
.         Load PatientDateOfBirth
.
PROC0000  MATCH     SP8,PBDATE
          IF        @EQUAL
            MOVE      "OPV.01-PatientDateOfBirth is blank",ERORDESC
            CALL      EROR0000
            GOTO      PROC9100
          ELSE
            UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
            PACK      OPVSEG05,CDAY,CMON,CCENT,CYEAR
          ENDIF
.
.         Load PatientFamilyName
.
          PACK      OPVSEG04,PSNAME,SP30,SP10    * Surname
          MATCH     SP40,OPVSEG04
          IF        @EQUAL
            MOVE      "OPV.02-PatientFamilyName is blank",ERORDESC
            CALL      EROR0000
            GOTO      PROC9100
          ENDIF
.
.         Load PatientFirstName
.
          PACK      OPVSEG03,PMPXFNAM
          MATCH     SP40,OPVSEG03
          IF        @EQUAL
            MOVE      "OPV.03-PatientFirstName is blank",ERORDESC
            CALL      EROR0000
            GOTO      PROC9100
          ENDIF
.
.         Load PatientMedicareCardNum & PatientReferenceNum
.
          MATCH     SP10,PMEDI
          IF        !@EQUAL
            MOVE      PMEDI,OPVSEG07
            MATCH     SP2,PTMXMCCD
            IF        @EQUAL
              MOVE      "OPV.04-PatientReferenceNum is blank",ERORDESC
              CALL      EROR0000
              GOTO      PROC9100
            ELSE
              MOVE      PTMXMCCD,DIM2
              SQUEEZE   DIM2                     * remove blanks
              MATCH     "0",DIM2                 * leading zero ?
              IF        @EQUAL
                UNPACK    DIM2,ANS,OPVSEG08      * yes - take second char.
              ELSE
                MOVE      DIM2,OPVSEG08
              ENDIF
            ENDIF
          ENDIF
.
.         Load remaining variables
.         
          MOVE      SP8,OPVSEG01       * EarliestDateOfService
          MOVE      "PVM",OPVSEG02     * OPVTypeCde
          MOVE      SP8,OPVSEG06       * ServicingProviderNum
          MOVE      PURNO,OPVSEG09     * PatientURNum
.
          DISPLAY   *P1:10,"EarliestDateOfService :[",*V2LON,OPVSEG01,*HOFF,"]":
                    *P1:11,"OPVTypeCde            :[",*V2LON,OPVSEG02,*HOFF,"]":
                    *P1:12,"PatientFirstName      :[",*V2LON,OPVSEG03,*HOFF,"]":
                    *P1:13,"PatientFamilyName     :[",*V2LON,OPVSEG04,*HOFF,"]":
                    *P1:14,"PatientDateOfBirth    :[",*V2LON,OPVSEG05,*HOFF,"]":
                    *P1:15,"ServicingProviderNum  :[",*V2LON,OPVSEG06,*HOFF,"]":
                    *P1:16,"PatientMedicareCardNum:[",*V2LON,OPVSEG07,*HOFF,"]":
                    *P1:17,"PatientReferenceNum   :[",*V2LON,OPVSEG08,*HOFF,"]":
                    *P1:18,"PatientURNum          :[",*V2LON,OPVSEG09,*HOFF,"]":
                    *P1:24
.
          MOVE      ZERO,EXIT
          GOTO      PROC9999
.
PROC9100  MOVE      ONE,EXIT
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               EXTR0000              Called by: MAIN0000   *
.*        Extract the data into a file ready for sending to Eclipse          *
.*****************************************************************************
.
EXTR0000  PREP      OPVXFILE,FILENAME
          CLOSE     OPVXFILE,DELETE
          PREP      OPVXFILE,FILENAME
.
          CALL      IBACLOCK                   * get current date
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          CALL      WHDR0000                   * write HDR segment
          BRANCH    EXIT,EXTR9999              * error occurred
.
          WRITE     OPVXFILE,SEQ;TILDA01,ANSO,ANSP,ANSV:
                                 OPVSEG01,OPVSEG02,OPVSEG03,OPVSEG04,OPVSEG05:
                                 OPVSEG06,OPVSEG07,OPVSEG08,OPVSEG09
.
.         Write trailer to separate each OPV request
.
          WRITE     OPVXFILE,SEQ;TILDA01,ANST,ANSL,ANSR
.
          CLOSE     OPVXFILE                     * close text file
.
          CALL      FNAM0000
          CALL      MFIL0000                     * move file to outbound dir.
.
EXTR9999  RETURN
+
.******************************************************************************
.*                             WHDR0000            Called by: PROC0000        *
.*                  Write the header segment                                  *
.* Returns: EXIT   0 = no errors occurred                                     *
.*                 1 = one or more errors occurred                            *
.******************************************************************************
.
.         Get the first active location - it is irrelevant which location it
.         is, even for a multi-hospital environment.
.
WHDR0000  MOVE      SP8,KEY8
          CALL      RSPTLCN1                     * position at start of file
WHDR0500  CALL      RKPTLCN1                     * read next record
          IF        OVRCD = 1
            MOVE      "HDR.01-Current Location not set up",ERORDESC
            CALL      EROR0000
            GOTO      WHDR9100
          ENDIF
.
          MATCH     ANSA,PTLNACTV                * active location ?
          GOTO      WHDR0500 IF NOT EQUAL        * no - ignore record
.
.         We have an active location
.
          MATCH     SP8,PTLNLOCN                 * blank location ?
          GOTO      WHDR0500 IF EQUAL            * yes - ignore record
.
          MOVE      PTLNLOCN,HDRSEG01            * set location id
.
.         Get the certificate that covers today
.
WHDR2000  PACK      KEY16,HDRSEG01,CURRDATE
          CALL      RDPTCTI1                     * record found for today ?
          COMPARE   ZERO,OVRCD
          GOTO      WHDR3000 IF EQUAL            * yes - use this certificate id
.
          CALL      RPPTCTI1                     * no - read previous record
          IF        OVRCD = 0
            MATCH     CURRDATE,PTCITDTE          * within date range ?
            GOTO      WHDR3000 IF NOT LESS       * yes - use this certificate id
          ENDIF
.
          MOVE      "HDR.02-Current Certificate not available",ERORDESC
          CALL      EROR0000
          GOTO      WHDR9100
.
WHDR3000  MATCH     SP10,PTCIIDEN
          IF        @EQUAL
            MOVE      "HDR.03-Certificate Identifier blank",ERORDESC
            CALL      EROR0000
            GOTO      WHDR9100
          ELSE
            MOVE      PTCIIDEN,HDRSEG02          * load certificate identifier
          ENDIF
.
          MATCH     SP60,PTCISNID
          IF        @EQUAL
            MOVE      "HDR.04-Sender Id blank",ERORDESC
            CALL      EROR0000
            GOTO      WHDR9100
          ELSE
            MOVE      PTCISNID,HDRSEG03          * load sender id
          ENDIF
.
          MATCH     SP1,PTCNUTPO
          IF        @EQUAL
            MOVE      "HDR.05-Test/Production parameter blank",ERORDESC
            CALL      EROR0000
            GOTO      WHDR9100
          ELSE
            MOVE      PTCNUTPO,HDRSEG04
          ENDIF
.
          WRITE     OPVXFILE,SEQ;TILDA01,ANSH,ANSD,ANSR:
                                 HDRSEG01,HDRSEG02,HDRSEG03,HDRSEG04
          MOVE      ZERO,EXIT
          GOTO      WHDR9999
.
WHDR9100  MOVE      ONE,EXIT
.
WHDR9999  RETURN
+
.
GETSVAR   RETURN
+
.*****************************************************************************
.*                               EROR0000              Called by: PROC0000   *
.*        An error has occurred due to insufficient data for a valid OPV,    *
.*        so write the error back to pmsxxxaf and finish.                    *
.*****************************************************************************
.
EROR0000  PACK      ERORDESC,ERORDESC,SP40
.
.>>>>>    Load data fields
.>>>>>    CALL      WRPMXXX1
.
EROR9999  RETURN
+
.*****************************************************************************
.*                               MFIL0000        Called by: PROC0000         *
.*       Move extract file from 'cd' directory to OPV outbound directory     *
.*****************************************************************************
.
MFIL0000  MOVE      ZERO,HLEF
          CALL      GETSCR00
          STRIP     PTCNOPVO
          CLEAR     CMDLINE
          APPEND    "mv ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    ".txt",CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    PTCNOPVO,CMDLINE
          APPEND    LONGFNAM,CMDLINE
          APPEND    TXT,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          CALL      PUTSCR00
.
          MATCH     "0       ",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to move extract file.  ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ELSE
            MOVE      ZERO,EXIT
          ENDIF
.
MFIL9999  RETURN
+
.*****************************************************************************
.*                               FNAM0000                                    *
.*           Get the next OPV batch filename                                 *
.*****************************************************************************
.
FNAM0000  MOVE      "123",PRXCODE                * System Lock Sector 123
          CALL      GETSLK00                     * Get System Lock-Sector 123
.
          READ      CONTROLF,HUND23;*123,PTCNOPBD,*131,PTCNOPBC
.
.         If the opv batch date field is blank, then update it with
.         today's date and set the opv batch count to 1.
.
          REP       " 0",PTCNOPBD
          MATCH     "00000000",PTCNOPBD
          IF        @EQUAL
            MOVE      CURRDATE,PTCNOPBD
            MOVE      "0002",PTCNOPBC
            PACK      LONGFNAM,CURRDATE,ZERO,ZERO,ZERO,ONE
            REP       " 0",LONGFNAM
            GOTO      FNAM8000
          ENDIF
.
.         If the opv batch date is less than today's date, then update it with
.         today's date and set the opv batch count to 1.
.
          MATCH     CURRDATE,PTCNOPBD            * opv batch date < curr. date ?
          IF        @LESS
            MOVE      CURRDATE,PTCNOPBD
            MOVE      "0002",PTCNOPBC
            PACK      LONGFNAM,CURRDATE,ZERO,ZERO,ZERO,ONE
            GOTO      FNAM8000
          ENDIF
.
.         The opv batch date is the same as today's date, so increment
.         the count by 1 and update.
.
          MOVE      ZERO,FORM4
          MOVE      PTCNOPBC,FORM4
          PACK      LONGFNAM,PTCNOPBD,FORM4
          REP       " 0",LONGFNAM
          ADD       ONE,FORM4
          MOVE      FORM4,PTCNOPBC
.
FNAM8000  WRITAB    CONTROLF,HUND23;*123,PTCNOPBD,*131,PTCNOPBC
.
          CALL      RELSLK00                    * Release System Lock-Sector 123
.
FNAM9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       CALCAGE
          INC       CLPATMAS
          INC       NZHISIIO
          INC       NZIBSRCH
          INC       PATCOMN1
          INC       PATSNDX
          INC       PATSNX2
.
          INC       BOKRC1IO/INC
          INC       NHIMASIO/INC
          INC       OUTPREIO/INC
          INC       PATCRTIO/INC
          INC       PATGSRIO/INC
          INC       PATLCNIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PMSOVRIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
.>>>>>>   INC       PMSXXXFD/INC

.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:     IBABIL81                                             *
.*                                                                            *
.*     FUNCTION:         DETAILED DISEASE REPORT                              *
.*                                                                            *
.*     DATE WRITTEN:     19/01/87                                             *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*       V12.00.01  16/05/2025  J.Tan          TSK 0955096                    *
.*                  Added alpanumeric visit number generation                 *
.*       V11.05.01  24/03/2025  Davin         TSK 0956210                     *
.*                  Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*       V11.02.01  31/03/2022  Davin         TSK 0917793                     *
.*                  Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*       V10.13.01  05/12/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (PSTROL) after processing               *
.*       V10.11.01  01/08/2017  J.Tan          TSK 0841947                    *
.*                  Pouplate ICDRDDTE - to validate ICD codes                 *
.*       V10.10.01  17/03/2017  Davin          TSK 0833093     HDP 2017/2018  *
.*                  Add ICD10 Ed.10 - Go-live Date PTCNGDVX (Sect.110 Pos.85) *
.*       V10.08.01  09/05/2016  Ebon Clements TSK 0818082                     *
.*                  Set exit to ZERO at KHOS0000                              *
.*       V10.06.01  17/03/2015  Davin         CAR 311669      HDP 2015/16     *
.*                  Add PTCNGDV9 for ICD10 Ed.9 Go-live Date (Sect.110 Pos.77)*
.*       V10.05.01  02/02/2015  Ania P        CAR 261630                      *
.*                  Removed use of PORT for temp file naming                  *
.*       V10.03.01  23/04/2013  Davin         CAR 284420      HDP 2013/14     *
.*                  Add PTCNGDV8 for ICD10 Ed.8 Go-live Date (Sect.110 Pos.69)*
.*       V10.02.01  23/03/2011  Mike Laming   CAR 240107                      *
.*                  Remove PTCNUCEP PATMRDFD/IO and PATMROFD/IO               *
.*                  Mods for Key changes in PATECDFD & PATECOFD - file change *
.*       V10.00.02  26/05/2010  Mike Laming   CAR 222491                      *
.*                  Change page header to "PRIMARY DISEASE :" at HEADNT       *
.*       V10.00.01  07/04/2010  Mike Laming   CAR 219246      HDP 2010        *
.*                  Add PTCNGDV7 for ICD10 Ed.7 Go-live Date (Sect.110 Pos.61)*
.*        V9.09.01  20/03/2008  Mike Laming   CAR 163918    HDP 2008          *
.*                  Add PTCNGDV6 for ICD10 Ed.6 Go-live                       *
.*        V9.06.01  15/05/2006  Mike Laming   CAR 105244      2006 HDP        *
.*                  ADD PTCNGDV5 for ICD10 Ed.5 Go-live                       *
.*        V9.04.01  23/08/2004  Lina Vo CAR 52901                             *
.*                  Added Multi-Hospital processing                           *
.*        V9.03.03  23/06/2004  Henry Son        CAR 49037                    *
.*                  ICD10 Ed.4 changes - add PTCNGDV4.                        *
.*        V9.03.02  30/12/2003  Sylvek Litewka  SRF 20222                     *
.*                  Replaced calls to RDPT10D1 (ICD10 Disease V2 file read)   *
.*                  with RDPTICD1 (latest ICD10 version read).                *
.*        V9.03.01  05/12/2003  Sylvek Litewka  SRF 45729                     *
.*                  Recompiled for changes made to PATECDFD.                  *
.* -------------------------------------------------------------------------- *
.*        V9.02.01  29/08/2002  Jill Habasque  SRF 17573                      *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V5.10.B01 02/04/2001  Steve Downing  SRF 9521                       *
.*                  Mods to check for ICD10 code before mapping to ICD9       *
.******************************************************************************
. *       V5.08.02  10.10.2000 Sandra Barcham                                 *
. *                 Recompiled for WEB Report Scheduler                       *
. *       V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *       V5.08.B01 15/03/2000  Greg Horvat                                   *
. *                 Changed to get the coding from the episode coding files   *
. *                   V5.07.B03 25/01/1999 Jim Stathopoulos                   *
. *                             Report Modifications                          *
. *                   V5.07.B02 01/12/1998  Steve Armstrong                   *
. *                             Removed reference to PATIC9FD                 *
. *                   V5.07.B01 13/11/1998 Jim Stathopoulos                   *
. *                             507 Changes                                   *
. *****************************************************************************
. *                       V3.01 09/09/88 G.Williams                           *
. *                             Add option for Bay Sequence                   *
. *                       V5.00 20/03/89 J.Tan                                *
. *                             Fixed PATIOMDI                                *
. *                       V5.01 16/05/89 DIG                                  *
. *                             Changed to standard and                       *
. *                             modified print for 8 digit                    *
. *                             U/R's etc                                     *
. *                       V5.02 27/05/89 J.Tan                                *
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMI1FD).     SRF 100750                    *
. *                       V5.03 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                    V5.01.01 07/07/89 K.Peak                               *
. *                             Changed Adm & U/R to 8 chrs                   *
. *                             11/08/89 Graeme Williams                      *
. *                             The "P", "S", "C" and "A"                     *
. *                             prefixs are now parameters                    *
. *                    V5.01.02 07/09/90 D.D.i.Paola                          *
. *                             Added disease code ranges                     *
. *                                           SRF 106354                      *
. *                             02/10/90 D.D.i.Paola                          *
. *                             Took out date confirmation                    *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                             Recompiled for changes to                     *
. *                             PATMX1FD                                      *
. *                   V5.01.122 29/01/91 J.Tan                                *
. *                             Fixed the grand total file                    *
. *                             to use key8 instead of key6                   *
. *                   V5.01.123 28/03/91 D.Di.Paola                           *
. *                             Fixed the unpacking of U/R                    *
. *                             No. to get the correct bay                    *
. *                             code....   SRF 108243                         *
. *                   V5.01.124 25/06/91 J.Tan   SRF 108361                   *
. *                             Added summary only option                     *
. *                   V5.01.125 04/09/91 Graeme Williams                      *
. *                             Print other primary disease                   *
. *                             codes in "Other" column                       *
. *                             SRF 110395                                    *
. *                   V5.01.126 28/01/93 ROD                                  *
. *                             Added new ICD9 (Dudley)                       *
. *       V5.01.127 04.03.93 Neeriem "I Hate Support" Dye                     *
. *                 Modify to use standard routine KEYDATE                    *
. *       V5.01.128 07/05/93 ROD                                              *
. *                 Replace PTCNACPS with PTCNUCEP                            *
. *       V5.01.128 21/06/93  WHIRLPOOL   SRF 123343                          *
. *                 Fixed KEY on read for PATMRDFD                            *
. *       V5.02.01  26/04/1995  Greg Horvat      SRF 131005                   *
. *                 Fixed display & keyin problems                            *
. *                                                                           *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATDO1FD/INC
          INC       PATECDFD/INC
          INC       PATICDFD/INC     * OPERATION FILE
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATBAYFD/INC
          INC       PATHSPFD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
+
.               WORK AREA
.------------------------------------------------------------------
.Name     Type       Length      Physical      Start
.----     ----       ------      --------      -----
BCODE     DIM           3              3          1
.PTEDCODE DIM           9              9          4
XDAADMNO  DIM           8              8         13
DUNIQUE   DIM           7              7         21
PNAME     DIM          22             22         28
.AURNO    DIM           8              8         50
.DPBDATE  DIM           8              8         58
.DPSEX    DIM           1              1         66
.DADATE   DIM           8              8         67
.DDDATE   DIM           8              8         75
NBRDAYS   FORM          4              3         83
DNAME     DIM          22             22         86
.
.  END OF RECORD                                108
. 
.  redefine form fields from key
.  -----------------------------
.Name     Type       Length      Start
.----     ----       ------      -----
.AADMNO    DIM           8          13
UNIQUE    FORM          7          19
.------------------------------------------------------------------
.
PSKIP     FORM    1
PSUMM     FORM    1
LSTADMN   DIM     8
SAVCODE   DIM     9
RTDAYS    FORM    4
SKEY30    DIM     30
.
CMDISP    DIM     1
CMDISS    DIM     1
CMDISC    DIM     1
CMDISA    DIM     1
PTCNI10D  DIM     8
PTCNGDV3  DIM     8
PTCNGDV4  DIM     8                         * Ed.4 Golive date
PTCNGDV5  DIM     8                         * Ed5 golive date
PTCNGDV6  DIM     8                         * Ed.6 Go-live date
PTCNGDV7  DIM     8                         * Ed.7 Go-live date 
PTCNGDV8  DIM     8                         * Ed.8 Go-live date
PTCNGDV9  DIM     8                         * Ed.9 Go-live date
PTCNGDVX  DIM     8                         * Ed.10 Go-live date
PTCNGDX1  DIM       8                       * Go live date for ICD10 Ed.11
PTCNGDX2  DIM       8                       * Go live date for ICD10 Ed.12
PTCNGDX3  DIM       8                       * Go live date for ICD10 Ed.13
.
CODEDESC  DIM     48
CODES1    DIM     9
CODES2    DIM     9
COUNT     FORM    1
.
DIM6      DIM     6
DIM8      DIM     8
DIM18     DIM     18
DIM60     DIM     60
DNAMEDIS  DIM     21
PNAMEDIS  DIM     21
DBAY      INIT    "( Bay Sequence )    "
DDIS      INIT    "( Disease Sequence )"
.
.
.      SORT VARIABLES
.
GNDFILE    IFILE     SQL, FIXED=108, KEY="U1-8"
PSTROL     FILE      ASCII, FIXED=256
TEMPFILE   IFILE     SQL, FIXED=108, KEY="U1-27"
CMDLINE    DIM       50
FNAMER     DIM       8
FNAMET     DIM       8
FNAMEG     DIM       8
OPCND      FORM      1
STATUS     FORM      1
STRTCODE   DIM       9
.
.      EXTRA VARIABLES
.
XDAY1      DIM     2
XMON1      DIM     2
XYEAR1     DIM     2
XCENT1     DIM     2
.
XDAY2      DIM     2
XMON2      DIM     2
XYEAR2     DIM     2
XCENT2     DIM     2
.
.
FIRSTFLG  FORM    1
FROMDATE  DIM     8        * FROM DATE IN (CCYYMMDD) FORMAT
TODATE    DIM     8        * TO DATE IN (CCYYMMDD) FORMAT
ENDDATE   DIM     8        * End Discharge Date in (CCYYMMDD) format
ENDCODE   DIM     9
.
WDATE1    DIM     8        *  used in nhospday
WDATE2    DIM     8        *  used in nhospday
.
ADMDATE   DIM     10           DD/MM/CCYY OF LDATE
DISDATE   DIM     10           DD/MM/CCYY OF RDATE
BIRDATE   DIM     10           DD/MM/CCYY OF RDATE
.
JYEAR     FORM    2
JDAYS     FORM    3
.
CALDAYS   FORM    4
.
DISDAYS   FORM    6
DISADMS   FORM    5
.
TOTADMS   FORM    6
TOTDAYS   FORM    6
.
AVGLOS    FORM    6.2
.
PRGID     INIT      "IBABIL81"
PRGNAM    INIT      "Detailed Disease Report"
VERSION   INIT      "V12.01.00"
+
.
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patbaysf";
          OPEN      PATBAYS1,"patbaysf"
.
          DISPLAY   *P64:24,"patbaysf";
          OPEN      PATBAYS2,"patbaysf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*235,CMDISP,CMDISS,CMDISC,CMDISA
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"paticddf";
          CALL      OPICD1
.
+
.
......... START OF PROGRAM
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEG
+
.
......... MAIN MENU
.
START     HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Disease Code Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Bay Sequence"
.
REKOPT    KEYIN     *P1:8,"Select Option : ",*EF,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF DISSQ,BAYSQ
.
          BEEP
          GOTO      REKOPT
.
DISSQ     DISPLAY   *P52:2,*V2LON,"- Disease Sequence "
          GOTO      GETDTES
.
BAYSQ     DISPLAY   *P52:2,*V2LON,"- Bay Sequence "
.
......... Initialize variables
.
GETDTES   MOVE      ZERO,NBRDAYS
          MOVE      ZERO,DISADMS
          MOVE      ZERO,DISDAYS
          MOVE      ZERO,UNIQUE
          MOVE      ZERO,TOTADMS 
          MOVE      ZERO,TOTDAYS
.
          DISPLAY    *P1:10,*EF:
                     *P1:10,"Keyin From Date :":
                     *P1:11,"Keyin To   Date :"
+
.
......... KEYIN FROM DATE
.
KTODAT    DISPLAY    *P20:10,*EL,*P20:11,*EF
          MOVE       ZERO,IDAY
          MOVE       ZERO,IMON
          MOVE       ZERO,IYEAR
          MOVE       ZERO,ICENT
          MOVE       TEN,CVERT
          MOVE       "19",CCOL
          CALL       KEYDATEX
.
          COMPARE    ZERO TO IDAY
          GOTO       START IF EQUAL
.
          PACK       FROMDATE WITH ICENT,IYEAR,IMON,IDAY
          REP        " 0",FROMDATE
          UNPACK     FROMDATE INTO XCENT1,XYEAR1,XMON1,XDAY1
.
......... KEYIN TO DATE 
.
          MOVE       ZERO,IDAY
          MOVE       ZERO,IMON
          MOVE       ZERO,IYEAR
          MOVE       ZERO,ICENT
          MOVE       TEN1,CVERT
          MOVE       "19",CCOL
          CALL       KEYDATEX
.
          COMPARE    ZERO TO IDAY
          GOTO       KTODAT IF EQUAL
.
          PACK       TODATE WITH ICENT,IYEAR,IMON,IDAY
          REP        " 0",TODATE
          UNPACK     TODATE INTO XCENT2,XYEAR2,XMON2,XDAY2
.
          MATCH      FROMDATE TO TODATE          * 'FROM' DATE MUST BE 
          GOTO       INVDATG IF LESS             * >= 'TO' DATE
+
.
.---------- if dates ok then continue -------------
.
CNTCHK    MOVE         ONE,PSKIP             * default to 'no'
          CALL         DCOD0000              * enter disease code range
          BRANCH       EXIT,CHECK
.
SUMCHK    MOVE         ONE,PSUMM
          KEYIN        *P1:16,*EF,"Print Summary Only (":
                       *V2LON,"Y",*HOFF,"/":
                       *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          REP          UPPLOW,ANS
.
          MATCH        "Y",ANS
          GOTO         ENDS1N IF EQUAL
.
          MATCH        "N",ANS
          GOTO         ENDS0N IF EQUAL
.
          BEEP
          GOTO         SUMCHK
.
ENDS0N    MOVE         ZERO,PSUMM
          KEYIN        *P1:18,*EF,"Would You Like Each Disease":
                       " On A Separate Page (":
                       *V2LON,"Y",*HOFF,"/":
                       *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          REP          UPPLOW,ANS
.
          MATCH         "Y",ANS
          GOTO          ENDS1Y IF EQUAL
.
          MATCH         "N",ANS
          GOTO          ENDS1N IF EQUAL
.
          BEEP
          GOTO          ENDS0N
.
ENDS1N    MOVE          ONE,PSKIP 
          GOTO          CHECK
.
ENDS1Y    MOVE          ZERO,PSKIP
          GOTO          CHECK
.
CHECK     CALL        KHOS0000
          BRANCH      EXIT,GETDTES
.
          CALL        CONTQST                 * Ok To Continue (Y/N/C) ?
          BRANCH      CEXIT,TRANNO,GETDTES,START
.                           Yes    No      Cancel
.
INVDATG   DISPLAY        *P1:24,*EL,*B,"From Date Must Be Greater Than To Date ";
          CALL           HITENTER
          GOTO           KTODAT
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The Date Ranges                           *
.******************************************************************************
KHOS0000  MOVE      ZERO,EXIT
.
          COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:20,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      "20",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:20,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.***********************************************************************
.*                           DCOD0000                                  *
.*                    Enter disease code range                         *
.***********************************************************************
DCOD0000  DISPLAY   *P1:13,*EL,"Disease Code From : ":
                    *P1:14,*EL,"Disease Code To   : ";
.
. ----- Get From Disease Code -----
.
DCOD1000  MOVE      ZERO,EXIT
          MOVE      SP9,STRTCODE
          MOVE      SP9,CODES1
          PACK      CODEDESC,SP30,SP20
          DISPLAY   *P21:13,*EL,UNDLN9;
          KEYIN     *P21:13,*V2LON,STRTCODE,*HOFF;
.
          RESET     STRTCODE
          GOTO      DCOD1100 IF EOS 
.
          CMATCH    QUEST,STRTCODE
          GOTO      DCOD1000 IF EQUAL
.
          ENDSET    STRTCODE
          APPEND    SP9,STRTCODE
          RESET     STRTCODE
.
          MATCH     SP9,STRTCODE
          GOTO      DCOD1200 IF NOT EQUAL
.
DCOD1100  MOVE      "Start    ",CODES1
          MOVE      SP9,STRTCODE
          GOTO      DCOD1300
.
DCOD1200  MOVE      STRTCODE,KEY9
          CALL      CCOD0000                * Check for a validate code
          BRANCH    EXIT,DCOD1000
.
          MOVE      STRTCODE,CODES1
DCOD1300  DISPLAY   *P21:13,*V2LON,CODES1,*HOFF,*P32:13,CODEDESC;
.
. ----- Get To Disease Code -----
.
DCOD2000  MOVE      ZERO,EXIT
          MOVE      SP9,ENDCODE
          MOVE      SP9,CODES2
          PACK      CODEDESC,SP30,SP20
          DISPLAY   *P21:14,*EL,UNDLN9;
          KEYIN     *P21:14,*V2LON,ENDCODE,*HOFF;
.
          RESET     ENDCODE
          GOTO      DCOD2100 IF EOS
.
          CMATCH    QUEST,ENDCODE
          GOTO      DCOD2000 IF EQUAL
.
          ENDSET    ENDCODE
          APPEND    SP9,ENDCODE
          RESET     ENDCODE
.
          MATCH     SP9,ENDCODE
          GOTO      DCOD2200 IF NOT EQUAL
.
DCOD2100  MOVE      "Finish   ",CODES2
          MOVE      "zzzzzzzzz",ENDCODE     * Max. end range
          GOTO      DCOD2300 
.
DCOD2200  MOVE      ENDCODE,KEY9
          CALL      CCOD0000                * Check for a validate code
          BRANCH    EXIT,DCOD2000
.
          MOVE      ENDCODE,CODES2
DCOD2300  DISPLAY   *P21:14,*V2LON,CODES2,*HOFF,*P32:14,CODEDESC;
.
. ----- Validate Disease Code Ranges -----
.
DCOD3000  MOVE      ZERO,EXIT
          MATCH     STRTCODE,ENDCODE        * Check if ranges are correct
          GOTO      DCOD8000 IF LESS
.
          MATCH     STRTCODE,ENDCODE        * Check if same code 
          GOTO      DCOD9999 IF NOT EQUAL
.
          MOVE      ONE,EXIT                * Set exit flag - Continue
          GOTO      DCOD9999
.
. ----- Invalid Code Range Entered ------
.
DCOD8000  DISPLAY   *P1:24,*EL,*B,*EL,"Invalid Ranges. ";
          CALL      HITENTER
          GOTO      DCOD0000
.
DCOD9999  RETURN
+
.***********************************************************************
.*                           CCOD0000                                  *
.*                    Check For A Valid Code                           *
.***********************************************************************
CCOD0000  MOVE     TODATE,ICDRDDTE              * use to validate icd code
          CALL     RDPTICD1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9D1
          ENDIF
          BRANCH    OVRCD,CCOD9500
.
          MOVE      DDESC,CODEDESC
          MOVE      ZERO,EXIT
          GOTO      CCOD9999
.
CCOD9500  DISPLAY   *P1:24,*EL,*B,"Invalid Disease Code. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
CCOD9999  RETURN
.
.------------------------------------------------------------
.                    Extract details 
.------------------------------------------------------------
.
.       Loop over discharge file
.
TRANNO    MOVE      "60",CLNO
          CLOCK     TIME,CTIMEIS
.
          MOVE      ZEROVISN,LSTADMN
          MOVE      TODATE,ENDDATE         * Save End Discharge Date
.
          CALL      CREAIDX                * Create and open index file
          BRANCH    OPCND OF START
.
          DISPLAY   *P1:24,*EL,"Discharged Date :",*P35:24,"Scanning :";
.
          PACK      KEY16 WITH FROMDATE,SP8
          CALL      RDSDSCH2
LOOP1     CALL      RDKDSCH2
          BRANCH    OVRCD OF SORTD
.
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      XDATE INTO XCENT,XYEAR,XMON,XDAY
          MOVE      XDATE,TODATE
.
          DISPLAY   *P46:24,XDAY,SLASH,XMON,SLASH,XYEAR,SP2,DADMNO;
.
          MATCH     XDATE,ENDDATE
          GOTO      SORTD IF LESS
.
.         Multi-Hospital Processing
.
          COMPARE   ONE,IBCNMHOS
          GOTO      LOOP10 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      LOOP10 IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LOOP1
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LOOP1 IF NOT EQUAL
.
LOOP10    MOVE      SP9,PTEDCODE 
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
.
LOOP2     CALL      RKPTECD2
          BRANCH    OVRCD OF LOOP1
.
          MATCH     DADMNO,PTEDADMN
          GOTO      LOOP1 IF NOT EQUAL
.
          MATCH     STRTCODE,PTEDCODE          * check disease code range
          GOTO      LOOP2 IF LESS
.
          MATCH     PTEDCODE,ENDCODE           * check disease code range
          GOTO      LOOP2 IF LESS
.
          MATCH     CMDISP,PTEDTYPE            * check disease type
          GOTO      LOOP2 IF NOT EQUAL
.
          MATCH     DADMNO,LSTADMN
          GOTO      WRTREC IF EQUAL
.
          DISPLAY   *P19:24,*V2LON,XDAY,SLASH,XMON,SLASH,XYEAR;
.
.         Get various bits of info about this admission
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF NOADMR
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NOURC
.
          MOVE      PGNAME,ANS
          PACK      PNAME WITH ANS,DOT,PSNAME
          REP       ", ",PNAME
          MOVE      PNAME,PNAMEDIS
.
          MOVE      SP3,BCODE
.
          BRANCH    OPTION OF GDNAM
.
          MOVE      PURNO,DIM8
          UNPACK    DIM8,DIM6,DIM2     * we only want the last 2 digits
          REP       " 0",DIM2
          PACK      KEY5 WITH DIM2,SP3
.
          CALL      RDSBAYS2
          CALL      RDKBAYS2
          BRANCH    OVRCD OF GDNAM
.
          MATCH     BAYUR,DIM2
          GOTO      GDNAM IF NOT EQUAL
.
          MOVE      BAYCODE,BCODE
.
GDNAM     MOVE      SP30,DNAME
          MOVE      SP30,DNAMEDIS
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NOADOC
.
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          REP       ", ",DNAME
          MOVE      DNAME,DNAMEDIS
.
NOADOC    UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      FROMDATE WITH XCENT,XYEAR,XMON,XDAY
.
          CALL      RTIODAYS
.
WRTREC    MOVE      AADMNO,KEY8
          READ      GNDFILE,KEY8;KEY8
          GOTO      WRTRE1 IF NOT OVER
          ADD       ONE,TOTADMS
          ADD       NBRDAYS,TOTDAYS    **GRAND TOTAL DAYS
          WRITE     GNDFILE,KEY8;KEY8
.
WRTRE1    PACK      KEY27 WITH BCODE,PTEDCODE,AADMNO,UNIQUE
          WRITE     TEMPFILE,KEY27;KEY27:
                             PNAME,AURNO,PBDATE:
                             PSEX,ADATE,DDATE,NBRDAYS,DNAME
.
          ADD       ONE,UNIQUE
          MOVE      AADMNO,LSTADMN
          DISPLAY   *P28:24,*V2LON,TOTADMS;
.
          GOTO      LOOP2
.
NOADMR    DISPLAY   *P1:23,*EL,*B,*V2LON,"Missing Admission Record":
                    " For Admission ",DADMNO;
          GOTO      LOOP1
.
NOURC     DISPLAY   *P1:22,*EL,*B,*V2LON,"Missing Master File Record":
                    " For U/R Number ",AURNO;
          GOTO      LOOP1
.
.
SORTD     COMPARE   ZERO,TOTADMS
          GOTO      NODISS IF EQUAL
.
          DISPLAY   *P1:24,*EL,"Printing Disease Codes :";
.
          MOVE      SP10,DPCODE
          MOVE      SP3,BAYCODE
          MOVE      ONE,FIRSTFLG
.
          READ      TEMPFILE,SP30;;
READLOP   READKS    TEMPFILE;BCODE,SAVCODE,XDAADMNO,DUNIQUE:
                                   PNAME,AURNO,PBDATE:
                                   PSEX,ADATE,DDATE,NBRDAYS,DNAME
          GOTO      ENDP IF OVER
          MOVE      XDAADMNO,AADMNO
          MOVE      PNAME,PNAMEDIS
          MOVE      DNAME,DNAMEDIS
.
          DISPLAY   *P26:24,*V2LON,BCODE,SP1,SAVCODE,SP1,AADMNO;
.
          BRANCH    PSUMM,READL10
.  
          CALL      DISPDA
          ADD       ONE,DISADMS
          ADD       NBRDAYS,DISDAYS
          GOTO      READLOP
.
......... Summary report only
.
READL10   CALL      DISPSU
          GOTO      READLOP
.
+
......... OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NODISS IF EQUAL
.
          BRANCH    PSUMM,ENDP8
          CALL      DTOTAL
.
          MOVE      TOTDAYS,AVGLOS
          DIV       TOTADMS,AVGLOS
.
          PRINT     *1,"                           -----":
                    *79,"------":
                    *87,"                       ---------":
                    *N," ***   GRAND TOTALS   *** ",TOTADMS:
                    *79,TOTDAYS:
                    *87,"                       ",AVGLOS:
                    *N,"                           -----":
                    *79,"------":
                    *87,"                       ---------"
          GOTO      ENDP9
.
ENDP8     CALL      DISP10          * Print the last record
.
          MOVE      TOTDAYS,AVGLOS
          DIV       TOTADMS,AVGLOS
.
          CALL      PAGEND
          PRINT     *1,"|",*132,"|":
                    *N,*1,"|"," ***   GRAND TOTALS   ***  ",*100,TOTADMS:
                    *113,TOTDAYS,*123,AVGLOS,*132,"|"
          CALL      PAGEND
.
ENDP9     PRINT     *N,"  ***  END OF REPORT  ***"
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      DELFILE
.
NODISS    DISPLAY   *P1:24,*EL,*P22:24,*B,*V2LON:
                    "** No Diseases During This Period **",*W2,*P1:24,*EL;
.
DELFILE   CALL      KILLIDX
          GOTO      REPEAT
+
.       Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          CLOSE     TEMPFILE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEG,CMDLINE
          RESET     CMDLINE
          CLOSE     GNDFILE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
CREAIDX   CALL      KILLIDX
          PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 108 U1-27",015:
                             "isbuild ",FNAMEG," 108 U1-8"
          WEOF      PSTROL,SEQ
.
          CALL      EXEROLL
          OPEN      TEMPFILE,FNAMET
          OPEN      GNDFILE,FNAMEG
          RETURN
+
EXEROLL   MOVE      ONE,STATUS
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          BRANCH    STATUS OF INDO1
.
          MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,"The Index File Not Created. ";
          CALL      HITENTER
.
INDO1     CLOSE     PSTROL,DELETE
          RETURN
.
.
.     PRINT ACTUAL DATA 
.
DISPDA    COMPARE   "50",CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    FIRSTFLG,FPRINTA
.
          MATCH     BCODE,BAYCODE
          GOTO      ENDDC IF NOT EQUAL
.
          MATCH     SAVCODE,DPCODE
          GOTO      FPRINTA IF EQUAL
.
ENDDC     CALL      DTOTAL
.
          PACK      DDESC WITH SP30,SP30,SP10
          MOVE      TODATE,ICDRDDTE              * use to validate icd code
          MOVE      SAVCODE,KEY9
          CALL      RDPTICD1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9D1
          ENDIF
.
.       If we are changing Bay sequence, then force a new page
.
          MATCH     BCODE,BAYCODE
          GOTO      FPRINTC IF NOT EQUAL
.
          COMPARE   "45",CLNO
          GOTO      FPRINTC IF NOT LESS
.
          BRANCH    PSKIP OF FPRINTB
.
FPRINTC   CALL      HEAD1
          GOTO      FPRINTA
.
FPRINTB   CALL      HEADNT
.
FPRINTA   UNPACK    PBDATE WITH XCENT,XYEAR,XMON,XDAY
          PACK      BIRDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          UNPACK    ADATE WITH XCENT,XYEAR,XMON,XDAY
          PACK      ADMDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          UNPACK    DDATE WITH XCENT,XYEAR,XMON,XDAY
          PACK      DISDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          PRINT      *1,"| ",PNAMEDIS,*24,"|",AURNO:
                   *33,"|",AADMNO,*42,"|",BIRDATE:
                   *53,"| ",PSEX,*57,"|",ADMDATE:
                   *68,"|",DISDATE,*79,"| ",NBRDAYS:
                   *86,"| ",DNAMEDIS,*110,"| ";
.
          MOVE       ZERO,FIRSTFLG
          MOVE       SP9,PTEDCODE 
          MOVE       ZERO,COUNT
          PACK       KEY16,AADMNO,SP20                                *C-240107
          CALL       RSPTECD2
.
LOOP3     CALL      RKPTECD2
          BRANCH    OVRCD OF NOMORE
.      
          MATCH      AADMNO,PTEDADMN
          GOTO       NOMORE IF NOT EQUAL
.
          MATCH      CMDISP,PTEDTYPE
          GOTO       PRTOTHR IF NOT EQUAL
.
          MATCH      SAVCODE,PTEDCODE
          GOTO       LOOP3 IF EQUAL
.
PRTOTHR   BRANCH     COUNT OF ONEC,TWOC
.
          PRINT      PTEDCODE,SP1;
          MOVE       TWO,COUNT
          GOTO       LOOP3
.
ONEC      PRINT      "|",*24,"|",*33,"|",*42,"|",*53,"|":
                     *57,"|",*68,"|",*79,"|",*86,"|":
                     *110,"| ",PTEDCODE,SP1;
          MOVE       TWO,COUNT
          GOTO       LOOP3
.
TWOC      PRINT      PTEDCODE,SP1,"|"
          MOVE       ONE,COUNT
          ADD        ONE,CLNO
          GOTO       LOOP3
.
NOMORE    BRANCH     COUNT OF ONEX,TWOX
TWOX      PRINT      *132,"|"
          ADD        ONE,CLNO
.
ONEX      RETURN
.
.
......... Display summary only
.
DISPSU    COMPARE   "60",CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    FIRSTFLG,DISP90
.
          BRANCH    OPTION,DISP05                * Disease code sequence
          MATCH     BCODE,BAYCODE
          GOTO      DISP10 IF NOT EQUAL
.
DISP05    MATCH     SAVCODE,DPCODE
          GOTO      DISP90 IF EQUAL
.
DISP10    BRANCH    OPTION OF DISP20
.
.         Print the bay code description
.
          MOVE      "UNKNOWN BAY CODE",BAYDESC
          PACK      KEY5 WITH BAYCODE,SP2
          CALL      RDBAYS1
.
.         Get description of disease code
.
DISP20    MOVE      DPCODE,KEY9
          CALL      RDPTICD1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9D1
          ENDIF
.
          MOVE      DISDAYS,AVGLOS
          DIV       DISADMS,AVGLOS
.
          CALL      PAGEND
          BRANCH    OPTION,DISP30               * by disease code sequence
.
          MOVE      DDESC,DIM60
          MOVE      BAYDESC,DIM18
          PRINT     *1,"| ",BAYCODE," ",DIM18,*25,"| ",DPCODE," ",DIM60:
                    *100,"|",DISADMS,*112,"|",DISDAYS,*122,"|",AVGLOS,*132,"|"
          GOTO      DISP40
.
DISP30    PRINT     *1,"| ",DPCODE," ",DDESC:
                    *100,"|",DISADMS,*112,"|",DISDAYS,*122,"|",AVGLOS,*132,"|"
.
DISP40    ADD       ONE,CLNO
          MOVE      ZERO,DISADMS
          MOVE      ZERO,DISDAYS
.
DISP90    ADD       ONE,DISADMS
          ADD       NBRDAYS,DISDAYS
          MOVE      ZERO,FIRSTFLG
          MOVE      BCODE,BAYCODE
          MOVE      SAVCODE,DPCODE
          RETURN
.
.
.       Subroutine to print sub totals
.
DTOTAL    COMPARE   TEN2,CLNO
          CALL      PAGEND IF NOT LESS
.
          MOVE      DISDAYS,AVGLOS
          DIV       DISADMS,AVGLOS
.
          PRINT     *N," Total Number of Patients  ",DISADMS:
                    *58,"Total Bed Days ",*79,DISDAYS:
                    *88,"Average Length of Stay",AVGLOS,*N
.
          ADD       THREE,CLNO
          MOVE      ZERO,DISADMS
          MOVE      ZERO,DISDAYS
          RETURN
.
.                 HEADINGS FOR OUTPUT
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
HEAD1     MOVE      ONE,CNOUNDLN
          LOAD      CPHDROPT,OPTION,DDIS,DBAY
          CALL      HEAD132
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME
          ENDIF
.
          PRINT     *40,"Disease Code From : ",CODES1,*71,"To ",CODES2,*N:
                    *40,"From Date : ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *64,"To Date : ",XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2 
.
          MOVE      FIVE,CLNO
.
          COMPARE   ONE,PSUMM
          GOTO      HEADSUM IF EQUAL
.
          BRANCH    OPTION OF HEADNT
.
.         Print the bay code description
.
          MOVE      BCODE,BAYCODE
          PACK      KEY5 WITH BAYCODE,SP2
          CALL      RDBAYS1
          BRANCH    OVRCD OF UNKBAY
.
          PRINT     *N,*40,"BAY     : ",BAYCODE,*61,BAYDESC
          ADD       TWO,CLNO
          GOTO      HEADNT
.
UNKBAY    PRINT     *N,*40,"BAY     : ",BAYCODE,*61,"UNKNOWN BAY CODE"
          ADD       TWO,CLNO
.
HEADNT    MOVE      SAVCODE,KEY9
          COMPARE   ONE,CPAGENO
          IF        @EQUAL
            CALL     RDPTICD1
            IF        OVRCD=1 & ICDRDVER>1
              CALL      RD10T9D1
            ENDIF
          ENDIF
.
          PRINT     *N,*40,"PRIMARY DISEASE : ",DPCODE,"  ",DDESC,*N
.
          CALL      PAGEND
.
          PRINT     *1,"| Patients Name",*24,"| U/R No":
                    *33,"| Adm No",*42,"| Date of":
                    *53,"|Sex",*57,"| Admission",*68,"| Discharge":
                    *79,"|  Bed",*86,"| Attending Doctor":
                    *110,"| Other Disease Codes",*132,"|":
                    *N,"|",*24,"|",*33,"|",*42,"|  Birth",*53,"|":
                    *57,"|   Date",*68,"|   Date":
                    *79,"| Days",*86,"|",*110,"|",*132,"|"
.
          CALL      PAGEND
.
          ADD       FIVE,CLNO
          RETURN
.
.
.       Heading for summary only
.       Bay code sequence
.
HEADSUM   CALL     PAGEND
          BRANCH   OPTION,HEADS10
          PRINT    *1,"| Bay code ",*25,"| Diagnosis",*100,"| No of Pats":
                   *112,"| Bed Days",*122,"| Av.L.O.S",*132,"|"
          GOTO     HEADS20
.
.       Disease code sequence
.
HEADS10   PRINT    *1,"| Diagnosis",*100,"| No of Pats",*112,"| Bed Days":
                   *122,"| Av.L.O.S",*132,"|"
.
HEADS20   ADD      THREE,CLNO
          RETURN
.
.          LAST LINE ON THE TRAN
.
PAGEND    PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.         Subroutine to key in a date
.
KEYDATEX   MOVE       IDAY,XDAY
          MOVE       IMON,XMON
          MOVE       IYEAR,XYEAR
          MOVE       ICENT,XCENT
.
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
.
          RETURN
+
.
          INC       STD001IO
          INC       PATHSPKY
.
          INC       PATBAYIO/INC
          INC       PATECDIO/INC
          INC       PATICDIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATDO1IO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
          INC       RTIODAYS
.
ENDIT    CHAIN      PGM
         STOP

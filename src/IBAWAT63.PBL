.*****************************************************************************
.*    Program      : IBAWAT63                                                *
.*    Description  : Valid Waiting List Maintenance                          *
.*                                                                           *
.*    Author       : ROD   15/12/92                                          *
.*    Function     : Maintain a Consultant/list type combination of W/L      *
.*                                                                           *
.*    Modifications:                                                         *
.*                   V5.07.01  28/05/1999 Caleb Sharman SRF 645599           *
.*                             Mods to display consultants code              *
.*                   V5.01.02  15/12/92  ROD                                 *
.*                             Change Status from codes field to Form 1      *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       WATVWLFD/INC
.
CODE      DIM       2
DIM3      DIM       3
DOCCODE   DIM       6
PTCNDCQS  FORM      1
UNDLN40   INIT      "________________________________________"
.
PRGID     INIT      "IBAWAT63"
PRGNAM    INIT      "Valid Waiting List Maintenance"
VERSION   INIT      "V12.01.00"
+
.*************************************************************************
.*  ML0000  :  Mainline code                                             *
.*************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * Menu options
          BRANCH    MOPTION,ML3000,ML4000,ML5000  * Insert, Change, Delete
          GOTO      ML9999                        * Exit
.
. --- Insert Mode ---
.
ML3000    CALL      KCON0000                      * keyin consultant
          BRANCH    EXIT,ML1000                   * nothing input
          CALL      KLTY0000                      * keyin list type
          BRANCH    EXIT,ML3000                   * nothing input
.
          CALL      SCRN0000                      * display screen
          CALL      CADD0000                      * Input fields
          BRANCH    EXIT,ML1000                   * EXITCHAR
          CALL      CSEL0000                      * change fields
          BRANCH    EXIT,ML3000                   * Cancel
          CALL      CPST0000                      * Post record
          GOTO      ML3000
.
. --- Change Mode ---
.
ML4000    CALL      KCON0000                      * keyin consultant
          BRANCH    EXIT,ML1000                   * nothing input
          CALL      KLTY0000                      * keyin list type
          BRANCH    EXIT,ML4000                   * nothing input
.
          CALL      SCRN0000                      * display screen
          CALL      DFLD0000                      * display fields
          CALL      CSEL0000                      * change fields
          BRANCH    EXIT,ML4000                   * Cancel
          CALL      CPST0000                      * Post record
          GOTO      ML4000
.
. Delete Mode
.
ML5000    CALL      KCON0000                      * keyin cancer code
          BRANCH    EXIT,ML1000                   * nothing input
          CALL      KLTY0000                      * keyin list type
          BRANCH    EXIT,ML5000                   * nothing input
          CALL      SCRN0000                      * display screen
          CALL      DFLD0000                      * display fields
          CALL      DELQ0000                      * delete record if desired
          BRANCH    CEXIT,ML5000,ML5000,ML1000    * yes, no, cancel input
.
ML9999    CHAIN     PGM
+
.*************************************************************************
.*  INIT0000  :  Initialisation                                          *
.*************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"watvwlaf"
          OPEN      WATVWLA1,"watvwlaf"
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P60:24,*EL,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.*************************************************************************
.*  MENU0000  :  Menu options                                            *
.*************************************************************************
MENU0000  HLINE     *G37,2,55,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Insert":
                    *P1:6,*V2LON,"2",*HOFF," = Change":
                    *P1:7,*V2LON,"3",*HOFF," = Delete":
                    *P1:9,"Select Option : ";
.
MENU1000  KEYIN     *P17:9,*DV,UNDLN1:
                    *P17:9,*V2LON,MOPTION
.
          IF        (MOPTION<0)|(MOPTION>3)
            BEEP
            GOTO      MENU1000
          ENDIF
.
          IF        MOPTION=1
            DISPLAY   *P55:2,*V2LON,"- Insert "
          ENDIF
          IF        MOPTION=2
            DISPLAY   *P55:2,*V2LON,"- Change "
          ENDIF
          IF        MOPTION=3
            DISPLAY   *P55:2,*V2LON,"- Delete "
          ENDIF
.
MENU9999  RETURN
+
.*************************************************************************
.*  SCRN0000  :  Display screen                                          *
.*************************************************************************
SCRN0000  DISPLAY   *P1:7,*V2LON," 1",*HOFF,". Description               : ":
                    *P1:8,*V2LON," 2",*HOFF,". Maximum Waiting Time Elec : ":
                    "         (Weeks)":
                    *P1:9,*V2LON," 3",*HOFF,". Maximum Waiting Time D.C. : ":
                    "         (Weeks)":
                    *P1:10,*V2LON," 4",*HOFF,". Status                    : "
.
SCRN9999  RETURN    
+
.*************************************************************************
.*  DFLD0000  :  Display fields                                          *
.*************************************************************************
DFLD0000  DISPLAY   *P33:7,*V2LON,WTVWDESC,*P33:8,WTVWWTEL,*P33:9,WTVWWTDC
.
          DISPLAY   *P33:10,*V2LON,WTVWSTAT
.
          IF        WTVWSTAT=0
            DISPLAY   *P42:10,"Active"
          ENDIF
          IF        WTVWSTAT=1
            DISPLAY   *P42:10,"Inactive"
          ENDIF
.
DFLD9999  RETURN
+
.*************************************************************************
.*  CPST0000  :  Post a record                                           *
.*************************************************************************
CPST0000  DISPLAY   *P1:24,*EL,*V2LON,*P35:24,"Posting...";
.
          PACK      KEY9,WTVWCONS,WTVWLTYP
          CALL      RAWTVWL1
          IF        OVRCD=0
            CALL      UPWTVWL1
          ELSE
            CALL      WRWTVWL1
          ENDIF
.
CPST9999  RETURN
+
.*************************************************************************
.*  DELQ0000  :  Delete record if desired                                *
.*************************************************************************
DELQ0000  CALL      DELQST                        * ask 'Ok to delete?'
          BRANCH    CEXIT,DELQ1000,DELQ9999,DELQ9999
.
DELQ1000  DISPLAY   *P1:24,*EL,*V2LON,*P35:24,"Deleting...";
.
          PACK      KEY9,WTVWCONS,WTVWLTYP
          CALL      DEWTVWL1
.
DELQ9999  RETURN
+
.*************************************************************************
.*  CADD0000  :  Add mode                                                *
.*************************************************************************
CADD0000  CALL      KDES0000                      * description
          CALL      KWTE0000                      * maximum waiting time electiv
          CALL      KWTD0000                      * maximum waiting time Day Cas
          CALL      KSTA0000                      * status
          BRANCH    EXIT,CADD9999                 * EXITCHAR
.
          MOVE      ZERO,EXIT                     * Everything Ok.
.
CADD9999  RETURN
+
.*************************************************************************
.*  CSEL0000  :  Change mode                                             *
.*************************************************************************
CSEL0000  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      CSEL9000 IF LESS              * Cancel?
          GOTO      CSEL8000 IF EQUAL             * Post?
.
          BRANCH    CCITEM,CSEL1000,CSEL1100,CSEL1200,CSEL1300
.
          BEEP
          GOTO      CSEL0000
.
CSEL1000  CALL      KDES0000                      * description
          GOTO      CSEL0000
.
CSEL1100  CALL      KWTE0000                      * maximum waiting time electiv
          GOTO      CSEL0000
.
CSEL1200  CALL      KWTD0000                      * maximum waiting time Day Cas
          GOTO      CSEL0000
.
CSEL1300  CALL      KSTA0000                      * status
          BRANCH    EXIT,CSEL9000                 * EXITCHAR
          GOTO      CSEL0000
.
CSEL8000  MOVE      ZERO,EXIT                     * Post
          GOTO      CSEL9999
.
CSEL9000  MOVE      ONE,EXIT                      * Cancel
.
CSEL9999  RETURN
+
.*************************************************************************
.*  KDES0000  :  keyin description                                       *
.*************************************************************************
KDES0000  KEYIN     *P33:7,*DV,UNDLN40:
                    *P33:7,*V2LON,WTVWDESC:
                    *P33:7,*DV,WTVWDESC
.
          ENDSET    WTVWDESC
          APPEND    SP20,WTVWDESC
          APPEND    SP20,WTVWDESC
          RESET     WTVWDESC
.
KDES9999  RETURN
+
.*************************************************************************
.*  KWTE0000  :  keyin Maximum waiting time Elective                     *
.*************************************************************************
KWTE0000  KEYIN     *P33:8,*DV,UNDLN3:
                    *P33:8,*V2LON,WTVWWTEL:
                    *P33:8,*DV,WTVWWTEL
.
          IF        WTVWWTEL<1
            BEEP
            GOTO      KWTE0000
          ENDIF
.
KWTE9999  RETURN
+
.*************************************************************************
.*  KWTD0000  :  keyin Maximum waiting time Day Case                     *
.*************************************************************************
KWTD0000  KEYIN     *P33:9,*DV,UNDLN3:
                    *P33:9,*V2LON,WTVWWTDC:
                    *P33:9,*DV,WTVWWTDC
.
          IF        WTVWWTDC<1
            BEEP
            GOTO      KWTD0000
          ENDIF
.
KWTD9999  RETURN
+
.*************************************************************************
.*  KSTA0000  :  keyin Status                                            *
.*************************************************************************
KSTA0000  DISPLAY   *P1:24,*EL,*V2LON,"0",*HOFF," = Active,  ",*V2LON,"1":
                    *HOFF," = Inactive"
.
          KEYIN     *P33:10,*EL,*DV,UNDLN1:
                    *P33:10,*V2LON,WTVWSTAT:
                    *P33:10,*DV,WTVWSTAT
.
          IF        WTVWSTAT=0
            DISPLAY   *P42:10,"Active"
            GOTO      KSTA9999
          ENDIF
          IF        WTVWSTAT=1
            DISPLAY   *P42:10,"Inactive"
            GOTO      KSTA9999
          ENDIF
          BEEP
          GOTO      KSTA0000
.
KSTA9999  RETURN
+
.*************************************************************************
.*  KLTY0000  :  keyin List type                                         *
.*************************************************************************
KLTY0000  MOVE      "33",ECOL
          MOVE      "5",EVERT
          MOVE      "WU",CODE
          MOVE      SP3,CKYIDEF3                  * no default
          MOVE      ZERO,CKYIMAND                 * not mandatory
.
          CALL      PATCODKY
.
          IF        (EXIT=1) | (EXIT=2)
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      KLTY9999
          ENDIF
.
          MOVE      ACODE,WTVWLTYP
          PACK      KEY9,WTVWCONS,WTVWLTYP
          CALL      RDWTVWL1
          IF        OVRCD=0
            IF        MOPTION=1
              DISPLAY   *P1:24,*EL,*B,"Consultant/List Type already on file.  ";
              CALL      HITENTER
              GOTO      KLTY0000
            ENDIF
          ELSE
            IF        MOPTION<>1
              DISPLAY   *P1:24,*EL,*B,"Consultant/List Type doesn't exist.  ";
              CALL      HITENTER
              GOTO      KLTY0000
            ENDIF
          ENDIF
.
          DISPLAY   *P42:5,TDESC;
.
KLTY9999  RETURN
+
.*************************************************************************
.*  KCON0000  :  keyin Consultant                                        *
.*************************************************************************
KCON0000  DISPLAY   *P1:4,*EF:
                    *P3:4,"Consultant",*P31:4,":":
                    *P3:5,"List Type",*P31:5,":"
.
          MOVE      "33",ECOL
          MOVE      "4",EVERT
          MOVE      SP6,CKYIDEF6                  * no default
          MOVE      ZERO,CKYIMAND                 * not mandatory
.
          CALL      PATDOCKY
.
          IF        (EXIT=1) | (EXIT=2)
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      KCON9999
          ENDIF
.
          MOVE      DOCCODE,WTVWCONS
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
          DISPLAY   *P33:4,DOCCODE,*P42:4,PACFNAME;
.
KCON9999  RETURN

.
          INC       STD001IO
          INC       PATCODKY
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       WATVWLIO/INC
.

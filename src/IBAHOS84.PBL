. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBAHOS84                         *
. *                                                        *
. *     FUNCTION:         MONTHLY COMPARATIVE REPORT       *
. *                                                        *
. *     DATE WRITTEN:     31/10/88                         *
. *                                                        *
. *     AUTHOR:           J.TAN                            *
. *                                                        *
. *     MODIFICATIONS:                                     ********************
.*        V10.03.01 17/10/2012  Jill Parkinson CAR 273375
.*                  Recompiled for DATECONV - first day of year calculation
.*        V9.02.01  24/10/2001  B.G.Ackland                                   *
.*                  Remove pi on Temp File                                    *
.*         V5.07.02 16/10/1999  J.Tan   SRF 635285                            *
.*                  Mods to initialise total number of beds                   *
.*         V5.07.01 27/07/1999  Steve Downing                                 *
.*                  Y2K mods                                                  *
.*         V5.07.01 27/07/1999  Steve Downing                                 *
.*                  Y2K mods                                                  *
.*        V5.07.B02 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 14/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
. *                       V5.01 11.05.89 S.Barcham         *
. *                             Fix 1516 I * L             *
. *                             SRF100516                  *
. *                       V5.02 17.06.89 S.Barcham         *
. *                             Compiled for new standbys  *
. *                             SRF 101101                 *
. *                       V5.03 01/07/89 J.Tan             *
. *                             Mods Average length of stay*
. *                             SRF 101328                 *
. *                    V5.01.01 03/07/89 K.Peak            *
. *                             Changed Adm & U/R to 8 chrs*
. *                             14/07/89 Graeme Williams   *
. *                             Allow for 13x4 week periods*
. *                    V5.01.02 28.09.89 S.Barcham         *
. *                             Fix 11020 I * C            *
. *                             SRF 102511                 *
. *                    V5.01.03 04/10/89 K.Peak            *
. *                             Removed HEAD132            *
. *                             SRF 102538                 *
. *                    V5.01.04 21/03/90 J.Tan  SRF 104307 *
. *                             Fixed total of Daily Ave.  *
. *                    V5.01.05 21/03/90 J.Tan             *
. *                             Fixed Percentage occupancy *
. *                    V5.01.06 21/06/90 J.Tan             *
. *                             Mods total admissions since*
. *                             hospital opened            *
. *                    V5.01.07 25/06/90 K.Peak            *
. *                             Changed the total for beds *
. *                             to a form4 from a form3    *
. *                    V5.01.08 28/06/90 J.Tan SRF 104921  *
. *                             Added Separation bed days  *
. *                    V5.01.09 23/06/92 J.Tan SRF 114792  *
. *                             Fixed ALOS                 *
. *                    V5.01.10 02/06/93 J.Tan SRF 121660  *
. *                             Added ward/bed history file*
. *                    V5.01.11 30/12/93 I.Rutt MTA        *
. *                             Added new include PATACDAY ********************
. *        V5.02.001 02/03/95 Paul Howells     SRF 135026                     *
. *                  Fixed the use of PATWBHFD file.                          *
. *        V5.03.01  11/04/1996  Steve Armstrong                              *
. *                  Mods to use WDACTVDY to calculate active bed days.       *
. *                  Mods to cater for changes to patmstsf.                   *
. *                  Mods to use PTCNH82W to control printing of no stat      *
. *                  wards.                                                   *
. *****************************************************************************
.
          INC           STD001FD
          INC           PATCOMM/INC
          INC           PATDRGFD/INC        * Period Range file
          INC           PATMSTFD/INC        * Monthly Inpatient Statistics file
          INC       PATWR1FD/INC        * Ward/bed file
          INC           PATWBHFD/INC        * Ward/bed history file
          INC           WDACTVVR/INC
.
+
.               VARIABLES DEFINITIONS
.
.--------------------------------------------------------------
.Name       Type      Length     Physical      Start
.----       ----      ------     --------      -----
.MSWARD     DIM           3            3          1
BEDNUM      FORM          4            3          4
TADMIT      FORM          6            4          7
TBDAY       FORM          6            4         11
TACDAYS     FORM          8            5         15
TDYMTH      FORM          2            2         20
TDSCH       FORM          6            4         22
TTROUT      FORM          6            4         26
LADMIT      FORM          6            4         30
LBDAY       FORM          6            4         34
LACDAYS     FORM          8            5         38
LDYMTH      FORM          2            2         43
LDSCH       FORM          6            4         45
LTROUT      FORM          6            4         49
PADMIT      FORM          6            4         53
PBDAY       FORM          6            4         57
PACDAYS     FORM          8            5         61
PDYMTH      FORM          2            2         66
PDSCH       FORM          6            4         68
PTROUT      FORM          6            4         72
TSDAY       FORM          6            4         76 *Current Separation beddays
LSDAY       FORM          6            4         80 *Last month
PSDAY       FORM          6            4         84 *This month last year
CHNGFLG     FORM          1            2         88
.
.  END OF RECORD                                 90
.--------------------------------------------------------------
.
PSTROL    FILE      ASCII, FIXED=256
TEMPFILE  IFILE     SQL, FIXED=90, KEY="U1-3"
.
ASKFLAG   FORM      1             * asterisk printed flag
.                                    0 = asterisk printed
.                                    1 = asterisk not printed
AVSTAY    FORM      4.2
AVDAY     FORM      4.2
.
CENDDT    DIM       8
CURMTH    DIM       6
CMDLINE   DIM       50
CPERMTH   DIM       6
CTOTADM   FORM      8
.
DIM4      DIM       4
DISFRDTE  DIM       10
DISTODTE  DIM       10
.
FDATCUR   DIM       8
FDATLST   DIM       8
FDATPRE   DIM       8
FORM3     FORM      3
FORM6     FORM      6
FORM62    FORM      6.2
FORM8     FORM      8
FORM10    FORM      10
FNAMET    DIM       8
FROMDATE  DIM       8
.
GTTADM    FORM      8
GTBDAY    FORM      6
GTSDAY    FORM      6
GTADMIT   FORM      6
GTDSCH    FORM      6
GTTROUT   FORM      6
GLBDAY    FORM      6
GLSDAY    FORM      6
GLACDAY   FORM      8
GLADMIT   FORM      6
GLDSCH    FORM      6
GLDYMTH   FORM      4.2
GLTROUT   FORM      6
GPACDAY   FORM      8
GPBDAY    FORM      6
GPDYMTH   FORM      4.2
GPSDAY    FORM      6
GPADMIT   FORM      6
GPDSCH    FORM      6
GPTROUT   FORM      6
GTACDAY   FORM      8
GTDYMTH   FORM      4.2
.
.
LNO       FORM      2
LSTMTH    DIM       6
LBEDNUM   FORM      4
LYEAR     FORM      "366"
.
OYEAR     FORM      "365"
OPCND     FORM     1
.
PCENTG    FORM      4.2
PBEDNUM   FORM      4
PREMTH    DIM       6
PTCNH82W  FORM      1
PTOCBDAY  FORM      1
.
RECFLG    FORM      1
SAVWARD   DIM       3
SELMTH    DIM       6
STATUS    FORM      1
SVHNOCC   FORM      3
.
TDATCUR   DIM       8
TDATLST   DIM       8
TDATPRE   DIM       8
TOTBDAY   FORM      6
TODATE    DIM       8
TOTADM    FORM      6
TOJULDD   FORM      3
TOJULYY   FORM      2
TOJULCC   FORM      2
TTBEDN    FORM      4
.
VERT      FORM      2
WARBEDAC  FORM     8.2
WARTOTBD  FORM      6  
WWRDES    DIM      12
WKDATE    DIM       8
XBNUM     FORM      4
XBDAY     FORM      6
XSDAY     FORM      6
XDSCH     FORM      6
XTROUT    FORM      6
.
ZZZ       INIT      "zzz"
.
PRGID     INIT      "IBAHOS84"
PRGNAM    INIT      "Monthly Comparative Report"

VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmstsf";
          OPEN      PATMSTS1,"patmstsf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patwbhaf";
          OPEN      PATWBHA1,"patwbhaf"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA3,"patdrgaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN9;*242,CTOTADM
          READ      CONTROLF,TWENTY;*250,PTCNH82W
          READ      CONTROLF,FIFTY9;*27,CPERMTH,*217,PTOCBDAY
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,NOCPER
.
          PACK      CURMTH,DRGYR,DRGNUM
.
          CLEAR     FNAMET
          APPEND    "hos84a",FNAMET
          APPEND    DIM2,FNAMET
          RESET     FNAMET
          REP       " 0" IN FNAMET
          GOTO      START
.
NOCPER    KEYIN     *P1:24,*EL,*V2LON,*B:
                    "Current Date not in Period File.  ";
          CALL      HITENTER
          GOTO      ENDIT
.
START   DISPLAY     *P1:4,*EF,"  Keyin ",CPERMTH,"/Year :";
.
.       Keyin date (MM/YY)
.
KADAT   CALL        KEYMN
        BRANCH      OVRCD,ENDIT
.
        PACK        SELMTH,DRGYR,DRGNUM
        MOVE        DRGTDTE,CENDDT
.
        MATCH       SELMTH,CURMTH
        GOTO        CALDAY IF EQUAL
        GOTO        REKEY IF NOT LESS
.
        DISPLAY     *P1:24,*B,*EL,*V2LON,"Date must be < ":
                    "current ",CPERMTH,".  ";
        CALL        HITENTER
        GOTO        START
.
.         If current period subtract one from display date
.
CALDAY  MOVE        CCC,CC
        MOVE        CYY,YY
        MOVE        CMM,MM
        MOVE        CDD,DD
        CALL        DMYCON
.
        MOVE        JULDAY,FORM4
        SUB         OYEAR,FORM4
        SUB         LEAPYR,FORM4
        COMPARE     ZERO,FORM4
        GOTO        XXXX IF LESS
.
        ADD         ONE,JULYR
        IF          JULYR = ZERO
          ADD         ONE,JULCC
        ENDIF
        MOVE        ZERO,JULDAY
XXXX    SUB         ONE,JULDAY
        MOVE        JULDAY,JWKDAY
        MOVE        JULYR,JWKYER
        MOVE        JULCC,JWKCC
        CALL        JULCON
        PACK        DRGTDTE,CC,YY,MM,DD         
.
.        Check if dates are OK or not
.
REKEY   MOVE        DRGFDTE,FDATCUR
        UNPACK      DRGFDTE,CCENT,CYEAR,CMON,CDAY
        CALL        PACDATE
        MOVE        CPCDATE,DISFRDTE
        MOVE        DRGTDTE,TDATCUR
        UNPACK      DRGTDTE,CCENT,CYEAR,CMON,CDAY
        CALL        PACDATE
        MOVE        CPCDATE,DISTODTE
        DISPLAY     *P35:4,*EL,DISFRDTE," to ",DISTODTE
.
        KEYIN       *P1:24,*EF," Date ok (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
        REP         "yYnN",ANS
        MATCH       "N" TO ANS
        GOTO        KADAT IF EQUAL
.
        MATCH       "Y" TO ANS
        GOTO        OKPROC IF EQUAL
        BEEP
        GOTO        REKEY
.
NOPREVM KEYIN       *P1:24,*EL,*V2LON,*B:
                    "** Cannot find the ",CPERMTH," prior to this date ** ":
                    *EOFF,ANS,*P1:24,*EL;
        GOTO        START
.
NOPREVY KEYIN       *P1:24,*EL,*V2LON,*B:
                    "** Cannot find this date for last year ** ":
                    *EOFF,ANS,*P1:24,*EL;
        GOTO        START
.
.       Continue processing
.
OKPROC   DISPLAY    *P1:24,*EL,*P10:24,*V2LON,"** Processing ":
                    "Monthly Comparative Statistics Report **",*W;
.
.        Get the previous month
.
         MOVE       SELMTH,KEY6
         CALL       RDDRGA1
         CALL       RDPDRGA1
         BRANCH     OVRCD,NOPREVM
.
         PACK       LSTMTH,DRGYR,DRGNUM
         MOVE       DRGFDTE,FDATLST
         MOVE       DRGTDTE,TDATLST
.
.        Get the same month, previous year
.
         UNPACK     SELMTH,DRGYR,DRGNUM
         MOVE       DRGYR,FORM4
         SUB        ONE,FORM4
         PACK       KEY6,FORM4,DRGNUM
         REP        " 0",KEY6
         CALL       RDDRGA1
         BRANCH     OVRCD,NOPREVY
.
         PACK       PREMTH,DRGYR,DRGNUM
         MOVE       DRGFDTE,FDATPRE
         MOVE       DRGTDTE,TDATPRE
.
         MOVE       ZERO,CPAGENO
         MOVE       ZERO,XBNUM
         MOVE       ZERO,XBDAY
         MOVE       ZERO,XSDAY
.
         MOVE       CTOTADM,GTTADM
.
         CALL       KILLIDX
         CALL       CREAIDX                     * Creating tempfile
         OPEN       TEMPFILE,FNAMET
.
         DISPLAY    *P1:24,*EL,*P19:24,CPERMTH,"/Year :":
                    *P50:24,"Ward :";
.
         MOVE       SP20,KEY15
         CALL       RDSMSTS1
NXTMST   CALL       RDKMSTS1
         BRANCH     OVRCD OF GENREP
.
         MOVE       ONE,RECFLG           * flag for this month
.
         MOVE       MSNPRAD,TOTADM
         ADD        MSNPBAD,TOTADM
         ADD        MSBPRAD,TOTADM
         ADD        MSBPBAD,TOTADM       * Total number of patients
.
.        Total admissions since hospital opened till the selected month/period
.
         MATCH      MSDATE,SELMTH
         GOTO       NXTM00 IF LESS
.
         ADD        TOTADM,GTTADM
.
NXTM00   MATCH      SELMTH,MSDATE
         GOTO       NXTM10 IF EQUAL
.
         MATCH      LSTMTH,MSDATE
         GOTO       CHKLST IF NOT EQUAL
.
         MOVE       TWO,RECFLG            * flag for last month
         GOTO       NXTM10
.
CHKLST   MATCH      PREMTH,MSDATE
         GOTO       NXTMST IF NOT EQUAL
         MOVE       THREE,RECFLG            * flag for this month last year
.
NXTM10   IF         PTCNH82W = 0
           CALL       ZERO0000                   * get ward total
           COMPARE    ZERO,FORM10                * zero stat ward ?
           GOTO       NXTMST IF EQUAL            * yes - ignore
         ENDIF
         MOVE       MSDATE,KEY6
         CALL       RDDRGA1
         DISPLAY    *P40:24,*V2LON,DRGCNUM,*HOFF,SLASH,*V2LON,DRGCYR:
                    *P58:24,MSWARD;
.
         MOVE       ZERO,XBNUM
         PACK       KEY6 WITH MSWARD,SP3
         CALL       RDWARD1
         BRANCH     OVRCD OF NXTW20
.
         LOAD       ACTVFDTE,RECFLG,FDATCUR,FDATLST,FDATPRE
         LOAD       ACTVTDTE,RECFLG,TDATCUR,TDATLST,TDATPRE
         MOVE       ONE,ACTVWFLG                 * don't need weekends
         PROC       WDACTVDY                     * get days ward was active
         COMPARE    ZERO,ACTVDAYS                * ward active during period ?
         IF         !@EQUAL
           MOVE       TOTBED,XBNUM               * yes - update bed count
         ELSE
           GOTO       NXTMST
         ENDIF
.
NXTW20   MOVE       MSWARD,KEY3
         CALL       RDTEMP1
         COMPARE    ZERO,OVRCD
         IF         @EQUAL
           IF         RECFLG = 1
             MOVE       XBNUM,BEDNUM
             MOVE       ACTVBCHG,CHNGFLG         * set change flag
           ENDIF
           GOTO       NXTW30
         ENDIF
.
         CALL       INITTMP
         IF         RECFLG = 1
           MOVE       XBNUM,BEDNUM
           MOVE       ACTVBCHG,CHNGFLG           * set change flag
         ENDIF
         CALL       WRTEMP1             * Create a new blank record
         CALL       RDTEMP1
.
NXTW30   MOVE       MSBBDAY,TOTBDAY
         ADD        MSNBDAY,TOTBDAY      * Total number of bed days
.
         LOAD       FORM2,RECFLG,TDYMTH,LDYMTH,PDYMTH
         ADD        MSDYMTH,FORM2
         STORE      FORM2,RECFLG,TDYMTH,LDYMTH,PDYMTH
.
         LOAD       FORM8,RECFLG,TACDAYS,LACDAYS,PACDAYS
         ADD        MSACBDAY,FORM8
         STORE      FORM8,RECFLG,TACDAYS,LACDAYS,PACDAYS
.
         LOAD       FORM6,RECFLG,TADMIT,LADMIT,PADMIT
         ADD        TOTADM,FORM6
         STORE      FORM6,RECFLG,TADMIT,LADMIT,PADMIT
.
         LOAD       FORM6,RECFLG,TBDAY,LBDAY,PBDAY
         ADD        TOTBDAY,FORM6
         STORE      FORM6,RECFLG,TBDAY,LBDAY,PBDAY
.
         LOAD       FORM6,RECFLG,TSDAY,LSDAY,PSDAY
         ADD        MSSEPBD,FORM6
         STORE      FORM6,RECFLG,TSDAY,LSDAY,PSDAY
.
         LOAD       FORM6,RECFLG,TDSCH,LDSCH,PDSCH
         ADD        MSDSCH,FORM6
         STORE      FORM6,RECFLG,TDSCH,LDSCH,PDSCH
.
         LOAD       FORM6,RECFLG,TTROUT,LTROUT,PTROUT
         ADD        MSTROUT,FORM6
         STORE      FORM6,RECFLG,TTROUT,LTROUT,PTROUT
.
         CALL       UPTEMP1
         GOTO       NXTMST
.
.
INITTMP  MOVE       ZERO,TADMIT     * Admissions
         MOVE       ZERO,TBDAY      * Occupied bed days
         MOVE       ZERO,TACDAYS
         MOVE       ZERO,TDYMTH
         MOVE       ZERO,TSDAY      * Separation bed days
         MOVE       ZERO,TDSCH      * Discharges
         MOVE       ZERO,TTROUT     * Transfer out
.
         MOVE       ZERO,LADMIT
         MOVE       ZERO,LBDAY
         MOVE       ZERO,LACDAYS
         MOVE       ZERO,LDYMTH
         MOVE       ZERO,LSDAY
         MOVE       ZERO,LDSCH
         MOVE       ZERO,LTROUT
.
         MOVE       ZERO,PADMIT
         MOVE       ZERO,PBDAY
         MOVE       ZERO,PACDAYS
         MOVE       ZERO,PDYMTH
         MOVE       ZERO,PSDAY
         MOVE       ZERO,PDSCH
         MOVE       ZERO,PTROUT
         MOVE       ZERO,CHNGFLG
         MOVE       ZERO,BEDNUM
         RETURN
.
+
.
GENREP   DISPLAY    *P1:24,*EL,*P10:24,*V2LON,"** Generating Report **",*W2;
         MOVE       ONE,ASKFLAG                  * set asterisk not printed
.
         CLOSE      TEMPFILE
         TRAP       NOOPEN IF IO
         OPEN       TEMPFILE,FNAMET
         TRAPCLR    IO
.
         DISPLAY    *P1:12,*EF
         BRANCH     OPCND OF ENDREP
.
         MOVE       "60",LNO
         CLOCK      TIME,CTIMEIS
.
         DISPLAY    *P1:24,*EL,*P50:24,"Ward :";
         CALL       INITVAR
         MOVE       SP10,KEY3
         CALL       RDSTEMP1
NXTTEMP  CALL       RDKTEMP1
         BRANCH     OVRCD OF ENDREP
.
.        Get the ward description
.
         DISPLAY    *P58:24,*V2LON,MSWARD;
.
         MOVE       SP20,WBDESC
         PACK       KEY6 WITH MSWARD,SP3
         CALL       RDWARD1
         BRANCH     OVRCD,NXTTEMP
         MOVE       WBDESC,WWRDES
.
         COMPARE    "50",LNO
         CALL       HEAD IF NOT LESS
.
         MOVE       TDSCH,XDSCH
         MOVE       TTROUT,XTROUT
         MOVE       TBDAY,XBDAY             * occupied bed days
         MOVE       TSDAY,XSDAY             * separation bed days
.
         MOVE       TACDAYS,FORM8
         MOVE       TDYMTH,FORM2
         CALL       CALCAV                * This month
.
         PRINT      *1,"|",WWRDES,*15,MSWARD,*18,BEDNUM;
         IF         CHNGFLG = 1
           MOVE       ZERO,ASKFLAG
           PRINT      *22,"*";
         ENDIF
         PRINT      *23,"|",TADMIT,*30,TDSCH,*37,PCENTG,*45,AVDAY,*52,AVSTAY;
.
         ADD        AVDAY,GTDYMTH
         ADD        BEDNUM,TTBEDN
         MOVE       LDSCH,XDSCH
         MOVE       LTROUT,XTROUT
         MOVE       LBDAY,XBDAY             * occupied bed days
         MOVE       LSDAY,XSDAY             * separation bed days
.
         MOVE       LACDAYS,FORM8
         MOVE       LDYMTH,FORM2
         CALL       CALCAV                * Last month
.
         PRINT      *59,"|",LADMIT,*67,LDSCH,*74,PCENTG,*82,AVDAY,*89,AVSTAY;
.
         ADD        AVDAY,GLDYMTH
         MOVE       PDSCH,XDSCH
         MOVE       PTROUT,XTROUT
         MOVE       PBDAY,XBDAY             * occupied bed days
         MOVE       PSDAY,XSDAY             * separation bed days
.
         MOVE       PACDAYS,FORM8
         MOVE       PDYMTH,FORM2
         CALL       CALCAV                * This month last year
.
         PRINT      *96,"|",PADMIT,*104,PDSCH,*111,PCENTG:
                    *118,AVDAY,*125,AVSTAY,*132,"|"
         ADD        ONE,LNO
         ADD        AVDAY,GPDYMTH
.
         ADD        TBDAY,GTBDAY
         ADD        TSDAY,GTSDAY      
         ADD        TADMIT,GTADMIT
         ADD        TDSCH,GTDSCH
         ADD        TTROUT,GTTROUT
         ADD        TACDAYS,GTACDAY
.
         ADD        LBDAY,GLBDAY
         ADD        LSDAY,GLSDAY
         ADD        LADMIT,GLADMIT
         ADD        LDSCH,GLDSCH
         ADD        LTROUT,GLTROUT
         ADD        LACDAYS,GLACDAY
.
         ADD        PBDAY,GPBDAY
         ADD        PSDAY,GPSDAY
         ADD        PADMIT,GPADMIT
         ADD        PDSCH,GPDSCH
         ADD        PTROUT,GPTROUT
         ADD        PACDAYS,GPACDAY
         GOTO       NXTTEMP
.
+
.
ENDREP   COMPARE   ZERO TO CPAGENO
         GOTO      END10 IF NOT EQUAL
.
         CALL      HEAD
         PRINT     *N,"  ***  END OF REPORT  ***"
         DISPLAY   *P10:24,*EL,*V2LON:
                   "** No Report Generated **",*W2;
         CALL        KILLIDX
         GOTO        START
.
END10    CALL      PRTLINE
.
.        Printing the totals
.
         MOVE       GTBDAY,XBDAY             * occupied bed days
         MOVE       GTSDAY,XSDAY             * separation bed days
         MOVE       GTDSCH,XDSCH
         MOVE       GTTROUT,XTROUT
.
         MOVE       GTACDAY,FORM8
         MOVE       ZERO,FORM2
         CALL       CALCAV                * This month
         PRINT      *1,"|Hospital Total",*18,TTBEDN;
         IF         ASKFLAG = 0
           PRINT      *22,"*";
         ENDIF
         PRINT      *23,"|",GTADMIT,*30,GTDSCH,*37,PCENTG,*45,GTDYMTH:
                    *52,AVSTAY;
.
         MOVE       GLBDAY,XBDAY             * occupied bed days
         MOVE       GLSDAY,XSDAY             * separation bed days
         MOVE       GLDSCH,XDSCH
         MOVE       GLTROUT,XTROUT
.
         MOVE       GLACDAY,FORM8
         MOVE       ZERO,FORM2
         CALL       CALCAV
.
         PRINT      *59,"|",GLADMIT,*67,GLDSCH,*74,PCENTG,*82,GLDYMTH:
                    *89,AVSTAY;
.
         MOVE       GPBDAY,XBDAY             * occupied bed days
         MOVE       GPSDAY,XSDAY             * separation bed days
.
         MOVE       GPDSCH,XDSCH
         MOVE       GPTROUT,XTROUT
.
         MOVE       GPACDAY,FORM8
         MOVE       ZERO,FORM2
         CALL       CALCAV
.
         PRINT      *96,"|",GPADMIT,*104,GPDSCH,*111,PCENTG:
                    *118,GPDYMTH,*125,AVSTAY,*132,"|"
.
         CALL      PRTLINE
         PRINT     *1,"Total Admissions Since Hospital Opened = ",GTTADM
         ADD       ONE,LNO
.
         COMPARE   "48",LNO
         CALL      HEAD IF NOT LESS
.
         PRINT     *N,*N,*1,"The Percentage Occupancy   = Daily Average":
                   *70,"The Daily Average =      Occupied Bed Days":
                   *N,*7,"                       -------------- * 100":
                   *90,"----------------------------------------":
                   *N,*7,"                       Number of Beds":
                   *90,"Number of Days Ward was Active in Period";
.
         BRANCH    PTOCBDAY,END80
         PRINT     *N,*N,*1,"The Average Length of Stay =   Occupied Bed Days";
         GOTO      END90
.
END80    PRINT     *N,*N,*1,"The Average Length of Stay =   ":
                            "Separation Bed Days";
.
END90    PRINT     *N,*7,"                         -----------------------":
                   *N,*7,"                         Separations + Trans.Out":
                   *N
.
         IF        ASKFLAG = 0
           PRINT     *N,"* - Some wards changed number of `Beds' during ":
                     "period."
         ENDIF
         PRINT     *N,"*** End of Report ***"
.
         DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                   "** Report Generation Completed **",*W2;
.
         CALL       KILLIDX
         GOTO       START
.
.
NOOPEN  MOVE        ONE,OPCND
        DISPLAY     *P1:24,*B,*EL,*V2LON:
                    "Temprorary file ",FNAMET," not found.  ";
        CALL        HITENTER
        RETURN
.
.
INITVAR  MOVE       ZERO,GTBDAY
         MOVE       ZERO,GTSDAY
         MOVE       ZERO,GTADMIT
         MOVE       ZERO,GTDSCH
         MOVE       ZERO,GTTROUT
         MOVE       ZERO,GTDYMTH
         MOVE       ZERO,GLDYMTH
         MOVE       ZERO,GPDYMTH
         MOVE       ZERO,TTBEDN
.
         MOVE       ZERO,GLBDAY
         MOVE       ZERO,GLSDAY
         MOVE       ZERO,GLADMIT
         MOVE       ZERO,GLDSCH
         MOVE       ZERO,GLTROUT
.
         MOVE       ZERO,GPBDAY
         MOVE       ZERO,GPSDAY
         MOVE       ZERO,GPADMIT
         MOVE       ZERO,GPDSCH
         MOVE       ZERO,GPTROUT
         RETURN
.
+
.        Calculate percentage occupancy, daily average and average stay
.
CALCAV   MOVE       ZERO,AVDAY
         COMPARE    ZERO,FORM2
         GOTO       CALC05 IF EQUAL
.
         ASSIGN     (XBDAY/FORM2),AVDAY 
.
CALC05   MOVE       ZERO,AVSTAY
         MOVE       XDSCH,FORM6
         ADD        XTROUT,FORM6
         COMPARE    ZERO,FORM6
         GOTO       CALC10 IF EQUAL
.
         BRANCH     PTOCBDAY,CALC06
         MOVE       XBDAY,FORM62        * Use occupied bed days
         GOTO       CALC07
.
CALC06   MOVE       XSDAY,FORM62        * Use separation bed days
.
CALC07   DIV        FORM6,FORM62        * Average length of stay
         MOVE       FORM62,AVSTAY
.
CALC10   MOVE       ZERO,PCENTG
         COMPARE    ZERO,FORM8           * Number of beds
         RETURN     IF EQUAL
.
         ASSIGN     ((XBDAY*100)/FORM8),PCENTG
         RETURN
+
.
.         Report heading
.
HEAD      IF        CPAGENO <> 0
            CALL      PRTLINE
          ENDIF
          MOVE      SP4,DRGCYR
          MOVE      SP10,DRGMDSC
          MOVE      SELMTH,KEY6
          CALL      RDDRGA1
.
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *N,*1,"Inpatients",*45,"*** ",DRGMDSC," ",DRGCYR:
                          "  ",DISFRDTE," to ",DISTODTE," ***"
.
          CALL      PRTLINE
          PRINT     *1,"|",*23,"|",*37,"This Month",*59,"|",*74,"Last Month":
                    *96,"|",*105," This Month Last Year",*132,"|":
                    *N,*1,"|",*23,"|-----------------------------------":
                    *59,"|------------------------------------":
                    *96,"|-----------------------------------",*132,"|",*N:
                    *1,"| Ward",*19,"Beds",*23,"|":
                    *41,"%",*46," Daily",*54,"  Ave":
                    *59,"|",*78,"%",*82,"  Daily",*90,"   Ave":
                    *96,"|",*115,"%",*119," Daily",*127,"  Ave",*132,"|":
                    *N,*1,"|",*23,"| Admit",*31,"Disch",*40,"Occ":
                    *46,"   Ave",*54," Stay":
                    *59,"| Admit",*68,"Disch",*77,"Occ":
                    *86,"Ave",*90,"  Stay":
                    *96,"| Admit",*105,"Disch",*114,"Occ":
                    *119,"   Ave",*127," Stay",*132,"|"
.
          CALL      PRTLINE
          MOVE      TEN,LNO
          RETURN
.
.
PRTLINE   PRINT    *1,"*--------------------------------------------------":
                      "--------------------------------------------------":
                      "------------------------------*"
          ADD      ONE,LNO
          RETURN
.
.
.         Subroutine to key in a valid month and year
.
KEYMN     MOVE      TWENTY5,CCOL
          MOVE      FOUR,CVERT
          MOVE      ZERO,CHIGHLT
          UNPACK    SP4,CYEAR,CMON
          MOVE      CCC,CCENT
.
          CALL      KEYPER
          BRANCH    OVRCD,KEYMN999
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,KEYMN900
.
          GOTO      KEYMN999
.
KEYMN900  DISPLAY   *P50:4,*V2LON,*B,"** Invalid Date **",*W2,*P50:4,*EL;
          GOTO      KEYMN
.
KEYMN999  RETURN
+
.       Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX CLOSE         TEMPFILE
        CLEAR         CMDLINE
        APPEND        "iserase ",CMDLINE
        APPEND        FNAMET,CMDLINE
        RESET         CMDLINE
        EXECUTE       CMDLINE,TASKID
        DISPLAY       *P1:14,*EF,*P1:14
        RETURN
.
CREAIDX CLEAR         CMDLINE
        APPEND        "isbuild ",CMDLINE
        APPEND        FNAMET,CMDLINE
        APPEND        " 90 u1-3",CMDLINE
        RESET         CMDLINE
        EXECUTE       CMDLINE,TASKID
        DISPLAY       *P1:14,*EF,*P1:14
        RETURN
+
.****************************************************************************
.*                           ZERO0000                                       *
.*               See if this record has zero stats                          *
.****************************************************************************
.
ZERO0000  MOVE      MSNPRAD,FORM10
          ADD       MSNPBAD,FORM10
          ADD       MSTRIN,FORM10
          ADD       MSTROUT,FORM10
          ADD       MSDSCH,FORM10
          ADD       MSNBDAY,FORM10
          ADD       MSBBDAY,FORM10
          ADD       MSDEATH,FORM10
          ADD       MSBPRAD,FORM10
          ADD       MSBPBAD,FORM10
          ADD       MSLVBD,FORM10
          ADD       MSAEADM,FORM10
          ADD       MSOBSTH,FORM10
          ADD       MSOUTTH,FORM10
          ADD       MSNOPAT,FORM10
.
ZERO9999  RETURN
+
.
.          I/O Routine for tempfile (used for MMHS outpatient)
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      TEMPFILE,KEY3;MSWARD,BEDNUM:
                                  TADMIT,TBDAY,TACDAYS,TDYMTH,TDSCH,TTROUT:
                                  LADMIT,LBDAY,LACDAYS,LDYMTH,LDSCH,LTROUT:
                                  PADMIT,PBDAY,PACDAYS,PDYMTH,PDSCH,PTROUT:
                                  TSDAY,LSDAY,PSDAY,CHNGFLG
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTEMP1  RESET     KEY3
          READ      TEMPFILE,KEY3;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMPFILE;MSWARD,BEDNUM:
                                  TADMIT,TBDAY,TACDAYS,TDYMTH,TDSCH,TTROUT:
                                  LADMIT,LBDAY,LACDAYS,LDYMTH,LDSCH,LTROUT:
                                  PADMIT,PBDAY,PACDAYS,PDYMTH,PDSCH,PTROUT:
                                  TSDAY,LSDAY,PSDAY,CHNGFLG
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TEMPFILE;MSWARD,BEDNUM:
                                  TADMIT,TBDAY,TACDAYS,TDYMTH,TDSCH,TTROUT:
                                  LADMIT,LBDAY,LACDAYS,LDYMTH,LDSCH,LTROUT:
                                  PADMIT,PBDAY,PACDAYS,PDYMTH,PDSCH,PTROUT:
                                  TSDAY,LSDAY,PSDAY,CHNGFLG
          RETURN
.
WRTEMP1   RESET     KEY3
          MOVE      ZERO,OVRCD
          READ      TEMPFILE,KEY3;ANS
          GOTO      OVERCOND IF NOT OVER
.
          WRITE     TEMPFILE,KEY3;KEY3,BEDNUM:
                                  TADMIT,TBDAY,TACDAYS,TDYMTH,TDSCH,TTROUT:
                                  LADMIT,LBDAY,LACDAYS,LDYMTH,LDSCH,LTROUT:
                                  PADMIT,PBDAY,PACDAYS,PDYMTH,PDSCH,PTROUT:
                                  TSDAY,LSDAY,PSDAY,CHNGFLG
          RETURN
+
.
         INC        CALCDAYS
         INC        DATECONV
         INC        WDACTVDY
.
         INCLUDE    PATDRGIO/INC
.
         INC       PATMSTIO/INC
.
         INC       PATWR1IO/INC
.
         INCLUDE    PATWBHIO/INC
.
         INCLUDE    PATCOMN3
.
         INCLUDE    STD001IO
.
ENDIT    CHAIN      PGM
         STOP

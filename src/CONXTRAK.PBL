.******************************************************************************
.* System    :   MEDICAL RECORDS TRACKING                                     *
.* Program   :   CONXTRAK                                                     *
.* Desc      :   Remove  Tracking Data (Location & History)                   *
.******************************************************************************
.* Date      :   05/08/96                                                     *
.* Author    :   ROD                                                          *
.* Mods     :                                                                 *
.*          V9.08.01  31/01/2007  Peter Vela    CAR 127930                    *
.*                    Recompled for MRTCONFD                                  *
.*          V9.04.01  29/04/2005  Peter Vela       CAR 55214                  *
.*                    Recompiled for MRTCONFD                                 *
.*          V9.03.01  18/12/2003 Peter Vela CAR 43134                         *
.*                    Recompiled for MRTCONFD                                 *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       MRTCONFD/INC
          INC       MRTMASFD/INC
          INC       MRTHISFD/INC
          INC       MRTLOCFD/INC
          INC       PATCODFD/INC
+
.******************************************************************************
.*        Layout of convtras.txt (Master data)                                *
.******************************************************************************
TRATMPS   FILE      ASCII, FIXED=74                                        * @@
.
.Name     Type    Length       Description
.----     ----    ------       -----------
XURNO     DIM       8      1   U/R Number
XLOCAT    DIM       4      9   Home Location                  
XTYPE     DIM       3     13   Type of document (Cat TY)
XVOLUME   DIM       2     16   Volume
XSTATUS   DIM       3     18   Record Status (Cat RS)
XFILM     DIM       25    21   MicroFilm Address                             @@
XEPDT     DIM       8     46   'Episode To' Date (CCYYMMDD)                  @@
XCOMMENT  DIM       20    54   Comments
. end of record           74
+
.******************************************************************************
.*        Layout of convtram.txt (History data)                               *
.******************************************************************************
TRATMPM   FILE      ASCII, FIXED=97                                        * @@
.
.Name     Type    Length       Description
.----     ----    ------       -----------
.XURNO    DIM       8     1    U/R Number
.XLOCAT   DIM       4     9    Home Location                  
.XTYPE    DIM       3    13    Type of document (Cat TY)
.XVOLUME  DIM       2    16    Volume
XLSTMDAT  DIM       8    18    Movement date
XLSTMTIM  DIM       8    26    Movement time
XCURLOC   DIM       4    34    Current location
XRECEIV   DIM       6    38    Receiver or record
XREASON   DIM       4    44    Movement reason
XOPER     DIM       6    48    Operator who moved record
XREQB     DIM       35   54    Requested by Free Text Field.               * @@
XDUEDAT   DIM       8    89    Due Date
.  end of record         97
.
COUNT     FORM      6
COUNTIN   FORM      7
COUNTHDE  FORM      7
COUNTMDE  FORM      7
.
DATAERR   FORM      1                                                  *I-90201
DATEEND   DIM       8         * end date
DATEFROM  DIM       8         * start date
DIM10     DIM       10
DISPVAR   FORM      6
DURATION  FORM      4         * duration
.
ERRORTYP  FORM      2
.
FILENAME  DIM       99[90]
FORM6     FORM      6
FORM8     FORM      8
.
INDEX     DIM       99[90]
KEY127    DIM       127
.
SAVADMN   FORM      8
SAVDIS    DIM       9
STRTCENT  FORM      2
STRTDAYS  FORM      3         * START DATE variables
STRTLEAP  FORM      1
STRTYR    FORM      2
.
TEMP      FILE      ASCII,FIXED=256
TEMPDIS   DIM       9
TIMEIS    DIM       8    
TODAY     DIM       8
.

UPDTREQD  FORM      1
WHICHFIL  FORM      1
.
CREATDAT  INIT      "20010101"
CREATTIM  INIT      "00:00:01"
.
UNDLN100  INIT      "*-------------------------------------------------":
                    "--------------------------------------------------"
UNDLN32   INIT      "-------------------------------*"
.
PRGNAM    INIT      "Remove Tracking Data"
PRGID     INIT      "CONXTRAK"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables
          BRANCH    EXIT,ML9999
.
ML1000    CALL      OPTN0000                * Select option
          BRANCH    EXIT,ML9999
.
          CALL      OPEN0000                * Open files
          CALL      PDAT0000                * Process the Data file
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                            Initialise Variables                            *
.******************************************************************************
INIT0000  MOVE      FALSE,EXIT
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          CALL      DISPHEAD
.
          TRAP      INIT9000 IF IO
          OPEN      TRATMPS,"convtras"
          TRAPCLR   IO
.
          TRAP      INIT9000 IF IO
          OPEN      TRATMPM,"convtram"
          TRAPCLR   IO
.
          GOTO      INIT9999
.
.------ files for conversion do not exist ------
.
INIT9000  NORETURN
          TRAPCLR   IO
          DISPLAY   *P1:24,*EL,*B,"convtras.txt or convtram.txt does not ":
                    "exist - Hit <",*V2LON,"ENTER",*HOFF,"> to continue ";
          KEYIN     ANS;
          MOVE      ONE,EXIT
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      SP1,ANS
          MOVE      FALSE,EXIT
.
          DISPLAY   *P1:4,*EF,*V2LON,"* Note : This program removes all ":
                    "records in the convtras.txt & convtram.txt files *",*HOFF:
                    *P1:24,*EL,"Ok to proceed (",*V2LON,"Y",*HOFF,"/":
                           *V2LON,"N",*HOFF,") ? ";
          KEYIN     *V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN0000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
.******************************************************************************
.*                                  OPEN0000                                  *
.*                                 Open Files                                 *
.******************************************************************************
OPEN0000  DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"mrthisaf";
          OPEN      MRTHISA1,"mrthisaf"
.
          DISPLAY   *P64:24,"mrtlocaf";
          OPEN      MRTLOCA1,"mrtlocaf"
.
          DISPLAY   *P64:24,"mrtmasaf";
          OPEN      MRTMASA1,"mrtmasaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
OPEN9999  RETURN
+
.******************************************************************************
.*                                  PDAT0000                                  *
.*                     Process Morbidity Data file                            *
.******************************************************************************
PDAT0000  CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,CLNO
          CALL      HEAD0000
.
          MOVE      ZERO,COUNTIN
          MOVE      ZERO,COUNTHDE
          MOVE      ZERO,COUNTMDE
          PRINT     *N,*1,"|",*50,"'convtram.txt'",*132,"|";
.
. ------- deletethe tracking HISTORY records ------
.
PDAT0500  DISPLAY   *P1:14,"* Processing ",*V2LON,"convtram.txt ",*HOFF," *":
                    *P1:21,*EL,"Records    : ";
.
          MOVE      ZERO,UPDTREQD           * type of output                 @@
          MOVE      ONE,COUNT
          MOVE      TWO,WHICHFIL            * flag updating History file
          MOVE      ZERO,DATAERR                                       *I-90201
          TRAP      FERR2000 IF FORMAT                                 *I-90201
.
          READ      TRATMPM,SEQ;XURNO,XLOCAT,XTYPE,XVOLUME,XLSTMDAT,XLSTMTIM:
                                XCURLOC,XRECEIV,XREASON,XOPER,XREQB,XDUEDAT
          TRAPCLR   FORMAT                                             *I-90201
          GOTO      PDAT4000 IF OVER
          BRANCH    DATAERR,PDAT0500        * Get next record          *I-90201
.
          ADD       ONE,COUNT
          ADD       ONE,COUNTIN
          ASSIGN    COUNT % 100,DISPVAR
          IF        DISPVAR = 0
            DISPLAY   *P14:18,*V2LON,XURNO:
                      *P14:19,*V2LON,XLOCAT:
                      *P14:20,XVOLUME:
                      *P14:21,COUNT;
          ENDIF
.
. make sure we have a tracking master file record
.
          MOVE      FIVE,ERRORTYP
          MOVE      XVOLUME,FORM2                                          * @@
          MOVE      FORM2,XVOLUME           * Get rid of leading Zeros       @@
          PACK      KEY17,XURNO,XLOCAT,XTYPE,XVOLUME
          CALL      RDMRMAS1
          BRANCH    OVRCD,PDAT0500
.
          PACK      KEY26,MRMAKEY,XLSTMDAT,XLSTMTIM
          CALL      RDMRHIS1
          IF        OVRCD=0
            CALL      DEMRHIS1                                             * @@
            ADD       ONE,COUNTHDE
          ENDIF
.
.------ increment the record count and get the next record to process ------
.
          GOTO      PDAT0500
.
PDAT1000  MOVE      ONE,WHICHFIL            * flag updating Master file
          MOVE      ONE,CNOUNDLN
          MOVE      ZERO,SAVADMN
          MOVE      ONE,COUNT
          DISPLAY   *P1:14,"* Processing ",*V2LON,"convtras.txt ",*HOFF," *":
                    *P1:16,"Started : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:18,"U/R Number : ":
                    *P1:19,"Location   : ":
                    *P1:20,"Volume     : ":
                    *P1:21,*EL,"Records    : ";
.
.------ read through the conversion file ------
.
PDAT1500  MOVE      ZERO,DATAERR                                       *I-90201
          TRAP      FERR0000 IF FORMAT                                 *I-90201
.
          READ      TRATMPS,SEQ;XURNO,XLOCAT,XTYPE,XVOLUME,XSTATUS:        * @@
                                XFILM,XEPDT,XCOMMENT                       * @@
          TRAPCLR   FORMAT                                             *I-90201
          GOTO      PDAT9000 IF OVER
          BRANCH    DATAERR,PDAT1500        * Get next record          *I-90201
.
          ADD       ONE,COUNT
          ADD       ONE,COUNTIN
          ASSIGN    COUNT % 100,DISPVAR
          IF        DISPVAR = 0
            DISPLAY   *P14:18,*V2LON,XURNO:
                      *P14:19,*V2LON,XLOCAT:
                      *P14:20,XVOLUME:
                      *P14:21,COUNT;
          ENDIF
.
          MOVE      XVOLUME,FORM2                                          * @@
          MOVE      FORM2,XVOLUME           * Get rid of leading Zeros       @@
          PACK      KEY17,XURNO,XLOCAT,XTYPE,XVOLUME
          CALL      RDMRMAS1                                               * @@
.
          COMPARE   ONE,OVRCD               * not on file ?      
          GOTO      PDAT1500 IF EQUAL 
.
.
PDAT2000  MOVE      SIX,ERRORTYP
.
. ------- Setup for tracking history file ------
.
          MOVE      MRMAEPDT,MRHIDATE       * med record "episode" date   
          MOVE      MRMAKEY,MRHIKEY         * Med history key  
          MOVE      CREATTIM,MRHITIME       * med record create time
.
. ------- Delete tracking master file ------
.
          CALL      DETMAS1                 * delete master file 
          ADD       ONE,COUNTMDE
.
.         PACK      KEY26,MRHIKEY,MRHIDATE,MRHITIME 
.         CALL      RDMRHIS1 
.         IF        OVRCD = 1
.           GOTO      PDAT1500              * go and get next record
.         ENDIF
.
.         CALL      DEMRHIS1                * delete history file   
.         ADD       ONE,COUNTHDE
.
          GOTO      PDAT1500                * go and get next record
.
. ------- print the record counts -----------------
.
PDAT4000  PRINT     *N,*1,"|",*132,"|":
                    *N,*1,"|",*50,COUNTIN:
                    " 'convtram.txt' records read",*132,"|":
                    *N,*1,"|",*50,COUNTHDE:
                    " MR History record deleted",*132,"|";
.
          PRINT     *N,*1,"|",*132,"|":
                    *N,*1,"|",*50,"'convtras.txt'",*132,"|";
          ADD       TEN,CLNO
.
          MOVE      ZERO,COUNTIN
          MOVE      ZERO,COUNTHDE
          MOVE      ZERO,COUNTMDE
.
          GOTO      PDAT1000
.
PDAT9000  PRINT     *N,*1,"|",*132,"|":
                    *N,*1,"|",*50,COUNTIN:
                    " 'convtras.txt' records read",*132,"|":
                    *N,*1,"|",*50,COUNTMDE:
                    " Med. Record Masters deleted",*132,"|":
                    *N,*1,"|",*50,COUNTHDE:
                    " MR History record deleted",*132,"|";
.
. ------- Finished -------
.
PDAT9999  CLOCK     TIME,CTIMEIS
          PRINT     *N,UNDLN100,UNDLN32;
          PRINT     *N,*N,"**** End of Report ****";
          DISPLAY   *P20:21,*V2LON,COUNT;
          KEYIN     *P40:16,"Finished : ",*V2LON,*DV,CTIMEIS:
                    *P1:24,*EL,*HOFF,"No more records.  Tap ",*V2LON,"<ENTER>":
                    *HOFF," to exit ... ",*EOFF,ANS;
          RETURN
+
.******************************************************************************
.*                                     FERR0000                               *
.*            Print TRAP data errors in "convtram.txt" & "convtras.txt"        *
.******************************************************************************
.
FERR0000  MOVE      ONE,DATAERR
          PRINT     *N,*1,"|",*3,XURNO,*12,XLOCAT,"   ":
                    "Data error occurred in this record - Record No. :":
                    COUNTIN,"   - Record bypassed",*132,"|"
          READ      TRATMPS,SEQ;XURNO       * Bypass rest of record
          GOTO      FERR9999
.
FERR2000  MOVE      ONE,DATAERR
          PRINT     *N,*1,"|",*3,XURNO,*12,XLOCAT,"   ":
                    "Data error occurred in this record - Record No. :":
                    COUNTIN,"   - Record bypassed",*132,"|"
          READ      TRATMPM,SEQ;XURNO       * Bypass rest of record
.
FERR9999  RETURN
+
.******************************************************************************
.*                                     HEAD0000                               *
.*              Routine to print the error report header                      *
.******************************************************************************
HEAD0000  IF        !(CPAGENO = 0)
            PRINT     *N,UNDLN100,UNDLN32;
          ENDIF
.
          CALL      HEAD132                 * print error report page header
.
          PRINT     *N,*N,"*** ERROR REPORT ***":
                    *N,*N,UNDLN100,UNDLN32;
          MOVE      SEVEN,CLNO
.
HEAD9999  RETURN
+
.*********************************************************************
.                   ENDDAT00
.         Calculate an end date given a start date and duration 
.         Para's  : DATEFROM      the starting date
.                   DURATION      the number of days
.         Returns : DATEEND       the end date
.*********************************************************************
.
.         turn the starting date into julian days
.
ENDDAT00  UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          MOVE      CDAY,DD                 * set up start day
          MOVE      CMON,MM                 *              month
          MOVE      CYEAR,YY                *              year
          MOVE      CCENT,CC                *              century
.
          CALL      DMYCON                  * get julian day
          MOVE      JULDAY,STRTDAYS         * get current julian day
          MOVE      JULYR,STRTYR            * get current julian year
          MOVE      JULCC,STRTCENT          * get current julian century
          MOVE      LEAPYR,STRTLEAP         * get leap year flag (1=leap)
.
.         add the number of days
.
          ADD       DURATION,STRTDAYS       * get the total days for the year
.
.         convert back to calender date
.
          MOVE      STRTDAYS,JWKDAY         * work days
          MOVE      STRTYR,JWKYER           * work year
          MOVE      STRTCENT,JWKCC          * work century
.
          CALL      JULCON                  * convert to DD,MM,YY,CC
.
.         check for change in centuries
.
          COMPARE   STRTYR,YY
          GOTO      ENDDAT50 IF NOT LESS    * ending year >= starting year
.
.         need a change in centuries
.
          ADD       ONE,STRTCENT            * add a century
.
.         set up end date
.
ENDDAT50  PACK      DATEEND,STRTCENT,YY,MM,DD
          REP       " 0",DATEEND
.
ENDDAT99  RETURN
+
+
.******************************************************************************
.*        I/O includes                                                        *
.******************************************************************************
.
. ------  INC       STDIO001                                                 @@
          INC       STD001IO                                               * @@
          INC       MRTMASIO/INC
          INC       MRTHISIO/INC
          INC       MRTLOCIO/INC
. ------  INC       PATIOCOD/INC                                             @@
          INC       PATCODIO/INC                                           * @@

.*****************************************************************************
.*  System       :  Waiting List                                             *
.*  Program      :  IBAWAT15                                                 *
.*  Function     :  Waiting List By GP Report                                *
.*****************************************************************************
.*  Date         :  30/04/93                                                 *
.*  Author       :  Whirlpool                                                *
.*  Modifactions :                                                           *
.*****************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                        *
.*           Recompiled as WATTR1FD changes                                  *
.*****************************************************************************
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                     *
.*                  Removed port number from temp file name                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                     *
.*                  Added pmsvx1af                                           *
.*        V5.08.01  28/08/2000  Caleb Sharman                                *
.*                  Changed Health Fund variables to be 8 chars              *
.*        V5.07.01  27/10/1999 J Habasque                                    *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*                  V5.01.02 10/05/93  Steve O'Gorman                        *
.*                           Moodified so it works. Whirlpool is a Nuff Nuff *
.*        V5.01.03  28/10/1993    Justin Coates  (SRF 440204)                *
.*                  changes for new GP practice file layout (PMSHCGFD)       *
.*        V5.01.04  22/06/1994    Paul Howells   (SRF 440804)                *
.*                  Check if a GPFH as at date on list                       *
.*        V5.01.05  04/08/1994    Paul Howells   (SRF 440041)                *
.*                  Added Specialty Option.                                  *
.*****************************************************************************
.
          INC       STD001FD
          INC       BOKRC1FD/INC
          INC       IBASEQFD/INC
          INC       PATDO1FD/INC
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCLFD/INC             * Patient System Link to GP Prac.
          INC       TFILEVAR/INC
          INC       WATTR1FD/INC
          INC       WATPROFD/INC
          INC       WATCATFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATPNPFD/INC
          INC       WATSUSFD/INC
          INC       WATWTAFD/INC
          INC       WEBERRFD/INC
.
BIRTHDAT  DIM       10
CMDLINE   DIM       100
CURRPTCL  DIM       1
SAVPTCLS  DIM       1
CODEPTCL  FORM      1
DESCPTCL  DIM       21
DESCCLS   DIM       13
DIM3      DIM       3
DIM8      DIM       8
DIM25     DIM       25
DIM33     DIM       33
DIM75     DIM       75
DISTRICT  DIM       10
DOCCODE   DIM       6
EDESC     DIM       25
EDOCTOR   DIM       6
EDOL      DIM       10
ENDDATE   DIM       8
NOGPADD   FORM      1
PLADDATE  DIM       10
PTCLA     INIT      "All patients"
PTCLD     INIT      "Day cases   "
PTCLI     INIT      "Inpatients  "
PTCNDCQS  FORM      1
PMHGFLAG  FORM      "0"             Flag for display of active GP prac's in ?
.                                   0 = Don't Display Inactive Gp's Prac's
.                                   1 = Display all Gp Practices
.
PMHGQARR  DIM       12[17]        * array for pmshcgds
PMHGQCNT  FORM      2             * count of items on screen
PMHLFLAG  FORM      "0"                   Flag to display all links in ?
.                                         0 = Don't display inactive links
.                                         1 = Display all links
.
REPWAIT   FORM      2
PBREAK    FORM      1
SAVDOCT   DIM       6
SAVDATE   DIM       8
SAVGP     DIM       8
SDESC     DIM       25
SDOCTOR   DIM       6
SDOL      DIM       10
STRTDATE  DIM       8
STRTDESC  DIM       25
ENDDESC   DIM       25
URGENCY   DIM       7
CFNAMEA   DIM       8
CDFRSPEC  DIM       3         * from specialty 
CDTOSPEC  DIM       3         * to   specialty 
DXFRSPEC  DIM       30        * from specialty 
DXTOSPEC  DIM       30        * to   specialty 
DISPSTAR  DIM       32                       * Display Starting Code
.
ENDCODE   DIM       8                        * Ending Practice Code
ENDCNT    DIM       2                        * Ending Practice count
STRTCODE  DIM       8                        * Starting Practice Code
STARCNT   DIM       2                        * Starting Practice count
REPTDATE  DIM       8         * date ccyymmdd
DATE10A   DIM       10        * date mm/ccyy
DISPEND   DIM       32                       * Display Ending Code
CODE      DIM       3
GPONE     INIT      " 1"
OPTDESC   DIM       20
TODAY     DIM       8
ASATDTE   DIM       8
NEXTDAY   DIM       8
FSTAT     FORM      1
WTSPDTCK  DIM       8
NEWPG     FORM      1
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSDAYS  FORM      6
CDYSYEAR  FORM      2
SAVPRACT  DIM       6
SAVPCNT   DIM       2
DIM35     DIM       35
PRDOB     DIM       10
CLINDATE  DIM       10
LISTDATE  DIM       10
SPECDESC  DIM       20
CONSNAME  DIM       30
GPNAME    DIM       30
DIM31A    DIM       31
DIM31B    DIM       31
DIM32A    DIM       32
DIM32B    DIM       32
WTSPSTRT  DIM       8         * suspension from date
WTSPSTOP  DIM       8         * suspension to   date
CLS       DIM       1
BRK       DIM       1
.
SPECOPT   INIT      "- by Specialty"
DOLOPT    INIT      "- by Date on List"
START     INIT      "Start "
FINISH    INIT      "Finish"
CATA      INIT      "A "
CATP      INIT      "P "
CODECC    INIT      "CC"
Z8        INIT      "zzzzzzzz"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
DASH      INIT      "-"
.
. old tempfile definition 
.
TDOCT     DIM       6      1
TGP       DIM       8      7
TDATE     DIM       8      15
TUR       DIM       8      23
TPROC     DIM       9      31
TPADATE   DIM       8      40
TFUND     FORM      1      48
TURG      DIM       3      50
TPRAC     DIM       6      53
TPCCNT    DIM       2      59
.
.
TMPFILE1  IFILE     SQL, FIXED=118, KEY="D1-6,7-8,117-117,56-58"
KEYA      INIT      " 118 D1-6,7-8,117-117,56-58 D1-6,7-8,117-117,26-33"  
TMPFILE2  IFILE     SQL, FIXED=118, KEY="D1-6,7-8,117-117,26-33"
.
XXPRACT   DIM       6     1
XXPCNT    DIM       2     7
XXCODE    DIM       9     9
XXURNO    DIM       8    18
XXLDATE   DIM       8    26 
XXCDATE   DIM       8    34
XXADATE   DIM       8    42
XXCONS    DIM       6    50 
XXSPEC    DIM       3    56 
XXREGGP   DIM       8    59 
XXDESC    DIM       50   67
XXPATCLS  DIM       1   117
.         EOR           118 
.
.                       
PRGID     INIT      "IBAWAT15"
PRGNAM    INIT      "WAITING LIST BY GP REPORT"
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              ML0000                                       *
.*                             Mainline                                      *
.*****************************************************************************
.
ML0000    CALL      INIT0000                * initialistion
.
ML1000    CALL      MENU0000                * Program Menu
          BRANCH    EXIT,ML9999
.
ML2000    CALL      INST0000                * enter ranges - insert mode 
          BRANCH    EXIT,ML9999             * exit
.
ML3000    CALL      CHGE0000                * change ranges
          BRANCH    EXIT,ML1000             * cancel
.
ML5000    CALL      EXTR0000                * Extract the data
.
          CALL      PRNT0000                * Print the report
.
          GOTO      ML1000
.
ML9999    CHAIN     PGM
.
.*****************************************************************************
.*                              INIT0000                                     *
.*                        Initialisation section                             *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          CALL      IBACLOCK
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PATCODE1,"patcodes"
.
          OPEN      PATDSCH1,"patdschf"
.
          OPEN      WATSEIA1,"watseiaf"
.
          OPEN      WATSEAA1,"watseaaf"
.
          OPEN      WATPNPA1,"watpnpaf"
          OPEN      WATPNPA2,"watpnpaf"
.
          OPEN      WATSUSA1,"watsusaf"
.
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
          OPEN      WATPROA1,"watproaf"
          OPEN      PMSHCGA1,"pmshcgaf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      WATCATA1,"watcataf"
          OPEN      WATCATA2,"watcataf"
.
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          MOVE      ONE,CNEWDS
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAMEA
          
INIT9999  RETURN
.
.*****************************************************************************
.*                              MENU0000                                     *
.*                        Program Menu Routine                               *
.*****************************************************************************
.
MENU0000  MOVE      ZERO,EXIT
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = by Specialty":
                    *P1:6,*V2LON,"2",*HOFF," = by Date on List":
                    *P1:8,"Select Option ? "

MENU1000  KEYIN     *P17:8,*V2LON,MOPTION:
                    *P17:8,*DV,MOPTION
.
          BRANCH    MOPTION,MENU9500,MENU9500
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BEEP
          GOTO      MENU1000
.
MENU9000  MOVE      ONE,EXIT
          GOTO      MENU9999
.
MENU9500  LOAD      OPTDESC,MOPTION,SPECOPT,DOLOPT
.
MENU9999  RETURN
                   
.
.
.*****************************************************************************
.*                              DSCR0000                                     *
.*                        Display the screen                                 *
.*****************************************************************************
DSCR0000  DISPLAY   *P46:02,*EL,*V2LON,OPTDESC 
.
DSCR5000  DISPLAY   *P1:03,*EF,*V2LON:
                    *P2:04,"1",*P2:05,"2",*P2:07,"3",*P2:08,"4",*P2:10,"5":
                    *P2:12,"6",*P2:14,"7",*P2:16,"8",*HOFF:
                    *P3:04,". GP Practice  from      : ":
                    *P3:05,". GP Practice    to      : ":
                    *P3:07,". Specialty    from      : ":
                    *P3:08,". Specialty      to      : ":
                    *P3:10,". As at Date             : ": 
                    *P3:12,". Patient Classification : ": 
                    *P3:14,". Page Break for each GP Practice (",*V2LON,ANSY:
                           *HOFF,"/",*V2LON,ANSN,*HOFF,") ?":
                    *P3:16,". Report those waiting longer than how many ":
                           "months ?"
.
DSCR9000  UNPACK    SP20,STRTDATE,ENDDATE
          UNPACK    SP20,SDOCTOR,EDOCTOR
          UNPACK    SP20,REPTDATE
          UNPACK    SP20,CDFRSPEC,CDTOSPEC
          UNPACK    SP20,DATE10A
          UNPACK    SP30,DXFRSPEC
          UNPACK    SP30,DXTOSPEC
          UNPACK    SP20,STRTCODE,ENDCODE
          MOVE      ZERO,REPWAIT
          MOVE      ZERO,PBREAK 
          MOVE      ZERO,REPWAIT
          MOVE      SP1,CODEPTCL
.
DSCR9999  RETURN
.
+
.*********************************************************************
.         insert the details
.*********************************************************************
INST0000  MOVE      ZERO,EXIT
.
          CALL      DSCR0000                * display the screen
.
          CALL      PRAC0000                * enter practice 
          BRANCH    EXIT,INST9999           * exit
.
          CALL      SPEC0000                * enter speciality
          BRANCH    EXIT,INST9999           * exit
.
          CALL      DATE0000                * enter as at date
          BRANCH    EXIT,INST9999           * exit
.
          MOVE      SP1,CLS
          CALL      CLAS0000                * enter patient class
          BRANCH    EXIT,INST9999           * exit
.
          CALL      PBRK0000                * page break for cons?
          BRANCH    EXIT,INST9999
.
          CALL      RWMT0000                * report wait?        
          BRANCH    EXIT,INST9999
.
INST9999  RETURN
+
.*********************************************************************
.         change the details
.*********************************************************************
CHGE0000  
. 
.
CHGE5000  CALL      MAINQST
          BRANCH    CCITEM,CHGE5100,CHGE5100,CHGE5200,CHGE5200,CHGE5300:
                           CHGE5350,CHGE5400,CHGE5500
.
          COMPARE   ZERO,CCITEM
          GOTO      CHGE9000 IF EQUAL       * post 
          GOTO      CHGE9100 IF LESS        * cancel
.
          BEEP
          GOTO      CHGE5000
.
CHGE5100  CALL      PRAC0000                * enter practice
          BRANCH    EXIT,CHGE9100           * exit
          GOTO      CHGE5000
.
CHGE5200  CALL      SPEC0000                * enter speciality
          BRANCH    EXIT,CHGE9100           * exit
          GOTO      CHGE5000
.
CHGE5300  CALL      DATE0000                * enter As at Date
          BRANCH    EXIT,CHGE9100           * exit
          GOTO      CHGE5000
.
CHGE5350  MOVE      CODEPTCL,CLS
          REP       "1I2D3A",CLS
          CALL      CLAS0000                * enter patient class
          BRANCH    EXIT,CHGE9100           * exit
          GOTO      CHGE5000
.
CHGE5400  CALL      PBRK0000                * page break for cons?
          BRANCH    EXIT,CHGE9100           * exit
          GOTO      CHGE5000
.
CHGE5500  CALL      RWMT0000                * report wait?        
          BRANCH    EXIT,CHGE9100           * exit
          GOTO      CHGE5000
.
.
CHGE9000  MOVE      ZERO,EXIT               * POST
          GOTO      CHGE9999
.
CHGE9100  MOVE      ONE,EXIT                * CANCEL
.
CHGE9999  RETURN
+
.****************************************************************************
.*                                PRAC0000             Called by: CODE0000  *
.*                                                                          *
.*                  Obtain PRACes for the Practice Codes                    *
.****************************************************************************
.
.---- Starting Practice Code ----
.
PRAC0000  COMPARE   TWO,CCITEM
          GOTO      PRAC1000 IF EQUAL       * change to specilality
.
          KEYIN     *P30:04,*EL,*DV,*V2LON,STRTCODE:
                    *P30:04,*RV,STRTCODE:
                    *P30:04,*DV,STRTCODE
.
          ENDSET    STRTCODE
          APPEND    SP20,STRTCODE
PRAC0100  RESET     STRTCODE                 * Nothing input - Default to Start
          MATCH     SP8,STRTCODE
          IF        @EQUAL
            MOVE      SP8,STRTCODE           * PRACe = Start
            MOVE      SP2,STARCNT            * PRACe = Start
            MOVE      "Start",DISPSTAR
            DISPLAY   *P30:04,*EL,*V2LON,DISPSTAR
          ELSE
.
            MATCH     QUEST,STRTCODE         * Help Option entered 
            IF        @EQUAL
              CALL      GETSCR00             * Save Screen
              MOVE      SP20,KEY8
              CALL      PMSHCGDS             * Call help screen
              BRANCH    EXIT,PRAC9999
              MOVE      PMHGPRAC,STRTCODE    * Move chosen option to start PRAC
              MOVE      PMHGCNTR,STARCNT
              CALL      PUTSCR00             * Replace Screen
            ELSE
              PACK      STRTCODE,STRTCODE,SP6
              MOVE      GPONE,STARCNT
            ENDIF 
.
            PACK      PMHGPRAC,STRTCODE,SP20
            PACK      KEY12,PMHGPRAC,STARCNT    * Ensure Start Code exists
            CALL      RDPMHCG1
            IF        OVRCD = ONE
              BEEP
              GOTO      PRAC0000
            ELSE
              PACK      STRTDESC,PMHGPNAM,SP20
              MOVE      PMHGPNAM,STRTDESC
              DISPLAY   *P30:04,*EL,*V2LON,STRTCODE,*P40:04,*HOFF,PMHGPNAM
              PACK      DISPSTAR,STRTCODE,SP1,PMHGPNAM
            ENDIF
          ENDIF
.
.---- Ending Pracitce Code -----
.
PRAC1000  MATCH     Z8,ENDCODE
          IF        @EQUAL
            MOVE      SP8,ENDCODE
          ENDIF 
.
          MATCH     SP8,ENDCODE
          IF        @EQUAL
            KEYIN     *P30:05,*EL,*RV,*V2LON,ENDCODE:
                      *P30:05,*DV,ENDCODE
          ELSE
            KEYIN     *P30:05,*EL,*DV,*V2LON,ENDCODE:
                      *P30:05,*RV,ENDCODE:
                      *P30:05,*DV,ENDCODE
          ENDIF
.
          ENDSET    ENDCODE
          APPEND    SP20,ENDCODE
PRAC1100  RESET     ENDCODE                  * Nothing input - Default to End 
          MATCH     SP8,ENDCODE
          IF        @EQUAL 
            MOVE      Z8,ENDCODE          * PRACe = Finish
            MOVE      Z8,ENDCNT
            MOVE      "Finish",DISPEND
            DISPLAY   *P30:05,*EL,*V2LON,DISPEND
          ELSE  
.
            MATCH     QUEST,ENDCODE          * Help Option entered 
            IF        @EQUAL
              CALL      GETSCR00             * Save Screen
              MOVE      SP20,KEY8
              CALL      PMSHCGDS             * Call help screen
              BRANCH    EXIT,PRAC9999
              MOVE      PMHGPRAC,ENDCODE     * Move chosen option to start PRAC
              MOVE      PMHGCNTR,ENDCNT
              CALL      PUTSCR00             * Replace Screen
            ELSE
              PACK      ENDCODE,ENDCODE,SP8
              MOVE      GPONE,ENDCNT
            ENDIF
.
            PACK      PMHGPRAC,ENDCODE,SP20
            PACK      KEY12,PMHGPRAC,ENDCNT   * Ensure End Code exists
            CALL      RDPMHCG1
            IF        OVRCD = ONE
              BEEP
              GOTO      PRAC1000
            ELSE
              PACK      ENDDESC,PMHGPNAM,SP20
              DISPLAY   *P30:05,*EL,*V2LON,ENDCODE,*P40:05,*HOFF,PMHGPNAM
              PACK      DISPEND,ENDCODE,SP1,PMHGPNAM
            ENDIF
          ENDIF
.
. ---- Check end code against start code ----
.
PRAC2000  MATCH     STRTCODE,ENDCODE
          IF        @LESS
            DISPLAY   *P1:24,*B,"End Practice is less than Start Practice.  ";
            CALL      HITENTER
            GOTO      PRAC0000
          ENDIF
PRAC9999  RETURN
.*********************************************************************
.         enter the specialty  codes
.*********************************************************************
SPEC0000  COMPARE   FOUR,CCITEM
          GOTO      SPEC5000 IF EQUAL       * change to specilality
.
. ******* enter FROM specialty  *******
.
SPEC1000  MOVE      "30",ECOL
          MOVE      "7",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      CDFRSPEC,CKYIDEF3
          MOVE      CATA,CODE
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          COMPARE   TWO,EXIT
          GOTO      SPEC9100 IF EQUAL
.
          MOVE      ACODE,CDFRSPEC
          IF        EXIT = ONE
            MOVE      START,DXFRSPEC
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,START
          ELSE
            PACK      DXFRSPEC,CDFRSPEC,SP5,TDESC,SP20
            DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P40:EVERT,TDESC
          ENDIF
.
. ******* enter TO specialty  ******
.
SPEC5000  MOVE      "30",ECOL
          MOVE      "8",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      CDTOSPEC,CKYIDEF3
          MOVE      CATA,CODE
          DISPLAY   *PECOL:EVERT,SP6
.
          MATCH     Z20,CKYIDEF3
          IF        @EQUAL
            MOVE      SP3,CKYIDEF3
          ENDIF
.
          CALL      PATCODKY
          COMPARE   TWO,EXIT
          GOTO      SPEC9100 IF EQUAL
.
          MOVE      ACODE,CDTOSPEC
          IF        EXIT = ONE
            MOVE      FINISH,DXTOSPEC
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,FINISH
            MOVE      Z20,CDTOSPEC
          ELSE
            PACK      DXTOSPEC,CDTOSPEC,SP5,TDESC,SP20
            DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P40:EVERT,TDESC
          ENDIF
.
          MATCH     CDFRSPEC,CDTOSPEC
          GOTO      SPEC9000 IF NOT LESS    * valid PRACe
.
          DISPLAY   *P1:24,*B,"Invalid Specialty Range.  ",*EL;
          CALL      HITENTER
          GOTO      SPEC1000
.
SPEC9000  MOVE      ZERO,EXIT               * continue
          GOTO      SPEC9999
SPEC9100  MOVE      ONE,EXIT                * exitchar
.
SPEC9999  RETURN
+
.*********************************************************************
.         enter the as at date
.*********************************************************************
DATE0000  MATCH     SP8,REPTDATE
          IF        @EQUAL
            CALL      IBACLOCK
            PACK      TODAY,CCC,CYY,CMM,CDD
            REP       " 0",TODAY
            UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          ELSE
            UNPACK    REPTDATE,CCENT,CYEAR,CMON,CDAY
          ENDIF
          MOVE      "30",CCOL
          MOVE      "10",CVERT
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE0000          * ? entered
          BRANCH    OVRCD,DATE0000           * nothing entered
.
          PACK      REPTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",REPTDATE
.
          UNPACK    REPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE10A
.
. ******* set exit flags *******
.
DATE9000  MOVE      ZERO,EXIT                * continue
          GOTO      DATE9999
DATE9100  MOVE      ONE,EXIT                 * exit
.
DATE9999  RETURN
+
.*************************************************************************
.*  PBRK0000  :  Ask Page break for each purchaser                       *
.*************************************************************************
PBRK0000  MOVE      ZERO,PBREAK                    * assume no page break reqd.
.
PBRK1000  KEYIN     *P45:14,*V2LON,*RV,BRK:
                    *P45:14,*DV,BRK
          REP       UPPLOW,BRK
          DISPLAY   *P45:14,*V2LON,BRK
          MOVE      ONE,PBREAK
          MATCH     "Y",BRK
          GOTO      PBRK9999 IF EQUAL
          MOVE      ZERO,PBREAK
          MATCH     "N",BRK
          GOTO      PBRK9999 IF EQUAL
          BEEP
          GOTO      PBRK1000
PBRK9999  RETURN
+
.*************************************************************************
.*  RWMT0000  :  Ask Report people waiting after how many months         *
.*************************************************************************
RWMT0000  
RWMT1000  KEYIN     *P56:16,*V2LON,*RV,REPWAIT:
                    *P56:16,*DV,REPWAIT
          COMPARE   ZERO,REPWAIT
          GOTO      RWMT1000 IF LESS
RWMT9999  RETURN
.
+
.*********************************************************************
.         enter the patient classification for report
.*********************************************************************
CLAS0000  DISPLAY   *P1:24,*EL,"(",*V2LON,ANSI,*HOFF,")npatients only, (":
                    *V2LON,ANSD,*HOFF,")ay cases only, (":
                    *V2LON,ANSA,*HOFF,")ll patients"
          KEYIN     *P30:12,*EL,*V2LON,*RV,CLS
          REP       UPPLOW,CLS
          REP       "I1D2A3",CLS
          MOVE      ZERO,CODEPTCL
          MOVE      CLS,CODEPTCL
          BRANCH    CODEPTCL,CLAS1000,CLAS1000,CLAS1000
          BEEP
          GOTO      CLAS0000
.
CLAS1000  LOAD      DESCPTCL,CODEPTCL,PTCLI,PTCLD,PTCLA
          DISPLAY   *P30:12,*EL,*V2LON,DESCPTCL,*P1:24,*EL;
.
CLAS9999  RETURN
+
.*********************************************************************
.*                        EXTR0000                                   *
.*                   Print the master listing                        *
.*********************************************************************
.
EXTR0000  DISPLAY   *P1:24,*EL,*V2LON,"***** Extracting Data *****"
.
          CALL      CTMP0000                * create the tempfile
.
.****** Position correctly in the file *****
.
          PACK      KEY27,REPTDATE,Z30
          CALL      RDSTREA3
EXTR1000  CALL      RDPTREA3             * get next record
          BRANCH    OVRCD,EXTR9999
.
          MOVE      REPTDATE,ASATDTE
          CALL      CHKSTAT              * check status at date
.
          COMPARE   FIVE,FSTAT          * ignore Discharged 
          GOTO      EXTR1000 IF EQUAL
.
          COMPARE   SIX,FSTAT           * ignore Removed
          GOTO      EXTR1000 IF EQUAL
.
          COMPARE   SEVEN,FSTAT         * Performed 
          GOTO      EXTR1000 IF EQUAL
.
. ******* see if suspended at report date *******
.
          MOVE      REPTDATE,WTSPDTCK       * set date to check for
          CALL      WATONSUS                * see if suspended
          COMPARE   ZERO,EXIT
          GOTO      EXTR1000 IF EQUAL       * suspended at given date
.
...          COMPARE   EIGHT,FSTAT         * Not Performed?
...          GOTO      EXTR1000 IF EQUAL
.
.
.         report if admitted and outcome is not performed
.
          PACK      KEY27,WMURNO,WMCODE,WMCNT,WMBOOK,SP30
          CALL      RDWTPNP2
          BRANCH    OVRCD,EXTR1050   
          PACK      KEY8,WMBOOK,SP10
          CALL      RDMISS1
          BRANCH    OVRCD,EXTR1000
          IF        ASTAT <> 2 
            GOTO      EXTR1000         * not a current admission
          ENDIF
.
          GOTO      EXTR1100
.
EXTR1050  COMPARE   FOUR,FSTAT          * ignore Admitted 
          GOTO      EXTR1000 IF EQUAL
.
EXTR1100
.
.         check if planned patient as at date
.
          UNPACK    ASATDTE,XCENT,XYEAR,XMON,XDAY
          MOVE      XCENT,CC
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      NEXTDAY,CC,YY,MM,DD
          REP       " 0",NEXTDAY
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANSC,ANSC,NEXTDAY,SP10
          CALL      RSWTCAT2
          CALL      RPWTCAT2
          BRANCH    OVRCD,EXTR1300
.
          MATCH     WMURNO,WTCAURNO
          GOTO      EXTR1300 IF NOT EQUAL
.
          MATCH     WMCODE,WTCAPROC
          GOTO      EXTR1300 IF NOT EQUAL
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      EXTR1300 IF NOT EQUAL
.
          MATCH     CODECC,WTCACATF
          GOTO      EXTR1300 IF NOT EQUAL
.
          PACK      KEY5,ANSC,ANSC,WTCACODT,SP5
          CALL      RDCODE1
          BRANCH    OVRCD,EXTR1300
          MATCH     ANSP,TCINDC3
          GOTO      EXTR1000 IF EQUAL          * exclude planned patients
.
EXTR1300  
.
          DISPLAY   *P50:24,WMURNO;
.
. check GP practice is in range
.
          MATCH     STRTCODE,WTWMGPPC
          GOTO      EXTR1000 IF LESS
.
          MATCH     WTWMGPPC,ENDCODE
          GOTO      EXTR1000 IF LESS
.
. check if specialty in range
.
          MATCH     CDFRSPEC,WMPCAT
          GOTO      EXTR1000 IF LESS
          MATCH     WMPCAT,CDTOSPEC
          GOTO      EXTR1000 IF LESS
.
. check if user has security to this record
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          IF        OVRCD=1
            PACK      KEY13,WMDOCTOR,WMUNIT,PASSCODE
            CALL      RDWTSEI1
            BRANCH    OVRCD,EXTR1000
          ENDIF
.
. check if the patient has waited longer than the specified waiting period
.
          CALL      VALW0000
          BRANCH    EXIT,EXTR1000                 * invalid waiting period
.
. ------- work out the patient classification value -------
.
          PACK      KEY5,CATP,WTWMCLSS      * patient category
          CALL      RDCODE1
          BRANCH    OVRCD,EXTR1000          * invalid code
          REP       " 1",TCINDC2            * default to inpatients
.
          IF        CODEPTCL = ONE
            MATCH     "1",TCINDC2
            GOTO      EXTR1000 IF NOT EQUAL   * not an Inpatient
            MOVE      ANSA,CURRPTCL           * only want Inpatients
          ENDIF
          IF        CODEPTCL = TWO
            MATCH     "2",TCINDC2
            GOTO      EXTR1000 IF NOT EQUAL   * not a Day Case
            MOVE      ANSD,CURRPTCL           * only want Day cases
          ENDIF
          IF        CODEPTCL = THREE
            MOVE      TCINDC2,CURRPTCL        * set using indicator
            REP       "1A2D",CURRPTCL         * get to required value
          ENDIF
.   only allow inpats and day cases??
        MATCH "A",CURRPTCL
        IF !@EQUAL
          MATCH "D",CURRPTCL
          GOTO EXTR1000 IF NOT EQUAL
        ENDIF
.
.
. set up vars for write
.
          MOVE      CURRPTCL,XXPATCLS
          MOVE      WTWMGPPC,XXPRACT
          MOVE      WTWMGPPT,XXPCNT
          MOVE      WMCODE,XXCODE
          MOVE      WMDESC,XXDESC
          MOVE      WMURNO,XXURNO
          MOVE      WTWMDTAD,XXCDATE
          MOVE      WMDATE1,XXLDATE
          MOVE      WMDATE3,XXADATE
          MOVE      WMPCAT,XXSPEC
          MOVE      WMDOCTOR,XXCONS
          MOVE      WTWMRFGP,XXREGGP
.
          PACK      KEY12,XXPRACT,XXPCNT,XXPATCLS,XXSPEC
          CALL      WRXXTMP1
.
          GOTO      EXTR1000
.
EXTR9999  RETURN
.
.*********************************************************************
.*                         HEAD0000                                  *
.*                   Print the required heading                      *
.*********************************************************************
.
HEAD0000
          LOAD      CPHDROPT,MOPTION,SPECOPT,DOLOPT
          CALL      IBACLOCK
          CALL      HEAD132 
          PRINT     " "
          MATCH     SP8,STRTCODE
          IF        @EQUAL
            PRINT     *40,"Practice  From : Start"
          ELSE 
            PRINT     *40,"Practice  From : ",STRTCODE,SP2,STRTDESC
          ENDIF
          MATCH     Z8,ENDCODE
          IF        @EQUAL
            PRINT     *40,"            To : Finish"
          ELSE 
            PRINT     *40,"            To : ",ENDCODE,SP2,ENDDESC
          ENDIF
.
          PRINT     *40,"Specialty From : ",DXFRSPEC,*N:
                    *40,"            To : ",DXTOSPEC
.
          PRINT     *40,"    As at Date : ",DATE10A
          PRINT     *40,"Waiting Period : > ",REPWAIT," months",*N
.
...          CALL      PTAB0000                      * print table
          MOVE      TEN,CLNO
          MOVE      ZERO,NOGPADD
.
HEAD9999  RETURN
+
.***********************************************************************
.*  VALW0000  :  check if valid waiting period                         *
.***********************************************************************
VALW0000  MOVE      ZERO,EXIT
.
          COMPARE   ZERO,REPWAIT
          GOTO      VALW9999 IF EQUAL
.
          MOVE      WMDATE1,CDYSFDTE              * from date
          MOVE      REPTDATE,CDYSTDTE             * to date
          CALL      CALCDAYS
          IF        (REPWAIT*30) > CDYSDAYS
            MOVE      ONE,EXIT                    * havent waited long enough
          ELSE
            MOVE      ZERO,EXIT
          ENDIF
.
VALW9999  RETURN
+
.***********************************************************************
.*  PTAB0000  :  print table                                           *
.***********************************************************************
PTAB0000  
.
          MOVE      SP30,DESCCLS
          MATCH     ANSA,XXPATCLS
          IF        @EQUAL
            MOVE      "Inpatients",DESCCLS
          ELSE
            MOVE      "Day Cases ",DESCCLS
          ENDIF
          IF        NOGPADD = 0
            CALL      GPAD0000
          ENDIF
          PRINT     *N,*1,DESCCLS
          PRINT     *1,"----------"
          CALL      UND132 
          PRINT       *1,"|",*3,"PROCEDURE",*46,"| U/R NUMBER":
                      *59,"| NAME AND ADDRESS",*97,"| ":
                      "CLINICAL DECISION",*118,"| PLANNED ADM":
                      *132,"|":
                      *N,*1,"|",*46,"|",*59,"|",*97,"|",*99,"DATE ON LIST":
                      *118,"|    DATE",*132,"|"
          CALL      UND132 
          ADD       EIGHT,CLNO
.          
PTAB9999  RETURN
+
.*********************************************************************
.*                        PRNT0000                                   *
.*                   Print the master listing                        *
.*********************************************************************
.
PRNT0000
          MOVE      ONE,CNOUNDLN
          MOVE      ZERO,CPAGENO
          DISPLAY   *P1:24,*EL,"Printing :"
          PACK      KEY12,SP20
          CALL      RSXXTMP1
          CALL      RKXXTMP1             * get next record
          IF        OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"No records found. ";
            CALL      HITENTER
            CALL      HEAD0000
            GOTO      PRNT8000
          ELSE
            MOVE      XXPRACT,SAVPRACT 
            MOVE      XXPCNT,SAVPCNT
            MOVE      XXPATCLS,SAVPTCLS
          ENDIF
       
          CALL      HEAD0000             * print the heading
          CALL      PTAB0000             * print table
.
. position correctly in the file
.
          IF        MOPTION=1
            PACK      KEY12,SP20          * by specialty
            CALL      RSXXTMP1
          ELSE
            PACK      KEY17,SP20         * by date on list
            CALL      RSXXTMP2
          ENDIF
.
. read next record using the correct sequence
.
PRNT1000  DISPLAY   *P12:24,*EL,*V2LON,XXPRACT,"/",XXPCNT
          BRANCH    MOPTION,PRNT1100,PRNT1200     * which sequence????
.
PRNT1100  CALL      RKXXTMP1             * get next record
          BRANCH    OVRCD,PRNT8000
          GOTO      PRNT1500
.
PRNT1200  CALL      RKXXTMP2             * get next record
          BRANCH    OVRCD,PRNT8000
.
. we have a record so do something with it
.
PRNT1500  MOVE      ZERO,NOGPADD
. 
          PACK      KEY8,XXURNO
          CALL      RDMAST1
          BRANCH    OVRCD,PRNT1000
.
          MOVE      PTITL,PACTITLE
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM35
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDOB
.
          CALL      FRPD0000      * format procedure desc into 2 lines
.
. format clinical decision to admit date 
.
PRNT1700  MOVE      SP10,CLINDATE
          PACK      XXCDATE,XXCDATE,SP8
          MATCH     SP8,XXCDATE
          GOTO      PRNT2000 IF EQUAL
.
          UNPACK    XXCDATE,CCENT,CYEAR,CMON,CDAY
          PACK      CLINDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
.
. format date on list
.
PRNT2000  MOVE      SP10,LISTDATE
          PACK      XXLDATE,XXLDATE,SP8
          MATCH     SP8,XXLDATE
          GOTO      PRNT3000 IF EQUAL
.
          UNPACK    XXLDATE,CCENT,CYEAR,CMON,CDAY
          PACK      LISTDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
. format planned admission date
.
PRNT3000  MOVE      SP10,PLADDATE
          PACK      XXADATE,XXADATE,SP8
          MATCH     SP8,XXADATE
          GOTO      PRNT4000 IF EQUAL
.
          UNPACK    XXADATE,CCENT,CYEAR,CMON,CDAY
          PACK      PLADDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.         
PRNT4000  COMPARE   "55",CLNO
          IF        !@LESS
            CALL      HEAD0000                      * print new page
            CALL      PTAB0000
            GOTO      PRNT4800
          ENDIF
.
. check if we have a new practice 
. 
          MATCH     XXPRACT,SAVPRACT
          GOTO      PRNT4500 IF NOT EQUAL
.
          MATCH     XXPCNT,SAVPCNT
          IF        !@EQUAL
            CALL      PTAB0000
            GOTO      PRNT4800
          ENDIF
.         
. ******* total for patient classification *******
.

          MATCH     SAVPTCLS,XXPATCLS
          IF        !@EQUAL
            MOVE      ONE,NOGPADD
            CALL      PTAB0000             * print table
            GOTO      PRNT4800
          ENDIF
.
          GOTO      PRNT4800
.
PRNT4500    IF        PBREAK=1
              CALL      HEAD0000                  * print new page
              CALL      PTAB0000             * print table
            ELSE
              CALL      PTAB0000   * new page not reqd. print only table heading
            ENDIF
.
.
PRNT4800    MOVE      XXPRACT,SAVPRACT 
            MOVE      XXPCNT,SAVPCNT
            MOVE      XXPATCLS,SAVPTCLS
.
. format address lines
.
          PACK        PADD2,PADD2,SP20,SP20
          PACK        PSUBRB,PSUBRB,SP20,SP20
          PACK        PADD4,PADD4,SP20,SP20
          PACK        PPOST,PPOST,SP8
.
. get specialty descripiton
.
          MOVE      "<<Missing>>",TDESC
          PACK      KEY5,ANSA,SP1,XXSPEC
          CALL      RDCODE1
          MOVE      TDESC,SPECDESC
.
. format the consultant name
.
          MOVE      "<<Missing Cons>>",CONSNAME
          PACK      KEY6,XXCONS
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,ANS
            MOVE      ANS,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,CONSNAME
          ENDIF
.
. format the GP name
.
          MOVE      "<<Missing GP>>",GPNAME
          PACK      PMHCHCPC,XXREGGP
          PACK      KEY10,XXREGGP
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      SP4,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,GPNAME
          ENDIF
.
. print the data
.
          PRINT    *1,"| Spec:",*13,XXSPEC,SP2,SPECDESC,*46,"|  ",XXURNO:
                   *59,"| ",DIM35,*97,"|  ",CLINDATE,*118,"| ",PLADDATE,*132,"|"
          PRINT     *1,"| Cons:",*13,CONSNAME,*46,"|",*59,"| ",PADD1:
                    *97,"|",*118,"|",*132,"|"
          PRINT     *1,"|",*46,"|",*59,"| ",PADD2,*97,"|",*118,"|",*132,"|"
          PRINT     *1,"| ",XXCODE,*13,DIM32A,*46,"|   D.O.B.",*59,"| ":
                    PSUBRB,*97,"|  ",LISTDATE,*118,"|",*132,"|"
          PRINT     *1,"|",*13,DIM32B,*46,"| ",PRDOB,*59,"| ",PADD4:
                    *97,"|",*118,"|",*132,"|"
          PRINT     *1,"| Reg. GP:",*13,GPNAME,*46,"|",*59,"| ",PPOST:
                    *97,"|",*118,"|",*132,"|"
.
          ADD       SIX,CLNO
          CALL      UND132
.
. new page required?
.
PRNT5000  GOTO      PRNT1000
.
. print end of report details
.
PRNT8000  PRINT     " ",*N:
                    "NOTE: (i)  Each Months Wait is Assumed to be 30 Days.":
                    *N,"      (ii) Utilises Date on List for Patient ":
                    "Selection.":
                    *N,"      (iii)Patients who are Suspended,Planned or":
                    " Removed As at the Report date are excluded from this report.":
                    *N,"      (iv) Patients who are unscheduled,scheduled or ":
                    "Pre-Admitted As at the Report date are included in this ":
                    "report.": 
                    *N,*N,"***** End of Report *****"
          DISPLAY   *P1:24,*EL
.
PRNT9999  RETURN
+
.************************************************************************
.*  FRPD0000  :  Format Procedure description                           *
.************************************************************************
FRPD0000  
...       MOVE      "<<Missing>>",DIM32A
...       MOVE      SP30,DIM32B
...       PACK      KEY9,XXCODE
...       CALL      RDPROA1
...       BRANCH    OVRCD,FRPD9999
.
          UNPACK    XXDESC,DIM32A,DIM32B
          MATCH     SP30,DIM32B                   * anything in the 2nd line?
          IF        !@EQUAL
            UNPACK    XXDESC,DIM31A,DIM32B
            PACK      DIM32A,DIM31A,DASH
          ELSE
            MOVE      XXDESC,DIM32A               * move all of it!
          ENDIF
.
FRPD9999  RETURN
.
.*****************************************************************************
.*                           GPAD0000                                        *
.*                     Print the gp ADDRESS                                  *
.*****************************************************************************
.
GPAD0000  
....          PRINT      *N,"GP Details    : ";
....          PACK       DIM75,SP30,SP30,SP20
.... 
....          PACK       PMHCHCPC,TGP
....          PACK       KEY10,PMHCHCPC
....          CALL       RDPMHCP1
....          BRANCH     OVRCD,GPAD5000
....          MOVE      SP3,PACTITLE
....          MOVE      PMHCSNAM,PACSNAME
....          MOVE      PMHCGNAM,ANS
....          MOVE      ANS,PACGNAME
....          MOVE      ONE,PACFRMT
....          CALL      PACNAME
....          MOVE      PACFNAME,DIM25
.
....          STRIP     DIM25
....       
....          PRINT      *17,TGP,*17,TGP,SP2,DIM25;
.
....          PRINT      " ";
....          STRIP      PMHCADR1
....          STRIP      PMHCADR2
....          STRIP      PMHCADR3
....          STRIP      PMHCADR4
....         MOVE       ", ",DIM2
....       PACK       DIM75,PMHCADR1,DIM2,PMHCADR2,DIM2,PMHCADR3,DIM2,PMHCADR4:
....                    DIM2,PMHCPOST
....          PRINT      *55,DIM75
.
GPAD5000  PRINT      *N,"GP Practice   : ",*17,XXPRACT,"/",XXPCNT:
                                        *17,XXPRACT,"/",XXPCNT;
.
          PACK       PMHGPRAC,XXPRACT,SP20
          PACK       KEY12,PMHGPRAC,XXPCNT
          CALL       RDPMHCG1
          BRANCH     OVRCD,GPAD9000
.
          PRINT      *27,PMHGPNAM,*55,"Fund Holder : ";
          MATCH      SP5,PMHGGPFH
          IF         !@EQUAL
            MATCH      XXLDATE,PMHGDTFA
            IF         @LESS | @EQUAL
              MATCH      PMHGDTFE,XXLDATE
              IF         @LESS | @EQUAL 
                PRINT      "Yes"
                GOTO       GPAD9000
              ENDIF
            ENDIF
          ENDIF
.
          PRINT      "No"
.
GPAD9000  
GPAD9999  RETURN
.
.
.ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ

.*****************************************************************************
.*                           FRMT0000                                        *
.*                     Format all details to print                           *
.*****************************************************************************
.
FRMT0000
          PACK      PADD1,SP30,SP10
          PACK      PADD2,SP30,SP10
          PACK      PSUBRB,SP30,SP10
          PACK      PADD4,SP30,SP10
          PACK      DIM33,SP30,SP10
          MOVE      SP30,DIM25
          UNPACK    SP30,DISTRICT,BIRTHDAT,PLADDATE
          MOVE      SP3,DIM3
          MOVE      SP30,PTMXDOFR
.
          PACK      KEY8,TUR
          CALL      RDMAST1
          BRANCH    OVRCD,FRMT5000
.
          MOVE      PTITL,PACTITLE
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM25
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BIRTHDAT
.
          MATCH     SP3,PTMXDOFR
          GOTO      FRMT5000 IF EQUAL
.
          PACK      KEY5,ANSD,ANSR,PTMXDOFR
          CALL      RDCODE1
          BRANCH    OVRCD,FRMT5000
.
          MOVE      TDESC,DISTRICT
.

FRMT5000  UNPACK    TPADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PLADDATE
.
          MOVE      SP30,TDESC
          PACK      KEY5,ANST,ANSP,TURG
          CALL      RDCODE1
.
          MOVE      TDESC,URGENCY
.
          PACK      KEY9,TPROC
          CALL      RDPROA1
          BRANCH    OVRCD,FRMT9999
.
          MOVE      WPDESC,DIM33
.
FRMT9999  RETURN
.
.*****************************************************************************
.*                           CHED0000                                        *
.*                     Print the coloumn headings                            *
.*****************************************************************************
.
CHED0000  CALL      UND132
          PRINT     *1,"|",*73,"|":
                    *83,"|",*85,"PLANNED",*96,"|",*132,"|"
          PRINT     *1,"| PATIENT DETAILS",*73,"|",*75,"URGENCY":
                    *83,"|",*85,"ADMISSION",*96,"|",*98,"PROCEDURE",*132,"|"
          CALL      UND132
.
          ADD       TWO,CLNO
CHED9999  RETURN
.
.*****************************************************************************
.*                           HEAD0000                                        *
.*                        Print the report heading                           *
.*****************************************************************************
.
SEAD0000  CALL      HEAD132
.
          PRINT     *40,"Consultant   from : ",SDESC:
                    *N,*40,"               to : ",EDESC,*N:
                    *N,*40,"Date on list from : ",SDOL:
                    *N,*40,"               to : ",EDOL
.
          UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PACK      PACFNAME,SP30,SP30
          PACK      KEY6,TDOCT
          CALL      RDDOCT1
          BRANCH    OVRCD,SEAD2000
.
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM25
.
SEAD2000  PRINT      *N,"Consultant    : ",*17,TDOCT,*17,TDOCT,SP2,PACFNAME,*N:
                        "Date on list  : ",*17,CPCDATE,*17,CPCDATE
.
          ADD        SEVEN,CLNO  
SEAD9999  RETURN
+
.*****************************************************************************
.*                              KDOC0000                                     *
.*                        Keyin the consultant code                          *
.*****************************************************************************
.
KDOC0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          MOVE      FOUR,EVERT
          MOVE      "25",ECOL
.
          COMPARE   TWO,CCITEM
          GOTO      KDOC5000 IF EQUAL       * change to specilality
.
          DISPLAY   *PECOL:EVERT,*EL
          CALL      PATDOCKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KDOC9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,START
            MOVE      SP6,SDOCTOR
            PACK      SDESC,START,SP30,SP10
            GOTO      KDOC5000
          ENDIF
.
          MOVE      DOCCODE,SDOCTOR
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM25
          DISPLAY   *P35:EVERT,DIM25
          PACK      SDESC,DOCCODE,SP2,DIM25
.
KDOC5000  MOVE      ZERO,EXIT
.
          MOVE      FIVE,EVERT
          DISPLAY   *PECOL:EVERT,*EL
          CALL      PATDOCKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KDOC9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,FINISH
            MOVE      "zzzzzzz",EDOCTOR
            MOVE      ZERO,EXIT
            PACK      EDESC,FINISH,SP30,SP10
            GOTO      KDOC9999
          ENDIF
.
          MOVE      DOCCODE,EDOCTOR
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM25
          DISPLAY   *P35:EVERT,DIM25
          PACK      EDESC,DOCCODE,SP2,DIM25
.
          MATCH     SDOCTOR,EDOCTOR
          IF        @LESS
            DISPLAY   *P1:24,*EL,"Consultant codes must be in sequence. ";
            CALL      HITENTER
            GOTO      KDOC0000
          ENDIF
          
.
KDOC9999  RETURN
.
.*****************************************************************************
.*                              KDAT0000                                     *
.*                        Keyin the date on list Range                       *
.*****************************************************************************
.
KDAT0000  MOVE      ZERO,EXIT
.
          MOVE      CCC,CCENT
          UNPACK    SP20,CYEAR,CMON,CDAY
          MOVE      "25",CCOL
          MOVE      SEVEN,CVERT
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CHIGHLT
.
          COMPARE   FOUR,CCITEM
          GOTO      KDAT5000 IF EQUAL       * change to specilality
.
          DISPLAY   *P25:7,*EL
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            DISPLAY   *PCCOL:CVERT,*V2LON,START
            MOVE      SP10,STRTDATE
            PACK      SDOL,START,SP10
            GOTO      KDAT5000
          ENDIF
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          PACK      SDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
KDAT5000  MOVE      EIGHT,CVERT
          DISPLAY   *P25:8,*EL
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            DISPLAY   *PCCOL:CVERT,*V2LON,FINISH
            MOVE      "zzzzzzzzzzzz",ENDDATE
            PACK      EDOL,FINISH,SP10
            GOTO      KDAT9999
          ENDIF
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          PACK      EDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,"Dates must be in sequence. ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
.
KDAT9999  RETURN
.
.*****************************************************************************
.*                           SAVR0000                                        *
.*                        SAVRact the relevant data                          *
.*****************************************************************************
.
SAVR0000
          DISPLAY   *P1:24,*EL,"SAVRacting for : "
.
          MOVE      ONE,EXIT
          MOVE      SP8,DIM8
.
          PACK      KEY27,STRTDATE,SP30
          CALL      RDSTREA3
SAVR1000  CALL      RDKTREA3
          BRANCH    OVRCD,SAVR9000 
.
          COMPARE   WMSTAT,THREE
          GOTO      SAVR1000 IF LESS
.
          MATCH     WMDATE1,ENDDATE
          GOTO      SAVR9000 IF LESS 
.
          MATCH     SDOCTOR,WMDOCTOR
          GOTO      SAVR1000 IF LESS
.
          MATCH     WMDOCTOR,EDOCTOR
          GOTO      SAVR1000 IF LESS
.
          MOVE      ZERO,EXIT
.
          MATCH     DIM8,WMDATE1
          IF        !@EQUAL
            UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            DISPLAY   *P18:24,*V2LON,CPCDATE
            MOVE      WMDATE1,DIM8
          ENDIF
.
          MOVE      WMDOCTOR,TDOCT
          MOVE      WTWMRFGP,TGP
          MOVE      WMDATE1,TDATE
          MOVE      WMURNO,TUR
          MOVE      WMCODE,TPROC
          MOVE      WMDATE3,TPADATE
          MOVE      WMPTY,TURG
          MOVE      WTWMGPPC,TPRAC
          MOVE      WTWMGPPT,TPCCNT
          
          PACK      WTWMGPFA,WTWMGPFA,SP20
          MATCH     WTWMGPFA,SP20
          IF        @EQUAL
            MOVE      ZERO,TFUND
          ELSE
            MOVE      ONE,TFUND
          ENDIF
.
          PACK      KEY22,TDOCT,TGP,TDATE
...          CALL      WRWLBGP1
          GOTO      SAVR1000
.
SAVR9000  IF        EXIT = 1
            DISPLAY   *P1:24,*EL,"No data found. "; 
            CALL      HITENTER
          ENDIF
        
SAVR9999  RETURN
.
.*****************************************************************************
.*                           SAVT0000                                        *
.*                        Print the report                                   *
.*****************************************************************************
.
SAVT0000  MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
.
          UNPACK    SP30,SAVDOCT,SAVDATE
          MOVE      "zzzzzzzzzz",SAVGP
.
          PACK      KEY22,SP30
..          CALL      RSWLBGP1
SAVT1000  
..CALL      RKWLBGP1
          BRANCH    OVRCD,SAVT9000
.
          IF        CLNO > 50
            UNPACK    SP30,SAVDOCT,SAVGP,SAVDATE
          ENDIF
.
          MATCH     TDOCT,SAVDOCT
          IF        !@EQUAL
            CALL      HEAD0000
            MOVE       TDOCT,SAVDOCT
            MOVE       TDATE,SAVDATE
            MOVE      "zzzzzzzzzz",SAVGP
          ELSE
            MATCH     TDATE,SAVDATE
            IF        !@EQUAL
              CALL      HEAD0000
              MOVE      "zzzzzzzzzz",SAVGP
              MOVE       TDOCT,SAVDOCT
              MOVE       TDATE,SAVDATE
            ENDIF
          ENDIF
.
          MATCH      TGP,SAVGP
          IF         !@EQUAL
            CALL       GPAD0000
            ADD        THREE,CLNO
            MOVE       TGP,SAVGP
            MOVE       TDOCT,SAVDOCT
            MOVE       TDATE,SAVDATE
            CALL       CHED0000             * print the column headings
          ENDIF
            
          CALL      FRMT0000                * format details for print

          PRINT     *1,"|",*3,TUR,*12,DIM25,*47,"Born",*59,": ",BIRTHDAT:
                    *73,"|",*75,TURG:
                    *83,"|",*85,PLADDATE,*96,"| ",TPROC,*132,"|"
          PRINT     *1,"|",*12,PADD1,*73,"|",*75,URGENCY:
                    *83,"|",*96,"| ",DIM33,*132,"|"
          PRINT     *1,"|",*12,PADD2,*47,"District",*59,": ",PTMXDOFR,*73,"|":
                    *83,"|",*96,"|",*132,"|"
          PRINT     *1,"|",*12,PSUBRB,*61,DISTRICT,*73,"|":
                    *83,"|",*96,"|",*132,"|"
          PRINT     *1,"|",*12,PADD4,*73,"|":
                    *83,"|",*85,*96,"|",*132,"|"
          PRINT     *1,"|",*38,PPOST,*73,"|":
                    *83,"|",*85,*96,"|",*132,"|"
          CALL      UND132
          ADD       SIX,CLNO
.
          GOTO      SAVT1000
.
SAVT9000  PRINT     *N:
                    "***** End of Report *****"

SAVT9999  RETURN
.
.************************************************************************
.*                            CTMP0000                                  *
.*                      Create the temp file                            *
.************************************************************************
.
CTMP0000  CALL      KTMP0000
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    CFNAMEA,CMDLINE
          APPEND    KEYA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPFILE1,CFNAMEA
          OPEN      TMPFILE2,CFNAMEA 
.
CTMP9999  RETURN
.
.
.************************************************************************
.*                            KTMP0000                                  *
.*                        Kill the temp file                            *
.************************************************************************
.
KTMP0000  CLOSE     TMPFILE1
          CLOSE     TMPFILE2
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    CFNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KTMP9999  RETURN
.
.*************************************************************************
.*  Temp I/O's                                                           *
.*************************************************************************
.
RSXXTMP1  RESET     KEY12 
          READ      TMPFILE1,KEY12;;
          RETURN
.
RDXXTMP1  MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      TMPFILE1,KEY12;XXPRACT,XXPCNT,XXCODE,XXURNO,XXLDATE,XXCDATE:
                                  XXADATE,XXCONS,XXSPEC,XXREGGP,XXDESC,XXPATCLS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKXXTMP1  MOVE      ZERO,OVRCD
          READKS    TMPFILE1;XXPRACT,XXPCNT,XXCODE,XXURNO,XXLDATE,XXCDATE:
                             XXADATE:
                                   XXCONS,XXSPEC,XXREGGP,XXDESC,XXPATCLS
          GOTO      OVERCOND IF OVER
          RETURN
.
RPXXTMP1  MOVE      ZERO,OVRCD
          READKP    TMPFILE1;XXPRACT,XXPCNT,XXCODE,XXURNO,XXLDATE,XXCDATE:
                             XXADATE:
                                   XXCONS,XXSPEC,XXREGGP,XXDESC,XXPATCLS
          GOTO      OVERCOND IF OVER
          RETURN
.
UPXXTMP1  UPDATE    TMPFILE1;XXPRACT,XXPCNT,XXCODE,XXURNO,XXLDATE,XXCDATE:
                             XXADATE:
                                   XXCONS,XXSPEC,XXREGGP,XXDESC,XXPATCLS
          RETURN
.
WRXXTMP1  RESET     KEY12
          WRITE     TMPFILE1,KEY12;XXPRACT,XXPCNT,XXCODE,XXURNO,XXLDATE,XXCDATE:
                                  XXADATE,XXCONS,XXSPEC,XXREGGP,XXDESC,XXPATCLS
          RETURN
.
DEXXTMP1  RESET     KEY12
          DELETE    TMPFILE1,KEY12
          RETURN
.
. 2nd index
.
RSXXTMP2  RESET     KEY17
          READ      TMPFILE2,KEY17;;
          RETURN
.
RKXXTMP2  MOVE      ZERO,OVRCD
          READKS    TMPFILE2;XXPRACT,XXPCNT,XXCODE,XXURNO,XXLDATE,XXCDATE:
                             XXADATE:
                             XXCONS,XXSPEC,XXREGGP,XXDESC,XXPATCLS
          GOTO      OVERCOND IF OVER
          RETURN
.
.*****************************************************************************
.*                            I/O Section                                    *
.*****************************************************************************
.
          INC       STD001IO
          INC       WATTR1IO/INC
          INC       IBASEQIO/INC
          INC       PATMA1IO/INC
          INC       PMSHCPIO/INC
          INC       PATDO1IO/INC
          INC       PATCODIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       WATPROIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCLIO/INC             * Patient System Link to GP Prac.
          INC       WATCATIO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WATPNPIO/INC
          INC       PATDSCIO/INC
          INC       WATSUSIO/INC
          INC       WATWTAIO/INC
          INC       WEBERRIO/INC
          INC       BOKRC1IO/INC
.
          INC       PATCODKY
          INC       PMSHCGDS
          INC       PATDOCKY
          INC       TFILENAM
          INC       WATONSUS
          INC       CHKSTAT 
          INC       CALCDAYS

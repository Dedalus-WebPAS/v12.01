.*******************************************************************************
.* System         :  PMI                                                       *
.* Program        :  IBAHMS41                                                  *
.* Description    :  Upload HCP Details into webPAS                            *
.*******************************************************************************
.* Date           :  23/02/2017                                                *
.* Author         :  Don Nguyen                                                *
.* Function       :  Upload HCP Details                                        *
.*******************************************************************************
.* Modifications  :                                                            *
.*******************************************************************************
.*   V11.04.01  28/03/2024 Thanh T                TSK 0935314                  *
.*              Recompiled for PMSQDRFD changes                                *
.*******************************************************************************
.*   V10.11.02  10/10/2017  Ania P          TSK 0845712                        *
.*                          Changed PMHCNHTA to PMHCMPGN                       *
.*   V10.11.01  09/08/2017  Tracey Nguyen   TSK 0299206                        *
.*                          Recompiled for PATDKIPR/PMSHKIPR                   *
.*   V10.10.01  08/03/2017  Don Nguyen      TSK 0824098                        *
.*                          Modified program to now use 'ibahms41.us1' instead.*
.*                          Modified PCOM0000 to allow empty comments write.   *
.*                          Modified VALI0000 to allow some blank values.      *
.*   V10.10.00  23/02/2017  Don Nguyen      TSK 0824098                        *
.*                          New program                                        *
.*******************************************************************************
.
          INC       STD001FD           * Standard Include
.
          INC       IBACONFD/INC       * Control File
          INC       MLTHCPFD/INC       * Multi Hospital HCP Codes
          INC       PATCODFD/INC       * Patient Codes File
          INC       PATCONFD/INC       * Patient Control File
          INC       PATDO1FD/INC       * Doctor Details File
          INC       PATDKIFD/INC       * Doctors Key Word File
          INC       PATDRGFD/INC       * Patient Period Date Range
          INC       PATDSTFD/INC       * Patient Doctor Stats File
          INC       PATHSPFD/INC       * Hospital Details File
          INC       PATIN1FD/INC       * Insurance Details File
          INC       PMSDUNFD/INC       * Doctor/Unit Combination File
          INC       PMSHCPFD/INC       * HCP Details File
          INC       PMSHKIFD/INC       * HCP Key Word File
          INC       PMSTCOFD/INC       * General HCP Comments
          INC       WEBSECFD/INC       * Web Security File
.
.*******************************************************************************
.*                     HCP Upload file                                         *
.*******************************************************************************
.
UPLDDTXT  FILE       HL7, FIXED=4096
.
.Name     Type      Size      Start Loc     Description
.----     ----      ----      ---------     -----------
UPLDHCPC  DIM         10              1     Final HCP Code
.PIPE     DIM          1             11     Pipe Delimiter
UPLDLDOC  DIM          6             12     HCP Linked Doctor Code
.PIPE     DIM          1             18     Pipe Delimiter
UPLDTITL  DIM         10             19     HCP Title
.PIPE     DIM          1             29     Pipe Delimiter
UPLDSNAM  DIM         35             30     HCP Surname
.PIPE     DIM          1             65     Pipe Delimiter
UPLDGNAM  DIM         35             66     HCP Given Name
.PIPE     DIM          1            101     Pipe Delimiter
UPLDSPC1  DIM          3            102     HCP Primary Speciality
.                                             (Category DT)
.PIPE     DIM          1            105     Pipe Delimiter
UPLDGEND  DIM          1            106     HCP Gender (M,F,I,U)
.PIPE     DIM          1            107     Pipe Delimiter
UPLDDTOB  DIM          8            108     HCP Date of Birth
.PIPE     DIM          1            116     Pipe Delimiter
UPLDADD1  DIM         60            117     Surgery Address Line 1
.PIPE     DIM          1            177     Pipe Delimiter
UPLDADD2  DIM         60            178     Surgery Address Line 2
.PIPE     DIM          1            238     Pipe Delimiter
UPLDADD3  DIM         60            239     Surgery Suburb
.PIPE     DIM          1            299     Pipe Delimiter
UPLDPOST  DIM          8            300     Surgery Post Code
.PIPE     DIM          1            308     Pipe Delimiter
UPLDADD4  DIM         60            309     Surgery State
.PIPE     DIM          1            369     Pipe Delimiter
UPLDSTEL  DIM         20            370     Surgery Telephone
.PIPE     DIM          1            390     Pipe Delimiter
UPLDMOBN  DIM         20            391     HCP Mobile Number
.PIPE     DIM          1            411     Pipe Delimiter
UPLDPRFC  DIM          3            412     Preferred Communication Method
.                                             (Category CZ)
.PIPE     DIM          1            415     Pipe Delimiter
UPLDRCPT  DIM         25            416     HCP Receptionist's Name
.PIPE     DIM          1            441     Pipe Delimiter
UPLDTYPE  DIM          2            442     HCP Type
.PIPE     DIM          1            444     Pipe Delimiter
UPLDHCST  DIM          2            445     HCP Type Code
.PIPE     DIM          1            447     Pipe Delimiter
UPLDPRV1  DIM         10            448     HCP Provider Number
.PIPE     DIM          1            458     Pipe Delimiter
UPLDMPGN  DIM         20            459     HCP National Registration #
.                                             (NEHTA Identifier)
.PIPE     DIM          1            479     Pipe Delimiter
UPLDSDAT  DIM          8            480     HCP Start Date
.PIPE     DIM          1            488     Pipe Delimiter
UPLDDFAC  DIM          8            489     HCP Date First Accredited
.PIPE     DIM          1            497     Pipe Delimiter
UPLDINSC  DIM          6            498     HCP Insurance Code
.PIPE     DIM          1            504     Pipe Delimiter
UPLDINTO  DIM          8            505     HCP Insurance Date To
.PIPE     DIM          1            513     Pipe Delimiter
UPLDEDIA  DIM        200            514     EDI Address
.                                           (HCP Comments - Category TZ, ind3=E)
.PIPE     DIM          1            714     Pipe Delimiter
UPLDPRMS  DIM        200            715     Practice Management Software
.                                           (HCP Comments - Category TZ, ind4=P)
.
.End of record                      915
.
.
.
.*******************************************************************************
.*                             Program Variables                               *
.*******************************************************************************
CMDLINE   DIM       200                          * Command Line
CMNTTNUM  FORM      2                            * Comment Type Indicator num
CMNTTIND  DIM       1                            * Comment Type Indicator value
CMNTTCOD  DIM       3                            * Comment Type Code (Cat TZ)
COMMTLEN  FORM      4                            * Comments length
CPTDATE   DIM       8                            * Date parameter for GPERD
DIM3      DIM       3                            * Category Code validation
ERRMESSG  DIM       100                          * Formatted Error Message
FIELDNME  DIM       20                           * Upload field description
FINYEAR   DIM       4                            * Current Financial Year
FNAMEINP  DIM       150                          * Input File path/name
LINENUMB  FORM      3                            * Comments Line No.
LEAPDAYS  FORM      2                            * Days in February
COMNTHCP  DIM       10                           * Comments HCP Code
COMNTLNE  DIM       200                          * Comments Line
COMNTTYP  DIM       3                            * Comments Type (Cat TZ)
NOUPDFLG  FORM      1                            * No update flag (Doctor File)
PATHNAME  DIM       100                          * Path Name of Input File
PMHCACTN  DIM       1                   * add/update PMSHCPFD flag for DGCLIM02
TEMPDATE  DIM       8                            * Date Validation
TEMPVAR   DIM       20
TODAY     DIM       8                            * Today's Date
TOTDOCTS  FORM      4                            * Number of HCPs Processed
TOTDRADD  FORM      4                            * Number of HCPs Added
TOTDRERR  FORM      4                            * Number of HCPs in Error
TOTDRUPD  FORM      4                            * Number of HCPs Updated
UPDTTIME  DIM       8                            * Current Time
USERID    DIM       10                           * Input User ID
.
.*******************************************************************************
.*                             Program Constants                               *
.*******************************************************************************
CAT_CZ    INIT      "CZ"
CAT_DT    INIT      "DT"
CAT_TZ    INIT      "TZ"
ENDOFRPT  INIT      "  ***  END OF REPORT  ***"
EXCEP001  INIT      "Category "
EXCEP002  INIT      " Code "
EXCEP003  INIT      " is invalid or inactive"
EXCEP004  INIT      "Hospital code "
EXCEP005  INIT      " invalid"
EXCEP006  INIT      "HCP Code "
EXCEP007  INIT      "HCP Type Code "
EXCEP008  INIT      "Doctor Code "
EXCEP009  INIT      "Start Date is invalid - "
EXCEP010  INIT      "End Date is invalid - "
EXCEP011  INIT      "Date First Accredited is invalid - "
EXCEP012  INIT      "Date of Birth is invalid - "
EXCEP013  INIT      "Gender Code "
EXCEP014  INIT      "Insurance Code "
EXCEP015  INIT      "Insurance Date To is invalid - "
EXCEP016  INIT      "HCP Type Code is Attending and no Doctor Code exists - "
EXCEP017  INIT      "Linked Doctor Code not in Doctor File - "
EXCEP018  INIT      "HCP Code, HCP Surname, or Provider Number does not match"
EXCEP019  INIT      " exists for different HCP - "
EXCEP020  INIT      "HCP "
EXCEP021  INIT      " cannot be blank"
EXCEP022  INIT      "HCP Code contains invalid characters"
.
.*******************************************************************************
.*                        Program Version Information                          *
.*******************************************************************************
PRGID     INIT      "IBAHMS41"
.                   ****************** NB Spaces at end of PRGNAM are deliberate
PRGNAM    INIT      "Upload HCP Details                 "
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  MAIN0000                                  *
.*                               Main Procedure                               *
.******************************************************************************
MAIN0000  CALL      INIT0000                     * Initialise variables & files
          BRANCH    EXIT,MAIN9900
.
          CALL      OPTN0000                     * Select Options
          BRANCH    EXIT,MAIN9999                * EXIT = 1 if 0 chosen in menu

          CALL      GFIL0000                     * Keyin Input Path & File name
          BRANCH    EXIT,MAIN9900
.
          CALL      GUSR0000                     * Get Web User Id
          BRANCH    EXIT,MAIN9900
.
          CALL      GPTH0000                     * Keyin Path Names
          BRANCH    EXIT,MAIN9900
.
MAIN1000  CALL      PROC0000                     * Process upload file
.
          CALL      PRTOT000                     * Print Upload Record totals
          PRINT     *N,*N,ENDOFRPT               * Print End of Report
.
          IF        COPTION=2
            CLEAR     CMDLINE                    * Run us3 sending spool file
            APPEND    "ibahms40.us3 ",CMDLINE    * name
            APPEND    SPLFILE,CMDLINE
            APPEND    SP70,CMDLINE
            RESET     CMDLINE
            EXECUTE   CMDLINE,TASKID
          ENDIF
.
          GOTO      MAIN9999
.
MAIN9900  CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
          IF        COPTION=2
            CLEAR     CMDLINE
            APPEND    "ibahms40.us2 ",CMDLINE    * Run us2 sending load
            APPEND    FNAMEINP,CMDLINE           * file name
            APPEND    SP70,CMDLINE
            RESET     CMDLINE
            EXECUTE   CMDLINE,TASKID
          ENDIF
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                           OPTN0000                  Called by: ML0000      *
.*                  Get user options or exit                                  *
.*                                                                            *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                             *
.*                        TRUE  (1)  Exit option selected                     *
.******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Load HCP Details":
                    *P1:6,*V2LON,TWO,*HOFF,". Auto Load HCP Details"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                           INIT0000                                         *
.*                  Initialise Variables & Open Files                         *
.******************************************************************************
INIT0000  CALL      DISPHEAD                     * Display screen heading
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
          CLOCK     TIME,UPDTTIME
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"          * Inpatient CONTROL File
.
          DISPLAY   *P64:24,"mlthcpaf";
          OPEN      MLTHCPA1,"mlthcpaf"          * Multi Hospital HCP Codes
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"          * Patient Codes File
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE2,"patcodes"          * Patient Codes File
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"          * Doctor Details File
.
          DISPLAY   *P64:24,"patdkiaf";
          OPEN      PATDKIA1,"patdkiaf"          * Doctors Key Word File
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"          * Period Date Range File
.
          DISPLAY   *P64:24,"patdstat";
          OPEN      PATDSTA1,"patdstat"          * Patient Doctor Statistics File
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"          * Hospital Details File
.
          DISPLAY   *P64:24,"pmsdunaf";
          OPEN      PMSDUNA1,"pmsdunaf"          * Doctor/Unit Combination File
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"          * HCP Details File
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA4,"pmshcpaf"          * HCP Details File
.
          DISPLAY   *P64:24,"pmshkiaf";
          OPEN      PMSHKIA1,"pmshkiaf"          * HCP Key Word File
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"          * Web Security File
.
          DISPLAY   *P64:24,"pmstcoaf";
          OPEN      PMSTCOA1,"pmstcoaf"          * General HCP Comments
.
          DISPLAY   *P64:24,"patin1af";
          OPEN      PATIN1A1,"patin1af"          * Insurance Details
.
          READ      CONTROLF,HUND16;*250,PTCNUPEA
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
.
.         Get the current financial year
.
          MOVE      TODAY,CPTDATE
          CALL      GPERD
          IF        EXIT = 1
            DISPLAY   *P1:24,*EL,*B,"Current Date Not Found In Period File.  ";
            CALL      HITENTER
            GOTO      INIT9999
          ENDIF
.
          MOVE      DRGYR,FINYEAR
.
          MOVE      ZERO,TOTDOCTS                * Number of HCPs Processed
          MOVE      ZERO,TOTDRADD                * Number of HCPs added
          MOVE      ZERO,TOTDRERR                * Number of HCPs in error
          MOVE      ZERO,TOTDRUPD                * Number of HCPs updated
          MOVE      SEVENTY,CLNO                 * Init Line Count
          MOVE      ZERO,CPAGENO                 * Init Page Count
.
INIT9999  RETURN
+
.******************************************************************************
.*                           GFIL0000                    Called by : MAIN0000 *
.*                  Keyin Upload File path for conversion (re-format)         *
.******************************************************************************
GFIL0000  MOVE      ZERO,EXIT
          PACK      FNAMEINP WITH SP70,SP70,SP70
.
          DISPLAY   *P26:11,*EF,"Upload HCP File";
.
          KEYIN     *P1:13,*EL,"Input Data File : ",*V2LON,FNAMEINP
.
          PACK      FNAMEINP,FNAMEINP,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEINP
          GOTO      GFIL9000 IF EQUAL
.
          SCAN      ".txt",FNAMEINP
          IF        !@EQUAL
            SQUEEZE   FNAMEINP
            ENDSET    FNAMEINP
            APPEND    ".txt",FNAMEINP              * add ".txt" if necessary
            APPEND    SP70,FNAMEINP
            RESET     FNAMEINP
          ENDIF
          RESET     FNAMEINP
.
          DISPLAY   *P19:13,*V2LON,*EL,FNAMEINP;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
.
          OPEN      UPLDDTXT,FNAMEINP            * Check if input file exists
.
          TRAPCLR   IO
          BRANCH    OVRCD,GFIL8000
.
          CALL      GFMT0000                     * Reformat input file
.
          OPEN      UPLDDTXT,FNAMEINP            * Open reformatted input file
.
          TRAPCLR   IO
          BRANCH    OVRCD,GFIL8000
.
          MOVE      ZERO,EXIT
          GOTO      GFIL9999
.
GFIL8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          IF        COPTION=2
            GOTO      GFIL9000
          ENDIF

          GOTO      GFIL0000
.
GFIL9000  MOVE      ONE,EXIT
.
GFIL9999  RETURN
+
.******************************************************************************
.*                           GFMT0000                     Called by: GFIL0000 *
.*                  Reformat tab delimited input file to HL7 pipe format      *
.******************************************************************************
GFMT0000  CLEAR     CMDLINE
          APPEND    "ibahms41.us1 ",CMDLINE
          APPEND    FNAMEINP,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
GFMT9999  RETURN
+
.******************************************************************************
.*                           GUSR0000                     Called by: MAIN0000 *
.*                  Get Web User ID                                           *
.******************************************************************************
GUSR0000  MOVE      SP70,USERID
          KEYIN     *P1:15,*EF,"Web User Id: ",*V2LON,USERID
.
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      GUSR9100 IF EQUAL
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,GUSR9100
.
          MOVE      ZERO,EXIT
          GOTO      GUSR9999
.
GUSR9100  MOVE      ONE,EXIT
.
GUSR9999  RETURN
+
.******************************************************************************
.*                           GPTH0000                    Called by : MAIN0000 *
.*                  Keyin Upload File path for output in report               *
.******************************************************************************
GPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P1:17,*EL,"Input Data File : ":
                    *P19:17,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
          PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      GPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      GPTH9999
.
GPTH9500  MOVE      ONE,EXIT
.
GPTH9999  RETURN
+
.******************************************************************************
.*                           LCUC0000                    Called by : PROC0000 *
.*                  Ensure all relevant file fields are in upper-case         *
.******************************************************************************
LCUC0000  REP       UPPLOW,UPLDHCPC         * HCP Code
          REP       UPPLOW,UPLDLDOC         * HCP Linked Doctor Code
.
LCUC9999  RETURN
+
.******************************************************************************
.*                           VALI0000                    Called by : PROC0000 *
.*                  Validate upload file values                               *
.******************************************************************************
VALI0000  MOVE      ZERO,EXIT
.
.         Validate HCP Code
          MOVE      ZERO,F2
          MOVELPTR  UPLDHCPC,F2
          COMPARE   ZERO,F2
          GOTO      VALI9100 IF EQUAL       * HCP Code can't be blank!
.
          SCAN      "'",UPLDHCPC
          GOTO      VALI9111 IF EQUAL
.
          SCAN      ",",UPLDHCPC
          GOTO      VALI9111 IF EQUAL
.
          SCAN      ".",UPLDHCPC
          GOTO      VALI9111 IF EQUAL
.
.
.         Validate Title
          MOVE      "Title",FIELDNME
          MOVE      ZERO,F2
          MOVELPTR  UPLDTITL,F2
          COMPARE   ZERO,F2
          GOTO      VALI9110 IF EQUAL       * Error if blank
.
.
.         Validate Surname
          MOVE      "Surname",FIELDNME
          MOVE      ZERO,F2
          MOVELPTR  UPLDSNAM,F2
          COMPARE   ZERO,F2
          GOTO      VALI9110 IF EQUAL       * Error if blank.
.
.         Validate Given Name
          MOVE      "Given Name",FIELDNME
          MOVE      ZERO,F2
          MOVELPTR  UPLDGNAM,F2
          COMPARE   ZERO,F2
          GOTO      VALI9110 IF EQUAL       * Error if blank
.
.
.         Validate Primary Speciality (Cat DT) Code
          MOVE      ZERO,F2
          MOVELPTR  UPLDSPC1,F2
          COMPARE   ZERO,F2
          GOTO      VALI1000 IF EQUAL       * Skip validation if blank
.
          PACK      DIM3,SP70
.
          MOVE      CAT_DT,DIM2
          MOVE      UPLDSPC1,DIM3
.
          PACK      KEY5,DIM2,DIM3,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VALI9101          * Invalid code
.
          MATCH     "I",PTCOACTV            * Inactive?
          GOTO      VALI9101 IF EQUAL
.
.
.         Validate Gender Code (M,F,I,U)
VALI1000  MOVE      ZERO,F2
          MOVELPTR  UPLDGEND,F2
          COMPARE   ZERO,F2
          GOTO      VALI1001 IF EQUAL       * Skip validation if blank
.
          MOVE      UPLDGEND,DIM1
          MOVE      "MFIU",TEMPVAR
          REP       " _",TEMPVAR
          SCAN      UPLDGEND,TEMPVAR
          GOTO      VALI9102 IF NOT EQUAL
.
.
.         Validate DOB
VALI1001  MOVE      ZERO,F2
          MOVELPTR  UPLDDTOB,F2
          COMPARE   ZERO,F2
          GOTO      VALI1002 IF EQUAL       * Skip validation if blank
.
          MOVE      UPLDDTOB,TEMPDATE
          CALL      VDTE0000
          BRANCH    EXIT,VALI9103
.
.
.         Validate Preferred Method of Contact (Cat CZ) Code
VALI1002  MOVE      ZERO,F2
          MOVELPTR  UPLDPRFC,F2
          COMPARE   ZERO,F2
          GOTO      VALI1003 IF EQUAL       * Skip validation if blank
.
          PACK      DIM3,SP70
.
          MOVE      CAT_CZ,DIM2
          MOVE      UPLDPRFC,DIM3
.
          PACK      KEY5,DIM2,DIM3,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VALI9101          * Invalid code
.
          MATCH     "I",PTCOACTV            * Inactive?
          GOTO      VALI9101 IF EQUAL
.
.
.         Validate HCP Start Date
VALI1003  MOVE      "Start Date",FIELDNME
          MOVE      ZERO,F2
          MOVELPTR  UPLDSDAT,F2
          COMPARE   ZERO,F2
          GOTO      VALI9110 IF EQUAL       * HCP Start Date can't be blank!
.
          MOVE      UPLDSDAT,TEMPDATE
          CALL      VDTE0000
          BRANCH    EXIT,VALI9104
.
.
.         Validate Date First Accredited
VALI1004  MOVE      ZERO,F2
          MOVELPTR  UPLDDFAC,F2
          COMPARE   ZERO,F2
          GOTO      VALI1005 IF EQUAL       * Skip validation if blank
.
          MOVE      UPLDDFAC,TEMPDATE
          CALL      VDTE0000
          BRANCH    EXIT,VALI9106
.
.
.         Validate Insurance Code
VALI1005  MOVE      ZERO,F2
          MOVELPTR  UPLDINSC,F2
          COMPARE   ZERO,F2
          GOTO      VALI1006 IF EQUAL       * Skip validation if blank
.
          MOVE      UPLDINSC,D6
          PACK      KEY6,UPLDINSC,SP70
          CALL      RDINSR1
          BRANCH    OVRCD,VALI9107
.
          MATCH     "1",PTINACTV            * Inactive?
          GOTO      VALI9107 IF EQUAL
.
.
.         Validate Insurance Date To
VALI1006  MOVE      ZERO,F2
          MOVELPTR  UPLDINTO,F2
          COMPARE   ZERO,F2
          GOTO      VALI1007 IF EQUAL       * Skip validation if blank
.
          MOVE      UPLDINTO,TEMPDATE
          CALL      VDTE0000
          BRANCH    EXIT,VALI9108
.
.
.         Validate HCP Type Code
VALI1007  PACK      DIM2,UPLDHCST,SP70
          STRIP     DIM2
          MOVE      DIM2,D2
.
          MATCH     SP70,D2
          GOTO      VALI9109 IF EQUAL
.
          RJUSTIFY  D2
          REP       " 0",D2
.
          TYPE      D2
          GOTO      VALI9109 IF NOT EQUAL
.
          MOVE      ZERO,FORM2
          MOVE      D2,FORM2
.
.         Valid HCP Type Code 0 thru 13
          COMPARE   ZERO,FORM2
          GOTO      VALI9109 IF LESS
.
          COMPARE   FORM2,TEN3
          GOTO      VALI9109 IF LESS         
.
          GOTO      VALI9800
.
VALI9100  MOVE      "HCP Code cannot be blank",ERRMESSG
          GOTO      VALI9199
.
VALI9101  PACK      ERRMESSG,EXCEP001,DIM2,EXCEP002,DIM3,EXCEP003,SP70
          GOTO      VALI9199
.
VALI9102  PACK      ERRMESSG,EXCEP013,DIM1,EXCEP005,SP70   * Gender Code
          GOTO      VALI9199
.
VALI9103  PACK      ERRMESSG,EXCEP012,TEMPDATE,SP70        * DOB
          GOTO      VALI9199
.
VALI9104  PACK      ERRMESSG,EXCEP009,TEMPDATE,SP70        * Start Date
          GOTO      VALI9199
.
VALI9105  PACK      ERRMESSG,EXCEP010,TEMPDATE,SP70        * End Date
          GOTO      VALI9199
.
VALI9106  PACK      ERRMESSG,EXCEP011,TEMPDATE,SP70      * Date First Accredited
          GOTO      VALI9199
.
VALI9107  PACK      ERRMESSG,EXCEP014,D6,EXCEP003,SP70     * Insurance Code
          GOTO      VALI9199
.
VALI9108  PACK      ERRMESSG,EXCEP015,TEMPDATE,SP70        * Insurance Date To
          GOTO      VALI9199
.
VALI9109  PACK      ERRMESSG,EXCEP007,DIM2,EXCEP005,SP70   * HCP Type Code
          GOTO      VALI9199
.
VALI9110  PACK      ERRMESSG,EXCEP020,FIELDNME,EXCEP021,SP70    * Blank value
          GOTO      VALI9199
.
VALI9111  PACK      ERRMESSG,EXCEP022,SP70    * HCP Code contains invalid chars
          GOTO      VALI9199
.
VALI9199  CALL      PRERRL00      * Print Error Line
          MOVE      ONE,EXIT
          GOTO      VALI9999
.
VALI9800  MOVE      ZERO,EXIT
.
VALI9999  RETURN
+
.******************************************************************************
.*                           MVDOC000                    Called by : PROC0000 *
.*                  Populate Doctor file fields with upload values            *
.******************************************************************************
MVDOC000  PACK      DCODE,UPLDHCPC,SP70          * Doctor Code
          PACK      DTITL,UPLDTITL,SP70          * Title
          PACK      DSNAME,UPLDSNAM,SP70         * Surname
          PACK      DGNAME,UPLDGNAM,SP70         * Given Name
          PACK      DRTYPE,UPLDSPC1,SP70         * Primary Speciality
          PACK      DBIRTH,UPLDDTOB,SP70         * Date of Birth
          PACK      DSADD1,UPLDADD1,SP70         * Surgery Address 1
          PACK      DSADD2,UPLDADD2,SP70         * Surgery Address 2
          PACK      DSADD3,UPLDADD3,SP70         * Surgery Suburb
          PACK      DSPOST,UPLDPOST,SP70         * Surgery Post Code
          PACK      DSADD4,UPLDADD4,SP70         * Surgery State
          PACK      DTELES,UPLDSTEL,SP70         * Surgery Telephone
          PACK      PTDOMOBL,UPLDMOBN,SP70       * Mobile Number
          PACK      PTDOCOMT,UPLDPRFC,SP70       * Pref. Method Contact (Cat CZ)
          PACK      DRNAME,UPLDRCPT,SP70         * Receptionist's Name
          PACK      DPROV,UPLDPRV1,SP70          * Provider Number
          PACK      PTDOSDAT,UPLDSDAT,SP70       * Start Date
          PACK      DAFIRST,UPLDDFAC,SP70        * Date First Accredited
          PACK      PTDOINSC,UPLDINSC,SP70       * Insurance Code
          PACK      PTDOINST,UPLDINTO,SP70       * Insurance Date To
.
MVDR9999  RETURN
+
.******************************************************************************
.*                           MVHC0000                    Called by : PROC0000 *
.*                  Populate HCP file fields with upload values               *
.******************************************************************************
MVHC0000  PACK      PMHCHCPC,UPLDHCPC,SP70       * HCP Code
          PACK      PMHCLDOC,UPLDLDOC,SP70       * Linked Doctor Code (patdo1af)
          PACK      PMHCTITL,UPLDTITL,SP70       * Title
          PACK      PMHCSNAM,UPLDSNAM,SP70       * Surname
          PACK      PMHCGNAM,UPLDGNAM,SP70       * Given Name
          PACK      PMHCSPC1,UPLDSPC1,SP70       * Primary Speciality
          PACK      PMHCGNDR,UPLDGEND,SP70       * Gender (M,F,I,U)
          PACK      PMHCDTOB,UPLDDTOB,SP70       * Date of Birth
          PACK      PMHCADR1,UPLDADD1,SP70       * Surgery Address 1
          PACK      PMHCADR2,UPLDADD2,SP70       * Surgery Address 2
          PACK      PMHCADR3,UPLDADD3,SP70       * Surgery Suburb
          PACK      PMHCPOST,UPLDPOST,SP70       * Surgery Post Code
          PACK      PMHCADR4,UPLDADD4,SP70       * Surgery State
          PACK      PMHCSTEL,UPLDSTEL,SP70       * Surgery Telephone
          PACK      PMHCMOBN,UPLDMOBN,SP70       * Mobile Number
          PACK      PMHCPRFC,UPLDPRFC,SP70       * Pref. Method Contact (Cat CZ)
          PACK      PMHCRCPT,UPLDRCPT,SP70       * Receptionist's Name
.
.         PACK      PMHCHCPT,UPLDTYPE,SP70       * Type
.
          RJUSTIFY  UPLDHCST
          REP       " 0",UPLDHCST
          MOVE      ZERO,FORM2
          MOVE      UPLDHCST,FORM2
          MOVE      FORM2,PMHCHCST               * Type Code
.
          PACK      PMHCPRV1,UPLDPRV1,SP70       * Provider Number
          PACK      PMHCMPGN,UPLDMPGN,SP70       * National Registration #
          PACK      PMHCNHTA,SP70                * Clear data from previously used Reg# 
          PACK      PMHCSDAT,UPLDSDAT,SP70       * Start Date
          PACK      PMHCDFAC,UPLDDFAC,SP70       * Date First Accredited
          PACK      PMHCINSC,UPLDINSC,SP70       * Insurance Code
          PACK      PMHCIDTT,UPLDINTO,SP70       * Insurance Date To
.
MVHC9999  RETURN
+
.******************************************************************************
.*                           ADDDOC00                    Called by : ADDREC00 *
.*        Add new Doctor File record for Linked Doctor (UPLDLDOC)             *
.******************************************************************************
ADDDOC00  CALL      CLPATDOC           * Clear all Doctor file fields
          CALL      MVDOC000           * Populate file fields with upload values
.
          MOVE      TODAY,DLUPDDTE     * Last update date
          MOVE      UPDTTIME,DLUPDTME  * Last update time
          MOVE      ZERO,DRSTAT        * Default status to Active
.
          PACK      KEY6,UPLDLDOC,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRDOCT1
          TRAPCLR   IO
          BRANCH    OVRCD,ADDDOC91
.
          CALL      DSCR0000           * Create blank patdstat records
          PROC      PATDKIUP           * Update Doctor keyword index
.
ADDDOC90  MOVE      ZERO,EXIT
          GOTO      ADDDOC99
.
ADDDOC91  MOVE      ONE,EXIT
          GOTO      ADDDOC99
.
ADDDOC99  RETURN
+
.******************************************************************************
.*                           ADDHCP00                    Called by : ADDREC00 *
.*        Add new HCP File record for the upload HCP Code (UPLDHCPC)          *
.******************************************************************************
ADDHCP00  CALL      CLPMSHCP           * Clear all HCP file fields
          CALL      MVHC0000           * Populate file fields with upload values
.
          MOVE      TODAY,PMHCCDTE
          MOVE      UPDTTIME,PMHCCTIM
          MOVE      USERID,PMHCWEBC
          MOVE      ZERO,PMHCSTTS      * Default status to Active
.
          PACK      KEY10,UPLDHCPC,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMHCP1
          TRAPCLR   IO
          BRANCH    OVRCD,ADDHCP91
.
          PROC      PMSHKIUP           * Update HCP keyword index
.
          MOVE      "A",PMHCACTN       * action flag for DGCLIM02
          MOVE      ONE,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLIM02           * send HL7 message
.
ADDHCP90  MOVE      ZERO,EXIT
          GOTO      ADDHCP99
.
ADDHCP91  MOVE      ONE,EXIT
          GOTO      ADDHCP99
.
ADDHCP99  RETURN
+
.******************************************************************************
.*                           ADDREC00                    Called by : PROC0000 *
.*                  Add new HCP/Doctor record                                 *
.******************************************************************************
ADDREC00  MATCH     SP70,UPLDLDOC
          GOTO      ADDREC10 IF EQUAL
.
          CALL      ADDDOC00           * Add new Doctor File record
.
ADDREC10  CALL      ADDHCP00           * Add new HCP File record
          BRANCH    EXIT,ADDREC91
.
          CALL      PRCMNT00           * Process HCP Comments
.
          ADD       ONE,TOTDRADD       * Increment total for HCP added
          MOVE      ZERO,EXIT
          GOTO      ADDREC99
.
ADDREC91  MOVE      ONE,EXIT
          GOTO      ADDREC99
.
ADDREC99  RETURN
+
.******************************************************************************
.*                           UPDR0000                    Called by : PROC0000 *
.*                  Update existing HCP/Doctor details                        *
.******************************************************************************
UPDR0000  MOVE      ZERO,EXIT
          MOVE      ZERO,NOUPDFLG           * Reset update flag (Doctor File)
.
          PACK      KEY10,UPLDHCPC,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,UPDR9101
.
          MATCH     UPLDSNAM,PMHCSNAM
          GOTO      UPDR9102 IF NOT EQUAL
.
          MATCH     UPLDPRV1,PMHCPRV1
          GOTO      UPDR9102 IF NOT EQUAL
.
.
.         Make sure we're not changing an existing 'Attending' HCP to something
.         else other than 'Attending' (PMHCHCST = 0, 2, 4, 6, 8, 10, 11, 13)
.
          MOVE      PMHCHCST,F2
          BRANCH    F2,UPDR2000,UPDR1000,UPDR2000,UPDR1000,UPDR2000:
                       UPDR1000,UPDR2000,UPDR1000,UPDR2000,UPDR1000:
                       UPDR1000,UPDR2000,UPDR1000
.
.         Existing HCP is 'Attending'; Are we trying to change it to something
.         other than 'Attending'? If so, bail with error.
UPDR1000  MOVE      UPLDHCST,F2
          IF        F2=1 | F2=3 | F2=5 | F2=7 | F2=9 | F2=12
            GOTO      UPDR9103              * Error
          ENDIF
.
.
.         Existing HCP is NOT 'Attending'; Is New HCP is NOT 'Attending' also?
UPDR2000  MOVE      UPLDHCST,F2
          IF        F2=1 | F2=3 | F2=5 | F2=7 | F2=9 | F2=12
            GOTO      UPDR3000              * Skip Linked Doctor validation
          ENDIF
.
.         Has a Linked Doctor been specified?
          MOVE      ZERO,F2
          MOVELPTR  UPLDLDOC,F2
          COMPARE   ZERO,F2
          GOTO      UPDR9104 IF EQUAL       * No Linked Doctor Code
.
          MATCH     SP70,UPLDLDOC           * Blank Linked Doctor Code
          GOTO      UPDR9104 IF EQUAL       * Yes; Error
.
.
.         Check whether Linked Doctor Code already exists for another HCP
          PACK      KEY16,UPLDLDOC,Z70
          CALL      RSPMHCP4
UPDR2100  CALL      RPPMHCP4
          BRANCH    OVRCD,UPDR3000
.
          MATCH     UPLDLDOC,PMHCLDOC       * Same Linked Doctor Code?
          GOTO      UPDR3000 IF NOT EQUAL   * No; skip Doctor File check
.
          MATCH     UPLDHCPC,PMHCHCPC       * Same HCP Code?
          GOTO      UPDR2200 IF NOT EQUAL   * Different HCP so check Doctor File
.
          MATCH     UPLDSNAM,PMHCSNAM       * Same Surname?
          GOTO      UPDR9102 IF NOT EQUAL   * No; Error
.
          MATCH     UPLDPRV1,PMHCPRV1       * Same Provider Number?
          GOTO      UPDR9102 IF NOT EQUAL   * No; Error
.
          GOTO      UPDR2100                * Get next HCP
.
.
.         Check Doctor file for existing Linked Doctor
UPDR2200  PACK      KEY6,UPLDLDOC,SP70
          CALL      RDADOCT1
          IF        OVRCD=0
            GOTO      UPDR9106      * Linked Doc exists for different HCP; Error
          ELSE
            CALL      ADDDOC00              * Add new Doctor File record
            IF        EXIT=0
              MOVE      ONE,NOUPDFLG        * Skip update of Doctor File record
            ENDIF
          ENDIF
.
.
.         UPDATE HCP and Doctor File record
.         ======
UPDR3000  PACK      KEY10,UPLDHCPC,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,UPDR9101
.
          CALL      MVHC0000                * Populate HCP file fields 
          MOVE      TODAY,PMHCLUPD
          MOVE      UPDTTIME,PMHCLUPT
          MOVE      USERID,PMHCWEBU
.
          CALL      UPPMHCP1
          PROC      PMSHKIUP                * Update HCP keyword index
.
          MOVE      "U",PMHCACTN            * action flag for DGCLIM02
          MOVE      TWO,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLIM02                * send HL7 message
.
          BRANCH    NOUPDFLG,UPDR9000       * Skip update of Doctor File?
.
          MOVE      ZERO,F2
          MOVELPTR  UPLDLDOC,F2
          COMPARE   ZERO,F2
          GOTO      UPDR9000 IF EQUAL
.
          MATCH     SP70,UPLDLDOC           * Blank Linked Doctor Code
          GOTO      UPDR9000 IF EQUAL       * Yes; skip
.
          PACK      KEY6,UPLDLDOC,SP70
          CALL      RDADOCT1
          IF        OVRCD=0
            CALL      RDDOCT1
            BRANCH    OVRCD,UPDR9000
.
            CALL      MVDOC000              * Populate Doctor file fields
            MOVE      TODAY,DLUPDDTE
            MOVE      UPDTTIME,DLUPDTME
.
            CALL      UPDOCT1
            PROC      PATDKIUP              * Update Doctor keyword index
          ELSE
            CALL      ADDDOC00              * Add new Doctor File record
          ENDIF
.
UPDR9000  CALL      PRCMNT00                * Process HCP Comments
          ADD       ONE,TOTDRUPD            * Increment total for HCP updated
          GOTO      UPDR9999
.
UPDR9101  PACK      ERRMESSG,EXCEP006,UPLDHCPC,EXCEP005,SP70
          GOTO      UPDR9800
.
UPDR9102  PACK      ERRMESSG,EXCEP018,SP70    * HCP Code, Name, Prov # mismatch
          GOTO      UPDR9800
.
UPDR9103  PACK      ERRMESSG,EXCEP007,UPLDHCST,EXCEP005,SP70   * HCP Type Code
          GOTO      UPDR9800
.
UPDR9104  PACK      ERRMESSG,EXCEP016,UPLDGNAM,SP1,UPLDSNAM,SP70
          GOTO      UPDR9800
.
.         Linked Doctor not in Doc File
UPDR9105  PACK      ERRMESSG,EXCEP017,UPLDLDOC,SP70
          GOTO      UPDR9800
.
.         Linked Doctor exists for a different HCP
UPDR9106  PACK      ERRMESSG,EXCEP008,UPLDLDOC,EXCEP019,PMHCHCPC,SP1,PMHCSNAM,SP70
          GOTO      UPDR9800
.
UPDR9800  CALL      PRERRL00      * Print Error Line
          MOVE      ONE,EXIT
          GOTO      UPDR9999
.
UPDR9999  RETURN
+
.******************************************************************************
.*                           VALDLD00                    Called by : PROC0000 *
.*        Validate Linked Doctor code exists in for another HCP               *
.*                                                                            *
.*        Returns:                                                            *
.*          EXIT    1 = Exists for another HCP                                *
.*                  0 = Unique                                                *
.******************************************************************************
VALDLD00  MATCH     SP70,UPLDLDOC
          GOTO      VALDLD90 IF EQUAL
.
          PACK      KEY16,UPLDLDOC,Z70
          CALL      RSPMHCP4
VALDLD10  CALL      RPPMHCP4
          BRANCH    OVRCD,VALDLD90
.
          MATCH     UPLDLDOC,PMHCLDOC       * Same Linked Doctor Code?
          GOTO      VALDLD90 IF NOT EQUAL   * No; skip Doctor File check
.
          MATCH     UPLDHCPC,PMHCHCPC       * Different HCP Code?
          GOTO      VALDLD91 IF NOT EQUAL   * Yes; so we check Doctor File
.
          GOTO      VALDLD10                * Get previous HCP
.
VALDLD90  MOVE      ZERO,EXIT
          GOTO      VALDLD99
.
VALDLD91  PACK      ERRMESSG,EXCEP008,UPLDLDOC,EXCEP019,PMHCHCPC,SP1,PMHCSNAM,SP70
          CALL      PRERRL00                * Print Error Line
          MOVE      ONE,EXIT
          GOTO      VALDLD99
.
VALDLD99  RETURN
+
.******************************************************************************
.*                           PROC0000                    Called by : MAIN0000 *
.*                  Process upload records                                    *
.******************************************************************************
PROC0000  READ      UPLDDTXT,SEQ;UPLDHCPC,UPLDLDOC,UPLDTITL,UPLDSNAM,UPLDGNAM:
                                 UPLDSPC1,UPLDGEND,UPLDDTOB,UPLDADD1,UPLDADD2:
                                 UPLDADD3,UPLDPOST,UPLDADD4,UPLDSTEL,UPLDMOBN:
                                 UPLDPRFC,UPLDRCPT,UPLDTYPE,UPLDHCST,UPLDPRV1:
                                 UPLDMPGN,UPLDSDAT,UPLDDFAC,UPLDINSC,UPLDINTO:
                                 UPLDEDIA,UPLDPRMS
.
          GOTO      PROC9999 IF OVER
.
.         Make sure we haven't read in a blank line
.
          MOVE      ZERO,F2
          MOVELPTR  UPLDHCPC,F2
          COMPARE   ZERO,F2
          GOTO      PROC0000 IF EQUAL       * Read next record
.
          CALL      LCUC0000                * Ensure all fields are upper-case
.
          CALL      VALI0000                * Validate upload file values
          BRANCH    EXIT,PROC9998
.
.
.         Validate Final HCP Code (UPLDHCPC) exists in HCP File
PROC1000  PACK      KEY10,UPLDHCPC,SP70
          CALL      RAPMHCP1
          BRANCH    OVRCD,PROC1100          * Not in HCP File;
.
          GOTO      PROC3000                * Update HCP/Doctor Details
.
.
.         Validate Linked Doctor code if HCP is type 'Attending'
PROC1100  MOVE      ZERO,FORM2
          MOVELPTR  UPLDLDOC,FORM2          * Store length of Linked Doctor code
.
          MOVE      UPLDHCST,F2
          BRANCH    F2,PROC1300,PROC1200,PROC1300,PROC1200,PROC1300:
                       PROC1200,PROC1300,PROC1200,PROC1300,PROC1200:
                       PROC1200,PROC1300,PROC1200
.
.         HCP Type 'Attending'
PROC1200  COMPARE   ZERO,FORM2
          GOTO      PROC9100 IF EQUAL       * No Linked Doctor Code
.
          MATCH     SP70,UPLDLDOC           * Blank Linked Doctor Code
          GOTO      PROC9100 IF EQUAL       * No; Error
.
.         HCP Type NOT 'Attending'
PROC1300  COMPARE   ZERO,FORM2
          GOTO      PROC2000 IF EQUAL       * No Linked Doctor Code
.
          MATCH     SP70,UPLDLDOC           * Is there a Linked Doctor?
          GOTO      PROC2000 IF EQUAL       * No; skip Doctor File validation
.
          PACK      KEY6,UPLDLDOC,SP70
          CALL      RDADOCT1
          BRANCH    OVRCD,PROC2000          * Linked Dr not in Doctor File
.
          CALL      VALDLD00                * Linked Doc exists for another HCP?
          BRANCH    EXIT,PROC9998           * Yes; bail
.
          GOTO      PROC2000
.
.
.         ADD
.         ==================
PROC2000  CALL      ADDREC00                * Add New HCP/Doctor Details
          GOTO      PROC9998
.
.
.         UPDATE
.         ==================
PROC3000  CALL      UPDR0000                * Update HCP/Doctor Details
          GOTO      PROC9998
.
PROC9100  PACK      ERRMESSG,EXCEP016,UPLDGNAM,SP1,UPLDSNAM,SP70
          CALL      PRERRL00                * Print Error Line
          GOTO      PROC9998
.
PROC9998  ADD       ONE,TOTDOCTS
          GOTO      PROC0000                * Read next record
.
PROC9999  RETURN
+
.******************************************************************************
.*                           PRCMNT00                    Called by : UPDR0000 *
.*                  Process HCP comments (Category TZ)                        *
.******************************************************************************
PRCMNT00  MOVE      ZERO,EXIT
.
.         EDI Address (Indicator 3=E)
.
          MOVE      THREE,CMNTTNUM
          MOVE      ANSE,CMNTTIND
          CALL      GCMTYP00                * Get Comment Type (category TZ)
          IF        EXIT=0
            PACK      COMNTLNE,SP70,SP70,SP70
            MOVE      UPLDHCPC,COMNTHCP
.
            PACK      COMNTHCP,COMNTHCP,SP70     * HCP Code
            MOVE      CMNTTCOD,COMNTTYP          * HCP Comment Type
            MOVE      UPLDEDIA,COMNTLNE          * Comment Line
            CALL      PCOM0000                   * Process HCP Comment
          ENDIF
.
.
.         Practice Management Software (Indicator 4=P)
.
          MOVE      FOUR,CMNTTNUM
          MOVE      ANSP,CMNTTIND
          CALL      GCMTYP00                * Get Comment Type (category TZ)
          IF        EXIT=0
            PACK      COMNTLNE,SP70,SP70,SP70
            MOVE      UPLDHCPC,COMNTHCP
.
            PACK      COMNTHCP,COMNTHCP,SP70     * HCP Code
            MOVE      CMNTTCOD,COMNTTYP          * HCP Comment Type
            MOVE      UPLDPRMS,COMNTLNE          * Comment Line
            CALL      PCOM0000                   * Process HCP Comment
          ENDIF
.
PRCMNT99  RETURN
+
.******************************************************************************
.*                           GCMTYP00           Called by: ADDREC00, UPDR0000 *
.*        Returns the first active HCP Comment Type code (Cat TZ) with a      *
.*        matching indicator value.                                           *
.*                                                                            *
.*        Requires:                                                           *
.*          CMNTTNUM - Indicator number                                       *
.*          CMNTTIND - Indicator value                                        *
.*                                                                            *
.*        Returns:                                                            *
.*          EXIT     - 0=Found  1=No match found                              *
.*          CMNTTCOD - Category TZ Code                                       *
.******************************************************************************
GCMTYP00  MOVE      SP10,CMNTTCOD
          MOVE      ZERO,FORM2
          MOVE      CMNTTNUM,F2
.
          COMPARE   ZERO,F2
          GOTO      GCMTYP91 IF EQUAL
.
          PACK      KEY5,CAT_TZ,SP70
          CALL      RDSCODE1
GCMTYP10  CALL      RDKCODE1
          BRANCH    OVRCD,GCMTYP91
.
          MATCH     CAT_TZ,TCODE
          GOTO      GCMTYP91 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV            * Inactive?
          GOTO      GCMTYP10 IF EQUAL
.
.
          LOAD      D1,F2,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          MATCH     D1,CMNTTIND
          GOTO      GCMTYP90 IF EQUAL
.
          GOTO      GCMTYP10
.
GCMTYP90  MOVE      ACODE,CMNTTCOD
          MOVE      ZERO,EXIT
          GOTO      GCMTYP99
.
GCMTYP91  MOVE      ONE,EXIT
          GOTO      GCMTYP99
.
GCMTYP99  RETURN
+
.*****************************************************************************
.*                  PCOM0000                   Called by: ADDREC00, UPDR0000 *
.*        Process General HCP comments. Write/update records in pmstcoaf.dat *
.*        Comments longer than 70 chars will be chopped into multiple lines. *
.*                                                                           *
.* Requires: COMNTHCP - Comments HCP Code                                    *
.*           COMNTTYP - Comments Type (Cat TZ)                               *
.*           COMNTLNE - Comments Line (max 200 chars)                        *
.*****************************************************************************
PCOM0000  STRIP     COMNTLNE                     * remove trailing spaces
          MOVE      ZERO,COMMTLEN
          MOVELPTR  COMNTLNE,COMMTLEN            * get comment field length
.
          COMPARE   ZERO,COMMTLEN                * any comments ?
          GOTO      PCOM2000 IF EQUAL            * no - clear existing
.
          MOVE      ZERO,LINENUMB                * yes - initialise line number
          PACK      COMNTHCP,COMNTHCP,SP70
.
.         Write a comment line
.
PCOM1000  ADD       ONE,LINENUMB                 * increment line number
.
          PACK      KEY16,COMNTHCP,COMNTTYP,LINENUMB,SP70
          CALL      RAPMTCO1
          IF        OVRCD=1
            MOVE      COMNTHCP,PMTCCODE          * HCP Code
            MOVE      COMNTTYP,PMTCTCOM          * Type of Comments (Cat TZ)
            MOVE      LINENUMB,PMTCLNUM          * Line number
            MOVE      COMNTLNE,PMTCCOMM
            PACK      PMTCCOMM,PMTCCOMM,SP70     * load comment line
            MOVE      TODAY,PMTCUDAT
            MOVE      UPDTTIME,PMTCUTIM
            MOVE      USERID,PMTCUUID            * WEB User Id Updated
            MOVE      SP70,PMTCSPAR
            CALL      WRPMTCO1
          ELSE
            CALL    RDPMTCO1
            IF      OVRCD=0
              MOVE      COMNTHCP,PMTCCODE        * HCP Code
              MOVE      COMNTTYP,PMTCTCOM        * Type of Comments (Cat TZ)
              MOVE      LINENUMB,PMTCLNUM        * Line number
              MOVE      COMNTLNE,PMTCCOMM
              PACK      PMTCCOMM,PMTCCOMM,SP70   * load comment line
              MOVE      TODAY,PMTCUDAT
              MOVE      UPDTTIME,PMTCUTIM
              MOVE      USERID,PMTCUUID          * WEB User Id Updated
              CALL      UPPMTCO1
            ENDIF
          ENDIF
.
          COMPARE   COMMTLEN,SEVENTY             * any comments remaining ?
          GOTO      PCOM9999 IF NOT LESS         * no - finished
.
.         There is at least one more line of comments to load
.
          BUMP      COMNTLNE,70                  * move forward 70 characters
          SUB       SEVENTY,COMMTLEN             * reduce string length by 70
          GOTO      PCOM1000                     * process next line
.
.
.         Clear all existing comments lines
PCOM2000  PACK      KEY16,COMNTHCP,COMNTTYP,SP70
          CALL      RSPMTCO1
PCOM2100  CALL      RKPMTCO1
          BRANCH    OVRCD,PCOM9999
.
          MATCH     COMNTHCP,PMTCCODE
          GOTO      PCOM9999 IF NOT EQUAL
.
          MATCH     COMNTTYP,PMTCTCOM
          GOTO      PCOM9999 IF NOT EQUAL
.
          MOVE      SP70,PMTCCOMM                * clear comment line
          MOVE      TODAY,PMTCUDAT
          MOVE      UPDTTIME,PMTCUTIM
          MOVE      USERID,PMTCUUID              * WEB User Id Updated
          CALL      UPPMTCO1
.
          GOTO      PCOM2100
.
PCOM9999  RETURN
+
.******************************************************************************
.*                           PRHD0000                    Called by : PRERRL00 *
.*                                                                   PRTOT000 *
.*                  Print HCP Upload Report header details                    *
.******************************************************************************
PRHD0000  COMPARE   "60",CLNO
          GOTO      PRHD9999 IF LESS
.
          CALL      HEAD132                      * Print New Page Header
.
          PRINT     *N,"Input Data File : ",PATHNAME,*N
.
          IF        TOTDRERR = 0
            MOVE      "6",CLNO
.
            GOTO      PRHD9999
          ENDIF
.
PRHD1000  CALL      UND132
.
          PRINT     *1,"| HCP Code":
                    *15,"| Error Message":
                    *132,"|"
.
          CALL      UND132
.
          MOVE      "9",CLNO
.
PRHD9999  RETURN
+
.******************************************************************************
.*                           PRTOT000                    Called by : MAIN0000 *
.*                  Print report totals                                       *
.******************************************************************************
PRTOT000  COMPARE   "60",CLNO
          CALL      PRHD0000 IF NOT LESS
.
          IF        TOTDRERR = 0
            GOTO      PRTOT100
          ENDIF
.
          CALL      UND132
          PRINT     *1,"|",*132,"|"
          PRINT     *1,"| Number of Errors: ",TOTDRERR,*132,"|"
          PRINT     *1,"|",*132,"|"
          CALL      UND132
          ADD       FIVE,CLNO
.
PRTOT100  PRINT     *N,*1," Number of HCP Codes Added        : ",TOTDRADD
          PRINT     *1," Number of HCP Codes Updated      : ",TOTDRUPD
          PRINT     *1," Number of HCP records processed  : ",TOTDOCTS
.
          ADD       FIVE,CLNO
.
PRTOT999  RETURN
+
.******************************************************************************
.*                           PRERRL00                    Called by : PROC0000 *
.*                                                                   VALI0000 *
.*                  Print Error Line                                          *
.******************************************************************************
PRERRL00  ADD       ONE,TOTDRERR       * increment error count
.
          COMPARE   "60",CLNO
          CALL      PRHD0000 IF NOT LESS
.
          PRINT     *1,"| ",UPLDHCPC:
                    *15,"| ",ERRMESSG:
                    *132,"|"
.
          ADD       ONE,CLNO
.
PRERRL99  RETURN
+
.*******************************************************************************
.*                           VDTE0000                     Called by : VALI0000 *
.*        Validates a date field                                               *
.*                                                                             *
.*        Requires:  TEMPDATE - Date in format ccyymmdd                        *
.*        Returns:   EXIT = 0 if no error                                      *
.*                   EXIT = 1 if an error in date validation occurs            *
.*******************************************************************************
VDTE0000  SQUEEZE   TEMPDATE
          MOVELPTR  TEMPDATE,FORM1
          IF        FORM1 <> 8
            GOTO      VDTE9800
          ENDIF
.
          UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,FORM2
          IF        FORM2 < 18 | FORM2 > CCC
            GOTO      VDTE9800
          ENDIF
.
          MOVE      CMON,FORM2
          IF        FORM2 < 1 | FORM2 > 12
            GOTO      VDTE9800
          ENDIF
.
          BRANCH    FORM2,VDTE1000:              * Jan
                          VDTE3000:              * Feb
                          VDTE1000:              * Mar
                          VDTE2000:              * Apr
                          VDTE1000:              * May
                          VDTE2000:              * Jun
                          VDTE1000:              * Jul
                          VDTE1000:              * Aug
                          VDTE2000:              * Sep
                          VDTE1000:              * Oct
                          VDTE2000:              * Nov
                          VDTE1000               * Dec
.                                                * Validate days for 31 day Mnths
VDTE1000  MOVE      CDAY,FORM2
          IF        FORM2 < 1 | FORM2 > 31
            GOTO      VDTE9800
          ENDIF
          GOTO      VDTE9900
.                                                * Validate days for 30 day Mnths
VDTE2000  MOVE      CDAY,FORM2
          IF        FORM2 < 1 | FORM2 > 30
            GOTO      VDTE9800
          ENDIF
          GOTO      VDTE9900
.                                                * Validate days for February
. * N.B. A leap year is one where:
. *      If the year is divisible 100 and 400, then it is a leap century year
. *      Otherwise every year divisible by 4 is a leap year.
VDTE3000  MOVE      CDAY,FORM2
          PACK      D4,CCENT,CYEAR
          MOVE      D4,FORM4
          MOVE      TWENTY8,LEAPDAYS             * Normal February Days
.
          IF        (FORM4%4) = 0
            IF        (FORM4%100) = 0
              IF        (FORM4%400) = 0
                MOVE      TWENTY9,LEAPDAYS       * Leap Century February Days
              ENDIF
            ELSE
              MOVE      TWENTY9,LEAPDAYS         * Leap Year February Days
            ENDIF
          ENDIF
.                                                * Check for February days
VDTE3100  IF        FORM2 < 1 | FORM2 > LEAPDAYS
            GOTO      VDTE9800
          ENDIF
          GOTO      VDTE9900
.
VDTE9800  MOVE      ONE,EXIT
          GOTO      VDTE9999
.
VDTE9900  MOVE      ZERO,EXIT
.
VDTE9999  RETURN
+
.******************************************************************************
.*                           DSCR0000                    Called by : ADDREC00 *
.*  Create blank records in patdstat file for each period this year           *
.******************************************************************************
DSCR0000  PACK      KEY6,FINYEAR,SP10
          CALL      RDSDRGA1
DSCR1000  CALL      RDKDRGA1
.
          BRANCH    OVRCD,DSCR9999
.
          MATCH     DRGYR,FINYEAR
          GOTO      DSCR9999 IF LESS
.
          MOVE      ZERO,PTDSBADM
          MOVE      ZERO,PTDSBBED
          MOVE      ZERO,PTDSBOPR
          MOVE      ZERO,PTDSBMBS
          MOVE      ZERO,PTDSADMS
          MOVE      ZERO,PTDSBEDD
          MOVE      ZERO,PTDSOPRS
          MOVE      ZERO,PTDSMBSC
          MOVE      FINYEAR,PTDSYEAR
          MOVE      DRGNUM,PTDSPERD
          MOVE      DCODE,PTDSCODE
.
          PACK      KEY13,PTDSYEAR,PTDSPERD,ANSR,PTDSCODE
          CALL      RADSTA1
          IF        OVRCD=1
            CALL      WRDSTA1
          ENDIF
          PACK      KEY13,PTDSYEAR,PTDSPERD,ANSA,PTDSCODE
          CALL      RADSTA1
          IF        OVRCD=1
            CALL      WRDSTA1
          ENDIF
.
          GOTO      DSCR1000
.
DSCR9999  RETURN
+
*******************************************************************************
          INC       STD001IO           * Standard Includes
.
          INC       CLPATDOC           * Clear Doctor File
          INC       CLPMSHCP           * Clear HCP File
          INC       PATCOMN3           * Patient Common Routines
          INC       PATDKIPR           * Doctor Key Word generation
          INC       PMSHKIPR           * HCP Key Word generation
          INC       DGCLIM02           * Send HL7 Messages
.
          INC       MLTHCPIO/INC       * Multi Hospital HCP Codes
          INC       PATCODIO/INC       * Patient Codes File
          INC       PATDO1IO/INC       * Doctor Details File
          INC       PATDKIIO/INC       * Doctors Key Word File
          INC       PATDRGIO/INC       * Patient Period Date Range File
          INC       PATDSTIO/INC       * Patient Doctor Statistics File
          INC       PATHSPIO/INC       * Hospital Details File
          INC       PATIN1IO/INC       * Insurance Details File
          INC       PMSDUNIO/INC       * Doctor/Unit Combination File
          INC       PMSHCPIO/INC       * HCP Details File
          INC       PMSHKIIO/INC       * HCP Key Word File
          INC       PMSTCOIO/INC       * General HCP Comments
          INC       WEBSECIO/INC       * Web Security File

.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT93                cloned from IBAADT83             *
.* Description    :  Upload Prosthetic Charges                                *
.******************************************************************************
.* Date           :  25/07/2003                                               *
.* Author         :  J.Tan / Mike Laming                                      *
.* Function       :  Upload Prosthetic charges from ASCII file                *
.* Modifications  :                                                           *
.*        V11.05.02 28/04/2025  J.Tan          TSK 0956044                    *
.*                  Mod to update Misc.charge Keyword                         *
.*        V11.05.01 22/04/2025  Bella Turco    TSK 0959376                    *
.*                  Added validation to prevent entering Eff End Date < Eff   *
.*                  Start Date                                                *
.*        V11.03.01 25/05/2023  J.Tan          TSK 0923703                    *
.*                  Recompiled for ABF new calculations                       *
.*        V11.02.01 07/08/2022 Alina Bhari    TSK 0922199                     *
.*                  Fixed the patient portion and rebate portion Misc Charge  *
.*                  to load 8 numeric value                                   *
.*                  Fixed the Misc Charge - change description Y for Yes      *
.*                  and N for No - VFLD0000, KCDI1000                         *
.*        V10.11.01 20/07/2017 Peter Vela     TSK 0835226                     *
.*                  Added Health Care Provider (Eclipse)                      *
.*        V10.06.01 17/06/2014 J.Tan          CAR 305159                      *
.*                  Mods to strip Eclipse Type of Service field               *
.*                  Fixed not to upd Eclipse type of serv. for prev.misc.chrg *
.*        V10.04.01 12/06/2014 Jill Parkinson CAR 301639                      *
.*                  Mods to call TFILENAM in INIT0000 and KILL0000 on exit    *
.*        V10.03.06 06/03/2013 J.Tan             CAR 281783                   *
.*                  Fixed populating GST Code for Option 3 - Default HF       *
.*        V10.03.05 19/12/2012 Patrick Adair     CAR 261630                   *
.*                  Removed port number from temp file name                   *
.*        V10.03.04 20/11/2012  J.Tan            CAR 257132                   *
.*                  Mods to get Eclipse Type of Service from spread sheet     *
.*        V10.03.03 05/03/2012  J.Tan            CAR 257132                   *
.*                  Mods to set Eclipse Type of Service to "13"               *
.*        V10.03.02 29/03/2012  J.Tan            CAR 257132                   *
.*                  Mods for new spreadsheet layout                           *
.*        V10.03.01 06/03/2012  J.Tan            CAR 261372                   *
.*                  Fixed keyin a blank effective todate                      *
.*        V10.02.02 22/06/2011  Saroeun Soeur    CAR 244995                   *
.*                  created EXUS0000 - execute us1 script                     *
.*        V10.02.01 20/04/2011  J.Tan            CAR 233160                   *
.*                  Fixed set up effective to date                            *
.*        V10.01.02 18/02/2011  Ebon Clements    CAR 233160                   *
.*                  Added effective to date                                   *
.*        V10.01.01 29/11/2010  Mike Laming      CAR 233046                   *
.*                  Mods for "Table Type" Cat."HT" (replaces Health Fund Table)*
.*        V10.00.01 27/08/2010  Steve Armstrong  CAR 229181                   *
.*                  Mods to make TXTACD DIM2 instead of DIM1 to handle        *
.*                  expanded Eclipse Types of Service values.                 *
.******************************************************************************
.*        V9.11.02  16/01/2009  J.Tan         CAR 187277                      *
.*                  Mods to check spaces for Eclipse equivalent               *
.*        V9.11.01  15/01/2009  J.Tan         CAR 187277                      *
.*                  Added Eclipse Type of Service & Eclipse equivalent        *
.*        V9.09.02  29/01/2008  Peter Vela        CAR 155334                  *
.*                  Added "Set Tracking on for Prosthetic Items (Y/N)"        *
.*        V9.09.01  10/07/2007  Steve Armstrong   CAR 144789                  *
.*                  Recompiled for changes to PATMC1IO.                       *
.******************************************************************************
.*        V9.04.01  11/01/2005  CAR 55773   Davin                             *
.*                  Changed to use newmchgf not patmchgf                      *
.******************************************************************************
.*        V9.03.00  28/04/2004  CAR 48475   Lina Vo                           *
.*                  Ported to V9.03. Changed read & write to patmchgf to use  *
.*                  effective date.  Added extra code for web (IBARSH).       *
.******************************************************************************
.*        V6.11.00  25/07/2003  CAR 19830   Mike Laming                       *
.*                  Cloned from IBAADT83 with new fields loaded to "patmchgf" *
.*                  Misc.Charges file                                         *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATMCHFD/INC            * Miscellaneous charges file
          INC       NZPMCHFD/INC            * NZ priv misc charges
          INC       PATFN1FD/INC            * Fund file
          INC       PATCODFD/INC            * Code file
          INC       PATCONFD/INC            * Control file
          INC       PATHGPFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.         Prosthetic Charges Upload temp file
.
UMISCTXT  FILE       HL7, FIXED=1000        * Upload file for Misc.Charges
.
.Name     Type     Size     
.----     ----     ----    
XMCODE    DIM       9        Miscellaneous Charge Code
XMDESC    DIM      30        Miscellaneous Charge Description
XMREBT    FORM    8.2        Rebate portion ($ format = 12345.12)
XMPAPO    FORM    8.2        Patient Portion ($ format = 12345.12)
.
XCLMCD    DIM       3        Claim Code
XHFCOD    DIM       6        Health Fund Code
XHFTYP    DIM       3        Health Fund Table Type
XMCDSI    DIM       1        Change Description Indicator (Y/N)
XMCHTY    DIM       1        Charge Type (2=Theatre,3=Misc.Item)
XMMSGR    DIM       3        Misc. Charge Group
XMKRBI    DIM       1        Keyin Rebate Indicator (Y/N)
XMSDAT    DIM       8        Effective Start Date
XMEDAT    DIM       8        Effective End Date
XMCFCI    DIM       1        Cmix Funding Chargeable No. (0=>9)
XMDRCI    DIM       1        Doctor Charge Indicator (Y/N)
XMNINV    DIM       1        Number of Invoices
XMPTRK    DIM       1        Prosthetic Tracking
.
XMGSTA    DIM       1        GST Applicable
XMGSTC    DIM       6        GST Code
XMECEQ    DIM      11        Eclipse Equivalent
XMTACT    DIM       1        TAC Type
XMTDUR    DIM       4        TAC Duration
XMACTV    DIM       1        Active
.
XMEDIE    DIM      10        EDI Equivalent
XMTACD    DIM       2        Eclipse Type of service (right just.)
XEEHCP    DIM       2        Health Care Provider (Eclipse)
.
.End of Record
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
XFLD0006  DIM       30
XFLD0007  DIM       30
XFLD0008  DIM       30
XFLD0009  DIM       30
XFLD0010  DIM       30
XFLD0011  DIM       30
XFLD0012  DIM       30
XFLD0013  DIM       30
XFLD0014  DIM       30
XFLD0015  DIM       30
XFLD0016  DIM       30
XFLD0017  DIM       30
XFLD0018  DIM       30
XFLD0019  DIM       30
XFLD0020  DIM       30
XFLD0021  DIM       30
XFLD0022  DIM       30
XFLD0023  DIM       30
XFLD0024  DIM       30
XFLD0025  DIM       30
XFLD0026  DIM       30
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      8
CMDLINE   DIM       127
CODE      DIM       2
DIM3      DIM       3
DIM6      DIM       6
DIM11     DIM       11
ERRCOUNT  FORM      8
ERRORMSG  DIM       30
IBCNMHOS  FORM      1
FNAMEI    DIM       60
FNAME1    DIM       8
FORM12P2  FORM      12.2
UPDCOUNT  FORM      8
.
KYCLMCD   DIM       3
KYHFGRP   DIM       6
KYHFUND   DIM       6
KYHFTAB   FORM      1
KYCHGDS   DIM       1
KYCHRTY   DIM       1
KYGROUP   DIM       3
KYKEYRB   DIM       1
KYEFDAT   DIM       8
KYTODAT   DIM       8
KYCMXFN   DIM       1
KYDOCCH   DIM       1
KYNUMIV   FORM      1
KYPROST   DIM       1
.
MODE      DIM       1
NEWRECD   FORM      1
SAVKEY12  DIM       12
SAVKEY21  DIM       21
USERID    DIM       10
VALDPASS  FORM      1
VALFDATE  DIM       8
VALTDATE  DIM       8
WORKDATE  DATE      8
.
CODECL    INIT      "CL"
CODEFI    INIT      "FI"
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
LN        INIT      "|"
PATHNAME  DIM       150
SAVKEY20  DIM       20
MISCEL    INIT      "MISC. ITEM "
THEATRE   INIT      "THEATRE FEE"
ZERO8     INIT      "0000    "
.
TEMPFILE  IFILE     SQL, FIXED=4, KEY="uc1-3"
.
PRGID     INIT      "IBAADT93"
PRGNAM    INIT      "Upload Prosthetic Charges"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initailize variables & open files
.
          CALL      OPTN0000                * Choose main options
          BRANCH    EXIT,ML9999
.
          BRANCH    COPTION,ML1000
          GOTO      ML9999
.
ML1000    CALL      KASC0000                * Keyin ascii file
          BRANCH    EXIT,ML0000
.
          CALL      KOPT0000                * Choose options
          BRANCH    EXIT,ML9999
.
ML1100    CALL      SCRD0000                * Display screen
.
          CALL      KFLD0000                * Keyin fields
          BRANCH    EXIT,ML0000
.
          CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000                * Keyin pathname
.
          CALL      SFLD0000
          BRANCH    EXIT,ML1100
.
          CALL      PRHD0000                * Print header
.
          CALL      CTOT0000                * Clear total vars
.
          MOVE      ONE,VALDPASS
          CALL      POST0000                * Validating file
.
          IF        ERRCOUNT=0
            MOVE      ZERO,VALDPASS
            CALL      POST0000              * Updating file
          ENDIF
.
          CALL      PTOT0000                * print error report
          GOTO      ML9999
.
ML9900    DISPLAY   *P1:24,"Failed to open upload file. ";
          CALL      HITENTER
.
ML9999    CALL      KILL0000                * Delete the tempfile
          CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK

          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patmchgf";
          OPEN      PATMCHG1,"patmchgf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATFN1A3,"patfn1af"
.
          DISPLAY   *P64:24,"pathgrpf"
          OPEN      PATHGRP1,"pathgrpf"
.
          DISPLAY   *P64:24,"controlf"          * Open CONTROLF
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME1
.
          MOVE      ZERO,EXIT
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000                                   *
.*                            Choose Main Option                              *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          CALL      DISPHEAD
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Load Prosthetic Charges";
          DISPLAY   *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,COPTION;
          BRANCH    COPTION,OPTN9999
          BEEP
.
OPTN9000  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Choose HF group or HF code                      *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:12,*EF:
                    *P1:12,*V2LON,"0",*HOFF," = Exit":
                    *P1:13,*V2LON,"1",*HOFF," = By Health fund Group":
                    *P1:14,*V2LON,"2",*HOFF," = By Health fund":
                    *P1:15,*V2LON,"3",*HOFF," = Default Health fund/table";
          DISPLAY   *P1:17,"Select Option : ";
.
KOPT1000  KEYIN     *P17:17,*V2LON,OPTION;
          BRANCH    OPTION,KOPT9999,KOPT9999,KOPT9999
.
          COMPARE   ZERO,OPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70
.
          KEYIN     *P1:10,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
          PACK      FNAMEI,FNAMEI,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:10,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      UMISCTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXUS0000             * Execute us1 command
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*        execute us1 script to backup file
.******************************************************************************
EXUS0000  CLEAR     CMDLINE
          APPEND    "ibaadt93.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXUS9999  RETURN
+
.******************************************************************************
.*                                 KFLD0000                                   *
.*                            Keyin Fields                                    *
.******************************************************************************
KFLD0000  CALL      IVAR0000                * Initialise variables
.
          CALL      KCLM0000                * Keyin claim code
          BRANCH    EXIT,KFLD9999
.
          COMPARE   THREE,OPTION
          GOTO      KFLD2000 IF EQUAL       * default hf/table
.
          CALL      KFND0000                * Keyin Health fund
          BRANCH    EXIT,KFLD9999
.
          CALL      KTAB0000                * Keyin health fund table
          BRANCH    EXIT,KFLD9999           * exit/eos
.
KFLD2000  CALL      KCDI0000                * Keyin Change Description
          CALL      KCTY0000                * Keyin charge type
          CALL      KGRP0000                * Keyin group
          CALL      KRPI0000                * Keyin Rebate Portion
          CALL      KEDT0000                * Keyin Effective date
          CALL      KTDT0000                * Keyin Effective to date
          CALL      KCFC0000                * Casemix Chargeable
          CALL      KDCI0000                * Keyin Doctor Charge Ind
          CALL      KNOI0000                * Keyin no of invoices
          CALL      KTPI0000                * Keyin Tracking for Prost.
          MOVE      ZERO,EXIT
.
KFLD9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P26:18,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:19,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                 IVAR0000                                   *
.*                            Initialise variable                             *
.******************************************************************************
IVAR0000  MOVE      SP3,KYCLMCD
          MOVE      SP70,KYHFGRP
          MOVE      SP70,KYHFUND
          MOVE      ZERO,KYHFTAB
          MOVE      "N",KYCHGDS
          MOVE      SP70,KYCHRTY
          MOVE      SP3,KYGROUP
          MOVE      "N",KYKEYRB
          UNPACK    SP70,KYEFDAT,KYTODAT
          MOVE      "0",KYCMXFN
          MOVE      SP70,KYDOCCH
          MOVE      ONE,KYNUMIV
          MOVE      "0",KYPROST
.
IVAR9999  RETURN
+
.******************************************************************************
.*                                 SCRD0000                                   *
.*                            Screen display                                  *
.******************************************************************************
SCRD0000  DISPLAY   *P1:4,*EF:
                    *P1:5," 1. Claim Code         :";
.
          COMPARE   THREE,OPTION
          GOTO      SCRD3000 IF NOT EQUAL
.
          DISPLAY   *P1:8," 2. Change Desc. (Y/N) :":
                    *P1:9," 3. Charge Type        :":
                   *P1:10," 4. Misc. Charge Group :":
                   *P1:11," 5. Keyin Rebate (Y/N) :":
                   *P1:12," 6. Effective Strt Date:":
                   *P1:13," 7. Effective End Date :":
                   *P1:14," 8. Casemix Chargeable :":
                   *P1:15," 9. Doctor Charge(Y/N) :":
                   *P1:16,"10. Number of Invoices :":
                   *P1:17,"11. Prosthetic Tracking:":
                   *P1:18,"12. Keyin User id      :":
                   *P1:19,"13. Keyin Path name    :";
          GOTO     SCRD9999
.
SCRD3000  IF       OPTION=1
            DISPLAY   *P1:6," 2. Health Fund Group  :";
          ELSE
            DISPLAY   *P1:6," 2. Health Fund Code   :";
          ENDIF
.
          DISPLAY   *P1:7," 3. Health Fund Table  :":
                    *P1:8," 4. Change Desc. (Y/N) :":
                    *P1:9," 5. Charge Type        :":
                   *P1:10," 6. Misc. Charge Group :":
                   *P1:11," 7. Keyin Rebate (Y/N) :":
                   *P1:12," 8. Effective Strt Date:":
                   *P1:13," 9. Effective End Date :":
                   *P1:14,"10. Casemix Chargeable :":
                   *P1:15,"11. Doctor Charge(Y/N) :":
                   *P1:16,"12. Number of Invoices :":
                   *P1:17,"13. Prosthetic Tracking:":
                   *P1:18,"14. Keyin User id      :":
                   *P1:19,"15. Keyin Path name    :";
.
SCRD9999  RETURN
+
.******************************************************************************
.*                                 KCLM0000                                   *
.*                            Keyin claim Code                                *
.******************************************************************************
KCLM0000  MOVE      SP10,KYCLMCD
          MOVE      ZERO,EXIT
          KEYIN     *P26:5,*EL,*V2LON,KYCLMCD;
          ENDSET    KYCLMCD
          APPEND    SP3,KYCLMCD
          RESET     KYCLMCD
.
          MATCH     SP3,KYCLMCD
          GOTO      KCLM9000 IF EQUAL
.
          CMATCH    EXITCHAR,KYCLMCD
          GOTO      KCLM9000 IF EQUAL
.
          CMATCH    QUEST,KYCLMCD
          GOTO      KCLM1000 IF EQUAL
.
          PACK      KEY5,CODECL,KYCLMCD
          CALL      RDCODE1
          BRANCH    OVRCD,KCLM2000
.
          DISPLAY   *P26:5,*V2LON,KYCLMCD,*P30:5,TDESC;
          MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM1000  CALL      GETSCR00
          MOVE      CODECL,CODE
          CALL      PATCODDS
          BRANCH    OVRCD,KCLM2000
          CALL      PUTSCR00
          GOTO      KCLM0000
.
KCLM2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid claim code.  ";
          CALL      HITENTER
          GOTO      KCLM0000
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.******************************************************************************
.         Keyin health fund group or health fund code
.******************************************************************************
KFND0000  IF        OPTION=1
            CALL      KFGR0000                * Keyin health fund group code
          ELSE
            CALL      KHFN0000                * Keyin health fund code
          ENDIF
KFND9999  RETURN
+
.******************************************************************************
.*                                 KFGR0000                                   *
.*                            Keyin Health fund group                         *
.******************************************************************************
KFGR0000  MOVE      SP10,KYHFGRP
          MOVE      ZERO,EXIT
          KEYIN     *P26:6,*EL,*V2LON,KYHFGRP;
          ENDSET    KYHFGRP
          APPEND    SP3,KYHFGRP
          RESET     KYHFGRP
.
          MATCH     SP3,KYHFGRP
          GOTO      KFGR9000 IF EQUAL
.
          PACK      KEY6,KYHFGRP
          CALL      RDPAHGP1
          BRANCH    OVRCD,KFGR2000
.
          DISPLAY   *P26:6,*V2LON,KYHFGRP,*P30:6,PTHGDESC;
          GOTO      KFGR8000
.
KFGR2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Health fund Group code.  ";
          CALL      HITENTER
          GOTO      KFGR0000
.
KFGR8000  MOVE      ZERO,EXIT
          GOTO      KFGR9999
.
KFGR9000  MOVE      ONE,EXIT
KFGR9999  RETURN
+
.******************************************************************************
.*                                 KHFN0000                                   *
.*                            Keyin Health fund code                          *
.******************************************************************************
KHFN0000  MOVE      SP10,KYHFUND
          MOVE      ZERO,EXIT
.
          KEYIN     *P26:6,*EL,*EL,*V2LON,KYHFUND;
          RESET     KYHFUND
          GOTO      KHFN9999 IF EOS
.
          ENDSET    KYHFUND
          APPEND    SP6,KYHFUND
          RESET     KYHFUND
.
          CMATCH    EXITCHAR,KYHFUND
          GOTO      KHFN9000 IF EQUAL
.
          MATCH     SP6,KYHFUND
          GOTO      KHFN9200 IF EQUAL
.
          PACK      KEY14,KYHFUND,ZERO8
          CALL      RDFUND1
          BRANCH    OVRCD,KHFN2000
.
          DISPLAY   *P26:6,*V2LON,KYHFUND,*P30:6,HFNAME;
          GOTO      KHFN9999
.
KHFN2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Health Fund code.  "
          CALL      HITENTER
          GOTO      KHFN0000
.
KHFN9000  MOVE      ONE,EXIT        * exit char. entered
          GOTO      KHFN9999
.
KHFN9200  MOVE      TWO,EXIT        * spaces entered
          GOTO      KHFN9999
.
KHFN9999  RETURN
+
.******************************************************************************
.*                                 KTAB0000                                   *
.*                            Keyin HF Table                                  *
.******************************************************************************
KTAB0000  MOVE      ZERO,KYHFTAB
          MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"1 = All, 2 = As per Upload";
          KEYIN     *P26:7,*EL,*V2LON,KYHFTAB;
          BRANCH    KYHFTAB,KTAB1000,KTAB1000
          DISPLAY   *P1:24,*EL,*B,*V2LON,"Must be one or two.  ";
          CALL      HITENTER
          GOTO      KTAB0000
.
KTAB1000  IF        KYHFTAB=1
            MOVE      "All                ",TDESC
          ELSE
            MOVE      "As Per Upload      ",TDESC
          ENDIF
          DISPLAY   *P26:7,*V2LON,KYHFTAB,*P36:7,TDESC;
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      KTAB9999
.
KTAB9000  MOVE      ONE,EXIT
KTAB9999  RETURN
+
.******************************************************************************
.*                                  KCDI0000                                  *
.*                   Keyin The Change Description Indicator                   *
.******************************************************************************
KCDI0000  MOVE      KYCHGDS,DIM1       
          KEYIN     *P26:8,*EL,*V2LON,*RV,DIM1;
          GOTO      KCDI9999 IF EOS
.
          ENDSET    DIM1
          APPEND    SP70,DIM1
          RESET     DIM1
.
          MATCH     SP70,DIM1
          GOTO      KCDI9999 IF EQUAL
.
          REP       UPPLOW,DIM1
          MATCH     "Y",DIM1
          GOTO      KCDI1000 IF EQUAL
.
          MATCH     "N",DIM1
          GOTO      KCDI1000 IF EQUAL
.
          BEEP
          GOTO      KCDI0000
.
KCDI1000  MOVE      DIM1,KYCHGDS
          REP       "Y1N0",DIM1
          MOVE      DIM1,FORM1
.
          MOVE      DNO,DIM3
          LOAD      DIM3,FORM1,DYES,DNO
          DISPLAY   *P26:8,*EL,*V2LON,DIM3;
.
KCDI9999  RETURN
+
.******************************************************************************
.*                                 KCTY0000                                   *
.*                            Keyin Charge Type                               *
.******************************************************************************
KCTY0000  DISPLAY   *P50:21,*EL,*V2LON,*ULON," CHARGE TYPE ",*HOFF:
                    *P55:22,*EL,*V2LON,TWO,SP2,*HOFF,THEATRE:
                    *P55:23,*EL,*V2LON,THREE,SP2,*HOFF,MISCEL;
./
          MOVE      ZERO,EXIT
          MOVE      SP1,KYCHRTY
          MOVE      ZERO,FORM1
          KEYIN     *P26:9,*V2LON,*EL,KYCHRTY;
          RESET     KYCHRTY
          GOTO      KCTY2000 IF EOS
.
          ENDSET    KYCHRTY
          APPEND    SP70,KYCHRTY
          RESET     KYCHRTY
.
          MATCH     SP70,KYCHRTY
          GOTO      KCTY2000 IF EQUAL
.
          MATCH     EXITCHAR,KYCHRTY
          GOTO      KCTY9000 IF EQUAL       * Exit char keyed in
.
          MATCH     "2",KYCHRTY
          GOTO      KCTY1000 IF EQUAL
.
          MATCH     "3",KYCHRTY
          GOTO      KCTY1000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Invalid charge Type.  ";
          GOTO	     KCTY0000
.
KCTY1000  MOVE      KYCHRTY,FORM1
          LOAD      DIM11,FORM1,SP20,THEATRE,MISCEL
          DISPLAY   *P26:9,*EL,*V2LON,KYCHRTY,*P30:9,DIM11;
.
KCTY2000  DISPLAY   *P50:21,*EL,*P55:22,*EL,*P55:23,*EL;
          GOTO      KCTY9999
.
KCTY9000  MOVE      ONE,EXIT
KCTY9999  RETURN
+
.******************************************************************************
.*                                 KGRP0000                                   *
.*                            Keyin Misc.Charge Group                         *
.******************************************************************************
KGRP0000  MOVE      "26",CCOL
          MOVE      "10",CVERT
.
KGRP1000  MOVE      SP3,KYGROUP
          MOVE      KYGROUP,DIM3
          MOVE      SP20,TDESC
          MOVE      ZERO,EXIT
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,*RV,DIM3;
          RESET     DIM3
          GOTO      KGRP9000 IF EOS
.
          ENDSET    DIM3
          APPEND    SP3,DIM3
          RESET     DIM3
.
          MATCH     SP70,DIM3
          GOTO      KGRP9000 IF EQUAL
.
          MATCH     EXITCHAR,DIM3
          GOTO      KGRP9100 IF EQUAL       * Exit char keyed in
.
          CMATCH    QUEST,DIM3
          GOTO      KGRP3000 IF EQUAL       * '?' keyed in
.
          MOVE      SP70,TDESC
          PACK      KEY5,CODEFI,DIM3,SP10
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,KGRP4000
.
          MOVE      DIM3,KYGROUP
          DISPLAY   *P26:10,*EL,*V2LON,KYGROUP,*P30:10,TDESC;
          GOTO      KGRP9000
.
KGRP3000  CALL      GETSCR00              * Save the current screen
          MOVE      CODEFI,CODE
          CALL      PATCODDS                * Display the miscell charge groups
          BRANCH    OVRCD,KGRP4000
          CALL      PUTSCR00
          GOTO      KGRP0000
.
KGRP4000  DISPLAY   *P1:24,*EL,*B,"Invalid Miscellaneous Charge Group Code.  ";
          CALL      HITENTER
          GOTO      KGRP1000
.
KGRP9000  MOVE      ZERO,EXIT
          GOTO      KGRP9999
.
KGRP9100  MOVE      ONE,EXIT
KGRP9999  RETURN
+
.******************************************************************************
.*                                  KRPI0000                                  *
.*                     Keyin The Rebate Portion Indicator                     *
.******************************************************************************
KRPI0000  MOVE      KYKEYRB,DIM1
          KEYIN     *P26:11,*EL,*V2LON,*RV,DIM1;
          RESET     DIM1
          GOTO      KGRP9000 IF EOS
.
          REP       UPPLOW,DIM1
          ENDSET    DIM1
          APPEND    SP3,DIM1
          RESET     DIM1
.
          MATCH     "Y",DIM1
          GOTO      KRPI1000 IF EQUAL
.
          MATCH     "N",DIM1
          GOTO      KRPI1000 IF EQUAL
.
          BEEP
          GOTO      KRPI0000
.
KRPI1000  MOVE      DIM1,KYKEYRB
          REP       "Y1N2",DIM1
          MOVE      DIM1,FORM1
.
          MOVE      DNO,DIM3
          LOAD      DIM3,FORM1,DYES,DNO
          DISPLAY   *P26:11,*EL,*V2LON,DIM3;
.
KRPI9999  RETURN
+
.******************************************************************************
.*                                  KEDT0000                                  *
.*                        Keyin The Effective date                            *
.* Returns: KYEFDAT = effective date                                          *
.******************************************************************************
.
KEDT0000  MOVE       "12",CVERT
          MOVE       "26",CCOL
          MOVE       ONE,CCANLDTE
          MOVE       SP70,KYEFDAT
.
          UNPACK     SP70,CDAY,CMON,CYEAR,CCENT
          CALL       KEYDTE00
          PACK       KYEFDAT WITH ICENT,IYEAR,IMON,IDAY
          REP        " 0",KYEFDAT
.
KEDT9999  RETURN
+
.******************************************************************************
.*                                  KTDT0000                                  *
.*                        Keyin The Effective to date                         *
.* Returns: KYTODAT = effective to date                                       *
.******************************************************************************
.
KTDT0000  MOVE       "13",CVERT
          MOVE       "26",CCOL
          MOVE       ONE,CCANLDTE
          MOVE       SP70,KYTODAT
.
          UNPACK     SP70,CDAY,CMON,CYEAR,CCENT
          CALL       KEYDATE
          BRANCH     OVRCD,KTDT9999
.
          PACK       KYTODAT WITH ICENT,IYEAR,IMON,IDAY
          REP        " 0",KYTODAT
.
KTDT9999  RETURN
+
.******************************************************************************
.*                                  KCFC0000                                  *
.*                   Keyin Casemix funding chargeable                         *
.******************************************************************************
KCFC0000  MOVE      "14",CVERT
.
KCFC1000  MOVE      "0",KYCMXFN
          DISPLAY   *P1:24,*EL,"Casemix funding chargeable from item 0 = No":
                               ", 1, 2, 3, 4, 5, 6, 7, 8, 9"
          KEYIN     *P26:CVERT,*EL,*V2LON,KYCMXFN
          RESET     KYCMXFN
          GOTO      KCFC9000 IF EOS
.
          REPLACE   UPPLOW,KYCMXFN
          ENDSET    KYCMXFN
          APPEND    SP3,KYCMXFN
          RESET     KYCMXFN
.
          MATCH     SP70,KYCMXFN
          GOTO      KCFC9000 IF EQUAL
.
          TYPE      KYCMXFN
          GOTO      KCFC9999 IF EQUAL
.
          BEEP
          GOTO      KCFC1000
.
KCFC9000  DISPLAY   *P1:24,*EL;
KCFC9999  RETURN
+
.******************************************************************************
.*                                  KDCI0000                                  *
.*                      Keyin The Doctor Charge Indicator                     *
.******************************************************************************
KDCI0000  KEYIN     *P26:15,*EL,*V2LON,DIM1;
          RESET     DIM1
          GOTO      KDCI9999 IF EOS
.
          REP       UPPLOW,DIM1
          ENDSET    DIM1
          APPEND    SP3,DIM1
          RESET     DIM1
.
          MATCH     SP70,DIM1
          GOTO      KDCI9999 IF EQUAL
.
          MATCH     "Y",DIM1
          GOTO      KDCI1000 IF EQUAL
.
          MATCH     "N",DIM1
          GOTO      KDCI1000 IF EQUAL
.
          BEEP
          GOTO      KDCI0000
.
KDCI1000  MOVE      DIM1,KYDOCCH
          REP       "Y1N2",DIM1
          MOVE      DIM1,FORM1
.
          MOVE      DNO,DIM3
          LOAD      DIM3,FORM1,DYES,DNO
          DISPLAY   *P26:15,*EL,*V2LON,DIM3;
.
KDCI9999  RETURN
+
.******************************************************************************
.*                                  KNOI0000                                  *
.*                        Keyin The Number Of Invoices                        *
.******************************************************************************
KNOI0000  MOVE      ONE,KYNUMIV
          MOVE      ZERO,EXIT
          KEYIN     *P26:16,*EL,*V2LON,*RV,KYNUMIV;
          BRANCH    KYNUMIV,KNOI9000,KNOI9000
.
          IF        KYNUMIV<1 | KYNUMIV>2
            DISPLAY   *P1:24,*EL,*B,"Number of Invoices must be 1 or 2.  ";
            CALL      HITENTER
            GOTO      KNOI0000
          ENDIF
.
KNOI9000  MOVE      ZERO,EXIT
KNOI9999  RETURN
+
.******************************************************************************
.*                                  KTPI0000                                  *
.*                   Keyin  Set Tracking on for Prosthetic Items              *
.******************************************************************************
KTPI0000  MOVE      SP70,DIM1
          KEYIN     *P26:17,*EL,*V2LON,DIM1
          RESET     DIM1
          GOTO      KTPI9999 IF EOS
.
          REP       UPPLOW,DIM1
          ENDSET    DIM1
          APPEND    SP3,DIM1
          RESET     DIM1
.
          MATCH     SP70,DIM1
          GOTO      KTPI9999 IF EQUAL
.
          MATCH     "Y",DIM1
          GOTO      KTPI1000 IF EQUAL
.
          MATCH     "N",DIM1
          GOTO      KTPI1000 IF EQUAL
.
          BEEP
          GOTO      KTPI0000
.
KTPI1000  MOVE      "0",KYPROST
          REP       "Y1N0",DIM1
          MOVE      DIM1,KYPROST
.
          MOVE      DIM1,FORM1
          MOVE      DNO,DIM3
          LOAD      DIM3,FORM1,DYES,DNO
          DISPLAY   *P26:17,*EL,*V2LON,DIM3;
.
KTPI9999  RETURN 
.******************************************************************************
.*                                  SFLD0000                                  *
.*                        Select Field                                        *
.******************************************************************************
SFLD0000  CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SFLD9999 IF EQUAL
          GOTO      SFLD1000 IF LESS
.
          MOVE      ZERO,EXIT
          IF        OPTION=3
            PERFORM   CCITEM,KCLM0000:             * Keyin claim code
                             KCDI0000:             * Keyin Change Description
                             KCTY0000:             * Keyin charge type
                             KGRP0000:             * Keyin group
                             KRPI0000:             * Keyin Rebate Portion
                             KEDT0000:             * Keyin Effective date
                             KTDT0000:             * Keyin Effective to date
                             KCFC0000:             * Casemix Chargeable   
                             KDCI0000:             * Keyin Doctor Charge Ind
                             KNOI0000:             * Keyin no of invoices
                             KTPI0000:             * Keyin Tracking for Prost.
                             KUSR0000:             * Keyin user id
                             KPTH0000              * Keyin pathname
          ELSE
            PERFORM   CCITEM,KCLM0000:             * Keyin claim code
                             KFND0000:             * Keyin Health fund/table
                             KTAB0000:             * Keyin health fund table
                             KCDI0000:             * Keyin Change Description
                             KCTY0000:             * Keyin charge type
                             KGRP0000:             * Keyin group
                             KRPI0000:             * Keyin Rebate Portion
                             KEDT0000:             * Keyin Effective date
                             KTDT0000:             * Keyin Effective to date
                             KCFC0000:             * Casemix Chargeable   
                             KDCI0000:             * Keyin Doctor Charge Ind
                             KNOI0000:             * Keyin no of invoices
                             KTPI0000:             * Keyin Tracking for Prost.
                             KUSR0000:             * Keyin user id
                             KPTH0000              * Keyin pathname
          ENDIF
          BRANCH    EXIT,SFLD9999
          GOTO      SFLD0000
.
SFLD1000  MOVE      ONE,EXIT
SFLD9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.*                        Post record                                         *
.******************************************************************************
POST0000  CALL      OPEN0000            * Open tempfile
.
          BRANCH    OPTION,POST2000     * by health fund group
.
          IF        OPTION=3
            MOVE      SP70,KYHFUND      * blank health fund
          ENDIF
          CALL      GFND0000            * Generate fees for the health fund
          GOTO      POST9999
.
.         Get health funds for the HF group
.
POST2000  MOVE      SP70,SAVKEY20
          PACK      KEY20,KYHFGRP,SP70
          CALL      RDSFUND3
POST4000  CALL      RDKFUND3
          BRANCH    OVRCD,POST9999
          MATCH     KYHFGRP,PTHFHGRP    * Same group?
          GOTO      POST9999 IF NOT EQUAL
          MATCH     "0000    ",HFTABL
          GOTO      POST4000 IF NOT EQUAL
.
          MOVE      HCODE,KYHFUND
          PACK      SAVKEY20,PTHFHGRP,HCODE,HFTABL,SP70
.
          CALL      GFND0000            * Generate fees for the health fund
.
          MOVE      SAVKEY20,KEY20
          CALL      RDFUND3             * Reposition
.
          GOTO      POST4000
.
POST9999  RETURN
+
.******************************************************************************
.*                                  GFND0000                                  *
.*                        Generate Misc.fees for a health fund                *
.******************************************************************************
GFND0000  DISPLAY   *P1:24,*EL,"Updating: ";
          MOVE      ZERO,ERRCOUNT            * Total error count
.
          OPEN      UMISCTXT,FNAMEI
GFND1000  
          READ      UMISCTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011,XFLD0012,XFLD0013,XFLD0014,XFLD0015:
                                 XFLD0016,XFLD0017,XFLD0018,XFLD0019,XFLD0020:
                                 XFLD0021,XFLD0022,XFLD0023,XFLD0024,XFLD0025:
                                 XFLD0026
          GOTO      GFND9999 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001
          GOTO      GFND1000 IF EQUAL         * blank misc.charge Code
.
          PACK      XMCODE,XFLD0001,SP70
          PACK      XMDESC,XFLD0002,SP70
.
          CALL      VFLD0000                * validate input fields
.
          MOVE      ZERO,XMREBT
          MOVE      XFLD0003,XMREBT           * Rebate amount
          MOVE      ZERO,XMPAPO
          MOVE      XFLD0004,XMPAPO           * Patient amount
.
          PACK      XCLMCD,XFLD0005,SP70
          MATCH     XCLMCD,KYCLMCD
          IF        !@EQUAL
            MOVE      "Claim Code Mismatch.",ERRORMSG
            CALL      PERR0000
            GOTO      GFND1000
          ENDIF
.
          BRANCH    OPTION,GFND2100,GFND2200,GFND2400
          GOTO      GFND1000
.
.         By Health Fund Group
.
GFND2100  PACK      XHFCOD,KYHFUND,SP70
          PACK      XHFTYP,XFLD0007,SP70
          GOTO      GFND3000
.
.         By Health Fund
.
GFND2200  MATCH     XHFCOD,KYHFUND
          IF        !@EQUAL
            MOVE      "Health Fund Mismatch",ERRORMSG
            CALL      PERR0000
            GOTO      GFND1000
          ENDIF
          GOTO      GFND3000
.
.         Default
.
GFND2400  MOVE      SP70,XHFCOD
          MOVE      SP70,XHFTYP
.
GFND3000  BRANCH    VALDPASS,GFND1000
.
          PACK      XFLD0018,XFLD0018,SP70
          PACK      XMGSTA,XFLD0018,SP70
          MATCH     SP70,XMGSTA
          IF        @EQUAL
            MOVE      "0",XMGSTA            * default - 0 GST free
          ENDIF
          PACK      XMGSTC,XFLD0019,SP70
          PACK      XMECEQ,XFLD0020,SP70
          PACK      XMTACT,XFLD0021,SP70
          PACK      XMTDUR,XFLD0022,SP70
.
          PACK      XFLD0023,XFLD0023,SP70
          PACK      XMACTV,XFLD0023,SP70
          MATCH     SP70,XMACTV
          IF        @EQUAL
            MOVE      "0",XMACTV            * default - 0 Active
          ENDIF
.
          PACK      XFLD0024,XFLD0024,SP70
          MATCH     SP70,XFLD0024
          IF        !@EQUAL
            PACK      XMEDIE,XFLD0024,SP70
          ELSE
            MOVE      "PX00",KEY4
            PACK      XMEDIE,KEY4,XMCODE,SP70
          ENDIF
.
          PACK      XFLD0025,XFLD0025,SP70
.         MATCH     SP70,XFLD0025
.         IF        !@EQUAL
.           PACK      XMTACD,XFLD0025,SP70
.         ELSE
.           MOVE      "13",XMTACD             * default to 13 for Prosthetic
.         ENDIF
.
          STRIP     XFLD0025
          MOVE      ZERO,F2
          MOVE      XFLD0025,F2
.
          MOVE      "13",XMTACD             * default to 13 for Prosthetic
          IF        (F2<>0) & (F2<17)
            MOVE      F2,XMTACD
          ENDIF
.
          PACK      XEEHCP,XFLD0026,SP70
.
          COMPARE   THREE,OPTION
          GOTO      GFND5000 IF EQUAL
.
          CALL      SETF0000                * set fields
.
          BRANCH    KYHFTAB,GFND6000        * All Health fund table or type
.
.         as per Upload file
.
GFND5000  CALL      WMCH0000                * Write record to Misc.item file
          GOTO      GFND1000
.
.         All health fund table type of the health fund
.
GFND6000  PACK      KEY14,KYHFUND,SP70
          CALL      RDSFUND1
GFND7000  CALL      RDKFUND1
          BRANCH    OVRCD,GFND8000
          MATCH     KYHFUND,HCODE
          GOTO      GFND8000 IF NOT EQUAL
.
          MATCH     "0000    ",HFTABL
          GOTO      GFND7000 IF EQUAL
.
          BRANCH    PHFTACT,GFND7000        * Inactive
.
          CALL      CHKFND00            * Check if HF type had been processed
          BRANCH    EXIT,GFND7000
.
          MOVE      PTHFBCAT,XHFTYP         * Health fund Table type
          CALL      WMCH0000
          GOTO      GFND7000                * get next record
.
GFND8000  CALL      DTMP0000                * clear tempfile
          GOTO      GFND1000
.
GFND9000  CLOSE     UMISCTXT
.
GFND9999  RETURN
+
.******************************************************************************
.         Set fields for misc.charge
.******************************************************************************
SETF0000  MOVE      ZERO,XMREBT
          MOVE      XFLD0003,XMREBT         * Rebate amount
          MOVE      ZERO,XMPAPO
          MOVE      XFLD0004,XMPAPO         * Patient amount
.
          PACK      XMCDSI,XFLD0008,SP70    * Change Description (Y/N)
          PACK      XMCHTY,XFLD0009,SP70    * Charge type
          PACK      XMMSGR,XFLD0010,SP70
          PACK      XMKRBI,XFLD0011,SP70
          PACK      XMSDAT,XFLD0012,SP70
          PACK      XMEDAT,XFLD0013,SP70    * Effective End date
          PACK      XMCFCI,XFLD0014,SP70
          PACK      XMDRCI,XFLD0015,SP70
          PACK      XMNINV,XFLD0016,SP70
          PACK      XMPTRK,XFLD0017,SP70
.
          PACK      XFLD0018,XFLD0018,SP70
          PACK      XMGSTA,XFLD0018,SP70
          MATCH     SP70,XMGSTA
          IF        @EQUAL
            MOVE      "0",XMGSTA            * default - 0 GST free
          ENDIF
          PACK      XMGSTC,XFLD0019,SP70
          PACK      XMECEQ,XFLD0020,SP70
          PACK      XMTACT,XFLD0021,SP70
          PACK      XMTDUR,XFLD0022,SP70
.
          PACK      XFLD0023,XFLD0023,SP70
          PACK      XMACTV,XFLD0023,SP70
          MATCH     SP70,XMACTV
          IF        @EQUAL
            MOVE      "0",XMACTV            * default - 0 Active
          ENDIF
.
SETF9999  RETURN
+
.******************************************************************************
.*                Deleting tempfile                                           *
.******************************************************************************
DTMP0000  MOVE      SP70,KEY3
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,DTMP9999
.
          PACK      KEY3,DIM3,SP70
          CALL      DETEMP1
          GOTO      DTMP0000
DTMP9999  RETURN
+
.******************************************************************************
.*                                  CHKFND00                                  *
.*                        Check if this HF type has been processed            *
.******************************************************************************
CHKFND00  MATCH     SP70,PTHFBCAT
          GOTO      CHKFND90 IF EQUAL
.
          MOVE      PTHFBCAT,KEY3
          CALL      RATEMP1
          COMPARE   ZERO,OVRCD
          GOTO      CHKFND90 IF EQUAL      * has been processed
.
          CALL      WRTEMP1
          MOVE      ZERO,EXIT
          GOTO      CHKFND99
.
CHKFND90  MOVE      ONE,EXIT
CHKFND99  RETURN
+
.******************************************************************************
.         Write record to Misc.Charge file
.******************************************************************************
WMCH0000  PACK      XHFTYP,XHFTYP,SP70
          PACK      KEY29,XCLMCD,XHFCOD,XHFTYP,XMCODE,XMSDAT,SP70
          CALL      RDMCHG1
          IF        OVRCD=1
            MOVE      XCLMCD,MCLMCOD
            MOVE      XHFCOD,MHFUND
            MOVE      XHFTYP,PTMCHFTT
            MOVE      XMCODE,MCHARGE
            MOVE      XMSDAT,PTMCEFDT
            CALL      SITM0000        * Set Misc.item fields
            CALL      WRMCHG1
            PROC      PATMKIUP        * update default misc charge keyword
            ADD       ONE,ADDCOUNT
          ELSE
            CALL      SITM0000        * Set Misc.item fields
            CALL      UPMCHG1
            PROC      PATMKIUP        * update default misc charge keyword
            ADD       ONE,UPDCOUNT
          ENDIF
.
          CALL      EDAT0000          * Auto populate prev items end date
          MOVE      ZERO,EXIT
.
WMCH9999  RETURN
+
.******************************************************************************
.           set misc.charge fields
.******************************************************************************
SITM0000  MOVE      XMDESC,MDESC
          MOVE      XMPAPO,MPATPOR
          MOVE      XMREBT,MHFPOR
.
          MOVE      XMCDSI,MINDIC
          MOVE      XMCHTY,MITMTYP
          MOVE      XMMSGR,MMSCGRP
          MOVE      XMKRBI,PTMCKREB
          MOVE      XMEDAT,PTMCETDT
          MOVE      XMCFCI,PTMCCFCY
          MOVE      XMDRCI,PTMCCOBD
          MOVE      XMNINV,MNINV
          MOVE      XMPTRK,PTMCPRTR
.
          MOVE      XMGSTA,PTMCGSTA
          MOVE      XMGSTC,PTMCGSTC
          MOVE      XMECEQ,PTMCECEQ
.
          MOVE      XMTACT,PTMCTACT
          MOVE      XMTDUR,PTMCTACD
          MOVE      XMACTV,PTMCACTV
.
          MOVE      XMEDIE,PTMCEDIE
          MOVE      XMTACD,PTMCECTS
          MOVE      XEEHCP,PTMCEHCP
.
          MOVE      SP70,PTMCALGR
          PACK      PTMCSPAR,SP70
SITM9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Upload Prosthetic Charges"
.
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID:
                    *N,"Claim Code            : ",KYCLMCD;
.
          IF        OPTION=1
            PRINT     *N,"By Health Fund Group  : ",KYHFGRP
          ELSE
            IF        OPTION=2
              PRINT     *N,"By Health Fund        : ",KYHFUND
            ELSE
              PRINT     *N,"Default Health Fund/table"
            ENDIF
          ENDIF
.
          IF        KYHFTAB=1
            MOVE      "All          ",KEY15
          ELSE
            MOVE      "As Per Upload",KEY15
          ENDIF
          PRINT     *N,"Health Fund Table          : ",KEY15:
                    *N,"Prosthetic Load file       : ":
                    *N,"     ",PATHNAME
.
PRHD9999  RETURN
+
.-----------------------------------------------------------------
. Auto populate the previous misc items effective to date if blank
.-----------------------------------------------------------------
EDAT0000  PACK      KEY29,XCLMCD,XHFCOD,XHFTYP,XMCODE,XMSDAT,SP70
          CALL      RDSMCHG1
          CALL      RDPMCHG1
          BRANCH    OVRCD,EDAT9999
.
          MATCH     XCLMCD,MCLMCOD
          GOTO      EDAT9999 IF NOT EQUAL
.
          MATCH     XHFCOD,MHFUND
          GOTO      EDAT9999 IF NOT EQUAL
.
          MATCH     XHFTYP,PTMCHFTT
          GOTO      EDAT9999 IF NOT EQUAL
.
          MATCH     XMCODE,MCHARGE
          GOTO      EDAT9999 IF NOT EQUAL
.
          MATCH     SP70,PTMCETDT                * Exit if effective to date is
          GOTO      EDAT9999 IF NOT EQUAL        * already populated
.
          MOVE      XMSDAT,WORKDATE
          SUB       ONE,WORKDATE
          MOVE      WORKDATE,PTMCETDT
.
          MOVE      "PX00",KEY4
          PACK      PTMCEDIE,KEY4,XMCODE,SP70
          CALL      UPMCHG1
.
EDAT9999  RETURN
+
.******************************************************************************
.*                                  VFLD0000                                  *
.*                            Validate some fields                            *
.******************************************************************************
VFLD0000  MOVE      ZERO,EXIT
.
          PACK      XFLD0005,XFLD0005,SP70
          MATCH     SP70,XFLD0005
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,XFLD0005,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Claim Code.",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
          PACK      XCLMCD,XFLD0005,SP70
.
          PACK      XFLD0006,XFLD0006,SP70
          MATCH     SP70,XFLD0006
          IF        !@EQUAL
            PACK      KEY6,XFLD0006,SP70
            PACK      KEY14,KEY6,ZERO8,SP70
            CALL      RDFUND1
            IF        OVRCD=1
              MOVE      "Invalid Health Fund",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
          PACK      XHFCOD,XFLD0006,SP70
.
          PACK      XFLD0007,XFLD0007,SP70
          MATCH     SP70,XFLD0007
          IF        !@EQUAL
            PACK      KEY5,ANSH,ANST,XFLD0007,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Health Fund Table Type",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
          PACK      XHFTYP,XFLD0007,SP70
          IF        OPTION=2
            MATCH     SP70,XHFTYP
            IF        @EQUAL
              MOVE      "Invalid blank Health Fund Table Type",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          PACK      XFLD0008,XFLD0008,SP70
          MATCH     SP70,XFLD0008
          IF        @EQUAL
            MATCH     SP70,KYCHGDS
            IF        @EQUAL
              MOVE      "Invalid Blank Change Desc.",ERRORMSG
              CALL      PERR0000
              GOTO      VFLD1200
            ELSE
              PACK      XFLD0008,KYCHGDS,SP70
            ENDIF
          ENDIF
.
          UNPACK    XFLD0008,DIM1           * only " ", "Y", "N" are valid
          REP       "Y1N0",DIM1
          MATCH     SP70,DIM1
          IF        @EQUAL
            MOVE      "Invalid Change Description",ERRORMSG
            CALL      PERR0000
          ENDIF
          PACK      XFLD0008,DIM1,SP70    * Change Description (Y/N)
          PACK      XMCDSI,DIM1,SP70    * Change Description (Y/N)
.
VFLD1200  PACK      XFLD0009,XFLD0009,SP70
          MATCH     SP70,XFLD0009
          IF        @EQUAL
            MATCH     SP70,KYCHRTY
            IF        @EQUAL
              MOVE      "Invalid Blank Charge Type.",ERRORMSG
              CALL      PERR0000
              GOTO      VFLD1400
            ELSE
              PACK      XFLD0009,KYCHRTY,SP70
            ENDIF
          ENDIF
          PACK      XMCHTY,XFLD0009,SP70    * Charge type
.
          UNPACK    XFLD0009,DIM1
          REP       "2 3 ",DIM1
          MATCH     SP1,DIM1
          GOTO      VFLD1400 IF EQUAL
.
          MOVE      "Invalid Charge Type.",ERRORMSG
          CALL      PERR0000
.
VFLD1400  PACK      XFLD0010,XFLD0010,SP70
          MATCH     SP70,XFLD0010
          IF        @EQUAL
            MATCH     SP70,KYGROUP
            IF        @EQUAL
              MOVE      "Invalid Blank Misc.Charge Group.",ERRORMSG
              CALL      PERR0000
              GOTO      VFLD1600
            ELSE
              PACK      XFLD0010,KYGROUP,SP70
            ENDIF
          ENDIF
          PACK      XMMSGR,XFLD0010,SP70
.
          PACK      KEY5,CODEFI,XFLD0010
          CALL      RDCODE1                 * Read the codes file
          IF        OVRCD=1
            MOVE      "Invalid Misc.Charge Group.",ERRORMSG
            CALL      PERR0000
          ENDIF
.
VFLD1600  PACK      XFLD0011,XFLD0011,SP70
          MATCH     SP70,XFLD0011
          IF        @EQUAL
            MATCH     SP70,KYKEYRB
            IF        @EQUAL
              MOVE      "Invalid Blank Keyin Rebate.",ERRORMSG
              CALL      PERR0000
              GOTO      VFLD2000
            ELSE
              PACK      XFLD0011,KYKEYRB,SP70
            ENDIF
          ENDIF
          PACK      XMKRBI,XFLD0011,SP70
.
          UNPACK    XFLD0011,DIM1             * only " ", "Y", "N" are valid
          REP       "Y N ",DIM1
          MATCH     SP1,DIM1
          IF        !@EQUAL
            MOVE      "Invalid Keyin Rebate.",ERRORMSG
            CALL      PERR0000
          ENDIF
.
VFLD2000  PACK      XFLD0012,XFLD0012,SP70       * Effective start date
          MATCH     SP70,XFLD0012
          IF        @EQUAL
            MATCH     SP70,KYEFDAT
            IF        !@EQUAL
              PACK      XFLD0012,KYEFDAT,SP70
            ELSE
              MOVE      "Invalid Blank Effective Start Date.",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
          PACK      XMSDAT,XFLD0012,SP70
.
          PACK      XFLD0013,XFLD0013,SP70
          MATCH     SP70,XFLD0013
          IF        @EQUAL
            MATCH     SP70,KYTODAT
            IF        !@EQUAL
              PACK      XFLD0013,KYTODAT,SP70
            ENDIF
          ELSE
            MATCH     XFLD0012,XFLD0013
            IF        @LESS
              MOVE      "Eff End Date < Eff Start Date.",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
          PACK      XMEDAT,XFLD0013,SP70    * Effective End date
.
          PACK      XFLD0014,XFLD0014,SP70
          MOVE      SP70,DIM1
          MATCH     SP70,XFLD0014
          IF        @EQUAL
            MATCH     SP70,KYCMXFN
            IF        @EQUAL
              MOVE      "Invalid Blank Casemix Funding.",ERRORMSG
              CALL      PERR0000
              GOTO      VFLD2300
            ELSE
              PACK      XFLD0014,KYCMXFN,SP70    * Casemix Funding
            ENDIF
          ENDIF
          PACK      XMCFCI,XFLD0014,SP70
.
          UNPACK    XFLD0014,DIM1
          TYPE      DIM1                  * only want 0 => 9 is valid
          IF        !@EQUAL
            MOVE      "Invalid Casemix Funding.",ERRORMSG
            CALL      PERR0000
          ENDIF
.
VFLD2300  PACK      XFLD0015,XFLD0015,SP70
          MATCH     SP70,XFLD0015
          IF        @EQUAL
            MATCH     SP70,KYDOCCH
            IF        @EQUAL
              MOVE      "Invalid Blank Doctor Charge.",ERRORMSG
              CALL      PERR0000
              GOTO      VFLD3200
            ELSE
              PACK      XFLD0015,KYDOCCH,SP70
            ENDIF
          ENDIF
          PACK      XMDRCI,XFLD0015,SP70
.
          MOVE      XFLD0015,DIM1             * only " ", "Y", "N" are valid
          REP       "Y N ",DIM1
          MATCH     SP1,DIM1
          IF        !@EQUAL
            MOVE      "Invalid Doctor Charge.",ERRORMSG
            CALL      PERR0000
          ENDIF
.
VFLD3200  PACK      XFLD0016,XFLD0016,SP70
          MATCH     SP70,XFLD0016
          IF        @EQUAL
            IF        KYNUMIV<>1
              MOVE      "Invalid No of Invoice.",ERRORMSG
              CALL      PERR0000
              GOTO      VFLD5000
            ELSE
              PACK      XFLD0016,KYNUMIV,SP70
            ENDIF
          ENDIF
          PACK      XMNINV,XFLD0016,SP70
.
VFLD5000  PACK      XFLD0017,XFLD0017,SP70
          MATCH     SP70,XFLD0017
          IF        @EQUAL
            MATCH     SP70,KYPROST
            IF        !@EQUAL
              PACK      XFLD0017,KYPROST,SP70    * Prosthetic tracking
            ENDIF
          ENDIF
          PACK      XMPTRK,XFLD0017,SP70
.
          PACK      XFLD0026,XFLD0026,SP70
          MATCH     SP70,XFLD0026
          IF       !@EQUAL
            LJUSTIFY  XFLD0026
            UNPACK    XFLD0026,KEY1,KEY29
            MATCH     SP70,KEY29
            IF        @EQUAL
              MOVE      "Health Care Provider (Eclipse), must be length of 2. ",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
VFLD9999  RETURN
+
.******************************************************************************
.*                                  PERR0000                                  *
.*                             Print Error Message                            *
.******************************************************************************
PERR0000  COMPARE   ONE,VALDPASS
          GOTO      PERR9999 IF NOT EQUAL
.
          COMPARE   ZERO,ERRCOUNT
          CALL      PERH0000 IF EQUAL
.
          ADD       ONE,ERRCOUNT            * Total error count
.
          PACK      XMCODE,XFLD0001,SP70
          PACK      XCLMCD,XFLD0005,SP70
          PACK      XHFCOD,XFLD0006,SP70
.
          PRINT     *1,"|",ERRCOUNT,*12,"|",ERRORMSG,*44,"|":
                    XMCODE,*60," | ",XCLMCD,*75," | ",XHFCOD,*132,"|"
.
          MOVE      ONE,EXIT
PERR9999  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ADDCOUNT
          MOVE      ZERO,UPDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
.         Print error header
.******************************************************************************
PERH0000  CALL      HEAD132
          CALL      UND132
          PRINT     *1,"| Line No.",*12,"|Error Messages",*44,"|Misc.Charge":
                    *60," |Claim Code",*75," |Health Fund",*132,"|"
          CALL      UND132
PERH9999  RETURN
+
.******************************************************************************
.*                                  PTOT0000                                  *
.*                        Print end of error report                           *
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
          PRINT     *1,"| Total Updated : ",UPDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
+
.******************************************************************************
.*                                  KEYDTE00                                  *
.*                      Subroutine to key in a valid date                     *
.******************************************************************************
KEYDTE00  DISPLAY   *PCCOL:CVERT,*EL
.
          MOVE      IDAY,CDAY
          MOVE      IMON,CMON
          MOVE      IYEAR,CYEAR
          MOVE      ICENT,CCENT
          MOVE      "1",CDEFDTE
          MOVE      "0",CHIGHLT
          CALL      KEYDATE
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          MOVE      DD,IDAY
          MOVE      MM,IMON
          MOVE      YY,IYEAR
          MOVE      CC,ICENT
.
KEYDTE99  RETURN
+
.******************************************************************************
.*                                  OPEN0000              Called by: INCL000  *
.*                              Open The Tempfile         RSENT000 & RREJT000 *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 4 U1-3",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * isbuild the temp file
          OPEN      TEMPFILE,FNAME1
.
OPEN9000  MOVE      ZERO,EXIT
OPEN9999  RETURN
.
.******************************************************************************
.*                                 KILL0000               Called by: ML0000   *
.*                            Delete The Tempfile                  & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * iserase the temp file
.
KILL9000  MOVE      ZERO,EXIT
KILL9999  RETURN
+
.******************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      TEMPFILE,KEY3;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY3
          WRITE     TEMPFILE,KEY3;KEY3
          RETURN
.
RSTEMP1  RESET     KEY3
         READ      TEMPFILE,KEY3;;
         RETURN
.
RKTEMP1  MOVE      ZERO,OVRCD
         READKS    TEMPFILE;DIM3
         GOTO      OVERCOND IF OVER
         RETURN
.
DETEMP1  MOVE      ZERO,OVRCD
         RESET     KEY3
         DELETE    TEMPFILE,KEY3
         RETURN
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATFNDDS
          INC       PATMCHVL
          INC       TFILENAM                     * Generate Temp File Name
          INC       PATMKIPR
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATFN1IO/INC
          INC       PATMCHIO/INC
          INC       NZPMCHIO/INC                 * NZ priv misc charges
          INC       PATHGPIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
.
.
.
.

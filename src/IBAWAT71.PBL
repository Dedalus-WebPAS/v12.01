.******************************************************************************
.* System         : Waiting Lists                                             *
.* Program        : IBAWAT71.PBL                                              *
.* Name           : Waiting List Suspension Report                            *
.******************************************************************************
.* Date           : 11/02/1993                                                *
.* Author         : Justin Coates                                             *
.* Function       : To print all waiting list procedures currently suspended. *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*       V10.11.01  07/09/2017  Thanh T.          TSK 0821710                 *
.*                  Removed PATCOMFD/IO                                       *   
.*       V10.05.01  01/08/2015  Ania P            CAR 261630                  *
.*                  Removed use of PORT for temp file naming                  *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*                  V5.07.B01 19/01/1999  Nicole Harrington 507 rebound 040   *
.*                            Mods to print century                           *
.*                  V5.01.02  20/04/93  ROD                                   *
.*                            Fixed report as desired by Dudley               *
.*                  V5.01.03  31/12/93  ROD           SRF 440560              *
.*                            Fixed report as desired by Dudley again!!!!!    *
.*                  V5.01.04  07/02/94  ROD                                   *
.*                            see above!                                      *
.******************************************************************************
.
.         FD includes 
.
          INC       STD001FD                * standard variables
          INC       PATCODFD/INC            * codes file
          INC       PATCONFD/INC            * codes file
          INC       PATDO1FD/INC            * codes file
          INC       PATMA1FD/INC            * codes file
          INC       WATMESFD/INC
          INC       WATPROFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATSUSFD/INC
          INC       WATTR1FD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.         program variables
.
ASATDATE  DIM       8
COUNT     FORM      2
CMDLINE   DIM       70
CURRDATE  DIM       8
DATE10A   DIM       10
DATE10B   DIM       10
DATE10C   DIM       10
DESC18    DIM       18
DESC44    DIM       44
DIM3      DIM       3
DIM30     DIM       30
DOCCODE   DIM       6
DOCNAME   DIM       30
FHDR      INIT      "wat71a"
FNAME     DIM       8
PATNAME   DIM       35
PATSEX    DIM       7
PATTYPE   DIM       20
PRASATD   DIM       10
PRDLIST   DIM       10
PRSUSFR   DIM       10
PRSUSTO   DIM       10
.
.Temp file definition
... TEMPFILE  IFILE     SQL, FIXED=106, KEY="U1-8,9-16,17-25,26-27,28-29"
TEMPFILE  IFILE     SQL, FIXED=106, KEY="U79-86,9-16,17-25,26-27,28-29"
XXDLIST   DIM       8       1     * date on list
XXURNO    DIM       8       9     * UR Number
XXPROC    DIM       9      17     * Procedure
XXPCNT    DIM       2      26     * Procedure count
XXSCNT    DIM       2      28     * Suspension count
XXGNAME   DIM       19     30     * Given name
XXSNAME   DIM       19     49     * Surname
XXLTYPE   DIM       3      68     * List type
XXDSUSFR  DIM       8      71     * Suspension From date
XXDSUSTO  DIM       8      79     * Suspension To date
XXSREAS   DIM       19     87     * Suspension reason
.         End of record   106
.
.         program constants
.
CATWN     INIT      "WN"
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBAWAT71"
PRGNAM    INIT      "Procedure Suspension Report"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.*********************************************************************
.         Mainline Code / Controlling Logic
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000
          BRANCH    EXIT,ML9999             * exit selected
.
ML3000    CALL      KCON0000                * keyin consultant
          BRANCH    EXIT,ML1000             * nothing input
.
          CALL      KDAT0000                * keyin as at date
          BRANCH    EXIT,ML3000             * nothing input
.
          CALL      CONTQST                 * Ok to continue?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
.
ML4000    CALL      LOAD0000                * load data into temp file
          CALL      REPT0000                * print report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.********************************************************************* 
.         Initialization
.*********************************************************************
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
.
          MOVE      ONE,CNEWDS
          DISPLAY   *P50:24,"Opening"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"     * codes file
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"     * codes file
          OPEN      PATMX1A1,"patmx1af"     * codes file
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"     * codes file
          OPEN      PATDO1A2,"patdo1af"     * codes file
          DISPLAY   *P58:24,"watmesaf"
          OPEN      WATMESA1,"watmesaf"     * codes file
          DISPLAY   *P58:24,"watproaf"
          OPEN      WATPROA1,"watproaf"     * codes file
          DISPLAY   *P58:24,"watseaaf"
          OPEN      WATSEAA1,"watseaaf"     * codes file
          DISPLAY   *P58:24,"watseiaf"
          OPEN      WATSEIA1,"watseiaf"     * codes file
          DISPLAY   *P58:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"     * codes file
          DISPLAY   *P58:24,"watsusaf"
          OPEN      WATSUSA1,"watsusaf"     * codes file
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY;*238,PTCNURCO
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.*********************************************************************
.         See if to produce listing or not
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Produce report":
                    *P1:7,"Select option : "
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL       * exit
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.*********************************************************************
.*  KCON0000  :  Keyin Consultant                                    *
.*********************************************************************
KCON0000  DISPLAY   *P1:10,*EF,"Consultant : ",*P1:12,"As at Date : "
.
          MOVE      "14",ECOL
          MOVE      "10",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      PATDOCKY
          BRANCH    EXIT,KCON9999,KCON0000        * blank,exitchar
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          DISPLAY   *P23:10,PACFNAME
          MOVE      PACFNAME,DOCNAME
          MOVE      ZERO,EXIT
.
KCON9999  RETURN
+
.**************************************************************************
.*  KDAT0000  :   Keyin as at date                                        *
.**************************************************************************
KDAT0000  UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "14",CCOL
          MOVE      TEN2,CVERT
          MOVE      CCC,CCENT
          MOVE      ZERO,CDEFDTE
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT9000                * anything input
.
          PACK      ASATDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ASATDATE
          CALL      PACDATE
          MOVE      CPCDATE,PRASATD
.
          MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT
.
KDAT9999  RETURN
+
.*********************************************************************
.* LOAD0000  :  Load data into a temp file                           *
.*********************************************************************
LOAD0000  CALL      CRET0000                      * create temp file
.
          MOVE      SP30,KEY21
          CALL      RSWTSUS1
LOAD1000  CALL      RKWTSUS1
          BRANCH    OVRCD,LOAD9999          * end of report
.
          MATCH     ASATDATE,WTSUTDAT
          GOTO      LOAD1000 IF LESS
.
          MATCH     WTSUFDAT,ASATDATE
          GOTO      LOAD1000 IF LESS
.
          PACK      KEY19,WTSUURNO,WTSUCODE,WTSUCNT
          CALL      RDTREA1                          * get list type
          BRANCH    OVRCD,LOAD1000
.
          DISPLAY   *P25:24,WMDOCTOR;
          MATCH     WMDOCTOR,DOCCODE              * same doctor code
          GOTO      LOAD1000 IF NOT EQUAL
.
.         does the user have security clearance?
.
LOAD3000  PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          IF        OVRCD=1
            PACK      KEY13,DOCCODE,WMUNIT,PASSCODE
            CALL      RDWTSEI1
            BRANCH    OVRCD,LOAD1000
          ENDIF
.
          DISPLAY   *P1:24,*EL,WTSUURNO,SP1,WTSUCODE,SP1,WTSUCNT,SP1,WTSUSCNT
          PACK      KEY8,WTSUURNO           * get patient name
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD1000
.
          MOVE      "Invalid code",TDESC    * get the suspension reason
          PACK      KEY5,CATWN,WTSUREAS
          CALL      RDCODE1
          MOVE      TDESC,XXSREAS
.
. post a record to the temp file
.
          MOVE      WMDATE1,XXDLIST
          MOVE      WTSUURNO,XXURNO
          MOVE      WTSUCODE,XXPROC
          MOVE      WTSUCNT,XXPCNT
          MOVE      WTSUSCNT,XXSCNT
          MOVE      PGNAME,XXGNAME
          MOVE      PSNAME,XXSNAME
          MOVE      WMUNIT,XXLTYPE
          MOVE      WTSUFDAT,XXDSUSFR
          MOVE      WTSUTDAT,XXDSUSTO
.
          PACK      KEY29,XXDSUSTO,XXURNO,XXPROC,XXPCNT,XXSCNT
          CALL      WRTEMP1
.
          DISPLAY   *P1:24,*V2LON,WTSUURNO,SP1,WTSUCODE,SP1,WTSUCNT,SP1,WTSUSCNT
          GOTO      LOAD1000
.
LOAD9999  RETURN
+
.*********************************************************************
.         Produce the report
.*********************************************************************
REPT0000  CALL      IBACLOCK
          MOVE      ZERO,CPAGENO            * clear page number
          CALL      HEAD0000                * print header
.
          MOVE      SP30,KEY29
          CALL      RSTEMP1
REPT1000  CALL      RKTEMP1
          BRANCH    OVRCD,REPT9000          * end of report
.
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          CALL      FDAT0000                * format data for printing
.
          PRINT     *1,"| ",XXURNO,*12,XXSNAME,*32,"| ",XXLTYPE,*39,"| ":
                    DESC44,*87,"|",PRDLIST,*98,"|",PRSUSFR,*110,"| ",XXSREAS:
                    *132,"|":
                    *N,*1,"|",*12,XXGNAME,*32,"|",*39,"|",*87,"|",*98,"|":
                    PRSUSTO,*110,"|",*132,"|"
.
          ADD       TWO,CLNO
.
          CALL      PCOM0000                      * print W/L comments
          CALL      UND132
          GOTO      REPT1000
.
REPT9000  PRINT     " ",*N:
                    "NOTE: (i) The Report displays in Suspension To Date Sequence.": 
                    *N,*N,"*** End of Report ***",*N
          CALL      KILL0000                      * erase temp file
.
REPT9999  RETURN
+
.*********************************************************************
.*  FDAT0000  :  Format data for printing                            *
.*********************************************************************
FDAT0000  PACK      WPDESC,SP30,SP30,SP10
          PACK      KEY9,XXPROC
          CALL      RDPROA1
          MOVE      WPDESC,DESC44
.
          UNPACK    XXDLIST,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDLIST
.
          UNPACK    XXDSUSFR,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRSUSFR
.
          UNPACK    XXDSUSTO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRSUSTO
.
FDAT9999  RETURN
+
.*********************************************************************
.*  PCOM0000  :  Print W/L comments                                  *
.*********************************************************************
PCOM0000  PACK      KEY21,XXURNO,XXPROC,XXPCNT,SP3
          CALL      RDSMESA1
PCOM1000  CALL      RDKMESA1
          BRANCH    OVRCD,PCOM9999
.
          PACK      KEY19,WAURNO,WAPROC,WACNT
          MATCH     KEY19,KEY21
          GOTO      PCOM9999 IF NOT EQUAL
.
          PRINT     *1,"|",*32,"|",*39,"| ",WATEXT,*132,"|"
          ADD       ONE,CLNO
          GOTO      PCOM1000
.
PCOM9999  RETURN
+
.*********************************************************************
.         Print the page header
.*********************************************************************
HEAD0000  CALL      HEAD132
          PRINT     *40,"Consultant: ",DOCCODE,SP3,DOCNAME:
                    *N,*40,"As at date: ",PRASATD,*N
.
          CALL      UND132
          PRINT     *1,"| U/R No.  Surname",*32,"| List | Procedure ":
                    "Description",*87,"| Date",*98,"| Date Susp",*110:
                    "| Reason",*132,"|":
                    *N,"|          Given Name",*32,"| Type | Comments":
                    *87,"| on List",*98,"| From/To",*110,"|",*132,"|"
          CALL      UND132
                   
          MOVE      "8",CLNO
HEAD9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temporary file                                    *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*  CRET0000  :  create temporary file                                   *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 106 U79-86,9-16,17-25,26-27,28-29",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TFILNAME
CRET9999  RETURN
+
.*************************************************************************
.*  Temp file IO's                                                       *
.*************************************************************************
RSTEMP1   RESET     KEY29
          READ      TEMPFILE,KEY29;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXDLIST,XXURNO,XXPROC,XXPCNT,XXSCNT,XXGNAME:
                             XXSNAME,XXLTYPE,XXDSUSFR,XXDSUSTO,XXSREAS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY29
          WRITE     TEMPFILE,KEY29;XXDLIST,XXURNO,XXPROC,XXPCNT,XXSCNT,XXGNAME:
                             XXSNAME,XXLTYPE,XXDSUSFR,XXDSUSTO,XXSREAS
          RETURN
.
.
          INC       STD001IO                * standard routine
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * codes file
          INC       PATMA1IO/INC            * codes file
          INC       WATPROIO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WATSUSIO/INC
          INC       WATTR1IO/INC
          INC       WATMESIO/INC
          INC       PATDOCKY
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC

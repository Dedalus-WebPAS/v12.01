.******************************************************************************
.* System         : Patient Management                                        *
.* Program        : IBAADT57.PBL                                              *
.* Name           : Revenue Analysis by Doctor                                *
.******************************************************************************
.* Date           : 31/12/2008                                                *
.* Authors        : J.Tan                                                     *
.* Function       : Calculates the 'Revenue Analysis by Doctor' for a specific*
.*                  date range.                                               *
.* Modifications  :                                                           *
.*        V12.00.01 15.05.2025  DA SArkies        TSK 0955096                 *
.*                  Updated for Alpha-Numeric Visit Number                    *
.*        V10.06.01 15/04/2015 Ania P         CAR 261630                      *
.*                  Removed use of temporary file name                        *
.*        V10.03.01 25/02/2013 Ania P         CAR 281021                      *
.*                  Removed unnecessary modification of constants.            *
.*        V10.01.01 16/02/2011  J.Tan         CAR 236997                      *
.*                  Added hidden option 5 to print patient invoice details    *
.*        V10.00.02 08/09/2010  Mike Laming   CAR 229678                      *
.*                  Changed all FORM 7.2 to FORM 9.2 and adjusted all Print Lns
.*        V10.00.01 02/09/2010  Mike Laming   CAR 221943                      *
.*                  Added PMSVX1FD to read "Referring GP" from PMVXRHC1 if    *
.*                  ADOCTR is blank - also use PMVXDOCA if ADOCTA is blank    *
.*        V9.12.01  13/10/2009  J.Tan         CAR 207556                      *
.*                  Added option to group by claim code                       *
.*        V9.11.01  12/02/2009  J.Tan         CAR 166453                      *
.*                  Mods to calculation of admissions and bed days            *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATDTRFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATINPFD/INC
          INC       PATINVFD/INC
          INC       PMSHCPFD/INC
          INC       PATTRNFD/INC
          INC       PMSFCIFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC

+
.               WORK AREA
.
ASATDATE  DIM       16
CMDLINE   DIM       80
CURDTE8   DIM       8
CALDAYS   FORM      5
DOCTNAME  DIM       40
DCASEFLG  FORM      1
ENDDTE8   DIM       8                       * End date (ccyymmdd)
FNAMEA    DIM       8
FNAMEI    DIM       8
FNAMEX    DIM       8
..FORM5     FORM      5
FORM5A    FORM      5
FORM5B    FORM      5
FORM5C    FORM      5
FORM5P2A  FORM      5.2
FORM5P2B  FORM      5.2
..FORM9P2   FORM      9.2
FORM9P2A  FORM      9.2
FORM9P2B  FORM      9.2
FORM9P2C  FORM      9.2
FORM9P2D  FORM      9.2
FORM9P2E  FORM      9.2
FROMDATE  DIM       8
ISERASE   INIT      "iserase "
NBRDAYS   FORM      5
REPFLAG   FORM      1
RECFLAG   FORM      1
STRTDTE8  DIM       8                       * Start date (ccyymmdd)
TODATE    DIM       8
TOTDAYS   FORM      5
TOTDCAS   FORM      5
TOTIAMT   FORM     12.2
.
TEMP4     IFILE     SQL,FIXED=9, KEY="U1-8"
.Name     Type      Length    Physical  Start    Description
.----     ----      ------    --------  -----    -----------
XXADMNUM  DIM       8             8         1    Admission number
.EOR                                        9
.
.
TEMP1     IFILE     SQL,FIXED=85, KEY="U1-1,12-14,2-11"
.
TEMP2     IFILE     SQL,FIXED=85, KEY="U1-1,18-20,2-11,15-17"
TEMP3     IFILE     SQL,FIXED=85, KEY="U1-1,12-14,15-17"
.
.Name     Type      Length    Physical  Start    Description
.----     ----      ------    --------  -----    -----------
XRECTYPE  DIM       1            1        1      Record Type
.                                                0-invoiced 1=tobe invoiced
XDOCCODE  DIM       10          10        2      Doctor Code
XDOCTYPE  DIM       3            3       12      Doctor Type
XADMTYPE  DIM       3            3       15      Admission type
XCLMCODE  DIM       3            3       18      Claim Code
XNUMADMN  FORM      8            8       21      Number of Admissions overnight
XBEDDAYS  FORM      8            8       29      Bed days
XNUMDCAS  FORM      8            8       37      Number of Daycases
XAMTDCAS  FORM      12.2         8       45      Daycase Accomomodation charges
XAMTWARD  FORM      12.2         8       53      Overnight Accommodation charges
XAMTTHEA  FORM      12.2         8       61      Theatre Charges
XAMTMISC  FORM      12.2         8       69      Miscellaneous charges
XAMTLUMP  FORM      12.2         8       77      Lumpsum amount
.EOR                                     85
.
XNETTOTS  FORM      9.2
XALOSDAY  FORM      9.2
XAVERDAY  FORM      9.2
.
GRPCLAIM  FORM      1
GTOTFLAG  FORM      1
GNUMADMN  FORM      5
GBEDDAYS  FORM      5
GNUMDCAS  FORM      5
GAMTDCAS  FORM      9.2
GAMTWARD  FORM      9.2
GAMTTHEA  FORM      9.2
GAMTMISC  FORM      9.2
GAMTLUMP  FORM      9.2
GNETTOTS  FORM      9.2
GALOSDAY  FORM      9.2
GAVERDAY  FORM      9.2
.
STATFLAG  FORM      1
SDAYCASE  FORM      1
SNUMADMN  FORM      8
SBEDDAYS  FORM      5
SAMTDCAS  FORM      9.2
SAMTWARD  FORM      9.2
SAMTTHEA  FORM      9.2
SAMTMISC  FORM      9.2
SAMTLUMP  FORM      9.2
SRECTYPE  DIM       1
.
SAVCCODE  DIM       3
SAVDCODE  DIM       10
SAVDTYPE  DIM       3
SAVATYPE  DIM       3
.
CNUMADMN  FORM      5
CBEDDAYS  FORM      5
CNUMDCAS  FORM      5
CAMTDCAS  FORM      9.2
CAMTWARD  FORM      9.2
CAMTTHEA  FORM      9.2
CAMTMISC  FORM      9.2
CAMTLUMP  FORM      9.2
CNETTOTS  FORM      9.2
CALOSDAY  FORM      9.2
CAVERDAY  FORM      9.2
.
TNUMADMN  FORM      5
TBEDDAYS  FORM      5
TNUMDCAS  FORM      5
TAMTDCAS  FORM      9.2
TAMTWARD  FORM      9.2
TAMTTHEA  FORM      9.2
TAMTMISC  FORM      9.2
TAMTLUMP  FORM      9.2
TNETTOTS  FORM      9.2
TALOSDAY  FORM      9.2
TAVERDAY  FORM      9.2
.
. ----- Program Constants -----
.
PRGID     INIT      "IBAADT57"
PRGNAM    INIT      "Revenue Analysis by Doctor"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000
          GOTO      ML9999
.
ML2000    CALL      DATE0000                * Keyin the invoice date range
          BRANCH    EXIT,ML1000
          CALL      REPT0000                * Keyin Report type
          BRANCH    EXIT,ML1000
.
          MOVE      ZERO,GRPCLAIM
          IF        COPTION=3 | COPTION=4
            CALL      GCLM0000              * Group by Claim code?
            BRANCH    EXIT,ML1000
          ENDIF
.
          CALL      OKTC0000                * Ok to Continue? (Y/N)
          BRANCH    EXIT,ML1000             * No
.
          CALL      CRTF0000                * Create and open Tempfile
          CALL      CLER0000                * Clear all records of tempfiles
          CALL      RINV0000                * Processing invoiced
          CALL      TINV0000                * Processing tobe invoiced
.
          CALL      PRNT0000                * Print Report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000                                 *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                   * call display head routine
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATINPA1,"patinpaf"
          OPEN      PATINVR2,"patinvrf"
          OPEN      PATDTRA4,"patdtraf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSFCIA1,"pmsfciaf"
.
          DISPLAY   *P64:24,"pmsvx1af";  
          OPEN      PMSVX1A1,"pmsvx1af" 
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEA
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEX
.
          PACK      CURDTE8,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE8
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Revenue Analysis by Doctor":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN1,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                        Keyin The Invoice Date Range                        *
.******************************************************************************
DATE0000  MOVE      "21",CCOL
          MOVE      "12",CVERT
          DISPLAY   *P1:CVERT,*EF,"Starting Date     : ";
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,"Ending Date       : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
          MOVE      ZERO,CHIGHLT            * Use highlight
.
. ----- Enter starting date -----
.
DATE1000  SUB       ONE,CVERT
DATE2000  MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500
          BRANCH    CQUEST,DATE2000
.
          PACK      STRTDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE8
.
          MATCH     STRTDTE8,CURDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Start date must not be after the current ":
                      "date.  ";
            GOTO      DATE2000
          ENDIF
.
. ----- Enter ending date -----
.
          ADD       ONE,CVERT
DATE3000  UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000
          BRANCH    CQUEST,DATE3000
.
          PACK      ENDDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE8
.
          MATCH     ENDDTE8,CURDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must not be after the current ":
                      "date.  ";
            GOTO      DATE3000
          ENDIF
.
          MATCH     STRTDTE8,ENDDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must be after the start date.  ";
            GOTO      DATE3000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  REPT0000              Called by: ML0000   *
.*                        Keyin The Report Type                               *
.******************************************************************************
REPT0000  MOVE      ZERO,COPTION
          DISPLAY   *P1:15,*V2LON,"0",*HOFF," = Exit":
                    *P1:16,*V2LON,"1",*HOFF," = By Attending Doctor":
                    *P1:17,*V2LON,"2",*HOFF," = By Referring Doctor":
                    *P1:18,*V2LON,"3",*HOFF," = Details By Attending Doctor":
                    *P1:19,*V2LON,"4",*HOFF," = Details By Referring Doctor":
                    *P1:20,*V2LON,"5",*HOFF," = Patient Details By Att.Doctor":
                    *P1:22,"Select Option : ";
.
REPT1000  KEYIN     *P17:22,*V2LON,*DV,UNDLN1,*P17:22,COPTION;
.
          COMPARE   ZERO,COPTION
          GOTO      REPT9000 IF EQUAL
.
          BRANCH    COPTION,REPT9200,REPT9400,REPT9200,REPT9400,REPT9200
          GOTO      REPT1000
.
REPT9000  MOVE      ONE,EXIT
          GOTO      REPT9999
.
REPT9200  MOVE      "- By Attending Doctor",CPHDROPT
          GOTO      REPT9800
.
REPT9400  MOVE      "- By Referring Doctor",CPHDROPT
.
REPT9800  MOVE      ZERO,EXIT
REPT9999  RETURN
+
.******************************************************************************
.         Group by Claim code
.******************************************************************************
GCLM0000  MOVE      ZERO,EXIT
          MOVE      SP70,ANS
          DISPLAY   *P1:22,*EL,"Group by Claim Code : ";
          KEYIN     *P24:22,ANS;
          REP       "nNyY",ANS
          CMATCH    "Y",ANS
          GOTO      GCLM2000 IF EQUAL
          CMATCH    "N",ANS
          GOTO      GCLM9999 IF EQUAL
          MOVE      ONE,EXIT
          GOTO      GCLM9999
.
GCLM2000  MOVE      ONE,GRPCLAIM
          GOTO      GCLM9999
GCLM9999  RETURN
+
.******************************************************************************
.*                                  OKTC0000                                  *
.*                               Ok to Continue?                              *
.******************************************************************************
OKTC0000  DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH:
                    *V2LON,ANSN,*HOFF,") ? ";
OKTC1000  KEYIN     *P24:24,*V2LON,*DV,UNDLN1,*P24:24,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSY,ANS
          GOTO      OKTC9000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OKTC9500 IF EQUAL
.
          BEEP
          GOTO      OKTC1000
.
OKTC9000  DISPLAY   *P1:22,*EF
          MOVE      ZERO,EXIT               * Yes
          GOTO      OKTC9999
.
OKTC9500  MOVE      ONE,EXIT                * No
OKTC9999  RETURN
+
.******************************************************************************
.*                                  CRTF0000              Called by: ML0000   *
.*                  Open and Create the Tempfile                              *
.******************************************************************************
CRTF0000  MOVE      ONE,EXIT
          BRANCH    COPTION,CRTF1000,CRTF1000,CRTF2000,CRTF2000,CRTF1000
          GOTO      CRTF9999
.
CRTF1000  CLOSE     TEMP1
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,FNAMEA
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    " 85 u1-1,12-14,2-11",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP1,FNAMEA
          GOTO      CRTF8000
.
CRTF2000  CLOSE     TEMP2
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,FNAMEA
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    " 85 u1-1,18-20,2-11,15-17",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP2,FNAMEA
.
          CLOSE     TEMP3
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,FNAMEI
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 85 u1-1,12-14,15-17",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP3,FNAMEI
.
CRTF8000  CLOSE     TEMP4
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,FNAMEX
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEX,CMDLINE
          APPEND    " 9 u1-8",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP4,FNAMEX
.
          MOVE      ZERO,EXIT
CRTF9999  RETURN
+
.******************************************************************************
.         Clear all records
.******************************************************************************
CLER0000  BRANCH    COPTION,CLER1000,CLER1000,CLER2000,CLER2000,CLER1000
          GOTO      CLER9999
.
CLER1000  MOVE      SP70,KEY14
          CALL      RSTEMP1
          CALL      RKTEMP1
          IF        OVRCD=0
            PACK      KEY14,XRECTYPE,XDOCTYPE,XDOCCODE,SP70
            CALL      DETEMP1
            GOTO      CLER1000
          ENDIF
          GOTO      CLER9999
.
CLER2000  MOVE      SP70,KEY17
          CALL      RSTEMP2
          CALL      RKTEMP2
          IF        OVRCD=0
            PACK      KEY17,XRECTYPE,XCLMCODE,XDOCCODE,XADMTYPE,SP70
            CALL      DETEMP2
            GOTO      CLER2000
          ENDIF
.
CLER3000  MOVE      SP70,KEY7
          CALL      RSTEMP3
          CALL      RKTEMP3
          IF        OVRCD=0
            PACK      KEY7,XRECTYPE,XDOCTYPE,XADMTYPE,SP70
            CALL      DETEMP3
            GOTO      CLER3000
          ENDIF
CLER9999  RETURN
+
.******************************************************************************
.         Delete admission tempfile
.******************************************************************************
DTMP0000  MOVE      SP70,KEY8
          CALL      RSTEMP4
          CALL      RKTEMP4
          IF        OVRCD=0
            PACK      KEY8,XXADMNUM,SP70
            CALL      DETEMP4
            GOTO      DTMP0000
          ENDIF
DTMP9999  RETURN
+
.******************************************************************************
.*                          RINV0000                                          *
.*            Read Invoice file for Invoiced in the period
.******************************************************************************
RINV0000  CALL      DTMP0000                * Clear admission tempfile
          MOVE      ZERO,TOTDAYS
          MOVE      ZERO,TOTDCAS
          MOVE      ZERO,TOTIAMT
          IF        COPTION=5
            CALL      IBACLOCK
            CALL      HEAD132
            CALL      UND80
            PRINT     *1,"| Invoice No",*15,"| Invoice Date":
                      *30,"| Admission No",*45,"|  Days",*53,"| DC":
                      *60,"| Invoice Amount",*80,"|"
            CALL      UND80
          ENDIF
.
          UNPACK    SP70,XCLMCODE,XDOCCODE,XDOCTYPE,XADMTYPE
          MOVE      "0",SRECTYPE            * record type
          PACK      KEY16,STRTDTE8,SP70
          CALL      RDSINV2
RINV1000  CALL      RDKINV2
          BRANCH    OVRCD,RINV9800
.
          MATCH     PINVDTE,ENDDTE8
          GOTO      RINV9800 IF LESS        * Out of range
.
          MATCH     "1",PTINCRST
          GOTO      RINV1000 IF EQUAL       * Full credit notes
.
          COMPARE   THREE,PINVSYS
          GOTO      RINV1000 IF NOT EQUAL   * Not inpatient
.
          MOVE      PINVADM,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,RINV1000
.
RINV2000  CALL      RDPMVX11                * read Visit ext'n record *I-221943
          BRANCH    OVRCD,RINV1000
.
          MOVE      SP70,DDATE
          IF        ASTAT=3
            CALL      RDDSCH1
            BRANCH    OVRCD,RINV1000
          ENDIF
.
          IF        COPTION=1 | COPTION=3 | COPTION=5
            MATCH     SP6,ADOCTA
            IF        @EQUAL
              PACK      XDOCCODE,PMVXDOCA,SP70   * Attending HCP
            ELSE
              PACK      XDOCCODE,ADOCTA,SP70     * Attending doctor
            ENDIF
          ELSE
            MATCH     SP6,ADOCTR
            IF        @EQUAL
              PACK      XDOCCODE,PMVXRHC1,SP70   * Referring HCP
            ELSE
              PACK      XDOCCODE,ADOCTR,SP70     * Referral doctor
            ENDIF
          ENDIF
.
RINV3000  PACK      KEY6,XDOCCODE,SP70                    * start     *C-221943
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DRTYPE,XDOCTYPE
          ELSE
            PACK      KEY10,XDOCCODE,SP70
            CALL      RDPMHCP1
            IF        OVRCD=0
              MOVE      PMHCSPC1,XDOCTYPE
            ENDIF
          ENDIF                                           * end       *C-221943
.
          MOVE      ATYPE,XADMTYPE
          MOVE      PINVCLM,XCLMCODE
.
          MOVE      ZERO,SNUMADMN
          MOVE      ZERO,SBEDDAYS         * no of bed days
          MOVE      ZERO,SDAYCASE
.
          MOVE      ZERO,SAMTDCAS
          MOVE      ZERO,SAMTWARD
          MOVE      ZERO,SAMTTHEA
          MOVE      ZERO,SAMTMISC
          MOVE      ZERO,SAMTLUMP
.
          MOVE      ZERO,DCASEFLG
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      ONE,DCASEFLG
          ENDIF
.
          MOVE      AADMNO,KEY8
          CALL      RDTEMP4
          COMPARE   ZERO,OVRCD
          GOTO      RINV9000 IF EQUAL  * admission already been added
          MOVE      AADMNO,XXADMNUM
          CALL      WRTEMP4
.
          BRANCH    DCASEFLG,RINV8000
.
.         Check if patient admitted prior or within the period
.
          MATCH     ADATE,ENDDTE8
          GOTO      RINV9000 IF LESS
.
          MOVE      ONE,SNUMADMN       * Number of patients admitted in period
          GOTO      RINV9000
.
RINV8000  MOVE      ONE,SDAYCASE
.
RINV9000  CALL      CALC0000           * Calculate charges
          UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          IF        COPTION=5
            PRINT     *1,"|",PINVNO,*15,"| ",CPCDATE:
                      *30,"|",PINVADM,*45,"|",SBEDDAYS,*53,"|    ",DCASEFLG:
                      *60,"|",PINVAMT,*80,"|"
            ADD       SBEDDAYS,TOTDAYS
            ADD       DCASEFLG,TOTDCAS
            ADD       PINVAMT,TOTIAMT
          ENDIF
          CALL      UFIL0000           * Update tempfile
          GOTO      RINV1000
.
RINV9800  IF        COPTION=5
            CALL      UND80
            PRINT     *1,"| Total : ":
                      *45,"|",TOTDAYS,*53,"|",TOTDCAS,*60,"|",TOTIAMT,*80,"|"
            CALL      UND80
          ENDIF
.
RINV9999  RETURN
+
.******************************************************************************
.*                          CALC0000                                          *
.*                      Calculate charges                                     *
.******************************************************************************
CALC0000  PACK      KEY23,PINVADM,PINVNO,SP70
          CALL      RDSDTRN4
CALC1000  CALL      RDKDTRN4
          BRANCH    OVRCD,CALC9999
.
          MATCH     TADMNO,PINVADM       * Check admission number
          GOTO      CALC9999 IF NOT EQUAL
.
          MATCH     TREF,PINVNO
          GOTO      CALC9999 IF NOT EQUAL
.
          ADD       PTDTGSTM,TPATAMTT
.
          MATCH     "1",PTDTCRST
          GOTO      CALC1000 IF EQUAL           * Fully credited
          MATCH     "2",PTDTCRST
          GOTO      CALC5000 IF NOT EQUAL
.
.         Partial credit notes
.
          PACK      KEY30,DTADMNO,TREF,TTRANSN1,SP70
          CALL      RSPMFCI1
CALC2000  CALL      RKPMFCI1
          BRANCH    OVRCD,CALC5000
.
          MATCH     PMFCVISN,DTADMNO
          GOTO      CALC5000 IF NOT EQUAL       * Different admission
.
          MATCH     PMFCINVN,TREF
          GOTO      CALC5000 IF NOT EQUAL       * Different invoice number
.
          MATCH     PMFCTRAN,DTTRANSN1
          GOTO      CALC5000 IF NOT EQUAL       * Different admission
.
          ADD       PMFCCAMT,TPATAMTT
          ADD       PMFCCGST,TPATAMTT
          GOTO      CALC2000
.
CALC5000  BRANCH    TRECTYPE,CALC6000,CALC7000,CALC8000
          GOTO      CALC1000
.
.         Accommodation record
.
CALC6000  IF        DCASEFLG=1
            ADD       TPATAMTT,SAMTDCAS      * charges for daycase
          ELSE
            ADD       TPATAMTT,SAMTWARD      * charges for overnight
          ENDIF
          ADD       PTDTLSPT,SAMTLUMP        * Lumpsum amount
          ADD       PTDTLSRB,SAMTLUMP
          ADD       PTDTGSTL,SAMTLUMP
          IF        DCASEFLG<>1
            ADD       TNODAYS,SBEDDAYS       * bed days for overnight
          ENDIF
          GOTO      CALC1000
.
CALC7000  ADD       TPATAMTT,SAMTTHEA        * Theatre charges
          GOTO      CALC1000
.
CALC8000  ADD       TPATAMTT,SAMTMISC        * Miscellaneous charges
          GOTO      CALC1000
.
CALC9999  RETURN
+
.******************************************************************************
.*                          TINV0000                                          *
.*            Read Invoice Pending file for accrued not invoiced
.******************************************************************************
TINV0000  CALL      DTMP0000                * Clear admission tempfile
          MOVE      SP70,ASATDATE
          UNPACK    SP70,XCLMCODE,XDOCCODE,XDOCTYPE,XADMTYPE
          MOVE      "1",SRECTYPE            * Record type
          PACK      KEY16,SP70
          CALL      RSPTINP1
TINV1000  CALL      RKPTINP1
          BRANCH    OVRCD,TINV9999
.
          MOVE      PTIPADMN,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,TINV1000
.
TINV2000  CALL      RDPMVX11                * read Visit ext'n record *I-221943
          BRANCH    OVRCD,TINV1000
.
.  Skip patient who admitted after the end date
.
          MATCH     ADATE,ENDDTE8
          GOTO      TINV1000 IF LESS
.
          MOVE      SP70,DDATE
          IF        ASTAT=3
            CALL      RDDSCH1
            BRANCH    OVRCD,TINV1000
          ENDIF
          PACK      ASATDATE,PTIPCDAT,PTIPCTIM,SP70
.
          IF        COPTION=1 | COPTION=3 | COPTION=5
            MATCH     SP6,ADOCTA
            IF        @EQUAL
              PACK      XDOCCODE,PMVXDOCA,SP70   * Attending HCP
            ELSE
              PACK      XDOCCODE,ADOCTA,SP70     * Attending doctor
            ENDIF
          ELSE
            MATCH     SP6,ADOCTR
            IF        @EQUAL
              PACK      XDOCCODE,PMVXRHC1,SP70   * Referring HCP
            ELSE
              PACK      XDOCCODE,ADOCTR,SP70     * Referral doctor
            ENDIF
          ENDIF
.
TINV3000  PACK      KEY6,XDOCCODE,SP70
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DRTYPE,XDOCTYPE
          ELSE
            PACK      KEY10,XDOCCODE,SP70
            CALL      RDPMHCP1
            IF        OVRCD=0
              MOVE      PMHCSPC1,XDOCTYPE
            ENDIF
          ENDIF  
.
          MOVE      ATYPE,XADMTYPE
          MOVE      ACLAIM,XCLMCODE
.
          MOVE      ZERO,SNUMADMN
          MOVE      ZERO,SBEDDAYS         * no of bed days
          MOVE      ZERO,SDAYCASE
.
          MOVE      ZERO,SAMTDCAS
          MOVE      ZERO,SAMTWARD
          MOVE      ZERO,SAMTTHEA
          MOVE      ZERO,SAMTMISC
          MOVE      ZERO,SAMTLUMP
.
          MOVE      ZERO,DCASEFLG
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      ONE,DCASEFLG
          ENDIF
.
          MOVE      ZERO,DCASEFLG
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      ONE,DCASEFLG
          ENDIF
.
          MOVE      AADMNO,KEY8
          CALL      RDTEMP4
          COMPARE   ZERO,OVRCD
          GOTO      TINV9000 IF EQUAL  * admission already been added
          MOVE      AADMNO,XXADMNUM
          CALL      WRTEMP4
.
          BRANCH    DCASEFLG,TINV8000
.
.         Check if patient admitted prior or within the period
.
          MATCH     ADATE,ENDDTE8
          GOTO      TINV9000 IF LESS
.
          MOVE      ONE,SNUMADMN       * Number of patients admitted in period
          GOTO      TINV9000
.
TINV8000  MOVE      ONE,SDAYCASE
.
TINV9000  CALL      SFLD0000           * Set charges
          CALL      UFIL0000           * Update tempfile
          GOTO      TINV1000
.
TINV9999  RETURN
+
.******************************************************************************
.         Set charges
.******************************************************************************
SFLD0000  BRANCH    PTIPTTYP,SFLD1000,SFLD2000,SFLD3000
          GOTO      SFLD9999
.
.         Accommodation record
.
SFLD1000  IF        DCASEFLG=1
            MOVE      PTIPAREV,SAMTDCAS      * charges for daycase
          ELSE
            MOVE      PTIPAREV,SAMTWARD      * charges for overnight
          ENDIF
          MOVE      PTIPPPOR,SAMTLUMP        * Lumpsum amount
          ADD       PTIPRPOR,SAMTLUMP
          IF        DCASEFLG<>1
            ADD       PTIPBDAY,SBEDDAYS      * bed days for overnight
          ENDIF
          GOTO      SFLD9999
.
.         Theatre record
.
SFLD2000  MOVE      PTIPTREV,SAMTTHEA        * Theatre charges
          GOTO      SFLD9999
.
.         Miscellaneous record
.
SFLD3000  MOVE      PTIPMREV,SAMTMISC        * Miscellaneous charges
          ADD       PTIPPREV,SAMTMISC
          ADD       PTIPAREV,SAMTMISC
          ADD       PTIPIREV,SAMTMISC
          ADD       PTIPTREV,SAMTMISC
          ADD       PTIPHREV,SAMTMISC
          ADD       PTIPLREV,SAMTMISC
SFLD9999  RETURN
+
.******************************************************************************
.         Write/Update the tempfile
.******************************************************************************
UFIL0000  BRANCH    COPTION,UFIL1000,UFIL1000
          COMPARE   FIVE,COPTION
          GOTO      UFIL1000 IF EQUAL
          GOTO      UFIL2000
.
.         Index on Doctor type and Doctor code
.
UFIL1000  PACK      KEY14,SRECTYPE,XDOCTYPE,XDOCCODE,SP70
          CALL      RDTEMP1
          CALL      SVAR0000
          IF        OVRCD=1
            UNPACK    KEY14,XRECTYPE,XDOCTYPE,XDOCCODE
            CALL      WRTEMP1
          ELSE
            CALL      UPTEMP1
          ENDIF
          GOTO      UFIL9999
.
.         Index on Doctor code and Admission type
.
UFIL2000  PACK      KEY17,SRECTYPE,XCLMCODE,XDOCCODE,XADMTYPE,SP70
          CALL      RDTEMP2
          CALL      SVAR0000
          IF        OVRCD=1
            UNPACK    KEY17,XRECTYPE,XCLMCODE,XDOCCODE,XADMTYPE
            CALL      WRTEMP2
          ELSE
            CALL      UPTEMP2
          ENDIF
.
.         Index on Doctor type and Admission type
.
          PACK      KEY7,SRECTYPE,XDOCTYPE,XADMTYPE,SP70
          CALL      RDTEMP3
          CALL      SVAR0000
          IF        OVRCD=1
            UNPACK    KEY7,XRECTYPE,XDOCTYPE,XADMTYPE
            CALL      WRTEMP3
          ELSE
            CALL      UPTEMP3
          ENDIF
UFIL9999  RETURN
+
.******************************************************************************
.         Setup variables
.******************************************************************************
SVAR0000 IF         OVRCD=1
            MOVE      SNUMADMN,XNUMADMN
            MOVE      SBEDDAYS,XBEDDAYS
            MOVE      SDAYCASE,XNUMDCAS
.
            MOVE      SAMTDCAS,XAMTDCAS
            MOVE      SAMTWARD,XAMTWARD
            MOVE      SAMTTHEA,XAMTTHEA
            MOVE      SAMTMISC,XAMTMISC
            MOVE      SAMTLUMP,XAMTLUMP
         ELSE
            ADD       SNUMADMN,XNUMADMN
            ADD       SBEDDAYS,XBEDDAYS
            ADD       SDAYCASE,XNUMDCAS
.
            ADD       SAMTDCAS,XAMTDCAS
            ADD       SAMTWARD,XAMTWARD
            ADD       SAMTTHEA,XAMTTHEA
            ADD       SAMTMISC,XAMTMISC
            ADD       SAMTLUMP,XAMTLUMP
          ENDIF
SVAR9999  RETURN
+
.******************************************************************************
.         Print Report
.******************************************************************************
PRNT0000  MOVE      "60",CLNO
          MOVE      ZERO,GTOTFLAG
          BRANCH    COPTION,PRNT1000,PRNT1000,PRNT4000,PRNT4000,PRNT1000
          GOTO      PRNT9999
.
PRNT1000  MOVE      ZERO,REPFLAG
          MOVE      "0",SRECTYPE     * Invoiced
          CALL      PDAT0000         * Print without details
.
          MOVE      "1",SRECTYPE     * tobe invoiced
          CALL      PDAT0000         * Print without details
          GOTO      PRNT9000
.
PRNT4000  MOVE      ONE,REPFLAG
          MOVE      "0",SRECTYPE     * Invoiced
          CALL      PDET0000         * Print with details by doctor code
          MOVE      "1",SRECTYPE     * tobe invoiced
          CALL      PDET0000         * Print with details by doctor code
.
          MOVE      TWO,REPFLAG
          MOVE      "0",SRECTYPE     * Invoiced
          CALL      TDET0000         * Print with details by doctor type
          MOVE      "1",SRECTYPE     * tobe invoiced
          CALL      TDET0000         * Print with details by doctor type
.
PRNT9000  COMPARE   ZERO,CPAGENO
          GOTO      PRNT9800 IF EQUAL
.
          COMPARE   "35",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *N,*1,"*** End of Report ***":
                    *N,*N,"Admns":
                    *12,"Number of admissions that make up the revenue ":
                        "reported. This will include patients admitted prior ":
                        "to or ":
                    *N,*12,"within the period.":

                    *N,"Bed Days":
                       *12,"Number of inpatient bed days excluding day cases ":
                        "for the revenue reported. This will be the LOS since ":
                        "the last ":
                    *N,*12,"accommodation date for each patient invoiced":
                        " in the period.":

                    *N,"Day Cases":
                       *12,"Number of daycases that make up the revenue ":
                        "reported. This will include patients admitted prior ":
                        "to or within ":
                    *N,*12,"the period and invoiced in the period.":

                    *N,"$ Daycases":
                       *12,"Bed charges accumulated for daycase patients ":
                       "invoiced during the period including discounts and ":
                       "allowances, ":
                    *N,*12,"excluding casepayments.":

                    *N,"$ Ward":
                       *12,"Bed charges accumulated for non-daycase patients ":
                       "invoiced during the period including discounts ":
                       "and allowances.":

                    *N,"$ Theatre":
                       *12,"Theatre charges accumulated for patients invoiced ":
                           "during the period.":

                    *N,"$ Misc":
                       *12,"Miscellaneous charges accumulated for patients ":
                           "invoiced during the period.":

                    *N,"$ Lumpsum":
                       *12,"Includes both daycase and Ward lumpsum amounts ":
                           "invoiced during the period.":

                    *N,"$ Net":
                       *12," $ Daycases + $ Ward + $ Theatre + $ Misc of the ":
                           "values reported.":

                    *N,*1,"ALOS Days":
                       *12,"(Bed Days + Day Cases) / Admns":
                    *N,*1,"Av/Day":
                       *12,"$ Net/ (Bed Days + Day Cases)"
          GOTO      PRNT9999
.
PRNT9800  PRINT     *N,*1,"*** No Report Generated ***"
PRNT9999  RETURN
+
.******************************************************************************
.         Print Attending/Referring Doctor without details
.******************************************************************************
PDAT0000  MOVE      "60",CLNO
          MOVE      SP70,SAVCCODE
          MOVE      SP70,SAVDCODE
          MOVE      SP70,SAVDTYPE
          MOVE      SP70,SAVATYPE
          MOVE      ZERO,RECFLAG
          CALL      INTVAR00
          MOVE      ZERO,F1
          PACK      KEY14,SRECTYPE,SP70
          CALL      RDTEMP1
          COMPARE   ZERO,OVRCD
          GOTO      PDAT1200 IF EQUAL
.
          PACK      KEY14,SRECTYPE,SP70
          CALL      RSTEMP1
PDAT1000  CALL      RKTEMP1
          BRANCH    OVRCD,PDAT8000
.
PDAT1200  MATCH     SRECTYPE,XRECTYPE
          GOTO      PDAT8000 IF NOT EQUAL     * different record type
.
          COMPARE   ZERO,RECFLAG
          GOTO      PDAT3000 IF EQUAL
.
          MATCH     SAVDTYPE,XDOCTYPE       * Check doctor type
          GOTO      PDAT3000 IF EQUAL
.
          CLEAR     KEY18
          APPEND    "UNK Doc.Type-",KEY18
          APPEND    SAVDTYPE,KEY18
          APPEND    SP70,KEY18
          RESET     KEY18
.
          MOVE      ZERO,F1
          MATCH     SP70,SAVDTYPE
          IF        !@EQUAL
            PACK      KEY5,ANSD,ANST,SAVDTYPE,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,KEY18
            ENDIF
          ENDIF
          CALL      UND132
          ADD       ONE,CLNO
.
          MATCH     SP70,SAVDTYPE
          IF        @EQUAL
            PRINT     *1,"TOTAL FOR UNKNOWN CODE";
          ELSE
            PRINT     *1,"TOTAL:",KEY18;
          ENDIF
          CALL      PTOT0000                * Print total
.
PDAT3000  COMPARE   "52",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      XDOCTYPE,SAVDTYPE
          CLEAR     KEY26
          APPEND    "Unknown Doctor: ",KEY26
          APPEND    XDOCCODE,KEY26
          APPEND    SP70,KEY26
          RESET     KEY26
.
          PACK      KEY10,XDOCCODE,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,KEY26
          ENDIF
          CALL      PLIN0000
          MOVE      ONE,F1
          MOVE      ONE,RECFLAG
          GOTO      PDAT1000
.
PDAT8000  IF        F1=1
            CLEAR     KEY18
            APPEND    "UNK Doc.Type-",KEY18
            APPEND    SAVDTYPE,KEY18
            APPEND    SP70,KEY18
            RESET     KEY18
.
            MOVE      ZERO,F1
            MATCH     SP70,SAVDTYPE
            IF        !@EQUAL
              PACK      KEY5,ANSD,ANST,SAVDTYPE,SP70
              CALL      RDCODE1
              IF        OVRCD=0
                MOVE      TDESC,KEY18
              ENDIF
            ENDIF
.
            CALL      UND132
            ADD       ONE,CLNO
            MATCH     SP70,SAVDTYPE
            IF        @EQUAL
              PRINT     *1,"TOTAL FOR UNKNOWN CODE";
            ELSE
              PRINT     *1,"TOTAL:",KEY18;
            ENDIF
            CALL      PTOT0000           * Print Sub total
          ENDIF
          COMPARE   ZERO,RECFLAG
          GOTO      PDAT9999 IF EQUAL
          CALL      GTOT0000             * Print Grand total
PDAT9999  RETURN
+
.******************************************************************************
.         Print Attending/Referring doctor details by doctor code
.******************************************************************************
PDET0000  MOVE      "60",CLNO
          MOVE      SP70,SAVCCODE
          MOVE      SP70,SAVDCODE
          MOVE      SP70,SAVDTYPE
          MOVE      SP70,SAVATYPE
          MOVE      ZERO,RECFLAG
          MOVE      ZERO,F1
          CALL      INTVAR00
          PACK      KEY17,SRECTYPE,SP70
          CALL      RDTEMP2
          COMPARE   ZERO,OVRCD
          GOTO      PDET1200 IF EQUAL
.
          PACK      KEY17,SRECTYPE,SP70
          CALL      RSTEMP2
PDET1000  CALL      RKTEMP2
          BRANCH    OVRCD,PDET8000
PDET1200  MATCH     SRECTYPE,XRECTYPE
          GOTO      PDET8000 IF NOT EQUAL      * different record type
.
          IF        GRPCLAIM=0
            COMPARE   ZERO,RECFLAG
            GOTO      PDET2000 IF EQUAL
            GOTO      PDET1600
          ENDIF
.
          COMPARE   ZERO,RECFLAG
          GOTO      PDET1400 IF EQUAL
.
          MATCH     XCLMCODE,SAVCCODE
          GOTO      PDET1600 IF EQUAL          * different claim code
.
          CALL      UND132
          MATCH     SP70,SAVDCODE
          IF        @EQUAL
            PRINT     *2,"  TOTAL FOR UNKNOWN CODE";
          ELSE
            PRINT     *2,"  TOTAL FOR ",SAVDCODE;
          ENDIF
          CALL      PTOT0000
.
          CALL      UND132
          PRINT     *1,"  SUB-TOTAL ";
          CALL      CTOT0000
.
PDET1400  CALL      PCLM0000                    * Print claim code
          GOTO      PDET2000
.
PDET1600  MATCH     XDOCCODE,SAVDCODE
          GOTO      PDET4000 IF EQUAL          * different doctor code
.
          CALL      UND132
          MATCH     SP70,SAVDCODE
          IF        @EQUAL
            PRINT     *2,"  TOTAL FOR UNKNOWN CODE";
          ELSE
            PRINT     *2,"  TOTAL FOR ",SAVDCODE;
          ENDIF
          CALL      PTOT0000
.
PDET2000  MOVE      ZERO,F1
          CALL      PDOC0000                  * Print doctor name
.
          IF        CPAGENO=0
            CALL      HEAD0000
          ELSE
            COMPARE   "48",CLNO
            CALL      HEAD0000 IF NOT LESS
          ENDIF
          PRINT     *2,DOCTNAME
          CALL      UND132
          ADD       TWO,CLNO
          MOVE      ONE,RECFLAG
.
PDET4000  COMPARE   "52",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      XDOCCODE,SAVDCODE
          CLEAR     KEY26
          APPEND    "  Invalid Adm.Type-",KEY26
          APPEND    XADMTYPE,KEY26
          APPEND    SP70,KEY26
          RESET     KEY26
.
          MATCH     SP70,XADMTYPE
          IF        !@EQUAL
            PACK      KEY5,ANSA,SP1,XADMTYPE
            CALL      RDCODE1
            IF        OVRCD=0
              PACK      KEY26,SP2,TDESC,SP70
            ENDIF
          ENDIF
          CALL      PLIN0000            * Print record
          MOVE      ONE,F1
          GOTO      PDET1000
.
PDET8000  IF        F1=1
            CALL      UND132
            MATCH     SP70,SAVDCODE
            IF        @EQUAL
              PRINT     *2,"  TOTAL FOR UNKNOWN CODE";
            ELSE
              PRINT     *2,"  TOTAL FOR ",SAVDCODE;
            ENDIF
            CALL      PTOT0000
.
            IF        GRPCLAIM=1
              CALL      UND132
              PRINT     *1,"  SUB-TOTAL ";
              CALL      CTOT0000
            ENDIF
          ENDIF
.
          COMPARE   ZERO,RECFLAG
          GOTO      PDET9999 IF EQUAL
          CALL      GTOT0000             * Print Grand total
PDET9999  RETURN
+
.******************************************************************************
.         Print Claim code
.******************************************************************************
PCLM0000
          PACK      KEY5,ANSC,ANSL,XCLMCODE,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
          IF        CPAGENO=0
            CALL      HEAD0000
          ELSE
            COMPARE   "48",CLNO
            CALL      HEAD0000 IF NOT LESS
          ENDIF
          PRINT     *1,"Claim Code: ",XCLMCODE," - ",TDESC
          ADD       ONE,CLNO
          MOVE      XCLMCODE,SAVCCODE          * Save claim code
.
PCLM9888  RETURN
+
.******************************************************************************
.         Print Doctor name
.******************************************************************************
PDOC0000
          MOVE      SP70,DOCTNAME
          CLEAR     DOCTNAME
          APPEND    "Unknown Doctor: ",DOCTNAME
          APPEND    XDOCCODE,DOCTNAME
          APPEND    SP70,DOCTNAME
          RESET     DOCTNAME
.
          PACK      KEY10,XDOCCODE,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,DOCTNAME
          ENDIF
PDOC9999  RETURN
+
.******************************************************************************
.         Print Attending/Referring doctor details by doctor type
.******************************************************************************
TDET0000  MOVE      "60",CLNO
          MOVE      SP70,SAVDCODE
          MOVE      SP70,SAVDTYPE
          MOVE      SP70,SAVATYPE
          MOVE      ZERO,RECFLAG
          MOVE      ZERO,F1
          CALL      INTVAR00
          PACK      KEY7,SRECTYPE,SP70
          CALL      RDTEMP3
          COMPARE   ZERO,OVRCD
          GOTO      TDET1200 IF EQUAL
.
          PACK      KEY7,SRECTYPE,SP70
          CALL      RSTEMP3
TDET1000  CALL      RKTEMP3
          BRANCH    OVRCD,TDET8000
TDET1200  MATCH     SRECTYPE,XRECTYPE
          GOTO      TDET8000 IF NOT EQUAL      * different record type
.
          COMPARE   ZERO,RECFLAG
          GOTO      TDET2000 IF EQUAL
.
          MATCH     XDOCTYPE,SAVDTYPE
          GOTO      TDET4000 IF EQUAL
.
          CALL      UND132
          MATCH     SP70,SAVDTYPE
          IF        @EQUAL
            PRINT     *2,"  TOTAL FOR UNKNOWN CODE";
          ELSE
            PRINT     *2,"  TOTAL FOR ",SAVDTYPE;
          ENDIF
          CALL      PTOT0000
.
TDET2000  MOVE      ZERO,F1
          CLEAR     KEY20
          APPEND    "UNK Doct.Type-",KEY20
          APPEND    XDOCTYPE,KEY20
          APPEND    SP70,KEY20
          RESET     KEY20
.
          MATCH     SP70,XDOCTYPE
          IF        !@EQUAL
            PACK      KEY5,ANSD,ANST,XDOCTYPE,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              PACK      KEY20,TDESC,SP70
            ENDIF
          ENDIF
.
          IF        CPAGENO=0
            CALL      HEAD0000
          ELSE
            COMPARE   "48",CLNO
            CALL      HEAD0000 IF NOT LESS
          ENDIF
          PRINT     *2,KEY20
          CALL      UND132
          ADD       TWO,CLNO
          MOVE      ONE,RECFLAG
.
TDET4000  COMPARE   "52",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      XDOCTYPE,SAVDTYPE
          CLEAR     KEY26
          APPEND    "  Invalid Adm.Type-",KEY26
          APPEND    XADMTYPE,KEY26
          APPEND    SP70,KEY26
          RESET     KEY26
.
          MATCH     SP70,XADMTYPE
          IF        !@EQUAL
            PACK      KEY5,ANSA,SP1,XADMTYPE
            CALL      RDCODE1
            IF        OVRCD=0
              PACK      KEY26,SP2,TDESC,SP70
            ENDIF
          ENDIF
          CALL      PLIN0000             * Print record
          MOVE      ONE,F1
          GOTO      TDET1000
.
TDET8000  IF        F1=1
            CALL      UND132
            MATCH     SP70,SAVDTYPE
            IF        @EQUAL
              PRINT     *2,"  TOTAL FOR UNKNOWN CODE";
            ELSE
              PRINT     *2,"  TOTAL FOR ",SAVDTYPE;
            ENDIF
            CALL      PTOT0000           * Print Sub total
          ENDIF
          COMPARE   ZERO,RECFLAG
          GOTO      TDET9999 IF EQUAL
          CALL      GTOT0000             * Print Grand total
TDET9999  RETURN
+
.******************************************************************************
.         Add totals
.******************************************************************************
ADDTOT00  ADD       XNUMADMN,TNUMADMN
          ADD       XBEDDAYS,TBEDDAYS
          ADD       XNUMDCAS,TNUMDCAS
          ADD       XAMTDCAS,TAMTDCAS
          ADD       XAMTWARD,TAMTWARD
          ADD       XAMTTHEA,TAMTTHEA
          ADD       XAMTMISC,TAMTMISC
          ADD       XAMTLUMP,TAMTLUMP
          ADD       XNETTOTS,TNETTOTS
.
          ADD       XNUMADMN,CNUMADMN
          ADD       XBEDDAYS,CBEDDAYS
          ADD       XNUMDCAS,CNUMDCAS
          ADD       XAMTDCAS,CAMTDCAS
          ADD       XAMTWARD,CAMTWARD
          ADD       XAMTTHEA,CAMTTHEA
          ADD       XAMTMISC,CAMTMISC
          ADD       XAMTLUMP,CAMTLUMP
          ADD       XNETTOTS,CNETTOTS
.
          ADD       XNUMADMN,GNUMADMN
          ADD       XBEDDAYS,GBEDDAYS
          ADD       XNUMDCAS,GNUMDCAS
          ADD       XAMTDCAS,GAMTDCAS
          ADD       XAMTWARD,GAMTWARD
          ADD       XAMTTHEA,GAMTTHEA
          ADD       XAMTMISC,GAMTMISC
          ADD       XAMTLUMP,GAMTLUMP
          ADD       XNETTOTS,GNETTOTS
ADDTOT99  RETURN
+
.******************************************************************************
.         Print record   
.******************************************************************************
PLIN0000  MOVE      ZERO,XNETTOTS
          ADD       XAMTDCAS,XNETTOTS
          ADD       XAMTWARD,XNETTOTS
          ADD       XAMTTHEA,XNETTOTS
          ADD       XAMTMISC,XNETTOTS
          ADD       XAMTLUMP,XNETTOTS
.
          MOVE      ZERO,XALOSDAY
          IF        XNUMADMN<>0
            ADD       XBEDDAYS,XALOSDAY
            ADD       XNUMDCAS,XALOSDAY
            DIV       XNUMADMN,XALOSDAY
          ENDIF
.
          MOVE      XBEDDAYS,F5
          ADD       XNUMDCAS,F5
          MOVE      ZERO,XAVERDAY
          IF        F5<>0
            MOVE      XNETTOTS,XAVERDAY
            DIV       F5,XAVERDAY
          ENDIF
.
          MOVE      KEY26,KEY25
          MOVE      XNUMADMN,FORM5A
          MOVE      XBEDDAYS,FORM5B
          MOVE      XNUMDCAS,FORM5C
          MOVE      XAMTDCAS,FORM9P2A
          MOVE      XAMTWARD,FORM9P2B
          MOVE      XAMTTHEA,FORM9P2C
          MOVE      XAMTMISC,FORM9P2D
          MOVE      XAMTLUMP,FORM9P2E
          MOVE      XALOSDAY,FORM5P2A
          MOVE      XAVERDAY,FORM5P2B
.
          PRINT     *1,KEY25,*26,FORM5A,*32,FORM5B,*38,FORM5C,*43,"|":
                    *44,FORM9P2A,*56,FORM9P2B,*68,FORM9P2C,*80,FORM9P2D:
                    *92,FORM9P2E,*104,XNETTOTS,*116,"|":
                    *117,FORM5P2A,*125,FORM5P2B 
.
          ADD       ONE,CLNO
          CALL      ADDTOT00
PLIN9999  RETURN
+
.******************************************************************************
.         Print Sub-total for each claim code
.******************************************************************************
CTOT0000  MOVE      ZERO,CALOSDAY
          IF        CNUMADMN<>0
            ADD       CBEDDAYS,CALOSDAY
            ADD       CNUMDCAS,CALOSDAY
            DIV       CNUMADMN,CALOSDAY
          ENDIF
.
          MOVE      CBEDDAYS,F5
          ADD       CNUMDCAS,F5
          MOVE      ZERO,CAVERDAY
          IF        F5<>0
            MOVE      CNETTOTS,CAVERDAY
            DIV       F5,CAVERDAY
          ENDIF
.
          MOVE      CALOSDAY,FORM5P2A
          MOVE      CAVERDAY,FORM5P2B
.
          PRINT     *26,CNUMADMN,*32,CBEDDAYS,*38,CNUMDCAS:
                    *43,"|":
                    *44,CAMTDCAS:
                    *56,CAMTWARD,*68,CAMTTHEA,*80,CAMTMISC:
                    *92,CAMTLUMP,*104,CNETTOTS:
                    *116,"|":
                    *117,FORM5P2A,*125,FORM5P2B:
                    *N,"****************************************":
                    "****************************************":
                    "****************************************":
                    "************"
          ADD       TWO,CLNO
          MOVE      ZERO,CNUMADMN
          MOVE      ZERO,CBEDDAYS
          MOVE      ZERO,CNUMDCAS
          MOVE      ZERO,CAMTDCAS
          MOVE      ZERO,CAMTWARD
          MOVE      ZERO,CAMTTHEA
          MOVE      ZERO,CAMTMISC
          MOVE      ZERO,CAMTLUMP
          MOVE      ZERO,CNETTOTS
          MOVE      ZERO,CALOSDAY
          MOVE      ZERO,CAVERDAY
CTOT9999  RETURN
+
.******************************************************************************
.         Print Sub-total
.******************************************************************************
PTOT0000  MOVE      ZERO,TALOSDAY
          IF        TNUMADMN<>0
            ADD       TBEDDAYS,TALOSDAY
            ADD       TNUMDCAS,TALOSDAY
            DIV       TNUMADMN,TALOSDAY
          ENDIF
.
          MOVE      TBEDDAYS,F5
          ADD       TNUMDCAS,F5
          MOVE      ZERO,TAVERDAY
          IF        F5<>0
            MOVE      TNETTOTS,TAVERDAY
            DIV       F5,TAVERDAY
          ENDIF
.
          MOVE      TALOSDAY,FORM5P2A
          MOVE      TAVERDAY,FORM5P2B
.
          PRINT     *26,TNUMADMN,*32,TBEDDAYS,*38,TNUMDCAS:
                    *43,"|":
                    *44,TAMTDCAS:
                    *56,TAMTWARD,*68,TAMTTHEA,*80,TAMTMISC:
                    *92,TAMTLUMP,*104,TNETTOTS:
                    *116,"|":
                    *117,FORM5P2A,*125,FORM5P2B:
                    *N,"****************************************":
                    "****************************************":
                    "****************************************":
                    "************"
          ADD       TWO,CLNO
          MOVE      ZERO,TNUMADMN
	  MOVE      ZERO,TBEDDAYS
          MOVE      ZERO,TNUMDCAS
          MOVE      ZERO,TAMTDCAS
          MOVE      ZERO,TAMTWARD
	  MOVE      ZERO,TAMTTHEA
          MOVE      ZERO,TAMTMISC
          MOVE      ZERO,TAMTLUMP
          MOVE      ZERO,TNETTOTS
          MOVE      ZERO,TALOSDAY
          MOVE      ZERO,TAVERDAY
PTOT9999  RETURN
+
.******************************************************************************
.         Print Grand-total
.******************************************************************************
GTOT0000  MOVE      ZERO,GALOSDAY
          IF        GNUMADMN<>0
            ADD       GBEDDAYS,GALOSDAY
            ADD       GNUMDCAS,GALOSDAY
            DIV       GNUMADMN,GALOSDAY
          ENDIF
.
          MOVE      GBEDDAYS,F5
          ADD       GNUMDCAS,F5
          MOVE      ZERO,GAVERDAY
          IF        F5<>0
            MOVE      GNETTOTS,GAVERDAY
            DIV       F5,GAVERDAY
          ENDIF
.
          MOVE      GALOSDAY,FORM5P2A
          MOVE      GAVERDAY,FORM5P2B
.
          PRINT     *N,"****************************************":
                    "****************************************":
                    "****************************************":
                    "************":
                    *N,*1,"GRAND TOTAL":
                    *26,GNUMADMN,*32,GBEDDAYS,*38,GNUMDCAS:
                    *43,"|":
                    *44,GAMTDCAS:
                    *56,GAMTWARD,*68,GAMTTHEA,*80,GAMTMISC:
                    *92,GAMTLUMP,*104,GNETTOTS:
                    *116,"|":
                    *117,FORM5P2A,*125,FORM5P2B:
                    *N,"****************************************":
                    "****************************************":
                    "****************************************":
                    "************"
          MOVE      ONE,GTOTFLAG
GTOT9999  RETURN
+
.******************************************************************************
.         Header
.******************************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD132
.
          COMPARE   FIVE,COPTION
          GOTO      HEAD6000 IF EQUAL
          BRANCH    COPTION,HEAD6000,HEAD6000
          IF        REPFLAG=1
            PRINT     *40,"Doctor broken down by Admission Type"
          ELSE
            PRINT    *40,"Doctor Type broken down by Admission Type"
          ENDIF
.
HEAD6000  UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"Start Date: ",CPCDATE;
.
          UNPACK    ENDDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     " to ",CPCDATE
          ADD       TWO,CLNO
.
          MATCH     "1",SRECTYPE
          IF        @EQUAL
             REP       " 0",ASATDATE
             UNPACK    ASATDATE,CCENT,CYEAR,CMON,CDAY,PTIPCTIM
             CALL      PACDATE
             PRINT    *N,"To be Invoiced as at : ",CPCDATE," Time: ",PTIPCTIM:
                      *N,"================================================= "
             ADD      THREE,CLNO
          ENDIF
.
          PRINT     *N,*32,"  Bed",*38,"  Day":
                    *43,"|":
                    *44,"        Day":
                    *116,"|":
                    *117,"   ALOS":
                    *125,"   Aver."
.
          BRANCH    COPTION,HEAD7000,HEAD7200,HEAD7000,HEAD7200,HEAD7000
          GOTO      HEAD9999
.
HEAD7000  PRINT     *1,"Attending Doctor";
          GOTO      HEAD7400
.
HEAD7200  PRINT     *1,"Referring Doctor";
HEAD7400  PRINT     *26," Admns":
                    *32,"  Day":
                    *38,"Cases":
                    *43,"|":
                    *44," $    Cases":
                    *56," $     Ward":
                    *68," $  Theatre":
                    *80," $     Misc":
                    *92," $  Lumpsum":
                    *104," $      Net":
                    *116,"|":
                    *117,"   Days":
                    *125,"    Day"
          CALL      UND132
          ADD       THREE,CLNO
HEAD9999  RETURN
+
.******************************************************************************
.         Initialise variables
.******************************************************************************
INTVAR00  MOVE      ZERO,TNUMADMN
          MOVE      ZERO,TBEDDAYS
          MOVE      ZERO,TNUMDCAS
          MOVE      ZERO,TAMTDCAS
          MOVE      ZERO,TAMTWARD
          MOVE      ZERO,TAMTTHEA
          MOVE      ZERO,TAMTMISC
          MOVE      ZERO,TAMTLUMP
          MOVE      ZERO,TNETTOTS
          MOVE      ZERO,TALOSDAY
          MOVE      ZERO,TAVERDAY
.
          MOVE      ZERO,CNUMADMN
          MOVE      ZERO,CBEDDAYS
          MOVE      ZERO,CNUMDCAS
          MOVE      ZERO,CAMTDCAS
          MOVE      ZERO,CAMTWARD
          MOVE      ZERO,CAMTTHEA
          MOVE      ZERO,CAMTMISC
          MOVE      ZERO,CAMTLUMP
          MOVE      ZERO,CNETTOTS
          MOVE      ZERO,CALOSDAY
          MOVE      ZERO,CAVERDAY
.
          MOVE      ZERO,GNUMADMN
          MOVE      ZERO,GBEDDAYS
          MOVE      ZERO,GNUMDCAS
          MOVE      ZERO,GAMTDCAS
          MOVE      ZERO,GAMTWARD
          MOVE      ZERO,GAMTTHEA
          MOVE      ZERO,GAMTMISC
          MOVE      ZERO,GAMTLUMP
          MOVE      ZERO,GNETTOTS
          MOVE      ZERO,GALOSDAY
          MOVE      ZERO,GAVERDAY
INTVAR99  RETURN
.
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      TEMP1,KEY14;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   RESET     KEY14
          READ      TEMP1,KEY14;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                          XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                          XAMTTHEA,XAMTMISC,XAMTLUMP
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      TEMP1,KEY14;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                                  XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                                  XAMTTHEA,XAMTMISC,XAMTLUMP
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TEMP1;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                             XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                             XAMTTHEA,XAMTMISC,XAMTLUMP
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY14
          WRITE     TEMP1,KEY14;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                                  XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                                  XAMTTHEA,XAMTMISC,XAMTLUMP
          RETURN
.
DETEMP1   DELETE    TEMP1,KEY14
          RETURN
.
RATEMP2   MOVE      ZERO,OVRCD
          RESET     KEY17
          READ      TEMP2,KEY17;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP2   RESET     KEY17
          READ      TEMP2,KEY17;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMP2;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                          XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                          XAMTTHEA,XAMTMISC,XAMTLUMP
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP2   MOVE      ZERO,OVRCD
          RESET     KEY17
          READ      TEMP2,KEY17;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                                  XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                                  XAMTTHEA,XAMTMISC,XAMTLUMP
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP2   UPDATE    TEMP2;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                             XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                             XAMTTHEA,XAMTMISC,XAMTLUMP
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP2   RESET     KEY17
          WRITE     TEMP2,KEY17;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                                  XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                                  XAMTTHEA,XAMTMISC,XAMTLUMP
          RETURN
.
DETEMP2   DELETE    TEMP2,KEY17
          RETURN
.
RATEMP3   MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      TEMP3,KEY7;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP3   RESET     KEY7
          READ      TEMP3,KEY7;;
          RETURN
.
RKTEMP3   MOVE      ZERO,OVRCD
          READKS    TEMP3;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                          XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                          XAMTTHEA,XAMTMISC,XAMTLUMP
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP3   MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      TEMP3,KEY7;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                                  XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                                  XAMTTHEA,XAMTMISC,XAMTLUMP
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP3   UPDATE    TEMP3;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                             XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                             XAMTTHEA,XAMTMISC,XAMTLUMP
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP3   RESET     KEY7
          WRITE     TEMP3,KEY7;XRECTYPE,XDOCCODE,XDOCTYPE,XADMTYPE,XCLMCODE:
                                  XNUMADMN,XBEDDAYS,XNUMDCAS,XAMTDCAS,XAMTWARD:
                                  XAMTTHEA,XAMTMISC,XAMTLUMP
          RETURN
.
DETEMP3   DELETE    TEMP3,KEY7
          RETURN
.
RATEMP4   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMP4,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP4   RESET     KEY8
          READ      TEMP4,KEY8;;
          RETURN
.
RKTEMP4   MOVE      ZERO,OVRCD
          READKS    TEMP4;XXADMNUM
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP4   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMP4,KEY8;XXADMNUM
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP4   UPDATE    TEMP4;XXADMNUM
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP4   RESET     KEY8
          WRITE     TEMP4,KEY8;XXADMNUM
          RETURN
.
DETEMP4   DELETE    TEMP4,KEY8
          RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATDTRIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATINPIO/INC
          INC       PATINVIO/INC
          INC       PMSHCPIO/INC
          INC       PATTRNIO/INC
          INC       PMSFCIIO/INC
          INC       PMSVX1IO/INC 
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

+

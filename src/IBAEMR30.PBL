.***************************************************************************
.* System    :   Emergency                                                 *
.***************************************************************************
.* Program   :   IBAEMR30                                                  *
.* Desc      :   Research Project Visit Selection                          *
.***************************************************************************
.* Date      :   26/10/2012                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will select emergency visits for research    *
.*           :   projects.                                                 *
.*           :                                                             *
.* Modifications  :                                                        *
.*        V10.03.01 07/03/2013  Ebon Clements    CAR 278514                *
.*                  Fixed selection ranges and reset extract               *
.*        V10.03.00 26/10/2012  Ebon Clements    CAR 266929                *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       EMRRESFD/INC
          INC       EMRRSLFD/INC
          INC       EMRUNKFD/INC
          INC       EMRRVSFD/INC
          INC       EMRVCDFD/INC
          INC       EMRVISFD/INC
          INC       IBACONFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PMSVX1FD/INC
          INC       PATMA1FD/INC
          INC       WEBSECFD/INC

. LOCAL VARIABLE DEFINITION
. -------------------------
.
BJDAY     FORM      3
CATEU     INIT      "eu"
CJDAY     FORM      3
DIM3      DIM       3
DIM8      DIM       8
DIM10     DIM       10
ENDDDATE  DIM       8
HOSPCODE  DIM       3
PROJCODE  DIM       3
PSAGECON  DIM       3
PSAGETYP  DIM       3
SAVKEY19  DIM       19
STARTDAT  DIM       8
VALDTY01  FORM      1
VALDTY02  FORM      1
VALDTY03  FORM      1
VALDTY04  FORM      1
VALDTY05  FORM      1
WEBUSRID  DIM       10
.
PRGID     INIT      "IBAEMR30"
PRGNAM    INIT      "Research Project Visit Selection"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
.
          CALL      KPRG0000               * keyin research project code
          BRANCH    EXIT,MAIN1000
.
          CALL      KSDT0000               * keyin project start date
          BRANCH    EXIT,MAIN1000
.
          CALL      KUSR0000               * keyin web user id
          BRANCH    EXIT,MAIN1000
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN3000:        * yes
                          MAIN1000:        * no
                          MAIN1000         * cancel
.
MAIN3000  CALL      LVIS0000               * load tempfile by visit date
.
MAIN9999  CHAIN     PGM                    * chain back to program
.
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialisation                               *
.****************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"emrresaf";
          OPEN      EMRRESA1,"emrresaf"
.
          DISPLAY   *P64:24,"emrrslaf";
          OPEN      EMRRSLA1,"emrrslaf"
.
          DISPLAY   *P64:24,"emrunkaf";
          OPEN      EMRUNKA1,"emrunkaf"
.
          DISPLAY   *P64:24,"emrrvsaf";
          OPEN      EMRRVSA1,"emrrvsaf"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA4,"emrvisaf"
.
          DISPLAY   *P64:24,"emrvcdaf";
          OPEN      EMRVCDA1,"emrvcdaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT9999  RETURN
.
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Select Visits for ":
                                           "a Research project":
                    *P1:6,*V2LON,TWO,*HOFF,". Re Load Visits for ":
                                           "a Research project"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.****************************************************************************
. Keyin the research project code
.****************************************************************************
KPRG0000  KEYIN     *P1:10,*EF,"Research Project : ",PROJCODE;
.
          PACK      PROJCODE,PROJCODE,SP70
.
          MATCH     SP70,PROJCODE                * Exit if blank site code
          GOTO      KPRG9500 IF EQUAL
.
          PACK      KEY5,CATEU,PROJCODE,SP70
          CALL      RDCODE1 
          BRANCH    OVRCD,KPRG9000
.
          DISPLAY   *P24:10,TDESC
          MOVE      ZERO,EXIT
          GOTO      KPRG9999
.
KPRG9000  DISPLAY   *P1:24,*EL,*B,"Invalid Research Project Code.";
          CALL      HITENTER
          GOTO      KPRG0000
.
KPRG9500  MOVE      ONE,EXIT
.
KPRG9999  RETURN
.
.***************************************************************************
. Keyin the date range
.***************************************************************************
KSDT0000  DISPLAY   *P1:11,*EF:
                    *P1:11,"Project Start Date :"
.
KSDT1000  CALL      IBACLOCK 
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY 
.
          MOVE      TWENTY2,CCOL
          MOVE      TEN1,CVERT
.
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD,KSDT0000
.
          PACK      STARTDAT WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",STARTDAT
.
          PACK      KEY11,PROJCODE,STARTDAT,SP70
          CALL      RDEMRES1
          BRANCH    OVRCD,KSDT9000
.
          MOVE      EMREEDAT,ENDDDATE
.
          MOVE      ZERO,EXIT
          GOTO      KSDT9999
.
KSDT9000  DISPLAY   *P1:24,*EL,*B,"Invalid Research Project.";
          CALL      HITENTER
          MOVE      ZERO,EXIT
.
KSDT9999  RETURN
.
.****************************************************************************
. Keyin the web user id
.****************************************************************************
KUSR0000  KEYIN     *P1:12,*EF,"Web User Id : ",WEBUSRID;
.
          PACK      WEBUSRID,WEBUSRID,SP70
.
          MATCH     SP70,WEBUSRID
          GOTO      KUSR9500 IF EQUAL
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,KUSR9000
.
          MOVE      WBSEHOSP,HOSPCODE
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9000  DISPLAY   *P1:24,*EL,*B,"Invalid Web User Id.";
          CALL      HITENTER
          GOTO      KUSR0000
.
KUSR9500  MOVE      ONE,EXIT
.
KUSR9999  RETURN
.
.***************************************************************************
. Load ed visits
.***************************************************************************
LVIS0000  PACK      KEY11,PROJCODE,STARTDAT,SP70
          CALL      RDEMRES1
          IF        OVRCD=0
            MOVE      SP70,EMREGDAT
            MOVE      SP70,EMREGTIM
            CALL      UPEMRES1
          ENDIF
 
          IF        COPTION=2
            CALL      DVIS0000                   * Delete project patients
          ENDIF
.
          PACK      KEY24,STARTDAT,SP70
          CALL      RSEMVIS4
LVIS1000  CALL      RKEMVIS4                     * ED visits from start date
          BRANCH    OVRCD,LVIS9000
.
          MATCH     SP70,ENDDDATE
          IF        !@EQUAL
            MATCH     EMVIDATE,ENDDDATE          * Arrival after end date
            GOTO      LVIS9000 IF LESS
          ENDIF
.
          COMPARE   FOUR,EMVISTAT                * Skip cancelled
          GOTO      LVIS1000 IF EQUAL
.
          IF        IBCNMHOS=1
            PACK      KEY8,EMVIADMN,SP70         * Only load my hospital
            CALL      RDPMVX11
            BRANCH    OVRCD,LVIS1000
.
            MATCH     HOSPCODE,PMVXMHOS
            GOTO      LVIS1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,EMVIURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            PACK      KEY8,EMVIADMN,SP70
            CALL      RDEMUNK1
            BRANCH    OVRCD,LVIS1000
.
            MOVE      "       0",PURNO
            MOVE      EMUNDET2,PGNAME
            MOVE      EMUNDET1,PSNAME
            MOVE      EMUNSEX,PSEX
            MOVE      EMUNBDAT,PBDATE
            PACK      PTITL,SP70
          ENDIF
.
          MOVE      ZERO,PSAGEYRS
          MATCH     SP70,PBDATE
          IF        !@EQUAL
            PACK      AGEDATE,CCC,CYY,CMM,CDD
            REP       " 0",AGEDATE
            CALL      CALCAGE
          ENDIF
.
          CALL      VALD0000                     * Check it visit matched
          BRANCH    EXIT,LVIS1000                * the selection criteria
.
          PACK      EMRVRESP,PROJCODE,SP70
          PACK      EMRVSDAT,STARTDAT,SP70
          PACK      EMRVVISN,EMVIADMN,SP70
          MOVE      ZERO,EMRVPSTA
.
          MOVE      WEBUSRID,EMRVCUID
          PACK      EMRVCDAT,CCC,CYY,CMM,CDD
          REP       " 0",EMRVCDAT                * Current date
          CLOCK     TIME,EMRVCTIM                * Current time
          MOVE      SP70,EMRVUUID
          MOVE      SP70,EMRVUDAT
          MOVE      SP70,EMRVUTIM
          MOVE      SP70,EMRVSPAR
.
          PACK      KEY19,EMRVRESP,EMRVSDAT,EMRVVISN,SP70
          CALL      RAEMRVS1
          IF        OVRCD=1
            CALL      WREMRVS1
          ENDIF
.
          GOTO      LVIS1000
.
LVIS9000  PACK      KEY11,PROJCODE,STARTDAT,SP70
          CALL      RDEMRES1
          IF        OVRCD=0
            PACK      EMREGDAT,CCC,CYY,CMM,CDD
            REP       " 0",EMREGDAT                * Current date
            CLOCK     TIME,EMREGTIM                * Current time
            CALL      UPEMRES1
          ENDIF
.
LVIS9999  RETURN
.
.***************************************************************************
. Check if this ED visit matches the selection criteria
. If must have one matching selection for each type
. **************************************************************************
VALD0000  MOVE      TWO,VALDTY01
          MOVE      TWO,VALDTY02
          MOVE      TWO,VALDTY03
          MOVE      TWO,VALDTY04
          MOVE      TWO,VALDTY05
.
          PACK      KEY33,PROJCODE,STARTDAT,SP70
          CALL      RSEMRSL1
VALD1000  CALL      RKEMRSL1
          BRANCH    OVRCD,VALD8000
.
          MATCH     PROJCODE,EMRSRESP
          GOTO      VALD8000 IF NOT EQUAL
.
          MATCH     STARTDAT,EMRSSDAT
          GOTO      VALD8000 IF NOT EQUAL
.
          MATCH     "1",EMRSSTAT                 * Selection is inactive
          GOTO      VALD1000 IF EQUAL
.
          MATCH     "01",EMRSTYPE
          IF        @EQUAL
            IF        VALDTY01<>1
              MOVE      ZERO,VALDTY01
            ENDIF
            PACK      DIM3,EMRSSELF,SP70
            MATCH     DIM3,EMVIUC20
            GOTO      VALD1000 IF LESS
.
            PACK      DIM3,EMRSSELT,SP70
            MATCH     EMVIUC20,DIM3
            GOTO      VALD1000 IF LESS
            MOVE      ONE,VALDTY01
            GOTO      VALD1000
          ENDIF
.
          MATCH     "02",EMRSTYPE
          IF        @EQUAL
            IF        VALDTY02<>1
              MOVE      ZERO,VALDTY02
            ENDIF
            PACK      KEY14,DEMVIADM,ZERO,ZERO,FIVE,SP70
            CALL      RSEMVCD1
VALD2000    CALL      RKEMVCD1
            BRANCH    OVRCD,VALD1000
.
            MATCH     DEMVIADM,EMVCVIST
            GOTO      VALD1000 IF NOT EQUAL
.
            MATCH     "005",EMVCTYPE
            GOTO      VALD1000 IF NOT EQUAL
.
            PACK      DIM10,EMRSSELF,SP70
            MATCH     DIM10,EMVCMNCD
            GOTO      VALD2000 IF LESS
.
            PACK      DIM10,EMRSSELT,SP70
            MATCH     EMVCMNCD,DIM10
            GOTO      VALD2000 IF LESS
.
            MOVE      ONE,VALDTY02
            GOTO      VALD1000
          ENDIF
.
          MATCH     "03",EMRSTYPE
          IF        @EQUAL
            IF        VALDTY03<>1
              MOVE      ZERO,VALDTY03
            ENDIF
            SQUEEZE   EMRSSELF
            MOVE      EMRSSELF,F3
            COMPARE   F3,PSAGEYRS
            GOTO      VALD1000 IF LESS
.
            SQUEEZE   EMRSSELT
            MOVE      EMRSSELT,F3
            COMPARE   PSAGEYRS,F3
            GOTO      VALD1000 IF LESS
            MOVE      ONE,VALDTY03
            GOTO      VALD1000
          ENDIF
.
          MATCH     "04",EMRSTYPE
          IF        @EQUAL
            IF        VALDTY04<>1
              MOVE      ZERO,VALDTY04
            ENDIF
            PACK      DIM3,EMRSSELF,SP70
            MATCH     DIM3,PCONT
            GOTO      VALD1000 IF LESS
.
            PACK      DIM3,EMRSSELT,SP70
            MATCH     PCONT,DIM3
            GOTO      VALD1000 IF LESS
            MOVE      ONE,VALDTY04
            GOTO      VALD1000
          ENDIF
.
          MATCH     "05",EMRSTYPE
          IF        @EQUAL
            IF        VALDTY05<>1
              MOVE      ZERO,VALDTY05
            ENDIF
            PACK      DIM8,EMRSSELF,SP70
            MATCH     DIM8,PPOST
            GOTO      VALD1000 IF LESS
.
            PACK      DIM8,EMRSSELT,SP70
            MATCH     PPOST,DIM8
            GOTO      VALD1000 IF LESS
            MOVE      ONE,VALDTY05
            GOTO      VALD1000
          ENDIF
.
          GOTO      VALD1000
.
VALD8000  PACK      KEY5,VALDTY01,VALDTY02,VALDTY03,VALDTY04,VALDTY05,SP70
          REP       "21",KEY5
          MATCH     "11111",KEY5
          GOTO      VALD9000 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT
          GOTO      VALD9999
.
VALD9999  RETURN
.
.***************************************************************************
. Delete selected visits for this research project
. **************************************************************************
DVIS0000  PACK      KEY19,PROJCODE,STARTDAT,SP70
          CALL      RSEMRVS1
DVIS1000  CALL      RKEMRVS1
          BRANCH    OVRCD,DVIS9999
.
          MATCH     PROJCODE,EMRVRESP            * Same project
          GOTO      DVIS9999 IF NOT EQUAL
.
          MATCH     STARTDAT,EMRVSDAT            * Same project start date
          GOTO      DVIS9999 IF NOT EQUAL
.
          IF        IBCNMHOS=1
            PACK      KEY8,EMRVVISN,SP70         * Only load my hospital
            CALL      RDPMVX11
            BRANCH    OVRCD,DVIS1000
.
            MATCH     HOSPCODE,PMVXMHOS
            GOTO      DVIS1000 IF NOT EQUAL
          ENDIF
.
          PACK      SAVKEY19,EMRVRESP,EMRVSDAT,EMRVVISN,SP70
          PACK      KEY19,EMRVRESP,EMRVSDAT,EMRVVISN,SP70
          CALL      DEEMRVS1
.
          PACK      KEY19,SAVKEY19,SP70
          CALL      RSEMRVS1
          GOTO      DVIS1000
.
DVIS9999  RETURN
.
. **************************************************************************
.         I/O Includes                                                     *
.***************************************************************************
.
          INC       STD001IO
          INC       CALCAGE
.
          INC       EMRRESIO/INC
          INC       EMRRSLIO/INC
          INC       EMRRVSIO/INC
          INC       EMRUNKIO/INC
          INC       EMRVCDIO/INC
          INC       EMRVISIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PMSVX1IO/INC
          INC       WEBSECIO/INC
.

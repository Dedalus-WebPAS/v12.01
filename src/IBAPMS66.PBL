.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   IBAPMS66.PBL                                              *
.* Desc      :   Bulk resend sms messages                                  *
.***************************************************************************
.* Date      :   16/03/2016                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will check for SMS messages to be resent.    *
.*           :   It will select messages that initiall failed but have     *
.*           :   been corrected.                                           *
.* Modifications  :                                                        *
.***************************************************************************
.* V11.02.01 10/02/2022  Thanh T       TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                *
.* V11.00.02 20/03/2020  Peter Vela     TSK 0889143                        *
.*           Fixed OUTLET IO calls                                         *
.*           Fixed WATLET IO calls                                         *
.* V11.00.01 18/03/2020  Peter Vela     TSK 0889143                        *
.*           Recompiled for OUTLETFD                                       *
.*           Recompiled for WATLETFD                                       *
.* V10.14.03 15/08/2019  Thanh T.           TSK 0867186                    *
.*           Changed PROC0000 to fix the issue with waiting list sms rows  *
.*           not being processed                                           *
.* V10.14.02 02/08/2019  Thanh T.           TSK 0867186                    *
.*           Changed PROC0000 to process only rows which the user has      *
.*           hospital access in multi hospital mode                        *
.* V10.14.01 09/07/2019  Thanh T.           TSK 0867186                    *
.*           Added HOSP0000 for hospital filter in multi hospital mode     *
.* V10.08.03 22/11/2016  Ebon Clements      TSK 0294154                    *
.*           Changed message text to be on one line - PROC1000             *
.* V10.08.02 03/11/2016  Ebon Clements      TSK 0294154                    *
.*           Fixed resend end CDTATA tags                                  *
.* V10.08.01 26/10/2016  Ebon Clements      TSK 0294154                    *
.*           Fixed resend of entire message contents - PROC1000            *
.* V10.08.00 16/03/2016  Ebon Clements      TSK 0294154                    *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBASEQFD/INC       * Sequence file
          INC       PATILTFD/INC       * IP Letters
          INC       PMSSMHFD/INC       * SMS History
          INC       PMSSRRFD/INC       * SMS Responses Received
          INC       PMSVX1FD/INC       * Patient Visit Extension File
          INC       OUTLETFD/INC       * Outpatient Letters
          INC       WATLETFD/INC       * W/L Letters
          INC       WEBERRFD/INC       * WEB Errors
          INC       PATHSPFD/INC       * Hospital Details File
          INC       WATTR1FD/INC       
          INC       MLTSECFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CURRDATE  DIM       8                  * Current Date
DIM16     DIM       16
MESGCODE  DIM       3                  * SMS Message Code
MESGTYPE  FORM      2                  * SMS Message Type
SAVKEY18  DIM       18
SMSMDATE  DIM       8                  * SMS Message Date
IBCNMHOS  FORM      1
FILTHOSP  DIM       3
SMSHOSPC  DIM       3
USERID    DIM       10       * User ID
ALLHSPAC  FORM      1        * User has all hospital access flag
.
.---------------------------------------------------------------------
PRGID     INIT      "IBAPMS66"
PRGNAM    INIT      "Bulk Resend SMS Messages"
VERSION   INIT      "V12.01.00"
.---------------------------------------------------------------------
+
.**************************************************************************
.*                            MAIN0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN2000       * proceed with clean up
.
MAIN2000  CALL      GDAT0000               * Get SMS Message Date 
          BRANCH    EXIT,MAIN1000          * nothing entered
.
MAIN3000  CALL      GMTY0000               * Get SMS Message Type
          BRANCH    EXIT,MAIN2000          * nothing entered
.
MAIN4000  CALL      GMCD0000               * Get SMS Code
          BRANCH    EXIT,MAIN3000          * nothing entered
.  
          CALL      HOSP0000
          CALL      USID0000               * keyin the User Id
.
MAIN7000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN8000:          * yes
                          MAIN4000:          * no
                          MAIN1000           * cancel
.
MAIN8000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      IBASEQA1,"ibaseqaf"
          OPEN      PMSSMHA1,"pmssmhaf"
          OPEN      PMSSMHA2,"pmssmhaf"
          OPEN      PMSSMHA3,"pmssmhaf"
          OPEN      PMSSMHA4,"pmssmhaf"
          OPEN      PMSSMHA5,"pmssmhaf"
          OPEN      PMSSMHA6,"pmssmhaf"
          OPEN      PMSSRRA1,"pmssrraf"
          OPEN      PMSSRRA2,"pmssrraf"
          OPEN      PMSSRRA3,"pmssrraf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      OUTLETR1,"outletrf"
          OPEN      PATLETR1,"patletrf"
          OPEN      WATLETR1,"watletrf"
.
          OPEN      PATHSPA1,"pathspaf"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      MLTSECA1,"mltsecaf"
.
          OPEN      CONTROLF,"controlf"     * Inpatient CONTROL File
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          CALL      ALHSPA00
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Bulk Resend SMS Messages"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
+
.****************************************************************************
.*                             GDAT0000                Called by: MAIN0000  *
.*                       Get the cut-off date                               *
.* Returns: SMSMDATE - cut-off date (ccyymmdd)                              *
.*          CURRDATE - Current Date (ccyymmdd)                              *
.****************************************************************************
.
GDAT0000  DISPLAY   *P1:12,*EF,"SMS Message Date  :"
          MOVE      ONE,CCENTRY
          MOVE      TEN2,CVERT
          MOVE      TWENTY1,CCOL
          MOVE      ONE,CCANLDTE
.
          CALL      IBACLOCK                     * load current date
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
.
GDAT0500  CALL      KEYDATE
          BRANCH    OVRCD,GDAT9100
          PACK      SMSMDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     SMSMDATE,CURRDATE   * date in past ?
          GOTO      GDAT9000 IF NOT LESS             * yes
.
          DISPLAY   *P1:24,*EL,*B,"SMS date not prior to current date.  ";
          CALL      HITENTER
          GOTO      GDAT0500
.
GDAT9000  MOVE      ZERO,EXIT
          GOTO      GDAT9999
.
GDAT9100  MOVE      ONE,EXIT
.
GDAT9999  RETURN
.
+
.****************************************************************************
.*                             GMTY0000                Called by: MAIN0000  *
.*                   Get the type of SMS Message                            *
.* Returns:  MESGTYPE - Type of SMS                                         *
.*                      " 2" = Outpatient                                   *
.*                      " 3" = Inpatient                                    *
.*                      " 9" = Waiting List                                 *
.*           EXIT  0 = Ok to continue                                       *
.*                 1 = Nothing entered                                      *
.****************************************************************************
.
GMTY0000  KEYIN     *P1:14,*EF,"Type of Message   :":
                    *P21:14,*V2LON,MESGTYPE
.
          COMPARE   ZERO,MESGTYPE                 * Zero entered ?
          GOTO      GMTY9100 IF EQUAL             * no - finished
.
          IF        MESGTYPE=2
            DISPLAY   *P21:14,*V2LON,"Outpatient"
            GOTO      GMTY9000
          ENDIF
.
          IF        MESGTYPE=3
            DISPLAY   *P21:14,*V2LON,"Inpatient"
            GOTO      GMTY9000
          ENDIF
.
          IF        MESGTYPE=9
            DISPLAY   *P21:14,*V2LON,"Waiting List"
            GOTO      GMTY9000
          ENDIF
.
          BEEP
          GOTO      GMTY0000
.
GMTY9000  DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      GMTY9999
.
GMTY9100  MOVE      ONE,EXIT
.
GMTY9999  RETURN
+
.****************************************************************************
.*                             GMCD0000                Called by: MAIN0000  *
.*                   Get the SMS Message Code                               *
.* Returns:  MESGCODE - SMS Message code according to message type          *
.*           EXIT  0 = Ok to continue                                       *
.*                 1 = Nothing entered                                      *
.****************************************************************************
.
GMCD0000  KEYIN     *P1:16,*EF,"Message Code      :":
                    *P21:16,*V2LON,MESGCODE
.
          PACK      MESGCODE,MESGCODE,SP70
.
          MATCH     EXITCHAR,MESGCODE             * Exit char ?
          GOTO      GMCD9100 IF EQUAL
.
          MATCH     SP70,MESGCODE                 * Blank All ?
          IF        @EQUAL
            DISPLAY   *P21:16,*V2LON,"All"
            GOTO      GMCD9000
          ENDIF
.
          RJUSTIFY  MESGCODE
.
          IF        MESGTYPE=2
            PACK      KEY6,MESGCODE,SP1,SP1,ZERO,SP70
            CALL      RDOTLET1
            BRANCH    OVRCD,GMCD8000
.
            MATCH     "1",OTLTCOMM               * SMS Message
            GOTO      GMCD8000 IF NOT EQUAL
.
            DISPLAY   *P21:16,*V2LON,OTLTTEXT
            GOTO      GMCD9000
          ENDIF
.
          IF        MESGTYPE=3
            PACK      KEY6,MESGCODE,SP1,SP1,ZERO,SP70
            CALL      RDPTLET1
            BRANCH    OVRCD,GMCD8000
.
            MATCH     "1",PTLECOMM               * SMS Message
            GOTO      GMCD8000 IF NOT EQUAL
.
            DISPLAY   *P21:16,*V2LON,PTLETEXT
            GOTO      GMCD9000
          ENDIF
.
          IF        MESGTYPE=9
            PACK      KEY6,MESGCODE,SP1,SP1,ZERO,SP70
            CALL      RDLET1
            BRANCH    OVRCD,GMCD8000
.
            DISPLAY   *P21:16,*V2LON,WLTEXT
            GOTO      GMCD9000
          ENDIF
.
          MATCH     "1",WLETCOMM               * SMS Message
          GOTO      GMCD8000 IF NOT EQUAL
.
          BEEP
          GOTO      GMCD0000
.
GMCD8000  DISPLAY   *P1:24,*EL,*B,"Invalid SMS Code. ";
          CALL      HITENTER
          GOTO      GMCD0000
.
GMCD9000  DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      GMCD9999
.
GMCD9100  MOVE      ONE,EXIT
.
GMCD9999  RETURN
+
.****************************************************************************
.* Get hospital id                                                          *
.****************************************************************************
.
HOSP0000  IF        IBCNMHOS = 1
            DISPLAY   *P1:18,*EL,"Hospital Id       : ";
            MOVE      "21",ECOL
            MOVE      "18",EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
HOSP1100    CALL      PATHSPKY
            IF        EXIT = ONE
              MOVE      SP70,FILTHOSP
              MOVE      "All Hospitals",PTHSNAME
              MOVE      SP70,PTHSHOSP
              DISPLAY   *P21:18,*EL,PTHSNAME,*W2;
            ELSE
              IF        EXIT = 2
                MOVE      SP70,FILTHOSP
                GOTO      HOSP9400
              ELSE
                MOVE      KEY3,FILTHOSP
                DISPLAY   *P21:18,*EL,PTHSNAME,*W2;
              ENDIF
            ENDIF
          ENDIF
.
HOSP1199  MOVE      ZERO,EXIT
          GOTO      HOSP9999
.
HOSP9400  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Hospital ID ** ";
          CALL      HITENTER
          GOTO      HOSP0000
.
HOSP9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: MAIN8000*
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY18,SP1,FIVE,SP70
          PRINT     *1,"<messages>"
          CALL      RSPMSMH4
PROC1000  CALL      RKPMSMH4
          BRANCH    OVRCD,PROC9999
.
          PACK      SAVKEY18,PMSMSTAT,PMSMUMID,SP70
.
          MATCH     " 5",PMSMSTAT                           * Error fixed
          GOTO      PROC9999 IF NOT EQUAL
.
          MOVE      PMSMUMID,DIM16
          PACK      KEY32,DIM16,SP70
          CALL      RSPMSMH6
          CALL      RKPMSMH6                * Read original message to find 
          BRANCH    OVRCD,PROC1000          * send date
.
          MATCH     DIM16,PMSMRMID          * Re-Sent Message Id
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SMSMDATE,PMSMSDAT       * Original Sent date
          GOTO      PROC1000 IF NOT EQUAL
.
          MOVE      SAVKEY18,KEY18
          CALL      RDPMSMH4                * Re read message to be sent
          BRANCH    OVRCD,PROC1000
.
          MOVE      ZERO,F2
          MOVE      PMSMMTYP,F2
          COMPARE   MESGTYPE,F2                             * Message type
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP70,MESGCODE                           * SMS Message Code
          IF        !@EQUAL
            MATCH     MESGCODE,PMSMSCOD
            GOTO      PROC1000 IF NOT EQUAL
          ENDIF
.
          CALL      HOSPDT00
.
          IF        IBCNMHOS = 1
            MATCH     SP70,FILTHOSP           * Hospital Filter
            IF        !@EQUAL
              MATCH     SMSHOSPC,FILTHOSP
              GOTO      PROC1000 IF NOT EQUAL
            ELSE
              IF        ALLHSPAC = ZERO
                PACK      KEY13,USERID,SMSHOSPC,SP70  * This hospital access
                CALL      RDMLSEC1
                BRANCH    OVRCD,PROC1000        * No access to hospital
              ENDIF
            ENDIF
          ENDIF
.
          PRINT     *1,"<sms_message>"                      * sms message
          PRINT     *1,"<resend>1</resend>"                 * Resend sms
          PRINT     *1,"<msg_id>",*+,PMSMUMID,*-,"</msg_id>"   * Message Id
          PRINT     *1,"<msg_code>",PMSMSCOD,"</msg_code>"  * sms code
          PRINT     *1,"<key>",PMSMRKEY,"</key>"            * sms key
          PRINT     *1,"<phone_no>",*+,PMSMMNUM,*-,"</phone_no>"  * Phone Number
          PRINT     *1,"<urno>",PMSMURNO,"</urno>"          * UR No
          PRINT     *1,"<user_id>",PMSMSUID,"</user_id>"    * User ID
          PRINT     *1,"<other_contacts>0</other_contacts>" * No other contacts
          PRINT     *1,"<text><![CDATA["
          PRINT     *1,PMSMMESA,PMSMMESB,PMSMMESC,PMSMMESD: * Message text
                       PMSMMESE,PMSMMESF
          PRINT     *1,"]]></text>"
          PRINT     *1,"</sms_message>"
.
          MOVE     " 4",PMSMSTAT       * Waiting to be sent
          CALL     UPPMSMH4
.
PROC9000  MOVE     SAVKEY18,KEY18
          CALL     RSPMSMH4
          GOTO     PROC1000
.
PROC9999  PRINT     *1,"</messages>"
          RETURN
.
.----------------------------------------------------------------------------
. Get hospital details
.----------------------------------------------------------------------------
HOSPDT00  MOVE      SP70,SMSHOSPC
.
          MATCH     " 9",PMSMMTYP
          GOTO      HOSPDT50 IF EQUAL
.
. Message Type - Inpatient or Ourpatient
.
          MOVE      PMSMRKEY,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,HOSPDT99
.
          MOVE      PMVXMHOS,SMSHOSPC
          GOTO      HOSPDT99
.
. Message Type - Waiting list
.
HOSPDT50  PACK      KEY19,PMSMRKEY,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,HOSPDT99
.
          MOVE      WTWMIHSP,SMSHOSPC
.
HOSPDT99  RETURN
.
.****************************************************************************
.         Keyin user id
.****************************************************************************
USID0000  KEYIN     *P1:20,*EL,"User Id           : ",USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      USID0000 IF EQUAL
USID9999  RETURN
.
.------------------------------------------------------------------------------
. Get user has access to all hospital flag
.------------------------------------------------------------------------------
ALHSPA00  MOVE     ZERO,ALLHSPAC
.
          PACK      KEY13,USERID,SP70
          CALL      RDMLSEC1
          IF        OVRCD=0
            MOVE     ONE,ALLHSPAC           * user has All hospital access
          ENDIF
.
ALHSPA99  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATHSPKY
          INC       SMSMESID
.
          INC       IBASEQIO/INC       * Sequence file
          INC       OUTLETIO/INC       * Outpatient Letters
          INC       PATILTIO/INC       * IP Letters
          INC       PMSSMHIO/INC       * SMS History
          INC       PMSSRRIO/INC       * SMS Responses Received
          INC       PMSVX1IO/INC       * Patient Visit Extension File
          INC       WATLETIO/INC       * W/L Letters
          INC       WEBERRIO/INC       * WEB Errors
          INC       PATHSPIO/INC       * Hospital Details File
          INC       WATTR1IO/INC
          INC       MLTSECIO/INC

. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     FIXHOS04                         *
. *                                                        *
. *     FUNCTION:         OUTSTANDING DEBTORS REPORT       *
. *                                                        *
. *     DATE WRITTEN:     07/04/88                         *
. *                                                        *
. *     AUTHOR:           PAUL LOO                         *
. *                                                        *
. *     MODIFICATIONS:                                     *
. *       V12.00.01  14/05/2025  Peter Vela     TSK 0955096*
. *                  Alphanumeric Visit Number changes     *
. *       V10.04.00  06/03/2014 J.Tan          CAR 296128  *
. *                  Ported from v6.18                     *
. *        V6.17.01  28/07/2008 J.Tan          CAR 202119  *
. *                  Added Emergency System                *
. *        V6.16.01  14/07/2008 Sandra Barcham CAR 173574  *
. *                  Created Text table                    *
. *                  14/10/2008 J.Tan              173574  *
. *                  Added Health fund to text file and    *
. *                  HF mapping text file                  *
. *        V6.15.02  06/02/2008  J.Tan         CAR 160840  *
. *                  Mods to include GST Journal           *
. *        V6.15.01  07/08/2007  J.Tan         CAR 147108  *
. *                  Mods to write to a file               *
. *        V6.14.00  22/03/2007  J.Tan         CAR 136665  *
. *                  Ported from V5.14                     *
. *                                                        *
. **********************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
          INC       IBAPASFD/INC
          INC       IBASECFD/INC
          INC       OUTSITFD/INC
          INC       PATCODFD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PMSVX1FD/INC
          INC       PATMI1FD/INC
          INC       PATSELDT/INC
          INC       PATWC1FD/INC
.
.        EXTRA   VARIABLES
.
OUTDFILE  FILE      ASCII, FIXED=256
.
.Name     Type   Length   Start   Description
.----     ----   ------   -----   -----------
.DWADMNO   DIM        8        1   Admission Number
.WURNO     DIM        8        9   U/R Number
.WINVNO    DIM        8       17   Invoice Number
.WIDATE    DIM        8       25   Invoice Date
WPATAMNT   FORM      12.2     33   Patient Invoice Amount
WREBAMNT   FORM      12.2     48   Rebate Invoice Amount
.WCLAIM    DIM        3       63   Claim Code
WHFUND     DIM        6       66   Health fund
WHFNDT     DIM        8       72   Health fund table
WINVSYS    DIM        1       80   System (1=AAE 3=INPAT)
.EOR                 81
.
SORTSTR   DIM       20
SORT1     INIT      "u1-37"
SORT2     INIT      "u79-82,1-37"
.
WORKFILE  IFILE     SQL, FIXED=113, KEY=SORTSTR
ROLLFILE  FILE      ASCII, FIXED=256
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.WSNAME         DIM       20         20             1
.WGNAME         DIM        1          1            21
DWADMNO         DIM        8          8            22
.WINVNO         DIM        8          8            30
.WINVTOT        FORM       8.2        6            38
.WFDINS         DIM        6          6            44
.WFUNP          FORM       6.2        5            50
.WINSP          FORM       6.2        5            55
.WPATP          FORM       6.2        5            60
.WJOUP          FORM       9.2        7            65
.WBALANCE       FORM       9.2        7            72
.WCLAIM         DIM        3          3            79
.WURNO          DIM        8          8            82
.WIDATE         DIM        8          8            90
.WHFUND         DIM        6          6            98
.WHFNDT         DIM        8          8           104
.WINVSYS        DIM        1          1           112
. ----- EOR 113 -----
.
ZEROES    FORM      " .00"
.
CMDLINE   DIM       80
FNAMED    INIT      "hos04deb"
FNAMET    DIM       8
TEMPFILE  DIM       8
FORM12P2  FORM      12.2
DATFILEW  INIT      "HOS04A"
TMPFILEH  INIT      "HOS04B"
.
COUNT     FORM      2
CODE      DIM       2
CODECL    INIT      "CL"
.
DIM8      DIM       8
DIM10     DIM       10
DIM22     DIM       22
.
FIVE9     FORM      "59"
FROMDATE  DIM       8
.
PNAME     DIM       22
PATSYST   FORM      1
.
SAVPG     FORM      4
SAVCLM    DIM       3
.
TODATE    DIM       8
.
LNO       FORM      2
LN55      FORM      "55"
.
. ----- Variables from tempfile WORKFILE
.
WADMNO    DIM       8                                                  *I-73905
WSNAME    DIM      20
WGNAME    DIM       1
WINVNO    DIM       8 
WINVTOT   FORM      8.2
WFDINS    DIM       6 
WFUNP     FORM      6.2
WINSP     FORM      6.2
WPATP     FORM      6.2
WJOUP     FORM      9.2
WBALANCE  FORM      9.2
WCLAIM    DIM       3
WURNO     DIM       8
WIDATE    DIM       8
.
TINVTOT   FORM      8.2
TFUNP     FORM      6.2
TINSP     FORM      6.2
TPATP     FORM      6.2
TJOUP     FORM      9.2
TBALANCE  FORM      9.2
.
GINVTOT   FORM      8.2
GFUNP     FORM      6.2
GINSP     FORM      6.2
GPATP     FORM      6.2
GJOUP     FORM      9.2
GBALANCE  FORM      9.2
.
COTHSFN   FORM      1
CMABINS   DIM       6       SECTOR 13, POS 188
CVETINS   DIM       6       SECTOR 13, POS 194
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
.
PRGID     INIT      "FIXHOS04"
PRGNAM    INIT      "Outstanding Debtors Report"
VERSION   INIT      "V12.01.00"
.
.         START OF PROGRAM
.
          CALL          DISPHEAD
          DISPLAY       *P56:24,"Opening patinvrf";
          OPEN          PATINVR2,"patinvrf"
.
          DISPLAY       *P64:24,"patma1af";
          OPEN          PATMA1A1,"patma1af"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN          PATMI1A1,"patmi1af"
.
          DISPLAY       *P64:24,"patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"patwc1af";
          OPEN          PATWC1A1,"patwc1af"
.
          DISPLAY       *P64:24,"outsitaf";
          OPEN          OUTSITA1,"outsitaf"
.
          OPEN          PATMX1A1,"patmx1af"
          OPEN          PMSVX1A1,"pmsvx1af"
.
          DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
.
          READ          CONTROLF,ZERO;*85,IBCNUGST
          READ          CONTROLF,TEN3;*188,CMABINS,*194,CVETINS
.
          PACK          FNAMET WITH DATFILEW,PORT
          REP           " 0",FNAMET
.
          PACK          TEMPFILE WITH TMPFILEH,PORT
          REP           " 0",TEMPFILE
.
MENU      MOVE        SP3,SAVCLM
          MOVE        SP4,SAVPG
.
BEGIN     HLINE       *G37,2,53,80
          DISPLAY     *P1:3,*EF:
                      *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                      *P1:5,*V2LON,ONE,*HOFF," = Surname Sequence":
                      *P1:6,*V2LON,TWO,*HOFF," = Surname Sequence with ":
                                                 "Claim Code"
.
         KEYIN 	      *P1:8,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF KSORT10,KSORT20
          DISPLAY   *P35:8,*EL,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      BEGIN 
.
KSORT10   MOVE      SORT1,SORTSTR        * Surname sequence only
          GOTO      KADAT   
.
KSORT20   MOVE      SORT2,SORTSTR        * Claim code and surname
.
.         Keyin from date
.
KADAT     DISPLAY   *P1:12,*EF,"From Date : ":
                        *P1:14,"To Date   :"
.
          MOVE      TEN2,CVERT
          MOVE      TEN5,CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          PACK      FROMDATE,CCC,CYY,CMM,CDD
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      "45",ECOL           * Error message
          MOVE      CVERT,EVERT
          CALL      KEYDATE
          BRANCH    OVRCD OF BEGIN
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
.         Keyin todate
.
          MOVE      TEN4,CVERT
          MOVE      TEN5,CCOL
.
          MOVE      "45",ECOL          * Error message
          MOVE      CVERT,EVERT
          CALL      KEYDATE
          BRANCH    OVRCD OF KADAT
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      INVDATE IF LESS
.
          CALL      CONTQST
          BRANCH    CEXIT,CONFIRM,KADAT,MENU
.
CONFIRM   
.         CALL      PATSELFN
.         BRANCH    EXIT,BEGIN
.
          DISPLAY     *P1:3,*EF:
                      *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                      *P1:5,*V2LON,ONE,*HOFF," = Emergency":
                      *P1:6,*V2LON,TWO,*HOFF," = Inpatient":
                      *P1:7,*V2LON,THREE,*HOFF," = Both Emergency & Inpatient"
.
          KEYIN     *P1:9,*EL,"Please Select : ",*V2LON,PATSYST;
          COMPARE   ZERO TO PATSYST
          GOTO      BEGIN IF EQUAL
.
          BRANCH    OPTION OF OKCONTN,OKCONTN,OKCONTN
          DISPLAY   *P35:9,*EL,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      CONFIRM
.
OKCONTN   KEYIN     *P1:24,*EL,"Ok to continue (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS,*C,*EL
          REP       UPPLOW,ANS                                         *I-73905
.
          CMATCH    ANSY,ANS
          GOTO      PROCESS IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      BEGIN IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.
INVDATE   DISPLAY   *P1:24,*EL,*P20:24,*V2LON,*B:
                    "** Fromdate must be less than Todate **",*W3;
          GOTO      KADAT
.
PROCESS   DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialise Work File **",*W2;
          CALL      CREATWRK
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Process Outstanding Debtors **",*W2;
          DISPLAY   *P1:24,*EL,*P10:24:
                    "Processing Invoice Number :",*P50:24,"Scanning :"
.
          PACK      KEY16,FROMDATE,SP20
          CALL      RDSINV2 
LOPINV    CALL      RDKINV2
          BRANCH    OVRCD OF ENDINV
.
          MATCH     PINVDTE,TODATE
          GOTO      ENDINV IF LESS
.
.         Ignore  cancelled invoice
.
          MATCH     ZEROVISN,PINVADM
          GOTO      LOPINV IF EQUAL
.
          DISPLAY   *P61:24,PINVNO;
.
          BRANCH    PATSYST,LOP100,LOP200
.
LOP100    COMPARE   ONE,PINVSYS
          GOTO      LOP300 IF EQUAL        * emergency
.
          BRANCH    PATSYST,LOPINV
.
LOP200    COMPARE   THREE,PINVSYS
          GOTO      LOPINV IF NOT EQUAL

LOP300    MOVE      PINVAMT,WBALANCE
          ADD       PINVHFDA,WBALANCE
          ADD       PINVOTHA,WBALANCE
          ADD       PINVPATA,WBALANCE
          ADD       PINVJOUR,WBALANCE
          ADD       PTINCRTT,WBALANCE
          IF        IBCNUGST=2
            ADD       PTINGSTJ,WBALANCE
          ENDIF
.
          COMPARE   ZEROES,WBALANCE
          GOTO      LOPINV IF EQUAL
.
          DISPLAY   *P38:24,*V2LON,PINVNO;
.
          PACK      PSNAME WITH SP10,SP10
          PACK      PGNAME WITH SP10,SP10,SP10
.
          MOVE      PINVUR,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMAST
.
          MOVE      PGNAME,WGNAME
          MOVE      PINVADM,DWADMNO
          MOVE      PSNAME,WSNAME
          PACK      KEY37 WITH WSNAME,WGNAME,DWADMNO,PINVNO
.
          MOVE      PINVCLM,WCLAIM
          MOVE      PINVADM,WADMNO
          MOVE      PINVNO,WINVNO
          MOVE      PINVAMT,WINVTOT
          MOVE      PINVHFDA,WFUNP
          MOVE      PINVOTHA,WINSP
          MOVE      PINVPATA,WPATP
          MOVE      PINVJOUR,WJOUP
          IF        IBCNUGST=2
            ADD       PTINGSTJ,WJOUP
          ENDIF
          MOVE      PURNO,WURNO
          MOVE      PINVDTE,WIDATE
.
.         UNPACK    SP5 WITH TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.         MOVE      "CL",TCODE
.         PACK      KEY5 WITH TCODE,PINVCLM
.         CALL      RDCODE1
.
.         MOVE      ZERO,COUNT
.CLMLOOP   ADD       ONE,COUNT
.         COMPARE   SIX,COUNT
.         GOTO      NOTACLM IF NOT LESS
.
.         LOAD      ANS USING COUNT OF TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
.         CMATCH    "W",ANS
.         GOTO      WORKCMP IF EQUAL
.
.         CMATCH    "M",ANS
.         GOTO      MABCODE IF EQUAL
.
.         CMATCH    "V",ANS
.         GOTO      VETCODE IF EQUAL
.
.         GOTO      CLMLOOP
.
NOTACLM   MOVE      SP6,AFUNDH
          MOVE      SP70,AFNDTB
          MOVE      SP70,WFDINS
          MOVE      PINVADM,KEY8
          CALL      RDMISS1
          IF        OVRCD=0
            MOVE      AFUNDH,WFDINS         * Health fund
          ENDIF
.         GOTO      OKWRT
.
.WORKCMP   MOVE      "(WC)  ",WFDINS
.         MOVE      PINVADM,KEY8
.         CALL      RDWCOM1
.         BRANCH    OVRCD OF OKWRT
.
.         MATCH     SP6,WCICODE
.         GOTO      OKWRT IF EQUAL
.
.         MOVE      WCICODE,WFDINS
.         GOTO      OKWRT
.
.MABCODE   MOVE      "(MAB) ",WFDINS
.         MATCH     SP6,CMABINS
.         GOTO      OKWRT IF EQUAL
.         MOVE      CMABINS,WFDINS
.         GOTO      OKWRT
.
.VETCODE   MOVE      "(VET) ",WFDINS
.         MATCH     SP6,CVETINS
.         GOTO      OKWRT IF EQUAL
.
.         MOVE      CVETINS,WFDINS
.
OKWRT     MOVE      WFDINS,WHFUND
          MOVE      AFNDTB,WHFNDT
          MOVE      SP70,WINVSYS
          MOVE      PINVSYS,WINVSYS
          CALL      WRWORK1
          CALL      WOSTD000
          GOTO      LOPINV
.
.      PRINT OUTSTANDING DEBTOR REPORT
.
ENDINV 
        MOVE         ZERO,TINVTOT
        MOVE         ZERO,TFUNP
        MOVE         ZERO,TINSP
        MOVE         ZERO,TPATP
        MOVE         ZERO,TJOUP
        MOVE         ZERO,TBALANCE
.
        MOVE         ZERO,GINVTOT
        MOVE         ZERO,GFUNP
        MOVE         ZERO,GINSP
        MOVE         ZERO,GPATP
        MOVE         ZERO,GJOUP
        MOVE         ZERO,GBALANCE
        MOVE         SP3,SAVCLM
.
        DISPLAY      *P1:24,*EL,*P10:24,*V2LON:
                     "** Generating Outstanding Debtor Report **":
                     *W2,*C,*EL
.
        DISPLAY      *P1:24,*EL,*P10:24:
                     "Printing Patient Name :"
.
        MOVE         ZERO,CPAGENO
        MOVE         "60",LNO
.
        PACK         KEY37 WITH SP30,SP5
        CALL         RDSWORK1
PRTLOP  CALL         RDKWORK1
        BRANCH       OVRCD OF EOPRT
. 
        PACK         DIM22 WITH WGNAME,DOT,WSNAME
        MOVE         WADMNO,DIM8
.
        DISPLAY      *P34:24,*V2LON,DIM22;
.
        COMPARE      ZERO,CPAGENO
        GOTO         NOTFST IF NOT EQUAL
.
        MOVE         WADMNO,PINVADM       **** SAVE ADMISSION NUMBER
        MOVE         WADMNO,DIM8
        GOTO         OKCONT
.
NOTFST  MATCH        WADMNO,PINVADM
        GOTO         NOTSAM IF NOT EQUAL
.
        MOVE         SP8,DIM8
        MOVE         SP30,DIM22
        GOTO         OKCONT
.
NOTSAM  MOVE         WADMNO,PINVADM       **** SAVE ADMISSION NUMBER
.
OKCONT  MULT         SEQ,WFUNP
        MULT         SEQ,WINSP
        MULT         SEQ,WPATP
        MULT         SEQ,WJOUP
.
        UNPACK       WIDATE,CCENT,CYEAR,CMON,CDAY
        PACK         DIM10,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
        RESET        DIM10
        REP          " 0",DIM10
.
        BRANCH       OPTION,OKCNT1
.
        MATCH        WCLAIM,SAVCLM
        GOTO         OKCNT1 IF EQUAL
.
        MOVE         WCLAIM,SAVCLM
.
        COMPARE      ZERO,CPAGENO
        GOTO         OKCNT1 IF EQUAL
.
        CALL         SUBTOT
        CALL         PRTHED0
.
OKCNT1  COMPARE      LN55,LNO
        CALL         PRINTH IF NOT LESS
.
.       PRINT        *1,"!",DIM8,"  ! ",WURNO," !",WINVNO," !",WINVTOT:
.                    " ! ",DIM10," !",WFUNP," ! ",WINSP:
.                    " ! ",WPATP," !",WJOUP," !",WBALANCE," !  ",WCLAIM:
.                    *132,"!"
.
        MOVE         SP70,KEY1
        MATCH        "1",WINVSYS
        IF           @EQUAL
          MOVE         "*",KEY1
        ENDIF
        PRINT        *1,"!",DIM8,KEY1,"!",WURNO,"!",WINVNO,"!",WINVTOT:
                     "!",DIM10,"!",WFUNP,"! ",WINSP:
                     "! ",WPATP," !",WJOUP," !",WBALANCE,"!",WCLAIM:
                     "!",WHFUND,"!",WHFNDT:
                     *132,"!"
.
        ADD          WINVTOT,TINVTOT
        ADD          WFUNP,TFUNP
        ADD          WINSP,TINSP
        ADD          WPATP,TPATP
        ADD          WJOUP,TJOUP
        ADD          WBALANCE,TBALANCE
.
        ADD          ONE,LNO
        GOTO         PRTLOP
.
EOPRT   COMPARE      ZERO,CPAGENO
        GOTO         NODEBT IF EQUAL
.
        COMPARE      ONE,OVRCD
        GOTO         PRTLIN IF NOT EQUAL
.
        CALL         LINE
.
PRTLIN  BRANCH       OPTION OF PRTTOT,PRTGRD
.
PRTTOT  PRINT        *20,"!TOTAL   !",TINVTOT,"!",*52,"!",TFUNP:
                     "! ",TINSP,"!  ",TPATP:
                     "! ",TJOUP,"!",TBALANCE,"!   !      !",*132,"!"
.
        GOTO         CONPRT
.
PRTGRD  CALL         SUBTOT
        PRINT        *20,"!G/TOTAL !",GINVTOT,"!",*52,"!",GFUNP:
                     "! ",GINSP,"!  ",GPATP:
                     "! ",GJOUP,"!",GBALANCE,"!   !      !",*132,"!" 
.
CONPRT  PRINT        *20,"*-----------------------------------------":
                          "-----------------------------------------":
                          "-----------------------------*":     
                     *N," * Denotes Emergency Patients":
                     *N,*N,"*** End Of Report ***"
.
        DISPLAY      *P1:24,*EL,*P10:24,*V2LON:
                     "** Generation Completed Successfully **":
                     *W2,*C,*EL
        CALL         DELETWRK
        GOTO         BEGIN
.
SUBTOT  COMPARE      ONE,OVRCD
        GOTO         SBTOT1 IF EQUAL
.        
        CALL         LINE
.
SBTOT1  PRINT        *20,"!TOTAL   !",TINVTOT,"!",*52,"!",TFUNP: 
                     "! ",TINSP,"!  ",TPATP:
                     "! ",TJOUP,"!",TBALANCE,"!   !      !",*132,"!":
                     *N,*20,"*----------------------------------------":
                          "-----------------------------------------":
                          "------------------------------*"     
.
        ADD          TINVTOT,GINVTOT
        ADD          TFUNP,GFUNP
        ADD          TINSP,GINSP
        ADD          TPATP,GPATP
        ADD          TJOUP,GJOUP
        ADD          TBALANCE,GBALANCE
.
        MOVE         ZERO,TINVTOT
        MOVE         ZERO,TFUNP
        MOVE         ZERO,TINSP
        MOVE         ZERO,TPATP
        MOVE         ZERO,TJOUP
        MOVE         ZERO,TBALANCE
        RETURN
.
NODEBT  DISPLAY      *P1:24,*EL,*P10:24,*V2LON:
                     "** No Outstanding Debtors **":
                     *W3,*C,*EL
.
        CLOSE        OUTDFILE
        PREP         OUTDFILE,FNAMED
        CLOSE        OUTDFILE
        GOTO         BEGIN
.
PRINTH  COMPARE     ZERO,CPAGENO
        CALL        LINE IF NOT EQUAL
.
PRTHED0 UNPACK      FROMDATE,XCENT,XYEAR,XMON,XDAY
        UNPACK      TODATE,XCENT1,XYEAR1,XMON1,XDAY1
        BRANCH      OPTION OF PRTHED1,PRTHED2
.
PRTHED1 MOVE        ONE,CNOUNDLN
        MOVE        "SURNAME SEQUENCE",CPHDROPT
        CALL        IBACLOCK
        CALL        HEAD132
        PRINT       *N,*40,"From Date : ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    " to ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1,*N;
        GOTO        CPRINTH
.
PRTHED2 MOVE        ONE,CNOUNDLN
        MOVE        SP20,TDESC
.
        MATCH       SP3,WCLAIM
        GOTO        PRTHED3 IF EQUAL
.
        PACK        KEY5 WITH CODECL,WCLAIM
        CALL        RDCODE1
.
PRTHED3 CALL        IBACLOCK
        CALL        HEAD132
        PRINT       *40,"SURNAME SEQUENCE ":
                        "WITH CLAIM CODE":
                    *N,*40,"CLAIM CODE - ",WCLAIM,"   ",TDESC,*N
.
CPRINTH CALL        PRTSEL
        CALL        LINE
.
        PRINT       *1,"! Adm No  !U/R No. !Invoice !":
                    "   Invoice !Invoice   !":
                    "   Fund  ! Insurance!  Patient  !    Journal":
                    "  !   Balance  !Clm|Health| HF",*132,"!",*N:
                    *1,"!",*11,"!",*20,"! Number !     Total ! Date     !":
                    "  Paid   !  Paid    !  Paid     !    Amount":
                    "   !   Amount   !Typ!Fund  |Table",*132,"!"
        CALL        LINE
.
        MOVE        TEN,LNO
        RETURN
.
LINE    PRINT       *1,"*-----------------------------":
                       "------------------------------":
                       "------------------------------":
                       "------------------------------":
                       "-----------*"
        RETURN
.
INVMAST DISPLAY    *P1:23,*EL,*B,"U/R No.: ",*V2LON,PINVUR:
                   "  *** Master File Missing ":
                     "- Contact IBA *** ",*W2;
        GOTO       LOPINV
.
.
WOSTD000 MULT      "-1",PINVPATA
         MOVE      WBALANCE,WPATAMNT
         MOVE      ZERO,WREBAMNT
.
.        If no Rebate invoice, have all the outstanding amount to patient inv.
.
         COMPARE   ZERO,PINVREB
         GOTO      WOSTD600 IF EQUAL
.
         MOVE      PINVAMT,FORM12P2
         SUB       PINVREB,FORM12P2
.
.        If no Patient invoice, have all the outstanding amount to hf inv.
.
         COMPARE   ZERO,FORM12P2
         GOTO      WOSTD500 IF EQUAL
.
.        If patient paid more than the patient invoice,
.        have the outstanding to hf invoice
.
         COMPARE   FORM12P2,PINVPATA
         GOTO      WOSTD500 IF NOT LESS
.
         SUB       PINVPATA,FORM12P2            * o/s amount to patient invoice
.
         IF        WPATAMNT<FORM12P2
           MOVE      ZERO,WREBAMNT
           GOTO      WOSTD600
         ENDIF
.
         MOVE      WPATAMNT,WREBAMNT
         SUB       FORM12P2,WREBAMNT            * hf invoice
         MOVE      FORM12P2,WPATAMNT            * patient invoice
         GOTO      WOSTD600
.
WOSTD500 MOVE      WPATAMNT,WREBAMNT            * all o/s amount to hf invoice
         MOVE      ZERO,WPATAMNT
.
WOSTD600 CALL      WRTOUTD
WOSTD999 RETURN
.
.
RDSWORK1 RESET     KEY37
         READ      WORKFILE,KEY37;;
         RETURN  
.
RDWORK1  MOVE      ZERO,OVRCD
         RESET     KEY37
         READ      WORKFILE,KEY37;WSNAME,WGNAME,DWADMNO,WINVNO,WINVTOT:
                                  WFDINS,WFUNP,WINSP,WPATP,WJOUP,WBALANCE:
                                  WCLAIM,WURNO,WIDATE,WHFUND,WHFNDT,WINVSYS
         GOTO      OVERCOND IF OVER
         MOVE      DWADMNO,WADMNO
         RETURN  
.
RDKWORK1 MOVE      ZERO,OVRCD
         READKS    WORKFILE;WSNAME,WGNAME,DWADMNO,WINVNO,WINVTOT:
                            WFDINS,WFUNP,WINSP,WPATP,WJOUP,WBALANCE:
                            WCLAIM,WURNO,WIDATE,WHFUND,WHFNDT,WINVSYS
         GOTO      OVERCOND IF OVER
         MOVE      DWADMNO,WADMNO
         RETURN  
.
.
WRWORK1  RESET     KEY37
         MOVE      ZERO,OVRCD
         WRITE     WORKFILE,KEY37;KEY37,WINVTOT:
                            WFDINS,WFUNP,WINSP,WPATP,WJOUP,WBALANCE:
                            WCLAIM,WURNO,WIDATE,WHFUND,WHFNDT,WINVSYS
         RETURN  
.
UPWORK1  UPDATE    WORKFILE;WSNAME,WGNAME,DWADMNO,WINVNO,WINVTOT:
                            WFDINS,WFUNP,WINSP,WPATP,WJOUP,WBALANCE:
                            WCLAIM,WURNO,WIDATE,WHFUND,WHFNDT,WINVSYS
         RETURN  
.
.
CREATWRK MOVE       ZERO,OVRCD
.
         CALL       DELETWRK
         PREP       ROLLFILE,TEMPFILE
.
         BRANCH     OPTION OF IDXSUR
.
         WRITE      ROLLFILE,SEQ;"isbuild ",FNAMET," 113 ","uc79-82,1-37"
         GOTO       IDXNOW
.
IDXSUR   WRITE      ROLLFILE,SEQ;"isbuild ",FNAMET," 113 ","uc1-37"
.
IDXNOW   WEOF       ROLLFILE,SEQ
         CLOSE      ROLLFILE
         CALL       MAINWRK1
         OPEN       WORKFILE,FNAMET
         PREP       OUTDFILE,FNAMED
         RETURN
.
.
DELETWRK CLOSE      WORKFILE
         CLEAR      CMDLINE
         APPEND     "iserase ",CMDLINE
         APPEND     FNAMET,CMDLINE
         RESET      CMDLINE
         EXECUTE    CMDLINE,TASKID
         RETURN
.
MAINWRK1 CLEAR      CMDLINE
         APPEND     "sh ",CMDLINE
         APPEND     TEMPFILE,CMDLINE
         APPEND     ".txt",CMDLINE
         RESET      CMDLINE
.
         DISPLAY    *P1:18,*EF;
         EXECUTE    CMDLINE,TASKID
         DISPLAY    *P1:18,*EF;
.
         PREP       ROLLFILE,TEMPFILE
         CLOSE      ROLLFILE
         RETURN
.
PRTSEL    BRANCH    PATSYST,AEHEAD,IPHEAD
.
          PRINT     *45,"A&E AND INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
AEHEAD    PRINT     *45,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          GOTO      PRTSEL9
.
IPHEAD    PRINT     *45,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
PRTSEL9   RETURN 
.
.*******************************************************************************
WRTOUTD  WRITE     OUTDFILE,SEQ;DWADMNO,WURNO,WINVNO,WIDATE:
                                WPATAMNT,WREBAMNT,WCLAIM,WHFUND,WHFNDT,WINVSYS
         RETURN
.
.
         INC        STD001IO
         INC       PATCODKY
         INCLUDE    PATSELFN
         INC       PATCODIO/INC
         INC       PATWC1IO/INC
         INC       PATMA1IO/INC
         INC       PMSVX1IO/INC
         INC       PATMI1IO/INC
         INC       PATINVIO/INC
         INC       OUTSITIO/INC
         INC       IBAPASIO/INC
         INC       IBASECIO/INC
.
ENDIT    CHAIN     PGM
         STOP
.

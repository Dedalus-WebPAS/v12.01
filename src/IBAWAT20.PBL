.******************************************************************************
.* System     :    Patient Review Report                                      *
.* Program    :    IBAWAT20                                                   *
.* Desc       :    List of patients                                           *
.******************************************************************************
.* Date       :    20/10/1988                                                 *
.* Author     :    N.S.                                                       *
.* Comments   :                                                               *
.* Mods.      :                                                               *
.******************************************************************************
.* V12.00.01 21/05/2025 Thanh T         TSK 0955096                           *
.*           Removed SWMBOOK since it is not used                             *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V9.02.01  01/11/2001  Greg Horvat                                   *
.*                  Recompiled for ACTCOMN1 - Added xtra option to the        *
.*                  parameter PNSWRACE                                        *
.*        V5.10.B01 26/03/2001  Glenn Saunders                                *
.*                  Remove all references to PATSUR file (old surname file).  *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                   Fixed global recompile bugs                              *
.******************************************************************************
.*                  22/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*        V5.01.10  24/05/90 P Duncan                                         *
.*                  Converted to new system specs                             *
.*                  10/09/1990 Bill Dew                                       *
.*                  Fix minor screen problems and set keyins to standards.    *
.*                  25/07/90 Paul Duncan                                      *
.*                  implemented second surname file                           *
.*                  09/08/90 Justin Coates                                    *
.*                  fixed minor print problems and standardized the code      *
.*        V5.01.121 25/01/91 Justin Coates                                    *
.*                  Added BOKRC1FD/IO                                         *
.*        V5.01.122 23/05/91  Steve O'Gorman                                  *
.*                  Fixed Errors found by the New Compiler                    *
.*        V5.01.123 02.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Reformat print for new address lengths                    *
.*        V5.01.124 10.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Print comments instead of address if WTCN20CM=1           *
.*        V5.01.125 11/03/1993    Justin "I Pooch Male Dogs" Coates  (DUDLEY) *
.*                  Separate report for suspended/unsuspended patients        *
.*        V5.01.126 16.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Change to print W/L Comments instead of U/R Comments      *
.*        V5.01.127 26/08/93  ROD                                             *
.*                  Recompile for NZHIS                                       *
.*        V5.01.128 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.04.01  25/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       BOKRC1FD/INC            * booking file
          INC       IBASEQFD/INC
          INC       OUTPREFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCOMM/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATDO1FD/INC
          INC       PATGSRFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC
          INC       WATMESFD/INC
          INC       WATCONFD/INC
          INC       WATSUSFD/INC
          INC       WATTR1FD/INC
          INC       WEBERRFD/INC
.
.         VARIABLE DECLARATIONS
.
FROMDATE  DIM       8        * FROMdate ccyymmdd
TODATE    DIM       8        * TOdate ccyymmdd
TODAY     DIM       8        * current date ccyymmdd
DATELIS   FORM      3         * months on list
DOCCODE   DIM       6
.
BJDAY     FORM      3
CJDAY     FORM      3
CMDLINE   DIM       80
FNAME1    DIM       8
FNAME2    DIM       8
FORM3     FORM      3
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
SCNDGNAM  DIM       40
STATUS    FORM      1
SEQ1      FORM      8
.
DESCOPT1  INIT      "Priority Code : "
DESCOPT2  INIT      "Unit/Clinic Code : "
DESCOPT3  INIT      "Doctor Code : "
INDSORT1  DIM       30
TBLEHEAD  DIM       49        * table heading
INDOCNAM  DIM       30
SINDOCNM  DIM       30
KURNOPT   FORM      1
DISP3     INIT      "DOCTOR : "
UNIQUE    FORM      8
DUNIQUE   DIM       8
.
WTSPDTCK  DIM       8
WTSPSTRT  DIM       8
WTSPSTOP  DIM       8
WTWMHOS   FORM      3      3         108     times cancelled by hosp.
WTWMPAT   FORM      3      3         110     times cancelled by patient
WTWMOTH   FORM      3      3         112     times cancelled by other
. ----- EOR 342 ---
.
PWMPTY    DIM       3        * previous wmpty
PWMUNIT   DIM       3        * previous wmunit
PWMDOCTR  DIM       6        * previous wmdoctor
TEMPFLG   FORM      1        * =0 nothing in tempfile    =1 tempfile not empty
PATCNT    FORM      5        * counts number of patients with multiples
TOTPAT    FORM      5        * total patients printed
.
PWMURNO   DIM       8        * previous ur number
PWMCNT    FORM      2        * previous count
DIMURNO   DIM       8        * wmurno in a DIM8 for print
URSTART   DIM       8        * start UR in range keyed in
UREND     DIM       8        * end UR in range keyed in
START     DIM       8
END       DIM       8
.
PNAME     DIM       50        * patient name to print (surname title givname)
FRNAM     DIM       45        * patient name to print (surname title givname)
TONAM     DIM       45        * patient name to print (surname title givname)
PATDOB    DIM       10        * patient DOB to print
ADDRESS   DIM       50        * padd1,psubrb,ppost
DIM31     DIM       31        * store wmdesc for print
PROPT     DIM       44        * used in report
UNITCLIN  DIM       3        * key in for what unit ( default is ALL )
DOCTOR    DIM       6        * key in for what doctor ( default is ALL )
UNITDESC  DIM       20        * descr unit (default = ALL)
DESCSEX   DIM       8         * sex descr (M/F)               * was 6  *C-49016
DOCTNAME  DIM       30        * descr doctor (default = ALL)
DSUNIT    DIM       20        * unit descr in print
DSPTY     DIM       20        * priority descr in print
DATE10    DIM       10        * wmdate1 (dd/mm/ccyy)
DATE20    DIM       10        * wmdate2 (dd/mm/ccyy)
DATE40    DIM       10        * wmdate4 (dd/mm/ccyy)
SCRNFLAG  FORM      1
NMPNUMB   DIM       20
SUSPTYPE  FORM      1         * 0=print all,1=unsuspend,2=suspended
CODEWU    INIT      "WU"       * category WU (unit)
CODETP    INIT      "TP"       * category TP (priority)
SP19      INIT      "                   "         
SP16      INIT      "                "
SP23      INIT      "                      "
CODE      DIM       2
DIM3      DIM       3
.
.
SWMURNO   DIM       8        * save variables for WATTR1FD WM.. into SWM..
SWMCODE   DIM       9
SWMCNT    FORM      2
SWMDESC   DIM       50
SWMTIME   FORM      5
SWMREAS   DIM       40
SWMDATE4  DIM       8
SWMREMOV  DIM       3
SWMUDEF1  DIM       3
SWMUDEF2  DIM       3
SWMUDEF3  DIM       3
SWMUDEF4  DIM       3
SWTWMSPAR  DIM       12
SWMSTAT   FORM      1
SWMDATE1  DIM       8
SWMDATE2  DIM       8
SWMDATE3  DIM       8
SWMUNIT   DIM       3
SWMDOCT   DIM       6
SWMSTAY   FORM      5
SWMPTY    DIM       3
.
THOME     INIT      "Home "
LHOME     INIT      "     "
TBUSIN    INIT      "Bus "
LBUSIN    INIT      "    "
TSEX      INIT      "Sex  : "
LSEX      INIT      "       "
TDOB      INIT      "D.O.B.   : "
LDOB      INIT      "           "
.
.
SRTKEY    DIM       40
WATMP1    IFILE     SQL, FIXED=214, KEY=SRTKEY
SORT1     INIT      "U20-22,72-79,1-8,9-17,18-19"
SORT2     INIT      "U79-81,20-22,72-79,1-8,9-17,18-19"
SORT3     INIT      "U23-28,20-22,72-79,1-8,9-17,18-19"
XDESC     DIM       11
TEMPPIPE  INIT      "|"
.
PRGID     INIT      "IBAWAT20"
PRGNAM    INIT      "Patient Review Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
ML0000    CALL      INIT0000                * open files     
.
ML0100    CALL      OPTN0000                * select options
          BRANCH    EXIT,ML9999
.
ML1000    CALL      CLRV0000                * clear the variables
          CALL      SCRN0000                * display srceen prompts
          CALL      KDAT0000                * keyin from/to date
          BRANCH    EXIT,ML0100
          CALL      RANG0000                * keyin UR range
          CALL      GUNT0000                * keyin unit code
          CALL      GDOC0000                * keyin doctor code
          CALL      CONT0000                * ok to continue ?
          BRANCH    EXIT,ML1000,ML0100
          CALL      TEMP0000                * create tempfile 
          CALL      CLR0000                 * clear vars patmas, wait 
.
          CALL      PROC0000                * read wattrefd, write watmp1
          BRANCH    EXIT,ML0100             * empty tempfile
.
          CALL      PRNT0000                * print the report
          GOTO      ML0100
.
ML9999    CALL      DLTP0000
          CHAIN     PGM
          STOP
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialisation , opening files                             *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CNOUNDLN
.
          DISPLAY   *P56:24,"Opening wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A2,"wattr1af"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"watmesaf";
          OPEN      WATMESA1,"watmesaf"
.
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          MOVE      ONE,CNEWDS
          READ      CONTROLF,THIRTY7;*214,WTCNUSOP,*219,WTCN20CM
          PACK      WTCN20CM,WTCN20CM,SP70
          REP       " 0",WTCN20CM
.
          MATCH     "1",WTCN20CM                   * print comments ?
          IF        @EQUAL
            MOVE      SP70,TEMPPIPE
          ENDIF
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME1
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML0100   *
.*        Option menu, set up subheadings                            *
.*********************************************************************
OPTN0000  MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EL
          CALL      SCRO0000                       * display main optionscreen
          DISPLAY   *P1:9,"Select Option : ";
.
OPTN1000  MOVE      FALSE,EXIT
          KEYIN     *P17:9,*DV,UNDLN1:
                    *P17:9,*V2LON,MOPTION:
                    *P17:9,*DV,MOPTION
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    MOPTION,OPTN1500,OPTN1500,OPTN1500
.
          BEEP                              * invalid answer
          GOTO      OPTN1000
.
OPTN1500  BRANCH    MOPTION,OPTN2000,OPTN3000
.
          DISPLAY   *P51:2,*V2LON,"- by Doctor Code "
          MOVE      "Doctor Code Sequence",CPHDROPT  * set up print subheading
          GOTO      OPTN9999
.        
OPTN2000  DISPLAY   *P51:2,*V2LON,"- by Priority Code "
          MOVE      "Priority Code Seq.  ",CPHDROPT  * set up print subheading
          GOTO      OPTN9999
.
OPTN3000  DISPLAY   *P51:2,*V2LON,"- by Unit/Clinic Code "
          MOVE      "Unit/Clinic Code Seq",CPHDROPT  * set up print subheading
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  SCRO0000                    Called by : ML1000   *
.*          Main option-screen                                       *
.*********************************************************************
SCRO0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Priority Code Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Unit/Clinic Code Sequence":
                    *P1:7,*V2LON,THREE,*HOFF," = Doctor Code Sequence"
SCRO9999  RETURN
+
.*********************************************************************
.*                  SCRN0000                    Called by : ML1000   *
.*        Second lot of inputs for user                              *
.*********************************************************************
SCRN0000  DISPLAY   *P1:12,*EF:
                    *P1:12,"From Date on list :":
                    *P1:13,"To   Date on list :":
                    *P1:15,"From U/R Number   :":
                    *P1:16,"To   U/R Number   :":
                    *P1:18,"Unit/Clinic Code  :":
                    *P1:20,"Doctor code       :"
SCRN9999  RETURN
+
.*********************************************************************
.*                  CLRV0000                    Called by : ML1000   *
.*        Clear the variables                                        *
.*********************************************************************
CLRV0000  UNPACK    SP30,FROMDATE,TODATE
          UNPACK    SP30,START,END
          PACK      FRNAM,SP30,SP30
          PACK      TONAM,SP30,SP30
          UNPACK    SP30,UNITCLIN,UNITDESC
          PACK      DOCTNAME,SP30,SP30
          MOVE      SP6,DOCTOR
CLRV9999  RETURN 
+
.*********************************************************************
.*                  KDAT0000                    Called by : ML1000   *
.*        keyin the dates for the search                             *
.*        date on list (=wmdate1) should be between these dates      *
.*********************************************************************
KDAT0000  MOVE      FALSE,EXIT                        * reset exit 
.
.         enter the from date
.
KDAT7000  MOVE      TWENTY1,CCOL
          MOVE      TEN2,CVERT
          UNPACK    SP6 INTO CYEAR,CMON,CDAY
.
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
          CALL      KEYDATE                 * enter the date
          BRANCH    OVRCD,KDAT7500
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.         PACK      DATE10,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.         REP       " 0",DATE10
.
.         enter the from date
.
KDAT7200  MOVE      TWENTY1,CCOL
          MOVE      TEN3,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
.
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD OF KDAT9000
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.         PACK      DATE20,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.         REP       " 0",DATE20
.
          MATCH     FROMDATE,TODATE         * is to date < from date ??
          GOTO      KDAT8000 IF LESS        * yes so invalid
.
          GOTO      KDAT9999                * valid dates
.
.         only enter hit to from date so display start
.
KDAT7500  MOVE      SP20,FROMDATE
          DISPLAY   *P21:12,*EL,*V2LON,"Start"
          GOTO      KDAT7200
.
.         ERROR: from date is before to date
.
KDAT8000  DISPLAY   *P1:24,*B,"Invalid Date range.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT0000
.
KDAT9000  MOVE      TRUE,EXIT               * no to date entered
.
KDAT9999  RETURN
+
.*********************************************************************
.*                  RANG0000                    Called by : ML1000   *
.*        Keyin range of UR (from - to)                              *
.* Comment 11/07/1990 Bill Dew : This routine was stuffed            *
.*                               until I fixed it                    *
.*********************************************************************
RANG0000   DISPLAY    *P21:15,*EL,UNDLN8
           MOVE       ZERO,EXIT                        * reset exit 
           MOVE       "       0",URSTART
           MOVE       "99999999",UREND
.
           MOVE       TEN5,CVERT
           MOVE       TWENTY1,CCOL
           MOVE       TWO,SRCHOPT
           MOVE       ZERO,KURNOPT
           CALL       KURN
           BRANCH     OVRCD,RANG5000
.
           COMPARE    ONE,EXIT
           GOTO       RANG0500 IF EQUAL
           MOVE       CPPURNO,URSTART
           MOVE       URSTART,START
           MOVE       PSNAME,PACSNAME     
           MOVE       PGNAME,PACGNAME
           MOVE       PTITL,PACTITLE
           MOVE       TWO,PACFRMT
           CALL       PACNAME
           MOVE       PACFNAME,FRNAM
           GOTO       RANG2000
.
RANG0500   MOVE       "Start  ",START
           PACK       PNAME,SP30,SP30
RANG2000
           DISPLAY    *P21:15,*EL,*V2LON,START,*HOFF,*P31:15,FRNAM:
                      *HOFF,*P21:16,*EL,UNDLN8
.
           MOVE       TEN6,CVERT
           MOVE       TWENTY1,CCOL
           MOVE       TWO,SRCHOPT
           MOVE       ONE,KURNOPT
           CALL       KURN
           BRANCH     OVRCD,RANG5111
           COMPARE    ONE,EXIT
           GOTO       RANG2500 IF EQUAL
           MOVE       CPPURNO,UREND
.
           MOVE       UREND,END
           MOVE       PSNAME,PACSNAME     
           MOVE       PGNAME,PACGNAME
           MOVE       PTITL,PACTITLE
           MOVE       TWO,PACFRMT
           CALL       PACNAME
           MOVE       PACFNAME,TONAM
           GOTO       RANG4000
RANG2500
           MOVE       "Finish",END
           PACK       PNAME,SP30,SP30
RANG4000
           DISPLAY    *PTWENTY1:16,*EL,*V2LON,END,*HOFF,*P31:16,TONAM,*HOFF
           GOTO       RANG9999
.
RANG5000
          DISPLAY   *P1:24,*EL,"Invalid U/R number for waiting lists file.  ";
          CALL      HITENTER
          GOTO      RANG0000
RANG5111
          DISPLAY   *P1:24,*EL,"Invalid U/R number for waiting lists file.  ";
          CALL      HITENTER
          GOTO      RANG2000
.
RANG9999   RETURN
+
.*********************************************************************
.*                  GUNT0000                                         *
.*   get unit / clinic code                                          *
.*   Created by Bill Dew 11/07/1990                                  *
.*********************************************************************
GUNT0000
          MOVE      ONE,SCRNFLAG       * set the redisplay flag
GUNT1000
          DISPLAY   *P1:18,*EL,"Unit/Clinic Code  : ___",*P21:18;
GUNT1111  
          KEYIN     *V2LON,UNITCLIN    * get unit clinic code
          ENDSET    UNITCLIN
          APPEND    SP3,UNITCLIN
          RESET     UNITCLIN
.
          MATCH     SP3,UNITCLIN       * test for return hit
          GOTO      GUNT7000 IF EQUAL
          CMATCH    EXITCHAR,UNITCLIN  * test for return hit
          GOTO      GUNT7777 IF EQUAL
.
          CMATCH    "?",UNITCLIN       * test for ? routine
          GOTO      GUNT6666 IF NOT EQUAL
          PACK      CODE,ANSW,ANSU
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG      * signal requiring redisplay
GUNT4444
          DISPLAY   *P1:24,*EL,"Unit/Clinic : ___",*P15:24;
          GOTO      GUNT1111
GUNT6666
          PACK      KEY5,ANSW,ANSU,UNITCLIN,SP5
          CALL      RDCODE1
          BRANCH    OVRCD,GUNT8888
          CALL      REDP0000           * do redisplay if needed
          DISPLAY   *P21:18,*V2LON,UNITCLIN,*HOFF,*P31:18,TDESC
          MOVE      TDESC,UNITDESC
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      GUNT9999
GUNT7000  
          CALL      REDP0000
          DISPLAY   *P21:18,*V2LON,*EL,"All Unit/Clinic codes"
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          BRANCH    SCRNFLAG,GUNT9999
          GOTO      GUNT0000
.
GUNT7777  
          DISPLAY   *P1:18,*EL
          MOVE      ONE,EXIT           * exit=1 if return hit
          GOTO      GUNT9999
GUNT8888
          DISPLAY   *P1:24,*B,*EL,"Unit/Clinic code not on file.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,GUNT1000
          GOTO      GUNT4444
.
GUNT9999  RETURN
+
.*********************************************************************
.*                  GDOC0000                                         *
.*        get doctor code                                            *
.*        Created by Bill Dew 11/07/1990                             *
.*********************************************************************
GDOC0000  DISPLAY   *P1:20,*EL,"Doctor code       : ______"
          MOVE      "21",ECOL
          MOVE      "20",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,GDOC7000,GDOC7777
.
          MOVE      DCODE,DOCTOR
          PACK      KEY6,DOCTOR,SP6
          CALL      DOCN0000  
          BRANCH    EXIT,GDOC8888
.
          DISPLAY   *P21:20,*V2LON,DOCTOR,*HOFF,*P31:20,DOCTNAME
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      GDOC9999
.
GDOC7000  DISPLAY   *P21:20,*V2LON,*EL,"All Doctors"
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          MOVE      SP10,DOCTOR
          GOTO      GDOC9999
.
GDOC7777  DISPLAY   *P1:20,*EL         * clear the above prompt
          MOVE      ONE,EXIT           * exit=1 if return hit
          GOTO      GDOC9999
.
GDOC8888  DISPLAY   *P1:24,*B,*EL,"Doctor Code not on file.  ";
          CALL      HITENTER
          GOTO      GDOC0000
.
GDOC9999  RETURN
+
.*********************************************************************
.*                  DOCN0000                                         *
.*        Setup doctors name in DOCTNAME                             *
.*********************************************************************
DOCN0000  CALL      RDDOCT1
          BRANCH    OVRCD,DOCN9990     * tell user his code not on file
.
DOCN1111  MOVE      TWO,PACFRMT        * title name surname
          MOVE      DSNAME,PACSNAME    * use pacdate to fix doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,DOCTNAME
          MOVE      ZERO,EXIT
          GOTO      DOCN9999
.
DOCN9990  MOVE      ONE,EXIT
          MOVE      "invalid doctor code",DOCTNAME
.
DOCN9999  RETURN
+
.*********************************************************************
.*                  REDP0000                                         *
.*        Do rediplay after / routine                                *
.*********************************************************************
REDP0000  BRANCH    SCRNFLAG,REDP9999
.
REDP1000
          CALL       SCRO0000
          DISPLAY    *P1:9,*HOFF,"Select Option : ",*P17:9,*V2LON,MOPTION
.
          CALL       SCRN0000          * display screen prompts
.
          MATCH      SP8,FROMDATE      * test for start in fromdate
          GOTO       REDP1111 IF NOT EQUAL
          DISPLAY    *P21:12,*V2LON,"Start"
          GOTO       REDP2222
.
REDP1111  MOVE       TWENTY1,CCOL
          MOVE       TEN2,CVERT
.
          MOVE       "40",ECOL
          MOVE       CVERT,EVERT
          MOVE       CCC,CCENT
          UNPACK     FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL       DSPDATE
.
REDP2222  MOVE       TWENTY1,CCOL
          MOVE       TEN3,CVERT
.
          MOVE       "40",ECOL
          MOVE       CVERT,EVERT
          MOVE       CCC,CCENT
          UNPACK     TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL       DSPDATE
.
          DISPLAY    *P21:15,*V2LON,START,*HOFF,*P31:15,FRNAM:
                     *P21:16,*V2LON,END,*HOFF,*P31:16,TONAM
.
          MATCH     SP3,UNITCLIN            * was all entered ??
          GOTO      REDP3000 IF NOT EQUAL   * no
.
          DISPLAY   *P21:18,*V2LON,*EL,"All Unit/Clinic codes"
          GOTO      REDP9999
.
REDP3000  DISPLAY   *V2LON,*P21:18,UNITCLIN,*HOFF,*P31:18,UNITDESC
.
REDP9999  RETURN
+
.*********************************************************************
.*                  CONT0000                                         *
.*        Ok to continue ? (Y/N)                                     *
.*********************************************************************
CONT0000  MOVE       FALSE,EXIT                         * reset exit
          KEYIN      *P1:24,*EL,"Ok to Continue (":
                     *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                     *V2LON,*DV,ANSN,*HOFF,*DV,SLASH:
                     *V2LON,*DV,ANSC,*HOFF,") ? ",*V2LON,ANS;
          REPLACE   UPPLOW,ANS
.
          CMATCH     ANSN,ANS
          GOTO       CONT9000 IF EQUAL
          CMATCH     ANSY,ANS
          GOTO       CONT9999 IF EQUAL
          CMATCH     ANSC,ANS
          GOTO       CONT8000 IF EQUAL
          BEEP
          GOTO       CONT0000
.
CONT8000  MOVE       TWO,EXIT
          GOTO       CONT9999
.
CONT9000  MOVE       TRUE,EXIT
          MOVE       SP30,FRNAM
          MOVE       SP30,TONAM
.
CONT9999  RETURN
+
.***************************************************************************
.*                  TEMP0000                                               *
.*        Open tempfile for U/R     sequence :                             *
.*                          Surname sequence :                             *
.*        Kill indexed temp files , and create new indexed tempfiles       *
.***************************************************************************
TEMP0000  CALL      DLTP0000
.
          LOAD      SRTKEY USING MOPTION OF SORT1,SORT2,SORT3
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 214 ",CMDLINE
          APPEND    SRTKEY,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
. ----- Open tempfile -----
.
          OPEN      WATMP1,FNAME1
.
TEMP9999  RETURN
+
.*****************************************************************************
.*        Clear PATMA1FD variables                                           *
.*****************************************************************************
CLR0000  MOVE      SP30,PURNO
CLR1000  MOVE      SP30,WMURNO
CLR9999  RETURN
+
.*****************************************************************************
.*        Clear Save variables SWM..                                         *
.*****************************************************************************
CLR5000  MOVE      SP30,SWMURNO
         RETURN
.
.****************************************************************************
.*        Save WATTR1FD variables  (WM.. into SWM..)                        *
.****************************************************************************
.
SAV1000  MOVE      WMURNO,SWMURNO

         RETURN
.
.*********************************************************************
.*                  PROC0000                                         *
.*        Read WATTR1FD and write to WATMP1/ WATMP2                  *
.*********************************************************************
PROC0000  MOVE      ZERO,TEMPFLG
          MOVE      ONE,FORM1
          MOVE      ZERO,UNIQUE
          LOAD      INDSORT1 USING MOPTION OF WMPTY,UNITCLIN,DOCTNAME
          MOVE      FALSE,EXIT
          MOVE      SP20,PWMURNO
          PACK      TODAY WITH CCC,CYY,CMM,CDD
          PACK      KEY19 WITH SP19
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON,"U/R number : "
          MOVE      FIVE,CCOL
          CALL      RDSTREA1
.
.         read next record
.
PROC1000  CALL      RDKTREA1
          BRANCH    OVRCD,PROC7000
.
          DISPLAY   *P54:24,*EL,WMURNO
.
          PACK      KEY8,WMURNO             * UR number as key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,PROC1000          * invalid UR number
.
          COMPARE   ONE,WMSTAT              * is it an unxcheduled patient ?
          GOTO      PROC1000 IF NOT EQUAL   * no
.
          MATCH     URSTART,WMURNO
          GOTO      PROC1000 IF LESS            * UR < start UR
.
          MATCH     WMURNO,UREND
          GOTO      PROC7000 IF LESS            * UR > end UR
.
          MATCH     FROMDATE,WMDATE1
          GOTO      PROC1000 IF LESS            * date1 < fromdate
.
          MATCH     WMDATE1,TODATE
          GOTO      PROC1000 IF LESS            * date1 > todate
.
          MATCH     SP3,UNITCLIN
          GOTO      PROC2000 IF EQUAL
.
          MATCH     UNITCLIN,WMUNIT
          GOTO      PROC1000 IF NOT EQUAL           * not right unitcode
.
PROC2000  MATCH     SP6,DOCTOR
          GOTO      PROC5000 IF EQUAL
          MATCH     DOCTOR,WMDOCTOR
          GOTO      PROC1000 IF NOT EQUAL           * not right doctorcode
.
PROC5000  ADD       ONE,UNIQUE
          MOVE      UNIQUE,DUNIQUE
.
          BRANCH    MOPTION TO PROC5005,PROC5010,PROC5015
.
.         write to temp file using priority as key
.
PROC5005  PACK      KEY30 WITH WMPTY,WMDATE1,WMURNO,WMCODE,WMCNT 
          RESET     KEY30
          MOVE      WMCNT,DWMCNT
          WRITE     WATMP1,KEY30;WMURNO,WMCODE,DWMCNT,WMPTY,WMDOCTOR,WMDESC:
                                 WMUNIT:
                                 WMSTAY,WMTIME,WMDATE1,WMDATE2,DATELIS,WTWMHOS:
                                 WTWMPAT,WTWMOTH
          DISPLAY   *P54:24,*EL,*V2LON,WMURNO
          GOTO      PROC5050
.
.         write to temp file using unit/clinic as key
.
PROC5010  PACK      KEY33,WMUNIT,WMPTY,WMDATE1,WMURNO,WMCODE,WMCNT
          RESET     KEY33
          MOVE      WMCNT,DWMCNT
          WRITE     WATMP1,KEY33;WMURNO,WMCODE,DWMCNT,WMPTY,WMDOCTOR,WMDESC:
                                 WMUNIT:
                                 WMSTAY,WMTIME,WMDATE1,WMDATE2,DATELIS,WTWMHOS:
                                 WTWMPAT,WTWMOTH
          DISPLAY   *P54:24,*EL,*V2LON,WMURNO
          GOTO      PROC5050
.
.         write to temp file using doctor as key
.
PROC5015  PACK      KEY36 WITH WMDOCTOR,WMPTY,WMDATE1,WMURNO,WMCODE,WMCNT
          RESET     KEY36
          MOVE      WMCNT,DWMCNT
          WRITE     WATMP1,KEY36;WMURNO,WMCODE,DWMCNT,WMPTY,WMDOCTOR,WMDESC:
                                 WMUNIT:
                                 WMSTAY,WMTIME,WMDATE1,WMDATE2,DATELIS,WTWMHOS:
                                 WTWMPAT,WTWMOTH
          DISPLAY   *P54:24,*EL,*V2LON,WMURNO
.
PROC5050  MOVE      ONE,TEMPFLG
          GOTO      PROC1000                   * read next record wattrea1
.
. ---- Last record read ----
.
PROC7000  COMPARE   ZERO,TEMPFLG
          GOTO      PROC8000 IF EQUAL          * no record qualified
          CALL      CLR1000                    * clear WATTR1FD vars WM..
          CALL      CLR5000                    * clear save vars SWM..
          GOTO      PROC9999
.
PROC8000  MOVE      TRUE,EXIT
          DISPLAY   *P1:24,*EL,*V2LON,"Empty Tempfile"
.
PROC9999  RETURN
+
.*********************************************************************
.         print the report for suspensions/non-suspensions
.*********************************************************************
PRNT0000  MOVE      ZERO,SUSPTYPE           * set to ignore suspensions
          LOAD      SUSPTYPE,WTCNUSOP,ONE   * set to print unsuspended
          CALL      PRTT0000                * print
          COMPARE   ZERO,SUSPTYPE
          GOTO      PRNT9500 IF EQUAL       * only do once
.
          MOVE      "***",WMPTY
          MOVE      "***",WMUNIT
          MOVE      "******",WMDOCTOR
          MOVE      TWO,SUSPTYPE            * set to print suspended
          MOVE      "70",CLNO               * set to print new page
          CALL      PRTT0000
.
PRNT9500  PRINT     *N,"*** End of Report ***",*N
          DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"** Generation Completed **";
PRNT9999  RETURN
+
.*********************************************************************
.*                  PRTT0000                                         *
.*        Print tempfile                                             *
.*********************************************************************
PRTT0000  MOVE      ZERO,PATCNT                     * reset patient counter
          MOVE      SP8,SWMURNO                     * reset compare variable
          MOVE      "***",PWMPTY
          MOVE      "***",PWMUNIT
          MOVE      "******",PWMDOCTR
          MOVE      THOME,LHOME
          MOVE      TBUSIN,LBUSIN
          MOVE      TSEX,LSEX
          MOVE      TDOB,LDOB
          DISPLAY   *P1:24,*EL,"Printing report...";
.
.         do the positional read on temp file acc. to index used
.
          BRANCH    MOPTION TO PRTT1100,PRTT1200,PRTT1300
.
.          using priority as key
.
PRTT1100  PACK      KEY30 WITH SP30,SP30
          MOVE      WMPTY,INDSORT1
          READ      WATMP1,KEY30;;
          GOTO      PRTT2000
.
.         using unit/clinc code as key
.
PRTT1200  PACK      KEY33 WITH SP30,SP30
          MOVE      WMUNIT,INDSORT1
          READ      WATMP1,KEY33;;
          GOTO      PRTT2000
.
.         using doctor code as key
.
PRTT1300  PACK      KEY36 WITH SP30,SP30
          MOVE      DOCTNAME,INDSORT1
          READ      WATMP1,KEY36;;
.
.         read next record 
.
PRTT2000  
          READKS    WATMP1;WMURNO,WMCODE,DWMCNT,WMPTY,WMDOCTOR,WMDESC,WMUNIT:
                           WMSTAY,WMTIME,WMDATE1,WMDATE2,DATELIS,WTWMHOS:
                           WTWMPAT,WTWMOTH
          GOTO      PRTT9000 IF OVER  
          MOVE      DWMCNT,WMCNT
.
. ******* see if to separate by suspensions *******
.
          COMPARE   ZERO,SUSPTYPE
          GOTO      PRTT3000 IF EQUAL       * print all patients
.
          PACK      WTSPDTCK,CCC,CYY,CMM,CDD
          REP       " 0",WTSPDTCK
          CALL      WATONSUS                * see if on suspension
.
. susptype = one -> want unsuspended patients
. susptype = two -> want suspended   patients
.
          IF        SUSPTYPE = ONE & EXIT = ZERO
            GOTO      PRTT2000                * patient is suspended
          ENDIF
          IF        SUSPTYPE = TWO & EXIT = ONE  
            GOTO      PRTT2000                * patient isnt suspended
          ENDIF
.
PRTT3000  BRANCH    MOPTION TO PRTT4100,PRTT4200,PRTT4300
.
.         see if a different priority
.
PRTT4100  MATCH     "***",PWMPTY
          GOTO      PRTT6000 IF EQUAL       * no totals to print
.
          MATCH     WMPTY,PWMPTY
          GOTO      PRTT6000 IF EQUAL
.
          MOVE      WMPTY,INDSORT1
          CALL      TOTL0000                     * Print sub-totals
          CALL      HEAD000 
          GOTO      PRTT6000
.
.         see if a different unit/clinic
.
PRTT4200  MATCH     "***",PWMUNIT
          GOTO      PRTT6000 IF EQUAL       * no totals to print
.
          MATCH     WMUNIT,PWMUNIT
          GOTO      PRTT6000 IF EQUAL
.
          MOVE      WMUNIT,INDSORT1
          CALL      TOTL0000                     * Print sub-totals
          CALL      HEAD000
          GOTO      PRTT6000
.           
.         see if a different doctor code
.
PRTT4300  MATCH     "******",PWMDOCTOR
          GOTO      PRTT6000 IF EQUAL       * no totals to print
.
          MATCH     WMDOCTOR,PWMDOCTR
          GOTO      PRTT6000 IF EQUAL
.
          MOVE      WMDOCTOR,INDSORT1
          CALL      TOTL0000                     * Print sub-totals
          CALL      HEAD000
.
.         print the details
.
PRTT6000  COMPARE   "55",CLNO
          CALL      HEAD000 IF NOT LESS
.
          CALL      DET0000
          CALL      PAT0000
          BRANCH    OVRCD,PRTT2000
.
          PACK      KEY6,WMDOCTOR,SP6
          CALL      DOCN0000  
          MOVE      DOCTNAME,INDOCNAM
          MOVE      WMURNO,DIMURNO
          CALL      TLIS0000                         * calculate time on list
          MOVE      WMDESC,DIM31
.
          MATCH     SP8,SWMURNO
          GOTO      PRTT8000 IF EQUAL                * first time in --> print
.
          MATCH     SWMURNO,WMURNO
          GOTO      PRTT8000 IF NOT EQUAL            * not same --> print UR
.
          PACK      ADDRESS WITH SP30,SP30
          PACK      PTELEP,SP20
          PACK      PTELEB,SP20
          PACK      DESCSEX,SP20
          PACK      PATDOB,SP20
          PACK      LHOME,SP7
          PACK      LBUSIN,SP20
          PACK      LSEX,SP7
          PACK      LDOB,SP20
.         SUB       ONE,PATCNT                     * neutralize the add later
.
. ---- Load description for status in statdes ----
.
PRTT8000  
          MATCH     "1",WTCN20CM                   * print comments ?
          IF        @EQUAL
            MOVE      SP70,ADDRESS
          ENDIF
.
          PRINT     *1,"|",DIMURNO,*11,PNAME,*61,TEMPPIPE," ",DIM31:
                    *94,TEMPPIPE," Added    : ",DATE10,SP2,DATELIS," months":
                    *132,"|":
                    *N,*1,"|",*11,ADDRESS,*61,TEMPPIPE," ",INDOCNAM:
                    *94,TEMPPIPE," Review   : ",DATE20,*132,"|":
                    *N,*1,"|",*11,LHOME,PTELEP,SP1,LBUSIN,PTELEB:
                    *61,TEMPPIPE," Unit : ",DSUNIT:
                    *94,TEMPPIPE," Priority : ",DSPTY,*132,"|":
                    *N,*1,"|",*11,LSEX,DESCSEX,*37,LDOB,PATDOB:
                    *61,TEMPPIPE," Stay : ":
                    *70,WMSTAY,SP2,"Proc. Time :",WMTIME:
                    *94,TEMPPIPE," Cancel   : ",DATE40,*132,"|"
          ADD       FOUR,CLNO
.
          MATCH     "1",WTCN20CM                   * print comments ?
          CALL      PCOM0000 IF EQUAL
.
          CALL      HEAD010 
          MOVE      THOME,LHOME
          MOVE      TBUSIN,LBUSIN
          MOVE      TSEX,LSEX
          MOVE      TDOB,LDOB
          ADD       ONE,PATCNT                    * add one to patient counter
          MOVE      WMURNO,SWMURNO
          MOVE      WMPTY,PWMPTY
          MOVE      WMPTY,SWMPTY
          MOVE      WMUNIT,PWMUNIT
          MOVE      WMUNIT,SWMUNIT
          MOVE      WMDOCTOR,PWMDOCTR
          MOVE      WMDOCTOR,SWMDOCT  
          GOTO      PRTT2000                 * read next rec in tempfile
.
. ---- Last record in tempfile read ----
.
PRTT9000  CALL      TOTL0000                * print subtotals
          CALL      PRTL0000                * print last line.
.
PRTT9999  RETURN
+
.*********************************************************************
.*                  PCOM0000                                         *
.*        Printing of comments                                       *
.*********************************************************************
PCOM0000   
          MOVE      "Comments : ",XDESC
          PACK      KEY21,WMURNO,WMCODE,DWMCNT,SP70
          CALL      RDSMESA1
.
PCOM1000  
          CALL      RDKMESA1
          BRANCH    OVRCD,PCOM9999
          PACK      KEY19,WAURNO,WAPROC,WACNT,SP70
          MATCH     KEY19,KEY21
          GOTO      PCOM9999 IF NOT EQUAL
.
          IF        60<CLNO
            CALL      HEAD010
            CALL      HEAD000
          ENDIF
          PRINT     *1,"|",*11,XDESC,WATEXT,*132,"|"
          ADD       ONE,CLNO
          MOVE      SP70,XDESC
          GOTO      PCOM1000
.
PCOM9999  RETURN
.*********************************************************************
.*                  PRTL0000                                         *
.*        Printing of last line                                      *
.*********************************************************************
PRTL0000   COMPARE    ZERO,CPAGENO
           GOTO       PRTL9000 IF EQUAL
.
           IF       SUSPTYPE = ZERO
             PRINT      *N,"Grand total of Patients Printed",*80,": ",TOTPAT
           ENDIF
           IF       SUSPTYPE = ONE
             PRINT      *N,"Grand total of Un-Suspended Patients Printed":
                        *80,": ",TOTPAT
           ENDIF
           IF       SUSPTYPE = TWO
             PRINT      *N,"Grand total of Suspended Patients Printed":
                        *80,": ",TOTPAT
           ENDIF
           MOVE       ZERO,TOTPAT
           GOTO       PRTL9999
.
PRTL9000   DISPLAY    *P1:24,*EL,*B:
                      *P40:24,*V2LON,"** No Available Data **";
.
PRTL9999   RETURN
+
.*********************************************************************
.*        Heading for print                                          *
.*********************************************************************
HEAD000   CLOCK     TIME,CTIMEIS
          CALL      HEAD132              * print standard heading
.
          IF        SUSPTYPE = ONE
            PRINT     *40,"UN-SUSPENDED PATIENTS",*N
          ENDIF
          IF        SUSPTYPE = TWO
            PRINT     *40,"SUSPENDED PATIENTS",*N
          ENDIF
.
.         print first line of extra page heading
.
          PRINT     *1,"From U/R Number : ",START;
.
          MATCH     SP8,FROMDATE
          GOTO      HEAD0010 IF NOT EQUAL
.
          MOVE      "Start",START
          PACK      CPCDATE,START,SP5
          GOTO      HEAD050
.
HEAD0010  UNPACK    FROMDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
HEAD050   PRINT     *40,"From Date on List : ",CPCDATE;
.
          MATCH     SP3,UNITCLIN            * are we using all units/clin ??
          GOTO      HEAD051 IF NOT EQUAL    * no
.
          PRINT     *75,"Unit/Clinic Code : ",*94,"All Units/Clinics"
          GOTO      HEAD052
.
HEAD051   PRINT     *75,"Unit/Clinic Code : ",*94,UNITDESC
.
.         print second line of extra page heading
.
HEAD052   PRINT     *1,"To   U/R Number : ",END;
.
          UNPACK    TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"To   Date on List : ",CPCDATE;
.
          MATCH     SP6,DOCTOR              * are we using all doctors ??
          GOTO      HEAD055 IF NOT EQUAL    * no, have one doctor
.
          PRINT     *75,"Doctor Code      : ",*94,"All Doctors"
          GOTO      HEAD056
.
HEAD055   PRINT     *75,"Doctor Code      : ",*94,DOCTNAME
.
HEAD056   MOVE      SP30,INDSORT1
.
. ******* see if to print a table heading *******
.
          MOVE      SP6,KEY6
          LOAD      KEY6,MOPTION,WMPTY,WMUNIT,WMDOCTOR
          MATCH     "***",KEY6
          GOTO      HEAD2000 IF EQUAL       * no heading to print
.
          BRANCH    MOPTION,HEAD0100,HEAD0200,HEAD0300
.
.         report is in priority order
.
HEAD0100  PACK      KEY5,CODETP,WMPTY
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD1000
          CLEAR     INDSORT1
          APPEND    WMPTY,INDSORT1
          APPEND    " - ",INDSORT1
          APPEND    TDESC,INDSORT1
          RESET     INDSORT1
          PACK      TBLEHEAD,DESCOPT1,INDSORT1    * set up table heading
          GOTO      HEAD1000
.
.         report is in unit/clinic order
.
HEAD0200  PACK      KEY5,CODEWU,WMUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD1000
          CLEAR     INDSORT1
          APPEND    WMUNIT,INDSORT1
          APPEND    " - ",INDSORT1
          APPEND    TDESC,INDSORT1
          RESET     INDSORT1
          PACK      TBLEHEAD,DESCOPT2,INDSORT1    * set up table heading
          GOTO      HEAD1000
.
.         report is in doctor order
.
HEAD0300  PACK      KEY6,WMDOCTOR,SP6
          CALL      DOCN0000  
          CLEAR     INDSORT1
          APPEND    WMDOCTOR,INDSORT1
          APPEND    " - ",INDSORT1
          APPEND    DOCTNAME,INDSORT1 
          RESET     INDSORT1
          PACK      TBLEHEAD,DESCOPT3,INDSORT1    * set up table heading
.                     
HEAD1000  PRINT     *N,*1,TBLEHEAD            * print which pri/clinic/doct
.
          CALL      HEAD010
          PRINT     *1,"|",*4,"U/R No",*11,"Personal Details":
                    *61,TEMPPIPE," Medical Details":
                    *94,TEMPPIPE," Waiting List Details",*132,"|"
          CALL      HEAD010
HEAD2000  MOVE      TEN5,CLNO
.
HEAD9999  RETURN
+
.****************************************************************************
.
HEAD010   PRINT       *1,"*---------------------------------------------":
                         "-------------------------------------------":
                         "------------------------------------------*"
          ADD         ONE,CLNO
          RETURN
.
.************************************************************************
.*                           TOTL0000                              PD   *
.*            Print the total for each page                             *
.*                   called by : PRTT4100,PRTT4200,PRTT4300             *
.************************************************************************
TOTL0000  COMPARE   ZERO,CPAGENO          * Don't print totals if first page
          GOTO      TOTL9999 IF EQUAL
.
          LOAD      KEY6,MOPTION,PWMPTY,PWMUNIT,PWMDOCTOR
          MATCH     "***",KEY6
          GOTO      TOTL9999 IF EQUAL       * no values to print
.
          MOVE      SP30,INDSORT1
          LOAD      INDSORT1,MOPTION,SWMPTY,SWMUNIT,PWMDOCTR
          BRANCH    MOPTION,TOTL1000,TOTL2000,TOTL3000
.
.         sorting on priority
.
TOTL1000  PACK      PROPT,SP30,SP30
          PACK      KEY5,CODETP,SWMPTY
          CALL      RDCODE1
          RESET     INDSORT1,30
          RESET     INDSORT1
          SCAN      SP6,INDSORT1
          APPEND    " - ",INDSORT1
          APPEND    TDESC,INDSORT1
          RESET     INDSORT1
          CLEAR     PROPT
          APPEND    "Priority ",PROPT 
          APPEND    INDSORT1,PROPT
          RESET     PROPT
          GOTO      TOTL5000
.
.         sorting on unit
.
TOTL2000  PACK      PROPT,SP30,SP30
          PACK      KEY5,CODEWU,SWMUNIT
          CALL      RDCODE1
          RESET     INDSORT1,30
          RESET     INDSORT1
          SCAN      SP6,INDSORT1
          APPEND    " - ",INDSORT1
          APPEND    TDESC,INDSORT1
          RESET     INDSORT1
          CLEAR     PROPT
          APPEND    "Unit/Clinic ",PROPT        * Set up propt for option 1
          APPEND    INDSORT1,PROPT
          RESET     PROPT
          GOTO      TOTL5000
.
.         sorting on doctor 
.
TOTL3000  PACK      PROPT,SP30,SP30
          MOVE      WMDOCTOR,SWMDOCT
          MOVE      PWMDOCTR,WMDOCTOR
          PACK      KEY6,WMDOCTOR,SP6
          CALL      DOCN0000  
          MOVE      SWMDOCT,WMDOCTOR
          RESET     INDSORT1,30
          RESET     INDSORT1
          SCAN      SP6,INDSORT1
          APPEND    " - ",INDSORT1
          APPEND    DOCTNAME,INDSORT1
          RESET     INDSORT1
          CLEAR     PROPT
          APPEND    "Doctor ",PROPT        * Set up propt for option 1
          APPEND    INDSORT1,PROPT
          RESET     PROPT
.         
TOTL5000  PRINT     *N,"Total Patients Printed for ",PROPT,*80,": ":
                    PATCNT
          ADD       PATCNT,TOTPAT
          MOVE      ZERO,PATCNT
.
TOTL9999  RETURN
+
.*********************************************************************
.*                  PAT0000                                          *
.*        Get patient name in a dim45  and patdob (dd/mm/ccyy)       *
.*********************************************************************
PAT0000   PACK        PNAME WITH SP30,SP30
          PACK        ADDRESS WITH SP30,SP30
          MOVE        SP10,PATDOB
          PACK        PTELEP,SP10
          PACK        PTELEB,SP10
          MOVE        SP10,DESCSEX
          MOVE        WMURNO,KEY8
          CALL        RDMAST1
          BRANCH      OVRCD OF PAT6000
.
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK        PATDOB WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DESCSEX
.* ******************************************   End of Changes         *C-49016
.
PAT4000   CALL        NAME0000
          CALL        ADDR0000
          GOTO        PAT9999
.
PAT6000   DISPLAY     *P20:24,"OVERCODE ",WMURNO
.
PAT9999   RETURN
.**********************************************************************
.*                             NAME0000                               *
.*                     Pack Patients Name                             *
.**********************************************************************
.
NAME0000 
          STRIP     PSNAME
          STRIP     PGNAME
          STRIP     PTITL
          PACK      PNAME,SP30,SP30
          CLEAR     PNAME
          APPEND    PSNAME,PNAME
          APPEND    ",",PNAME
          APPEND    SP1,PNAME
          APPEND    PTITL,PNAME
          APPEND    SP1,PNAME
          APPEND    PGNAME,PNAME
          RESET     PNAME
.
          RETURN
+
.**********************************************************************
.*                             ADDR0000                               *
.*                     Pack Patients Address                          *
.**********************************************************************
.
ADDR0000
          PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
.
          RETURN
.*********************************************************************
.
.*********************************************************************
.*                  DET0000                                          *
.*        Get treatment details descriptions                         *
.*********************************************************************
DET0000   PACK        DATE10,SP10
          PACK        DATE20,SP10
          PACK        DATE40,SP10
          PACK        DSUNIT,SP30
          PACK        DSPTY,SP30
          PACK        TDESC,SP30
          MATCH       SP8,WMDATE1
          GOTO        DET1000 IF EQUAL
          UNPACK      WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL        PACDATE
          PACK        DATE10,CPCDATE,SP70
DET1000
          MATCH       SP8,WMDATE2
          GOTO        DET2000 IF EQUAL
          UNPACK      WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL        PACDATE
          PACK        DATE20,CPCDATE,SP70
          GOTO        DET2100
.
DET2000   MOVE        DATE10,DATE20
.
DET2100   MATCH       SP8,WMDATE4
          GOTO        DET3000 IF EQUAL
          UNPACK      WMDATE4 INTO CCENT,CYEAR,CMON,CDAY
          CALL        PACDATE
          PACK        DATE40,CPCDATE,SP70
.
DET3000   PACK        KEY5 WITH CODEWU,WMUNIT
          CALL        RDCODE1
          BRANCH      OVRCD TO DET4000
          MOVE        TDESC,DSUNIT
          PACK        TDESC,SP30
DET4000   PACK        KEY5 WITH CODETP,WMPTY
          CALL        RDCODE1
          BRANCH      OVRCD TO DET9999
          MOVE        TDESC,DSPTY
DET9999   RETURN
+
.************************************************************************
.*                          CANC0000                                    *
.*            Read the cancellation numbers - QQQ file not done yet     *
.*                   called by : PROC0000                               *
.************************************************************************
CANC0000  MOVE        "000",WTWMHOS
          MOVE        "000",WTWMPAT
          MOVE        "000",WTWMOTH
CANC9999  RETURN
+
.************************************************************************
.*                          TLIS0000                                    *
.*               Calculate the time on the list                         *
.*                   called by : PRTT6000                               *
.************************************************************************
TLIS0000  UNPACK      WMDATE1,CCENT,CYEAR,CMON,CDAY
          MOVE        CCENT,ICENT
          MOVE        CYEAR,IYEAR
          MOVE        CMON,IMON
          MOVE        CDAY,IDAY
          COMPARE     ICENT,CCC                     * same century as today
          GOTO        TLIS1000 IF EQUAL             * yes - go on
.
          MOVE        CYY,FORM3                   * no - calculate no. of mon
          MULT        TEN2,FORM3                    * so far this century
          ADD         CMM,FORM3
          MOVE        FORM2,DATELIS
.
          MOVE        "100",FORM3                   * calculate no. of months
          SUB         IYEAR,FORM3                   * last century
          MULT        TEN2,FORM3
          SUB         IMON,FORM3
          ADD         FORM3,DATELIS
.
          COMPARE     IDAY,CDD                      * have we got a incompleted 
          GOTO        TLIS9999 IF NOT LESS          * month counted
.
          SUB         ONE,DATELIS                   * yes - subtract one
          GOTO        TLIS9999
.
TLIS1000  COMPARE     IYEAR,CYY                     * same year
          GOTO        TLIS2000 IF EQUAL
.
          MOVE        CYY,FORM3                     * no - how many years
          SUB         IYEAR,FORM3
          MULT        TEN2,FORM3                    * mult no. of yrs by 12
          SUB         IMON,FORM3                    * sub mon from first year
          ADD         CMM,FORM3                    * add extra months used
          MOVE        FORM3,DATELIS
.
          COMPARE     IDAY,CDD                      * have we counted an 
          GOTO        TLIS9999                      * incomplete month
.
TLIS2000  COMPARE     IMON,CMM                      * same month ?
          GOTO        TLIS3000 IF EQUAL
.
          MOVE        CMM,FORM3                     * no - how many
          SUB         IMON,FORM3
          MOVE        FORM3,DATELIS
.
          COMPARE     IDAY,CDD                      * have we counted an 
          GOTO        TLIS9999 IF NOT LESS          * incomplete month
.
          SUB         ONE,DATELIS                   * yes - sub one
          GOTO        TLIS9999
.
TLIS3000  MOVE        ZERO,DATELIS                  * no months
.
TLIS9999  RETURN
+
.*****************************************
. delete temp file
.*****************************************
DLTP0000  CLOSE     WATMP1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
DLTP9999  RETURN
+
.************************************************************************
GETSVAR   RETURN
.
REDISPS
          CALL      REDP1000
REDISP99  RETURN
.
.****************************************************************************
.
          INC       STD001IO
          INC       BOKRC1IO/INC            * booking file
          INC       CALCAGE
          INC       OUTPREIO/INC
          INC       PATCOMN1
          INC       PATCODKY
          INC       PATDOCKY
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATGSRIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATSNDX
          INC       PATSNX2
          INC       SEXDSCIO
          INC       WATMESIO/INC
          INC       WATTR1IO/INC
          INC       WATONSUS
          INC       WATSUSIO/INC
          INC       WEBERRIO/INC
          INC       NZCOMPIO/INC
AGECHK
CLPATMAS  RETURN
................................................................................

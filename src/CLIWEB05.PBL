.------------------------------------------------------------
. Program       : CLIWEB05
.
. Function      : Pathology Results Email Server
.
. Modifications : 
.------------------------------------------------------------
. V12.00.01 14/05/2025  Peter Vela     TSK 0955096
.                       Alphanumeric Visit Number changes
. V10.13.01 05/12/2018  Don Nguyen     TSK 0838558
.                       Deleted temp file (MAILFILE) after processing.
.                       Removed PORT from temp filename (MAILFNAM).
. V10.04.01 01/05/2014  Ania P         CAR 261630
.                       Removed PORT from temp file name
. V10.03.01 16/07/2013  Jill Parkinson CAR 287923
.           Added include of PATCONFD
. V10.02.02 13/07/2011  Steve Armstrong   CAR 240722
.                       Further mods to cater for second given name as
.                       part of PATGSRFD keys
. V10.02.01 22/06/2011  Steve Armstrong   CAR 240722
.                       Mods to cater for changes to PATGSRFD:
.                        - GSRSNAM & GSRGNAM extended to 30 chars.
.                        - PTGSGNM2 added.
.                        - all indexes changed in length.
.------------------------------------------------------------
. V10.01.01 13/01/2011  Jill Parkinson CAR 234014
.                       Added pmsvx1af
.------------------------------------------------------------
. V9.03.01  10/02/2004  Peter Vela CAR 38290
.                     Added functionality for opening yearly RESLNKFD files 
.                     (reln1999, reln2000, reln2001, reln2002, etc...)
.------------------------------------------------------------
. V9.02.01  09/09/2001  Sandra Barcham                                      
.                     Added DOB & SEX to patgsrnf
.------------------------------------------------------------
. V5.10.B01  3/04/2001  Glenn Saunders                                      
.                     Remove all references to PATSUR file (old surname file)
.------------------------------------------------------------
. V5.08.01  16/08/2000  Caleb Sharman
.                     Changed Health Fund variables to be 8 chars
.------------------------------------------------------------
. V5.07.01 20.12.1999 K.C.Nelson 
.                     Recompiled for IBAWEBDF/IBAWEBCD               
. V5.07.00 27.19.1999 K.C.Nelson 
.                     Port from 6.06 to 5.07                         
.------------------------------------------------------------
. V6.06.01 10.06.1999 B.G.Ackland
.                     Fix Update to Ward Link for Unmatched Results
. V6.06.00 31.03.1999 Katie Nelson
.                     Port from 6.05 to 6.06 
.------------------------------------------------------------
. V6.05.07 24.03.99 B.G.Ackland  
.                   Changed Creation Date to Collection Date
. V6.05.06 22.03.99 B.G.Ackland  
.                   Added Single Result Line Key to Header Line
. V6.05.05 11.03.99 B.G.Ackland  
.                   Fudge Header Line for Mater Pathology  
. V6.05.04 11.03.99 B.G.Ackland  
.                   Default Report time to Current if Blank
. V6.05.03 11.03.99 B.G.Ackland  
.                   Added System Name Look Up of Lab Code
. V6.05.02 10.03.99 B.G.Ackland  
.                   Added System Locking for Sequence Control
. V6.05.01 22.02.99 B.G.Ackland  
.                   Added Run No Checks and E-mail Forwarding
. V6.05.00 12.01.99 Katie Nelson
.                   Created Program
.------------------------------------------------------------
          INC     IBAWEBDF
          INC     PATCONFD/INC
          INC     AAEDE1FD/INC 
          INC     PATGSRFD/INC
          INC     PATMA1FD/INC
          INC     PATPR1FD/INC
          INC     PATMI1FD/INC
          INC     PMSPX2FD/INC
          INC     PMSVX1FD/INC
          INC     PATVISFD/INC
          INC     PATWR1FD/INC
          INC     RESLOGFD/INC
          INC     RESLNKFD/INC
          INC     RESPDSFD/INC
          INC     RESPHRFD/INC
          INC     RESPMPFD/INC
          INC     RESPENFD/INC
          INC     RESLABFD/INC
.
          INC       TFILEVAR/INC
.
RSINGLEC  FORM     4     * Single Result Counter
RSINGLEK  DIM      20    * Single Result Key
.
LABCNAME  DIM      40    * Lab Name
NOTIFKEY  DIM      32[9]
NOTIFCNT  FORM     2
MAILCMD   DIM      80
MAILFNAM  DIM      8
MAILFILE  FILE     ASCII,FIXED=127
MAILFLAG  FORM     1
RESULTNM  DIM      1     * Result Normal Y=Yes N=No
RESULTUG  DIM      1     * Result Urgent U=Urgent R=Routine
.
PREADM    FORM     1
PREBILL   DIM      8
PREURNO   DIM      8
CHAR015   INIT     015
CLRCR     DIM      2
.
. ------- Program Variables --------
.
DIM1A     DIM       1
DIM2A     DIM       2
DIM4      DIM       4
DIM8      DIM       8
DATE10    DIM       10
DIM11     DIM       11
DIM12     DIM       12
DIM13     DIM       13
DIM20     DIM       20
DIM22     DIM       22
DOB       DIM       10
.
ERRMESS   FORM      1   
ERRCOUNT  FORM      3
.
FORM8     FORM      8
.
HEADDET   DIM       127[99]
HEADLIN   DIM       3[99]
HEADRECN  FORM      2
LABRWARD  DIM       32
.
LINKTYPE  DIM       2
LINKKEYS  DIM       10
LNDCOUNT  DIM       3
LOGDATE   DIM       8
LOGTIME   DIM       5
LOGUNQID  FORM      10
LNECOUNT  FORM      3
.
MEDICARE  DIM       10
NAME32    DIM       32
.
RESCOUNT  FORM      3
RESLNKFL  DIM       8               * I CAR 38290
RUNNOERR  FORM      1
.
SAVEBILL  DIM       8
SEX       DIM       1
SCORE     FORM      1
SREPHRDT  DIM       8                       * save result date
SREPHRTM  DIM       5                       * save result time
SREPHRUN  DIM       4                       * save unique id
SREPHFUR  DIM       1                       * save PID is u/r?
SREPHPID  DIM       16                      * save patient id
SREPHVIS  DIM       8                       * save visit number
SREPHNAM  DIM       60                      * save test name
SREPHLAB  DIM       3                       * save laboratory
SREPHRRN  DIM       4                       * save report run number
SREPHCDT  DIM       8                       * save collect date
SREPHCTM  DIM       5                       * save collect time
SREPHNOR  DIM       1                       * save normal result
SREPHCON  DIM       1                       * save confidential
SREPHTCT  DIM       1                       * save test category
SREPHCOM  DIM       1                       * save test complete
.
SPATNAME  DIM       32
SURNAME   DIM       30
GIVEN     DIM       30
.
TMRSDET   DIM       127
TMRSFIL   DIM       1
TMRSLIN   DIM       3
.
OUTPFILE  FILE      ASCII, FIXED=250
.
PATHFILE  FILE      ASCII, FIXED=250
PATHNAME  DIM       60
.
UNIQUEID  FORM      4
LOGERROR  DIM       20
EXPECTED  FORM      6
STARTFIL  FORM      1
.
PRELEN    INIT      "reln"          * I CAR 38290
.
PRGID     INIT      "CLIWEB05"
PRGNAM    INIT      "Pathology Results Email Server"
VERSION   INIT      "V12.01.00"
.------------------------------------------------------------
. Start of Program
.------------------------------------------------------------
MAIN0000  CALL      INIT0000                 * initialisation
          BRANCH    EXIT,MAIN9990            * initialisation failed
.
          CALL      WRTLOG00                 * Write Log Record
          MOVE      ZERO,EXPECTED
          MOVE      ZERO,STARTFIL            * Process Header Section Flag
.
MAIN1000  MOVE      ZERO,RSINGLEC            * Flag as For a single line result
          CALL      PROH0000                 * Process Header Details
          BRANCH    EXIT,MAIN9000
.
          IF        STARTFIL=0
            MOVE      ONE,STARTFIL           * Update Header for 000-099 Sect
            CALL      LOGHD000               * Update the log record
          ENDIF
.
          CALL      PROD0000                 * Process Result Details
          BRANCH    EXIT,MAIN9000
.
          IF        RSINGLEC=1
            CALL      UPDSIN00
          ENDIF
.
          CALL      EMLNOT00                 * Perform Any E-mail Notifications
.
          GOTO      MAIN1000
.
MAIN9000  CALL      LOGFU000                 * Update the log record
          GOTO      MAIN9999
.
MAIN9990  DISPLAY   *P5:20,"Cannot find result file."
.
MAIN9999  STOP  
.----------------------------------------------------------------------
. Update Key for Single Result Line Access
.----------------------------------------------------------------------
UPDSIN00  PACK      KEY17,REPHRDT,REPHRTM,REPHRUN,SP70
          CALL      RDREPH1
          IF        OVRCD=0
            MOVE      RSINGLEK,REPHSTK
            CALL      UPREPH1
          ENDIF
UPDSIN99  RETURN
.----------------------------------------------------------------------
. Check for Single Line Results
.----------------------------------------------------------------------
CHKSIN00  IF       RSINGLEC>1
            GOTO     CHKSIN99      * More than One so no more checking
          ENDIF
          MATCH    "301",TMRSLIN   * Is it a detail line ?
          GOTO     CHKSIN99 IF NOT EQUAL
.
          PACK     TMRSDET,TMRSDET,SP70,SP70
          PACK     CLRCR,CHAR015,SP1        * Remove CR Character Octal 015
          REP      CLRCR,TMRSDET
          MATCH    SP70,TMRSDET    * Does it have any details
          GOTO     CHKSIN99 IF EQUAL
.
          PACK     RSINGLEK,REPDRDT,REPDRTM,REPDRUN,REPDLIN,SP70
          ADD      ONE,RSINGLEC
.
CHKSIN99  RETURN
.------------------------------------------------------------
. Initialisation
.------------------------------------------------------------
INIT0000  KEYIN    PATHNAME
          STRIP    PATHNAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATHFILE,PATHNAME
          TRAPCLR  IO
          BRANCH   OVRCD,INIT9000
.
          MOVE     SP70,SREPHRDT 
          MOVE     SP70,SREPHRTM 
          MOVE     SP70,SREPHRUN 
          MOVE     SP70,SREPHFUR 
          MOVE     SP70,SREPHPID 
          MOVE     SP70,SREPHVIS 
          MOVE     SP70,SREPHNAM 
          MOVE     SP70,SREPHLAB 
          MOVE     SP70,SREPHRRN 
          MOVE     SP70,SREPHCDT 
          MOVE     SP70,SREPHCTM 
          MOVE     SP70,SREPHNOR 
          MOVE     SP70,SREPHCON 
          MOVE     SP70,SREPHTCT 
          MOVE     SP70,SREPHCOM 
.
          OPEN     PATWR1A1,"patwr1af"
          OPEN     PATWR1A2,"patwr1af"
          OPEN     PATVISA1,"patvisaf"
          OPEN     PATVISA2,"patvisaf"
          OPEN     AAEDE1A1,"aaede1af"
          OPEN     PATMA1A1,"patma1af"
          OPEN     PATMX1A1,"patmx1af"
          OPEN     PATMI1A1,"patmi1af"
          OPEN     PMSPX2A1,"pmspx2af"
          OPEN     PMSVX1A1,"pmsvx1af"
          OPEN     PATPR1A1,"patpr1af"
          OPEN     RESLOGA1,"reslogaf"
          OPEN     RESLOGA2,"reslogaf"
.         OPEN     RESLNKA1,"reslnkaf"           * D CAR 38290
          OPEN     RESPDSA1,"respdsaf"
          OPEN     RESPHRA1,"resphraf"
          OPEN     RESPENA1,"respenaf"
          OPEN     RESPMPA1,"respmpaf"
          OPEN     RESLABA1,"reslabaf"
          OPEN     CONTROLF,"controlf"
..          READ      CONTROLF,EIGHTY;*1,HLBMDGUP,HLBMDGUA,HLBMDGMU:
..                                       HLBMDGUU:
..                                  *105,HLBMDGAD,HLBMDGDS,HLBMDGTR:
..                                       HLBMDGRG
.
          MOVE     ZERO,EXIT
.
          READ     CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC,EXITCHAR
          READ     CONTROLF,TWO;*4,CONAME
          MOVE     CONAME,CNAME
          CLOCK    DEPT,CCDEPT		*  set NSM department code
          CLOCK    PORT,PORT
.
          CALL     IBACLOCK
          CLOCK    TIME,CTIMEIS
          PACK     KEY8,CCC,CYY,CMM,CDD
          REP      " 0",KEY8
          UNPACK   KEY8,CCENT,CYEAR,CMON,CDAY
          CALL     PACDATE
          MOVE     ZERO,ERRCOUNT
.
          CALL      TFILENAM
          MOVE      TFILNAME,MAILFNAM
.
          GOTO     INIT9999
.
INIT9000  MOVE     ONE,EXIT
.
INIT9999  RETURN  
.------------------------------------------------------------
.  Write Log Record
.------------------------------------------------------------
WRTLOG00  MOVE     "9999999999",KEY10
          CALL     RLRELG1
          COMPARE  ZERO,OVRCD
          GOTO     WRTLOG10 IF EQUAL
          IF       OVRCD=1
            CALL     WRTLOC00
            GOTO     WRTLOG00
          ENDIF
          IF       OVRCD=2
            CALL     WAIT0000
            GOTO     WRTLOG00
          ENDIF

WRTLOG10  PACK     LOGDATE,CCC,CYY,CMM,CDD
          REP      " 0",LOGDATE
          PACK     LOGTIME,CTIMEIS
          MOVE     ZERO,LOGUNQID
          PACK     KEY10,Z70
          CALL     RSRELG1
          CALL     RPRELG1
          BRANCH   OVRCD,WRTLOG20
.
          MOVE     RELGUID,LOGUNQID
WRTLOG20  ADD      ONE,LOGUNQID 
          MOVE     LOGUNQID,RELGUID
          MOVE     LOGDATE,RELGDTE
          MOVE     LOGTIME,RELGTME
          MOVE     PATHNAME,RELGINF
          MOVE     SP70,RELGPCO  
          MOVE     SP70,RELGRUN
          MOVE     SP70,RELGMER
          MOVE     "N",RELGCMP
          MOVE     ZERO,RELGRPR
          MOVE     ZERO,RELGERR
          MOVE     ZERO,RELGEXP
.
          PACK     KEY10,LOGUNQID
          CALL     RARELG1
          COMPARE  ZERO,OVRCD
          GOTO     WRTLOG20 IF EQUAL
          CALL     WRRELG1
.
          RETURN
.
WAIT0000  DISPLAY  *W5,"Waiting on System Lock"
          RETURN
.------------------------------------------------------------
. Write Lock Record 
.------------------------------------------------------------
WRTLOC00  MOVE     "9999999999",RELGUID
          MOVE     LOGDATE,RELGDTE
          MOVE     LOGTIME,RELGTME
          MOVE     PATHNAME,RELGINF
          MOVE     LOGDATE,RELGDTE
          MOVE     LOGTIME,RELGTME
          MOVE     PATHNAME,RELGINF
          MOVE     SP70,RELGPCO  
          MOVE     SP70,RELGRUN
          MOVE     "Lock Control Record",RELGMER
          MOVE     "N",RELGCMP
          MOVE     ZERO,RELGRPR
          MOVE     ZERO,RELGERR
          MOVE     ZERO,RELGEXP
          CALL     WRRELG1
          RETURN
.------------------------------------------------------------
.  Log File Update
.------------------------------------------------------------
CHKLAB00  MOVE     SREPHLAB,KEY3
          CALL     RDRELB1
          COMPARE  ZERO,OVRCD
          GOTO     CHKLAB99 IF EQUAL
.
          MOVE     SP70,KEY3
          CALL     RSRELB1
CHKLAB10  CALL     RKRELB1
          BRANCH   OVRCD,CHKLAB99
          MATCH    LABCNAME,RELBDES
          GOTO     CHKLAB10 IF NOT EQUAL
          MOVE     RELBLCD,SREPHLAB
.
CHKLAB99  RETURN
.------------------------------------------------------------
.  Log File Update
.------------------------------------------------------------
LOGHD000  MOVE     RELGUID,KEY10
          CALL     RDRELG1
          IF       OVRCD=0
            MOVE     EXPECTED,RELGEXP
            MOVE     SREPHLAB,RELGPCO  
            MOVE     SREPHRRN,RELGRUN
            CALL     UPRELG1
          ENDIF
          MOVE     ZERO,F2
LOGHD100  MOVE     SREPHLAB,KEY3
          CALL     RLRELB1
          BRANCH   OVRCD,LOGHD800,LOGHD900
          SQUEEZE  SREPHRRN
          MOVE     SREPHRRN,F4
          ADD      ONE,RELBLRN
          IF       RELBLRN=F4
            MOVE     ZERO,RUNNOERR
          ELSE
            MOVE     ONE,RUNNOERR
            CALL     EMLRUN00 
            MOVE     SREPHRRN,RELBLRN
          ENDIF
          CALL     UPRELB1
          CALL     UURELB1
          GOTO     LOGHD999
.
LOGHD800  DISPLAY  *R,"Error Laboratory Code Does not Exist"
          GOTO     LOGHD999
.
LOGHD900  ADD      ONE,F2
          IF       F2<50
            DISPLAY  *W2
            GOTO     LOGHD100
          ENDIF
          DISPLAY  *R,"Error Locking Laboratory Code "
          GOTO     LOGHD999

LOGHD999  RETURN
.------------------------------------------------------------
. E-mail Run Number Sequence Error
.------------------------------------------------------------
EMLRUN00  
.         CLEAR    MAILFNAM
.         APPEND   "mailer",MAILFNAM
.         APPEND   PORT,MAILFNAM
.         RESET    MAILFNAM
.
          PREP     MAILFILE,MAILFNAM
          WRITE    MAILFILE,SEQ;"Subject: Pathology Results Warning"
          WRITE    MAILFILE,SEQ;"Message from Pathology Results System"
          WRITE    MAILFILE,SEQ;"Program : ",PRGID,"   Version : ",VERSION
          WRITE    MAILFILE,SEQ;"Date    : ",CPCDATE,"   Time : ",CTIMEIS
          WRITE    MAILFILE,SEQ;"Lab     : ",SREPHLAB
          WRITE    MAILFILE,SEQ;"Run     : ",SREPHRRN
          WRITE    MAILFILE,SEQ;"****** Possible Run Sequence Error  ******"
          CLOSE    MAILFILE
.
          MATCH    SP70,RELBHEM
          GOTO     EMLRUN50 IF EQUAL
.
          CLEAR    MAILCMD
          APPEND   "mail ",MAILCMD
          APPEND   RELBHEM,MAILCMD
          APPEND   "<",MAILCMD
          APPEND   MAILFNAM,MAILCMD
          APPEND   ".txt",MAILCMD
          RESET    MAILCMD
          EXECUTE  MAILCMD,TASKID
          DISPLAY  MAILCMD
.
EMLRUN50  MATCH    SP70,RELBEML
          GOTO     EMLRUN90 IF EQUAL
.
          CLEAR    MAILCMD
          APPEND   "mail ",MAILCMD
          APPEND   RELBEML,MAILCMD
          APPEND   "<",MAILCMD
          APPEND   MAILFNAM,MAILCMD
          APPEND   ".txt",MAILCMD
          RESET    MAILCMD
          EXECUTE  MAILCMD,TASKID
          DISPLAY  MAILCMD
.
EMLRUN90  PREP     MAILFILE,MAILFNAM
          CLOSE    MAILFILE,DELETE
.
EMLRUN99  RETURN
.------------------------------------------------------------
.  Log File Update
.------------------------------------------------------------
LOGFU000  MOVE     RELGUID,KEY10
          CALL     RDRELG1
          IF       OVRCD=0
            MOVE     "Y",RELGCMP
            MOVE     RESCOUNT,RELGRPR
            MOVE     ERRCOUNT,RELGERR
            MOVE     LOGERROR,RELGMER
            MATCH    SP70,RELGMER
            IF       @EQUAL
              IF       RUNNOERR=1
                MOVE     "Run Seq. Warning",RELGMER
              ENDIF
            ENDIF
            CALL     UPRELG1
          ENDIF
LOGFU999  RETURN
.------------------------------------------------------------
. Process header details
.------------------------------------------------------------
PROH0000  MOVE     ZERO,HEADRECN
          MOVE     ZERO,LNECOUNT
          MOVE     ZERO,NOTIFCNT
          MOVE     SP70,NOTIFKEY[1]
          MOVE     SP70,NOTIFKEY[2]
          MOVE     SP70,NOTIFKEY[3]
          MOVE     SP70,NOTIFKEY[4]
          MOVE     SP70,NOTIFKEY[5]
          MOVE     SP70,NOTIFKEY[6]
          MOVE     SP70,NOTIFKEY[7]
          MOVE     SP70,NOTIFKEY[8]
          MOVE     SP70,NOTIFKEY[9]
.
PROH1000  MOVE     SP70,TMRSLIN
          READ     PATHFILE,SEQ;TMRSLIN,TMRSFIL,TMRSDET
          GOTO     PROH9900 IF OVER
          MATCH    "999",TMRSLIN
          IF       @EQUAL
            MOVE     ONE,EXIT
            GOTO     PROH9999
          ENDIF
.
          MOVE     ZERO,F3
          MOVE     TMRSLIN,F3
          IF       F3<1|F3>399
            GOTO     PROH1000 
          ENDIF
          IF       F3>299
            ADD      ONE,HEADRECN
            MOVE     TMRSLIN,HEADLIN[HEADRECN]
            MOVE     TMRSDET,HEADDET[HEADRECN]
            GOTO     PROH9000
          ENDIF
          IF       F3>99
            ADD      ONE,HEADRECN
            MOVE     TMRSLIN,HEADLIN[HEADRECN]
            MOVE     TMRSDET,HEADDET[HEADRECN]
          ENDIF
.
          CALL      PROHED00                 * Process Header Fields        
.
          GOTO      PROH1000
.
PROH9000  CALL      CHKVAL00                 * Check for Valid Patient
.
          CALL      CHKLAB00
.
          CALL      WRTHED00                 * Write Result Header Record
.
          CALL      WRTLNK00                 * Write Link records
.
          MOVE      ZERO,F2
.
PROH9100  ADD       ONE,F2
          MOVE      HEADLIN[F2],TMRSLIN
          MOVE      HEADDET[F2],TMRSDET
          CALL      WRTDET00                 * Write Result Detail Line
          COMPARE   F2,HEADRECN
          GOTO      PROH9100 IF NOT EQUAL
.
          CALL      CHKSIN00                 * Check for Single Result
.
          MOVE      ZERO,EXIT
          GOTO      PROH9999
.
PROH9900  MOVE      ONE,EXIT
          MOVE      "No End of File      ",LOGERROR
.
PROH9999  RETURN
.------------------------------------------------------------
.  Check valid U/R or visit number 
.------------------------------------------------------------
CHKVAL00  PACK      KEY8,SREPHPID
          CALL      RDMAST1
          BRANCH    OVRCD,CHKVAL10
.
          PACK      KEY8,SREPHPID
          CALL      RDPMPX21
          BRANCH    OVRCD,CHKVAL10
.
          CALL      CHKMCH00       * compare medicare etc
          IF        SCORE<4
            GOTO      CHKVAL10
          ENDIF
.
          GOTO      CHKVAL80
.
CHKVAL10  PACK      KEY8,SREPHPID
          CALL      RDVISA1
          BRANCH    OVRCD,CHKVAL20
.
          PACK      KEY8,PVIURNO
          CALL      RDMAST1
          BRANCH    OVRCD,CHKVAL90
.
          PACK      KEY8,PVIURNO
          CALL      RDPMPX21
          BRANCH    OVRCD,CHKVAL90
.
          CALL      CHKMCH00       * compare medicare etc
          IF        SCORE<4
            GOTO      CHKVAL20
          ENDIF
.
          GOTO      CHKVAL80
.
CHKVAL20  CALL      SNAME000       * Search by Name
          BRANCH    EXIT,CHKVAL90
.
CHKVAL80  MOVE      ONE,SREPHFUR
          PACK      SREPHPID,PURNO,SP70
          MOVE      ZERO,EXIT
          MOVE      ZERO,ERRMESS
          GOTO      CHKVAL99
.
CHKVAL90  MOVE      ONE,ERRMESS
          MOVE      ZERO,SREPHFUR           * Is not a U/R number
.
CHKVAL99  RETURN
.----------------------------------------------------------------------
. Search for Strict Surname Match
.----------------------------------------------------------------------
SNAME000  PACK      KEY98,SURNAME,GIVEN,SP70,SP70          * CAR 240722
          MOVE      ZERO,PREADM
          CALL      RDPTGSR2
SNAME100  CALL      RKSTR000
          BRANCH    OVRCD,SNAME900
          MATCH     ZEROUR,GSRURNO
          IF        @EQUAL
            MOVE      GSRBILL,PURNO
            MOVE      PURNO,KEY8
            CALL      RDPRAM1                * Get the pre-adm details
            IF        OVRCD=1
              MOVE      GSRBILL,KEY8
              CALL      RDVISA1
              BRANCH    OVRCD,SNAME100
              MOVE      PVIURNO,GSRURNO
              MOVE      PVIURNO,PURNO
              MOVE      PURNO,KEY8
              CALL      RDMAST1
              BRANCH    OVRCD,SNAME100
              MOVE      PURNO,KEY8
              CALL      RDPMPX21
              BRANCH    OVRCD,SNAME100
            ELSE
              MOVE      GSRURNO,PURNO          * Restore correct U/R No.
            ENDIF
          ELSE
            MOVE      GSRURNO,PURNO
            MOVE      PURNO,KEY8
            CALL      RDMAST1
            BRANCH    OVRCD,SNAME100
            MOVE      PURNO,KEY8
            CALL      RDPMPX21
            BRANCH    OVRCD,SNAME100
          ENDIF
          CALL      CHKMCH00       * compare medicare etc
          IF        SCORE<5
            GOTO      SNAME100
          ENDIF
          MATCH     GSRURNO,ZEROUR
          IF        @EQUAL
            MOVE      ONE,PREADM
            MOVE      GSRURNO,PREURNO
            MOVE      GSRBILL,PREBILL
            GOTO      SNAME100
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      SNAME999
.
SNAME900  IF        PREADM=1 
            MOVE      GSRURNO,PREURNO
            MOVE      PREBILL,KEY8
            CALL      RDPRAM1                * Get the pre-adm details
            MOVE      PREURNO,PURNO          * Restore correct U/R No.
            MOVE      ZERO,EXIT
          ELSE
            MOVE      ONE,EXIT
          ENDIF
SNAME999  RETURN
.**************************************************************************
.*  RKSTR000  :  get next strict surname order record                     *
.**************************************************************************
RKSTR000  CALL      RKPTGSR2
          BRANCH    OVRCD,RKSTR999
          MATCH     SURNAME,GSRSNAM
          GOTO      RKSTR900 IF NOT EQUAL
          MATCH     GIVEN,SP70
          GOTO      RKSTR600 IF EQUAL
.
          MOVE      GIVEN,ANS       * Match First Initial Only
          MATCH     ANS,GSRGNAM
          GOTO      RKSTR650 IF LESS
          GOTO      RKSTR700 IF NOT EQUAL
.
RKSTR600  MOVE      ZERO,SRCHFLAG                * Valid Record
          MOVE      ZERO,OVRCD
          GOTO      RKSTR999
.
.        Records given name less than that wanted - jump forward
.
RKSTR650  MOVE      GIVEN,ANS       * Match First Initial Only
          PACK      KEY98,GSRSNAM,ANS,SP70,SP70          * CAR 240722
          CALL      RSPTGSR2
          GOTO      RKSTR000
.
.        We have finished the given names for this particular surname
.        try the next surname with the same starting string
.
RKSTR700  MOVE      "~",ANS
          PACK      KEY98,GSRSNAM,ANS,ANS,ANS,SP70,SP70
          CALL      RSPTGSR2
          GOTO      RKSTR000
.
RKSTR900  MOVE      ONE,OVRCD            * no more records
.
RKSTR999  RETURN
.----------------------------------------------------------------------
. Check for Matching Patient
.  Surname       2
.  Given Name    2  Initial       1
.  Medicare No   2
.  Date of Birth 1
.  Sex           1
.----------------------------------------------------------------------
CHKMCH00  MOVE      ZERO,SCORE
          STRIP     PTMASNAM
          STRIP     SURNAME
          MATCH     SURNAME,PTMASNAM
          IF        @EQUAL
            ADD       TWO,SCORE
          ENDIF
          PACK      SURNAME,SURNAME,SP70
.
          STRIP     PMPXFNAM
          STRIP     GIVEN
          MATCH     GIVEN,PMPXFNAM
          IF        @EQUAL
            ADD       TWO,SCORE
          ELSE
            MOVE      PMPXFNAM,KEY1
            MOVE      GIVEN,ANS
            MATCH     ANS,KEY1
            IF        @EQUAL
              ADD       ONE,SCORE
            ENDIF
          ENDIF
          PACK      GIVEN,GIVEN,SP70
.
          MATCH     SP70,PMEDI
          IF        !@EQUAL
            STRIP     PMEDI
            STRIP     MEDICARE
            MATCH     PMEDI,MEDICARE
            IF        @EQUAL
              ADD       TWO,SCORE
            ENDIF
            PACK      MEDICARE,MEDICARE,SP70
          ENDIF
.
          MATCH     PBDATE,DOB
          IF        @EQUAL
            ADD       TWO,SCORE
          ENDIF
.
          MATCH     PSEX,SEX
          IF        @EQUAL
            ADD       ONE,SCORE
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Process Result Details
.------------------------------------------------------------
PROD0000  MOVE     SP70,TMRSLIN
          READ     PATHFILE,SEQ;TMRSLIN,TMRSFIL,TMRSDET
          GOTO     PROD9900 IF OVER
.
          MOVE     ZERO,F3
          MOVE     TMRSLIN,F3
.
          IF       F3<1|F3>399
            GOTO     PROD0000 
          ENDIF
          IF       F3<299
            GOTO     PROD9999
          ENDIF
.
          CALL     WRTDET00      * Write Details Record
.
          CALL     CHKSIN00      * Check for Single Result Line
.
          MATCH    "399",TMRSLIN
          GOTO     PROD0000 IF NOT EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     PROD9999
.
PROD9900  MOVE      ONE,EXIT
          MOVE      "No End of File     ",LOGERROR
.
PROD9999  RETURN
.------------------------------------------------------------
. Process Header Fields From File Format
.------------------------------------------------------------
PROHED00  UNPACK    TMRSLIN,DIM1,DIM2
          MOVE      DIM1,FLDSETNO
          MOVE      DIM2,FLDITMNO
          ADD       ONE,FLDSETNO
          PERFORM   FLDSETNO,FLHEA000,REPIN000,RESHE000
PROHED99  RETURN
.------------------------------------------------------------
. File Header Fields (Field Set 0)  Pit Items 000-099
.------------------------------------------------------------
FLHEA000  BRANCH    FLDITMNO,FLHEA010,FLHEA020,FLHEA030,FLHEA040,FLHEA999:
                             FLHEA060,FLHEA999,FLHEA999,FLHEA090,FLHEA100:
                             FLHEA999,FLHEA999,FLHEA999,FLHEA999,FLHEA999:
                             FLHEA999,FLHEA999,FLHEA999,FLHEA190,FLHEA200:
                             FLHEA210,FLHEA999,FLHEA999,FLHEA999,FLHEA999:
                             FLHEA999,FLHEA999,FLHEA999,FLHEA290
          GOTO      FLHEA999
.
FLHEA010  MOVE      TMRSDET,LABCNAME
          GOTO      FLHEA999
FLHEA020
          GOTO      FLHEA999
.
FLHEA030  UNPACK    TMRSDET,KEY19,SREPHRRN,DIM12,DATE10,DIM11,SREPHCTM
          MATCH     "Report Run Number :",KEY19  * Fudge for Mater 11.03.99
          IF        @EQUAL
            UNPACK    TMRSDET,KEY20,SREPHRRN,DIM12,DATE10,KEY10,SREPHCTM
          ENDIF
          UNPACK    DATE10,CDAY,DIM1,CMON,DIM1,CCENT,CYEAR
          PACK      SREPHCDT,CCENT,CYEAR,CMON,CDAY
          GOTO      FLHEA999
.
FLHEA040
          GOTO      FLHEA999
FLHEA060
          GOTO      FLHEA999
FLHEA090
          GOTO      FLHEA999
FLHEA100
          GOTO      FLHEA999
FLHEA190
          GOTO      FLHEA999
FLHEA200
          GOTO      FLHEA999
FLHEA210  ADD       ONE,EXPECTED
          GOTO      FLHEA999
FLHEA290
.
FLHEA999  RETURN
.------------------------------------------------------------
. Result Report Information (Field Set 1) PIT Items 100-199
.------------------------------------------------------------
REPIN000  COMPARE   "00",FLDITMNO 
          IF        @EQUAL
            UNPACK    TMRSDET,DIM22,NAME32
            MOVE      NAME32,SPATNAME
            CALL      SETNAM00
            GOTO      REPIN999
          ENDIF
.
          BRANCH    FLDITMNO,REPIN010,REPIN999,REPIN999,REPIN040,REPIN050:
                             REPIN999,REPIN999,REPIN999,REPIN090,REPIN100:
                             REPIN110,REPIN120,REPIN999,REPIN999,REPIN150:
                             REPIN999,REPIN999,REPIN999,REPIN190,REPIN999:
                             REPIN210,REPIN220,REPIN230,REPIN999,REPIN999:
                             REPIN999,REPIN999,REPIN999,REPIN290,REPIN300:
                             REPIN310,REPIN999,REPIN999,REPIN999,REPIN999:
                             REPIN999,REPIN999,REPIN999,REPIN390

          GOTO      REPIN999
.
REPIN010   
          GOTO      REPIN999
.
REPIN040  UNPACK    TMRSDET,KEY33,CDAY,ANS,CMON,ANS,CCENT,CYEAR
          PACK      DOB,CCENT,CYEAR,CMON,CDAY
          UNPACK    TMRSDET,KEY64,SEX
          GOTO      REPIN999
.
REPIN050
          GOTO      REPIN999
REPIN090
          GOTO      REPIN999
.
REPIN100  UNPACK    TMRSDET,DIM22,SREPHPID
          PACK      CLRCR,CHAR015,SP1        * Remove CR Character Octal 015
          REP       CLRCR,SREPHPID
          SQUEEZE   SREPHPID
          MOVE      SREPHPID,FORM8
          PACK      SREPHPID,SP70
          MOVE      FORM8,SREPHPID
          GOTO      REPIN999
.
REPIN110  UNPACK    TMRSDET,SREPHLAB
          GOTO      REPIN999
.
REPIN120  UNPACK    TMRSDET,DIM22,MEDICARE
          GOTO      REPIN999
REPIN150
          GOTO      REPIN999
REPIN190
          GOTO      REPIN999
.
REPIN210  UNPACK    TMRSDET,DIM22,KEY32
          CALL      ADDNOT00         * Add Notification Key
          GOTO      REPIN999
.
REPIN220  UNPACK    TMRSDET,DIM22,KEY32
          CALL      ADDNOT00         * Add Notification Key
          GOTO      REPIN999
REPIN230
          GOTO      REPIN999
REPIN290
          GOTO      REPIN999
.
REPIN300  UNPACK    TMRSDET,DIM22,LABRWARD
          MOVE      LABRWARD,KEY32
          CALL      ADDNOT00         * Add Notification Key
          GOTO      REPIN999
.
REPIN310
          GOTO      REPIN999
REPIN390
.
REPIN999  RETURN
.------------------------------------------------------------
. Add Notification Key for Result 
.------------------------------------------------------------
ADDNOT00  PACK      CLRCR,CHAR015,SP1        * Remove CR Character Octal 015
          REP       CLRCR,KEY32
          MATCH     KEY32,NOTIFKEY[1]
          GOTO      ADDNOT99 IF EQUAL
          MATCH     KEY32,NOTIFKEY[2]
          GOTO      ADDNOT99 IF EQUAL
          MATCH     KEY32,NOTIFKEY[3]
          GOTO      ADDNOT99 IF EQUAL
          MATCH     KEY32,NOTIFKEY[4]
          GOTO      ADDNOT99 IF EQUAL
          MATCH     KEY32,NOTIFKEY[5]
          GOTO      ADDNOT99 IF EQUAL
          MATCH     KEY32,NOTIFKEY[6]
          GOTO      ADDNOT99 IF EQUAL
          MATCH     KEY32,NOTIFKEY[7]
          GOTO      ADDNOT99 IF EQUAL
          MATCH     KEY32,NOTIFKEY[8]
          GOTO      ADDNOT99 IF EQUAL
          MATCH     KEY32,NOTIFKEY[9]
          GOTO      ADDNOT99 IF EQUAL
          ADD       ONE,NOTIFCNT
          IF        NOTIFCNT<10
            MOVE      KEY32,NOTIFKEY[NOTIFCNT]
          ENDIF
ADDNOT99  RETURN
.-------------------------------------------------
. Format Name
.-------------------------------------------------
SETNAM00  PACK      CLRCR,CHAR015,SP1        * Remove CR Character Octal 015
          REP       CLRCR,NAME32
          PACK      SURNAME,SP70
          PACK      GIVEN,SP70
SETNAM05  CMATCH    SP1,NAME32               * Remove Leading Blanks
          IF        @EQUAL
            BUMP      NAME32
            GOTO      SETNAM05 IF NOT EOS
            GOTO      SETNAM99               * entire name if blank
          ENDIF
          PACK      KEY32,NAME32,SP70        * reset form pointer
          MOVE      KEY32,NAME32
.
          SCAN      COMMA,NAME32             * Locate the blank
          GOTO      SETNAM90 IF NOT EQUAL
          BUMP      NAME32,-1                * go back 1 to end of name
          MOVEFPTR  NAME32,CCITEM            * another handly form field
          RESET     NAME32                   * reset the form pointer
          SETLPTR   NAME32,CCITEM            * set logical length to end of name
.
.         Save this name
.
          MOVE      NAME32,SURNAME
          PACK      SURNAME,SURNAME,SP70
.
.         Check for any more names
.
          SETLPTR   NAME32,32              * reset logical length
          ADD       TWO,CCITEM             * position after comma
          RESET     NAME32,CCITEM          * reset to this point
          PACK      KEY32,NAME32,SP70      * reset form pointer
          MOVE      KEY32,NAME32
.
SETNAM10  CMATCH    SP1,NAME32               * Remove Leading Blanks
          IF        @EQUAL
            BUMP      NAME32
            GOTO      SETNAM10 IF NOT EOS
            GOTO      SETNAM99               * entire name if blank
          ENDIF
          PACK      KEY32,NAME32,SP70        * reset form pointer
          MOVE      KEY32,NAME32
.
          SCAN      SP1,NAME32             * Locate the blank
          GOTO      SETNAM80 IF NOT EQUAL
          BUMP      NAME32,-1              * go back 1 to end of name
          MOVEFPTR  NAME32,CCITEM          * another handly form field
          RESET     NAME32                 * reset the form pointer
          SETLPTR   NAME32,CCITEM          * set logical length to end of name
.
.         Save this name
.
          MOVE      NAME32,GIVEN
          PACK      GIVEN,GIVEN,SP70
          GOTO      SETNAM99
.
SETNAM80  MOVE      NAME32,GIVEN
          PACK      GIVEN,GIVEN,SP70
          GOTO      SETNAM99
.
SETNAM90  MOVE      NAME32,SURNAME
          PACK      SURNAME,SURNAME,SP70
.
SETNAM99  RETURN
.------------------------------------------------------------
. Result Header (Field Set 2)   PIT Items 200-299
.------------------------------------------------------------
RESHE000  COMPARE   "00",FLDITMNO
          IF        @EQUAL
            GOTO      RESHE999
          ENDIF
. 
          BRANCH    FLDITMNO,RESHE010,RESHE999,RESHE030,RESHE040,RESHE050:
                             RESHE060,RESHE070,RESHE080,RESHE090,RESHE100:
                             RESHE110,RESHE120
          GOTO      RESHE999
.
RESHE010   
          GOTO      RESHE999
.
RESHE030
          GOTO      RESHE999
.
RESHE040  UNPACK    TMRSDET,DIM22,DATE10,DIM2,SREPHCTM
          UNPACK    DATE10,DIM2,DIM1,DIM2A,DIM1A,DIM4
          PACK      SREPHCDT,DIM4,DIM2A,DIM2
          PACK      SREPHCTM,SREPHCTM,SP70
          GOTO      RESHE999
.
RESHE050  UNPACK    TMRSDET,DIM22,SREPHNAM
          GOTO      RESHE999
.
RESHE060  UNPACK    TMRSDET,DIM22,DATE10,DIM2,SREPHRTM
          UNPACK    DATE10,DIM2,DIM1,DIM2A,DIM1A,DIM4
          PACK      SREPHRDT,DIM4,DIM2A,DIM2
          PACK      SREPHRTM,SREPHRTM,SP70
          MATCH     SP70,SREPHRTM
          IF        @EQUAL
            CLOCK     TIME,CTIMEIS
            MOVE      CTIMEIS,SREPHRTM
          ENDIF
          GOTO      RESHE999
.
RESHE070  UNPACK    TMRSDET,DIM22,DIM1
          MOVE      ZERO,SREPHCON
          MATCH     "Y",DIM1
          IF        @EQUAL
            MOVE      ONE,SREPHCON
          ENDIF

          GOTO      RESHE999
.
RESHE080  UNPACK    TMRSDET,DIM22,SREPHTCT
          MOVE      SREPHTCT,RESULTUG
          GOTO      RESHE999
.
RESHE090
          GOTO      RESHE999
.
RESHE100  UNPACK    TMRSDET,DIM22,DIM1
          MOVE      DIM1,RESULTNM
          MOVE      ZERO,SREPHNOR
          MATCH     "Y",DIM1
          IF        @EQUAL
            MOVE      ONE,SREPHNOR
          ENDIF
          GOTO      RESHE999
.
RESHE110
          GOTO      RESHE999
.
RESHE120  UNPACK    TMRSDET,DIM22,DIM1
          MOVE      ZERO,SREPHCOM
          MATCH     "Y",DIM1
          IF        @EQUAL
            MOVE      ONE,SREPHCOM
          ENDIF
          GOTO      RESHE999
.
RESHE999  RETURN
.------------------------------------------------------------
. Write Result Detail Line
.------------------------------------------------------------
WRTDET00  ADD       ONE,LNECOUNT 
          MOVE      REPHRDT,REPDRDT
          MOVE      REPHRTM,REPDRTM
          MOVE      REPHRUN,REPDRUN 
          MOVE      LNECOUNT,REPDLIN
          MOVE      TMRSDET,REPDTXT
          MOVE      TMRSLIN,REPDPTL
.
          PACK      KEY20,REPDRDT,REPDRTM,REPDRUN,REPDLIN
          CALL      WRREPD1
. 
WRTDET99  RETURN
.------------------------------------------------------------
. Write Result Header Record & Links
.------------------------------------------------------------
WRTHED00  PACK      CLRCR,CHAR015,SP1        * Remove CR Character Octal 015
          REP       CLRCR,SREPHNAM
.
          MOVE      ZERO,SREPHRUN
          MOVE      ZERO,UNIQUEID 
          PACK      KEY17,SREPHRDT,SREPHRTM,Z70
          CALL      RSREPH1
          CALL      RPREPH1
          BRANCH    OVRCD,WRTHED10
          MATCH     SREPHRDT,REPHRDT
          GOTO      WRTHED10 IF NOT EQUAL
          MATCH     SREPHRTM,REPHRTM
          GOTO      WRTHED10 IF NOT EQUAL
          MOVE      REPHRUN,UNIQUEID
.
WRTHED10  ADD       ONE,UNIQUEID
          MOVE      UNIQUEID,SREPHRUN
          PACK      KEY17,SREPHRDT,SREPHRTM,SREPHRUN
          CALL      RAREPH1
          BRANCH    OVRCD,WRTHED20
          GOTO      WRTHED10
.
WRTHED20  MOVE      SREPHRDT,REPHRDT
          MOVE      SREPHRTM,REPHRTM
          MOVE      SREPHRUN,REPHRUN
          MOVE      SREPHFUR,REPHFUR
          MOVE      SREPHPID,REPHPID
          MOVE      SREPHVIS,REPHVIS
          MOVE      SREPHNAM,REPHNAM
          MOVE      SREPHLAB,REPHLAB
          MOVE      SREPHRRN,REPHRRN
          MOVE      SREPHCDT,REPHCDT
          MOVE      SREPHCTM,REPHCTM
          MOVE      SREPHNOR,REPHNOR
          MOVE      SREPHCON,REPHCON
          MOVE      SREPHTCT,REPHTCT
          MOVE      SREPHCOM,REPHCOM
          MOVE      SP70,REPHSTK
.
          PACK      KEY17,REPHRDT,REPHRTM,REPHRUN
          CALL      WRREPH1
          ADD       ONE,RESCOUNT
.
WRTHED99  RETURN
.------------------------------------------------------------
. Write Link Record
.------------------------------------------------------------
WRTLNK00  PACK      KEY35,SREPHLAB,LABRWARD,SP70
          CALL      RDREPM1
          BRANCH    OVRCD,WRTLNK10
.
          PACK      KEY6,REPMHWD,SP70
          CALL      RDWARD1
          IF        OVRCD=0
            MOVE      "04",LINKTYPE           * Link Ward/Dept from Result Text
            PACK      LINKKEYS,WWARD,SP70     * as well as Patients Ward
            CALL      NEWLNK00
          ENDIF
          GOTO      WRTLNK20
.
WRTLNK10  PACK      KEY6,LABRWARD,SP70
          CALL      RDWARD1
          IF        OVRCD=0
            MOVE      "04",LINKTYPE           * Link Ward/Dept from Result Text
            PACK      LINKKEYS,WWARD,SP70     * as well as Patients Ward
            CALL      NEWLNK00
          ENDIF
.
WRTLNK20  BRANCH    ERRMESS,WRTLNK90
.
          MOVE      "01",LINKTYPE
          MOVE      REPHPID,DIM8
          PACK      LINKKEYS,DIM8,SP70
          CALL      NEWLNK00
.
          CALL      GETVIS00
          MATCH     ZEROVISN,PVIBILL
          GOTO      WRTLNK99 IF EQUAL
.
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1           
          BRANCH    OVRCD,WRTLNK99     * Not Valid
.
          MOVE      "02",LINKTYPE
          PACK      LINKKEYS,PVIBILL,SP70
          CALL      NEWLNK00
.
          BRANCH    PVITYPE,WRTLNK50,WRTLNK60,WRTLNK70
.
WRTLNK50  CALL      LNKAAE00
          GOTO      WRTLNK99
.
WRTLNK60  CALL      LNKOUT00
          GOTO      WRTLNK99
.
WRTLNK70  CALL      LNKINP00
          GOTO      WRTLNK99
.
WRTLNK90  MOVE      "99",LINKTYPE           * Referring Doctor link
          PACK      LINKKEYS,RELGUID,SP70
          CALL      NEWLNK00
          ADD       ONE,ERRCOUNT            * Number of error messages
.
WRTLNK99  RETURN
.------------------------------------------------------------
. Link A & E Results
.------------------------------------------------------------
LNKAAE00  PACK      KEY8,PVIBILL
          CALL      RDDETA1
          BRANCH    OVRCD,LNKAAE99
.
          MOVE      "03",LINKTYPE
          PACK      LINKKEYS,ADADOCT,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE
          PACK      LINKKEYS,AEDARDOC,SP70
          CALL      NEWLNK00
.
LNKAAE99  RETURN
.------------------------------------------------------------
. Link Outpatient Results
.------------------------------------------------------------
LNKOUT00   PACK      KEY8,REPHPID
.          CALL      RDBOKB1
.          BRANCH    OVRCD,WRTLNK99
..
.          MOVE      "03",LINKTYPE
.          PACK      LINKKEYS,OBADOCT,SP70
.          CALL      NEWLNK00
..
.          PACK      KEY12,PVISITE,PVIDOCT
.          CALL      RDCLIA1
.          BRANCH    OVRCD,WRTLNK99
..
.          MOVE      "03",LINKTYPE
.          PACK      LINKKEYS,OCADOCT,SP70
.          CALL      NEWLNK00
..
LNKOUT99  RETURN
.------------------------------------------------------------
. Link Inpatient Results
.------------------------------------------------------------
LNKINP00  PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,LNKINP99          * No record
.
          MOVE      "04",LINKTYPE           * Ward link
          PACK      LINKKEYS,AWARD,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Attending doctor link
          PACK      LINKKEYS,ADOCTA,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Treating doctor link
          PACK      LINKKEYS,ADOCTT,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Referring Doctor link
          PACK      LINKKEYS,ADOCTR,SP70
          CALL      NEWLNK00
.
LNKINP99  RETURN
.------------------------------------------------------------
.  Get visit number
.------------------------------------------------------------
GETVIS00  MOVE      REPHPID,DIM8   
          PACK      KEY24,DIM8,REPHRDT,SP70
          CALL      RDSVISA2
          CALL      RDPVISA2
          BRANCH    OVRCD,GETVIS20
          MOVE      REPHPID,DIM8
          MATCH     PVIURNO,DIM8
          GOTO      GETVIS99 IF EQUAL
.
GETVIS20  PACK      KEY24,DIM8,REPHRDT,SP70
          CALL      RDSVISA2
          CALL      RDKVISA2
          BRANCH    OVRCD,GETVIS30
          MOVE      REPHPID,DIM8
          MATCH     PVIURNO,DIM8
          GOTO      GETVIS99 IF EQUAL
.
GETVIS30  MOVE      ZEROVISN,PVIBILL
.
GETVIS99  RETURN
.------------------------------------------------------------
. New link record
.------------------------------------------------------------
NEWLNK00  MATCH     SP70,LINKKEYS
          GOTO      NEWLNK99 IF EQUAL
.
          PACK      KEY29,LINKTYPE,LINKKEYS,REPHRDT,REPHRTM,REPHRUN
          UNPACK    KEY29,RELNLTY,RELNLKY,RELNRDT,RELNRTM,RELNRUN
.
          PACK      RESLNKFL,PRELEN,RELNRDT           * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      NEWLNK99 IF EQUAL                 * end I CAR 38290
.
          CALL      RDRELN1
          IF        OVRCD=1
            MOVE      ZERO,RELNMSN            * 0=no , 1=yes
            MOVE      SP70,RELNSDT
            MOVE      SP70,RELNSTM
            MOVE      SP70,RELNSPC
            MOVE      REPHFUR,RELNFUR
            MOVE      REPHPID,RELNPID
            MOVE      SP70,RELNUCS
            MOVE      SP70,RELNUSC
            MOVE      SP70,RELNDSS
            MOVE      REPHNOR,RELNNOR
.
            PACK      KEY29,RELNLTY,RELNLKY,RELNRDT,RELNRTM,RELNRUN
            CALL      WRRELN1
          ENDIF
.
NEWLNK99  RETURN
.------------------------------------------------------------
. E-mail Notifications
.------------------------------------------------------------
EMLNOT00  COMPARE  ZERO,NOTIFCNT
          GOTO     EMLNOT99 IF EQUAL
          MOVE     ZERO,MAILFLAG
.
          MOVE     ZERO,NOTIFCNT
EMLNOT10  ADD      ONE,NOTIFCNT
          MATCH    SP70,NOTIFKEY[NOTIFCNT]
          GOTO     EMLNOT90 IF EQUAL
          MOVE     NOTIFKEY[NOTIFCNT],KEY32
          CALL     RDREPE1
          BRANCH   OVRCD,EMLNOT90
          COMPARE  ONE,REPEALL
          GOTO     EMLNOT20 IF EQUAL
          IF       REPEABN=1
            MATCH    "N",RESULTNM
            GOTO     EMLNOT20 IF EQUAL
          ENDIF
          IF       REPEURG=1
            MATCH    "U",RESULTUG
            GOTO     EMLNOT20 IF EQUAL
          ENDIF
          GOTO     EMLNOT90
.  
EMLNOT20  IF       MAILFLAG=0
            CALL     EMLFIL00
            MOVE     ONE,MAILFLAG
          ENDIF
.
          CLEAR    MAILCMD
          APPEND   "mail ",MAILCMD
          APPEND   REPEEML,MAILCMD
          APPEND   "<",MAILCMD
          APPEND   MAILFNAM,MAILCMD
          APPEND   ".txt",MAILCMD
          RESET    MAILCMD
          EXECUTE  MAILCMD,TASKID
          DISPLAY  MAILCMD
.
EMLNOT90  COMPARE "9",NOTIFCNT
          GOTO    EMLNOT10 IF NOT EQUAL
          IF      MAILFLAG=1
            PREP     MAILFILE,MAILFNAM
            CLOSE    MAILFILE,DELETE
          ENDIF
.
EMLNOT99  RETURN
+
.------------------------------------------------------------
. Create E-mail File
.------------------------------------------------------------
EMLFIL00  
.         CLEAR    MAILFNAM
.         APPEND   "mailer",MAILFNAM
.         APPEND   PORT,MAILFNAM
.         RESET    MAILFNAM
.
          PREP     MAILFILE,MAILFNAM
          WRITE    MAILFILE,SEQ;"Subject: Pathology Result Notification"
          WRITE    MAILFILE,SEQ;CNAME
          WRITE    MAILFILE,SEQ;"Pathology Result Notification"
          WRITE    MAILFILE,SEQ;"============================="
          WRITE    MAILFILE,SEQ;"Date    : ",CPCDATE,"   Time : ",CTIMEIS
          WRITE    MAILFILE,SEQ;"Lab     : ",SREPHLAB
          WRITE    MAILFILE,SEQ;"Run     : ",SREPHRRN
          WRITE    MAILFILE,SEQ;"Pathology Result Received for"
          WRITE    MAILFILE,SEQ;"Patient : ",SPATNAME
          WRITE    MAILFILE,SEQ;"Test    : ",SREPHNAM
          WRITE    MAILFILE,SEQ;"Normal  : ",RESULTNM
          CLOSE    MAILFILE
.
EMLFIL99  RETURN
+
.------------------------------------------------------------
. Dummy Labels for Web Includes
.------------------------------------------------------------
SETPAR00  RETURN
CLRPAR00  RETURN
PROFLD00  RETURN
          INC     AAEDE1IO/INC 
          INC     PATGSRIO/INC
          INC     PATMA1IO/INC
          INC     PATPR1IO/INC
          INC     PATMI1IO/INC
          INC     PMSPX2IO/INC
          INC     PMSVX1IO/INC
          INC     PATVISIO/INC
          INC     PATWR1IO/INC
          INC     RESLOGIO/INC
          INC     RESLNKIO/INC
          INC     RESPDSIO/INC
          INC     RESPHRIO/INC
          INC     RESPMPIO/INC
          INC     RESPENIO/INC
          INC     RESLABIO/INC
          INC     IBAWEBCD
.
          INC       TFILENAM
.

.-------------------------------------------------------------------------------
. Program       : PHPWEB01 
.
. Function      : CISAM Version of PHP Calls
.
. Modifications  :
.-------------------------------------------------------------------------------
.         V10.13.01  05/12/2018  Don Nguyen      TSK 0838558
.                    Moved temp file creation (CRHTMC00) to comments processing
.                    section rather than on server startup.
.                    Removed PORT from temp filename (TEMPNAME).
.-------------------------------------------------------------------------------
.         V10.11.01  21/09/2017  Thanh T.        TSK 0821710
.                    Removed PATCOMFD since it is not used.
.-------------------------------------------------------------------------------
.         V10.08.01  23/08/2016  Peter McMullen  0823848
.                    Add lower case login search to XMLRED00
.-------------------------------------------------------------------------------
.         V10.07.01  05/11/2015  Steve Armstrong  CAR 303363
.                    Recompiled for changes to RESHEAFD
.-------------------------------------------------------------------------------
.         V10.06.01  24/07/2015 Davin             CAR 308063
.                    Added note security level (obccslv) to COMW1100 output
.         V10.04.01  20/02/2014 Steve Armstrong   CAR 290918
.                    Recompiled for changes to PMSDSDFD
.-------------------------------------------------------------------------------
.         V10.03.03  B.G. Ackland   CAR ENHANCEMENT
.                    Added Problems Flag
.         V10.03.02  B.G. Ackland   CAR 264486 
.                    Output Source Directory in url2 of document list
.         V10.03.01  B.G. Ackland   CAR 264486 
.                    New wbedcard option
.-------------------------------------------------------------------------------
.         V10.01.01  Saroeun Soeur  CAR 265085
.                    PATA1200 - fixed FLAGTHE check to use the correct index
.                    
.         V10.01.00  B.G. Ackland   CAR 265085
.                    Program created.
.-------------------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       WEBDATDF
          INC       PATMA1FD/INC
          INC       PATGSRFD/INC 
          INC       PMSCCDFD/INC 
          INC       BOKRC1FD/INC 
          INC       PATVISFD/INC 
          INC       OBSDETFD/INC 
          INC       OBSPCOFD/INC 
          INC       OBSPCTFD/INC       * Correspondence Storage Location Table
          INC       OBSMDTFD/INC 
          INC       OBSMTXFD/INC 
          INC       EMRVCDFD/INC 
          INC       VISMDTFD/INC 
          INC       VISMTXFD/INC 
.
          INC       OBSCNAFD/INC
          INC       OBSCNBFD/INC
          INC       OBSCNCFD/INC
          INC       OBSTYPFD/INC
          INC       PATHSPFD/INC
          INC       MLTSECFD/INC
.
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDGWFD/INC
          INC       PATFN1FD/INC
          INC       PATIN1FD/INC
          INC       PATITMFD/INC
          INC       PMSDSDFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPTD/INC
          INC       PMSHCGTD/INC
          INC       PMSPRBFD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       PMSVX1FD/INC
.
          INC       PATPR1FD/INC
          INC       PATWR1FD/INC
          INC       PATMI1FD/INC
          INC       MRTMASFD/INC
          INC       OUTSITFD/INC
          INC       RESCLNFD/INC
          INC       RESLURFD/INC
          INC       RESLNKFD/INC
          INC       RESHEAFD/INC
          INC       PATALRFD/INC
          INC       HL7CODFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
.
          INC       TFILEVAR/INC
.
. Variable Declarations  
.
ALERTDES  DIM       20
ALLVISIT  FORM      1
DESTINAT  DIM       50
RESLNK01  DIM       8   * Result Date
RESLNK02  DIM       5   * Time
RESLNK03  DIM       10  * DSS
RESLNK04  DIM       2   * Status
RESLNK05  DIM       1   * Normal
RESLNK06  DIM       25  * Provider
RESLNK07  DIM       20  * Reference
.
RESULTKY  DIM       17
REFERLKY  DIM       8
RESULTYR  DIM       4
YEAROPEN  DIM       4
UNIQUEID  FORM      4
TABCFLAG  FORM      1
AT        INIT      "@"
PREHEA    INIT      "reha"
PREHEB    INIT      "rehb"
PREHEC    INIT      "rehc"
PREHED    INIT      "rehd"
PREDEA    INIT      "reda"
PREDEB    INIT      "redb"
PREDEC    INIT      "redc"
PRELEN    INIT      "reln"    * I CAR 38290
ANSBK     INIT      "BK"
.
CMNDLINE  DIM       127
DATELUPD  DIM       11        * last update date
DTYPDESC  DIM       25
LINKTYPE  DIM       2
LINKKEYS  DIM       10
.
.
HL7TABID  DIM       4
SAVISIT   DIM       8         * Input date          
FORM8     FORM      8
CURRDATE  DIM       8         * Current date
CMDLINE   DIM       127       * Command line instruction
CORRESID  FORM      4
COPYFKEY  DIM       20        * Document Copied From Key
CORSFILE  DIM       20     
CORSP001  DIM       20        * Correspondence File Name
CORSP002  DIM       8         * Input date          
CORSP003  DIM       3         * Type of correspondance
CORSP004  DIM       8         * Input Time        
CORSP005  DIM       1         * Mark as deleted 
CORSP006  DIM       2         * Security Level  
CORSP007  DIM       50        * Comment 1
CORSP008  DIM       50        * Comment 2
CORSP009  DIM       30        * From 
CORSP010  DIM       30        * To
CORSP011  DIM       3         * User Def Code 1
CORSP012  DIM       3         * User Def Code 2 1
CORSP013  DIM       3         * User Def Y/N 1  
CORSP014  DIM       3         * User Def Y/N 1   
CORSP015  DIM       8         * User Def Date 1  
CORSP016  DIM       8         * User Def Date 2  
CORSP017  DIM       2         * Alert Category
CORSP018  DIM       3         * Alert Code
DRAFTDOC  DIM       1         * Current Draft Document   1=true
HTMLLINE  DIM       200        * HTML Page Submitted
HTMLLNK2  DIM       200
HTMLLNK3  DIM       200
HTMLLNK4  DIM       200
HTMLCNT   FORM      6         * HTML Page Submitted
TEMPNAME  DIM       8         * HTML Page Submitted
TEMPFILE  FILE      ASCII,FIXED=127
.
AUDITKEY  DIM       20        * Key to Patient Correspondence Table
AMBERSND  INIT      "\&"
DASH      INIT      "-"
DATE      DIM       11        * Correspondence Date
DETAILKY  DIM       20        * Key to Patient Correspondence Table
DIM4      DIM       4
DIM6      DIM       6
DIM8      DIM       8
DIM100    DIM       100
DOCTCODE  DIM       6
.
EMRGFLAG  DIM       1         * Flag for emergency
.
FORMACTN  DIM       3         * Form Action      
.
INPTDATE  DIM       8         * parameter used in INPDTE00 to display a date
INPTWUID  DIM       10        * parameter used in DISUSR00 to display user 
.
LINKTYP   DIM       3         * Type of Correspondance 
LNK2PRT   FORM      1
.
MBSDESC   DIM       40
CMDMOVE   INIT      "mv "
.
NEXTPAGE  FORM      1         * Nextpage parameter
NOTEDESC  DIM       20
NOTECODE  DIM       3 
.
OBPCT001  DIM       4         * Storage Location ID
OBPCT002  DIM       80        * Storage Directory
OBPCT003  DIM       80        * URL Prefix
OBPCT004  DIM       1         * Read ONly 0=NO, 1=YES
OBPCT005  DIM       8         * Date Closed
OBPCT006  DIM       1         * Set as Cuurent Storage Location 0=No, 1=Yes
OUTVAR    DIM       127
.
CATWI     INIT      "wi"
CLIDF001  DIM       5         * Data Field Number
CLIDF002  DIM       50        * Description
CLIDF003  DIM       20        * Group Description
CLIDF004  DIM       8         * Table
CLIDF005  DIM       8         * Field
.
CLICT001  DIM       5         * Download Type
CLICT002  DIM       35        * Description
CLICT003  DIM       2         * Security Level
CLICT004  DIM       5         * Copy from
CLICT005  DIM       5         * Copy to
.
CLICD001  DIM       5         * Download Type
CLICD002  DIM       5         * Data Field Number
CLICD003  DIM       3         * Download Sequence 
CLICD004  DIM       25        * Heading Name 
CLICD005  DIM       1         * Convert to Title Text
.
FAXNUMBR  DIM       20
RESLNKFL  DIM       8         * File name var for reslnk CAR 38290
RECIPNAM  DIM       20
SENDFAXB  DIM       1
TESTKEY   DIM       16        * Last Update date and time
TIME      DIM       5         * Time displayed in 00:00 format 
UPDATEKY  DIM       16        * Update key(date & time of last update) 
UPDTTYPE  DIM       1         * Update Type (A)dd,(U)pdate,(D)elete,Blank=disp
STARTKEY  DIM       10        * Starting Key
CORRTYPE  DIM       5         * Current Field Number for Next & Prev pages
CORRDESC  DIM       35        * Download Type Description 
VISTDATE  DIM       12
.
LOKKEY20  DIM       20                                               * I-90207
SAVE2PDF  FORM      1
HTML2PDF  INIT      "ConvertHTML2PDF"
SENDSUMM  INIT      "SendDischargeSummary"
PRINTEXE  INIT      "PrintDocument"
SENDTEXE  INIT      "SendDocument"
.
SENDTYPE  FORM      1         * Passed to SendDocument. 1=Fax, 2=Email, 3=Email Copy to Self, 4=Email & SMS Notification?
SENDTOPH  DIM       25        * Passed to SendDocument. Fax to Phone Number
SENDFREM  DIM       70        * Passed to SendDocument. Email From Address
SENDTOEM  DIM       70        * Passed to SendDocument. Email To Address
COPYTOCT  FORM      1         * Email Copy to Count
COPYTOEM  DIM       70[9]     * Passed to SendDocument. CC Email Addresses (Not Implemented)
HCPFROMC  DIM       10        * Send From HCP Code (Not Implemented)
HCPSENDC  DIM       10        * Send To HCP Code   (Not Implemented)
HCPCOPCT  FORM      1         * HCP Copy to Count
HCPCOPYC  DIM       10[9]     * Copy To HCP Code   (Not Implemented)
PRINTFMT  DIM       10        * Printing Format for HTML to PDF conversion
.
PHPFILEN  DIM       80        * PHP File
OBSFLAG   FORM      1
MEDFLAG   FORM      1
DOCFLAG   FORM      1
DIAFLAG   FORM      1
BILFLAG   FORM      1
HISFLAG   FORM      1
THEFLAG   FORM      1
PRBFLAG   FORM      1
VISFLAG   FORM      1
NOTFLAG   FORM      1
AMTFLAG   FORM      1
DISFLAG   FORM      1
FIRSTROW  FORM      1
ROWCOUNT  FORM      3
DISPDATE  DIM       13
DOCTTYPE  DIM       3
CATct     INIT      "ct"
CATBV     INIT      "BV"
CATDC     INIT      "DC"
BVDESC    DIM       20
BVIND1    DIM       1 
DCDESC    DIM       20
WARDCODE  DIM       3
WBEDCODE  DIM       3
.
.
.-----------------------------------------------------------------------
PRGID     INIT      "PHPWEB01"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "PHP Services for CISAM"
.-----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      XMLRED00       * Read Fifo WEBPID and USERID    * C-90207
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,MAIN1000
.
          MATCH     "comweb01",PHPFILEN
          IF        @EQUAL
            CALL      XMLHED00
            CALL      COMW0000
            CALL      XMLEND00
            GOTO      MAIN1000
          ENDIF
.
          MATCH     "cliweb02",PHPFILEN
          IF        @EQUAL
            CALL      XMLHED00
            CALL      CLIA0000
            CALL      XMLEND00
            GOTO      MAIN1000
          ENDIF
.
          MATCH     "cliweb08",PHPFILEN
          IF        @EQUAL
            CALL      XMLHED00
            CALL      CLIB0000
            CALL      XMLEND00
            GOTO      MAIN1000
          ENDIF
.
          MATCH     "patweb98",PHPFILEN
          IF        @EQUAL
            CALL      XMLHED00
            CALL      PATA0000
            CALL      XMLEND00
            GOTO      MAIN1000
          ENDIF
.
          MATCH     "patweb89",PHPFILEN
          IF        @EQUAL
            CALL      XMLHED00
            CALL      PATB0000
            CALL      XMLEND00
            GOTO      MAIN1000
          ENDIF
.
          MATCH     "wbedcard",PHPFILEN
          IF        @EQUAL
            CALL      XMLHED00
            CALL      BEDCRD00
            CALL      XMLEND00
            GOTO      MAIN1000
          ENDIF
.
          GOTO      MAIN1000
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.
PROFLD00
          RETURN
.-------------------------------------------------------------------------------
. Open Files and Initialize Variables
.-------------------------------------------------------------------------------
INIT0000  
.          CALL      INTMAS00
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSPRBA1,"pmsprbaf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      OBSDETA1,"obsdetaf"
          OPEN      OBSDETA1,"obsdetaf"
          OPEN      OBSPCOA2,"obspcoaf"
          OPEN      OBSPCOA5,"obspcoaf"
          OPEN      OBSPCTA1,"obspctaf"
          OPEN      OBSCNAA1,"obscnaaf"
          OPEN      OBSCNBA1,"obscnbaf"
          OPEN      OBSCNCA1,"obscncaf"
          OPEN      OBSCNCA2,"obscncaf"
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      OBSMDTA1,"obsmdtaf"
          OPEN      OBSMTXA1,"obsmtxaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      EMRVCDA1,"emrvcdaf"
          OPEN      BOKRC1A2,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          OPEN      OBSTYPA1,"obstypaf"
          OPEN      WEBSECA1,"websecaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND12;*101,PTCNUFFR
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
            OPEN      MLTSECA1,"mltsecaf"
          ENDIF
.
INIT9999  RETURN
.-------------------------------------------------------------------------------
. Clear Parameters
.-------------------------------------------------------------------------------
CLRPAR00  MOVE      SP70,PHPFILEN 
          MOVE      SP70,DOCTTYPE 
          MOVE      SP70,WARDCODE 
          MOVE      SP70,WBEDCODE 
          MOVE      SP70,URNUMBER 
          MOVE      SP70,ADMISSNO
          MOVE      SP70,FORMACTN
          MOVE      SP70,COPYFKEY
          MOVE      SP70,DRAFTDOC
          MOVE      ZERO,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,UPDATEKY
          MOVE      SP70,VIEWTYPE
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,STARTKEY
          MOVE      SP70,CORRTYPE
          MOVE      SP70,DETAILKY
          MOVE      SP70,AUDITKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,LINKTYP  
          MOVE      Z70,SENDFAXB 
          MOVE      Z70,RECIPNAM 
          MOVE      Z70,FAXNUMBR 
.
          MOVE      ZERO,SENDTYPE
          MOVE      SP70,SENDTOPH
          MOVE      SP70,SENDFREM
          MOVE      SP70,SENDTOEM
          MOVE      ZERO,COPYTOCT
          MOVE      SP70,COPYTOEM[1]
          MOVE      SP70,COPYTOEM[2]
          MOVE      SP70,COPYTOEM[3]
          MOVE      SP70,COPYTOEM[4]
          MOVE      SP70,COPYTOEM[5]
          MOVE      SP70,COPYTOEM[6]
          MOVE      SP70,COPYTOEM[7]
          MOVE      SP70,COPYTOEM[8]
          MOVE      SP70,COPYTOEM[9]
          MOVE      SP70,HCPFROMC
          MOVE      SP70,HCPSENDC
          MOVE      ZERO,HCPCOPCT
          MOVE      SP70,HCPCOPYC[1]
          MOVE      SP70,HCPCOPYC[2]
          MOVE      SP70,HCPCOPYC[3]
          MOVE      SP70,HCPCOPYC[4]
          MOVE      SP70,HCPCOPYC[5]
          MOVE      SP70,HCPCOPYC[6]
          MOVE      SP70,HCPCOPYC[7]
          MOVE      SP70,HCPCOPYC[8]
          MOVE      SP70,HCPCOPYC[9]
.
          MOVE      SP70,SELPRINT
          MOVE      ZERO,NOCOPIES
          MOVE      ZERO,SAVE2PDF
          MOVE      SP70,OBPCT001 
          MOVE      SP70,OBPCT002 
          MOVE      SP70,OBPCT003 
          MOVE      SP70,OBPCT004 
          MOVE      SP70,OBPCT005
          MOVE      ZERO,OBPCT006 
.
          MOVE      SP70,CLIDF001
          MOVE      SP70,CLIDF002
          MOVE      SP70,CLIDF003
          MOVE      SP70,CLIDF004
          MOVE      SP70,CLIDF005
.
          MOVE      SP70,CLICT001 
          MOVE      SP70,CLICT002 
          MOVE      SP70,CLICT003 
          MOVE      SP70,CLICT004 
          MOVE      SP70,CLICT005 
.
          MOVE      SP70,CLICD001  
          MOVE      SP70,CLICD002  
          MOVE      SP70,CLICD003 
          MOVE      SP70,CLICD004 
          MOVE      SP70,CLICD005 
          MOVE      SP70,CORRTYPE
          MOVE      SP70,CORRDESC
.
          MOVE      SP70,RESLNK01
          MOVE      SP70,RESLNK02
          MOVE      SP70,RESLNK03
          MOVE      SP70,RESLNK04
          MOVE      SP70,RESLNK05
          MOVE      SP70,RESLNK06
          MOVE      SP70,RESLNK07
.
          MOVE      SP70,CORSFILE
          MOVE      SP70,CORSP001
          MOVE      SP70,CORSP002
          MOVE      SP70,CORSP003
          MOVE      SP70,CORSP004
          MOVE      SP70,CORSP005
          MOVE      SP70,CORSP006
          MOVE      SP70,CORSP007
          MOVE      SP70,CORSP008
          MOVE      SP70,CORSP009
          MOVE      SP70,CORSP010
          MOVE      SP70,CORSP011
          MOVE      SP70,CORSP012
          MOVE      SP70,CORSP013
          MOVE      SP70,CORSP014
          MOVE      SP70,CORSP015
          MOVE      SP70,CORSP016
          MOVE      SP70,CORSP017
          MOVE      SP70,CORSP018
          MOVE      SP70,RESULTKY
          MOVE      SP70,PRINTFMT
          MOVE      SP70,REFERLKY
          MOVE      SP70,EMRGFLAG
.
CLRPAR99  RETURN
.-------------------------------------------------------------------------------
. Set Parameters
.-------------------------------------------------------------------------------
SETPAR00  MATCH     "phpfilen=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PHPFILEN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "docttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DOCTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wardcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WARDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wbedcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WBEDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "htmlline=",QRYNAME
          IF        @EQUAL
            CALL      CRHTMC00         * Create HTML comments temp file
            CALL      GETHTM00     
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "detailky=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DETAILKY
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------
.         Create HTML comments temp file
.------------------------------------------------------------
CRHTMC00  
.         CLEAR     TEMPNAME
.         APPEND    "CLIW08",TEMPNAME
.         APPEND    PORT,TEMPNAME
.         RESET     TEMPNAME
.         REP       " 0",TEMPNAME
.
          CALL      TFILENAM
          MOVE      TFILNAME,TEMPNAME
.
          CLEAR     KEY13
          APPEND    TEMPNAME,KEY13
          APPEND    ".html",KEY13
          RESET     KEY13
.
          PREP      TEMPFILE,KEY13
          MOVE      ZERO,HTMLCNT
.
CRHTMC99  RETURN
+
.------------------------------------------------------------
. Get Text Comments
.------------------------------------------------------------
GETHTM00  ADD       ONE,HTMLCNT
          PACK      HTMLLINE,QRYDATA,SP70,SP70
          WRITE     TEMPFILE,SEQ;HTMLLINE
.
GETHTM10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA * Read CONFFILE sequentially
          GOTO      GETHTM99 IF OVER             * If end of file
          MATCH     SP70,QRYNAME                 * If next CGI parameter
          GOTO      GETHTM80 IF NOT EQUAL
          ADD       ONE,HTMLCNT
          PACK      HTMLLINE,QRYDATA,SP70,SP70
          WRITE     TEMPFILE,SEQ;HTMLLINE
          GOTO      GETHTM10
.
GETHTM80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
GETHTM99  RETURN
.----------------------------------------------------------------------------------------------------
. comweb01.php functions
.----------------------------------------------------------------------------------------------------
COMW0000  BRANCH    REPORTNO,COMW1100,COMW1200,COMW1300,COMW1400
          GOTO      COMW9999
.----------------------------------------------------------------------------------------------------
. Note Type Options 
.
.       $qry = "SELECT obcctyp||obcctmi, obccdes
.               FROM   obscncaf
.               WHERE  obcctyp <> ' '";
COMW1100  PACK      KEY38,SP70
          CALL      RSOBCC2                 * Positional Read
COMW1110  CALL      RKOBCC2                 * Sequential Read by Description
          BRANCH    OVRCD,COMW9999          
          WRITE     HTMLFILE;"<option value=#"",OBCCTYP,OBCCTMI,OBCCSLV:
                                 "#">",OBCCDES,"</option>"
          GOTO      COMW1110
.----------------------------------------------------------------------------------------------------
. Alert Type Options (H1-H9)
.
.       $qry = "SELECT tcode, tdesc
.               FROM patcodes
.               WHERE tcode in ('H1','H2','H3','H4','H5','H6','H7','H8','H9')
.               AND acode = ' '
.               ORDER BY tcode" ;

COMW1200  MOVE      ONE,F1
COMW1210  PACK      KEY5,ANSH,F1,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,COMW1220
.
          MOVE      TDESC,KEY20
          PACK      KEY4,QUOTE,TCODE,QUOTE
          WRITE     HTMLFILE;"<option value=",KEY4,">",KEY20,"</option>"
.
COMW1220  COMPARE   NINE,F1
          GOTO      COMW9999 IF EQUAL
          ADD       ONE,F1
          GOTO      COMW1210
.----------------------------------------------------------------------------------------------------
. Observation Type Options
.
.       $qry = "SELECT obtytmad, obtydesc
.               FROM   obstypaf
.               WHERE  obtyactv = 1";

COMW1300  MOVE      SP70,KEY3
          CALL      RSOBTY1
COMW1310  CALL      RKOBTY1
          BRANCH    OVRCD,COMW9999
          MATCH     "1",OBTYACTV
          GOTO      COMW1310 IF NOT EQUAL
          WRITE     HTMLFILE;"<option value=#"",OBTYTMAD,"#">",OBTYDESC,"</option>"
          GOTO      COMW1310
.----------------------------------------------------------------------------------------------------
. Hospital Options
.
.       $qry = "SELECT pthshosp,pthsname
.               FROM pathspaf
.               where exists (select 1 from mltsecaf where mlscusid='$this->uid' and ( mlschosp=pthshosp or mlschosp=' ' ))
.               order by pthsname";

COMW1400  BRANCH    IBCNMHOS,COMW9999
          MOVE      ZERO,F1
          PACK      KEY13,USERID,SP70
          CALL      RDMLSEC1
          IF        OVRCD<>1
            MOVE      ONE,F1               * Access to all hospitals
          ENDIF
.
          PACK      KEY3,SP70
          CALL      RSPTHSP1
COMW1410  CALL      RKPTHSP1
          BRANCH    OVRCD,COMW9999
          IF        F1=1
            PACK      KEY13,USERID,PTHSHOSP,SP70
            CALL      RDMLSEC1
            BRANCH    OVRCD,COMW1410
          ENDIF
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          GOTO      COMW1410
.
COMW9999  RETURN
.----------------------------------------------------------------------------------------------------
. patweb98.php functions
.----------------------------------------------------------------------------------------------------
PATA0000  BRANCH    REPORTNO,PATA1100,PATA1200
          GOTO      PATA9999
.----------------------------------------------------------------------------------------------------
. Check for Visits
.
.       $qry = "SELECT count(*)
.               FROM patvisaf
.               WHERE dpvibill='{$this->admis}'";
.       $this->getScalarValue($qry);
.
PATA1100  MOVE      ZERO,F1
          PACK      KEY24,URNUMBER,SP70
          CALL      RSPTVIS2
          CALL      RKPTVIS2
          IF        OVRCD=0
            MATCH     URNUMBER,PVIURNO
            IF        @EQUAL
              MOVE      ONE,F1
            ENDIF
          ENDIF
          WRITE     HTMLFILE;F1
          GOTO      PATA9999
.
PATA1200  MOVE      ZERO,OBSFLAG
          MOVE      ZERO,MEDFLAG
          MOVE      ZERO,DOCFLAG
          MOVE      ZERO,DIAFLAG
          MOVE      ZERO,BILFLAG
          MOVE      ZERO,HISFLAG
          MOVE      ZERO,PRBFLAG
          MOVE      ZERO,THEFLAG
          MOVE      ZERO,VISFLAG
          MOVE      ZERO,NOTFLAG
          MOVE      ZERO,AMTFLAG
          MOVE      ZERO,DISFLAG
          MOVE      ZERO,F1
.
          PACK      KEY24,URNUMBER,SP70
          CALL      RSPTVIS2
          CALL      RKPTVIS2
          IF        OVRCD=0
            MATCH     URNUMBER,PVIURNO
            IF        @EQUAL
              MOVE      ONE,VISFLAG
            ENDIF
          ENDIF

.
. Check for Observations for this Visit
.
          PACK      KEY24,ADMISSNO,SP70
          CALL      RSOBDE1
          CALL      RKOBDE1
          IF        OVRCD=0
            MATCH     ADMISSNO,OBDEVIST
            IF        @EQUAL
              MOVE      ONE,OBSFLAG
            ENDIF
          ENDIF
.
.    Check for Clinical Documents
.
          PACK      KEY28,ADMISSNO,SP70
          CALL      RSOBPC2
          CALL      RKOBPC2
          IF        OVRCD=0
            MATCH     ADMISSNO,OBPCCVIS
            IF        @EQUAL
              MOVE      ONE,DOCFLAG
            ENDIF
          ENDIF
.
.    Check for Clinical Notes
.
          PACK      KEY12,ADMISSNO,SP70
          CALL      RSOBCA1
          CALL      RKOBCA1
          IF        OVRCD=0
            MATCH     ADMISSNO,OBCAVIS
            IF        @EQUAL
              MOVE      ONE,NOTFLAG
            ENDIF
          ENDIF
.
.    Check for Emergency Clinical Notes for this Visit
.
          MOVE      "001",KEY3
          PACK      KEY17,ADMISSNO,KEY3,SP70
          CALL      RSVSMDT1
          CALL      RKVSMDT1
          IF        OVRCD=0
            MATCH     KEY3,VSMDTYPE
            IF        @EQUAL
              MATCH     ADMISSNO,VSMDVISN
              IF        @EQUAL
                MOVE      ONE,NOTFLAG
              ENDIF
            ENDIF
          ENDIF
.
.    Check for Emergency Past History Notes for this U/R
.
          PACK      KEY14,URNUMBER,SP70
          CALL      RSOBMDT1
          CALL      RKOBMDT1
          IF        OVRCD=0
            MATCH     URNUMBER,VSMDVISN
            IF        @EQUAL
              MOVE      ONE,HISFLAG
            ENDIF
          ENDIF
.
.    Check for Emergency Procedures
.
          MOVE      "004",KEY3
          PACK      KEY14,ADMISSNO,KEY3,SP70
          CALL      RSEMVCD1
          CALL      RKEMVCD1
          IF        OVRCD=0
            MATCH     KEY3,EMVCTYPE
            IF        @EQUAL
              MATCH     ADMISSNO,EMVCVIST
              IF        @EQUAL
                MOVE      ONE,BILFLAG
              ENDIF
            ENDIF
          ENDIF
.
.    Check for Emergency Diagnosis
.
          MOVE      "005",KEY3
          PACK      KEY14,ADMISSNO,KEY3,SP70
          CALL      RSEMVCD1
          CALL      RKEMVCD1
          IF        OVRCD=0
            MATCH     KEY3,EMVCTYPE
            IF        @EQUAL
              MATCH     ADMISSNO,EMVCVIST
              IF        @EQUAL
                MOVE      ONE,DIAFLAG
              ENDIF
            ENDIF
          ENDIF
.
.    Check for Theatre
.
          PACK      KEY16,URNUMBER,SP70
          CALL      RSBKREC6
          CALL      RKBKREC6
          IF        OVRCD=0
            MATCH     URNUMBER,BKREURNO
            IF        @EQUAL
              PACK      KEY5,ANSBK,BKRETYPE
              CALL      RDCODE1
              IF        OVRCD=0
                MATCH     ACODE,BKRETYPE
                IF        @EQUAL
                  MATCH     "THE",THCSCOD
                  MOVE      ONE,THEFLAG
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
.    Check for Problems
.
          PACK      KEY13,URNUMBER,SP70
          CALL      RSPMPRB1
          CALL      RKPMPRB1
          IF        OVRCD=0
            MATCH     URNUMBER,PMPBURNO
            IF        @EQUAL
              MOVE      ONE,PRBFLAG
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"[{#"observation#":#"",OBSFLAG,"#",":
                              "#"medications#":#"",MEDFLAG,"#",":
                              "#"clinicalDoc#":#"",DOCFLAG,"#",":
                              "#"diagnosis#":#"",DIAFLAG,"#",":
                              "#"billing#":#"",BILFLAG,"#",":
                              "#"history#":#"",HISFLAG,"#",":
                              "#"theatre#":#"",THEFLAG,"#",":
                              "#"visits#":#"",VISFLAG,"#",":
                              "#"notes#":#"",NOTFLAG,"#",":
                              "#"problems#":#"",PRBFLAG,"#",":
                              "#"dischargeAmount#":#"",AMTFLAG,"#",":
                              "#"discharge#":#"",DISFLAG,"#"}]";
          GOTO      PATA9999
.
PATA9999  RETURN
.----------------------------------------------------------------------------------------------------
. patweb89.php functions
.----------------------------------------------------------------------------------------------------
PATB0000  BRANCH    REPORTNO,PATB1100,PATB1200
          GOTO      PATB9999
.----------------------------------------------------------------------------------------------------
. Alias List
.        $qry = "SELECT gsrsnam,gsrgnam,gsrdob,gsrsex
.                FROM   patgsrnf
.                WHERE  gsrurno = '{$this->urno}'";
.        $this->getAliasList($qry);
.
PATB1100  MOVE      ONE,FIRSTROW
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PATB9999
          WRITE     HTMLFILE;"<span style='color:#324F85;display:inline-block;'><table  border='0' cellspacing='0' cellpadding='0'>"
.
          PACK      KEY70,URNUMBER,SP70
          CALL      RSPTGSR1
PATB1110  CALL      RKPTGSR1
          BRANCH    OVRCD,PATB1190
.
          MATCH     GSRURNO,PURNO
          GOTO      PATB1190 IF NOT EQUAL
.
          MATCH     GSRSNAM,PSNAME
          GOTO      PATB1120 IF NOT EQUAL
.
          MATCH     GSRGNAM,PGNAME
          GOTO      PATB1120 IF NOT EQUAL
.
          MATCH     GSRDOB,PBDATE
          GOTO      PATB1120 IF NOT EQUAL
.
          MATCH     GSRSEX,PSEX
          GOTO      PATB1110 IF EQUAL
.
PATB1120  UNPACK    GSRDOB,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      SP70,KEY13
          IF        FIRSTROW=1
            MOVE      ZERO,FIRSTROW
            WRITE     HTMLFILE;"<tr style='display:block;text-align:left;'><td>",GSRSNAM,"</td>";
          ELSE
            WRITE     HTMLFILE;"<tr class='dynamicRowAlias' style='display:block;text-align:left;'><td>",GSRSNAM,"</td>";
          ENDIF
          WRITE     HTMLFILE;"<td  style=';min-width:160px;text-transform:capitalize;'>,",GSRGNAM,"</td>":
                             "<td  style='min-width:100px;text-transform:capitalize;'>,",CPCDATE,"</td>":
                             "</td><td style='text-transform:capitalize;width:30px;'>,",GSRSEX,"</td></tr>"
          GOTO      PATB1110
.
PATB1190  WRITE     HTMLFILE;"</table></span>"
          GOTO      PATB9999
 
          
.--------------------------------------------------------------------------------
. Concession cards
.        $qry = "SELECT pat.tdesc, pms.pmcdcnum,pms.pmcdexdt
.                FROM pmsccdaf pms, patcodes pat
.                WHERE pms.PMCDURNO = '{$this->urno}'
.                AND pat.tcode = 'ct'
.                AND pat.acode = pms.pmcdctyp";
.
PATB1200  MOVE      ONE,FIRSTROW
          WRITE     HTMLFILE;"<span style='color:#324F85;display:inline-block;'><table border='0' cellspacing='0' cellpadding='0'>";
          PACK      KEY19,URNUMBER,Z70
          CALL      RSPMCCD1
PATB1210  CALL      RPPMCCD1
          BRANCH    OVRCD,PATB1290
          MATCH     URNUMBER,PMCDURNO
          GOTO      PATB1290 IF NOT EQUAL
.
          PACK      KEY5,CATct,PMCDCTYP,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
.
          MATCH     SP70,PMCDEXDT
          IF        @EQUAL | @EOS
            MOVE      SP70,DISPDATE
          ELSE
            UNPACK    PMCDEXDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          IF        FIRSTROW=1
            MOVE      ZERO,FIRSTROW
            WRITE     HTMLFILE;"<tr style='display:block;text-align:left;text-transform:capitalize;'>":
                               "<td>",TDESC,"</td>";
          ELSE
            WRITE     HTMLFILE;"<tr class='dynamicRowAlias'":
                               " style='display:block;text-align:left;text-transform:capitalize;'>":
                               "<td>",TDESC,"</td>";
          ENDIF
          WRITE     HTMLFILE;"<td  style='min-width:140px;text-transform:capitalize;'>",PMCDCNUM,"</td>":
                             "<td  style='min-width:140px;text-transform:capitalize;'>,":
                              DISPDATE,"</td></tr>";
          GOTO      PATB1210
PATB1290  WRITE     HTMLFILE;"</table></span>"
          GOTO      PATB9999
PATB9999  RETURN
.
.----------------------------------------------------------------------------------------------------
. cliweb02.php functions
.----------------------------------------------------------------------------------------------------
CLIA0000  BRANCH    REPORTNO,CLIA1100,CLIA1200,CLIA1300
          GOTO      CLIA9999
.----------------------------------------------------------------------------------------------------
.      case 1: //check observation number
.        $qry =  "SELECT count(*)
.                 FROM obsdetaf
.                 WHERE obdevist = '{$this->admis}'";
.        $this->getScalarValue($qry);

CLIA1100  MOVE      ZERO,ROWCOUNT
          PACK      KEY24,ADMISSNO,SP70
          CALL      RSOBDE1
CLIA1110  CALL      RKOBDE1
          BRANCH    OVRCD,CLIA1190
          MATCH     ADMISSNO,OBDEVIST
          GOTO      CLIA1190 IF NOT EQUAL
          ADD       ONE,ROWCOUNT
          GOTO      CLIA1110
CLIA1190  WRITE     HTMLFILE;ROWCOUNT
          GOTO      CLIA9999
.--------------------------------------------------------------------------------
.      case 2: //check Last Observation EWS Record
.        $qry =  "SELECT obdeuy1,obdeuy2,
.                        obdecom1,
.                        obdeun1,obdeun2,
.                        obdebpsy,obdebpdi,
.                        obdeun3,obdeun4,
.                        obdefin,
.                        obdeuc2
.                 FROM   obsdetaf
.                 WHERE  obdevist = '{$this->admis}'
.                 AND    obdetyp = 'P'
.                 AND    rownum = 1
.                 ORDER  BY obdedate desc, obdetime desc";
.        $this->setForm($qry);
.
CLIA1200  PACK      KEY24,ADMISSNO,Z70
          WRITE     HTMLFILE;"function setDefaults() { ";
          CALL      RSOBDE1
CLIA1210  CALL      RPOBDE1
          BRANCH    OVRCD,CLIA1290
          MATCH     ADMISSNO,OBDEVIST
          GOTO      CLIA1290 IF NOT EQUAL
          MATCH     "P",OBDETYP
          GOTO      CLIA1210 IF NOT EQUAL
          WRITE     HTMLFILE;"setCheckbox(AddForm.obdet028,'",OBDEUY1,"');":
                             "setCheckbox(AddForm.obdet029,'",OBDEUY2,"');":
                             "setText(AddForm.obdet038,'",OBDECOM1,"');":
                             "setSelect(AddForm.obdet034,'",OBDEUN1,"');":
                             "setSelect(AddForm.obdet035,'",OBDEUN2,"');":
                             "setSelect(AddForm.obdet006,'",OBDEBPSY,"');":
                             "setSelect(AddForm.obdet007,'",OBDEBPDI,"');":
                             "setSelect(AddForm.obdet036,'",OBDEUN3,"');":
                             "setSelect(AddForm.obdet037,'",OBDEUN4,"');":
                             "setSelect(AddForm.obdet015,'",OBDEFIN,"');":
                             "setSelect(AddForm.obdet033,'",OBDEUC2,"');"

CLIA1290  WRITE     HTMLFILE;"} ";
          GOTO      CLIA9999
.--------------------------------------------------------------------------------
.      case 3: //check Last Observation EWS Record
.        $qry =  "SELECT obdeuy1,obdeuy2,
.                        obdecom1,
.                        obdeun1,obdeun2,
.                        obdebpsy,obdebpdi,
.                        obdeun3,obdeun4,
.                        obdefin,
.                        obdeuc2
.                 FROM   obsdetaf
.                 WHERE  obdevist = '{$this->admis}'
.                 AND    obdetyp = 'P'
.                 AND    rownum = 1
.                 ORDER  BY obdedate desc, obdetime desc";
.        $this->setCalculation($qry);
.
CLIA1300  PACK      KEY24,ADMISSNO,Z70
          WRITE     HTMLFILE;"function setDefaults() { ";
          CALL      RSOBDE1
CLIA1310  CALL      RPOBDE1
          BRANCH    OVRCD,CLIA1390
          MATCH     ADMISSNO,OBDEVIST
          GOTO      CLIA1390 IF NOT EQUAL
          MATCH     "P",OBDETYP
          GOTO      CLIA1310 IF NOT EQUAL
          WRITE     HTMLFILE;"nfmaFlag='",OBDEUY1,"';":
                             "nfaFlag='",OBDEUY2,"';":
                             "rpLower=",OBDEUN1,";rpUpper=",OBDEUN2,";":
                             "bpLower=",OBDEBPSY,";bpUpper=",OBDEBPDI,";":
                             "puLower=",OBDEUN3,";puUpper=",OBDEUN4,";":
                             "opLower=",OBDEFIN,";":
                             "lcLimit=",OBDEUC2,";"
.
CLIA1390  WRITE     HTMLFILE;"} ";
          GOTO      CLIA9999
.
CLIA9999  RETURN
.----------------------------------------------------------------------------------------------------
. cliweb08.php functions
.----------------------------------------------------------------------------------------------------
CLIB0000  BRANCH    REPORTNO,CLIB1100,CLIB1200,CLIB1300,CLIB1400,CLIB1500:
                             CLIB1600,CLIB1700,CLIB1800,CLIB1900
          GOTO      CLIB9999
.----------------------------------------------------------------------------------------------------
.      case 1: //clinical documents
.
CLIB1100  CALL      DOCL0000
          GOTO      CLIB9999
.
.--------------------------------------------------------------------------------
.  Clinical Notes inc Emergency
.
CLIB1200  WRITE     HTMLFILE;"function AddNoteRows() {"
          MOVE      TEMPLATE,F3
          BRANCH    F3,CLIB1210,CLIB1250
.
. All Visits
.
CLIB1210  PACK      KEY24,URNUMBER,SP70
          CALL      RSPTVIS2
CLIB1220  CALL      RKPTVIS2
          BRANCH    OVRCD,CLIB1290
          MATCH     URNUMBER,PVIURNO
          GOTO      CLIB1290 IF NOT EQUAL
          MOVE      PVIBILL,ADMISSNO
          CALL      VISN0000
          CALL      EMRN0000
          GOTO      CLIB1220
.
. This Visit
.
CLIB1250  CALL      VISN0000
          CALL      EMRN0000
.
CLIB1290  WRITE     HTMLFILE;" }"
          GOTO      CLIB9999
.--------------------------------------------------------------------------------
.  Count of Clinical Documents
.
CLIB1300  MOVE      ZERO,ROWCOUNT
          PACK      KEY28,ADMISSNO,SP70
          CALL      RSOBPC2
          CALL      RKOBPC2
          IF        OVRCD=0
            MATCH     ADMISSNO,OBPCCVIS
            IF        @EQUAL
              MATCH     "0",OBPCMDEL
              IF        @EQUAL
                ADD       ONE,ROWCOUNT
              ENDIF
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;ROWCOUNT
          GOTO      CLIB9999
.--------------------------------------------------------------------------------
.  Discharge Notes
CLIB1400  WRITE     HTMLFILE;"function AddDischargeNoteRows() {"
          CALL      DNOT0000
          WRITE     HTMLFILE;"}"
          GOTO      CLIB9999
.--------------------------------------------------------------------------------
.  
CLIB1500  MOVE       ONE,FIRSTROW
          WRITE      HTMLFILE;"[ ";
          PACK       KEY3,SP70
          CALL       RSOBCC1
CLIB1510  CALL       RKOBCC1
          BRANCH     OVRCD,CLIB1590
          MOVE       OBCCSPA,OBCDTYP 
          MATCH      "Y",OBCDTYP 
          GOTO       CLIB1510 IF NOT EQUAL
.
          IF         FIRSTROW=1
            MOVE       ZERO,FIRSTROW
          ELSE
            WRITE      HTMLFILE;",";
          ENDIF
          WRITE      HTMLFILE;" {#"dischargeType#":#"",OBCCTYP,"#",":
                                "#"description#":#"",OBCCDES,"#",":
                                "#"templateInput#":#"",OBCCTMI,"#",":
                                "#"templateOutput#":#"",OBCCTMO,"#"}";
          GOTO      CLIB1510
CLIB1590  WRITE     HTMLFILE;"] ";
          GOTO      CLIB9999
.--------------------------------------------------------------------------------
.  List Documents
.
CLIB1600  CALL      LSTD0000
          GOTO      CLIB9999
.--------------------------------------------------------------------------------
.  
.     case 7: //get document details
.
CLIB1700  PACK      KEY20,URNUMBER,ADMISSNO,Z70
          CALL      RSOBPC1
CLIB1710  CALL      RPOBPC1
          BRANCH    OVRCD,CLIB9999
          MATCH     URNUMBER,OBPCURNO
          GOTO      CLIB9999 IF NOT EQUAL
          MATCH     ADMISSNO,OBPCCVIS
          GOTO      CLIB9999 IF NOT EQUAL
          MATCH     "3",OBPCMDEL
          GOTO      CLIB1710 IF NOT EQUAL
          MATCH     DOCTTYPE,OBPCCTYP
          GOTO      CLIB1710 IF NOT EQUAL
.
          PACK      KEY4,OBPCSLID
          CALL      RDOBPT1
.
          REPLACE   " 0",OBPCCVIS
          REPLACE   " 0",OBPCCPID
          WRITE     HTMLFILE;OBPCURNO,OBPCCVIS,OBPCCPID,"|",OBPTURLP,OBPCCVIS,"-",OBPCCPID,".html":
                             "|",OBPCCTOO,"|",OBPCCOM1
.
          GOTO      CLIB9999
.--------------------------------------------------------------------------------
.  
.     case 8: //get document details
.
CLIB1800  PACK      KEY20,DETAILKY,SP70
          CALL      RDOBPC1
          BRANCH    OVRCD,CLIB9999
          MATCH     "3",OBPCMDEL
          GOTO      CLIB9999 IF NOT EQUAL
.
          PACK      KEY4,OBPCSLID
          CALL      RDOBPT1
.
          REPLACE   " 0",OBPCCVIS
          REPLACE   " 0",OBPCCPID
          WRITE     HTMLFILE;OBPCURNO,OBPCCVIS,OBPCCPID,"|",OBPTURLP,OBPCCVIS,"-",OBPCCPID,".html":
                             "|",OBPCCTOO,"|",OBPCCOM1,"|",OBPCCTYP
.
          GOTO      CLIB9999
.--------------------------------------------------------------------------------
.  Past Medical History
.
CLIB1900  CALL      MEDH0000
          GOTO      CLIB9999
.
CLIB9999  RETURN
.--------------------------------------------------------------------------------
. Past Medical History
.--------------------------------------------------------------------------------
MEDH0000  WRITE     HTMLFILE;"function AddNoteRows() {";
          PACK      KEY14,URNUMBER,Z70
          CALL      RSOBMDT1
MEDH1000  CALL      RPOBMDT1
          BRANCH    OVRCD,MEDH9000
          MATCH     URNUMBER,OBMDURNO
          GOTO      MEDH9000 IF NOT EQUAL
          MATCH     "0",OBMDDELT
          GOTO      MEDH1000 IF NOT EQUAL
.
          PACK      KEY10,OBMDUSER,SP70
          CALL      RDWBSE1
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,OBMDDATE,OBMDTIME,QUOTE,COMMA:
                    QUOTE,WBSENAM,QUOTE,COMMA:
                    QUOTE,OBMDURNO,QUOTE,COMMA:
                    QUOTE,OBMDNOTE,QUOTE,COMMA:
                    QUOTE;
          PACK      KEY17,OBMDURNO,OBMDNOTE,SP70
          CALL      RSOBMTX1
MEDH2000  CALL      RKOBMTX1
          BRANCH    OVRCD,MEDH3000
          MATCH     OBMDURNO,OBMTURNO
          GOTO      MEDH3000 IF NOT EQUAL
          MATCH     OBMDNOTE,OBMTNOTE
          GOTO      MEDH3000 IF NOT EQUAL
          REP       "#"'",OBMTCMNT
          STRIP     OBMTCMNT
          MOVELPTR  OBMTCMNT,LENGTH
          SFORMAT   OUTVAR,LENGTH
          MOVE      OBMTCMNT,OUTVAR
          WRITE     HTMLFILE;OUTVAR,"<br>";
          GOTO      MEDH2000
.
MEDH3000  WRITE     HTMLFILE;QUOTE,");"
          GOTO      MEDH1000
.
MEDH9000  WRITE     HTMLFILE;"}";
          RETURN
.         
.
.--------------------------------------------------------------------------------
. Document List
.--------------------------------------------------------------------------------
DOCL0000  WRITE     HTMLFILE;"function AddDocRows() {";
          MOVE      "CLIWEB08",KEY8
          PACK      KEY18,WBSEUID,KEY8,SP70
          CALL      RDWBST1
          IF        OVRCD=1
            MOVE      ZERO,WBSTLEV
          ENDIF
          PACK      KEY28,URNUMBER,Z70
          CALL      RSOBPC5
DOCL1110  CALL      RPOBPC5
          BRANCH    OVRCD,DOCL1190
          MATCH     URNUMBER,OBPCURNO
          GOTO      DOCL1190 IF NOT EQUAL
          MOVE      SP70,KEY30
.
          PACK      KEY5,OBPCACAT,OBPCACOD,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,KEY30
          ENDIF
          PACK      KEY5,CATwi,OBPCCTYP,SP70
          CALL      RDCODE1
.         
          PACK      KEY5,CATwi,OBPCCTYP
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      ZERO,TCFORM6
          ENDIF
          IF        TCFORM6>0
            IF        WBSTLEV<TCFORM6
              GOTO      DOCL1110
            ENDIF
          ENDIF
.
          PACK      KEY4,OBPCSLID
          CALL      RDOBPT1
.
          STRIP     OBPTURLP
          MATCH     "00000000",OBPCCVIS
          IF        @EQUAL
            MOVE      "UR",KEY2
            PACK      CMDLINE,OBPTURLP,KEY2,OBPCURNO,DASH,OBPCCPID,OBPCFEXT
          ELSE
            PACK      CMDLINE,OBPTURLP,OBPCCVIS,DASH,OBPCCPID,OBPCFEXT
          ENDIF
          STRIP     CMDLINE
          REP       " 0",CMDLINE
.
          MOVELPTR  CMDLINE,LENGTH
          SFORMAT   OUTVAR,LENGTH
          MOVE      CMDLINE,OUTVAR
.
          PACK      KEY20,OBPCURNO,OBPCCVIS,OBPCCPID,SP70
          REP       " +",KEY20,
          REP       "> ",OBPCCFRM
          REP       "< ",OBPCCFRM
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,OUTVAR,QUOTE,COMMA;
.
          STRIP     OBPTSDIR
          MATCH     "00000000",OBPCCVIS
          IF        @EQUAL
            MOVE      "UR",KEY2
            PACK      CMDLINE,OBPTSDIR,KEY2,OBPCURNO,DASH,OBPCCPID,OBPCFEXT
          ELSE
            PACK      CMDLINE,OBPTSDIR,OBPCCVIS,DASH,OBPCCPID,OBPCFEXT
          ENDIF
          STRIP     CMDLINE
          REP       " 0",CMDLINE
.
          MOVELPTR  CMDLINE,LENGTH
          SFORMAT   OUTVAR,LENGTH
          MOVE      CMDLINE,OUTVAR
.
          WRITE     HTMLFILE;QUOTE,OUTVAR,QUOTE,COMMA:
                    QUOTE,KEY20,QUOTE,COMMA:
                    QUOTE,OBPCINDT,OBPCINTM,QUOTE,COMMA:
                    QUOTE,OBPCCTYP,QUOTE,COMMA:
                    QUOTE,OBPCINUS,QUOTE,COMMA:
                    QUOTE,OBPCCOM1,QUOTE,COMMA:
                    QUOTE,OBPCCOM2,QUOTE,COMMA:
                    QUOTE,OBPCCFRM,QUOTE,COMMA:
                    QUOTE,OBPCCTOO,QUOTE,COMMA:
                    QUOTE,KEY30,QUOTE,COMMA:
                    QUOTE,TDESC,QUOTE,COMMA:
                    QUOTE,WBSENAM,QUOTE,COMMA:
                    QUOTE,WBSTLEV,QUOTE,COMMA:
                    QUOTE,TCFORM6,QUOTE,COMMA:
                    QUOTE,TCINDC1,QUOTE,COMMA:
                    QUOTE,OBPCMDEL,QUOTE,");"
          GOTO      DOCL1110
DOCL1190  WRITE     HTMLFILE;" };";
          RETURN
.--------------------------------------------------------------------------------
. Visit Notes
.--------------------------------------------------------------------------------
VISN0000  PACK      KEY12,ADMISSNO,SP70
          CALL      RSOBCA1
VISN1000  CALL      RKOBCA1
          BRANCH    OVRCD,VISN9999
          MATCH     ADMISSNO,OBCAVIS
          GOTO      VISN9999 IF NOT EQUAL
          MATCH     "0",OBCAMDL
          GOTO      VISN1000 IF NOT EQUAL
.
          PACK      KEY3,OBCATYP,SP70
          CALL      RDOBCC1
.
          MOVE      SP70,TDESC
          MATCH     SP70,OBCAOCG
          IF        !@EQUAL
            MOVE      "w0",KEY2
            PACK      KEY5,KEY2,OBCAOCG,SP70
            CALL      RDCODE1
          ENDIF
          MATCH     "1",OBCAP1D
          IF        @EQUAL
            MOVE      SP70,OBCAPT1
          ENDIF
.
          MATCH     "1",OBCAP2D
          IF        @EQUAL
            MOVE      SP70,OBCAPT2
          ENDIF
.
          MATCH     "1",OBCAP3D
          IF        @EQUAL
            MOVE      SP70,OBCAPT3
          ENDIF
.
          MATCH     "1",OBCAP4D
          IF        @EQUAL
            MOVE      SP70,OBCAPT4
          ENDIF
.
          PACK      KEY10,OBCAUID
          CALL      RDWBSE1
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,OBCADTE,OBCATME,QUOTE,COMMA:
                    QUOTE,WBSENAM,QUOTE,COMMA:
                    QUOTE,OBCAVIS,QUOTE,COMMA:
                    QUOTE,OBCACID,QUOTE,COMMA:
                    QUOTE,TDESC,QUOTE,COMMA:
                    QUOTE,OBCAPT1,QUOTE,COMMA:
                    QUOTE,OBCAPT2,QUOTE,COMMA:
                    QUOTE,OBCAPT3,QUOTE,COMMA:
                    QUOTE,OBCAPT4,QUOTE,COMMA:
                    QUOTE,OBCCDES,QUOTE,COMMA:
                    QUOTE;
          PACK      KEY15,OBCAVIS,OBCACID,SP70
          CALL      RSOBCB1
VISN2000  CALL      RKOBCB1
          BRANCH    OVRCD,VISN3000
          MATCH     OBCAVIS,OBCBVIS
          GOTO      VISN3000 IF NOT EQUAL
          MATCH     OBCACID,OBCBCID
          GOTO      VISN3000 IF NOT EQUAL
          REP       "#"'",OBCBLDT
          STRIP     OBCBLDT
          MOVELPTR  OBCBLDT,LENGTH
          SFORMAT   OUTVAR,LENGTH
          MOVE      OBCBLDT,OUTVAR
          WRITE     HTMLFILE;OUTVAR,"<br>";
          GOTO      VISN2000
VISN3000
          WRITE     HTMLFILE;QUOTE,COMMA:
                             QUOTE,OBCCTMO,QUOTE,COMMA:
                             QUOTE,OBCCTMO,QUOTE,");"
          GOTO      VISN1000
.
VISN9999  RETURN
.
EMRN0000  MOVE      "001",KEY3
          PACK      KEY17,ADMISSNO,KEY3,SP70
          CALL      RSVSMDT1
EMRN1000  CALL      RKVSMDT1
          BRANCH    OVRCD,EMRN9999
          MATCH     ADMISSNO,VSMDVISN
          GOTO      EMRN9999 IF NOT EQUAL
          MATCH     "0",VSMDDELT
          GOTO      EMRN1000 IF NOT EQUAL
          MATCH     "001",VSMDTYPE
          GOTO      EMRN1100 IF EQUAL
          MATCH     "008",VSMDTYPE
          GOTO      EMRN1800 IF EQUAL
          GOTO      EMRN1000 
.
.                                         001 - Emergency Clinical Notes       
.                                         002 - Emergency Management Notes     
.                                         007 - Emergency Doctors Notes        
.                                         008 - Referral Notes                 
.                                         009 - Bed Request Notes              
.                                         010 - Eclipse Notes                  
EMRN1100  MOVE      "Emergency",NOTEDESC
          MOVE      "EMR",NOTECODE
          GOTO      EMRN1900
.
EMRN1800  MOVE      "Referral",NOTEDESC
          MOVE      "REF",NOTECODE
          GOTO      EMRN1900
.
EMRN1900  MOVE      SP70,TDESC
          MATCH     SP70,VSMDOCCG
          IF        !@EQUAL
            MOVE      "w0",KEY2
            PACK      KEY5,KEY2,VSMDOCCG,SP70
            CALL      RDCODE1
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,VSMDDATE,VSMDTIME,QUOTE,COMMA:
                    QUOTE,WBSENAM,QUOTE,COMMA:
                    QUOTE,VSMDVISN,QUOTE,COMMA:
                    QUOTE,VSMDNOTE,QUOTE,COMMA:
                    QUOTE,TDESC,QUOTE,COMMA:
                    QUOTE,SP1,QUOTE,COMMA:
                    QUOTE,SP1,QUOTE,COMMA:
                    QUOTE,SP1,QUOTE,COMMA:
                    QUOTE,SP1,QUOTE,COMMA:
                    QUOTE,NOTEDESC,QUOTE,COMMA:
                    QUOTE;
          PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
EMRN2000  CALL      RKVSMTX1
          BRANCH    OVRCD,EMRN3000
          MATCH     VSMDVISN,VSMTVISN
          GOTO      EMRN3000 IF NOT EQUAL
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      EMRN3000 IF NOT EQUAL
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      EMRN3000 IF NOT EQUAL
          REP       "#"'",VSMTCMNT
          STRIP     VSMTCMNT
          MOVELPTR  VSMTCMNT,LENGTH
          SFORMAT   OUTVAR,LENGTH
          MOVE      VSMTCMNT,OUTVAR
          WRITE     HTMLFILE;OUTVAR,"<br>";
          GOTO      EMRN2000
EMRN3000  WRITE     HTMLFILE;QUOTE,COMMA:
                             QUOTE,NOTECODE,QUOTE,COMMA:
                             QUOTE,NOTECODE,QUOTE,");"
          GOTO      EMRN1000
.
EMRN9999  RETURN
.--------------------------------------------------------------------------------
. Document List
.--------------------------------------------------------------------------------
LSTD0000  WRITE     HTMLFILE;"function AddDocRows() {";
          MOVE      "CLIWEB08",KEY8
          PACK      KEY18,WBSEUID,KEY8,SP70
          CALL      RDWBST1
          IF        OVRCD=1
            MOVE      ZERO,WBSTLEV
          ENDIF
          PACK      KEY28,URNUMBER,Z70
          CALL      RSOBPC5
LSTD1110  CALL      RPOBPC5
          BRANCH    OVRCD,LSTD1190
          MATCH     URNUMBER,OBPCURNO
          GOTO      LSTD1190 IF NOT EQUAL
          MOVE      SP70,KEY30
.
          PACK      KEY5,OBPCACAT,OBPCACOD,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,KEY30
          ENDIF
          PACK      KEY5,CATwi,OBPCCTYP,SP70
          CALL      RDCODE1
.         
          PACK      KEY5,CATwi,OBPCCTYP
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      ZERO,TCFORM6
          ENDIF
          IF        TCFORM6>0
            IF        WBSTLEV<TCFORM6
              GOTO      LSTD1110
            ENDIF
          ENDIF
.
          PACK      KEY4,OBPCSLID
          CALL      RDOBPT1
.
          STRIP     OBPTSDIR
          MATCH     "00000000",OBPCCVIS
          IF        @EQUAL
            MOVE      "UR",KEY2
            PACK      CMDLINE,OBPTSDIR,KEY2,OBPCURNO,DASH,OBPCCPID,OBPCFEXT
          ELSE
            PACK      CMDLINE,OBPTSDIR,OBPCCVIS,DASH,OBPCCPID,OBPCFEXT
          ENDIF
          STRIP     CMDLINE
          REP       " 0",CMDLINE
.
          MOVELPTR  CMDLINE,LENGTH
          SFORMAT   OUTVAR,LENGTH
          MOVE      CMDLINE,OUTVAR
.
          PACK      KEY20,OBPCURNO,OBPCCVIS,OBPCCPID,SP70
          REP       " +",KEY20,
          REP       "> ",OBPCCFRM
          REP       "< ",OBPCCFRM
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,OUTVAR,QUOTE,COMMA:
                    QUOTE,KEY20,QUOTE,COMMA:
                    QUOTE,OBPCINDT,OBPCINTM,QUOTE,COMMA:
                    QUOTE,OBPCCTYP,QUOTE,COMMA:
                    QUOTE,OBPCINUS,QUOTE,COMMA:
                    QUOTE,OBPCCOM1,QUOTE,COMMA:
                    QUOTE,OBPCCOM2,QUOTE,COMMA:
                    QUOTE,OBPCCFRM,QUOTE,COMMA:
                    QUOTE,OBPCCTOO,QUOTE,COMMA:
                    QUOTE,KEY30,QUOTE,COMMA:
                    QUOTE,TDESC,QUOTE,COMMA:
                    QUOTE,WBSENAM,QUOTE,COMMA:
                    QUOTE,WBSTLEV,QUOTE,COMMA:
                    QUOTE,TCFORM6,QUOTE,COMMA:
                    QUOTE,TCINDC1,QUOTE,COMMA:
                    QUOTE,OBPCMDEL,QUOTE,");"
          GOTO      LSTD1110
LSTD1190  WRITE     HTMLFILE;" };";
          RETURN
.--------------------------------------------------------------------------------
. discharge notes
.--------------------------------------------------------------------------------
DNOT0000  PACK      KEY12,ADMISSNO,SP70
          CALL      RSOBCA1
DNOT1000  CALL      RKOBCA1
          BRANCH    OVRCD,DNOT9999
          MATCH     ADMISSNO,OBCAVIS
          GOTO      DNOT9999 IF NOT EQUAL
          MATCH     "0",OBCAMDL
          GOTO      DNOT1000 IF NOT EQUAL
.
          PACK      KEY3,OBCATYP,SP70
          CALL      RDOBCC1
          BRANCH    OVRCD,DNOT1000
          MOVE      OBCCSPA,OBCDTYP 
          MATCH     "Y",OBCDTYP
          GOTO      DNOT1000 IF NOT EQUAL
.
          PACK      KEY10,OBCAUID
          CALL      RDWBSE1
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,OBCADTE,OBCATME,QUOTE,COMMA:
                    QUOTE,WBSENAM,QUOTE,COMMA:
                    QUOTE,OBCACID,QUOTE,COMMA:
                    QUOTE,OBCCTYP,QUOTE,COMMA:
                    QUOTE,OBCCDES,QUOTE,COMMA:
                    QUOTE,OBCCTMO,QUOTE,COMMA:
                    QUOTE,OBCCTMI,QUOTE,COMMA:
                    QUOTE;
          PACK      KEY15,OBCAVIS,OBCACID,SP70
          CALL      RSOBCB1
DNOT2000  CALL      RKOBCB1
          BRANCH    OVRCD,DNOT3000
          MATCH     OBCAVIS,OBCBVIS
          GOTO      DNOT3000 IF NOT EQUAL
          MATCH     OBCACID,OBCBCID
          GOTO      DNOT3000 IF NOT EQUAL
          REP       "#"'",OBCBLDT
          STRIP     OBCBLDT
          MOVELPTR  OBCBLDT,LENGTH
          SFORMAT   OUTVAR,LENGTH
          MOVE      OBCBLDT,OUTVAR
          WRITE     HTMLFILE;OUTVAR,"<br>";
          GOTO      DNOT2000
DNOT3000
          WRITE     HTMLFILE;QUOTE,");"
          GOTO      DNOT1000
DNOT9999  RETURN

.--------------------------------------------------------------------------------
. bedcare details
. $qry = "SELECT
.          '<b>'||initcap(p.psname) || '</b>, ' || initcap(p.ptitl) || ' ' || initcap(p.pgname)  ||
.         ' (' || trim(p.purno) ||')' patfname
.         ,ward.wbdesc||' / '||w.wbed wardbed
.         ,d.dsname||', '||d.dtitl||' '||d.dgname doctor
.         ,w.ptwdbdst
.         ,p.purno
.         ,w.dwadmno
.         ,(select tdesc from patcodes where tcode='BV' and acode=w.ptwdhkst and acode>' ')
.         ,(select tcindc1 from patcodes where tcode='BV' and acode=w.ptwdhkst and acode>' ')
.         ,(select tdesc from patcodes where tcode='DC' and acode=a.adiet and acode>' ')
.         FROM      patwr1af w
.         join      patwr1af ward on ward.wward=w.wward and ward.wbed=' '
.         left join patvisaf v on v.dpvibill=w.dwadmno
.         left join patmi1af a on a.daadmno=v.dpvibill
.         left join patma1af p on p.purno=v.pviurno
.         left join patdo1af d on d.dcode=a.adocta
.         WHERE  w.wward='$wardcode'
.         AND    w.wbed='$bedcode'
.        ";
.
.--------------------------------------------------------------------------------
BEDCRD00  MOVE      SP70,DOCFNAME
          MOVE      SP70,PATFNAME
          MOVE      SP70,DCDESC
          MOVE      SP70,BVDESC
          MOVE      SP70,BVIND1
.
          PACK      KEY6,WARDCODE,WBEDCODE,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,BEDCRD90
.
          MOVE      DWADMNO,KEY8
          CALL      RDMISS1
          IF        OVRCD=0
            MOVE      AURNO,KEY8
            CALL      RDMAST1
            IF        OVRCD=0
              CALL      PATNAM00
            ENDIF
            MOVE      ADOCTA,KEY6
            CALL      RDDOCT1
            IF        OVRCD=0
              CALL      DOCNAM00
            ENDIF
.
            MATCH     SP70,ADIET
            IF        !@EQUAL
              PACK      KEY5,CATDC,ADIET
              CALL      RDCODE1
              IF        OVRCD=0
                MOVE      TDESC,DCDESC
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,PTWDHKST
          IF        !@EQUAL
            PACK      KEY5,CATBV,PTWDHKST
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,BVDESC
              MOVE      TCINDC1,BVIND1
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;PATFNAME,"|":
                             WWARD,"/",WBED,"|":
                             DOCFNAME,"|":
                             PTWDBDST,"|":
                             PURNO,"|":
                             DWADMNO,"|":
                             BVDESC,"|":
                             BVIND1,"|":
                             DCDESC
          GOTO      BEDCRD99
BEDCRD90
          WRITE     HTMLFILE;"Invalid Ward Bed Code ",KEY6
.
BEDCRD99  RETURN
.------------------------------------------------------------
. Format Patient Name SURNAME, Title Given Names
.------------------------------------------------------------
PATNAM00  CLEAR     DISPNAME
          REP       "#" ",PSNAME
          REP       "#" ",PTMASNAM
          REP       "#" ",PGNAME
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          REP       UPPLOW,PTMASNAM
          MATCH     SP70,PTMASNAM
          IF        !@EQUAL
            STRIP     PTMASNAM
            APPEND    PTMASNAM,DISPNAME
          ELSE
            STRIP     PSNAME
            APPEND    PSNAME,DISPNAME
          ENDIF
          PACK      NAME35,COMMA,PTITL,SP70
          CALL      NAMSTR00
          STRIP     PGNAME
          PACK      NAME35,PGNAME,SP70  
          STRIP     NAME35
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          RETURN
.
.
          INC       PATWR1IO/INC
          INC       PATMI1IO/INC
          INC       PATDO1IO/INC
          INC       PATDOCTM    
.
          INC       PATMA1IO/INC
          INC       PATGSRIO/INC 
          INC       PMSCCDIO/INC 
          INC       PMSPRBIO/INC
          INC       BOKRC1IO/INC 
          INC       PATVISIO/INC 
          INC       PMSVX1IO/INC 
          INC       OBSDETIO/INC 
          INC       OBSPCOIO/INC 
          INC       OBSPCTIO/INC 
          INC       OBSMDTIO/INC 
          INC       OBSMTXIO/INC 
          INC       EMRVCDIO/INC 
          INC       VISMDTIO/INC 
          INC       VISMTXIO/INC 
.
          INC       OBSCNAIO/INC 
          INC       OBSCNBIO/INC 
          INC       OBSCNCIO/INC 
          INC       PATHSPIO/INC
          INC       OBSTYPIO/INC
          INC       MLTSECIO/INC
.
          INC       DAYOFWEK
          INC       CALCAGE
          INC       NAMSTRCD
          INC       WEBDATCD
          INC       IBAWEBCD
.
          INC       TFILENAM

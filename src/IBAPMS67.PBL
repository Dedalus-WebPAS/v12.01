.--------------------------------------------------------------------------
. Program       : IBAPMS67
. Function      : Set Expired Referrals to Closed Report 
. Modifications :
.--------------------------------------------------------------------------
. V11.02.05  15/07/2022 Ebon Clements   TSK 0903574
.            Only check departments hospital if multi hospital - PROCA000
. V11.02.04  13/04/2022 Thanh T         TSK 0903754
.            Changed PRTRF300 to use OBATIME instead of OBASTRT time
. V11.02.03  31/03/2022 Thanh T         TSK 0903754
.            Changed DEPT0000 to update the referral status to closed (2).
.            Fixed issue with not unlinking for second booking visit 
. V11.02.02  29/03/2022 Thanh T         TSK 0903754
.            Fixed issue with not processing referrals for single hospital 
. V11.02.01  01/03/2022 Thanh T         TSK 0903754
.            Changed to update the ALRERCLO closed reason code from Cat LL
. V11.02.00  17/02/2022 Thanh T         TSK 0903754
.            Initial Version
.--------------------------------------------------------------------------
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       ALLCONFD/INC
.
          INC       PATCODFD/INC
          INC       PATHSPFD/INC
          INC       ALLREFFD/INC
          INC       ALLDEPFD/INC
          INC       ALLLNKFD/INC
          INC       ALLSTSFD/INC
          INC       OUTBOAFD/INC
          INC       OUTCLIFD/INC
          INC       WEBSECFD/INC
.
. WORK AREA
.
DIM3      DIM       3
LINECNT   FORM      3
FILTHOSP  DIM       3
DEPTCODE  DIM       3
USERID    DIM       10 
NOFDAYS   DIM       3
NOFUAREF  FORM      5
NOFDALNK  FORM      5
FORM3     FORM      3
.
WORKDATE  DATE      8
ENDDATE   DATE      8
ENDDATEX  DIM       8
.
CALDAYS   FORM      4
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
.
CFILEPRE  DIM       3
CODE      DIM       2
CURRDATE  DIM       8
CURRTIME  DIM       8
CLDRSNCD  DIM       3
EXPDATEX  DIM       10
DEPTDESC  DIM       20
RDEPNAME  DIM       20
BOOKNO    DIM       8
BOOKDTTM  DIM       18
CLINDESC  DIM       37
COUNTER   FORM      2
NOOFDEPT  FORM      2
ALLNKFND  DIM       1
SAVKEY16  DIM       16
DEPTCDAR  DIM       3[99]
.
ATCHAR    INIT      "@"
.
.---------------------------------------------------------------------
VERSION   INIT      "V12.01.00"
PRGID     INIT      "IBAPMS67"
PRGNAM    INIT      "Set Expired Referrals to Closed Report"
.---------------------------------------------------------------------
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN2000       * proceed with clean up
.
MAIN2000  CALL      HOSP0000         
          BRANCH    EXIT,MAIN2000          * nothing entered
.
MAIN4000  CALL      KDEPT000               * keyin Department code
          CALL      NDAY0000               * keyin No. of days
          CALL      USID0000               * keyin the User Id
.
MAIN7000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN8000:          * yes
                          MAIN4000:          * no
                          MAIN1000           * cancel
.
MAIN8000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.-------------------------------------------------------------------------
. The initialisation routine is used to display headings and open files.
.-------------------------------------------------------------------------
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *ES;
          CALL      DISPHEAD              * call display header routine
          CALL      IBACLOCK
.
          DISPLAY   *P64:24,"Opening pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"
.
          DISPLAY   *P64:24,"allrefaf";
          OPEN      ALLREFA4,"allrefaf"
.
          DISPLAY   *P64:24,"allaudre";
          OPEN      ALLAUDRE,"allaudre"
.
          DISPLAY   *P64:24,"alldepaf";
          OPEN      ALLDEPA1,"alldepaf"
.
          DISPLAY   *P64:24,"alldepaf";
          OPEN      ALLDEPA2,"alldepaf"
.
          DISPLAY   *P64:24,"alllnkaf";
          OPEN      ALLLNKA1,"alllnkaf"
.
          DISPLAY   *P64:24,"alllnkaf";
          OPEN      ALLLNKA2,"alllnkaf"
.
          DISPLAY   *P64:24,"allstsaf";
          OPEN      ALLSTSA1,"allstsaf"
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf""
.   
          MOVE      "out",CFILEPRE          * Save Current File Prefix
          CALL      OPNOUT00
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT1000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          CLOCK     TIME,CURRTIME
.
INIT9999  RETURN
+
.------------------------------------------------------------
. Open Outpatient Files
.------------------------------------------------------------
OPNOUT00  CLOSE     OUTBOKA1
          PACK      CFNAME,CFILEPRE,FILBOKA1
          OPEN      OUTBOKA1,CFNAME
.
          CLOSE     OUTCLIA1
          PACK      CFNAME,CFILEPRE,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
OPNOUT99  RETURN
.
.-------------------------------------------------------------------------
.                        get user options or exit                       
.                                                                      
.    Returns:  EXIT    = FALSE (0)  Run clean up                      
.                        TRUE  (1)  Exit option selected             
.-------------------------------------------------------------------------
OPTN0000  MOVE       ZERO,CPAGENO
.
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Set Expired Referrals to Closed"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.-------------------------------------------------------------------------
. Get hospital id 
.-------------------------------------------------------------------------
HOSP0000  IF        IBCNMHOS = 1
            DISPLAY   *P1:10,*EL,"Hospital Id : ";
            MOVE      "15",ECOL
            MOVE      "10",EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
HOSP1100    CALL      PATHSPKY
            IF        EXIT = ONE
              MOVE      SP70,FILTHOSP
              MOVE      "All Hospitals",PTHSNAME
              MOVE      SP70,PTHSHOSP
              DISPLAY   *P19:10,*EL,PTHSNAME,*W2;
            ELSE
              IF        EXIT = 2
                MOVE      SP70,FILTHOSP
                GOTO      HOSP9400
              ELSE
                MOVE      KEY3,FILTHOSP
                DISPLAY   *P19:10,*EL,PTHSNAME,*W2;
              ENDIF
            ENDIF
          ENDIF
.
HOSP1199  MOVE      ZERO,EXIT
          GOTO      HOSP9999
.
HOSP9400  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Hospital ID ** ";
          CALL      HITENTER
          GOTO      HOSP0000
.
HOSP9999  RETURN
.
.-------------------------------------------------------------------------
. Select Department Code
.-------------------------------------------------------------------------
KDEPT000  MOVE      ZERO,EXIT
          MOVE      ZERO,COUNTER
          MOVE      ZERO,NOOFDEPT
          MOVE      11,CVERT
          KEYIN     *P1:CVERT,"Number of Departments: ",NOOFDEPT;
          IF        NOOFDEPT < 1
            GOTO      KDEPT999
          ENDIF
          MOVE      ONE,COUNTER
.
KDEPT200  ADD       ONE,CVERT
          KEYIN     *P1:CVERT,*EL,"Department : ",DEPTCODE
.
KDEPT300  PACK      DEPTCDAR[COUNTER],DEPTCODE,SP70
          ADD       ONE,COUNTER
          COMPARE   NOOFDEPT,COUNTER
          GOTO      KDEPT200 IF LESS
          GOTO      KDEPT200 IF EQUAL
.
KDEPT800  SUB       ONE,COUNTER
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,COUNTER," Department(s) selected."
          MOVE      ZERO,EXIT
.
KDEPT999  RETURN
.
.-------------------------------------------------------------------------
. Select No. Of Days
.-------------------------------------------------------------------------
NDAY0000  KEYIN     *P1:15,*EL,"No. Of Days : ",NOFDAYS;
          PACK      NOFDAYS,NOFDAYS,SP70
          MATCH     SP70,NOFDAYS
          GOTO      NDAY0000 IF EQUAL
.
NDAY9999  RETURN
.
.-------------------------------------------------------------------------
. Keyin user id
.-------------------------------------------------------------------------
USID0000  KEYIN     *P1:16,*EL,"User Id     : ",USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      USID0000 IF EQUAL
.
USID9999  RETURN
.
.-------------------------------------------------------------------------
. Process the report
.-------------------------------------------------------------------------
PROC0000  DISPLAY   *P10:24,"Processing : ";
          MOVE      0,NOFUAREF
          MOVE      0,NOFDALNK
          MOVE      SP70,CLDRSNCD
. 
          MOVE      "LL",KEY2       
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
PROC0200  CALL      RDKCODE1
          BRANCH    OVRCD,PROC1000
.
          MATCH     KEY2,TCODE
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     ANSI,PTCOACTV
          GOTO      PROC0200 IF EQUAL
.
          MATCH     ANSE,TCINDC3
          GOTO      PROC0200 IF NOT EQUAL
.
          MOVE      ACODE,CLDRSNCD
.
PROC1000  IF        NOOFDEPT = 1
            MOVE      DEPTCDAR[1],DEPTCODE
            MATCH     "***",DEPTCODE          * All department is selected    
            IF        @EQUAL
              CALL      PROCA000
              GOTO      PROC8000
            ENDIF
          ENDIF
.          
          MOVE      ZERO,F2
PROC5000  IF        F2 = 99
            GOTO      PROC8000
          ENDIF
.
. For selected department
.  
          ADD       ONE,F2
          IF        F2 <= NOOFDEPT 
            PACK      DEPTCODE,DEPTCDAR[F2],SP70
            PACK      KEY3,DEPTCODE,SP70
            CALL      RDALDEP1      
            CALL      DEPT0000
            GOTO      PROC5000 
          ENDIF
.
. End of Report
.
PROC8000  PRINT     *1," "
          PRINT     *1,"No. of allrefaf updated: ",NOFUAREF
          PRINT     *1,"No. of alllnkaf deleted: ",NOFDALNK
          PRINT     *1," "
          PRINT     "*** End of report ***";   
.
PROC9999  RETURN
.
.-------------------------------------------------------------------------
. Process the report for all departments
.-------------------------------------------------------------------------
PROCA000  IF        IBCNMHOS = 1
            PACK      DIM3,FILTHOSP,SP70
          ELSE 
            PACK      DIM3,SP70 
          ENDIF
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          IF        OVRCD = 1
            MOVE      SP70,WBSEDALL
          ENDIF
.
          PACK      KEY6,DIM3,SP70
          CALL      RSALDEP2
PROCA200  CALL      RKALDEP2
          BRANCH    OVRCD,PROCA999
.
          IF        IBCNMHOS = 1
            MATCH     DIM3,ALDEHOSP         * Only check departments hospital
            GOTO      PROCA999 IF NOT EQUAL * if multi hospital is on
          ENDIF
.
          MATCH     "1",ALDEACTV
          GOTO      PROCA200 IF EQUAL       * Skip Inactive
.
          MATCH     "0",ALDESECU
          GOTO      PROCA400 IF EQUAL
.
          MATCH     "2",ALDESECU
          IF        @EQUAL
            MATCH     WBSEDALL,ALDEDEPT
            GOTO      PROCA400 IF EQUAL
            GOTO      PROCA200
          ENDIF
.
          GOTO      PROCA200
.
PROCA400  MOVE      ALDEDEPT,DEPTCODE
          CALL      DEPT0000
.
          GOTO      PROCA200 
.
PROCA999  RETURN
.
.-------------------------------------------------------------------------
. Process referrals for one department code
.-------------------------------------------------------------------------
DEPT0000  MATCH     SP70,DEPTCODE
          GOTO      DEPT9999 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,DEPTCODE,SP70
          CALL      RDCODE1
          IF        OVRCD = 0
            MOVE      TDESC,DEPTDESC
          ENDIF
.
          CALL      PRTHDR00                 * Print header
.         
          PACK      KEY22,DEPTCODE,ONE,SP70
          CALL      RSALREF4                * positional read
DEPT0200  CALL      RKALREF4                * read a record forward
          BRANCH    OVRCD,DEPT9999
.
          MATCH     DEPTCODE,ALREDEPT
          GOTO      DEPT9999 IF NOT EQUAL
.
          MATCH     "1",ALRESTAT
          GOTO      DEPT9999 IF NOT EQUAL
.
          IF        IBCNMHOS = 1 
            MATCH     FILTHOSP,ALREHOSN
            GOTO      DEPT0200 IF NOT EQUAL
          ENDIF 
.
          MATCH     SP70,ALREREXP
          GOTO      DEPT0200 IF EQUAL
.
          MATCH     SP70,ALREDCLO
          GOTO      DEPT0200 IF NOT EQUAL
.
          MOVE      ZERO,FORM3
          STRIP     NOFDAYS
          MOVE      NOFDAYS,FORM3
          MOVE      ALREREXP,ENDDATE
          ADD       FORM3,ENDDATE
          MOVE      ENDDATE,ENDDATEX                                 
.
          MATCH     ENDDATEX,CURRDATE
          GOTO      DEPT0200 IF LESS
.
          CALL      PRTRF000 
          CALL      CLSREF00 
.
          GOTO      DEPT0200
.
DEPT9999  RETURN
.
.-------------------------------------------------------------------------
. Print the report
.-------------------------------------------------------------------------
PRTRF000  MOVE      "0",ALLNKFND
.
          MOVE      SP70,RDEPNAME
.
          MATCH     "2",ALDESECU
          IF        @EQUAL
            MOVE      DEPTDESC,RDEPNAME
          ELSE 
            MATCH     SP70,ALRERDEP
            IF        !@EQUAL
              PACK      KEY5,ANSC,ANSG,ALRERDEP,SP70
              CALL      RDCODE1
              IF        OVRCD = 0
                MOVE      TDESC,RDEPNAME
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      SP70,EXPDATEX
          MATCH     SP70,ALREREXP
          IF        !@EQUAL
            UNPACK    ALREREXP,CCENT,CYEAR,CMON,CDAY
            PACK      EXPDATEX,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          ENDIF 
.
          MOVE      SP70,BOOKNO
          MOVE      SP70,BOOKDTTM
          MOVE      SP70,CLINDESC
.
          PACK      KEY16,ALREVISN,SP70
          CALL      RSALLNK1
PRTRF200  CALL      RKALLNK1
          BRANCH    OVRCD,PRTRF800
.
          MATCH     ALREVISN,ALLNVISN
          GOTO      PRTRF800 IF NOT EQUAL
.
          PACK      SAVKEY16,ALREVISN,ALLNLNKV,SP70
.
PRTRF300  PACK      KEY28,ALLNSITE,ALLNCLIN,ALLNDATE,ALLNSTRT,ALLNSLOT,SP70
          CALL      RDBOKA1
          BRANCH    OVRCD,PRTRF200
.
          MATCH     "1",DOBASTAT
          GOTO      PRTRF200 IF NOT EQUAL  
.
          MOVE      ALLNLNKV,BOOKNO
          MATCH     SP70,ALLNDATE
          IF        !@EQUAL
            UNPACK    ALLNDATE,CCENT,CYEAR,CMON,CDAY
            PACK      BOOKDTTM,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                      SP1,ATCHAR,SP1,OBATIME
          ENDIF
.
. Remove linking for booking
.
          PACK      KEY16,ALLNLNKV,ALLNVISN,SP70
          CALL      RDALLNK2
          BRANCH    OVRCD,PRTRF400
.
          CALL      DEALLNK2      
          ADD       ONE,NOFDALNK
.
PRTRF400  PACK      KEY20,ALLNSITE,ALLNCLIN,Z70
          CALL      RDSCLIA1 
          CALL      RDPCLIA1 
          BRANCH    OVRCD,PRTRF800
.
          MATCH     ALLNSITE,OCASITE
          GOTO      PRTRF800 IF NOT EQUAL
.
          MATCH     ALLNCLIN,OCACLIN
          GOTO      PRTRF800 IF NOT EQUAL
.
          PACK      CLINDESC,OCACLIN,SP1,OCADESC
.
          CALL      PRTLIN00
.        
          MOVE      SAVKEY16,KEY16
          MOVE      SP70,BOOKNO
          MOVE      SP70,BOOKDTTM
          MOVE      SP70,CLINDESC
          MOVE      "1",ALLNKFND
.
          GOTO      PRTRF200 
.          
PRTRF800  MATCH     "1",ALLNKFND
          GOTO      PRTRF999 IF EQUAL
.
          CALL      PRTLIN00
.
PRTRF999  RETURN
.
.------------------------------------------------------------------------------
. Print referal detail
.------------------------------------------------------------------------------
PRTLIN00  ADD       "1",LINECNT
          IF        LINECNT > 60
            CALL      PRTHDR00
            ADD       "1",LINECNT
          ENDIF
.
          PRINT     *3,ALREVISN:
                    *12,"|":
                    *14,RDEPNAME:
                    *35,"|":
                    *37,EXPDATEX:
                    *49,"|":
                    *52,BOOKNO:
                    *63,"|":
                    *65,BOOKDTTM:
                    *86,"|":
                    *88,CLINDESC
.
PRTLIN99  RETURN
.
.------------------------------------------------------------------------------
. Close referral dtails
.------------------------------------------------------------------------------
CLSREF00  PACK      KEY8,ALREVISN,SP70
          CALL      RDALREF1
          BRANCH    OVRCD,PRTRF999
.
          MOVE      TWO,AUDTTYPE            * before history record
          CALL      WAALRE00
.
          MOVE      "2",ALRESTAT            * Closed
          MOVE      CURRDATE,ALREDCLO
          MOVE      CURRTIME,ALRETCLO
          MOVE      CLDRSNCD,ALRERCLO
.
          MOVE      SP70,ALRERINA            * in database
          MOVE      SP70,ALREDINA
          MOVE      SP70,ALRETINA
          MOVE      SP70,ALREDINU
          MOVE      SP70,ALREUINA
.
          MOVE      SP70,ALRESREJ
          MOVE      SP70,ALRESRDT
          MOVE      SP70,ALRESRTM
          MOVE      SP70,ALRESRUI
.
          CALL      UPALREF1
          ADD       ONE,NOFUAREF
.
          MOVE      THREE,AUDTTYPE          * after history record
          CALL      WAALRE00
.                                
          MOVE      ALREDCLO,ALSTSDAT
          CALL      WRTRSH00                * write a record to allstsaf
.
CLSREF99  RETURN
.
.------------------------------------------------------------------------------
. Print header details
.------------------------------------------------------------------------------
PRTHDR00  MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          IF        IBCNMHOS = 1
            PRINT     *40,"Hosiptal    :",*54,PTHSNAME
          ENDIF
          PRINT     *40,"Department  :",*54,DEPTDESC
          PRINT     *40,"Close Referrals xxx days since Expiry Date :":
                    *85,NOFDAYS
          PRINT     *1," "
          CALL      UND132
          PRINT     *1,"Referral ":
                    *10,"## | Department           | Expiry Date |":
                    *51,"Outpatient ##| Appointment Date/Time| Clinic Id"
          CALL      UND132
.
          IF        IBCNMHOS = 1
            MOVE      9,LINECNT
          ELSE
            MOVE      8,LINECNT
          ENDIF
.
PRIHDR99  RETURN
.
.******************************************************************************
.      write a record to the Referral Status History Table
.******************************************************************************
WRTRSH00  MOVE      ALREVISN,ALSTVISN       * Populate all the fields and write
          MOVE      ALRESTAT,ALSTSTAT
          PACK      ALSTCDAT,CCC,CYY,CMM,CDD
          REP       " 0",ALSTCDAT
          CLOCK     TIME,ALSTCTIM
          MOVE      WBSEUID,ALSTCUID
          PACK      KEY17,ALSTVISN,ALSTSTAT,ALSTSDAT,SP70
          CALL      RAALSTS1
          IF        OVRCD=0
            CALL      UPALSTS1                * update to the file
          ELSE
            CALL      WRALSTS1                * write to the file
          ENDIF
.
WRTRSH99  RETURN
.
          INC       STD001IO
          INC       CALCDAYS
          INC       PATCODKY
          INC       PATHSPKY
.
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       ALLREFIO/INC
          INC       ALLDEPIO/INC
          INC       ALLLNKIO/INC
          INC       ALLSTSIO/INC
          INC       OUTBOAIO/INC
          INC       OUTCLIIO/INC
          INC       WEBSECIO/INC
.

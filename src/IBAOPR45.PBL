.***************************************************************************
.* System    :   Theatre                                                   *
.* Program   :   IBAOPR45                                                  *
.* Desc      :   Print theatre registration form                           *
.***************************************************************************
.* Date      :   16/04/2002                                                *
.* Author    :   Ebon Clements                                             *
.*                                                                         *
.* Mods      :                                                             *
.*-------------------------------------------------------------------------*
.* V11.03.03 06/06/2023  Thanh T        TSK 0933298                        *
.*           Changed PROC0000 to pass hospital id when calling RDOPSES7    *    
.* V11.03.02 19/04/2023  Thanh T        TSK 0909393                        *
.*           Changed CASEKEYS size from 23 to 26                           *
.* V11.03.01 20/03/2023  PJ Le Febour   Task 0909393                       *
.*           Amended KEY19 to KEY22 for theatre index changes              *
.*-------------------------------------------------------------------------*
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                *
.*                  Added SEXDSCIO include                                 *
.*        V10.10.02 24/04/2017 Tracey Nguyen   TSK 0837246                 *
.*                  Added missing MBS Codes (OPDAPRV2 and OPDAPRV3) to     * 
.*                  Operation Code                                         *
.*        V10.10.01 14/02/2017   J.Tan         TSK 0829089                 *
.*                  Changed index on oprthiaf file                         *
.*        V10.04.01 23/06/2014   J.Tan         CAR 261630                  *
.*                  Removed port number from temp file name                *
.*        V10.01.01 03/02/2011  Jill Parkinson CAR 233088                  *
.*                  Added PMSVX1FD                                         *
.*        V9.03.03  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2   *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" - *
.*                  "Intersex" added                                       *
.*               V9.03.02 25/02/2004 Mike Laming   CAR 42948               *
.*                        Change to use OPSGUC11 instead of OPSGINFC       *
.*               V9.03.01 26/06/2003 Peter Vela    CAR 40412               *
.*                        Recompiled for OPRPIMFD, OPRPIMIO                *
.*               V9.02.04 17/07/2002 Ebon Clements SRF 18285               *
.*                        Added admitting and recovery nurses from         *
.*                        oprardaf to nurse display                        *
.*               V9.02.03 21/06/2002 Ebon Clements SRF 18285               *
.*                        Fixed display of skin prep and anaesthetic       *
.*               V9.02.02 Sandra Barcham                                   *
.*                        Fixed create form details                        *
.*               V9.02.01 Ebon Clements                                    *
.*                        Added create form details                        *
.*               V9.02.00 Ebon Clements                                    *
.*                        Created program                                  *
.***************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       PATDHEAD/INC
          INC       PATCALAG/INC
          INC       OPRDEAFD/INC
          INC       BOKRC1FD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       OPRARDFD/INC
          INC       PATCONFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       OPRSRGFD/INC
          INC       OPRTSMFD/INC
          INC       OPRNURFD/INC
          INC       OPRTQEFD/INC
          INC       OPRPIMFD/INC
          INC       OPRTHIFD/INC
          INC       OPRSESFD/INC
          INC       PATDO1FD/INC
.
.----------------------------------------------------------------------------
.         TEMPORARY INDEXED FILE DEFINITION
.----------------------------------------------------------------------------
.         Filename : opr45axx.dat          (xx = port id)
.
DOCTEMP1  IFILE SQL, FIXED=30, KEY="U1-1,2-3,7-29"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
XXTYPE    DIM       1           1           1       Staff Type
XXCATE    DIM       2           2           2       Staff Category
XXCODE    DIM       3           3           4       Staff Code     
XXNAME    DIM       23          23          7       Name                   
.
.End of Record                             30
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ADMISSNO  DIM       8
BJDAY     FORM      3
CMDLINE   DIM       80
CASEKEYS  DIM       26
COMM      INIT      ", "
COUNTER   FORM      2
CODEDES1  DIM       20
CODEDES2  DIM       20
CODEDES3  DIM       20
CODEDES4  DIM       20
CJDAY     FORM      3
DIM4      DIM       4
DIM5      DIM       5
DIM20     DIM       20
ERASE     INIT      "iserase "
FLAG1     FORM      1
FLAG2     FORM      1
FORMATNM  DIM       20       * Format Name
FRSTTEAM  FORM      1        * Team Number Flag            
ISBUILD   INIT      "isbuild "
ONOFF1    DIM       3
ONOFF2    DIM       3
ONOFF3    DIM       3
ONOFF4    DIM       3
ONOFF5    DIM       3
ONOFF6    DIM       3
ONOFF7    DIM       3
ONOFF8    DIM       3
PRINT1    DIM       15
PRINT2    DIM       15
PRINT3    DIM       15
PRINT4    DIM       15
PRINNUF1  DIM       15       * Recovery Nurse Front 1
PRINNUF2  DIM       15       * Recovery Nurse Front 2
PRINNUB1  DIM       15       * Recovery Nurse Back 1
PRINNUB2  DIM       15       * Recovery Nurse Back 2
PRINNUD1  DIM       15       * Recovery Nurse Day 1
PRINNUD2  DIM       15       * Recovery Nurse Day 2
PRINTDAT  DIM       10       * Format Date
PRINTAGE  DIM       10       * Format Age
PATFNAME  DIM       40
SEXDESC   DIM       8        * Sex Description                * was 7  *C-49016
TEMPFILE  DIM       8
TEAMNUMB  DIM       1        * Team Number
TIME1     DIM       5
TIME2     DIM       5
TIME3     DIM       5
TIME4     DIM       5
TIME5     DIM       5
TIME6     DIM       5
TIME7     DIM       5
UKEY      INIT      " 30 U1-1,2-3,7-29"
UNIQUENO  DIM       10       * Unique Number
URNUMBER  DIM       8        * U/R Number
.
PRGID     INIT      "IBAOPR45"
PRGNAM    INIT      "Print Registration Form      "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      KEYC0000               * Get Case Key
          BRANCH    EXIT,ML0100
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                * display heading
.
          DISPLAY   *P64:24,"oprdeaaf";     * Operation Details File
          OPEN      OPRDETA1,"oprdetaf"
.
          DISPLAY   *P64:24,"bokrc1af";     * Booking File             
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,"patcodes";     * Codes File               
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";     * Patient Master File      
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"oprardaf";     * Operation Arrival File   
          OPEN      OPRARDA1,"oprardaf"
.
          DISPLAY   *P64:24,"patmi1af";     * Admission File   
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattrnaf";     * Transfer File   
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"oprsrgaf";     * Surgical File   
          OPEN      OPRSRGA1,"oprsrgaf"
.
          DISPLAY   *P64:24,"oprtsmaf";     * Staff File   
          OPEN      OPRTSMA1,"oprtsmaf"
.
          DISPLAY   *P64:24,"oprnuraf";     * Nurse File   
          OPEN      OPRNURA1,"oprnuraf"
.
          DISPLAY   *P64:24,"oprtqeaf";     * Tourniquet File   
          OPEN      OPRTQEA1,"oprtqeaf"
.
          DISPLAY   *P64:24,"oprpimaf";     * Implant File   
          OPEN      OPRPIMA1,"oprpimaf"
.
          DISPLAY   *P64:24,"oprthiaf";     * Implant File   
          OPEN      OPRTHIA1,"oprthiaf"
.
          DISPLAY   *P64:24,"oprsesaf";     * Session File   
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
.
          DISPLAY   *P64:24,"patdo1af";     * Doctor File   
          OPEN      PATDO1A1,"patdo1af"
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      CASEKEYS,KEY26
          CALL      RDOPDEA1                * Read Operation Deatils
          BRANCH    OVRCD,PROC9999
.
          PACK      KEY22,OPDADATE,OPDATIME,OPDACLIN,OPDAHOSP,SP70
          CALL      RDOPSES7                * Read Session File
          BRANCH    OVRCD,PROC9999
.
          MOVE      OPDAUNIQ,UNIQUENO
. 
          MOVE      OPDAADMN,KEY8
          CALL      RDBKREC1                * Read Booking Details
          BRANCH    OVRCD,PROC9999
.
          MOVE      BKREURNO,URNUMBER
          MOVE      BKREBOOK,ADMISSNO
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1                * Read Patient Master File
          BRANCH    OVRCD,PROC9999
.
          MOVE      UNIQUENO,KEY10
          CALL      RDOPARD1               * Read Arrival File
          BRANCH    OVRCD,PROC9999
.
          MOVE      ADMISSNO,KEY8
          CALL      RDMISS1                * Read Admission File
          BRANCH    OVRCD,PROC9999
.
          PACK      KEY30,ADMISSNO,SP70
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,PROC0900
.
          MATCH     ADMISSNO,DTADMN
          GOTO      PROC0900 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE
          GOTO      PROC0900 IF NOT EQUAL
          GOTO      PROC1000
.
PROC0900  MOVE      SP70,TWARD
          MOVE      SP70,TDEPT
.
          MOVE      ZERO,FRSTTEAM
PROC1000  PACK      KEY11,UNIQUENO,TEAMNUMB,SP70
          CALL      RSOPSRG1
PROC1500  CALL      RKOPSRG1
          BRANCH    OVRCD,PROC9999
.
          MATCH     UNIQUENO,OPSGUNID
          GOTO      PROC9999 IF NOT EQUAL
.
          MOVE      OPSGTMNO,TEAMNUMB
.
          BRANCH    FRSTTEAM,PROC2000      * Only print header for team 1
          CALL      PRHD0000               * Print Header
.
PROC2000  CALL      PRBA0000               * Print Body
          CALL      PRBF0000               * Print Body 
          CALL      PRBG0000               * Print Body 
          CALL      PRBB0000               * Print Body 
          CALL      PRBC0000               * Print Body 
          CALL      PRBD0000               * Print Body 
          CALL      PRBE0000               * Print Body 
.
          MOVE      ONE,FRSTTEAM           * Loop again for other teams
          GOTO      PROC1500
.
PROC9999  RETURN
+
.***************************************************************************
.*                               KEYC0000                                  *
.* Keyin Case key                                                          *
.***************************************************************************
.
KEYC0000    MOVE      ZERO,EXIT
            DISPLAY   *P1:9,*EF,"Please enter CaseKey :";
.
            KEYIN     *P25:9,*V2LON,CASEKEYS
.
            PACK      CASEKEYS,CASEKEYS,SP70
.
            MATCH     SP70,CASEKEYS
            GOTO      KEYC9000 IF EQUAL
.
            GOTO      KEYC9999
.
KEYC9000    MOVE      ONE,EXIT
.
KEYC9999    RETURN
+
.**************************************************************************
.*                               PRHD0000              Called by: PROC0000*
.* Print standard pmi and header details                                  *
.**************************************************************************
.
PRHD0000  PRINT     *1,"CREATEFORM":                * Create Form info 
                    *N,*1,"opregform"
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,SEXDESC
.* ******************************************   End of Changes         *C-49016
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY   * Format DOB
          CALL      PACDATE
          MOVE      CPCDATE,PRINTDAT
.
          CALL      IBACLOCK
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * today's date
          ENDIF
          REP       " 0",AGEDATE
.
          CALL      CALCAGE
          CALL      WRTAGE00
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format the patients name
          MOVE      PACFNAME,PATFNAME
.
          PRINT     *1,"Patient: ",*10,URNUMBER,*19,PATFNAME,*55,SEXDESC:
                    *62,PRINTDAT,*74,PRINTAGE
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY   * Format Admission 
          CALL      PACDATE
          MOVE      CPCDATE,PRINTDAT
.
          MOVE      ATIME,DIM5
.
          PRINT     *1,"Episode: ",ADMISSNO,*19,"Admission Date/Time: ":
                    *40,PRINTDAT:
                    *51,DIM5,*60,"Ward: ",*66,TWARD,*72,"Unit: ",*78,TDEPT
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY   * Operation Admission 
          CALL      PACDATE
          MOVE      CPCDATE,PRINTDAT
.
          PRINT     *N,*1,"Operation No: ",*15,OPDACASE,*19,"Operation Date: ":
                    *35,PRINTDAT,*60,"Delay Reason: ",*74,OPARRLAT
.
          PRINT     *1,"Theatre: ",*10,OPDATHET
.
          PRINT     *1,"Operation Code: ",*17,OPDAPROV,*27,OPDADES1:
                    *N,*17,OPDAPRV2,*27,OPDADES2:
                    *N,*17,OPDAPRV3,*27,OPDADES3
.
PRHD9999  RETURN
+
.**************************************************************************
.*                               PRHD0000              Called by: PROC0000*
.* Print standard body details                                            *
.**************************************************************************
.
PRBA0000  IF        FRSTTEAM = 0
            PRINT     *N,*1,"Times: ",*10,"Called For",*22,"Arrived":
                      *32,"Anaes.",*42,"Prep.",*51,"Dressing":
                      *62,"In RR",*72,"Out RR"
          ELSE
            PRINT     *F,*1,*V2LON,"Team ",TEAMNUMB:
                      *N,*1,*HOFF,"Times: ",*10,"Called For",*22,"Arrived":
                      *32,"Anaes.",*42,"Prep.",*51,"Dressing":
                      *62,"In RR",*72,"Out RR"
          ENDIF
.
          MOVE      OPARTCAL,TIME1
          MOVE      OPARATIM,TIME2
          MOVE      OPARANAS,TIME3
          MOVE      OPSGTIPS,TIME4
          MOVE      OPSGTIDR,TIME5
          MOVE      OPARTIRF,TIME6
          MOVE      OPARTETC,TIME7
.
          PRINT     *12,TIME1,*22,TIME2,*32,TIME3,*42,TIME4,*52,TIME5:
                    *62,TIME6,*72,TIME7
.
          MOVE      SP70,FORMATNM
          PACK      KEY6,OPSEANAE,SP70
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DGNAME,DIM1
            STRIP     DSNAME
            PACK      FORMATNM,DSNAME,COMM,DIM1
          ENDIF
.      
          PACK      TCODE,ANSO,ANSA,SP70         * Anaesthetic type description
          MOVE      OPDAANAE,ACODE
          CALL      GTCOD000
.
PRBA1000  PRINT     *N,*1,"Anaesthetist(s):",*18,FORMATNM:
                    *40,"Anaesthetic Type:",*58,TDESC
.
PRBA9999  RETURN
+
.**************************************************************************
.*                               PRBF0000              Called by: PROC0000*
.* Print standard body details                                            *
.**************************************************************************
.
PRBF0000  MOVE      SP70,FORMATNM
          PACK      KEY6,OPSECLIN,SP70
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DGNAME,DIM1
            STRIP     DSNAME
            PACK      FORMATNM,DSNAME,COMM,DIM1
          ENDIF
.
PRBF1000  PRINT     *N,*1,"Surgeon: ",*10,FORMATNM,*40,"Nurses:"
.
PRBF9999  RETURN
+
.**************************************************************************
.*                               PRBG0000              Called by: PROC0000*
.* Print standard body details                                            *
.**************************************************************************
.
PRBG0000  CALL      CRTV0000                        * Creat Temp File for Staff
.
          PACK      KEY35,UNIQUENO,TEAMNUMB,SP70
          CALL      RSOPTSM1
PRBG1000  CALL      RKOPTSM1
          BRANCH    OVRCD,PRBG5000
.
          MATCH     UNIQUENO,OPTSUNID
          GOTO      PRBG5000 IF NOT EQUAL    
.
          MATCH     TEAMNUMB,OPTSTMNO
          GOTO      PRBG5000 IF NOT EQUAL    
.
          BRANCH    OPTSSIND,PRBG2000,PRBG2000,PRBG2000
.
          GOTO      PRBG1000
.
PRBG2000  IF        OPTSSIND = 2
            MOVE      ONE,XXTYPE                     * Nurse Staff
            MOVE      "ON",XXCATE                    * Staff Category
          ELSE
            MOVE      ZERO,XXTYPE                    * Doctor or Other Staff
            IF        OPTSSIND = 1
              MOVE      "OP",XXCATE                  * Staff Category
            ELSE
              MOVE      "OF",XXCATE                  * Staff Category
            ENDIF
          ENDIF
.
          MOVE      OPTSSTYP,XXCODE                  * Staff Categegory Code
.
          MOVE      SP70,FORMATNM
          IF        OPTSSIND = 2
            PACK      KEY6,OPTSSCDE,SP70
            CALL      RDOPNUR1                               * Nurse
            IF        OVRCD=0
              MOVE      OPNRGNAM,DIM1
              STRIP     OPNRSNAM
              PACK      FORMATNM,OPNRSNAM,COMM,DIM1
            ENDIF
          ELSE
            PACK      KEY6,OPTSSCDE,SP70
            CALL      RDDOCT1                                * Doctor or Other
            IF        OVRCD=0
              MOVE      DGNAME,DIM1
              STRIP     DSNAME
              PACK      FORMATNM,DSNAME,COMM,DIM1
            ENDIF
          ENDIF
          MOVE      FORMATNM,XXNAME
.
          MATCH     SP70,XXNAME
          GOTO      PRBG1000 IF EQUAL
.
          PACK      KEY26,XXTYPE,XXCATE,XXNAME,SP70
          CALL      RATEMP1
          IF        OVRCD = 1
            CALL      WRTEMP1
          ENDIF
.
          GOTO      PRBG1000
.
PRBG5000  CALL      GETR0000                     * Get recovery nurse details
.
          MATCH     SP70,OPARANUR                * Print first line of staff
          GOTO      PRBG5500 IF EQUAL            * for the admitting nurse
.                                                * This is done because the 
          PACK      KEY6,OPARANUR,SP70           * admitting nurse is not in
          CALL      RDOPNUR1                     * the temp file
          BRANCH    OVRCD,PRBG5500

          MOVE      OPNRGNAM,DIM1
          STRIP     OPNRSNAM
          PACK      FORMATNM,OPNRSNAM,COMM,DIM1
          MOVE      FORMATNM,PRINT4
.
          CALL      GETS0000                     * Get first display for doctor
.
          PRINT     *1,PRINT1,*20,PRINT2,*40,"Admitting",*60,PRINT4
.
PRBG5500  CALL      GETV0000                     * Get Variables to Print
.
          PRINT     *1,PRINT1,*20,PRINT2,*40,PRINT3,*60,PRINT4
.
          IF        FLAG1 = 1 & FLAG2 = 1
            GOTO     PRBG9000
          ENDIF                
          GOTO     PRBG5500
.
PRBG9000  CALL      KILV0000
.
PRBG9999  RETURN
+
.**************************************************************************
.*                               GETV0000              Called by:PRBG0000 *
.* Get details for doctors                                                *
.**************************************************************************
GETV0000  MOVE      ZERO,FLAG1
          MOVE      ZERO,FLAG2
          UNPACK    SP70,PRINT1,PRINT2
          UNPACK    SP70,PRINT3,PRINT4
.
          PACK      KEY26,ZERO,SP70
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,GETV6000
.
          MATCH     "0",XXTYPE
          GOTO      GETV6000 IF NOT EQUAL
.
          MOVE      XXCATE,TCODE
          PACK      ACODE,XXCODE,SP70
          CALL      GTCOD000
          MOVE      TDESC,PRINT1
.
          MOVE      XXNAME,PRINT2
.
          PACK      KEY26,XXTYPE,XXCATE,XXNAME,SP70
          CALL      DETEMP1
          GOTO      GETV6500
.
GETV6000  MOVE      ONE,FLAG1                    * No more Doctors or Other
.                                                * to print
GETV6500  PACK      KEY26,ONE,SP70
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,GETV8000
.
          MATCH     "1",XXTYPE
          GOTO      GETV8000 IF NOT EQUAL
.
          MOVE      XXCATE,TCODE
          PACK      ACODE,XXCODE,SP70
          CALL      GTCOD000
          MOVE      TDESC,PRINT3
.
          MOVE      XXNAME,PRINT4
.
          PACK      KEY26,XXTYPE,XXCATE,XXNAME,SP70
          CALL      DETEMP1
          GOTO      GETV9999
.
GETV8000  MATCH     SP70,PRINNUF1
          IF        !@EQUAL
            MOVE      "Recovery Front ",PRINT3
            MOVE       PRINNUF1,PRINT4
            MOVE       SP70,PRINNUF1
            GOTO      GETV9999
          ENDIF
.
          MATCH     SP70,PRINNUF2
          IF        !@EQUAL
            MOVE      "Recovery Front ",PRINT3
            MOVE       PRINNUF2,PRINT4
            MOVE       SP70,PRINNUF2
            GOTO      GETV9999
          ENDIF
.
          MATCH     SP70,PRINNUB1
          IF        !@EQUAL
            MOVE      "Recovery Back  ",PRINT3
            MOVE       PRINNUB1,PRINT4
            MOVE       SP70,PRINNUB1
            GOTO      GETV9999
          ENDIF
.
          MATCH     SP70,PRINNUB2
          IF        !@EQUAL
            MOVE      "Recovery Back  ",PRINT3
            MOVE       PRINNUB2,PRINT4
            MOVE       SP70,PRINNUB2
            GOTO      GETV9999
          ENDIF
.
          MATCH     SP70,PRINNUD1
          IF        !@EQUAL
            MOVE      "Recovery Day   ",PRINT3
            MOVE       PRINNUD1,PRINT4
            MOVE       SP70,PRINNUD1
            GOTO      GETV9999
          ENDIF
.
          MATCH     SP70,PRINNUD2
          IF        !@EQUAL
            MOVE      "Recovery Day   ",PRINT3
            MOVE       PRINNUD2,PRINT4
            MOVE       SP70,PRINNUD2
            GOTO      GETV9999
          ENDIF
.
          MOVE      ONE,FLAG2                    * No more Nurses to print
.
GETV9999  RETURN
+
.**************************************************************************
.*                               GETR0000              Called by:PRBG0000 *
.* Get Recovery nurse details                                             *
.**************************************************************************
GETR0000  MOVE      SP70,PRINNUF1                * Clear recovery nurse details
          MOVE      SP70,PRINNUF2
          MOVE      SP70,PRINNUB1
          MOVE      SP70,PRINNUB2
          MOVE      SP70,PRINNUD1
          MOVE      SP70,PRINNUD2
.
          PACK      KEY6,OPARNUF1,SP70           * Recovery Nurse 1 Front    
          CALL      RDOPNUR1
          IF        OVRCD = 0
            MOVE      OPNRGNAM,DIM1
            STRIP     OPNRSNAM
            PACK      FORMATNM,OPNRSNAM,COMM,DIM1
            MOVE      FORMATNM,PRINNUF1
          ENDIF
.
          PACK      KEY6,OPARNUF2,SP70           * Recovery Nurse 2 Front    
          CALL      RDOPNUR1
          IF        OVRCD = 0
            MOVE      OPNRGNAM,DIM1
            STRIP     OPNRSNAM
            PACK      FORMATNM,OPNRSNAM,COMM,DIM1
            MOVE      FORMATNM,PRINNUF2
          ENDIF
.
          PACK      KEY6,OPARNUB1,SP70           * Recovery Nurse 1 Back     
          CALL      RDOPNUR1
          IF        OVRCD = 0
            MOVE      OPNRGNAM,DIM1
            STRIP     OPNRSNAM
            PACK      FORMATNM,OPNRSNAM,COMM,DIM1
            MOVE      FORMATNM,PRINNUB1
          ENDIF
.
          PACK      KEY6,OPARNUB2,SP70           * Recovery Nurse 2 Back     
          CALL      RDOPNUR1
          IF        OVRCD = 0
            MOVE      OPNRGNAM,DIM1
            STRIP     OPNRSNAM
            PACK      FORMATNM,OPNRSNAM,COMM,DIM1
            MOVE      FORMATNM,PRINNUB2
          ENDIF
.
          PACK      KEY6,OPARNUD1,SP70           * Recovery Nurse 1 Day     
          CALL      RDOPNUR1
          IF        OVRCD = 0
            MOVE      OPNRGNAM,DIM1
            STRIP     OPNRSNAM
            PACK      FORMATNM,OPNRSNAM,COMM,DIM1
            MOVE      FORMATNM,PRINNUD1
          ENDIF
.
          PACK      KEY6,OPARNUD2,SP70           * Recovery Nurse 2 Day     
          CALL      RDOPNUR1
          IF        OVRCD = 0
            MOVE      OPNRGNAM,DIM1
            STRIP     OPNRSNAM
            PACK      FORMATNM,OPNRSNAM,COMM,DIM1
            MOVE      FORMATNM,PRINNUD2
          ENDIF
.
GETR9999  RETURN
+
.**************************************************************************
.*                               GETS0000              Called by:PRBG0000 *
.* Get details for doctors                                                *
.**************************************************************************
GETS0000  UNPACK    SP70,PRINT1,PRINT2
.
          PACK      KEY26,ZERO,SP70
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,GETS9999
.
          MATCH     "0",XXTYPE
          GOTO      GETS9999 IF NOT EQUAL
.
          MOVE      XXCATE,TCODE
          PACK      ACODE,XXCODE,SP70
          CALL      GTCOD000
          MOVE      TDESC,PRINT1
.
          MOVE      XXNAME,PRINT2
.
          PACK      KEY26,XXTYPE,XXCATE,XXNAME,SP70
          CALL      DETEMP1
          GOTO      GETS9999
.
GETS9999  RETURN
+
.**************************************************************************
.*                               PRBB0000              Called by: PROC0000*
.* Print standard body details                                            *
.**************************************************************************
PRBB0000   MOVE      "Tf",TCODE    
           PACK      ACODE,OPSGUC01,SP70
           CALL      GTCOD000
           MOVE      TDESC,CODEDES1
.
           MOVE      "Tf",TCODE    
           PACK      ACODE,OPSGUC02,SP70
           CALL      GTCOD000
           MOVE      TDESC,CODEDES2
.
           MOVE      "Tf",TCODE    
           PACK      ACODE,OPSGUC03,SP70
           CALL      GTCOD000
           MOVE      TDESC,CODEDES3
.
           MOVE      "Ti",TCODE    
           PACK      ACODE,OPSGUC04,SP70
           CALL      GTCOD000
           MOVE      TDESC,CODEDES4
.
           PRINT     *N,*1,"Skin Prep: ",*12,CODEDES1:
                     *40,"Diathermy:",*51,CODEDES4
.
           PRINT     *12,CODEDES2,*40,"Number: ",*49,OPSGDNM1

           PRINT     *12,CODEDES3,*40,"Plate Site: ",*52,OPSGDPS1
.
           MOVE      "Tj",TCODE    
           PACK      ACODE,OPSGUC05,SP70
           CALL      GTCOD000
           MOVE      TDESC,CODEDES1
.
           PRINT     *40,"Diathermy: ",*51,CODEDES1
           PRINT     *40,"Number: ",*48,OPSGDNM2
           PRINT     *40,"Plate Site: ",*52,OPSGDPS2
.
           PRINT     *N,*40,"Calf Stimulator: ",*58,OPSGCLFS
PRBB9999  RETURN
+
.**************************************************************************
.*                               PRBC0000              Called by: PROC0000*
.* Print standard body details                                            *
.**************************************************************************
PRBC0000  PRINT      *N,*1,"Infection Control Classification: ",*35,OPSGUC11
.*                                                     * was OPSGINFC  *C-42948
.
          PRINT      *N,*1,"Catheter: ",*13,OPSGCAT1,OPSGUF07
          PRINT      *1,"Catheter: ",*13,OPSGCAT2,OPSGUF08
          PRINT      *1,"Drain Tube: ",*13,OPSGDRT1,OPSGUF01
          PRINT      *1,"Drain Tube: ",*13,OPSGDRT2,OPSGUF02
          PRINT      *1,"Packing:    ",*13,OPSGPACK,OPSGUF03
          PRINT      *N,*1,"Specimens:  ",*12,OPSGSPMI:
                     *35,"Anatomical Pathology: ",*57,OPSGSPAP:
                     *70,"Other: ",*77,OPSGOTHR
.
PRBC9999  RETURN
+
.**************************************************************************
.*                               PRBD0000              Called by: PROC0000*
.* Print standard body details                                            *
.**************************************************************************
PRBD0000  PRINT       *N,*1,"Tourniquet Details:"
          PRINT       *N,*20,"Right Leg",*35,"Left Leg":
                      *50,"Right Arm",*65,"Left Arm"
.
          BRANCH      FRSTTEAM,PRBD9999             * Tourniquet only for team 1
.
          MOVE        ZERO,COUNTER
.
PRBD1000  MOVE        ZERO,FLAG1                      * No record to Print
          ADD         ONE,COUNTER
.
          MOVE        "OFF",ONOFF1 
          MOVE        "OFF",ONOFF2 
          PACK        KEY13,UNIQUENO,ONE,COUNTER
          CALL        RDOPTQE1
          IF          OVRCD = 0
            MOVE        ONE,FLAG1
            MATCH       SP70,OPTQADAT
            IF          !@EQUAL
              MOVE        "ON ",ONOFF1
            ENDIF
            MATCH       SP70,OPTQRDAT
            IF          !@EQUAL
              MOVE        "OFF",ONOFF2
            ENDIF
          ENDIF
.
          MOVE        "OFF",ONOFF3 
          MOVE        "OFF",ONOFF4 
          PACK        KEY13,UNIQUENO,TWO,COUNTER
          CALL        RDOPTQE1
          IF          OVRCD = 0
            MOVE        ONE,FLAG1
            MATCH       SP70,OPTQADAT
            IF          !@EQUAL
              MOVE        "ON ",ONOFF3
            ENDIF
            MATCH       SP70,OPTQRDAT
            IF          !@EQUAL
              MOVE        "OFF",ONOFF4
            ENDIF
          ENDIF
.
          MOVE        "OFF",ONOFF5 
          MOVE        "OFF",ONOFF6 
          PACK        KEY13,UNIQUENO,THREE,COUNTER
          CALL        RDOPTQE1
          IF          OVRCD = 0
            MOVE        ONE,FLAG1
            MATCH       SP70,OPTQADAT
            IF          !@EQUAL
              MOVE        "ON ",ONOFF5
            ENDIF
            MATCH       SP70,OPTQRDAT
            IF          !@EQUAL
              MOVE        "OFF",ONOFF6
            ENDIF
          ENDIF
.
          MOVE        "OFF",ONOFF7 
          MOVE        "OFF",ONOFF8 
          PACK        KEY13,UNIQUENO,FOUR,COUNTER  
          CALL        RDOPTQE1
          IF          OVRCD = 0
            MOVE        ONE,FLAG1
            MATCH       SP70,OPTQADAT
            IF          !@EQUAL
              MOVE        "ON ",ONOFF7
            ENDIF
            MATCH       SP70,OPTQRDAT
            IF          !@EQUAL
              MOVE        "OFF",ONOFF8
            ENDIF
          ENDIF
.
          COMPARE     ZERO,FLAG1                * No records to Print
          GOTO        PRBD9999 IF EQUAL
.
          PRINT       *20,ONOFF1,*25,ONOFF2,*35,ONOFF3,*40,ONOFF4:
                      *50,ONOFF5,*55,ONOFF6,*65,ONOFF7,*70,ONOFF8
.
          GOTO        PRBD1000
.
PRBD9999  RETURN
+
.**************************************************************************
.*                               PRBE0000              Called by: PROC0000*
.* Print standard body details                                            *
.**************************************************************************
PRBE0000  PRINT     *N,*1,"Implant Details":
                    *N,*1,"Implant Type"
.
          PRINT     *N,*1,"Cat No.",*20,"Description":
                    *45,"Lot/Serial No.",*70,"Cost Code"
.
          PACK      KEY13,UNIQUENO,TEAMNUMB,SP70
          CALL      RSOPPIM1
PRBE1000  CALL      RKOPPIM1
          BRANCH    OVRCD,PRBE9999
.
          MATCH     OPPIUNID,UNIQUENO
          GOTO      PRBE9999 IF NOT EQUAL
.
          MATCH     OPPITMNO,TEAMNUMB
          GOTO      PRBE9999 IF NOT EQUAL
.
          MOVE      SP70,DIM20
          PACK      KEY75,OPPIICDE,OPTIBCOD,SP70
          CALL      RDOPTHI1
          IF        OVRCD = 0
            MOVE      OPTIDESC,DIM20
          ENDIF
.
          PRINT     *1,OPPIICDE,*20,DIM20,*45,OPPILTSN,*70,OPPICOST
.
          GOTO      PRBE1000
.
PRBE9999  RETURN
.
+
.**************************************************************************
.*                               WRTAGE00              Called by:WRTAGE00 *
.* Print standard pmi and header details                                  *
.**************************************************************************
.
WRTAGE00  MATCH     "y",PSAGETYP
          GOTO      WRTAGE10 IF NOT EQUAL
          MOVE      " yrs",DIM4
          PACK      PRINTAGE,PSAGEYRS,DIM4
          GOTO      WRTAGE99
.
WRTAGE10  MATCH     "d",PSAGETYP
          GOTO      WRTAGE20 IF NOT EQUAL
          MOVE      " days",DIM4
          PACK      PRINTAGE,PSAGEDAY,DIM4     
          GOTO      WRTAGE99
.
WRTAGE20  MATCH     "m",PSAGETYP
          GOTO      WRTAGE30 IF NOT EQUAL
          MOVE      " mths",DIM4
          PACK      PRINTAGE,PSAGEMNT,DIM4     
          GOTO      WRTAGE99
.
WRTAGE30  MATCH     "w",PSAGETYP
          GOTO      WRTAGE99 IF NOT EQUAL
          MOVE      " wks",DIM4
          PACK      PRINTAGE,PSAGEWKS,DIM4    
.
WRTAGE99  RETURN
.------- Get a codes file record -------
.
GTCOD000  MOVE      SP20,TDESC               * Initialise description
          UNPACK    SP20,THCSCOD,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
          MOVE      ZERO,TCFORM6
.
.         Is the code blank ?
.
          MATCH     SP3,ACODE
          GOTO      GTCOD999 IF EQUAL
.
.         Get the code file record
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1
.
GTCOD999  RETURN
.
+
.**************************************************************************
.*                              CRTV0000               Called by:         *
.* Create Temp Visit file                                                 *
.**************************************************************************
CRTV0000  CALL      KILV0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      DOCTEMP1,TEMPFILE            * open temp index file
.
          CALL      CLET0000                     * Clear tempfile
.
CRTV9999  RETURN
+
.****************************************************************************
.*                              KILV0000               Called by:           *
.*               close and erase the temporary visit file                   *
.****************************************************************************
KILV0000  CLOSE     DOCTEMP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILV9999 RETURN
.
+
.****************************************************************************
.*        Clear tempfile                                                    *
.****************************************************************************
CLET0000  MOVE      SP70,KEY26
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLET9999
.
          PACK      KEY26,XXTYPE,XXCATE,XXNAME,SP70
          CALL      DETEMP1
          GOTO      CLET0000
.
CLET9999  RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILES                                   *
.****************************************************************************
.
RSTEMP1   READ      DOCTEMP1,KEY26;;
          RETURN
.
RATEMP1   MOVE      ZERO,OVRCD
          READ      DOCTEMP1,KEY26;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    DOCTEMP1;XXTYPE,XXCATE,XXCODE,XXNAME
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     DOCTEMP1,KEY26;XXTYPE,XXCATE,XXCODE,XXNAME
          RETURN
.
DETEMP1   DELETE    DOCTEMP1,KEY26
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       SEXDSCIO
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       CALCAGE
          INC       OPRDEAIO/INC
          INC       BOKRC1IO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       OPRARDIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       OPRSRGIO/INC
          INC       OPRTSMIO/INC
          INC       OPRNURIO/INC
          INC       OPRTQEIO/INC
          INC       OPRPIMIO/INC
          INC       OPRTHIIO/INC
          INC       OPRSESIO/INC
          INC       PATDO1IO/INC

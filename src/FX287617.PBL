.***************************************************************************
.* System    :   Allied Health                                             *
.* Program   :   FX287617                                                  *
.* Desc      :   Populate the event program field on referrals and contacts*
.***************************************************************************
.* Date      :   15/10/2013                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will prompt the user for an event program.   *
.*           :   It will then loop the referral file for VINAH episode     *
.*           :   referrals. If the event program is blank on the           *
.*           :   referral it will be updated. It will then check any       *
.*           :   associated contacts and update any that have a blank      *
.*           :   event program.                                            *
.* Modifications  :                                                        *
.*        V10.03.00 15/10/2013  Ebon Clements CAR 287617                   *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC  
          INC       ALLENCFD/INC  
          INC       PATCODFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CATZG     INIT      "zG"
CODE      DIM       2
LOOPCNTR  FORM      4
EVENTPRG  DIM       3
ENCCOUNT  FORM      8
REFCOUNT  FORM      8
.
PRGID     INIT      "FX287617"
PRGNAM    INIT      "Populate Event Program on Referrals and Contacts"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
          GOTO      ML9999
.
ML1000    CALL      EVPR0000               * Keyin the event program
          BRANCH    EXIT,ML0100 
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      ALLREFA1,"allrefaf"
          OPEN      ALLENCA1,"allencaf"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,HUND06;*225,ALCNDEVP  * using event program 
.
          MATCH     "1",ALCNDEVP
          IF         !@EQUAL
             DISPLAY   *P1:24,*EL,*B,*V2LON,"Error display event ":
                       "program parameter is not turned on.";
             CALL      HITENTER
             GOTO      INIT9000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9000  MOVE      ONE,EXIT
          GOTo      INIT9999
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Populate blank event program ":
                    "field on VINAH epsiode referrals and contacts "
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,REFCOUNT
          MOVE      ZERO,ENCCOUNT
          MOVE      ZERO,LOOPCNTR
.
          PACK      KEY8,SP70
          CALL      RSALREF1                     * Loop referrals
PROC1000  CALL      RKALREF1
          BRANCH    OVRCD,PROC9000
.
          ADD       ONE,LOOPCNTR
          IF        LOOPCNTR=5000
            DISPLAY   *P15:20,*V2LON,"Referral Number :",ALREVISN
            MOVE      ZERO,LOOPCNTR
          ENDIF
.
          MATCH     "1",ALREUYN4                 * Skip referral in
          GOTO      PROC1000 IF EQUAL
        
          MATCH     SP70,ALREEVPR                * Skip referral if the event
          GOTO      PROC1000 IF NOT EQUAL        * program is already populated
.
          PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1                      * Read cat CG to check if this
          BRANCH    OVRCD,PROC1000               * is a VINAH Referral
.
          MATCH     SP70,TCINDC8                 * Skip not a VINAH referral
          GOTO      PROC1000 IF EQUAL
.
          MOVE      EVENTPRG,ALREEVPR            * Populate event program
          CALL      UPALREF1
          ADD       ONE,REFCOUNT
.
          PACK      KEY16,ALREVISN,SP70
          CALL      RSALENC1                     * Loop contacts for 
PROC5000  CALL      RKALENC1                     * this referral
          BRANCH    OVRCD,PROC1000
.
          MATCH     ALREVISN,ALENVISN            * Contact for this referral
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP70,ALENUC33                * Skip contact if the event
          GOTO      PROC5000 IF NOT EQUAL        * program is already populated
.
          MOVE      EVENTPRG,ALENUC33            * Populate event program
          CALL      UPALENC1
          ADD       ONE,ENCCOUNT
          GOTO      PROC5000
.
PROC9000  DISPLAY   *P1:23,*EF,*B,"Update Count - Referrals :",REFCOUNT:
                                  "  Contacts : ",ENCCOUNT
          CALL      HITENTER
.
PROC9999  RETURN
.
.****************************************************************************
.* Keyin the event program  - Cat zG                                        *
.****************************************************************************
EVPR0000  DISPLAY   *P1:9,"Event Program : ",SP30
          MOVE      SP70,EVENTPRG
          MOVE      "17",ECOL
          MOVE      "9",EVERT
          MOVE      ONE,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,CATZG
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,EVPR9100,EVPR9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P25:EVERT,TDESC
          MOVE      ACODE,EVENTPRG
.
          MATCH     ANSE,TCINDC2            * Exclude from VINAH Extract
          GOTO      EVPR9200 IF EQUAL
.
          MATCH     SP70,TCINDC8            * Any program stream
          GOTO      EVPR9300 IF NOT EQUAL
.
EVPR9000  MOVE      ZERO,EXIT
          GOTO      EVPR9999
.
EVPR9100  MOVE      ONE,EXIT
          GOTO      EVPR9999
.
EVPR9200  DISPLAY   *P1:24,*EL,*B,*V2LON,"Must be an include in ":
                       "VINAH extract event program.";
          CALL      HITENTER
          GOTO      EVPR0000
.
EVPR9300  DISPLAY   *P1:24,*EL,*B,*V2LON,"Program stream must be blank";
          CALL      HITENTER
          GOTO      EVPR0000
.
EVPR9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODKY
.
          INC       ALLREFIO/INC  
          INC       ALLENCIO/INC  
          INC       PATCODIO/INC

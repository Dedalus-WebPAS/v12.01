.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT35.PBL                                              *
.* Name           : Referral Statistics Report                                *
.******************************************************************************
.* Date           : 03/03/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To report on the Letters still waiting to be booked       *
.* Modifications  :                                                           *
.*        V10.05.01 15/07/2014  Ebon Clements     CAR 261630                  *
.*                  Removed port number from temp file name                   *
.******************************************************************************
.*                  V5.04.01  21/09/96 Howellsy SRF641938                     *
.*                  Use new standard for patdocky keyin (CNEWDS)              *
.*                  V5.01.002 19/02/93 J.Tan    SRF 117804                    *
.*                  Fixed all letters to include every priority               *
.*                  V5.01.003 04/06/93  ROD     SRF 117804                    *
.*                  Fixed ALL letters option so it prints something!  READKS  *
.*        V5.04.02  20/12/1996  Howellsy          EOC & ACC                   *
.*                  Recompiled for OUTRF1FD                                   *
.*        V5.04.03  30/01/1997  Howellsy          EOC & ACC                   *
.*                  Outpatient Referrals Only - OTRLTREF = 0                  *
.******************************************************************************
.
          INC       STD001FD/PBL
          INC       IBASEQFD/INC
          INC       OUTRF1FD/INC            * referral letters
          INC       PATCODFD/INC            * codes file
          INC       PATDO1FD/INC            * doctors file
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.         program variables
.
CMDLINE   DIM       80
CODECONS  DIM       6         * consultant code
CURRCONS  DIM       6         * current consultant
CURRDATE  DIM       8         * current date
DATE10A   DIM       10        * starting date dd/mm/ccyy
DATE10B   DIM       10        * ending   date dd/mm/ccyy
DATESTOP  DIM       8         * end   date
DATESTRT  DIM       8         * start date
DAYS      FORM      5         * number of days b/w dates
DESCCONS  DIM       30        * consultant name
DIM3      DIM       3         * patdsdoc
DOCCODE   DIM       6         * for keyindoc
ENDCENT   FORM      2         * ending   date century
ENDDAY    FORM      3         * ending   date days
ENDLEAP   FORM      1         * ending   date leap year
ENDYR     FORM      3         * ending   date year
DIM22     DIM       22
PTCNDCQS  FORM      1
STRTCENT  FORM      2         * starting date century
STRTDAY   FORM      3         * starting date days
STRTLEAP  FORM      1         * starting date leap year
STRTYR    FORM      3         * starting date year
.
.         temp file
.
TEMP1     IFILE     SQL, FIXED=35, KEY="U1-3,4-9"
TEMP2     IFILE     SQL, FIXED=35, KEY="U4-9,1-3"
.
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  ------------------------------------
XPRTY     DIM       3          3       1  Priority
XCONS     DIM       6          6       4  Consultant
XRNG1     FORM      4          3      10  Amount in range 1    0 -  13
XRNG2     FORM      4          3      13  Amount in range 2   14 -  30
XRNG3     FORM      4          3      16  Amount in range 3   31 -  60
XRNG4     FORM      4          3      19  Amount in range 4   61 -  90
XRNG5     FORM      4          3      22  Amount in range 5   91 - 120
XRNG6     FORM      4          3      25  Amount in range 6  121 - 180
XRNG7     FORM      4          3      28  Amount in range 7  181 - 
XTOTL     FORM      5          4      31  Total
.End of Record                        35
.
.         program constants
.
CATPR     INIT      "PR"
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
KEY       INIT      " 35 U1-3,4-9 U4-9,1-3"
MENUOPT1  INIT      "All Letters           "
MENUOPT2  INIT      "All Letters/Consultant"
MENUOPT3  INIT      "Individual Consultant "
ZED3      INIT      "zzz"
ZED6      INIT      "zzzzzz"
ZED9      INIT      "zzzzzzzzz"
.
PRGID     INIT      "IBAOUT35"
PRGNAM    INIT      "Referral Statistics Report"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        MAINLINE CODE / CONTROLLING LOGIC                          *
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * run the program
          BRANCH    EXIT,ML9999             * exit
.
ML2000    CALL      KDAT0000                * enter date range
          BRANCH    EXIT,ML1000             * exit 
.
          CALL      DATA0000                * produce the data
          CALL      REPT0000                * produce the report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization                                             *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
          DISPLAY   *P58:24,"outrf1af"
          OPEN      OUTRF1A3,"outrf1af"
          OPEN      OUTRF1A5,"outrf1af"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P58:24,"patcodes";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ONE,CNEWDS
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAME
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        Run the option ??                                          *
.*********************************************************************
OPTN0000  HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*P1:5,"1",*P1:6,"2",*P1:7,"3",*HOFF:
                    *P2:4,". Exit":
                    *P2:5,". All letters":
                    *P2:6,". All letters by Consultant":
                    *P2:7,". Individual Consultant":
                    *P1:9,"Select option :"
.
OPTN1000  KEYIN     *P17:9,*DV,UNDLN1:
                    *P17:9,*V2LON,MOPTION
          BRANCH    MOPTION,OPTN9000,OPTN9000,OPTN9000
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9100 IF EQUAL       * exit
.
OPTN9000  MOVE      ZERO,EXIT
          LOAD      DIM22,MOPTION,MENUOPT1,MENUOPT2,MENUOPT3
          STRIP     DIM22
          DISPLAY   *P53:2,*V2LON,"- ",*+,DIM22,SP1
          GOTO      OPTN9999
.
OPTN9100  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  KDAT0000                    Called by : ML2000   *
.*        Enter the date ranges                                      *
.*        Returns : EXIT = 1      exit chosen                        * 
.*                  DATESTRT      starting date  ccyymmdd            *
.*                  DATESTOP      ending date    ccyymmdd            *
.*                  DATE10A       starting date  dd/mm/ccyy          *
.*                  DATE10B       ending date    dd/mm/ccyy          *
.*********************************************************************
KDAT0000  DISPLAY   *P1:11,*EF:
                    *P1:12,"Starting date : ",*EL:
                    *P1:14,"Ending   date : ",*EL;
          MOVE      ONE,CCANLDTE            * blank cancel default
          MOVE      ONE,CDEFDTE             * enter once for default
          MOVE      ZERO,CHIGHLT            * use highlight
.
.         enter starting date
.
KDAT1000  MOVE      "12",CVERT              * row
          MOVE      "17",CCOL               * column
          UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9000          * nothing entered
          BRANCH    CQUEST,KDAT1000
.
          PACK      DATESTRT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATESTRT           * set up start date
          CALL      PACDATE
          MOVE      CPCDATE,DATE10A         * formatted start date
.
          MATCH     DATESTRT,CURRDATE
          GOTO      KDAT2000 IF NOT LESS    * date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT1000
.
.         enter ending date (default to starting date)
.
KDAT2000  MOVE      "14",CVERT              * row
          MOVE      "17",CCOL               * column
          UNPACK    DATESTRT,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9000          * nothing entered
          BRANCH    CQUEST,KDAT2000
.
          PACK      DATESTOP,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATESTOP           * set up end date
          CALL      PACDATE
          MOVE      CPCDATE,DATE10B         * formatted end date
.
          MATCH     DATESTRT,DATESTOP
          GOTO      KDAT3000 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT2000
.
.         check end date is before current date
.
KDAT3000  MATCH     DATESTOP,CURRDATE
          GOTO      KDAT5000 IF NOT LESS    * date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT2000
.
KDAT5000  CALL      CONS0000
          BRANCH    EXIT,KDAT0000,KDAT0000  * nothing entered
.
          CALL      CONTQST
          BRANCH    CEXIT,KDAT8000,KDAT0000,KDAT9000
.                         Yes      No       Cancel
.
KDAT8000  MOVE      ZERO,EXIT               * valid dates
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT                * exit selected
.
KDAT9999  RETURN
+
.*********************************************************************
.*                  CONS0000                    Called by : KDAT5000 *
.*        Enter the counslutant                                      *
.*********************************************************************
CONS0000  COMPARE   THREE,MOPTION
          GOTO      CONS9000 IF NOT EQUAL   * dont enter
.
          DISPLAY   *P1:16,"Consultant : "
          MOVE      "16",EVERT
          MOVE      "14",ECOL
          MOVE      ZERO,CKYIMAND           * not mand
          MOVE      SP6,CKYIDEF6            * no def
.
          CALL      PATDOCKY
          BRANCH    EXIT,CONS9999:          * nothing
                         CONS9999           * exitchar
.
          MOVE      DOCCODE,CODECONS        * code
          MOVE      DOCCODE,KEY6
          CALL      FDOC0000
          DISPLAY   *P14:16,*V2LON,CODECONS,*HOFF,SP2,DESCCONS
.
CONS9000  MOVE      ZERO,EXIT
.
CONS9999  RETURN
+
.*********************************************************************
.*                  DATA0000                    Called by : ML1000   *
.*        Produce the data for the report                            *
.*********************************************************************
DATA0000  DISPLAY   *P1:24,"Processing data - ":
                    *V2LON,*BLINKON,"Please wait",*EL;
          CALL      RDSF0000                * positional read on outrf1af
.
DATA1000  CALL      RDKF0000                * next read on outrf1af
          BRANCH    OVRCD,DATA9999          * have collected all the stats
.
          MATCH     DATESTRT,OTRLDATE
          GOTO      DATA1000 IF LESS        * before start date
.
          COMPARE   ZERO,OTRLTREF
          GOTO      DATA1000 IF NOT EQUAL   * Not Outpatient Referral
.
          BRANCH    MOPTION,DATA2000        * to include every priority
.
          MATCH     OTRLDATE,DATESTOP
          GOTO      DATA9999 IF LESS        * past end date
          GOTO      DATA3000
.
DATA2000  MATCH     OTRLDATE,DATESTOP
          GOTO      DATA1000 IF LESS
.
DATA3000  DISPLAY   *P50:24,OTRLOUTN,SP1,OTRLDATE
.
          MATCH     SP6,OTRLBSIT
          GOTO      DATA1000 IF NOT EQUAL   * letter is booked
.
          CALL      CDAY0000                * determine days waiting
.
.         update all temp file values
.
          PACK      KEY9,OTRLPRIO,OTRLCONS  * priority/consultant total
          CALL      UTMP0000
          PACK      KEY9,OTRLPRIO,ZED6      * priority total
          CALL      UTMP0000
          PACK      KEY9,ZED3,OTRLCONS      * consultant total
          CALL      UTMP0000
          PACK      KEY9,ZED3,ZED6          * grand total
          CALL      UTMP0000
          GOTO      DATA1000
.
DATA9999  RETURN
+
.*********************************************************************
.*                  REPT0000                    Called by : ML2000   *
.*        Produce the report from the data on temp file              *
.*********************************************************************
REPT0000  MOVE      ZERO,CPAGENO
          DISPLAY   *P1:24,"Printing report - ":
                    *V2LON,*BLINKON,"Please wait",*EL;
          CLOCK     TIME,CTIMEIS
          CALL      HEAD0000  
          MOVE      SP6,CURRCONS            * clear current consultant
.
          MOVE      SP9,KEY9                * position on both indexs
          IF        MOPTION = ONE
            READ      TEMP1,KEY9;;            * priority/cons
          ELSE
            READ      TEMP2,KEY9;;            * cons/priority
          ENDIF
.
.         loop over temp file
.
REPT1000  IF        MOPTION = ONE
            READKS    TEMP1;XPRTY,XCONS,XRNG1,XRNG2,XRNG3:
                            XRNG4,XRNG5,XRNG6,XRNG7,XTOTL
          ELSE
            READKS    TEMP2;XPRTY,XCONS,XRNG1,XRNG2,XRNG3:
                            XRNG4,XRNG5,XRNG6,XRNG7,XTOTL
          ENDIF
          GOTO      REPT9000 IF OVER
.
          DISPLAY   *P50:24,XPRTY,SP1,XCONS
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * print new page
.
          PACK      KEY9,XPRTY,XCONS
          MATCH     ZED9,KEY9
          GOTO      REPT8000 IF EQUAL       * print grand total
.
          PERFORM   MOPTION,OPTA0000,OPTB0000,OPTC0000
          GOTO      REPT1000
.
.         print grand total
.
REPT8000  PRINT     *1,"-------------------- ----  ----  ----  ----  ":
                    "----   ----   ----  -----"
          PRINT     *1,"GRAND TOTAL",*22,XRNG1,SP2,XRNG2,SP2,XRNG3:
                    SP2,XRNG4,SP2,XRNG5,SP3,XRNG6,SP3,XRNG7,SP2,XTOTL
          PRINT     *1,"-------------------- ----  ----  ----  ----  ":
                    "----   ----   ----  -----"
.
REPT9000  PRINT     *N,"*** End of Report ***",*N
          CALL      DTMP0000                * delete temp file
.
REPT9999  RETURN
+
.*********************************************************************
.*                  RDSF0000                    Called by : DATA0000 *
.*        Perform the required positional read on outrf1af file      *
.*********************************************************************
RDSF0000  CALL      CTMP0000
          BRANCH    MOPTION,RDSF1000,RDSF2000,RDSF3000
.
RDSF1000  MOVE      SP20,KEY19
          CALL      RSOTRFL3
          MOVE      "- All letters",CPHDROPT
          GOTO      RDSF9999
.
RDSF2000  MOVE      SP30,KEY22
          CALL      RSOTRFL5
          MOVE      "- All letters/Cons",CPHDROPT
          GOTO      RDSF9999
.
RDSF3000  PACK      KEY22,CODECONS,DATESTRT,SP10
          CALL      RSOTRFL5
          MOVE      "- Single Consult. ",CPHDROPT
.
RDSF9999  RETURN
+
.*********************************************************************
.*                  RDKF0000                    Called by : DATA1000 *
.*        Perform the next read on outrf1af file                     *
.*********************************************************************
RDKF0000  BRANCH    MOPTION,RDKF1000,RDKF2000,RDKF3000
.
RDKF1000  CALL      RKOTRFL3
          GOTO      RDKF9999
.
RDKF2000  CALL      RKOTRFL5
          GOTO      RDKF9999
.
RDKF3000  CALL      RKOTRFL5
          BRANCH    OVRCD,RDKF9999
.
          MATCH     CODECONS,OTRLCONS
          GOTO      RDKF9999 IF EQUAL       * save cons
          MOVE      ONE,OVRCD               * set as end of search
.
RDKF9999  RETURN
+
.********************************************************************
.*                  CDAY0000                   Called by : DATA1000 *
.*        Get the number of days between two dates                  *
.*        Returns : DAYS          days between two dates            *
.********************************************************************
CDAY0000  UNPACK    OTRLDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CDAY,DD                 * set up starting day
          MOVE      CMON,MM                 *                 month
          MOVE      CYEAR,YY                *                 year
          MOVE      CCENT,CC                *                 century
.
          CALL      DMYCON                  * julian day
          MOVE      JULDAY,STRTDAY          * starting julian day
          MOVE      JULYR,STRTYR            * starting julian year
          MOVE      JULCC,STRTCENT          * starting julian century
          MOVE      LEAPYR,STRTLEAP         * starting leap year flag
.
.         turn ending date into julian days
.
          UNPACK    DATESTOP,CCENT,CYEAR,CMON,CDAY
          MOVE      CDAY,DD                 * set up currrent day
          MOVE      CMON,MM                 *                 month
          MOVE      CYEAR,YY                *                 year
          MOVE      CCENT,CC                *                 century
.
          CALL      DMYCON
          MOVE      JULDAY,ENDDAY           * ending julian day
          MOVE      JULYR,ENDYR             * ending julian year
          MOVE      JULCC,ENDCENT           * ending julian century
          MOVE      LEAPYR,ENDLEAP          * ending leap year
.
          MOVE      ZERO,DAYS               * clear the day difference
.
.         if the current and start years are the same, dont worry about 
.         allowing for current date being in the next year
.
          COMPARE   ENDYR,STRTYR            * check for the same year
          GOTO      CDAY2000 IF EQUAL       * same year - just sub dates
.
.         ending year after the starting year
.         idea is to add 365/366 to the starting julian day
.         to make it eg, the 369th day of the previous year
.         for the 4th of january
.
          MOVE      ENDYR,DAYS              * ending year
          SUB       STRTYR,DAYS             * get # years b/w dates
          MULT      "365",DAYS              * get # days b/w years of dates
          ADD       STRTLEAP,DAYS           * add the leap year days
          
CDAY2000  ADD       ENDDAY,DAYS             * days of end date
          SUB       STRTDAY,DAYS            * - days of start date
.
CDAY9999  RETURN
+
.*********************************************************************
.*                  UTMP0000                    Called by : DATA1000 *
.*        Update the temp file                                       *
.*        Returns : KEY9          key for temp file                  *
.*                  DAYS          the number of days                 *
.*********************************************************************
UTMP0000  MOVE      ZERO,XRNG1
          MOVE      ZERO,XRNG2
          MOVE      ZERO,XRNG3
          MOVE      ZERO,XRNG4
          MOVE      ZERO,XRNG5
          MOVE      ZERO,XRNG6
          MOVE      ZERO,XRNG7
          MOVE      ZERO,XTOTL
.
          MOVE      ZERO,OVRCD
          READ      TEMP1,KEY9;XPRTY,XCONS,XRNG1,XRNG2,XRNG3:
                               XRNG4,XRNG5,XRNG6,XRNG7,XTOTL
          CALL      OVERCOND IF OVER
.
.         see which day range the day amount fits in
.
          COMPARE   "14",DAYS
          GOTO      UTMP1000 IF NOT LESS    * >= 14
.
          ADD       ONE,XRNG1               * add to range 1
          GOTO      UTMP9000
.
UTMP1000  COMPARE   "31",DAYS
          GOTO      UTMP2000 IF NOT LESS    * >= 31
.
          ADD       ONE,XRNG2               * add to range 2
          GOTO      UTMP9000
.
UTMP2000  COMPARE   "61",DAYS
          GOTO      UTMP3000 IF NOT LESS    * >= 61
.
          ADD       ONE,XRNG3               * add to range 3
          GOTO      UTMP9000
.
UTMP3000  COMPARE   "91",DAYS
          GOTO      UTMP4000 IF NOT LESS    * >= 91
.
          ADD       ONE,XRNG4               * add to range 4
          GOTO      UTMP9000
.
UTMP4000  COMPARE   "121",DAYS
          GOTO      UTMP5000 IF NOT LESS    * >= 121
.
          ADD       ONE,XRNG5               * add to range 5
          GOTO      UTMP9000
.
UTMP5000  COMPARE   "181",DAYS
          GOTO      UTMP6000 IF NOT LESS    * >= 181
.
          ADD       ONE,XRNG6               * add to range 6
          GOTO      UTMP9000
.
UTMP6000  ADD       ONE,XRNG7               * add to range 7
.
UTMP9000  ADD       ONE,XTOTL               * add to total
.
          UNPACK    KEY9,XPRTY,XCONS
          IF        OVRCD = ONE
            WRITE     TEMP1,KEY9;XPRTY,XCONS,XRNG1,XRNG2,XRNG3:
                                 XRNG4,XRNG5,XRNG6,XRNG7,XTOTL
          ELSE
            UPDATE    TEMP1;XPRTY,XCONS,XRNG1,XRNG2,XRNG3:
                            XRNG4,XRNG5,XRNG6,XRNG7,XTOTL
          ENDIF
.
UTMP9999  RETURN
+
.*********************************************************************
.*                  CTMP0000                    Called by : RDSF0000 *
.*        Create a temp file                                         *
.*********************************************************************
CTMP0000  CALL      DTMP0000                * delete temp file
.
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAME,KEY,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMP1,CFNAME            * open the temp file
          OPEN      TEMP2,CFNAME            * open the temp file
.
CTMP9999  RETURN
+
.*********************************************************************
.*                  DTMP0000                    Called by : CTMP0000 *
.*        Delete temp file A                                REPT0000 *
.*********************************************************************
DTMP0000  CLOSE     TEMP1                   * close temp file
          CLOSE     TEMP2                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAME  * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
.
DTMP9999  RETURN
+
.*********************************************************************
.*                  HEAD0000                    Called by : REPT0000 *
.*        Print new page header                                      *
.*********************************************************************
HEAD0000  CALL      HEAD80
          PRINT     *20,"For Period : ",DATE10A," to ",DATE10B
.
          BRANCH    MOPTION,HEAD5000,HEAD9999,HEAD3000
.
HEAD3000  PRINT     *20,"Consultant : ",CODECONS,SP2,DESCCONS
          ADD       ONE,CLNO
.
HEAD5000  PRINT     *N,"Priority",*22,"<14  14-30 31-60 61-90 91-120 121-180":
                    " >180  Total"
          PRINT     *1,"-------------------- ---- ----- ----- ----- ":
                    "------ ------- ----  -----"
          ADD       "3",CLNO
.
HEAD9999  RETURN
+
.*********************************************************************
.*                  OPTA0000                    Called by : REPT1000 *
.*        Report for Option 1                                        *
.*********************************************************************
OPTA0000  MATCH     ZED6,XCONS
          GOTO      OPTA9999 IF NOT EQUAL   * not a priority total
.
          MOVE      "No Priority",TDESC
          MATCH     SP3,XPRTY
          IF        !@EQUAL
            MOVE      "Invalid Code",TDESC
            PACK      KEY5,CATPR,XPRTY
            CALL      RDCODE1
          ENDIF
          PRINT     *1,TDESC,SP1,XRNG1,SP2,XRNG2,SP2,XRNG3:
                    SP2,XRNG4,SP2,XRNG5,SP3,XRNG6,SP3,XRNG7,SP2,XTOTL
          ADD       ONE,CLNO
.
OPTA9999  RETURN
+
.*********************************************************************
.*                  OPTB0000                    Called by : REPT1000 *
.*        Report for Option 2                                        *
.*********************************************************************
OPTB0000  MATCH     ZED6,XCONS
          GOTO      OPTB9999 IF EQUAL       * dont print priority total
.
          MATCH     CURRCONS,XCONS
          GOTO      OPTB2000 IF EQUAL       * same consultant
.
          MOVE      XCONS,KEY6
          CALL      FDOC0000                * format name
          PRINT     *N,"Consultant : ",XCONS,SP1,DESCCONS
          PRINT     "Priority",*22,"<14  14-30 31-60 61-90 91-120 121-180":
                    " >180  Total"
          PRINT     "-------------------- ---- ----- ----- ----- ":
                    "------ ------- ----  -----"
          ADD       "3",CLNO
          MOVE      XCONS,CURRCONS
.
OPTB2000  MATCH     ZED3,XPRTY
          IF        @EQUAL
            MOVE      "     TOTAL",TDESC      * default to total value
            PRINT     "-------------------- ---- ----- ----- ----- ":
                      "------ ------- ----  -----"
            ADD       "1",CLNO
          ELSE
            MOVE      "No Priority",TDESC
            MATCH     SP3,XPRTY
            IF        !@EQUAL
              MOVE      "Invalid Code",TDESC
              PACK      KEY5,CATPR,XPRTY
              CALL      RDCODE1
            ENDIF
          ENDIF
.
          PRINT     *1,TDESC,SP1,XRNG1,SP2,XRNG2,SP2,XRNG3:
                    SP2,XRNG4,SP2,XRNG5,SP3,XRNG6,SP3,XRNG7,SP2,XTOTL
          ADD       "1",CLNO
.
OPTB9999  RETURN
+
.*********************************************************************
.*                  OPTC0000                    Called by : REPT1000 *
.*        Report for Option 3                                        *
.*********************************************************************
OPTC0000  MATCH     ZED3,XPRTY
          GOTO      OPTC9999 IF EQUAL       * dont want total for cons.
          MATCH     ZED6,XCONS
          GOTO      OPTC9999 IF EQUAL       * dont want total for priority
.
          MOVE      "No Priority",TDESC
          MATCH     SP3,XPRTY
          IF        !@EQUAL
            MOVE      "Invalid Code",TDESC
            PACK      KEY5,CATPR,XPRTY
            CALL      RDCODE1
          ENDIF
.
          PRINT     *1,TDESC,SP1,XRNG1,SP2,XRNG2,SP2,XRNG3:
                    SP2,XRNG4,SP2,XRNG5,SP3,XRNG6,SP3,XRNG7,SP2,XTOTL
          ADD       ONE,CLNO
.
OPTC9999  RETURN
+
.*********************************************************************
.*                  FDOC0000                    Called by : xxxx0000 *
.*        Format the doctor name                                     *
.*********************************************************************
FDOC0000  PACK      DESCCONS,SP30,SP30
          CALL      RDDOCT1
          BRANCH    OVRCD,FDOC9999
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DESCCONS       * name
.
FDOC9999  RETURN
+
.
.         I/O includes
.
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       STD001IO/PBL
          INC       OUTRF1IO/INC            * referral letters
          INC       PATDOCKY/PBL            * ? for doctors file
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       WEBERRIO/INC
..............................................................................

.******************************************************************************
.* System   : WAITING LIST                                                    *
.* Program  : IBAWAT22           (N.Z. Only)                                  *
.* Desc     : 4 Weekly Analysis of Movement of Surgical Waiting List          *
.******************************************************************************
.* Author   : K.Peak                                                          *
.* Date     : 10/11/1989                                                      *
.* Mods.    :                                                                 *
.*            V10.05.01 05/01/2015  Ebon Clements     CAR 261630              *
.*                      Removed port number from temp file name               *
.*            V5.07.B02 23/11/1998  Nicole Harrington                         *
.*                      507 changes                                           * 
.*            V5.01.02  09/05/90 DGP                SRF 104905                *
.*                      - Fix page breaking                                   *
.*                      - Don't print detail lines of zero values             *
.*                      - Swap Admitted & Added List columns around           *
.*            V5.01.03  04/09/90 i chung            SRF 106348                *
.*                      Fix page breaking problem                             *
.*            V5.01.04  30/10/90 i chung            SRF 106919                *
.*                      - Added "Forward Calling" footnote                    *
.*                      - Centered and capitalized option headings on report  *
.*                      - Pending Patients Only option : fixed PENDING STAT   *
.*                        CHANGE calculation                                  *
.*            V5.01.10  06/07/90 Bill Dew                                     *
.*                      Make changes for Wellingtion specifications - add     *
.*                      Pending Patients report.  This program 09/07/90 is    *
.*                      un-tested until WAT90 is modified to write data to    *
.*                      "watupcaf".                                           *
.*                      23/08/90 Bill Dew                                     *
.*                      Fix report to balance "Added to List" and "Pending    *
.*                      Stat Change".  Please read programmers notes.         *
.*                                                                            *
.*           ***************************************************************  *
.*           * --- Programmers Notes ---                                   *  *
.*           * 11/10/90 Bill Dew                                           *  *
.*           * This report will not balance because patients changed from  *  *
.*           * pending to a non pending priority code are not catered for. *  *
.*           * This is an easy fix, simply convert PTNP0000 to desired     *  *
.*           * alternative.                                                *  *
.*           ***************************************************************  *
.*            V5.01.11  09/11/90 i chung            SRF 106919                *
.*                      Changed Total column calculation - Last Period+Added  *
.*                      To List+Pending Stat Change-Deletions-Status Changes  *
.*            V5.01.12  14/11/90 i chung            SRF 106963                *
.*                      Changed category for removal reasons from "BC" to "WR *
.*            V5.01.13  30/11/90 i chung            SRF 107251                *
.*                      Added "Added To List" footnote for option 1           *
.*        V5.01.14  16/02/1993    Justin Coates  (DUDLEY 3.4)                 *
.*                  attemped to implement suspended patients                  *
.*                  which is replacing pending patients                       *
.*                  also allowed for status 7 and 8                           *
.*                  This has been one of the ugliest changes I've had to do ! *
.*        V5.01.15  25/02/1993    Steve O'Gorman                              *
.*                  Changed Version Number                                    *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC          * codes file
          INC       PATCOMM/INC
          INC       PATCONFD/INC          * control file
          INC       PATDO1FD/INC          * doctor file
          INC       PATDRGFD/INC          * period date range file
          INC       TFILEVAR/INC
          INC       WATCONFD/INC 
          INC       WATSCUFD/INC          * w/list unit/doctor suspensions file
          INC       WATUDCFD/INC          * w/list unit/doctor code file
          INC       WATUDSFD/INC          * w/list unit/doctor summary file
          INC       WATUPCFD/INC          * unit/doctor priority changes
          INC       WEBERRFD/INC
.
.         program variables
.
ANATMP    DIM       8       * temporary file index 1
.
BNBTMP    DIM       8       * temporary file index 2
.
CLNDESC   DIM       20      * clinic/unit description
CMDLINE   DIM       50
CODETP    INIT      "TP"    * category for priority
CODEWR    INIT      "WR"    * category for removal reason
CODEWU    INIT      "WU"    * category for unit
CSTPER    DIM       6       * ccyymm
CURPER    DIM       6       * current period
.
DELT1     FORM      7       * total for deletion 1
DELT2     FORM      7       * total for deletion 2
DELT3     FORM      7       * total for deletion 3
DELT4     FORM      7       * total for deletion 4
DELT5     FORM      7       * total for deletion 5
DELT6     FORM      7       * total for deletion 6
DELDESC   DIM       51      * deletion code description
DELDESC1  DIM       51      * deletion codes 1 description
DELDESC2  DIM       51      * deletion codes 2 description
DELDESC3  DIM       51      * deletion codes 3 description
DELDESC4  DIM       51      * deletion codes 4 description
DELDESC5  DIM       51      * deletion codes 5 description
DELDESC6  DIM       51      * deletion codes 6 description
DESCFLG   FORM      1       * description deletion codes flag
DOCDESC   DIM       20      * doctor description
.
ENDDATE   DIM       10      * enddate
.
FADMDTE   DIM       8       * ccyymmdd
.
GADDED    FORM      7       * number patients added to list g/total
GADMIT    FORM      7       * number patients admitted from list g/total
GDELET1   FORM      7       * number removed from list, type 1 g/total
GDELET2   FORM      7       * number removed from list, type 2 g/total
GDELET3   FORM      7       * number removed from list, type 3 g/total
GDELET4   FORM      7       * number removed from list, type 4 g/total
GDELET5   FORM      7       * number removed from list, type 5 g/total
GDELET6   FORM      7       * number removed from list, type 6 g/total
GLASTP    FORM      7       * number on list at start of period g/total
GPRTYD    FORM      7       * number of semi urgent priorities on list g/total
GPRTYR    FORM      7       * number of routine priorities on list g/total
GPRTYU    FORM      7       * number of urgent priorities on list g/total
GTOTAL    FORM      7       * total number pf patients on list g/total
.
INDC      FORM      1       * indicator for deletion code descriptions
.
PDATE     DIM       7        * period for printing
PRTFLG    FORM      1        * print routine flag
PRTYD     FORM      7        * number of semi urgent priorities
PRTYR     FORM      7        * number of routine priorities
PRTYU     FORM      7        * number of urgent priorities
PSUMFLG   FORM      1        * print summary data flag
.
SIXZ      INIT      "ZZZZZZ"
.
STATCHGE  FORM      8        * Pending Stat Change work variable
STATADD   FORM      8        * added to list work variable
STATADMT  FORM      8        * admitted from list work variable
STATLAST  FORM      8        * number on w/l last period
.
STRDATE   DIM       10       * start date
SUMMFLG   FORM      1        * summary page flag
PENSUSA   DIM       9        * suspended/pending description Lower case
PENSUSB   DIM       9        * suspended/pending description CAPITALS
.
TADMDTE   DIM       8        * ccyymmdd
TOTAL     FORM      7        * total number of patients on list
TOTDESC   DIM       20       * subtotal description
TOTHED    INIT      "Subtotal "
.
ANATMPXX  IFILE SQL, FIXED=75, KEY="u1-3,4-9"
BNBTMPXX  IFILE SQL, FIXED=75, KEY="u4-9,1-3"
.
.----------------------------------------------------------------------
.         Temporary File Variables
.
.Name     Type   Length   Physical   Start    Description
.----     ----   ------   --------   -----    -----------
XUNIT     DIM         3          3       1    Unit Code (cat WU)
XDOCT     DIM         6          6       4    Doctor Code or "ZZZZZZ"
XLASTP    FORM        7          5      10    Number on list at start of period
XADMIT    FORM        7          5      15    Number patients admitted from list
XADDED    FORM        7          5      20    Number patients added to list
XDELET1   FORM        7          5      25    Number removed form list, type 1
XDELET2   FORM        7          5      30    Number removed form list, type 2
XDELET3   FORM        7          5      35    Number removed form list, type 3
XDELET4   FORM        7          5      40    Number removed form list, type 4
XDELET5   FORM        7          5      45    Number removed form list, type 5
XDELET6   FORM        7          5      50    Number removed form list, type 6
XPRTYR    FORM        7          5      55    Number of routine priorities 
XPRTYD    FORM        7          5      60    Number of semi urgent priorities 
XPRTYU    FORM        7          5      65    Number of urgent priorities 
XTOTAL    FORM        7          5      70    Total number of patients on list
.
.End of record                          75
.
PRGID     INIT      "IBAWAT22"
PRGNAM    INIT      "4 Weekly Analysis Of Surgical Waiting List"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
ML0000    CALL      INIT0000        * initialisation
.
ML1000    CALL      OPTN0000         * select option
          BRANCH    OPTION,ML1500,ML1500
          GOTO      ML9000
.
ML1500    CALL      CLR0000         * clear variables
          CALL      DAT0000         * keyin period
          BRANCH    EXIT,ML1000
.
          CALL      SUMM0000        * summary page only y/n ?
          CALL      CONTQST         * ok to continue y/n ?
          BRANCH    CEXIT,ML2000,ML1500,ML1000
.
ML2000    CALL      CREA0000        * create temporary file 
          CALL      DSPS0000        * display screen       
.
          CALL      WATS0000        * process watudsfd data
          CALL      WATC0000        * process watudcfd data
          CALL      PRNT0000        * print detail data  
          CALL      PSUM0000        * print summary page  
          CALL      END0000         * end of report
          GOTO      ML1000
.
ML9000    CALL      KILL0000        * remove temporary file 
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialisation                                *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"watudcaf";
          OPEN      WATUDCA1,"watudcaf"
.
          DISPLAY   *P64:24,"watudsaf";
          OPEN      WATUDSA1,"watudsaf"
.
          DISPLAY   *P64:24,"watupcaf";
          OPEN      WATUPCA1,"watupcaf"
.
          DISPLAY   *P64:24,"watscuaf";
          OPEN      WATSCUA1,"watscuaf"
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,ANATMP
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,BNBTMP
.
          READ      CONTROLF,THIRTY7;*185,WTCNTPDS,*214,WTCNUSOP,WTCNWCOW
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
          STRIP     WTCNTPDS
          IF        WTCNUSOP = ONE
            MOVE      "Suspended",PENSUSA
            MOVE      "SUSPENDED",PENSUSB
          ELSE
            MOVE      "Pending  ",PENSUSA
            MOVE      "PENDING  ",PENSUSB
            STRIP     PENSUSA
            STRIP     PENSUSB
          ENDIF
.
INIT9999  RETURN
+
.**********************************************************************
.*                         CLR0000                                    *
.*                     Clear Variables                                *
.**********************************************************************
.
CLR0000   MOVE      ZERO,CPAGENO         * page counter
          MOVE      "60",CLNO            * line counter
.
          PACK      DELDESC,SP30,SP30    * deletion code
          PACK      DELDESC1,SP30,SP30   * deletion codes 1
          PACK      DELDESC2,SP30,SP30   * deletion codes 2
          PACK      DELDESC3,SP30,SP30   * deletion codes 3
          PACK      DELDESC4,SP30,SP30   * deletion codes 4
          PACK      DELDESC5,SP30,SP30   * deletion codes 5
          PACK      DELDESC6,SP30,SP30   * deletion codes 6
.
          MOVE      SP3,XUNIT     
          MOVE      SP6,XDOCT     
          MOVE      ZERO,XLASTP    
          MOVE      ZERO,XADMIT    
          MOVE      ZERO,XADDED    
          MOVE      ZERO,XDELET1  
          MOVE      ZERO,XDELET2 
          MOVE      ZERO,XDELET3
          MOVE      ZERO,XDELET4  
          MOVE      ZERO,XDELET5 
          MOVE      ZERO,XDELET6
          MOVE      ZERO,XPRTYR
          MOVE      ZERO,XPRTYD  
          MOVE      ZERO,XPRTYU 
          MOVE      ZERO,XTOTAL
.
          MOVE      ZERO,GLASTP    
          MOVE      ZERO,GADMIT   
          MOVE      ZERO,GADDED  
          MOVE      ZERO,GDELET1
          MOVE      ZERO,GDELET2  
          MOVE      ZERO,GDELET3 
          MOVE      ZERO,GDELET4
          MOVE      ZERO,GDELET5  
          MOVE      ZERO,GDELET6 
          MOVE      ZERO,GPRTYR 
          MOVE      ZERO,GPRTYD
          MOVE      ZERO,GPRTYU 
          MOVE      ZERO,GTOTAL
.
          RETURN
+
.**********************************************************************
.*                         OPTN0000                                    *
.*                      Main Option Screen                            *
.**********************************************************************
OPTN0000  
          HLINE     *G37,2,60,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Non-",*+,PENSUSA:
                    " Patients Only":
                    *P1:6,*V2LON,TWO,*HOFF," = ",*+,PENSUSA," Patients Only"
.
OPTN1000  KEYIN     *P1:8,*EL,"Select Option : _",*P17:8,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
.
          DISPLAY   *P17:8,*V2LON,OPTION
          BRANCH    OPTION,OPTN2222,OPTN3333
          BEEP
          GOTO      OPTN1000
.
OPTN2222  DISPLAY   *P60:2,*V2LON," Non-",*+,PENSUSA
          GOTO      OPTN9999
.
OPTN3333  DISPLAY   *P60:2,*V2LON," ",*+,PENSUSA," Pats "
.
OPTN9999  RETURN
+
.**********************************************************************
.*                         DAT0000                                    *
.*                       Keyin Period                                 *
.**********************************************************************
DAT0000   DISPLAY   *P1:10,*EF,"Enter the ",CPERMTH," of the report : "
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,DAT2000
.
          PACK      CURPER,DRGYR,DRGNUM
.
.         Find previous period
.
          PACK      KEY6,CURPER
          CALL      RDDRGA1
          CALL      RDPDRGA1
          PACK      CSTPER,DRGCYR,DRGCNUM
.
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT       * 0 = highlight 1 = no highlight
          MOVE      "34",CCOL          * column for keyin
          MOVE      TEN,CVERT          * row for keyin
          CALL      KEYPER
          BRANCH    OVRCD,DAT0000      * no date keyed in
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DAT3000
          PACK      PDATE,CMON,SLASH,CCENT,CYEAR
.
.         Pack appropriate start dates
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,STRDATE
          PACK      FADMDTE,CCENT,CYEAR,CMON,CDAY
.
.         Pack appropriate end dates
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENDDATE
          PACK      TADMDTE,CCENT,CYEAR,CMON,CDAY
.
          PACK      CSTPER,DRGYR,DRGNUM
.
          DISPLAY   *P43:10,*EL,DRGLDSC:
                    *P43:11,*EL,STRDATE," to ",ENDDATE
          GOTO      DAT8000
.
.         No current period on file
.
DAT2000   DISPLAY   *P1:24,*EL,*B:
                    "Error: Current date not found in Period file.  ";
          CALL      HITENTER
          GOTO      DAT9000
.
.         Invalid date entered
.
DAT3000   KEYIN     *P1:24,*EL,*B,"Invalid Date.  ";
          CALL      HITENTER
          GOTO      DAT0000
.
DAT8000   MOVE      FALSE,EXIT
          GOTO      DAT9999
.
DAT9000   MOVE      TRUE,EXIT
.
DAT9999   RETURN
+
.**********************************************************************
.*                         SUMM0000                                   *
.*                    Summary Page Y/N ?                              *
.**********************************************************************
SUMM0000  MOVE      ZERO,SUMMFLG
.
          DISPLAY   *P1:13,*EL:
                    "Do you want the summary page only (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ";
.
SUMM1000  KEYIN     *P43:13,*EL,*DV,UNDLN1,*P43:13,*V2LON,ANS;
.
          RESET     ANS
          GOTO      SUMM2000 IF EOS
          REPLACE   UPPLOW,ANS
          DISPLAY   *P43:13,*V2LON,ANS
. 
          MATCH     ANSY,ANS
          GOTO      SUMM3000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      SUMM9999 IF EQUAL
.
SUMM2000  BEEP     
          GOTO      SUMM1000
.
SUMM3000  MOVE      ONE,SUMMFLG       * summary page only required
.
SUMM9999  RETURN
+
.**********************************************************************
.*                         DSPS0000                                   *
.*                       Display Screen                               *
.**********************************************************************
DSPS0000  DISPLAY   *P1:24,*EL,*P20:24:
                    "Unit : ",*P48:24,"Scanning : "
          RETURN
+
.**********************************************************************
.*                         WATS0000                                   *
.*                   Process WATUDSFD Data                            *
.*   Loop through the unit/doctor summary file and put the data       *
.*   found into the temporary file.       Modified 19/7/90 Bill Dew   *
.**********************************************************************
WATS0000  PACK       KEY18,CSTPER,SP20
          CALL       RDSUDSA1
.
WATS2222  CALL       RDKUDSA1
          BRANCH     OVRCD,WATS9999
.
          DISPLAY   *P59:24,WTUDUNIT;
.
          MATCH     WTUDDATE,CSTPER    
          GOTO      WATS9999 IF NOT EQUAL
.
          PACK      KEY5,CODEWU,WTUDUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,WATS2222
.
          MATCH     ANSS,TCINDC1
          GOTO      WATS2222 IF NOT EQUAL   * not a surgical unit
.
          BRANCH    WTCNUSOP,WATS55555      * dont care about priority for susp.
.
          PACK      KEY5,CODETP,WTUDPRTY,SP5
          CALL      RDCODE1                 * get 2nd indicator 
          BRANCH    OVRCD,WATS2222
.
          BRANCH    OPTION,WATS3333,WATS4444
.
.         option 1 : non-pending
.
WATS3333  MATCH     ANSP,TCINDC2
          GOTO      WATS2222 IF EQUAL       * not a non-pending priority
          GOTO      WATS5555
.
.         option 2 : pending
.
WATS4444  MATCH     ANSP,TCINDC2
          GOTO      WATS2222 IF NOT EQUAL   * not a pending priority
.
.         process the data
.
WATS5555  IF        WTCNUSOP = ONE
            CALL      WTSCA000                * get susp/non-susp off watscu
          ELSE
            CALL      PTNP0000                * get pend/non-pend off watupc
          ENDIF
          CALL      CATP0000                * categorize Priority status
          CALL      CUDS0000                * write data to temp files
          MOVE      SIXZ,WTUDDOCT     
          CALL      CUDS0000                * write sub totals to temp files
          GOTO      WATS2222
.
WATS9999  RETURN
+
.*******************************************************************************
.*                                PTNP0000                                     *
.*   Created by Bill Dew 18/7/90                                               *
.*   Pending to non-pending patients : read watupcaf for status changes.       *
.*   The number of pending to non-pending are added directly to the WTUDADD of *
.*   watudsaf.  This is the "Added To" column and ONLY applies to option 1     *
.*   (Non-Pending Patients Only).  Furthermore the new priority code must      *
.*   equal the priority code .                                                 *
.*                                                                             *
.*   In option 2 (Pending Patients Only) the "admitted patients" column is     *
.*   changed to the number of pending patients changed from non-pending to     *
.*   pending priority code.  The routine will simply calculate this value and  *
.*   move it into WTUDADMT.                                                    *
.*******************************************************************************
PTNP0000  BRANCH    OPTION,PTNP1111,PTNP4444
.
.         Option 1 - Non-Pending
.
PTNP1111  PACK      KEY21,CSTPER,WTUDUNIT,WTUDDOCT,SP30
          CALL      RSWTUPC1
.
PTNP2222  CALL      RKWTUPC1
          BRANCH    OVRCD,PTNP9999
.
          MATCH     CSTPER,WTUPDATE      * test same period
          GOTO      PTNP9999 IF NOT EQUAL
.
          MATCH     WTUDUNIT,WTUPUNIT       * test same unit
          GOTO      PTNP9999 IF NOT EQUAL
.
          MATCH     WTUDDOCT,WTUPDOCT       * test same doctor
          GOTO      PTNP9999 IF NOT EQUAL
.
          MATCH     SP3,WTUPOPTY         * test if any old code
          GOTO      PTNP2222 IF EQUAL
.
          MATCH     WTUPNPTY,WTUDPRTY    * test priority watudsaf = New priority
          GOTO      PTNP2222 IF NOT EQUAL
.
          PACK      KEY5,CODETP,WTUPOPTY
          CALL      RDCODE1
          BRANCH    OVRCD,PTNP2222
          MATCH     ANSP,TCINDC2         * must be pending status
          GOTO      PTNP2222 IF NOT EQUAL
.
          PACK      KEY5,CODETP,WTUPNPTY
          CALL      RDCODE1
          BRANCH    OVRCD,PTNP2222
          MATCH     ANSP,TCINDC2         * must be non-pending status
          GOTO      PTNP2222 IF EQUAL
.
          ADD       WTUPNUMB,WTUDADD     * pending to non-pending
          GOTO      PTNP2222
.
.         Option 2 - Pending
.
PTNP4444  MOVE      ZERO,STATCHGE
.
          PACK      KEY21,CSTPER,WTUDUNIT,WTUDDOCT,SP30
          CALL      RSWTUPC1
.
PTNP5555  CALL      RKWTUPC1
          BRANCH    OVRCD,PTNP6666
.
          MATCH     CSTPER,WTUPDATE         * test same period
          GOTO      PTNP6666 IF NOT EQUAL
.
          MATCH     WTUDUNIT,WTUPUNIT       * test same unit
          GOTO      PTNP6666 IF NOT EQUAL
.
          MATCH     WTUDDOCT,WTUPDOCT       * test same doctor
          GOTO      PTNP6666 IF NOT EQUAL
.
          MATCH     SP3,WTUPOPTY            * test if any "old" code is blank
          GOTO      PTNP5555 IF EQUAL
.
          PACK      KEY5,CODETP,WTUPOPTY
          CALL      RDCODE1
          BRANCH    OVRCD,PTNP5555
          MATCH     ANSP,TCINDC2            * must be non-pending status
          GOTO      PTNP5555 IF EQUAL
.
          PACK      KEY5,CODETP,WTUPNPTY
          CALL      RDCODE1
          BRANCH    OVRCD,PTNP5555
          MATCH     ANSP,TCINDC2            * must be pending status
          GOTO      PTNP5555 IF NOT EQUAL
.
          ADD       WTUPNUMB,STATCHGE       * non-pending to pending
          GOTO      PTNP5555
.
PTNP6666  COMPARE   WTUDADMT,STATCHGE
          GOTO      PTNP9999 IF EQUAL
          MOVE      STATCHGE,WTUDADMT
.
PTNP9999  RETURN
+
.*******************************************************************************
.*                             CATP0000                                        *
.*                Categorize Priority status                                   *
.*******************************************************************************
CATP0000  DISPLAY   *P27:24,*V2LON,WTUDUNIT;
.
          MOVE      ZERO,PRTYR          * no. of routine priorities on list
          MOVE      ZERO,PRTYD          * no. of semi urgent priorities on list
          MOVE      ZERO,PRTYU          * no. of urgent priorities on list
          MOVE      ZERO,TOTAL          * total no. of patients on list   
.
          BRANCH    OPTION,CATP1000,CATP2000
.
. *******  option 1 : non-pending / non-suspended *******
.
CATP1000  CALL      GUSPC000                * get # Un-susp. pats
          CALL      NPCAT000                * categorise priority 
          GOTO      CATP9999
.
. ******* option 2 : pending / suspended *******
.
CATP2000  IF        WTCNUSOP = ONE
            CALL      WTSCB000                * suspended patients (watscuaf)
          ELSE
            CALL      WTUPB000                * pending patients (watupcaf)
          ENDIF
.
CATP9999  RETURN
+
.*******************************************************************************
.*                         CUDS0000                                            *
.*                Write watudsaf data to Temporary File                        *
.*                                                                             *
.*   The addtion of priority codes was changed from CUDC0000 to here.          *
.*   If first indicator is not (U)rgent (S)emi-urgent then it will be classed  *
.*   as Routine.                               Modified 20/7/90 Bill Dew       *
.*******************************************************************************
CUDS0000  PACK      KEY9,WTUDUNIT,WTUDDOCT
          CALL      RDTMP1
          BRANCH    OVRCD,CUDS2000
.
          ADD       WTUDLAST,XLASTP
          ADD       WTUDADD,XADDED
          ADD       WTUDADMT,XADMIT
          ADD       PRTYD,XPRTYD
          ADD       PRTYR,XPRTYR
          ADD       PRTYU,XPRTYU
.
          BRANCH    OPTION,CUDS1000
.
.         For Option 2 (Pending) Last Period adds Added To List adds Pending
.         Stat Change and then subtracts Status Changes
.
          MOVE      WTUDLAST,TOTAL
          ADD       WTUDADD,TOTAL
          ADD       WTUDADMT,TOTAL
          SUB       PRTYD,TOTAL
          SUB       PRTYR,TOTAL
          SUB       PRTYU,TOTAL
.
CUDS1000  ADD       TOTAL,XTOTAL
          CALL      UPTMP1
          GOTO      CUDS9999
.
CUDS2000  MOVE      WTUDUNIT,XUNIT
          MOVE      WTUDDOCT,XDOCT
          MOVE      WTUDLAST,XLASTP
          MOVE      WTUDADD,XADDED
          MOVE      WTUDADMT,XADMIT
          MOVE      PRTYD,XPRTYD
          MOVE      PRTYR,XPRTYR
          MOVE      PRTYU,XPRTYU
.
          BRANCH    OPTION,CUDS3000
          MOVE      WTUDLAST,TOTAL
          ADD       WTUDADD,TOTAL
          ADD       WTUDADMT,TOTAL
          SUB       PRTYD,TOTAL
          SUB       PRTYR,TOTAL
          SUB       PRTYU,TOTAL
.
CUDS3000  MOVE      TOTAL,XTOTAL
          CALL      WRTMP1
.
CUDS9999  RETURN
+
.**********************************************************************
.*                         WATC0000                                   *
.*                   Process WATUDCFD Data                            *
.**********************************************************************
WATC0000  PACK      KEY21,CSTPER,SP20
          CALL      RDSUDCA1
.
WATC1000  CALL      RDKUDCA1
          BRANCH    OVRCD,WATC9999
.
          DISPLAY   *P59:24,*EL,WTUCUNIT;
.
          MATCH     WTUCDATE,CSTPER    
          GOTO      WATC9999 IF NOT EQUAL
.
          PACK      KEY5,CODETP,WTUCPRTY
          CALL      RDCODE1                  * get 2nd indicator 
          BRANCH    OVRCD,WATC1000
.
. if we are using suspensions, only non-pending/unsuspended patients can be
. deleted so if we are using suspensions, set all priorities to NON-PENDING
.
          LOAD      TCINDC2,WTCNUSOP,SP1    * using suspensions so set as NP
.
          BRANCH    OPTION,WATC3333,WATC4444
.
. ******* option 1 : non-pending *******
.
WATC3333  MATCH     ANSP,TCINDC2             * test for pending status P
          GOTO      WATC1000 IF EQUAL        * ignore this record
          GOTO      WATC5555
.
. ******* option 2 : pending *******
.
WATC4444  MATCH     ANSP,TCINDC2             * test for pending status P
          GOTO      WATC1000 IF NOT EQUAL    * ignore this record not equal
.
WATC5555  CALL      CUDC0000                 * write data to temp files
          MOVE      SIXZ,WTUCDOCT
          CALL      CUDC0000                 * write sub totals to temp files
          GOTO      WATC1000
.
WATC9999  RETURN
+
.**********************************************************************
.*                         CUDC0000                                   *
.*                Write watudcaf data to Temporary File               *
.**********************************************************************
CUDC0000  PACK      KEY5,CODEWU,WTUCUNIT
          CALL      RDCODE1
.
.         Check whether the unit is a surgical unit, as we only 
.         want surgical units
.
          MATCH     "S",TCINDC1
          GOTO      CUDC9999 IF NOT EQUAL
.
          DISPLAY   *P27:24,*V2LON,WTUCUNIT;
.
          MOVE      ZERO,DELT1          * no. removed from list, type 1
          MOVE      ZERO,DELT2          * no. removed from list, type 2
          MOVE      ZERO,DELT3          * no. removed from list, type 3
          MOVE      ZERO,DELT4          * no. removed from list, type 4
          MOVE      ZERO,DELT5          * no. removed from list, type 5
          MOVE      ZERO,DELT6          * no. removed from list, type 6
.
.         Cancellation code
.
          MOVE      SP1,TCINDC1
          PACK      KEY5,CODEWR,WTUCCCOD
          CALL      RDCODE1
.
.         Branch to appropriate deletion code
.
          REP       " 606768696",TCINDC1
          MOVE      TCINDC1,FORM1
.
.         Add one to the correct deletion code
.
          STORE     WTUCNUMB,FORM1,DELT1,DELT2,DELT3,DELT4,DELT5,DELT6
.
          PACK      KEY9,WTUCUNIT,WTUCDOCT
          CALL      RDTMP1
          BRANCH    OVRCD,CUDC4000
.
          ADD       DELT1,XDELET1
          ADD       DELT2,XDELET2
          ADD       DELT3,XDELET3
          ADD       DELT4,XDELET4
          ADD       DELT5,XDELET5
          ADD       DELT6,XDELET6
.
          BRANCH    OPTION,CUDC3000
.
.         For Option 2 (Pending) deletions are being subtracted
.
          SUB       XDELET1,XTOTAL
          SUB       XDELET2,XTOTAL
          SUB       XDELET3,XTOTAL
          SUB       XDELET4,XTOTAL
          SUB       XDELET5,XTOTAL
          SUB       XDELET6,XTOTAL
.
CUDC3000  CALL      UPTMP1
          GOTO      CUDC9999
.
CUDC4000  MOVE      WTUCUNIT,XUNIT
          MOVE      WTUCDOCT,XDOCT
          MOVE      DELT1,XDELET1
          MOVE      DELT2,XDELET2
          MOVE      DELT3,XDELET3
          MOVE      DELT4,XDELET4
          MOVE      DELT5,XDELET5
          MOVE      DELT6,XDELET6
.
          BRANCH    OPTION,CUDC5000
          SUB       XDELET1,XTOTAL
          SUB       XDELET2,XTOTAL
          SUB       XDELET3,XTOTAL
          SUB       XDELET4,XTOTAL
          SUB       XDELET5,XTOTAL
          SUB       XDELET6,XTOTAL
.
CUDC5000  CALL      WRTMP1
.
CUDC9999  RETURN
+
.**********************************************************************
.*                         END0000                                    *
.*                      End of Report                                 *
.**********************************************************************
.
END0000   COMPARE   ZERO,CPAGENO
          GOTO      END1000 IF NOT EQUAL
.
.         No data selected
.
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON:
                    "** No Record Selected **",*W2;
          CALL      HEAD0000
          PRINT     *N,*N,"***  End of Report  ***"
          GOTO      END9999
.
END1000   COMPARE   "60",CLNO
          GOTO      END1100 IF LESS
          CALL      HEAD0000
          PRINT     *N;
          GOTO      END2000
.
END1100   PRINT     *N;
          CALL      PGE0000
.
.         Print Hospital Totals (Grand Totals)
.
END2000   PRINT     *1,"Hospital Totals",*22,GLASTP,*32,GADDED:
                    *43,GADMIT,*52,GDELET1,*60,GDELET2,*68,GDELET3:
                    *76,GDELET4,*84,GDELET5,*92,GDELET6,*101,GPRTYR:
                    *109,GPRTYD,*117,GPRTYU,*125,GTOTAL
.
          ADD       TWO,CLNO
          CALL      PGE0000
.
          CALL      DESC0000       * find deletion code descriptions
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *N,*1,"Deletion 1 = ",DELDESC1,*67,"Deletion 4 = ",DELDESC4:
                    *N,*1,"Deletion 2 = ",DELDESC2,*67,"Deletion 5 = ",DELDESC5:
                    *N,*1,"Deletion 3 = ",DELDESC3,*67,"Deletion 6 = ",DELDESC6
.
          BRANCH    OPTION,END3000
          PRINT     *N,PENSUSA," Stat Change = No. of Patients changed from ":
                       "Non-",*+,PENSUSA," to ",*+,PENSUSA;
          IF        WTCNUSOP <> ONE
            PRINT      " priority";
          ENDIF
          PRINT     " during the period":
                    *N,"Status Changes        = No. of Patients changed from ":
                       *+,PENSUSA," to Non-",*+,PENSUSA;
          IF        WTCNUSOP <> ONE
            PRINT      " priority";
          ENDIF
          PRINT     " during the period":
                    *N,"Total                 = Last Period + Added To List + ":
                       *+,PENSUSA," Stat Change - Deletions - Status Changes"
          GOTO      END4000
.
END3000   PRINT     *N,"Note on Added To List : Does not include patients ":
                       "added in the period and subsequently changed to ":
                       *+,PENSUSA," within the same period"
.
END4000   PRINT     *N,"Forward Calling Patients are included":
                    *N,*N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:23,*EF,*P24:24,*V2LON:
                    "** Report Generation Completed **";
.
END9999   CALL      KILL0000        * remove temporary file 
          RETURN
+
.**********************************************************************
.*                         DESC0000                                   *
.*                   Find Deletion Descriptions                       *
.**********************************************************************
DESC0000  MOVE      ONE,INDC
          MOVE      ZERO,DESCFLG
          PACK      DELDESC,SP30,SP30
          CLEAR     DELDESC
.
.         Find out descriptions for deletion codes 1 - 6
.
DESC1000  PACK      KEY5,CODEWR,SP3
          CALL      RDSCODE1
DESC2000  CALL      RDKCODE1
          BRANCH    OVRCD,DESC3000
.
          MATCH     CODEWR,TCODE
          GOTO      DESC3000 IF NOT EQUAL
.
          MOVE      SIX,FORM1
          REP       " 606768696",TCINDC1
          MOVE      TCINDC1,FORM1
.
          COMPARE   FORM1,INDC
          GOTO      DESC2000 IF NOT EQUAL
.
. if printing non-pending AND are on deletion type six AND 
.    are using performed/not performed AND are at the start of string
.
          IF        OPTION = ONE & INDC = SIX & WTCNUSOP = ONE & DESCFLG = ZERO
            MOVE       ONE,DESCFLG
            MOVE       "NOT PERFORMED",DELDESC
            ENDSET     DELDESC
          ENDIF
.
          CALL      PDEL0000       * pack deletion code descriptions
          GOTO      DESC2000
.
DESC3000  RESET     DELDESC
          STORE     DELDESC,INDC,DELDESC1,DELDESC2,DELDESC3,DELDESC4:
                                 DELDESC5,DELDESC6
.
          ADD       ONE,INDC
          COMPARE   SEVEN,INDC
          GOTO      DESC9999 IF EQUAL
          MOVE      ZERO,DESCFLG
          PACK      DELDESC,SP30,SP30
          CLEAR     DELDESC
          GOTO      DESC1000
.
DESC9999  RETURN
+
.**********************************************************************
.*                         PDEL0000                                   *
.*                   Pack Deletion Descriptions                       *
.**********************************************************************
PDEL0000  
.         This routine finds the end of the deletion code description, 
.         it then appends it to DELDESC along with a slash
.
          COMPARE   ZERO,DESCFLG
          GOTO      PDEL1000 IF EQUAL
.
          APPEND    SLASH,DELDESC
.
PDEL1000  MOVE      ONE,DESCFLG
          STRIP     TDESC
          APPEND    TDESC,DELDESC
          RETURN
+
.**********************************************************************
.*                         PRNT0000                                   *
.*                        Print Data                                  *
.**********************************************************************
PRNT0000  MOVE      ZERO,PSUMFLG
.
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON:
                    "*** Generating Report ***":
                    *P1:24,*EL,*P35:24,*HOFF,"Unit : ";
.
          BRANCH    SUMMFLG,PRNT9999          * summary page only required
          MOVE      ZERO,PRTFLG
.
          MOVE      SP10,KEY9
          CALL      RDSTMP1
.
PRNT1000  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT9999
.
          DISPLAY   *P42:24,*V2LON,XUNIT;
.
          MATCH     XDOCT,SIXZ
          GOTO      PRNT2000 IF NOT EQUAL
.
.         Print clinic sub totals
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      TOTDESC,TOTHED,CLNDESC
          CALL      PGE0000
          PRINT     TOTDESC,*22,XLASTP,*32,XADDED,*43,XADMIT,*52,XDELET1:
                    *60,XDELET2,*68,XDELET3:
                    *76,XDELET4,*84,XDELET5,*92,XDELET6,*101,XPRTYR:
                    *109,XPRTYD,*117,XPRTYU,*125,XTOTAL
          ADD       ONE,CLNO
          MOVE      ZERO,PRTFLG
          GOTO      PRNT1000 
.
PRNT2000  
.         If PRTFLG is set to zero it will print the clinic description,
.         and the the totals. If set to a one it will only print the totals
.
          COMPARE   ZERO,PRTFLG
          GOTO      PRNT2500 IF NOT EQUAL
.          
          MOVE      "UNKNOWN CLINIC      ",CLNDESC     * clinic description
          PACK      KEY5,CODEWU,XUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT2200
          MOVE      TDESC,CLNDESC
.
PRNT2200  COMPARE   "60",CLNO
          GOTO      PRNT2300 IF LESS
          CALL      HEAD0000
          GOTO      PRNT2400
.
PRNT2300  CALL      PGE0000
.
PRNT2400  PRINT     *1,CLNDESC,*N
          ADD       TWO,CLNO
          MOVE      ONE,PRTFLG
.
PRNT2500  MOVE      "UNKNOWN DOCTOR      ",DOCDESC     * doctor description
          MOVE      XDOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,PRNT3000
          MOVE      DGNAME,ANS
          PACK      DOCDESC,ANS,DOT,DSNAME
.
PRNT3000  COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.*******************************************************************************
.  Check to see if Last Period (XLASTP), Total (XTOTAL) & Added to List (XADDED)
.  are all zero, if so we assume that the rest of the column values are also
.  zero, and therefore we ommit from the printing of the detail line.
.  SRF #140905.     09.05.90     DGP     (Refer J.C.)
.*******************************************************************************
.
          COMPARE   ZERO,XLASTP
          GOTO      PRNT4000 IF EQUAL
          GOTO      PRNT6000
.
PRNT4000  COMPARE   ZERO,XTOTAL
          GOTO      PRNT5000 IF EQUAL
          GOTO      PRNT6000
.
PRNT5000  COMPARE   ZERO,XADDED
          GOTO      PRNT1000 IF EQUAL
.
PRNT6000  PRINT     *1,DOCDESC,*22,XLASTP,*32,XADDED,*43,XADMIT,*52,XDELET1:
                    *60,XDELET2,*68,XDELET3:
                    *76,XDELET4,*84,XDELET5,*92,XDELET6,*101,XPRTYR:
                    *109,XPRTYD,*117,XPRTYU,*125,XTOTAL
.
          ADD       ONE,CLNO
          GOTO      PRNT1000 
.
PRNT9999  RETURN
+
.**********************************************************************
.*                         PSUM0000                                   *
.*                   Print Summary Data                               *
.**********************************************************************
.
PSUM0000  MOVE      ZERO,PSUMFLG
.
          OPEN      BNBTMPXX,ANATMP
.
          PACK      KEY9,SIXZ,SP3
          CALL      RDSTMP2
PSUM1000  
          CALL      RDKTMP2
          BRANCH    OVRCD,PSUM9999
.
          COMPARE   ZERO,PSUMFLG
          GOTO      PSUM1500 IF NOT EQUAL
.
          MOVE      ONE,PSUMFLG
          CALL      HEAD0000
.
PSUM1500  COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          DISPLAY   *P42:24,*V2LON,XUNIT;
.
          MOVE      "UNKNOWN CLINIC      ",CLNDESC     * clinic description
          PACK      KEY5,CODEWU,XUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,PSUM2000
          MOVE      TDESC,CLNDESC
.
.         Print clinic sub totals
.
PSUM2000  PRINT     *N,*1,CLNDESC,*22,XLASTP,*32,XADDED,*43,XADMIT,*52,XDELET1:
                    *60,XDELET2,*68,XDELET3:
                    *76,XDELET4,*84,XDELET5,*92,XDELET6,*101,XPRTYR:
                    *109,XPRTYD,*117,XPRTYU,*125,XTOTAL
          ADD       TWO,CLNO
.
          ADD       XLASTP,GLASTP        * add totals to grand totals
          ADD       XADMIT,GADMIT
          ADD       XADDED,GADDED
          ADD       XDELET1,GDELET1
          ADD       XDELET2,GDELET2
          ADD       XDELET3,GDELET3
          ADD       XDELET4,GDELET4
          ADD       XDELET5,GDELET5
          ADD       XDELET6,GDELET6
          ADD       XPRTYR,GPRTYR
          ADD       XPRTYD,GPRTYD
          ADD       XPRTYU,GPRTYU
          ADD       XTOTAL,GTOTAL
          GOTO      PSUM1000
.
PSUM9999  RETURN
+
.**********************************************************************
.*                         HEAD0000                                   *
.*                      Print Headings                                *
.**********************************************************************
.
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      PGE0000 IF NOT EQUAL
.
          CLOCK     TIME,CTIMEIS
.
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          PRINT     *40,"Period : ",PDATE,*59,DRGLDSC:
                    *N,*59,STRDATE," to ",ENDDATE,*N
.
          BRANCH    OPTION,HEAD1111,HEAD2222
HEAD1111
          BRANCH    SUMMFLG,HEAD1333
          BRANCH    PSUMFLG,HEAD1333
          PRINT     *40,"NON-",*+,PENSUSB," PATIENTS ONLY",*N
          GOTO      HEAD1666
HEAD1333
          PRINT     *40,"NON-",*+,PENSUSB," PATIENTS ONLY (Summary)",*N
HEAD1666
          CALL      PGE0000
          PRINT     *25,"Last",*34,"Added";
          IF        WTCNUSOP = ONE
            PRINT     *42,*+,WTCNTPDS;
          ELSE  
            PRINT     *43,"Admitted";
          ENDIF
          PRINT     *54,"------------------ Deletions ----------------":
                    *103,"----- Priority ------":
                    *N,*1,"Clinic Speciality",*24,"Period",*32,"To List";
          IF        WTCNUSOP = ONE
            PRINT   *42,"Performed";
          ELSE
            PRINT   *42,"From List";
          ENDIF
          PRINT     *58,"1",*66,"2",*74,"3",*82,"4",*90,"5":
                    *98,"6",*103,"Rout.",*112,"Semi",*118,"Urgent",*128,"Total"
          GOTO      HEAD3333
HEAD2222
          BRANCH    SUMMFLG,HEAD2333
          BRANCH    PSUMFLG,HEAD2333
          PRINT     *40,*+,PENSUSB," PATIENTS ONLY",*N
          GOTO      HEAD2666
HEAD2333
          PRINT     *40,*+,PENSUSB," PATIENTS ONLY (Summary)",*N
HEAD2666
          CALL      PGE0000
          PRINT     *25,"Last",*34,"Added",*43,PENSUSA:
                    *54,"------------------ Deletions ----------------":
                    *103,"--- Status Changes --":
                    *N,*1,"Clinic Speciality",*24,"Period",*32,"To List":
                    *41,"Stat Change",*58,"1",*66,"2",*74,"3",*82,"4",*90,"5":
                    *98,"6",*103,"Rout.",*112,"Semi",*118,"Urgent",*128,"Total"
HEAD3333
          CALL      PGE0000
          MOVE      TEN2,CLNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                         PGE0000                                    *
.*                        Print Line                                  *
.**********************************************************************
PGE0000   PRINT     *1,"---------------------------------------------------":
                    "---------------------------------------------------":
                    "------------------------------"
          ADD       ONE,CLNO
          RETURN
+
.*********************************************************************
.         work out the following values 
.         for both        : number of W/L last period
.         for suspensions : added to list
.                           become suspended
.         for un-suspen's : added to list
.                           become unsuspended
.*********************************************************************
WTSCA000  MOVE      ZERO,STATADD
          MOVE      ZERO,STATADMT
          MOVE      ZERO,STATLAST
          PACK      KEY18,CSTPER,WTUDUNIT,WTUDDOCT,WTUDPRTY
          CALL      RDWTSCU1
          BRANCH    OVRCD,WTSCA900
.
          IF        OPTION = ONE
            ADD       WTSCSBEU,STATADD        * added to list
            ADD       WTSCSSEU,STATADMT       * become unsuspended 
            ADD       WTSCLPUN,STATLAST       * unsuspended last period
          ELSE
            ADD       WTSCSBES,STATADD        * added to list (suspended)
            ADD       WTSCSUES,STATADMT       * become suspended
            ADD       WTSCLPSU,STATLAST       * suspended last period
          ENDIF
.
WTSCA900  MOVE      STATADD,WTUDADD         * set number added
          MOVE      STATADMT,WTUDADMT       * set number admit./number susp.
          MOVE      STATLAST,WTUDLAST       * set number on W/L last period
.
WTSCA999  RETURN
+
.*********************************************************************
.         process status changes for suspended patients
.         break up by priority for suspened -> non-suspened
.*********************************************************************
WTSCB000  PACK      KEY18,CSTPER,WTUDUNIT,WTUDDOCT,WTUDPRTY
          CALL      RDWTSCU1
          BRANCH    OVRCD,WTSCB999
.
          PACK      KEY5,CODETP,WTSCPRTY
          CALL      RDCODE1
          BRANCH    OVRCD,WTSCB999
.
          MATCH     ANSU,TCINDC1
          GOTO      WTSCB200 IF NOT EQUAL
          ADD       WTSCSSEU,PRTYU           * urgent priority
          GOTO      WTSCB999
.
WTSCB200  MATCH     ANSS,TCINDC1
          GOTO      WTSCB300 IF NOT EQUAL
          ADD       WTSCSSEU,PRTYD           * semi urgent priority
          GOTO      WTSCB999
.
WTSCB300  ADD       WTSCSSEU,PRTYR           * routine priority
.
WTSCB999  RETURN
+
.*********************************************************************
.         process status changes for pending patients
.         break up by priority for pending -> non-pending
.*********************************************************************
WTUPB000  PACK      KEY21,CSTPER,WTUDUNIT,WTUDDOCT,SP30
          CALL      RSWTUPC1
.
WTUPB100  CALL      RKWTUPC1
          BRANCH    OVRCD,WTUPB999
.
          MATCH     CSTPER,WTUPDATE         * test same period
          GOTO      WTUPB999 IF NOT EQUAL
.
          MATCH     WTUDUNIT,WTUPUNIT       * test same unit
          GOTO      WTUPB999 IF NOT EQUAL
.
          MATCH     WTUDDOCT,WTUPDOCT       * test same doctor
          GOTO      WTUPB999 IF NOT EQUAL
.
          MATCH     SP3,WTUPOPTY            * test if any old code
          GOTO      WTUPB100 IF EQUAL
.
          PACK      KEY5,CODETP,WTUPOPTY
          CALL      RDCODE1
          BRANCH    OVRCD,WTUPB100
          MATCH     ANSP,TCINDC2            * must be pending status
          GOTO      WTUPB100 IF NOT EQUAL
.
          PACK      KEY5,CODETP,WTUPNPTY
          CALL      RDCODE1
          BRANCH    OVRCD,WTUPB100
          MATCH     ANSP,TCINDC2            * must be non-pending status
          GOTO      WTUPB100 IF EQUAL
.
          MATCH     "U",TCINDC1
          GOTO      WTUPB200 IF NOT EQUAL
          ADD       WTUPNUMB,PRTYU           * urgent priority
          GOTO      WTUPB100
.
WTUPB200  MATCH     "S",TCINDC1
          GOTO      WTUPB300 IF NOT EQUAL
          ADD       WTUPNUMB,PRTYD           * semi urgent priority
          GOTO      WTUPB100
.
WTUPB300  ADD       WTUPNUMB,PRTYR           * routine priority
          GOTO      WTUPB100
.
WTUPB999  RETURN
+
.*********************************************************************
.         determine the priority classification for non-pending pats.
.*********************************************************************
NPCAT000  MOVE      SP1,TCINDC1             * set a default value to indicator
          PACK      KEY5,CODETP,WTUDPRTY    * this is a non-pending priority
          CALL      RDCODE1
.
          MATCH     "U",TCINDC1
          GOTO      NPCAT500 IF NOT EQUAL
.
          MOVE      WTUDNUMB,PRTYU           * urgent priority
          MOVE      WTUDNUMB,TOTAL           * total number of patients
          GOTO      NPCAT999
.
NPCAT500  MATCH     "S",TCINDC1
          GOTO      NPCAT600 IF NOT EQUAL
.
          MOVE      WTUDNUMB,PRTYD           * semi urgent priority
          MOVE      WTUDNUMB,TOTAL           * total number of patients
          GOTO      NPCAT999
.
NPCAT600  MOVE      WTUDNUMB,PRTYR           * routine priority
          MOVE      WTUDNUMB,TOTAL           * total number of patients
.
NPCAT999  RETURN
+
.*********************************************************************
.         get the number of unsuspended patients for the priority
.*********************************************************************
GUSPC000  COMPARE   ONE,WTCNUSOP
          GOTO      GUSPC999 IF NOT EQUAL   * not using suspensions
.
          PACK      KEY18,CSTPER,WTUDUNIT,WTUDDOCT,WTUDPRTY
          CALL      RDWTSCU1
          BRANCH    OVRCD,GUSPC999          * no record for that priority
.
. priority code total = 
.  #unsusp at SOP + number added + number became un-susp 
.        - number became susp - number deleted
.
          ASSIGN    (WTSCLPUN+WTSCSBEU+WTSCSSEU-WTSCSUES-WTSCDELT),WTUDNUMB
.
GUSPC999  RETURN
+
.**********************************************************************
.*                         CREA0000                                   *
.*                  Create Temporary File                             *
.**********************************************************************
.
CREA0000  CALL      KILL0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    ANATMP,CMDLINE
          APPEND    " 75 u1-3,4-9 u4-9,1-3",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      ANATMPXX,ANATMP
.
          RETURN
+
.**********************************************************************
.*                         KILL0000                                   *
.*                  Remove Temporary File                             *
.**********************************************************************
.
KILL0000  CLOSE     ANATMPXX
          CLOSE     BNBTMPXX
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    ANATMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**********************************************************************
.*              I/O For Temporary File                                *
.**********************************************************************
.
.         Temporary File index 1
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      ANATMPXX,KEY9;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    ANATMPXX;XUNIT,XDOCT,XLASTP,XADMIT,XADDED,XDELET1:
                             XDELET2,XDELET3,XDELET4,XDELET5,XDELET6:
                             XPRTYR,XPRTYD,XPRTYU,XTOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      ANATMPXX,KEY9;XUNIT,XDOCT,XLASTP,XADMIT,XADDED,XDELET1:
                             XDELET2,XDELET3,XDELET4,XDELET5,XDELET6:
                             XPRTYR,XPRTYD,XPRTYU,XTOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    UPDATE    ANATMPXX;XUNIT,XDOCT,XLASTP,XADMIT,XADDED,XDELET1:
                             XDELET2,XDELET3,XDELET4,XDELET5,XDELET6:
                             XPRTYR,XPRTYD,XPRTYU,XTOTAL
          RETURN
.
WRTMP1    RESET     KEY9
          WRITE     ANATMPXX,KEY9;KEY9,XLASTP,XADMIT,XADDED,XDELET1:
                             XDELET2,XDELET3,XDELET4,XDELET5,XDELET6:
                             XPRTYR,XPRTYD,XPRTYU,XTOTAL
          RETURN
.
.         Temporary File index 2
.
RDSTMP2   RESET     KEY9
          READ      BNBTMPXX,KEY9;;
          RETURN
.
RDKTMP2   MOVE      ZERO,OVRCD
          READKS    BNBTMPXX;XUNIT,XDOCT,XLASTP,XADMIT,XADDED,XDELET1:
                             XDELET2,XDELET3,XDELET4,XDELET5,XDELET6:
                             XPRTYR,XPRTYD,XPRTYU,XTOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP2    MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      BNBTMPXX,KEY9;XUNIT,XDOCT,XLASTP,XADMIT,XADDED,XDELET1:
                             XDELET2,XDELET3,XDELET4,XDELET5,XDELET6:
                             XPRTYR,XPRTYD,XPRTYU,XTOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP2    UPDATE    BNBTMPXX;XUNIT,XDOCT,XLASTP,XADMIT,XADDED,XDELET1:
                             XDELET2,XDELET3,XDELET4,XDELET5,XDELET6:
                             XPRTYR,XPRTYD,XPRTYU,XTOTAL
          RETURN
.
WRTMP2    RESET     KEY9
          WRITE     BNBTMPXX,KEY9;KEY9,XLASTP,XADMIT,XADDED,XDELET1:
                             XDELET2,XDELET3,XDELET4,XDELET5,XDELET6:
                             XPRTYR,XPRTYD,XPRTYU,XTOTAL
          RETURN
+
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       PATCOMN3
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       PATDRGIO/INC       * period date range file
          INC       PATCODIO/INC       * patient codes file
          INC       PATDO1IO/INC       * patient doctor file
          INC       WATSCUIO/INC          * w/list unit/doctor suspensions file
          INC       WATUDCIO/INC       * w/list unit/doctor codes file
          INC       WATUDSIO/INC       * w/list unit/doctor summary file
          INC       WATUPCIO/INC       * unit/doctor priority changes
          INC       WEBERRIO/INC
............................................................................

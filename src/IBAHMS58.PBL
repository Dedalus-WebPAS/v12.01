. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAHMS58                                            *
. *                                                                           *
. *     FUNCTION:         ABF Billing Audit report                            *
. *                                                                           *
. *     DATE WRITTEN:     22/11/2019                                          *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
. *---------------------------------------------------------------------------*
. * V12.00.01 16/05/2025  J.Tan          TSK 0955096                          *
. *           Added alpanumeric visit number generation                       *
. * V11.05.01 04/02/2025 J.Tan           TSK 0952533                          *
. *           Recompiled for PATCATBI - changes for MV using Ventilation file *
. *           14/02/2025 J.Tan           TSK 0946490                          *
. *           Recompiled for PATCATBI - Changes for MV CWO Payment Model      *
. *           14/02/2025 J.Tan           TSK 0955356                          *
. *           Recompiled for PATCALF - writting to Financial Summary details  *
. * V11.04.08 23/10/2024 J.Tan             TSK 0951578                        *
. *           Recompiled for ABFPTINV - Added Minutes to round up ICU Hrs(ABF)*
. * V11.04.07 23/09/2024  J.Tan          TSK 0949848                          *
. *           Recompiled for ABFPTINV - DRG List used in for COVID Adjustor   *
. * V11.04.06 11/09/2024  J.Tan          TSK 0947315                          *
. *           Recompiled for ABFPTINV - fixed Total Unqualified               *
. * V11.04.05 01/08/2024  J.Tan          TSK 0947804                          *
. *           Recompiled for ABFMHINV - Changes for Discharges after 1/7/2023 *
. * V11.04.04 17/07/2024  J.Tan          TSK 0947315                          *
. *           Recompiled for ABFPTINV (LOS ICU adjusted & Newborn)            *
. * V11.04.03 15/03/2024  J.Tan          TSK 0910994                          *
. *           Recompiled for CMXMATRX                                         *
. * V11.04.02 29/01/2024  Jacob Jackson  TSK 0919335                          *
. *           Add HF History includes                                         *
. * V11.04.01 29/01/2024  Jacob Jackson  TSK 0919335                          *
. *           Add new local variable and recompile for GETTFEES               *
. * V11.03.06 17/11/2023  J.Tan          TSK 0940089                          *
. *           Recompiled for ABFPTINV - checking for blank Disc.DRG           *
. * V11.03.05 13/11/2023  J.Tan          TSK 0939019                          *
. *           Recompiled for ABFPTINV - HAC ICD10 Edition 12                  *
. * V11.03.04 04/10/2023  J.Tan          TASK 0938230                         *
. *           Recompiled for ABFAECCL - Emergency ABF                         *
. * V11.03.03 29/08/2023  J.Tan          TSK 0916870                          *
. *           Recompiled for ABFPTINV - Claim code Cat CL Multiple NEP value  *
. * V11.03.02 15/06/2023  J.Tan          TSK 0923703                          *
. *           Recompiled for ABF new calculations                             *
. * V11.03.01 14/06/2023  J.Tan         TSK 0923703                           *
. *           Recompiled for ABF Mental event                                 *
. * V11.02.02 29/06/2022 J.Tan           TSK 0920712                          *
. *           Recompiled for ABFAECCL - I*D on emrvcdaf file                  *
. * V11.02.01 03/03/2022  J.Tan           TSK 0902652                         *
. *           Recompiled for OUTCATBI/AAECATBI - Pat.Invoice new functionality*
. * V11.01.09 17/11/2021  J.Tan          TSK 0913621                          *
. *           Recompiled for ABFPTINV - Mod for Multiple NEP Values           *
. * V11.01.08 28/10/2021 J.Tan           TSK 0913056                          *
. *           Recompiled for ABFAECCL-ADMFLAG and CCAR0000(Clinical Care)     *
. * V11.01.07 13/10/2021 J.Tan           TSK 0905305                          *
. *           Recompiled for ABFAECCL - Adjustment type 068 & 069             *
. * V11.01.06 29/09/2021  J.Tan          TSK 0901515                          *
. *           Recompiled for CMXMATRX - Added Primary Proc.to Matrix          *
. * V11.01.05 14/09/2021  J.Tan          TSK 0906222                          *
. *           Recompiled for ABFPTINV - Mod for Multiple NEP Values           *
. * V11.01.04 09/08/2021  J.Tan           TSK 0905305                         *
. *           Recompiled for AAECATBI - AE Care class (ABFAECCL)              *
. * V11.01.03 07/07/2021  J.Tan           TSK 0908588                         *
. *           Recompiled for PATCATBI-Exclude Unqualified days for Casepayment*
. * V11.01.02 28/04/2021  Thanh T         TSK 0895165                         *
. *           Recompiled for OPRARDFD changes                                 *
. *           07/04/2021  Tracey Nguyen   TSK 0901515                         *
. *           Recompiled for PMSCMTFD - Added new fields                      *
. *           a) 1 File type for ICD procedure (PMCCIPF)                      *
. *           b) 3 new CMBS Field Type (PMCCFTS4/PMCCFT5C/PMCCFT6C)           *
. *           c) 3 new CMBS Codes From (PMCCPC4F,PMCCSC5F,PMCCTC6F)           *
. *           d) 3 new CMBS Codes To   (PMCCPC4T/PMCCSC5T,PMCCTC6T)           *
. *           27/04/2021  J.Tan           TSK 0863287                         *
. *           Mod for Patient Invoice new functionality                       *
. * V11.01.01 10/03/2021  Tracey Nguyen   TSK 0901515                         *
. *           Recompiled for PMSCMTFD - Added Primary ICD Procedure 1-5       *
. *           (PMCCICP1-5)                                                    *
. *---------------------------------------------------------------------------*
. *        V11.00.10 10/11/2020  J.Tan           TSK 0899562                  *
. *                  Recompiled for CMXMATRX - setup date for reading MBS item*
. *        V11.00.09 29/09/2020  J.Tan          TSK 0896829                   *
. *                  Recompiled for CMXMATRX - use National(PTPGNDRG)DRG      *
. *        V11.00.08 22/09/2020  J.Tan           TSK 0895330                  *
. *                  Recompiled for CMXMATRX - reposition read of Matrix table*
. *        V11.00.07 20/07/2020  J.Tan           TSK 0892319                  *
. *                  Recompiled for ABFPTINV - Additional NEP and Psych.Adjus.*
. *        V11.00.06 14/07/2020  J.Tan          TSK 0892902                   *
. *                  Recompiled for PATCATBI - Mod to initialise CASMXKEY     *
. *        V11.00.05 01/07/2020  J.Tan           TSK 0892765                  *
. *                  Recompiled for PATCATBI-Low outlier exc. unqualified days*
. *                  05/07/2020  J.Tan           TSK 0892746                  *
. *                  Recompiled for ABFPTINV - CALCAGE as at Admission date   *
. *        V11.00.04 29/06/2020  J.Tan           TSK 0893767                  *
. *                  Recompiled for ABFPTINV - Changed CALCFLAG to 01/01/2021 *
. *        V11.00.03 20/04/2020 J.Tan           TSK 0890733                   *
. *                  Recompiled for ABFPTINV - ABF new 2020 calculation       *
. *        V11.00.02 01/06/2020  J.Tan          TSK 0892273                   *
. *                  Mod to exclude Unqualifed baby                           *
. *        V11.00.01 20/03/2020  J.Tan          TSK 0838262                   *
. *                  Added Effective from and to date to MBS Item file        *
. *        V10.15.06 24/01/2020  J.Tan          TSK 0886176                   *
. *                  Rename FNAME1 to FNAMEP                                  *
. *        V10.15.05 18/12/2019  Peter Vela     TSK 0857392                   *
. *                  Added DRG / Inv DRG column                               *
. *                  Added MDB / URG / Inv MDB / Inv URG rows and columns     *
. *        V10.15.04 17/12/2019  Peter Vela     TSK 0857392                   *
. *                  Added outpatient heading HEAD3000                        *
. *        V10.15.03 16/12/2019  Peter Vela     TSK 0857392                   *
. *                  Added URG / MDB in emergency                             *
. *                  Move 1 to PRABFINV before call to AAETBI                 *
. *                  Move 1 to PRABFINV before call to OUTTBI                 *
. *        V10.15.02 13/12/2019  Peter Vela     TSK 0857392                   *
. *                  Added subtotal layouts for emergency and outpatients     *
. *        V10.15.01 11/12/2019  Peter Vela     TSK 0857392                   *
. *                  Added $ values subtotals for comparison                  *
. *                  Fixed typo in HEAD2000 HEAD4000                          * 
. *        V10.15.00 22/11/2019  J.Tan          TSK 0857392                   *
. *                  Created ABF Billing Audit report                         *
. *                                                                           *
. ******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
          INC       PABXVARS/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEDTRFD/INC
          INC       EMRVCDFD/INC
          INC       EMRURGFD/INC
          INC       IBASECFD/INC
          INC       IBAPASFD/INC
          INC       IBAGEDFD/INC
          INC       IBAGSTFD/INC
          INC       IBACONFD/INC
          INC       IBATMDFD/INC
          INC       IBATMHFD/INC
          INC       NHIMASFD/INC
.
          INC       MLTCFNFD/INC
          INC       NZPBFEFD/INC
          INC       NZPCFNFD/INC
          INC       NZPCMTFD/INC
          INC       NZPEXTFD/INC
          INC       NZPRDTFD/INC
          INC       NZPFIGFD/INC
          INC       NZPFINFD/INC
          INC       NZPIBRFD/INC
          INC       NZPMCHFD/INC
          INC       NZPMXCFD/INC
          INC       NZPPFEFD/INC
          INC       NZPRBRFD/INC
          INC       NZPRFNFD/INC
          INC       NZPPICFD/INC
          INC       NZPSPRFD/INC
          INC       NZPTFEFD/INC
          INC       NZPRVBFD/INC
          INC       NZPIVBFD/INC
          INC       OPRARDFD/INC
          INC       OPRDEAFD/INC
          INC       OPRSESFD/INC
.
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
          INC       OUTSESFD/INC
          INC       PATCHCFD/INC
          INC       PATELGFD/INC
          INC       PATBFEFD/INC
          INC       PATCALAG/INC
          INC       PATCFAFD/INC
          INC       PATCODFD/INC
          INC       PMSABFFD/INC
          INC       PMSCIDFD/INC
          INC       PMSCCDFD/INC
          INC       PMSVMTFD/INC
          INC       PATCONFD/INC
          INC       PATCTFFD/INC
          INC       PATDHEAD/INC
          INC       PATDINVS/INC
          INC       PATDGWFD/INC
          INC       PATDO1FD/INC
          INC       PATDRGFD/INC
          INC       PATDSCFD/INC
          INC       PATDTRFD/INC
          INC       PATECDFD/INC
          INC       PATECOFD/INC
          INC       PATFINFD/INC
          INC       PATFMOFD/INC
          INC       PATFX1FD/INC
          INC       PATFN1FD/INC
          INC       PATFN2FD/INC
          INC       PATGTUFD/INC
          INC       PATHSPFD/INC
          INC       PATHTRFD/INC
          INC       EMRHTRFD/INC
          INC       OUTHTRFD/INC
          INC       PATIBLFD/INC
          INC       PATICUFD/INC
          INC       PATIN1FD/INC
          INC       PATINMFD/INC
          INC       PATINVFD/INC
          INC       PATIOVFD/INC
          INC       PATIPEFD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC
          INC       PATMCHFD/INC
          INC       PATMI1FD/INC
          INC       PATOVBFD/INC
          INC       PATPGRFD/INC
          INC       PATRE1FD/INC
          INC       PATRBLFD/INC
          INC       PATRATFD/INC
          INC       PATRCAUD/INC
          INC       PATRFNFD/INC
          INC       PATSELDT/INC
          INC       PATSRBFD/INC
          INC       PATTFEFD/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PATVENFD/INC
          INC       PMSHATFD/INC
          INC       PATWC1FD/INC
          INC       PATWMAFD/INC
          INC       PATWR1FD/INC
          INC       PATWVEFD/INC
          INC       PMSEVTFD/INC
          INC       PMSCNOFD/INC
          INC       PMSHCPFD/INC
          INC       PMSSINFD/INC
          INC       PATCMTFD/INC
          INC       PMSCMTFD/INC
          INC       PMSSGAFD/INC
          INC       PMSMTIFD/INC
          INC       PMSMPRFD/INC
          INC       PMSVX1FD/INC
          INC       PRIITMFD/INC
          INC       PATSTAFD/INC
          INC       PATAA1FD/INC
.
          INC       PATCMXFD/INC
          INC       PATAFEFD/INC
          INC       PATASDFD/INC
          INC       PATHDFFD/INC
          INC       PATHLFFD/INC
          INC       PATLDFFD/INC
          INC       PATLSDFD/INC
.
          INC       EMRSITFD/INC
          INC       EMRHISFD/INC
          INC       EMRVISFD/INC
          INC       COMDEPFD/INC
          INC       RCPBNKFD/INC
          INC       RCPDTEFD/INC
          INC       RCPBDTFD/INC
          INC       RCPCONFD/INC
          INC       PMSPBRFD/INC
          INC       PMSIDEFD/INC
          INC       PMSEDGFD/INC       * Effective DRG Dates
          INC       PATDDHFD/INC
.
.               WORK AREA
.
TMPABFA1   IFILE    SQL, FIXED=335, KEY="U1-8,9-16,17-19"
.
.
PIPE      INIT      "|"
.
ADMISSNO  DIM       8
ADMNSTAT  FORM      1
ADAY      DIM       2
AMON      DIM       2
AYER      DIM       2
ACEN      DIM       2
ACCDTE    DIM       8     * accommodation date used in AAECATBI
AECNFTYP  FORM      1
AECNCHRS  FORM      1
ACTION    DIM       1                       * Action for PATAA1FD
ADD       DIM       2                       * Day for PATAA1FD
AMM       DIM       2                       * Month for PATAA1FD
AYY       DIM       2                       * Year for PATAA1FD
ACC       DIM       2                       * Century for PATAA1FD
OPERCODE  DIM       4                       * Operator code for PATAA1FD
.
COUNTER   FORM      3
DCOUNTR   DIM       3
CSTRTYR   DIM       8
CDYSDAYS  FORM      6                       * CALCDAYS number of days
CDYSFDTE  DIM       8                       * CALCDAYS from date
CDYSTDTE  DIM       8                       * CALCDAYS to date
CDYSYEAR  FORM      2                       * CALCDAYS year
BULKBILL  DIM       1
CODE      DIM       2
CURRDATE  DIM       8
CMDLINE   DIM       80
CMDLIN1   DIM       70
CMDLIN2   DIM       16
CURSESS   DIM       18
CUTDAY    DIM       2
CUTMON    DIM       2
CUTYER    DIM       2
CUTCEN    DIM       2
CALDAYS   FORM      3
CATCR     INIT      "cr"
CODECL    INIT      "CL"
CODEDP    INIT      "DP"
CODEPY    INIT      "Py"
CERTINDC  FORM      1
.
DB        INIT      "DB"
DDDAY     DIM       2
DDMON     DIM       2
DDYER     DIM       2
DDCEN     DIM       2
DIM1A     DIM       1
*DIM3      DIM       3
DIM4      DIM       4
DIM5A     DIM     5
DIM6B     DIM       6
DIM6C     DIM       6
DIM9      DIM       9
DIM11     DIM       11
DIM13     DIM       13
DIM14     DIM     14
DIM18     DIM       18
DIM18A    DIM       18
DIM24A    DIM       24
DIM30     DIM       30
DOCTCODE  DIM       6
DOCFNAME  DIM       50
.
EMCNGINV  DIM       1
EMCNDPOC  DIM       1
EMCNOPOC  DIM       15
EFTDATE   DIM       8         * Effective date (ccyymmdd)
.
FORM12P4  FORM      12.4
FORM3A    FORM      3
FORM5     FORM      5
FIVE9     FORM      "59"
FMTGNAME  DIM       35
FMTTITLE  DIM       10
FMTSNAME  DIM       35
.
HOSPID    DIM       3
HOSPNAME  DIM       50
HINVDES   DIM       30
HESYST    FORM      1
HEADFLAG  FORM      1
HFHIFUND  FORM      2
.
JYEAR     FORM      2
.
KEY13A    DIM       13
KEYFLAG   FORM      1
.
MBSFLAG   FORM      1
NEWVALUE  FORM      12.4
.
PASS      FORM      1
.
OPCNCONS  DIM       1
OPOCFLAG  FORM      1
OPNFLAG   FORM      "0"      * Used in stepdown
OPRDATE   DIM       8
OTCNCFEE  FORM      1
OTCNFTYP  FORM      1
.
SYSTOPTN  FORM      2
FRMDATE   DIM       8
ENDDATE   DIM       8
SERVDOCT  DIM       10
SAVAMNT   FORM      12.2
SAVALLW   FORM      12.2
SAVBEND   FORM      3
SAVDISC   FORM      12.2
SAVEXTRA  FORM      12.2
SP12      INIT      "            "
SP14      INIT      "              "
SUROPT    FORM      1
ABFINVNO  DIM       8
.
TEMPFILE  DIM       8
TFEEIND   FORM      1
THCFLG    FORM      2
THREED    INIT      "3"
TTBILL    FORM      12.2
TTDAYS    FORM      8
TTNETT    FORM      12.2
UNKGST    FORM      1
UPADMTYP  DIM       1
WDATE1    DIM       8
WDATE2    DIM       8
WARNINGL  DIM       20
WBSEUID   DIM       10
ZERO4     INIT      "0000"
ZEROINVC  DIM       1
.
DISCONLY  FORM      1     * discharged only 1=no 0=yes
.
.         Web Variables (only the include files use these variables)
.
DAYFORMT  DIM       4
NJAN      INIT      "January"
NFEB      INIT      "February"
NMAR      INIT      "March"
NAPR      INIT      "April"
NMAY      INIT      "May"
NJUN      INIT      "June"
NJUL      INIT      "July"
NAUG      INIT      "August"
NSEP      INIT      "September"
NOCT      INIT      "October"
NNOV      INIT      "November"
NDEC      INIT      "December"
.
HTMLFILE  FIFO      ASCII, FIXED=250
MTHNAM3   DIM       3
MTHNAM3A  DIM       3
.
CFPAYCOD  DIM       3        * 3M Codefinder PAY Code
.
.
PRGID     INIT      "IBAHMS58"
PRGNAM    INIT      "ABF Billing Audit"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                              MAIN0000                                      *
.*                  Controlling Logic (Mainline code)                         *
.******************************************************************************
.
MAIN0000  CALL      INIT0000            * Initialisation and open files
.
MAIN1000  CALL      OPTN0000            * Select options
          BRANCH    EXIT,MAIN9999       * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN2000:   * Proceed to process
                            MAIN2000:   * Proceed to process
                            MAIN2000    * Proceed to process
.
          GOTO      MAIN1000            * invalid selection; get again
.
MAIN2000  CALL      KDAT0000
          CALL      KHOS0000
.
          CALL      CONTQST             * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:     * Yes
                          MAIN2000:     * No; prompt options again
                          MAIN1000      * Cancel
.
MAIN5000  CALL      PROC0000            * Processing
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM                 * Chain back to program
+
.******************************************************************************
.*                                INIT0000             Called by: MAIN0000    *
.*                  Display headings and open files                           *
.******************************************************************************
.
INIT0000  CALL      DISPHEAD                      * display heading
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
          READ      CONTROLF,TEN;*104,PTCNMBSF:                        *C-47385
                                 *217,CAUDQ    * added for V5.01.14 - SRF 105195
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TEN5;*247,CSTDOWN
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,TWENTY;*164,PTRESDAY,*193,PTCNDCLM:
                                    *199,PTCNBCUT,*211,PTCNCNDY
          READ      CONTROLF,TWENTY1;*120,PTCNADDC
          READ      CONTROLF,THIRTY;*200,AECNFTYP
          READ      CONTROLF,THIRTY1;*163,OTCNFTYP,OTCNCFEE
          READ      CONTROLF,FIVE9;*1,CDLSTEP,*5,CREMINV:
                                   *66,CGSTPERC,CGSTITEM
          READ      CONTROLF,SEVENTY7;*214,PTCNBTNM
          READ      CONTROLF,SEVENTY9;*68,PTCNMFEE,PTCNJFEE
          READ      CONTROLF,SEVENTY9;*248,PTCNIPEN
          READ      CONTROLF,EIGHTY8;*162,PTCFEEOF
          READ      CONTROLF,HUND05;*99,PTCNNWCM
          READ      CONTROLF,HUND12;*102,PTCNHITC
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMCHG1,"patmchgf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patchcof";
          OPEN      PATCHCO1,"patchcof"
.
          DISPLAY   *P64:24,"patcmxaf";
          OPEN      PATCMXA1,"patcmxaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
          OPEN      PATDTRA5,"patdtraf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
          OPEN      PMSMTIA2,"pmsmtiaf"
          OPEN      PMSMTIA4,"pmsmtiaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"aaedtref";
          OPEN      AAEDTRE1,"aaedtref"
          OPEN      AAEDTRE4,"aaedtref"
.
          DISPLAY   *P64:24,"emrvcdaf";
          OPEN      EMRVCDA1,"emrvcdaf"
          OPEN      EMRVCDA2,"emrvcdaf"
.
          DISPLAY   *P64:24,"emrurgaf";
          OPEN      EMRURGA1,"emrurgaf"
          OPEN      EMRURGA2,"emrurgaf"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"oprardaf";
          OPEN      OPRARDA1,"oprardaf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmsvmtaf";
          OPEN      PMSVMTA1,"pmsvmtaf"
.
          DISPLAY   *P64:24,"rcpbdtaf";
          OPEN      RCPBDTA1,"rcpbdtaf"
.
          DISPLAY   *P64:24,"nzpbfeaf";
          OPEN      NZPBFEA1,"nzpbfeaf"
.
          DISPLAY   *P64:24,"nzpcfnaf"
          OPEN      NZPCFNA1,"nzpcfnaf"
.
          DISPLAY   *P64:24,"nzpcmtaf"
          OPEN      NZPCMTA1,"nzpcmtaf"
          OPEN      NZPCMTA2,"nzpcmtaf"
          OPEN      NZPCMTA3,"nzpcmtaf"
.
          DISPLAY   *P64:24,"nzpfigaf"
          OPEN      NZPFIGA1,"nzpfigaf"
          OPEN      NZPFIGA2,"nzpfigaf"
.
          DISPLAY   *P64:24,"nzpfinaf"
          OPEN      NZPFINA1,"nzpfinaf"
          OPEN      NZPFINA2,"nzpfinaf"
.
          DISPLAY   *P64:24,"nzpibraf"
          OPEN      NZPIBRA1,"nzpibraf"
.
          DISPLAY   *P64:24,"nzpmchaf"
          OPEN      NZPMCHA1,"nzpmchaf"
          OPEN      NZPMCHA2,"nzpmchaf"
.
          DISPLAY   *P64:24,"nzppicaf"
          OPEN      NZPPICA1,"nzppicaf"
          OPEN      NZPPICA2,"nzppicaf"
.
          DISPLAY   *P64:24,"nzprbraf"
          OPEN      NZPRBRA1,"nzprbraf"
.
          DISPLAY   *P64:24,"nzprfnaf"
          OPEN      NZPRFNA1,"nzprfnaf"
          OPEN      NZPRFNA2,"nzprfnaf"
.
          DISPLAY   *P64:24,"nzppfeaf"
          OPEN      NZPPFEA1,"nzppfeaf"
.
          DISPLAY   *P64:24,"nzpspraf"
          OPEN      NZPSPRA1,"nzpspraf"
          OPEN      NZPSPRA2,"nzpspraf"
.
          DISPLAY   *P64:24,"nzptfeaf"
          OPEN      NZPTFEA1,"nzptfeaf"
          OPEN      NZPTFEA2,"nzptfeaf"
.
          DISPLAY   *P64:24,"oprdetaf"
          OPEN      OPRDETA1,"oprdetaf"
.
          DISPLAY   *P64:24,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"oprsesaf"
          OPEN      OPRSESA1,"oprsesaf"
.
          OPEN      PMSEVTA1,"pmsevtaf"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATMCHG1,"patmchgf"
.
          IF        IBCNUGST = TWO
            DISPLAY   *P64:24,"patgtuaf";
            OPEN      PATGTUA1,"patgtuaf"
          ENDIF
.
          DISPLAY   *P64:24,"patipenf";
          OPEN      PATIPEN1,"patipenf"
.
...  used in ABFAECCL
          CALL      TFILENAM                * Get file name calculating Exponent
          MOVE      TFILNAME,FNAMEN
.
...  used in PATCATBI CRMV0000
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEX
.
...  used in PATCATBI PTMP0000
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEZ
.
...  used in PATCATBI PT2P0000
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEY
.
...  used in PABXBILL
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PBXFNAME
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PBXFNAM2
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEP
.
INIT9999  RETURN
.
.******************************************************************************
.*                                OPTN0000             Called by: MAIN0000    *
.*                  Get user options or exit                                  *
.*                                                                            *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                    *
.*                        TRUE  (1)  Exit option selected                     *
.*              COPTION = 0 Exit                                              *
.*                        1 Run Report                                        *
.******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Inpatient":
                    *P1:6,*V2LON,TWO,*HOFF," = Outpatient":
                    *P1:7,*V2LON,THREE,*HOFF," = Emergency"
.
OPTN1000  KEYIN     *P1:9,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:9,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL             * exit
.
          BRANCH    COPTION,OPTN9000,OPTN9000,OPTN9000
.
.         BEEP
.         GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          MOVE      COPTION,SYSTOPTN
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.         Processing
.******************************************************************************
PROC0000  CALL      CRTP0000          * Create Temporary file
.
          DISPLAY   *P1:24,*EL,*P20:24,"Admission No :":
                    *P50:24,"Scanning :";
.
          CALL      HEAD0000
          MOVE      ZERO,HEADFLAG
.
          BRANCH    SYSTOPTN,PROC1000,PROC2000,PROC3000
          GOTO      PROC9999
.
PROC1000  CALL      INPP0000          * Process Inpatient
          GOTO      PROC8000
.
PROC2000  CALL      OUTP0000          * Process Inpatient
          GOTO      PROC8000
.
PROC3000  CALL      EMRP0000          * Process Inpatient
.
PROC8000  CALL      PEND0000          * End of report
.
PROC9999  RETURN
+
.******************************************************************************
.         Discharges
.******************************************************************************
INPP0000  PACK      KEY16 WITH FRMDATE,SP8
          CALL      RDSDSCH2
INPP1000  CALL      RDKDSCH2
          BRANCH    OVRCD OF INPP9999
.
          MATCH     DDATE,ENDDATE           * check the end date
          GOTO      INPP9999 IF LESS
.
          DISPLAY   *P62:24,DADMNO;
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,INPP1000
.
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,INPP1000
          MATCH     "A",TCINDC2
          GOTO      INPP1000 IF NOT EQUAL    * Not an ABF Funding
.
          MOVE      DADMNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,INPP1000
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INPP1000
.
          PACK      KEY8,DADMNO,SP8
          CALL      RDPMVX11
          BRANCH    OVRCD,INPP1000
.
          IF        IBCNMHOS =1
            MATCH     SP10,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID
              GOTO      INPP1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          CALL      CQUA0000              * Check for Unqualified Baby
          BRANCH    EXIT,INPP1000
.
          MOVE      AADMNO,KEY8
          CALL      IPRT0000              * Check if invoice printed
          BRANCH    EXIT,INPP1000         * no invoice raised
.
          MOVE      ZERO,DISFLG
          MOVE      ONE,PROGFLAG
          MOVE      ZERO,TOTDAYS
          CALL      CALCTBI
          MOVE      NETTOT,GROSSTOT
.
          CALL      ABIN0000              * Get already Invoiced ABF charges
          COMPARE   FORM12P2,GROSSTOT
          GOTO      INPP1000 IF EQUAL     * total invoice hasn't changed
.
          MOVE      AADMNO,ADMISSNO
          CALL      PDAT0000              * Print data
.
          GOTO      INPP1000
.
INPP9999  RETURN
+
.*******************************************************************************
.         Check for Unqualified Event
.*******************************************************************************
CQUA0000  MATCH     SP70,ACARE
          GOTO      CQUA8000 IF EQUAL
.
          MOVE      "CC",KEY2
          PACK      KEY5,KEY2,ACARE
          CALL      RDCODE1
          BRANCH    OVRCD,CQUA8000
.
          MATCH     "5",TCINDC9
          GOTO      CQUA8000 IF NOT EQUAL   * qualified
.
.         Check if the patient were unqualified throughout the stay
.
          PACK      KEY24,AADMNO,SP70
          CALL      RDSCHCO1
CQUA2000  CALL      RDKCHCO1
          BRANCH    OVRCD,CQUA9000     * no change of Care class
.
          MATCH     AADMNO,CHADMN
          GOTO      CQUA9000 IF NOT EQUAL
.
          MATCH     "CC",CHCATG
          GOTO      CQUA2000 IF NOT EQUAL * Not a care class change
.
          PACK      KEY5,CHCATG,CHCODE
          CALL      RDCODE1
          BRANCH    OVRCD,CQUA2000
.
          MATCH     "5",TCINDC9
          GOTO      CQUA2000 IF EQUAL  * Unqualified care type
.
.         found a qualified care type
.
CQUA8000  MOVE      ZERO,EXIT          * Qualified
          GOTO      CQUA9999
.
CQUA9000  MOVE      ONE,EXIT           * Total Unqualified event
          MOVE      ZERO,ABFCHRGE      * zero ABF Charge
.
CQUA9999  RETURN
+
.******************************************************************************
.         Processing Outpatients
.******************************************************************************
OUTP0000  PACK      KEY6,SP70
          CALL      RDSSITA1
OUTP0500  CALL      RDKSITA1
          BRANCH    OVRCD,OUTP9999 
.
          CLOSE     OUTSESA6
          PACK      CFNAME,OSTFILE,FILSESA6,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTSESA6,CFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,OUTP0500
.
          PACK      CFNAME,OSTFILE,FILBOKA1,SP70
          CLOSE     OUTBOKA1
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTBOKA1,CFNAME      
          TRAPCLR   IO
          BRANCH    OVRCD,OUTP0500
.
          PACK      CFNAME,OSTFILE,FILBB1A1,SP70
          CLOSE     OUTBB1A1
          CALL      OPOTBB11
.
          PACK      KEY25,OSTSITE,FRMDATE,SP70
          CALL      RDSSESA6
OUTP1000  CALL      RDKSESA6
          BRANCH    OVRCD,OUTP0500
.
          MATCH     OSESITE,OSTSITE
          GOTO      OUTP0500 IF NOT EQUAL
.
          MATCH     OSEDATE,ENDDATE
          GOTO      OUTP0500 IF LESS
.
          PACK      KEY25,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
          PACK      KEY28,KEY25,SP70
          CALL      RDSBOKA1
OUTP1200  CALL      RDKBOKA1
          BRANCH    OVRCD,OUTP1000
.
          PACK      DIM25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          MATCH     DIM25,KEY25
          GOTO      OUTP1000 IF NOT EQUAL
.
          COMPARE   FIVE,OBASTAT
          GOTO      OUTP1200 IF EQUAL          * not attended
.
          DISPLAY   *P61:24,*EL,*V2LON,OBAOUTNO;  * Display number on screen
.
.         Check for ABF
.
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,OUTP1200
.
          PACK      KEY5,ANSC,ANSL,OBACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,OUTP1200
.
          MATCH     "A",TCINDC2          * ABF Funding ?
          GOTO      OUTP1200 IF NOT EQUAL
.
          MATCH     SP70,OTBBTCOD
          GOTO      OUTP1200 IF EQUAL         * Blank Tier 2
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,OUTP1200
.
          MOVE      OBAURNO,AURNO
          MOVE      AURNO,KEY8
          CALL      RDMAST1                      * Read in the PMI record
          BRANCH    OVRCD,OUTP1200
.
          PACK      KEY8,OBAOUTNO,SP8
          CALL      RDPMVX11
          BRANCH    OVRCD,OUTP1200
.
          MOVE      OBAOUTNO,KEY8
          CALL      IPRT0000              * Check if invoice printed
          BRANCH    EXIT,OUTP1200         * no invoice raised
.
          CLOSE     OUTDTRO1
          CLOSE     OUTDTRO4
          PACK      CFNAME,OSTFILE,FILDTRO1
          OPEN      OUTDTRO1,CFNAME
          OPEN      OUTDTRO4,CFNAME
.
          MOVE      OBAOUTNO,OTNUMB
.
          MOVE      ZERO,IBCNUGST          * Set to No GST for to be invoiced
          MOVE      ONE,PROGFLAG
          MOVE      ONE,PRABFINV
          CALL      OUTTBI                 * Calculate the amt to invoice
          MOVE      NETTOT,GROSSTOT
.
          CALL      ABIN0000              * Get already Invoiced ABF charges
.
          COMPARE   FORM12P2,GROSSTOT
          GOTO      OUTP1200 IF EQUAL     * total invoice hasn't changed
.
          MOVE      OBAOUTNO,ADMISSNO
          CALL      PDAT0000              * Print data
.
          GOTO      OUTP1200
.
OUTP9999  RETURN
+
.******************************************************************************
.         Processing Emergency
.******************************************************************************
EMRP0000  OPEN      EMRVISA4,"emrvisaf" 
          PACK      KEY24,FRMDATE,SP70
          CALL      RSEMVIS4
EMRP1000  CALL      RKEMVIS4
          BRANCH    OVRCD,EMRP9999
.
          MATCH     EMVIDATE,ENDDATE
          GOTO      EMRP9999 IF LESS
.
          COMPARE   FOUR,EMVISTAT
          GOTO      EMRP1000 IF EQUAL      * Cancelled
.
          DISPLAY   *P61:24,*EL,*V2LON,EMVIADMN;  * Display number on screen
.
          MOVE      EMVIADMN,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD OF EMRP1000
.
          CALL      RDDETA1
          BRANCH    OVRCD OF EMRP1000
.
          PACK      KEY5,ANSC,ANSL,ADACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,EMRP1000
.
          MATCH     "A",TCINDC2          * ABF Funding ?
          GOTO      EMRP1000 IF NOT EQUAL
.
          MOVE      ADAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF EMRP1000
.
          MOVE      ADANUMB,KEY8
          CALL      IPRT0000              * Check if invoice printed
          BRANCH    EXIT,EMRP1000         * no invoice raised
.
          MOVE      ZERO,IBCNUGST         * set no GST for pending report
          MOVE      ONE,PROGFLAG
          MOVE      ONE,PRABFINV
          CALL      AAETBI
          MOVE      NETTOT,GROSSTOT
.
          CALL      ABIN0000              * Get already Invoiced ABF charges
.
          COMPARE   FORM12P2,GROSSTOT
          GOTO      EMRP1000 IF EQUAL     * total invoice hasn't changed
.
          MOVE      ADANUMB,ADMISSNO
          CALL      PDAT0000              * Print data
          GOTO      EMRP1000
.
EMRP9999  RETURN
+
.******************************************************************************
.         Check if invoice is printed
.******************************************************************************
IPRT0000  MOVE      SP70,ABFINVNO
          MOVE      ONE,EXIT
          PACK      KEY16,KEY8,Z70
          CALL      RDSINV3
IPRT1000  CALL      RDPINV3
          BRANCH    OVRCD,IPRT9999
.
          MATCH     KEY8,DPINVADM
          GOTO      IPRT9999 IF NOT EQUAL
.
          COMPARE   THREE,PTINCMIX
          GOTO      IPRT1000 IF NOT EQUAL   * Not an ABF invoice
.
          MATCH     "0",PTINCRST
          GOTO      IPRT1000 IF NOT EQUAL   * Credited
.
          MOVE      ZERO,EXIT          * invoice is raised
          MOVE      PINVNO,ABFINVNO
.
IPRT9999  RETURN
+
.******************************************************************************
.         Get already Invoiced ABF charged
.******************************************************************************
ABIN0000  MOVE      ZERO,FORM12P2
          BRANCH    SYSTOPTN,ABIN2000,ABIN4000,ABIN6000
          GOTO      ABIN9999
.
.         Inpatient
.
ABIN2000  MOVE      "9",KEY1              * For ABF record
          PACK      KEY23,ABFINVNO,KEY1,SP70
          CALL      RDSDTRN5
ABIN2200  CALL      RDKDTRN5
          BRANCH    OVRCD,ABIN9999
.
          MATCH     ABFINVNO,TREF
          GOTO      ABIN9999 IF NOT EQUAL
.
          MATCH     KEY1,DTRECTYP
          GOTO      ABIN2200 IF NOT EQUAL
.
          MOVE      TPATAMTT,FORM12P2
          GOTO      ABIN9999
.
.         Outpatient
.
ABIN4000  MOVE      "7",KEY1
          PACK      KEY23,ABFINVNO,KEY1,SP70
          CALL      RDSDTRO4
ABIN4200  CALL      RDKDTRO4
          BRANCH    OVRCD,ABIN9999
.
          MATCH     ABFINVNO,OTINVNO
          GOTO      ABIN9999 IF NOT EQUAL
.
          MATCH     KEY1,DOTRECTY
          GOTO      ABIN4200 IF NOT EQUAL
.
          MOVE      OTPATAMT,FORM12P2
          GOTO      ABIN9999
.
.         Emergency
.
ABIN6000  MOVE      "6",KEY1
          PACK      KEY23,ABFINVNO,KEY1,SP70
          CALL      RDSDTRE4
ABIN6200  CALL      RDKDTRE4
          BRANCH    OVRCD,ABIN9999
.
          MATCH     ABFINVNO,ATINVNO
          GOTO      ABIN9999 IF NOT EQUAL
.
          MATCH     KEY1,DATRECTY
          GOTO      ABIN6200 IF NOT EQUAL
.
          MOVE      ATPATAMT,FORM12P2
          GOTO      ABIN9999
.
ABIN9999  RETURN
+
.******************************************************************************
.         Print record 
.******************************************************************************
PDAT0000  UNPACK    PGNAME,KEY1
          MOVE      ".",ANS
          PACK      KEY22,KEY1,ANS,PSNAME
          PRINT     *1,"|",KEY22,*25,"| ",PVIBILL;
.
          BRANCH    SYSTOPTN,PDAT2000,PDAT4000,PDAT6000
          GOTO      PDAT8000
.
.         Print Inpatient data
.
PDAT2000  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *37,"|",CPCDATE;
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *49,"|",CPCDATE;
.                   *61,"|":
.                   *70,"|",PTDSDDRG;
          GOTO      PDAT8000
.
.         Print Outpatient data
.
PDAT4000  UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *37,"| ",CPCDATE;
          GOTO      PDAT8000
.
.         Print Emergency data
.
PDAT6000  
.         UNPACK    SP70,DIM6,DIM6A
.
.         PACK      KEY19,ADMISSNO,ABFINVNO,ZERO,SIX,ZERO,SP70
.         CALL      RDPMABF1
.         IF        OVRCD=0
.           UNPACK    PMABCDES,DIM6                      * URG
.         ENDIF
.
.         PACK      KEY19,ADMISSNO,ABFINVNO,ZERO,SIX,FIVE,SP70
.         CALL      RDPMABF1
.         IF        OVRCD=0
.           UNPACK    PMABCDES,DIM6A                     * MDB
.         ENDIF
.
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *37,"| ",CPCDATE;
.         PRINT     *54,"|",DIM6A,*62,"|",DIM6;
          GOTO      PDAT8000
.
PDAT8000  CALL      DADJ0000           * Print adjustment
.
PDAT9999  RETURN
+
.******************************************************************************
.         Print patient Adjustment
.******************************************************************************
DADJ0000  CLOSE     PMSABFA1
          CLOSE     TMPABFA1
.
          OPEN      PMSABFA1,"pmsabfaf"
          OPEN      TMPABFA1,FNAMEP
.
.         Read New adjustment 
.
          MOVE      ZERO,HEADFLAG
          MOVE      ZERO,COUNTER
          MOVE      SP70,PMABINVN
.
DADJ1000  ADD       ONE,COUNTER
          COMPARE   "66",COUNTER
          GOTO      DADJ9000 IF NOT LESS
.
          MOVE      COUNTER,DCOUNTR
          REP       " 0",DCOUNTR
.
          MOVE      ZERO,NEWVALUE
          MOVE      ZERO,FORM12P4
.
          MOVE      SP70,PMABINVN
          PACK      KEY19,ADMISSNO,PMABINVN,DCOUNTR,SP70
          CALL      RDTMABF1
          IF        OVRCD=0
            MOVE      PMABADJV,NEWVALUE        * New amount
          ENDIF
.
          PACK      KEY19,ADMISSNO,ABFINVNO,DCOUNTR,SP70
          CALL      RDPMABF1
          IF        OVRCD=0
            MOVE      PMABADJV,FORM12P4        * already raised amount
          ENDIF
.
          COMPARE   FORM12P4,NEWVALUE
          GOTO      DADJ1000 IF EQUAL
.
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    SYSTOPTN,DADJ2100,DADJ2200,DADJ2300
          GOTO      DADJ9999
.
DADJ2100  UNPACK    SP70,DIM8
.
          MOVE      PMABADJT,DIM3
.
          PACK      KEY19,ADMISSNO,ABFINVNO,ZERO,SIX,TWO,SP70
          CALL      RDPMABF1
          IF        OVRCD=0
            UNPACK    PMABADRG,DIM8                      * DRG
          ENDIF
.
          IF        HEADFLAG=1
            PRINT     *1,"| ",*25,"|":
                      *37,"|",*49,"|",*61,"|",*70," ";
          ELSE
          PRINT     *61,"|",DIM8:
                    *70,"/",PTDSDDRG;
          ENDIF
.
          PRINT     *79,"| ",DIM3,*89,"| ",NEWVALUE,*109,"| ",FORM12P4:
                    *132,"|"
          GOTO      DADJ4000
.
DADJ2200  IF        HEADFLAG=1
            PRINT     *1,"| ",*25,"|":
                      *37,"|";
          ENDIF
          PRINT     *54,"| ",PMABADJT,*64,"| ",NEWVALUE,*85,"| ",FORM12P4:
                    *132,"|"
          GOTO      DADJ4000
.
DADJ2300  UNPACK    SP70,DIM6,DIM6A
.
          MOVE      PMABADJT,DIM3 
.
          PACK      KEY19,ADMISSNO,ABFINVNO,ZERO,SIX,ZERO,SP70
          CALL      RDPMABF1
          IF        OVRCD=0
            UNPACK    PMABCDES,DIM6                      * URG
          ENDIF
.
          PACK      KEY19,ADMISSNO,ABFINVNO,ZERO,SIX,FIVE,SP70
          CALL      RDPMABF1
          IF        OVRCD=0
            UNPACK    PMABCDES,DIM6A                     * MDB
          ENDIF
.
          IF        HEADFLAG=1
            PRINT     *1,"| ",*25,"|":
                      *37,"|",*54,"|",*62,"|";
          ELSE
            PRINT     *54,"| ",DIM6A,*62,"| ",DIM6;
          ENDIF
          PRINT     *70,"| ",DIM3,*80,"| ",NEWVALUE,*101,"| ",FORM12P4:
                    *132,"|"
.
          IF        HEADFLAG<>1
.
            UNPACK    SP70,DIM6B,DIM6C
.
            CALL      CURG0000
            IF        EXIT=0
              MOVE      PMVMMCOD,DIM6B
              MOVE      EMURURGV,DIM6C
            ENDIF
.
            PRINT     *1,"| ",*25,"|":
                      *37,"|",*54,"| ",DIM6B,*62,"| ",DIM6C;
            PRINT     *70,"| ",*80,"| ",*101,"| ":
                      *132,"|"
.
            ADD       ONE,CLNO
          ENDIF
.
.
DADJ4000  MOVE      ONE,HEADFLAG
          ADD       ONE,CLNO
          GOTO      DADJ1000
.
DADJ9000  IF        HEADFLAG=0
            PRINT     *64,"|",*132,"|";
          ENDIF
.
          IF        SYSTOPTN=ONE
.
            PRINT     *1,"|",*25,"|",*37,"|",*49,"|",*61,"|",*70," ";
            PRINT     *79,"| ",*89,"| ",*109,"| ",*132,"|"
        PRINT     *1,"|Subtotal: $(NWAU x NEP)",*25,"|",*37,"|",*49,"|",*61,"|";
            PRINT     *70," ";
            PRINT     *79,"| ",*89,"|   ",GROSSTOT,*109,"|   ",FORM12P2,*132,"|"
.
          ELSE
            IF        SYSTOPTN=TWO
.
              PRINT     *1,"| ",*25,"|":
                        *37,"|";
              PRINT     *54,"| ",*64,"| ",*85,"| ",*132,"|"
              PRINT     *1,"|Subtotal: $(NWAU x NEP)",*25,"|":
                        *37,"|";
              PRINT     *54,"| ",*64,"|   ",GROSSTOT,*85,"|   ",FORM12P2:
                        *132,"|"
.
            ELSE
.
              PRINT     *1,"| ",*25,"|":
                        *37,"|",*54,"|",*62,"|";
              PRINT     *70,"| ",*80,"| ",*101,"| ",*132,"|"
              PRINT     *1,"|Subtotal: $(NWAU x NEP)",*25,"|":
                        *37,"|",*54,"|",*62,"|";
              PRINT     *70,"| ",*80,"|   ",GROSSTOT,*101,"|   ",FORM12P2:
                        *132,"|"
.
            ENDIF
.
          ENDIF
.
DADJ9900  CALL      UND132
.
          CLOSE     PMSABFA1
          CLOSE     TMPABFA1
          OPEN      PMSABFA1,FNAMEP
.
DADJ9999  RETURN
+
.******************************************************************************
.         Header
.******************************************************************************
HEAD0000  IF        SYSTOPTN=1
            MOVE      "- Inpatient",CPHDROPT
          ELSE
            IF        SYSTOPTN=2
              MOVE      "- Outpatient",CPHDROPT
            ELSE
              IF        SYSTOPTN=3
                MOVE      "- Emergency",CPHDROPT
              ENDIF
            ENDIF
          ENDIF
          CALL      HEAD132
.
          UNPACK    FRMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *37,"From Date : ",CPCDATE;
.
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *63,"to : ",CPCDATE
.
          CALL      UND132
          PRINT     "|Name",*25,"| Visit No.";
.
          BRANCH    SYSTOPTN,HEAD2000,HEAD3000,HEAD4000
          GOTO      HEAD9999
.
HEAD2000  PRINT     *37,"|Adm.Date",*49,"|Dis.Date",*61,"|Inv DRG":
                    *70,"/ DRG":
                    *79,"|Adj.Type",*89,"|   Recalc.Value":
                    *109,"| Raised Invoice Value":
                    *132,"|"
          GOTO      HEAD9000
.
HEAD3000  PRINT     *37,"|Attend.Date":
                    *54,"|Adj.Type",*64,"|   Recalc.Value":
                    *85,"| Raised Invoice Value":
                    *132,"|"
          GOTO      HEAD9000
.
HEAD4000  PRINT     *37,"|Attend.Date":
                    *54,"|Inv MDB",*62,"|Inv URG":
                    *70,"|Adj.Type",*80,"|   Recalc.Value":
                    *101,"| Raised Invoice Value":
                    *132,"|"
.
          PRINT     "|",*25,"|",*37,"|":
                    *54,"|    MDB",*62,"|    URG":
                    *70,"|",*80,"|":
                    *101,"|":
                    *132,"|"

.
          GOTO      HEAD9900
.
HEAD9000  CALL      UND132
          ADD       FOUR,CLNO
          GOTO      HEAD9999
.
HEAD9900  CALL      UND132
          ADD       FIVE,CLNO
          GOTO      HEAD9999
.
HEAD9999  RETURN
+
.******************************************************************************
.         Keyin from and to date
.******************************************************************************
KDAT0000  UNPACK    SP70,FRMDATE,ENDDATE
          KEYIN     *P1:4,*EF:
                    *P1:6,"  From Date : ":
                    *P1:8,"  To   Date : "
.
.         From date
.
KDAT1000  UNPACK    SP70 WITH CCENT,CYEAR,CMON,CDAY
          MOVE      "6",CVERT
          MOVE      "24",CCOL
          CALL      KEYDATE                 * Keyin from date
          BRANCH    OVRCD,KDAT9999
.
          MATCH     SP2,CDAY
          GOTO      KDAT9999 IF EQUAL
.
          DISPLAY   *P24:6,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON:
                    *HOFF,SLASH,*V2LON,CCENT,CYEAR
.
          PACK      FRMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FRMDATE
.
          MATCH     FRMDATE,CURRDATE
          GOTO      KDAT8000 IF LESS
.
.         End date
.
KDAT2000  UNPACK    SP70 WITH CCENT,CYEAR,CMON,CDAY
          MOVE      "8",CVERT
          MOVE      "24",CCOL
          CALL      KEYDATE
.
          DISPLAY   *P24:8,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON:
                    *HOFF,SLASH,*V2LON,CCENT,CYEAR
.
          PACK      ENDDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          MATCH     ENDDATE,CURRDATE
          GOTO      KDAT8200 IF LESS
.
          MATCH     FRMDATE,ENDDATE
          GOTO      KDAT9999 IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                    "To Date **",*W2;
          GOTO      KDAT1000
.
KDAT8000  DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                    "current Date **",*W2;
          GOTO      KDAT1000
.
KDAT8200  DISPLAY   *P1:24,*B,*EL,*V2LON,"** To Date must be <= ":
                    "to current Date **",*W2;
          GOTO      KDAT2000
.
KDAT9999  RETURN
+
.******************************************************************************
.         Keyin Hospital id
.******************************************************************************
KHOS0000  MOVE       SP10,HOSPID
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          DISPLAY    *P1:20,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       "20",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS2000,KHOS9999
          MOVE       PTHSHOSP,HOSPID
          MOVE       PTHSNAME,HOSPNAME
          GOTO       KHOS4000
.
KHOS2000  MOVE       "All Hospitals",HOSPNAME
          MOVE       SP10,HOSPID
.
KHOS4000  DISPLAY    *P23:20,*V2LON,PTHSNAME;
KHOS9999  RETURN
+
.******************************************************************************
.         End of Report
.******************************************************************************
PEND0000  PRINT     *F,*N:
                    *N,"001 - Same-Day Payment List (1=Yes, 0 or blank=No)":
                    *N,"002 - Bundled ICU (1=Yes, 0 or blank=No)":
                    *N,"003 - ALOS (excluding designated SD and unbundled ICU)":
                    *N,"004 - Lower bound (Inlier Bounds)":
                    *N,"005 - Upper bound (Inlier Bounds)":
                    *N,"006 - Private Patient Service (Adjustments) %":
                    *N,"007 - Paediatric (Adjustments) %":
                    *N,"008 - Same Day (Price Weight)":
                    *N,"009 - Short Stay Outlier Base (Price Weight)":
                    *N,"010 - Short Stay Outlier Per Diem (Price Weight)":
                    *N,"011 - Inlier (Price Weight)":
                    *N,"012 - Long Stay Outlier Per Diem (Price Weight)":
                    *N,"013 - Specialist Psychiatric Age <=17 w MDC=19 ":
                       "or 20 Adjustment %":
                    *N,"014 - Specialist Psychiatric Age <=17 no MDC=19 ":
                       "or 20 Adjustment %":
                    *N,"015 - Specialist Psychiatric Age >17 no MDC=19 ":
                       "or 20 Adjustment %":
                    *N,"016 - Patient Remoteness Area Adjustment - ":
                       "Outer % - R2":
                    *N,"017 - Patient Remoteness Area Adjustment - ":
                       "Remote % - RA3":
                    *N,"018 - Patient Remoteness Area Adjustment - ":
                       "Remote % (ED Service)":
                    *N,"019 - Patient Remoteness Area Adjustment - ":
                       "Very Remote % - RA4":
                    *N,"020 - Patient Remoteness Area Adjustment - ":
                       "Very Remote % (ED Service)":
                    *N,"021 - Indigenous Adjustment %":
                    *N,"022 - Radiotherapy Adjustment %":
                    *N,"023 - Dialysis Adjustment %":
                    *N,"024 - ICU Adjustment":
                    *N,"025 - Private Patient Accommodation Adjustment ":
                       "Per Diem Price Weight Same day":
                    *N,"026 - Private Patient Accommodation Adjustment ":
                       "Per Diem Price Weight Overnight":
                    *N,"027 - Private Patient Accommodation Adjustment % ":
                       "Admitted Sub-Acute and Non-Acute - Palliative Care":
                    *N,"028 - Private Patient Accommodation Adjustment % ":
                       "Admitted Sub-Acute and Non-Acute - Rehabilitation":
                    *N,"029 - Private Patient Accommodation Adjustment % ":
                      "Admitted Sub-Acute and Non-Acute - Psychogeriatric Care":
                    *N,"030 - Private Patient Accommodation Adjustment % ":
                       "Admitted Sub-Acute and Non-Acute - GEM":
                    *N,"031 - Private Patient Accommodation Adjustment % ":
                       "Admitted Sub-Acute and Non-Acute - Maintenance":
                    *N,"032 - Tier 2":
                    *N,"033 - Multidisciplinary Clinic Adjustment %":
                    *N,"034 - Emergency Care Age Adjustment % 65-79 years old":
                    *N,"035 - Emergency Care Age Adjustment % ":
                       "Over 79 years old":
                    *N,"036 - Hospital Remoteness Area Adjustment - Remote ":
                       "(Admitted Acute or Sub Acute)":
                    *N,"037 - Hospital Remoteness Area Adjustment - Very ":
                       "Remote (Admitted Acute or Sub Acute)":
                    *N,"038 - HAC 1 - Pressure Injury":
                    *N,"039 - HAC 2 - Falls resulting in fracture or ":
                       "other intracranial injury":
                    *N,"040 - HAC 3 - Healthcare associated infection":
                    *N,"041 - HAC 4 - Surgical complications requiring ":
                       "unplanned return to theatre":
                    *N,"042 - HAC 6 - Respiratory complications":
                    *N,"043 - HAC 7 - Venous thromboembolism":
                    *N,"044 - HAC 8 - Renal failure":
                    *N,"045 - HAC 9 - Gastrointestinal bleeding":
                    *N,"046 - HAC 10 - Medication complications":
                    *N,"047 - HAC 11 - Delirium":
                    *N,"048 - HAC 12 - Persistent incontinence":
                    *N,"049 - HAC 13 - Malnutrition":
                    *N,"050 - HAC 14 - Cardiac complications":
                    *N,"051 - HAC 15":
                    *N,"052 - HAC 16":
                    *N,"053 = Calculated NWAU (National Weighted Activity ":
                       "Unit)":
                    *N,"054 = Clinic Header Code and Description":
                    *N,"055 = ED Type of Visit (EMR)":
                    *N,"056 = ED Episode End Status (EMR)":
                    *N,"057 = ED Triage Category (EMR)":
                    *N,"058 = ED ICD10 Code & Description":
                    *N,"059 = Charlson Score":
                    *N,"060 = URG Code (EMR)":
                    *N,"061 = NEP":
                    *N,"062 = Patient Price Weight (PW)":
                    *N,"063 = HAC Adjustment":
                    *N,"064 = Patient LOS (ICU-adjusted)":
                    *N,"065 = MDB Code (EMR)"
.
PEND9999  RETURN
+
.******************************************************************************
.         Create tempfile
.******************************************************************************
CRTP0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEP,CMDLINE
          APPEND    " 335 U1-8,9-16,17-19 U9-16,17-19,1-8 ",CMDLINE
          APPEND    "U1-8,17-19,9-16",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * isbuild the temp file
.
          OPEN      PMSABFA1,FNAMEP
          MOVE      ZERO,EXIT
.
CRTP9999  RETURN
+
.******************************************************************************
.*                                 KILL0000               Called by: ML0000   *
.*                            Delete The Tempfile                  & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     PMSABFA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * iserase the temp file
.
KILL9000  MOVE      ZERO,EXIT
KILL9999  RETURN
+
.*******************************************************************************
.         Generate Patient URG code
.*******************************************************************************
CURG0000  PACK      KEY17,ADMISSNO,SP70
          CALL      RSEMVCD2
CURG1000  CALL      RKEMVCD2
          BRANCH    OVRCD,CURG9000
.
          MATCH     ADMISSNO,EMVCVIST
          GOTO      CURG9000 IF NOT EQUAL
.
          COMPARE   FIVE,EMVCCSYS             * check for coding system emricdaf
          GOTO      CURG1000 IF NOT EQUAL
.
.         Get Mapping MDB Code for ICD
.
          MOVE      "emricdaf",KEY8
          PACK      KEY30,EMVCMNCD,SP70
          MOVE      "MDB",PMVMMVSI           * Set ID
          PACK      KEY41,KEY8,KEY30,PMVMMVSI,SP70
          CALL      RDPMVMT1
          BRANCH    OVRCD,CURG1000           * Next diagnosis code
.
          PACK      EMURMDBC,PMVMMCOD,SP70   * MDB Code
          MOVE      EMVIDSTA,EMURESTA        * Episode End status
          MOVE      EMVITRIG,EMURTRIA        * Triage code
          MOVE      EMVIUC13,EMURVTYP        * Type of visit
          PACK      EMUREDAT,EMVIDATE
.
.         Use MDB Code to get URG Code
.
          PACK      SKEY39,EMURESTA,EMURTRIA,EMURVTYP,EMURMDBC,SP70
          PACK      KEY47,SKEY39,EMVIDATE,SP70
          CALL      RDEMURG2
          COMPARE   ZERO,OVRCD
          GOTO      CURG6000 IF EQUAL
.
CURG2000  CALL      RKEMURG2
          BRANCH    OVRCD,CURG1000
.
          PACK      KEY39,EMURESTA,EMURTRIA,EMURVTYP,EMURMDBC,SP70
          MATCH     KEY39,SKEY39
          GOTO      CURG1000 IF NOT EQUAL
.
          MATCH     EMVIDATE,EMUREDAT
          GOTO      CURG2000 IF LESS
.
CURG6000  MOVE      ZERO,EXIT
          GOTO      CURG9999
.
CURG9000  MOVE      ONE,EXIT
CURG9999  RETURN
+
.******************************************************************************
RDTMABF1  RESET     KEY19
          MOVE      ZERO,OVRCD
          READ      TMPABFA1,KEY19;PMABADMN,PMABINVN,PMABADJT,PMABINVC,PMABABFT:
                                   PMABEPSN,PMABADRG,PMABDRGD,PMABADJV,PMABCDES:
                                   PMABIMPR,PMABCDAT,PMABCTIM,PMABCUID,PMABUDAT:
                                   PMABUTIM,PMABUUID,PMABSPAR
          GOTO      OVERCOND IF OVER
          RETURN
+
.******************************************************************************
.         IO includes
.
          INC       STD001IO
          INC       AAECATBI
          INC       AAEDE1IO/INC
          INC       AAEDTRIO/INC
          INC       EMRVCDIO/INC
          INC       EMRURGIO/INC
          INC       CALCAGE
          INC       CLNZPPIC
          INC       EXCLNEWB
          INC       IBAGEDIO/INC
          INC       IBAGSTIO/INC
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
          INC       IBATMDIO/INC
          INC       NHIMASIO/INC
          INC       MLTCFNIO/INC

          INC       NZPBFEIO/INC
          INC       NZPCFNIO/INC
          INC       NZPCMTIO/INC
          INC       NZPEXTIO/INC
          INC       NZPRDTIO/INC
          INC       NZPFIGIO/INC
          INC       NZPIBRIO/INC
          INC       NZPMCHIO/INC
          INC       NZPMXCIO/INC
          INC       NZPPFEIO/INC
          INC       NZPPICIO/INC
          INC       NZPRBRIO/INC
          INC       NZPRFNIO/INC
          INC       NZPFINIO/INC
          INC       NZPSPRIO/INC
          INC       NZPTFEIO/INC
          INC       NZPIVBIO/INC
          INC       NZPRVBIO/INC
.
          INC       OPRDEAIO/INC
          INC       OPRSESIO/INC
          INC       OUTCATBI
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       OUTSESIO/INC
          INC       PATCATBI
          INC       PATCODDS
          INC       CHKCMXPT                * Check if casemix & get epis type
          INC       PATCMSTP
          INC       PATGNCMX
          INC       CMGETTHR
          INC       CMXMATRX
          INC       NHOSPDAY
          INC       CALCDAYS
          INC       PABXBILL
          INC       PATCOMN3
          INC       PATIVMES
          INC       CLPATDTR                * Clear DTR file variables
          INC       PATHSPKY
          INC       PRBILCMN
          INC       PATMCHRD
          INC       PATITMRD
          INC       GETCNEFF
          INC       GETBFEES
          INC       GETTFEES
.
          INC       PATAFEIO/INC       * for casemix
          INC       PATASDIO/INC
          INC       PATCMXIO/INC
          INC       PATHDFIO/INC
          INC       PATHLFIO/INC
          INC       PATLDFIO/INC
          INC       PATLSDIO/INC
.
          INC       PATCHCIO/INC
          INC       PATELGIO/INC
          INC       PATBFEIO/INC
          INC       PATTFEIO/INC       *theatre fee
          INC       PATCODIO/INC
          INC       PMSABFIO/INC
          INC       PMSCIDIO/INC
          INC       PMSCCDIO/INC
          INC       PATCFAIO/INC
          INC       PATCTFIO/INC
          INC       PATDGWIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATDTRIO/INC
          INC       PATDRGIO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATGTUIO/INC
          INC       PATRE1IO/INC
          INC       PATHSPIO/INC
          INC       PATHTRIO/INC
          INC       EMRHTRIO/INC
          INC       OUTHTRIO/INC
          INC       PATFINIO/INC
          INC       PATFMOIO/INC
          INC       PATFX1IO/INC
          INC       PATFN1IO/INC
          INC       PATFN2IO/INC
          INC       PATIBLIO/INC
          INC       PATICUIO/INC
          INC       PATIN1IO/INC
          INC       PATINMIO/INC
          INC       PATINVIO/INC
          INC       PATIOVIO/INC
          INC       PATIPEIO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       PATMCHIO/INC
          INC       PATMI1IO/INC
          INC       PATOVBIO/INC
          INC       PATPGRIO/INC
          INC       PATRBLIO/INC
          INC       PATRCAIO/INC
          INC       PATRATIO/INC
          INC       PATSRBIO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATVENIO/INC
          INC       PMSHATIO/INC
          INC       PATWC1IO/INC
          INC       PATWMAIO/INC
          INC       PATWR1IO/INC
          INC       PATWVEIO/INC
          INC       PMSEVTIO/INC
          INC       PMSCNOIO/INC
          INC       PMSHCPIO/INC
          INC       PMSSINIO/INC
          INC       PATCMTIO/INC
          INC       PMSVMTIO/INC
          INC       PMSCMTIO/INC
          INC       PMSSGAIO/INC
          INC       PMSMTIIO/INC
          INC       PMSMPRIO/INC
          INC       PMSVX1IO/INC
          INC       RCPBNKIO/INC
          INC       RCPBDTIO/INC
          INC       OPRARDIO/INC
          INC       PRIITMIO/INC
.
          INC       PATAA1IO/INC
          INC       PATSTAIO/INC
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       EMRSITIO/INC
          INC       EMRHISIO/INC
          INC       EMRVISIO/INC
          INC       COMDEPIO/INC
          INC       RCPDTEIO/INC
          INC       PMSPBRIO/INC
          INC       PMSIDEIO/INC
          INC       PMSEDGIO/INC       * Effective DRG Dates
          INC       PATDDHIO/INC
          INC       CLPMSIDE
          INC       STEPDOWN
          INC       VALCMXFN
          INC       ICDSCLAS
          INC       PATSELFN
          INC       TFILENAM                * Generate Temp File Name
          INC       GETEFDRG
.
SBDY0000
PATSTRYR
ADDWRN00
WRITETRN
GETTHR
PATCALF
TAIL
INITIVAR
PRTEOL
PROCMISC
PRTCLMS
NXTTRAN1
WEBERR00
PRTINVTL
SINVT000
DOCNM000
PEOL0000
IPRH0000
PRLN0000
ENDPRT00
PROACC00
CKTL0000
PRTIPROV
ENDPRT    RETURN
.

.*******************************************************************************
.* System    :   Reports                                                       *
.* Program   :   IBARSH18                                                      *
.* Desc      :   Schedule program to create new Storage Locations              *
.*******************************************************************************
.* Date      :   12/04/2013                                                    *
.* Author    :   Ania P                                                        *
.* Function  :   This program will create a storage location at regular        *
.*           :   intervals, as determined by the schedule.                     *
.*           :                                                                 *
.* Mods      :                                                                 *
.*           :   V10.08.01 12/05/2016 Ebon Clements       TSK 0297390          *
.*           :             Added DocumentView.php url prefix functionality     *
.*           :   V10.03.00 12/04/2013 Ania P              CAR 280425           *
.*           :             Created program                                     *
.*******************************************************************************
.
          INC       STD001FD
          INC       PATDO1FD/INC
          INC       OBSPCTFD/INC
          INC       WEBCONFD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CURRSLOC  FORM      4                      * Current Storage Location KEY
MNTHPATH  DIM       2                      * Subdirectory of YEARPATH
YEARPATH  DIM       4                      * Subdirectory of Storage Location
OLDDATE   FORM      6
NEWDATE   FORM      6
OLDSDIR   DIM       80
OLDURLP   DIM       80
DATEDIFF  FORM      6

.
CMDLINE   DIM       100                    * Command line for creating new dir.
MKDIR     INIT      "mkdir -p 0777 "
DIM6      DIM       6
DIM7      DIM       7
DIM8      DIM       8
DOCUVPHP  INIT      "DocumentView.php?"
FSLASH    INIT      "/"
FULLDATE  DIM       8
FILECGIN  INIT      "&filename="
LOCNCGIN  INIT      "&location="
MINUS1    INIT      "-1"
MINUS7    INIT      "-7"
.
PRGID     INIT      "IBARSH18"
PRGNAM    INIT      "Create New Storage Location"
VERSION   INIT      "V12.01.00"
+
.*******************************************************************************
.*                              ML0000                                         *
.*                  Controlling Logic (Mainline code)                          *
.*******************************************************************************
.
ML0000    CALL      INIT0000                * initialisation and open files
.
ML0100    CALL      OPTN0000                * select options
          BRANCH    EXIT,ML9999             * EXIT = 1 if 0 chosen in menu
.
          BRANCH    OPTION,ML1100
.
          GOTO      ML0100
.
ML1100    CALL      ADDSL000                * Create New Storage Location
          BRANCH    EXIT,ML0100
          GOTO      ML9999
.
ML9999    CHAIN     PGM                     * chain back to program
+
.*******************************************************************************
.*                                      INIT0000                               *
.*    The initialisation routine is used to display headings and open files.   *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      CONTROLF,"controlf"
          OPEN      OBSPCTA1,"obspctaf"
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                      OPTN0000                               *
.*                              Get user options or exit                       *
.*                                                                             *
.*    Returns:  EXIT    = FALSE (0)  Create New Storage Location               *
.*                        TRUE  (1)  Exit option selected                      *
.*******************************************************************************
.
OPTN0000  HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Create New Storage Location";
.
OPTN0500  KEYIN     *P1:22,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:22,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION                                * exit selected ?
          GOTO      OPTN9500 IF EQUAL                          * yes
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*******************************************************************************
.*                                      ADDSL000                               *
.*                             Add a new storage location                      *
.*******************************************************************************
.
ADDSL000  CALL      CONTQST                             * ok to continue
          BRANCH    CEXIT,ADDSL500,ADDSL999,ADDSL999    * 1=Yes, 2=No, 3=Cancel
.
ADDSL500  CALL      ADSL0000
.
ADDSL999  RETURN
.
.*******************************************************************************
.*                                      ADSL0000                               *
.*******************************************************************************
ADSL0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON:
                    "Creating New Storage Location... ";
.
          MOVE      ZERO,CPAGENO            * initialize page counter
          MOVE      ZERO,CLNO               * initialize line counter
          MOVE      ONE,CNOUNDLN            * dont print underlines
          MOVE      SP20,CPHDROPT           * no subheading
          CALL      IBACLOCK                * get date and time
          CALL      HEAD80
.
          READ      CONTROLF,NINTY1;*219,CWEBCLOC   * Read current Storage Loc.
          MATCH     "9999",CWEBCLOC
          GOTO      ADSL9000 IF EQUAL
.
          PACK      KEY4,CWEBCLOC,SP70
          CALL      RDOBPT1
          BRANCH    OVRCD,ADSL9100                  * No Storage Loc. details
.
          MOVE      OBPTSDIR,OLDSDIR
          MOVE      OBPTURLP,OLDURLP
.
          RJUSTIFY  CWEBCLOC                        * Change format to numeric
          REP       " 0",CWEBCLOC
          MOVE      CWEBCLOC,CURRSLOC
.
ADSL1000  ADD       ONE,CURRSLOC                    * Get next free Storage Loc
          PACK      KEY4,CURRSLOC
          REP       " 0",KEY4
          CALL      RDOBPT1
          BRANCH    OVRCD,ADSL2000                  * New Unused Storage Loc KEY
.
          GOTO      ADSL1000                        * Increment KEY & try again
.
ADSL2000  STRIP     OLDSDIR                         * Find last month's YYMM
          STRIP     OLDURLP
          ENDSET    OLDSDIR
          ENDSET    OLDURLP
          BUMP      OLDSDIR,MINUS7
          BUMP      OLDURLP,MINUS7
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,FSLASH,CMM,FSLASH
          REP       " 0",DIM8
          MATCH     DIM8,OLDSDIR
          GOTO      ADSL9200 IF EQUAL               * Directory already exists
.
          IF        CMM=1
            ADD       "11",CMM                      * Check for last months dir
            SUBTRACT  "1",CYY
          ELSE
            SUBTRACT  "1",CMM
          ENDIF
          PACK      DIM8,CCC,CYY,FSLASH,CMM,FSLASH
          REP       " 0",DIM8
          MATCH     DIM8,OLDSDIR
          GOTO      ADSL3000 IF EQUAL               * Overwrite date path
.
          GOTO      ADSL4000                        * Append Date path  
.        
ADSL3000  CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,FSLASH,CMM,FSLASH
          REP       " 0",DIM8
          BUMP      OLDSDIR,MINUS1
          BUMP      OLDURLP,MINUS1
          APPEND    DIM8,OLDSDIR
          APPEND    DIM8,OLDURLP
          RESET     OLDSDIR
          RESET     OLDURLP
          GOTO      ADSL5000
.
ADSL4000  ENDSET    OLDSDIR
          ENDSET    OLDURLP
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,FSLASH,CMM,FSLASH
          REP       " 0",DIM8
          APPEND    DIM8,OLDSDIR
          APPEND    DIM8,OLDURLP
          RESET     OLDSDIR
          RESET     OLDURLP
.
ADSL5000  PACK      OBPTSLID,CURRSLOC
          REP       " 0",OBPTSLID
.
          PACK      OBPTSDIR,OLDSDIR,SP70
          PACK      OBPTURLP,OLDURLP,SP70 
.
          MATCH     DOCUVPHP,OLDURLP             * If DocumentView.php
          IF        @EQUAL
            PACK      OBPTURLP,DOCUVPHP,LOCNCGIN,OBPTSLID,FILECGIN,SP70
          ENDIF
.
          MOVE      ZERO,OBPTROLY
          CALL      IBACLOCK
          PACK      OBPTCRDT,CCC,CYY,CMM,CDD
          REP       " 0",OBPTCRDT
          PACK      OBPTCLDT,SP70
          PACK      OBPTSPAR,SP70
.
          CALL      WROBPT1
.
          PACK      CMDLINE,MKDIR,OBPTSDIR
          EXECUTE   CMDLINE,TASKID
          MATCH     "0",TASKID
          GOTO      ADSL9300 IF NOT EQUAL
.
          WRITAB    CONTROLF,NINTY1;*219,OBPTSLID
          GOTO      ADSL9900
.
ADSL9000  PRINT     "Error: Storage Location record limit reached."
          GOTO      ADSL9999
.
ADSL9100  PRINT     "Error: No Storage Location details in obspctaf."
          GOTO      ADSL9999
.
ADSL9200  PRINT     "Error: Storage Location already exists.";
          GOTO      ADSL9999
.
ADSL9300  PRINT     "Error: Could not create directory.";
          GOTO      ADSL9999
.
ADSL9900  PRINT     "New Storage Location created successfully."
          GOTO      ADSL9999
.
ADSL9999  RETURN
.
.*******************************************************************************
.
          INC       STD001IO
          INC       PATDO1IO/INC
          INC       OBSPCTIO/INC



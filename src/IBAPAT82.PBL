. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAPAT82                                            *
. *                                                                           *
. *     FUNCTION:         IN-PATIENT REPORT                                   *
. *                                                                           *
. *     DATE WRITTEN:     09/08/85                                            *
. *                                                                           *
. *     AUTHOR:           MALCOLM HAYSE (I.B.A)                               *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *       V12.00.01 28/05/2025  J.Tan          TSK 0955096                    *
. *                 Added Alphanumeric visit number changes                   *
. *       V11.02.04 04/05/2022  Jacob Jackson   TSK 0864505                   *
. *                 Replace BRANCH with PERFORM, to fix issue where the way   *
. *                 preferred name was displayed was changed                  *
. *       V11.02.03 01/03/2022  Jacob Jackson   TSK 0864505                   *
. *                 Add preferred names for "Ward/Bed Sequence" selection     *
. *       V11.02.02 10/02/2022  Jacob Jackson   TSK 0864505                   *
. *                 Rename 'PRNAME' to 'PRFRNAME' due to conflict             *
. *       V11.02.01 20/01/2022  Jacob Jackson TSK 0864505                     *
. *                 Added optional preferred name(s) to name column           *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
. *       V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
. *                 Deleted temp file (PSTROL) after processing               *
. *       V10.06.01 14/11/2013  J.Tan          CAR 306640                     *
. *                 Added option to create Export file                        *
. *       V10.05.03 01/01/2015  Ania P         CAR 261630                     *
. *                 Removed use of PORT for temp file naming                  *
. *       V10.05.02 05/12/2014  J.Tan          CAR 307673                     *
. *                 Fixed to recalculate patient age                          *
. *       V10.05.01 22/09/2014  J.Tan          CAR 294962                     *
. *                 Added input Hospital id for multi hospital                *
. *       V10.03.03 08/11/2013 Peter Vela     CAR 286076                      *
. *                 Skip over inactive wards NEXTA                            *
. *       V10.03.02 20/09/2013 Peter Vela     CAR 286076                      *
. *                 Commented out display of inactive beds                    *
. *       V10.03.01 02/07/2013 J.Tan          CAR 286076                      *
. *                 Mods checking for Inactive/Closed bed                     *
. *       V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
. *                 Added pmsvx1af                                            *
. *       V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
. *                 - Add Sex Description routine SEXDSC00 with Code "R" -    *
. *                 "Intersex" added                                          *
. *       V9.02.01  04/09/2002  Jill Habasque SRF 17573                       *
. *                 Changed to use RTIODAYS instead of NHOSPDAY               *
. *       V5.08.02  29/08/2000  Tonii                                         *
. *                 Mods to accept Unknown & Indeterminate as valid patient   *
. *                 sex description                                           *
. *       V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *       V5.07.01  11/10/1999  Andrew Peel  SRF 646139                       *
. *                 Fixed title of report for ward/bed printout               *
. *       V5.07.B02 25/01/1999  Nicole Harrington 507 rebound 134             *
. *                 Mods to use HEAD132                                       *
. *                 09/11/1998  Glenn Berry      5.07 changes                 *
. *****************************************************************************
. *        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                 *
. *                  Changed temp file name "pstidx" to standard format       *
. *****************************************************************************
. *                                                                           *
. *              V5.01.02 07.09.89 S.Barcham                                  *
. *                       Fix Read Of patnobef                                *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD    
+
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       PATHSPFD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PATCONFD/INC
.
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PMSPX2FD/INC
          INC       PATWR1FD/INC
          INC       PATDO1FD/INC
          INC       PATTRNFD/INC
          INC       PATNOBFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.               WORK AREA
.
PSKIP     FORM      1
PAGENO    FORM      3
BJDAY     FORM      3                       * For CALCAGE
DPSKIP    DIM       3
.
DALPHA    DIM       6
DNAME     DIM       27
EXPORTFL  FORM      1         * Create Export File
.
HOSPID    DIM       3
HOSPNAME  DIM       50
IBCNMHOS  FORM      1
.
STANDBY   DIM       15
OPTION1   FORM      1
.
SAVDATE   DIM       6
SAVTYPE   DIM       1
SAWARD    DIM       3
SAVEKEY6  DIM       6
.
CODE      DIM       2
PRENAOPD  FORM      1
TYPPRDIS  FORM      1
CODEC     INIT      "C "
CODEM     INIT      "M "
.
.
PSTROL    FILE      ASCII, FIXED=256
TEMPFILE  IFILE     SQL, FIXED=141, KEY="u1-28"          
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.PSNAME         DIM       20         20             1     
DUNIQUE         DIM        8          8            21    
.PURNO          DIM        8          8            29
.WWARD          DIM        3          3            37
.WBED           DIM        3          3            40
.PSAGE          FORM       3          3            43
.DSEX           DIM        8          8            46
.DMSTAT         DIM        9          9            54
.WADMNO         DIM        8          8            63
.PHDAYS         FORM       4          3            71
.DNAME          DIM       27         27            74
.PGNAME         DIM       25         25           101
.STANDBY        DIM       15         15           126
. ----- EOR 141 -----
.
FNAMET    DIM       8
FNAMER    DIM       8
.
OPCND     FORM      1
STATUS    FORM      1
CMDLINE   DIM       50
UNIQUE    FORM      8
.
FIVE5     FORM    "55"
.
.      EXTRA VARIABLES
.
WRDESC    DIM     18
SRLGTYP   DIM     3
DRLGTYP   DIM     30
CDESC     DIM     20
PHDAYS    FORM     4
DHHMM     DIM      5
MIN1      FORM     1
.
.        WORK AREA
.
CALDAYS   FORM    4
CJDAY     FORM    3           * JULIAN CURRENT DAY
AJDAY     FORM    3           * JULIAN ADMISSION DAY
FROMDATE  DIM     8
JDAYS     FORM    3
JYEAR     FORM    2
LYEAR     FORM    "366"
NBRDAYS   FORM    4
OYEAR     FORM    "365"
TODATE    DIM     8
RTDAYS    FORM    4
SKEY30    DIM     30
WDATE1    DIM     8
WDATE2    DIM     8
.
NAME      DIM     22         * MFNAME.MSNAME
DMSTAT    DIM     9
.
DSEX      DIM     8                                            * was 6  *C-49016
.
SWARD     DIM     3
SBED      DIM     3
.
DDATE     INIT    "        "              * Dummy Variable for NHOSPDAY
.
PRGID     INIT      "IBAPAT82"
PRGNAM    INIT      "In-patient Report"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL          DISPHEAD
.
          DISPLAY       *P56:24,"Opening patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"patma1af";
          OPEN          PATMA1A1,"patma1af"
.
          DISPLAY       *P64:24,"patmx1af";
          OPEN          PATMX1A1,"patmx1af"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN          PATMI1A2,"patmi1af"
          OPEN          PMSVX1A1,"pmsvx1af"
.
          DISPLAY       *P64:24,"pattranf";
          OPEN          PATTRAN2,"pattranf"
.
          DISPLAY       *P64:24,"patdo1af";
          OPEN          PATDO1A1,"patdo1af"
.
          DISPLAY       *P64:24,"patwr1af";
          OPEN          PATWR1A1,"patwr1af"
.
          DISPLAY       *P64:24,"patnobef";
          OPEN          PATNOBE1,"patnobef"
.
OPFIL     DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
          READ          CONTROLF,ZERO;*43,IBCNMHOS
          READ          CONTROLF,HUND28;*226,PTCNNMPR
.
          MOVE      PTCNNMPR,PRENAOPT
          IF        PRENAOPT > 0
            DISPLAY   *P64:24,"pmspx2af";
            OPEN      PMSPX2A1,"pmspx2af"
          ENDIF
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
+
.         START OF PROGRAM
.
.         SELECT OPTION
.
START     MOVE       ZERO,PAGENO
          MOVE       ONE TO OPTION1
.
OPT2      DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Patient Surname Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Ward/Bed Sequence"
.
          KEYIN     *P1:8,"Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF KHOSP,KHOSP
          BEEP
          GOTO      OPT2
+
KHOSP     MOVE       SP10,HOSPID
          IF         IBCNMHOS=1
            CALL       KHOS0000        * Hospital id
            BRANCH     EXIT,OPT2
          ENDIF
.
.
          BRANCH    OPTION OF SURNAM,KWARD
.
.           ACCESS VIA PATIENT SURNAME
.
SURNAM    MOVE      SP6,KEY6
          MOVE      SP3,SAWARD
          CALL      RDSWARD1
.
          CALL       KILLIDX
          CALL       CREAIDX
.
          OPEN       TEMPFILE,FNAMET
          MOVE       ONE TO UNIQUE
.
          CALL      CRTX0000                 * Create Export File for Excel
.
          GOTO      ENDS1Y
+
.
.               PRINT VIA WARD NUMBER
.
KWARD     KEYIN     *P40:18,"<ENTER> for all Wards":
                    *P1:18,"Enter Ward Number : ",*V2LON,SAWARD
.
          RESET     SAWARD
          GOTO      ALLWS IF EOS
.
          PACK      KEY6 WITH SAWARD,SP6
          CALL      RDWARD1
          BRANCH    OVRCD OF INVWARD
.
          MOVE      WBDESC,WRDESC
          DISPLAY   *P40:18,*EL,*V2LON,WRDESC
.
.         If this is a Ward Only ward, then loop through the No-Bed file
.
          CALL      CRTX0000                 * Create Export File for Excel
.
          COMPARE  ZERO,WINPUT
          GOTO     NOBEDL IF NOT EQUAL
.
          GOTO      ENDS1Y
.
ALLWS     CALL      CRTX0000                 * Create Export File for Excel
.
          MOVE      SP6,KEY6
          MOVE      SP3,SAWARD
          CALL      RDSWARD1
.
          KEYIN   *P1:20,"Would you like each Ward ":
                         "on a separate Page (":
                         *V2LON,"Y",*HOFF,"/":
                         *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          CMATCH   "Y",ANS
          GOTO     ENDS1Y IF EQUAL
.
          MOVE     "N",ANS
          MOVE     ONE,PSKIP
          GOTO     ENDS1
.
ENDS1Y    MOVE     ZERO,PSKIP
.
ENDS1     MOVE     "60",CLNO
          DISPLAY  *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :"
.
          IF        EXPORTFL=1
            CALL      EHED0000               * Print the heading
            MOVE      ZERO,CLNO
          ENDIF
.
NEXTA     CALL     RDKWARD1
          BRANCH   OVRCD OF SORTD
.
          PACK     SAVEKEY6,WWARD,WBED,SP70
          PACK     KEY6,WWARD,SP70
          CALL     RDWARD1
.         BRANCH   OVRCD,NEXTA
.
          IF       OVRCD=1
            MOVE     SAVEKEY6,KEY6
            CALL     RDWARD1
            GOTO     NEXTA
          ENDIF
.
          IF         IBCNMHOS=1
            MATCH     SP70,HOSPID
            IF        !@EQUAL
              MATCH     PTWDHOSN,HOSPID  * Check hospital id
              IF        !@EQUAL
                MOVE      SAVEKEY6,KEY6
                CALL      RDWARD1
                GOTO      NEXTA 
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE  ONE,WACTIVE
          IF       @EQUAL
            MOVE     SAVEKEY6,KEY6
            CALL     RDWARD1
            GOTO     NEXTA 
          ENDIF
.
          MOVE     SAVEKEY6,KEY6
          CALL     RDWARD1
.
          MOVE     SP20,STANDBY
.
          MATCH    SP3,SAWARD
          GOTO     ALLWRDS IF EQUAL
.
          MATCH    SAWARD,WWARD
          GOTO     SORTD IF NOT EQUAL
.
ALLWRDS   MATCH    SP3,WBED
          GOTO     CHKWAD IF NOT EQUAL
.
          MOVE     WBDESC,WRDESC
.
.         If this is a Ward Only ward, then loop through the No-Bed file
.
          COMPARE  ZERO,WINPUT
          GOTO     NEXTA IF EQUAL
.
NOBEDL    PACK     KEY13 WITH WWARD,SP10
          CALL     RDSNOBE1
NOBLP     CALL     RDKNOBE1
          BRANCH   OVRCD OF ENDNOB
.
          MATCH    WWARD,NBWARD
          GOTO     ENDNOB IF NOT EQUAL
.
          MOVE     NBADMNO,WADMNO
          CALL     GETDATA
          GOTO     NOBLP
.
ENDREG    MOVE     WWARD,SWARD
          GOTO     NEXTA
.
ENDNOB    MATCH    SP3 TO SAWARD
          GOTO     NEXTA
.
CHKWAD    MATCH    ZEROVISN,WADMNO
          GOTO     MTBED IF EQUAL
.
          CALL     GETDATA
.
.         Check for standby patient
.
          MATCH    ZEROVISN,WSTBY
          GOTO     NEXTA IF EQUAL
.
          MOVE     WSTBY,WADMNO
          MOVE     "*** Standby ***",STANDBY
          MOVE     "*** Standby ***",WBDESC
          CALL     GETDATA
          GOTO     NEXTA
.
.         Empty bed
.
MTBED     BRANCH   OPTION OF NEXTA,PRTMTY
.
PRTMTY    CALL     NRMWRD
          GOTO     NEXTA
.
.         Subroutine to put the data into the temporary file
.
GETDATA   PACK     KEY9 WITH TWO,WADMNO
          CALL     RDMISS2
          BRANCH   OVRCD OF LOSTDAT1
.
          IF         IBCNMHOS=1
            MATCH     SP70,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID  * Check hospital id
              RETURN    IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE     AURNO,KEY8
          CALL     RDMAST1
.
          CALL      IBACLOCK
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          CALL      CALCAGE                 * Calculate the patients age
          MOVE      PSAGEYRS,PSAGE
.
          MOVE     SP30,DNAME
          MOVE     ADOCTA,KEY6
          CALL     RDDOCT1
          BRANCH   OVRCD OF NODOCA
.
          MOVE     DGNAME,ANS
          PACK     DNAME WITH ANS,DOT,DSNAME
          MOVE     DSNAME,DALPHA
.
.         RELIGION OPTION TEST
.
NODOCA    BRANCH   OPTION1 OF DSPCT1
.
          MATCH    PREG,SRLGTYP
          GOTO     ENDREG IF NOT EQUAL
.
          MOVE     SP30,DNAME
          MATCH    SP3,PCONT
          GOTO     DSPCT1 IF EQUAL
.
          PACK     KEY5 WITH CODEC,PCONT
          CALL     RDCODE1
          BRANCH   OVRCD OF DSPCT1
.
.         PUTTING         THE NATIONALITY DESCRIPTION TO DNAME FOR PRINTING
.
          MOVE     TDESC,DNAME
.
DSPCT1    MOVE     SP9,DMSTAT
          PACK     KEY5 WITH CODEM,PMSTAT
          CALL     RDCODE1
          BRANCH   OVRCD OF GETHSP
          MOVE     TDESC,DMSTAT
.
GETHSP    CALL     HOSPDAY
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.
ENDSEX    DISPLAY  *P27:24,*V2LON,WWARD,*P46:24,WBED
          BRANCH   OPTION OF SRTNAME,NRMWRD
.
SRTNAME   MOVE     UNIQUE,DUNIQUE
          PACK     KEY28 WITH PSNAME,DUNIQUE
          WRITE    TEMPFILE,KEY28;PSNAME,DUNIQUE,PURNO,WWARD,WBED,PSAGE,DSEX:
                                  DMSTAT,WADMNO,PHDAYS,DNAME,PGNAME,STANDBY
.
          ADD      ONE TO UNIQUE
          RETURN
.
.         Ward/Bed sequence - print as we read
.
NRMWRD    COMPARE   FIVE5,CLNO
          CALL      HEAD IF NOT LESS
.
          MATCH     WWARD,SWARD
          GOTO      FPRINTA IF EQUAL
.
.         New Ward - check if we want a new page
.
          BRANCH    PSKIP OF FPRINTO
          CALL      HEAD
          GOTO      FPRINTA
.
FPRINTO   CALL      HEAD12
.
.         Print this bed
.
FPRINTA   CALL       DISPDA1
          RETURN
.
SORTD     BRANCH    OPTION OF SORTIT,ENDP
.
SORTIT    DISPLAY   *P1:24,*EL,*P20:24,"** PROCESSING STATISTICS **",*W;
. 
          CLOSE     TEMPFILE
          GOTO      SORTFI
.
NOOPEN    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*B,*EL,"** TEMPORARY STATISTICS  FILE ":
                                   FNAMET," NOT FOUND **",*W2;
          RETURN
.
SORTFI    TRAP      NOOPEN IF IO
          OPEN      TEMPFILE,FNAMET
          TRAPCLR   IO
.
          DISPLAY   *P1:12,*EF
.
          BRANCH    OPCND OF ENDP
.
          DISPLAY   *P1:24,*EL,*P20:24,"Patient Name :";
          MOVE      SP30 TO KEY28
          READ      TEMPFILE,KEY28;;
.
READLOP   READKS    TEMPFILE;PSNAME,DUNIQUE,PURNO,WWARD,WBED,PSAGE,DSEX:
                           DMSTAT,WADMNO,PHDAYS,DNAME,PGNAME,STANDBY
          GOTO      ENDP IF OVER
          MOVE      DUNIQUE,UNIQUE
.
          DISPLAY   *P40:24,*EL,*V2LON,PSNAME
.
          CALL      DISPDA2
          GOTO      READLOP
.
NOWARD    DISPLAY   *P20:24,*EL,*B,*V2LON:
                    "** No Available Data on File **",*W2;
          GOTO      START
.
INVWARD   DISPLAY   *P20:24,*EL,*B,*V2LON:
                    "** Ward does not Exist **",*W2;
          GOTO      START
+
.
.         OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      BRANCH    EXPORTFL,ENDP0
.
          COMPARE   ZERO,PAGENO
          GOTO      NOWARD IF EQUAL
.
          CALL      PAGEND
          PRINT     *N,"  ***  END OF REPORT  ***"
          DISPLAY   *P40:24,*EL,*V2LON:
                    "** Report Generation Completed **",*W2;
ENDP0     BRANCH    OPTION TO ENDP1,START
          GOTO      START
.
ENDP1     CLOSE    TEMPFILE
          CALL     KILLIDX
          GOTO     START
.
LOSTDAT1  DISPLAY  *P1:23,*EL,*V2LON:
                   "*** Ward/Bed ",*V2LON,WWARD,"/",WBED,*HOFF:
                   " has invalid Admission ",*V2LON,WADMNO,*HOFF," ***"
.
          COMPARE   FIVE5,CLNO
          CALL      HEAD IF NOT LESS
          IF        EXPORTFL<>1
            PRINT    *N,"*** Ward/Bed ",WWARD,"/",WBED:
                     " has invalid Admission ",WADMNO," ***":
                     " Contact I.B.A Immediately  ***",*N
            ADD       THREE,CLNO
          ENDIF
          RETURN
.
OPMSFI    IF        PRENAOPT > 0
            MOVE      PURNO,KEY8
            CALL      RDPMPX21
          ENDIF
          RETURN
+
.
.         PRINT ACTUAL DATA
.
DISPDA1   COMPARE   FIVE5,CLNO
          CALL      HEAD IF NOT LESS
.
          MATCH     WWARD,SWARD
          GOTO      FPRINTB IF EQUAL
. 
          CALL      HEAD12
.
FPRINTB  MATCH     ZEROVISN,WADMNO
         GOTO      EMPBED1 IF EQUAL
.
         IF        EXPORTFL=1
           CALL      PWBD0000                * Print export file line
         ELSE
           UNPACK    WBDESC,KEY17
           UNPACK    PGNAME,KEY23
           UNPACK    DNAME,KEY26
           CALL      OPMSFI
           PRINT     *1,"! ",WBED,*6,"! ",KEY17:
                     *25,"! ",PURNO,*35,"! ",PSNAME:
                     *60,"! ",PSAGE,*65,"! ",DSEX:
                     *75,"! ",DMSTAT,*88,"! ",WADMNO:
                     *98,"!",PHDAYS:
                     *104,"! ",KEY26,*132,"!":
                     *N,"!",*6,"!":
                     *25,"!",*35,"! ",KEY23,*60,"!",*65,"!":
                     *75,"! ",*88,"!",*98,"!",*104,"!",*132,"!"
          ENDIF
.
          MOVE      2,TYPPRDIS
          IF        PRENAOPT > 0
            CLEAR     PRNAMES
            CLEAR     PRFRNAME
            MOVE      PMPXPFGN,PREGNAME
            MOVE      PMPXPFSN,PRESNAME
            PERFORM   PRENAOPT,DISPPNMG,DISPPNMS,DISPPNMB
          ELSE
            CALL      DISPEMPA
          ENDIF
.
          MOVE      WWARD,SWARD
          ADD       THREE,CLNO
          RETURN
.
.
+
.
.         PRINT ACTUAL DATA VIA SURNAME
.
DISPDA2   IF        EXPORTFL=1
            CALL      PSRN0000                * Print export file line
          ELSE
            COMPARE   FIVE5,CLNO
            CALL      HEAD2 IF NOT LESS
            CALL      OPMSFI
.
            UNPACK    DNAME,KEY26
            PRINT     *1,"! ",PSNAME,*30,"!  ",PURNO:
                      *41,"!  ",WWARD,*50,"!  ",WBED:
                      *60,"! ",PSAGE,*65,"! ",DSEX:
                      *75,"! ",DMSTAT,*88,"! ",WADMNO:
                      *98,"!",PHDAYS:
                      *104,"! ",KEY26,*132,"!":
                      *N,"! ",PGNAME,*30,"!":
                      *41,"!",*50,"!",*60,"!",*65,"!":
                      *75,"!",*88,"!",*98,"!",*104,"! ",STANDBY:
                      *132,"!"
.
            MOVE      1,TYPPRDIS
            IF        PRENAOPT > 0         
              CLEAR     PRNAMES
              CLEAR     PRFRNAME
              MOVE      PMPXPFGN,PREGNAME
              MOVE      PMPXPFSN,PRESNAME
              PERFORM   PRENAOPT,DISPPNMG,DISPPNMS,DISPPNMB
            ELSE
              CALL      DISPEMP
            ENDIF
.
            ADD       THREE,CLNO
          ENDIF
.
          RETURN
.
DISPPNMG  MATCH     SP70,PMPXPFGN
          IF        !@EQUAL
            CALL      PRNM0000
            PERFORM   TYPPRDIS,DISPPNM,DISPPNMA
          ELSE
            PERFORM   TYPPRDIS,DISPEMP,DISPEMPA
          ENDIF
          RETURN
.
DISPPNMS  MATCH     SP70,PMPXPFSN
          IF        !@EQUAL
            CALL      PRNM0000
            PERFORM   TYPPRDIS,DISPPNM,DISPPNMA
          ELSE
            PERFORM   TYPPRDIS,DISPEMP,DISPEMPA
          ENDIF
          RETURN
.
DISPPNMB  MOVE      PRENAOPT,PRENAOPD
          MATCH     SP70,PMPXPFGN
          IF        !@EQUAL
            MATCH     SP70,PMPXPFSN
            IF        @EQUAL
              MOVE      1,PRENAOPT
            ENDIF
            CALL      PRNM0000
            PERFORM   TYPPRDIS,DISPPNM,DISPPNMA
          ELSE
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL
              MOVE      2,PRENAOPT
              CALL      PRNM0000
              PERFORM   TYPPRDIS,DISPPNM,DISPPNMA
            ELSE
              PERFORM   TYPPRDIS,DISPEMP,DISPEMPA
            ENDIF
          ENDIF
          MOVE      PRENAOPD,PRENAOPT
          RETURN
.
DISPPNM   PRINT       *1,"! ",PRFRNAME,*30,"!  ",*41,"| ":
                      *50,"!  ",*60,"! ",*65,"! ",*75,"! ":
                      *88,"! ",*98,"!",*104,"! ",*132,"!":
                      *N,"!",*30,"!":
                      *41,"!",*50,"!",*60,"!",*65,"!":
                      *75,"!",*88,"!",*98,"!",*104,"!",*132,"!"

          RETURN
.
DISPPNMA  PRINT       "!",*6,"!",*25,"!",*35,"! ",PRFRNAME,*60,"!":
                      *65,"!",*75,"! ",*88,"!",*98,"!",*104,"!",*132,"!":
                      *N,"!",*6,"!",*25,"!",*35,"! ",*60,"!":
                      *65,"!",*75,"! ",*88,"!",*98,"!",*104,"!",*132,"!"
.
          RETURN
.
DISPEMP   PRINT       "!",*30,"!":
                      *41,"!",*50,"!",*60,"!",*65,"!":
                      *75,"!",*88,"!",*98,"!",*104,"!",*132,"!"
          RETURN
.
DISPEMPA  PRINT       "!",*6,"!",*25,"!",*35,"! ",*60,"!":
                      *65,"!",*75,"! ",*88,"!",*98,"!",*104,"!",*132,"!"
.
          RETURN      
.
+
.****************************************************************************
.*                                PEMT0000                                  *
.* Empty bed - Create export file for excel                                 *
.****************************************************************************
PEMT0000  PRINT     "<tr><td>&nbsp;",WWARD,"</td>":
                      "<td>&nbsp;",WBED,"</td>":
                      "<td>&nbsp;</td>":
                      "<td>*** EMPTY BED ***</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>"
.
          IF        IBCNMHOS=1
            PRINT     "<td>&nbsp;</td>"
          ENDIF
          PRINT       "<td>&nbsp;</td></tr>"
.
PEMT999   RETURN
+
.****************************************************************************
.*                                PCLS0000                                  *
.* Closed bed - Create export file for excel                                *
.****************************************************************************
PCLS0000  PRINT     "<tr><td>&nbsp;",WWARD,"</td>":
                      "<td>&nbsp;",WBED,"</td>":
                      "<td>&nbsp;</td>":
                      "<td>*** CLOSED BED ***</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>":
                      "<td>&nbsp;</td>"
.
          IF        IBCNMHOS=1
            PRINT     "<td>&nbsp;</td>"
          ENDIF
          PRINT       "<td>&nbsp;</td></tr>"
.
PCLS999   RETURN
+
.****************************************************************************
.*                                PWBD0000                                  *
.* Create export file for excel by ward/bed                                 *
.****************************************************************************
PWBD0000  PRINT     "<tr><td>&nbsp;",WWARD,"</td>":
                    "<td>&nbsp;",WBED,"</td>":
                    "<td>",PURNO,"</td>":
                    "<td>",PSNAME,"</td>":
                    "<td>&nbsp;",PGNAME,"</td>":
                    "<td>&nbsp;",PSAGE,"</td>":
                    "<td>&nbsp;",DSEX,"</td>":
                    "<td>&nbsp;",DMSTAT,"</td>":
                    "<td>",WADMNO,"</td>":
                    "<td>",PHDAYS,"</td>":
                    "<td>&nbsp;",DNAME,"</td>"
.
          IF        IBCNMHOS=1
            PRINT     "<td>&nbsp;",PMVXMHOS,"</td>"
          ENDIF
.
          PRINT     "<td>&nbsp;",STANDBY,"</td></tr>"
.
PWBD9999  RETURN
+
.****************************************************************************
.*                                PSRN0000                                  *
.* Create export file for excel by surname                                  *
.****************************************************************************
PSRN0000  PRINT     "<tr><td>",PSNAME,"</td>":
                    "<td>&nbsp;",PGNAME,"</td>":
                    "<td>",PURNO,"</td>":
                    "<td>&nbsp;",WWARD,"</td>":
                    "<td>&nbsp;",WBED,"</td>":
                    "<td>&nbsp;",PSAGE,"</td>":
                    "<td>&nbsp;",DSEX,"</td>":
                    "<td>&nbsp;",DMSTAT,"</td>":
		    "<td>&nbsp;",WADMNO,"</td>":
                    "<td>",PHDAYS,"</td>":
                    "<td>",DNAME,"</td>"
.
          IF        IBCNMHOS=1
            PRINT     "<td>&nbsp;",PMVXMHOS,"</td>"
          ENDIF
.
          PRINT     "<td>&nbsp;",STANDBY,"</td></tr>"
.
PSRN9999  RETURN
.
.****************************************************************************
.         EMPTY BED PRINTOUT
.
EMPBED1   IF        WACTIVE=1
.           PRINT     *1,"! ",WBED,*6,"! ",WBDESC:
.                     *25,"!",*35,"! *** INACTIVE BED***";
            GOTO      EMPBED3
          ENDIF
.
          MATCH     "1",PTWDBDST
          IF        @EQUAL
            IF        EXPORTFL=1
              CALL      PCLS0000                * Print Closed bed
              MOVE      WWARD,SWARD
              RETURN
            ELSE
              UNPACK    WBDESC,KEY17
              PRINT     *1,"! ",WBED,*6,"! ",KEY17:
                        *25,"!",*35,"! ***  CLOSED BED ***";
            ENDIF
            GOTO      EMPBED2
          ENDIF
.
          IF        EXPORTFL=1
            CALL      PEMT0000                * Print empty bed
            MOVE      WWARD,SWARD
            RETURN
          ELSE
            UNPACK    WBDESC,KEY17
            PRINT     *1,"! ",WBED,*6,"! ",KEY17:
                      *25,"!",*35,"! ***  EMPTY BED  ***";
          ENDIF
.
EMPBED2   PRINT     *60,"!",*65,"!":
                    *75,"! ",*88,"!",*98,"!",*104,"!",*132,"!":
                    *N,"!",*6,"!",*25,"!":
                    *35,"!",*60,"!",*65,"!":
                    *75,"! ",*88,"!",*98,"!",*104,"!",*132,"!"
.
          MOVE      WWARD,SWARD
          ADD       TWO,CLNO
          RETURN
+
EMPBED3   BRANCH    EXPORTFL,EMPBED9
          PRINT     *1,"!",*6,"!",*25,"!":
                    *35,"!",*60,"!",*65,"!":
                    *75,"! ",*88,"!",*98,"!",*104,"!",*132,"!"
.
          MOVE      WWARD,SWARD
          ADD       ONE,CLNO
EMPBED9   RETURN
+
.
.         LAST LINE ON THE CODE
.
PAGEND    PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      BRANCH    EXPORTFL,HEAD1888
.
          BRANCH    OPTION OF HEAD2,HEAD1
.
HEAD1     ADD       ONE,PAGENO
.
          COMPARE   ONE,PAGENO
          CALL      PAGEND IF NOT EQUAL
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          MOVE      ZERO,CLNO
          IF       IBCNMHOS =1
            PRINT    *40,"Hospital : ",HOSPID,"    ",HOSPNAME,*N
            ADD      TWO,CLNO
          ENDIF
.
          PRINT     *40,"(WARD/BED SEQUENCE)"
          ADD       THREE,CLNO
          GOTO      HEAD13
.
HEAD12    BRANCH    EXPORTFL,HEAD1888
.
          CALL      PAGEND
          PRINT     " "
          ADD       ONE,CLNO
HEAD13
          PRINT     *40,"WARD : ",WWARD,*56,WRDESC;
          ADD       ONE,CLNO
.
          BRANCH    OPTION1 OF PRNT1A,PRNT1B
.
PRNT1A    PRINT     *N
          ADD       TWO,CLNO
          GOTO      PRTCONT1
.
PRNT1B    PRINT     *80,"RELIGION : ",DRLGTYP,*N
          ADD       TWO,CLNO
.
PRTCONT1  CALL     PAGEND
.
          PRINT     *1,"! Bed",*6,"! Bed Description":
                    *25,"! U/R No",*35,"! Patients Name":
                    *60,"! Age",*65,"! Sex",*75,"! Marital St ":
                    *88,"! Adm No",*98,"! Days";
          ADD       ONE,CLNO
.
.         RELIGION OPTION TEST
.
          BRANCH          OPTION1 OF HEAD1A,HEAD1B
.
HEAD1A    PRINT     *104,"! Attending Doctor",*132,"!"
          GOTO      HEAD1CNT
.
HEAD1B    PRINT     *104,"! Patient Nationality",*132,"!"
.
HEAD1CNT  CALL      PAGEND
HEAD1888  MOVE      WWARD,SWARD
HEAD1999  RETURN
+
.
.         HEADINGS FOR OUTPUT
.
HEAD2     ADD       ONE,PAGENO
.
          COMPARE   ONE,PAGENO
          CALL      PAGEND IF NOT EQUAL
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          MOVE      ZERO,CLNO
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",HOSPID,"    ",HOSPNAME,*N
            ADD       TWO,CLNO
          ENDIF
.
          PRINT     *40,"(SURNAME SEQUENCE)"
          ADD       THREE,CLNO
.
          BRANCH    OPTION1 OF PRTCONT2
.
          PRINT     *40,"Religion : ",DRLGTYP,*N
          ADD       TWO,CLNO
.
PRTCONT2  CALL     PAGEND
.
          PRINT     *1,"! Patients Name",*30,"! U/R No":
                    *41,"!  Ward",*50,"!  Bed":
                    *60,"! Age",*65,"! Sex",*75,"! Marital St ":
                    *88,"! Adm No",*98,"! Days";
          ADD       ONE,CLNO
.
.         RELIGION OPTION TEST
.
          BRANCH     OPTION1 OF HEAD2A,HEAD2B
.
HEAD2A    PRINT     *104,"! Attending Doctor",*132,"!"
          GOTO      HEAD2CNT
.
HEAD2B    PRINT     *104,"! Patient Nationality",*132,"!"
.
HEAD2CNT  CALL      PAGEND
HEAD2999  RETURN
+
.****************************************************************************
.         Routine to print heading for Export File
.*******************************************************************************
EHED0000  BRANCH    OPTION,EHED4000,EHED1000
.
EHED1000  PRINT     "<table border=1><tr><td>Ward</td>":
                    "<td>Bed</td>":
                    "<td>U/R No</td>":
                    "<td>Patients Surname</td>":
                    "<td>Given Name</td>":
                    "<td>Age</td>":
                    "<td>Sex</td>":
                    "<td>Marital Status</td>":
                    "<td>Adm No</td>":
                    "<td>Days</td>":
                    "<td>Attending Doctor</td>"
.
          IF        IBCNMHOS=1
            PRINT     "<td>Hospital Id</td>"
          ENDIF
          PRINT     "<td>&nbsp;</td></tr>"
.
          GOTO      EHED9999
.
EHED4000  PRINT     "<table border=1><tr><td>Patients Surname</td>":
                    "<td>Given Name</td>":
                    "<td>U/R No</td>":
                    "<td>Ward</td>":
                    "<td>Bed</td>":
                    "<td>Age</td>":
                    "<td>Sex</td>":
                    "<td>Marital Status</td>":
                    "<td>Adm No</td>":
                    "<td>Days</td>":
                    "<td>Attending Doctor</td>"
.
          IF        IBCNMHOS=1
            PRINT     "<td>Hospital Id</td>"
          ENDIF
          PRINT     "<td>&nbsp;</td></tr>"

EHED9999  RETURN
+
.****************************************************************************
.         READ TRANSFER FILE
.
HOSPDAY   UNPACK          ADATE,XCENT,XYEAR,XMON,XDAY
          PACK            FROMDATE,XCENT,XYEAR,XMON,XDAY
          REP             " 0" IN FROMDATE
          PACK            TODATE,CCC,CYY,CMM,CDD
          REP             " 0" IN TODATE
          CALL            RTIODAYS
          MOVE            NBRDAYS,PHDAYS
          RETURN
+
.         Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLEAR         CMDLINE
          APPEND        "iserase ",CMDLINE
          APPEND        FNAMET,CMDLINE
          RESET         CMDLINE
          EXECUTE       CMDLINE,TASKID
          DISPLAY       *P1:13,*EF,*P1:13
          RETURN
.
CREAIDX   PREP          PSTROL,FNAMER
          WRITE         PSTROL,SEQ;"isbuild ",FNAMET," 141 u1-28"
          WEOF          PSTROL,SEQ
.
          MOVE          ONE,STATUS
          DISPLAY       *P1:13,*EF,*P1:13
.
          CLEAR         CMDLINE
          APPEND        "sh ",CMDLINE
          APPEND        FNAMER,CMDLINE
          APPEND        ".txt",CMDLINE
          RESET         CMDLINE
.
          EXECUTE      CMDLINE,TASKID
          BRANCH       STATUS TO INDOK
.
          MOVE         ONE,OPCND
          DISPLAY      *P20:24,*B,*V2LON:
                       "** The Index File Not Created **",*W3;
.
INDOK     DISPLAY       *P1:13,*EF,*P1:13
          CLOSE         PSTROL,DELETE
          RETURN
+
.         ======================================================================
.         KHOS0000: Keyin hospital ID
.         ======================================================================
KHOS0000  DISPLAY    *P1:16,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       "16",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS1000,KHOS2000
          MOVE       PTHSHOSP,HOSPID
          MOVE       PTHSNAME,HOSPNAME
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS1000  MOVE       "All Hospitals",HOSPNAME
          MOVE       SP10,PTHSHOSP
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS2000  MOVE       ONE,EXIT
KHOS9999  RETURN
+
.****************************************************************************
.*                                CRTX0000             Called by: ML0000    *
.* Create export file for excel                                             *
.****************************************************************************
CRTX0000  MOVE      ZERO,EXIT
          MOVE      ZERO,EXPORTFL                  * No to create export file
          DISPLAY   *P1:22,"Create Export file for Excel : "
          KEYIN     *P32:22,*V2LON,*RV,DIM1:
                    *P32:22,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:22,*V2LON,DIM1
          MOVE      ONE,EXPORTFL
          MATCH     "Y",DIM1
          GOTO      CRTX9999 IF EQUAL
          MOVE      ZERO,EXPORTFL
.
CRTX9999  RETURN
+
          INC       PATHSPKY
          INC       CALCAGE
          INC       SEXDSCIO
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PMSPX2IO/INC
          INC       PATDO1IO/INC
          INC       PATWR1IO/INC
          INC       PATTRNIO/INC
          INC       PATNOBIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
          INC       STD001IO
          INC       RTIODAYS      * Calculates number of days in hospital
.
ENDIT     CHAIN      PGM
          STOP

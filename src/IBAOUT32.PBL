.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT32.PBL                                              *
.* Name           : Patients to be Invoiced but missing from Invoice Pending  *
.*                  file.                                                     *
.******************************************************************************
.* Date           : 29/04/2003                                                *
.* Author         : Dean Cramer                                               *
.* Function       : Print patients to be invoiced but missing from invoice    *
.*                  pending file.                                             *
.* Modifications  :                                                           *
.*        V9.03.01  23/10/2003  J.Tan   CAR   44172                           *
.*                  Mods for Misc.Theatre item file (pmsmtiaf)                *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       OUTDTRFD/INC            * DTR fileion file
          INC       PMSMTIFD/INC            * Misc.Theatre item file
          INC       PATIPEFD/INC            * Invoice Pending file
.
. ----- Program variables -----
.
ENDDTE8   DIM       8                       * End date (ccyymmdd)
GRNDTOTL  FORM      11.2                    * Grand total
INVTOTAL  FORM      10.2                    * Invoice total
PATNAME   DIM       40                      * Patient name
RECCOUNT  FORM      4                       * Record counter
SAADMN    DIM       8                       * Saved admission no.   
STRTDTE8  DIM       8                       * Start date (ccyymmdd)
.
. ----- Program constants -----
.
PRGID     INIT      "IBAOUT32"
PRGNAM    INIT      "Patients to be Invoiced but not in Invoice Pending file"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Select option
          BRANCH    EXIT,ML9999 
.
          CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,ML3000,ML1000,ML1000
.
ML3000    CALL      PROC0000                * Process the files
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          PACK      CFNAME,UID,FILDTRO2
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTDTRO1,CFNAME
.
          DISPLAY   *P64:24,"patipenf";
          OPEN      PATIPEN1,"patipenf"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
.
          MOVE      ONE,CNOUNDLN            * No report underline
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report":
                    *P1:7,"Select Option : ";
.
OPTN1000  MOVE      ZERO,OPTION
          KEYIN     *P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Process The Files                             *
.******************************************************************************
PROC0000  MOVE      ZERO,CPAGENO            * Reset page no
          CALL      HEAD0000                * Print the report heading
.
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Printing : ";
          MOVE      ZERO,GRNDTOTL           * Clear the grand total
          MOVE      ZERO,RECCOUNT           * Clear the record counter
.
          PACK      SAADMN,SP30
          PACK      KEY14,SP30
          CALL      RSPMMTI1                * Position on & read the clinic
PROC1000  CALL      RKPMMTI1                  session B file
          BRANCH    OVRCD,PROC5000 
.
          MATCH     SAADMN,PMMIVISN
          GOTO      PROC1000 IF EQUAL
.
PROC4000  PACK      KEY8,PMMIVISN,SP10
          CALL      RDIPEN1
          BRANCH    OVRCD,PROC4500
.
          GOTO      PROC1000
.
PROC4500  CALL      PRNT0000                * Print invoice details
          ADD       ONE,RECCOUNT            * record counter
          MOVE      PMMIVISN,SAADMN          
          GOTO      PROC1000
. 
PROC5000  PRINT     *N:
                    *10,"Number of records : ",RECCOUNT:
                    *1,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,"Report Completed.  ";
          CALL      HITENTER
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PROC0000 *
.*                            Print Invoice Details                           *
.******************************************************************************
PRNT0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * Print the report heading
.
          PRINT     *N;
          PRINT     *1,PMMIVISN;                                       
          ADD       TWO,CLNO
          DISPLAY   *P46:24,*V2LON,PMMIVISN;
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PROC0000 *
.*                          Print The Report Heading               & PRNT0000 *
.******************************************************************************
HEAD0000  CLOCK     TIME,CTIMEIS
          PRINT     *N;
          CALL      HEAD132                 * Print the report heading
.
          PRINT     *N;
.
          PRINT     *3,"O/P No"
          CALL      UND132
          PRINT     *N;
          ADD       "6",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       OUTDTRIO/INC            * DTR file
          INC       PMSMTIIO/INC            * Misc.Theatre item file
          INC       PATIPEIO/INC            * Invoice Pending file
+
.


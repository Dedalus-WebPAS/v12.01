.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAINP93                                                 *
.* Description    :  Discharged Patients DRG Report                           *
.******************************************************************************
.* Date           :  02/12/1994                                               *
.* Author         :  Greg Horvat                                              *
.* Function       :  To report patients within a given discharge date range   *
.*                   who have a range of DRG codes, & those without DRG codes.*
.* Modifications  :                                                           *
.*       V12.00.01 16/05/2025  J.Tan          TSK 0955096                     *
.*                 Added alpanumeric visit number generation                  *
.*       V10.14.01 24/06/2019  Davin         TSK 0876270                      *
.*                 Added DRG versions 8.0/9.0/10.0 (VDRG0000/GDRG0000)        *
.*       V10.11.01 08/09/2017  Ebon Clements TSK 0818097                      *
.*                 Added excluded newborn functionality -  EXNB0000           *
.*       V10.04.01 15/06/2014 Jill Parkinson CAR 301639                       *
.*                 Moved TFILENAM to INIT0000                                 *
.*       V10.03.03 22/08/2013  Davin            CAR 290423 HDP 2013 DRG 7.0   *
.*                 Added DRG070 & DRGD70 an added DRG 7.0 to screen selections*
.* V10.03.02  28/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.03.01 17/07/2012  Mike Laming      CAR 269384                   *
.*                  Added DRG62 & DRG062 an added DRG 6.2 to screen selections*
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V10.01.01 10/02/2011  Mike Laming      CAR 230782                   *
.*                  Change ML6000 so PRNT0000 returns to ML1000 (not ML2000)  *
.*        V9.12.01  19/06/2009  Mike Laming      CAR 197814 HDP 2009 DRG 6.0  *
.*                  Added DRG60 & DRG060 an added DRG 6.0 to screen selections*
.*        V9.09.01  01/11/2007  Peter McMullen CAR 153023 (see below)         *
.*        V9.08.02  01/11/2007  Peter McMullen CAR 153023                     *
.*                  re-apply CAR 80273 (from 6.13.01) exclude borders from    *
.*                  discharge report                                          *
.*        V9.08.01  04/05/2007  Mike Laming  CAR 137125 HDP 2007 DRG 5.2      *
.*                  Mods to PATDGWFD - Add field PTDWDRGV (add to Keys)       *
.*        V9.07.01  14/09/2006  Mike Laming   CAR 117741                      *
.*                  Added DRG Vers 5.1                                        *
.*        V9.04.02  04/09/2004  Guomin Fu     CAR 52766                       *
.*                  Added options for DRG versions in report                  *
.*        V9.04.01  23/08/2004  Lina Vo CAR 52901                             *
.*                  Added Multi-Hospital processing                           *
.*        V9.02.01  04/09/2002  Jill Habasque SRf 17583                       *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V5.08.03  07/12/2000  Caleb Sharman                                 *
.*                  Mods to use keyin routine for DRG entries                 *
.*        V5.08.02  28/08/2000  J.Tan                                         *
.*                  Fixed report layout                                       *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 15/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*        V5.07.03  24/11/1999  Davin                                         *
.*                  Changed for 4 character drgs                              *
.*        V5.07.02  25/10/1999  Andrew Peel  SRF  635228                      *
.*                  Definitely fixed U * R for DRG codes plus a layout fix    *
.*        V5.07.01  07/10/1999  Andrew Peel  SRF  635045                      *
.*                  Fixed U * R for DRG codes Max 5 diseases and 10 operations*
.*        V5.07.B03 29/01/1999  Jim Stathopoulos                              *
.*                  Report Modifications                                      *
.*        V5.07.B02 19/11/1998  Nicole Harrington                             *
.*                  Changed for Y2K fixes                                     *
.******************************************************************************
.*        V5.03.00  21/07/1995  Greg Horvat                                   *
.*                  Ported from release 6.03, version V6.03.01                *
.*        V5.03.01  11/09/1995  Greg Horvat                                   *
.*                  Changed to display the DRG descriptions next to the DRG's *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCONFD/INC            * 
          INC       IBACONFD/INC
          INC       PATDGWFD/INC            * DRG description & weightings file
          INC       PATDO1FD/INC            * Doctors file
          INC       PATDSCFD/INC            * Discharge file
          INC       PATCODFD/INC            * Codes file
          INC       PATECDFD/INC            * Patient episode disease file
          INC       PATECOFD/INC            * Patient episode operation file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC            * Patient master file
          INC       PATMI1FD/INC            * Patient admission file
          INC       PATTRNFD/INC            * Patient transfer file
          INC       PATPGRFD/INC            * patpgraf  
          INC       PATFN1FD/INC            * patfundf  
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. ----- Tempfile Definition -----
.
TEMP1     IFILE     SQL, FIXED=74, KEY="U1-4,5-12,13-20"
FILELINE  INIT      " 74 U1-4,5-12,13-20"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
DRG       DIM       4        4          1       DRG code
URNO      DIM       8        8          5       U/R number
ADMISS    DIM       8        8          13      Admission number
TMPCOUNT  FORM      3        3          21      Tempfile counter
PATNAME   DIM       17       17         24      Patient name
DSPDSDTE  DIM       10       10         41      Display discharge date
ADOCNAME  DIM       15       15         51      Attending doctor name
LOS       FORM      5        4          66      Length of stay
MDC       DIM       4        4          70      MDC code
.
.End of record                          74
.
. ----- Program Variables -----
.
CALDAYS   FORM      5                       * For NHOSPDAY
CMDLINE   DIM       70
CODECONT  FORM      2                       * Counter for printing codes
CPTDATE   DIM       8
CURDATE   DIM       8                       * Current date
CALCDRGN  DIM       4
.
DIM8      DIM       8
DIM27     DIM       27
DIM52     DIM       52
DISCOUNT  FORM      2                       * Disease line counter
DISLINE   DIM       29[10]                  * Disease line
DISPNTER  FORM      2                       * Disease line pointer
DRGCODE   DIM       4                       * DRG code
DRGVERZ   DIM       3                       * DRG Version             *I-137125
DV1       DIM       1                                                 *I-137125
DV2       DIM       1                                                 *I-137125
DSPEDDRG  DIM       6                       * Display ending DRG
DSPEDDTE  DIM       10                      * Display ending date
DSPSTDRG  DIM       6                       * Display starting DRG
DSPSTDTE  DIM       10                      * Display starting date
.
..... 
DRGD31    INIT      "3.1"
DRGD41    INIT      "4.1"
DRGD42    INIT      "4.2"
DRGD50    INIT      "5.0"
DRGD51    INIT      "5.1"                                             *I-117741
DRGD52    INIT      "5.2"                                             *I-137125
DRGD60    INIT      "6.0"                                             *I-197814
DRGD62    INIT      "6.2"                                             *I-269384
DRGD70    INIT      "7.0"                                             *I-290423
DRGD80    INIT      "8.0"                                             *I-0876270
DRGD90    INIT      "9.0"                                             *I-0876270
DRGD100   INIT      "10.0"                                            *I-0876270
DRGHF     INIT      "HF DRG"
DRG031    INIT      "031"
DRG041    INIT      "041"
DRG042    INIT      "042"
DRG050    INIT      "050"
DRG051    INIT      "051"                                             *I-117741
DRG052    INIT      "052"                                             *I-127125
DRG060    INIT      "060"                                             *I-197814
DRG062    INIT      "062"                                             *I-269384
DRG070    INIT      "070"                                             *I-290423
DRG080    INIT      "080"                                             *I-0876270
DRG090    INIT      "090"                                             *I-0876270
DRG100    INIT      "100"                                             *I-0876270
.... End I
ENDDATE   DIM       8                       * Ending date (CCYYMMDD)
ENDDRG    DIM       4                       * Ending DRG
.
FNAME     DIM       8                       * Tempfile name
FROMDATE  DIM       8                       * For NHOSPDAY
.
JYEAR     FORM      2                       * For NHOSPDAY
.
NBRDAYS   FORM      5                       * For NHOSPDAY
NEWPNTER  FORM      2                       * Code pointer
.
OPRCOUNT  FORM      2                       * Operation line counter
OPRLINE   DIM       29[10]                  * Operation line
OPRPNTER  FORM      2                       * Operation line pointer
.
PATCOUNT  FORM      3                       * Patient counter
.
RTDAYS    FORM      4
SAVDRG    DIM       4                       * Save DRG
SKEY30    DIM       30
STRTDATE  DIM       8                       * Starting date (CCYYMMDD)
STRTDRG   DIM       4                       * Starting DRG
.
TODATE    DIM       8                       * For NHOSPDAY
.
VERSNDRG  DIM      3                        * DRG version
VERSNUMB  FORM     2                        * 
VERSDESC  DIM      10                       * 
.
WDATE1    DIM       8                       * For NHOSPDAY
WDATE2    DIM       8                       * For NHOSPDAY
.
. ----- Program Constants -----
.
SP14      INIT      "              "
SP65      INIT      "                                ":
                    "                                 "
.
ZED4      INIT      "zzzz"
.
PRGID     INIT      "IBAINP93"
PRGNAM    INIT      "Discharged Patients DRG Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialize variables & open files
ML1000    CALL      OPTN0000                * Choose option
          BRANCH    EXIT,ML9000
.
ML2000    CALL      DATE0000                * Keyin discharge date range
          BRANCH    EXIT,ML1000
.
          BRANCH    COPTION,ML4000,ML5000
.
ML4000    CALL      VDRG0000                * Keyin DRG version
          CALL      DRGR0000                * Keyin the DRG range
          BRANCH    EXIT,ML2000
.
ML5000    CALL      KHOS0000
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * O.K. to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML6000,ML2000,ML1000
.                         Yes    No     Cancel
.
ML6000    CALL      LOAD0000                * Load the tempfile
          BRANCH    EXIT,ML2000
.
          CALL      PRNT0000                * Print details
          GOTO      ML1000
.
ML9000    CALL      KILL0000                * Delete tempfile
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                      Initialize Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P64:24,"controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND05;*158,PTCNDGPV         * 
          DISPLAY   *P56:24,"Opening patdgwaf";
          OPEN      PATDGWA1,"patdgwaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patpgraf"       * PATPGRFD/INC
          OPEN      PATPGRA1,"patpgraf"
.
          DISPLAY   *P64:24,"patfn1af"       * PATFN1AD/INC
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 DATE0000               Called by: ML0000   *
.*                        Keyin Discharge Date Range                          *
.******************************************************************************
DATE0000  MOVE      ZERO,EXIT
          MOVE      ONE,CCANLDTE            * Cancel default date
.
. ----- Enter Starting Date -----
.
DATE1000  DISPLAY   *P1:10,*EF,"Starting Date : ":
                    *P1:11,*EL,"Ending Date   : ";
          MOVE      "10",CVERT
          MOVE      "17",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,DATE9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,DSPSTDTE
          REP       " 0",DSPSTDTE
.
. ----- Check If Starting Today Is Greater Than Today's Date -----
.
          MATCH     STRTDATE,CURDATE
          IF        @LESS
            DISPLAY    *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                       "date. ";
            CALL       HITENTER
            GOTO       DATE1000
          ENDIF
.
. ----- Enter Ending Date -----
.
DATE2000  MOVE      "11",CVERT
          MOVE      "17",CCOL
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,DATE1000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the ending date
          MOVE      CPCDATE,DSPEDDTE
          REP       " 0",DSPEDDTE
.
. ----- Check If The Ending Today Is Greater Than Today's Date -----
.
          MATCH     ENDDATE,CURDATE
          IF        @LESS
            DISPLAY    *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                       "date. ";
            CALL       HITENTER
            GOTO       DATE2000
          ENDIF
.
. ----- Check That The Ending Date Is Greater Than The Starting Date -----
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending date must be greater than the ":
                      "starting date. ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
.
. ----- Check If Discharge Date Range Exists -----
.
...          PACK      KEY16,STRTDATE,SP14
...          CALL      RDSDSCH2                * Position on discharge file
...          CALL      RDKDSCH2                * Read the discharge file
...          BRANCH    OVRCD,DATE3000
.
...          MATCH     DDATE,ENDDATE
...          GOTO      DATE9999 IF NOT LESS * Patients found in date range, exit
.
...DATE3000  DISPLAY   *P1:24,*EL,*B,"No discharged patients found in the ":
...                    "date range. ";
...          CALL      HITENTER
...          GOTO      DATE1000
.
             GOTO      DATE9999
.
DATE9000  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000               Called by: ML0000   *
.*                               Choose Option                                *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Discharged Patients With ":
                                            "DRG Codes":
                    *P1:6,*V2LON,"2",*HOFF," = Discharged Patients Without ":
                                            "DRG Codes":
                    *P1:8,*HOFF,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,COPTION;
          BRANCH    COPTION,OPTN9999,OPTN9999
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The Date Ranges                           *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:18,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      "18",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:18,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.*                                 DRGR0000               Called by: ML0000   *
.*                              Keyin DRG Range                               *
.******************************************************************************
DRGR0000  MOVE      ZERO,EXIT
.
. ----- Enter Starting DRG Code -----
.
DRGR1000  DISPLAY   *P1:15,*EF,"Starting DRG Code : ":
                    *P1:16,"Ending DRG Code   : ";
.
          MOVE      TEN5,EVERT
          MOVE      TWENTY1,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATDGWKY
.
          MOVE      ZERO,EXIT
          MOVE      DRGCODE,STRTDRG
          ENDSET    STRTDRG
          APPEND    SP4,STRTDRG
          RESET     STRTDRG
.
          MATCH     SLASH,STRTDRG
          GOTO      DRGR9000 IF EQUAL       * Exit char entered
.
          MATCH     SP4,STRTDRG
          IF        @EQUAL
            MOVE      SP4,STRTDRG
            MOVE      "Start ",DSPSTDRG
            DISPLAY   *P21:15,*V2LON,DSPSTDRG;
            GOTO      DRGR2000
          ENDIF
          MOVE      STRTDRG,DSPSTDRG
.
. ----- Validate Starting DRG Code -----
.
          PACK      KEY7,STRTDRG,DRGVERZ,SP10
          CALL      RDPTDGW1                * Read the DRG file
.
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Starting DRG code does not exist. ";
            CALL      HITENTER
            GOTO      DRGR1000
          ENDIF
          MOVE      PTDWDESC,DIM52
          DISPLAY   *P21:15,*V2LON,PTDWDRGC,*P28:15,*HOFF,DIM52;
.
. ----- Enter Ending DRG Code -----
.
DRGR2000  MOVE      TEN6,EVERT
          MOVE      TWENTY1,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATDGWKY
.
          MOVE      ZERO,EXIT
          MOVE      DRGCODE,ENDDRG
.
          ENDSET    ENDDRG
          APPEND    SP4,ENDDRG
          RESET     ENDDRG
.
          MATCH     SLASH,ENDDRG
          GOTO      DRGR1000 IF EQUAL       * Exit char entered
.
          MATCH     SP4,ENDDRG
          IF        @EQUAL
            MOVE      ZED4,ENDDRG
            MOVE      "Finish",DSPEDDRG
            DISPLAY   *P21:16,*V2LON,DSPEDDRG;
            GOTO      DRGR3000
          ENDIF
          MOVE      ENDDRG,DSPEDDRG
.
. ----- Validate Ending DRG Code -----
.
          PACK      KEY7,ENDDRG,DRGVERZ,SP10
          CALL      RDPTDGW1                * Read the DRG file
.
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Ending DRG code does not exist. ";
            CALL      HITENTER
            GOTO      DRGR2000
          ENDIF
          MOVE      PTDWDESC,DIM52
          DISPLAY   *P21:16,*V2LON,PTDWDRGC,*P28:16,*HOFF,DIM52;
.
. ----- Check That The Ending DRG Code Is Greater Than The Starting DRG Code --
.
DRGR3000  MATCH     STRTDRG,ENDDRG
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"The ending DRG code must be greater than ":
                      "the starting DRG code.     ";
            CALL      HITENTER
            GOTO      DRGR2000
          ENDIF
          GOTO      DRGR9999
.
DRGR9000  MOVE      ONE,EXIT
DRGR9999  RETURN
+
.**************************************************************************
.*                                 VDRG0000            Called by: ML0000  *
.*                         Version of DRG's to report            *
.*    Returns:  VERSNDRG = Version of DRG's to report                     *
.**************************************************************************
VDRG0000  MOVE      ZERO,EXIT
          MOVE      SP10,VERSDESC
          MOVE      SP3,VERSNDRG
          MOVE      ZERO,VERSNUMB
.                                                                     *C-137125
          LOAD      VERSDESC,VERSNUMB,DRGD31,DRGD41,DRGD42,DRGD50:
                             DRGD51,DRGD52,DRGD60,DRGD62,DRGD70:
                             DRGD80,DRGD90,DRGD100,DRGHF *C-269384
          DISPLAY   *P1:13,*EF,"DRG Version to report : ",*V2LON,VERSDESC
.
          MOVE      "60",HLEF
          MOVE      "9",HTOP                                          *C-269384
          MOVE      "78",HRIG
          MOVE      "23",HBOT                                         *C-197814
.
          CALL      GETSCR00                     * save screen
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,*V2LON,HLEF,HTOP,HRIG,HBOT
.
          DISPLAY   *P62:10,*V2LON,"1.",*HOFF," DRG Ver 3.1 ":
                    *P62:11,*V2LON,"2.",*HOFF," DRG Ver 4.1 ":
                    *P62:12,*V2LON,"3.",*HOFF," DRG Ver 4.2 ":
                    *P62:13,*V2LON,"4.",*HOFF," DRG Ver 5.0 ":
                    *P62:14,*V2LON,"5.",*HOFF," DRG Ver 5.1 ":
                    *P62:15,*V2LON,"6.",*HOFF," DRG Ver 5.2 ":
                    *P62:16,*V2LON,"7.",*HOFF," DRG Ver 6.0 ":        *I-197814
                    *P62:17,*V2LON,"8.",*HOFF," DRG Ver 6.2 ":        *I-269384
                    *P62:18,*V2LON,"9.",*HOFF," DRG Ver 7.0 ":        *I-290423
                    *P62:19,*V2LON,"10.",*HOFF,"DRG Ver 8.0 ":
                    *P62:20,*V2LON,"11.",*HOFF,"DRG Ver 9.0 ":
                    *P62:21,*V2LON,"12.",*HOFF,"DRG Ver 10.0 ":
                    *P62:22,*V2LON,"13.",*HOFF,"HF DRG      ";
.
          KEYIN     *P25:13,*V2LON,VERSNUMB
          CALL      PUTSCR00                     * restore screen
.                                                                     *C-290423
          IF        VERSNUMB < 1 | VERSNUMB > 13
            DISPLAY   *P1:24,*EL,"Invalid DRG Version ";
            CALL      HITENTER
            GOTO      VDRG0000
          ENDIF
.
.                                                                     *C-137125
          MOVE      PTCNDGPV,DRGVERZ
          LOAD      DRGVERZ,VERSNUMB,DRG031,DRG041,DRG042,DRG050,DRG051:
                                     DRG052,DRG060,DRG062,DRG070:     *C-290423
                                     DRG080,DRG090,DRG100             *C-0876270
          LOAD      VERSDESC,VERSNUMB,DRGD31,DRGD41,DRGD42,DRGD50,DRG051:
                                      DRGD52,DRGD60,DRGD62,DRGD70:    *C-290423
                                      DRGD80,DRGD90,DRGD100,DRGHF     *C-0876270
          DISPLAY   *P25:13,*V2LON,VERSDESC
.
VDRG9999  RETURN
+
.******************************************************************************
.*                                 LOAD0000               Called by: ML0000   *
.*                             Load The Tempfile                              *
.******************************************************************************
LOAD0000  MOVE      ZERO,EXIT
          MOVE      ZERO,TMPCOUNT
          MOVE      ZERO,CPAGENO
          MOVE      SP4,CALCDRGN            *
          DISPLAY   *P1:24,*EL,"Scanning : ",*P25:24,"Loading : ";
          CALL      OPEN0000                * Open the tempfile
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2                * Position on discharge file
LOAD1000  CALL      RDKDSCH2                * Read the discharge file
          BRANCH    OVRCD,LOAD9999
.
          MATCH     DDATE,ENDDATE
          GOTO      LOAD9999 IF LESS        * Out of date range, exit
.
.         Multi-Hospital Processing
.
          COMPARE   ONE,IBCNMHOS
          GOTO      LOAD2000 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      LOAD2000 IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LOAD1000
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LOAD1000 IF NOT EQUAL
.
LOAD2000  IF        COPTION=1
.
. ----- By DRG Code Range -----
.
            MATCH     SP4,PTDSDDRG
            GOTO      LOAD1000 IF EQUAL     * Ignore patients without DRG codes
.
            CALL      GDRG0000              * 
.                                           * Get DRG code from patpgraf
            MATCH     STRTDRG,CALCDRGN
            GOTO      LOAD1000 IF LESS      * DRG code < starting DRG code, get
.                                             next discharge record
            MATCH     CALCDRGN,ENDDRG
            GOTO      LOAD1000 IF LESS      * DRG code > ending DRG code, get
.                                             next discharge record
          ELSE
.
. ----- Discharges Without DRG Codes -----
.
            MATCH     SP4,PTDSDDRG
            GOTO      LOAD1000 IF NOT EQUAL * Ignore patients with DRG codes
          ENDIF
.
          DISPLAY   *P12:24,*V2LON,DADMNO;
          PACK      KEY8,DURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,LOAD1000
.
          PACK      KEY8,DADMNO
          CALL      RDMISS1                 * Read the admissions file
          BRANCH    OVRCD,LOAD1000
.
          CALL      EXNB0000                * Excluded newborn
          BRANCH    EXIT,LOAD1000
.
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1                * Cat."A "
          BRANCH    OVRCD,LOAD2100
          MATCH     "B",TCINDC3            * is this a boarder?
          GOTO      LOAD1000 IF EQUAL      * ignore boarders
.
LOAD2100  CALL      CLER0000                * Clear tempfile variables
          MOVE      CALCDRGN,DRG            * C-52766
          MOVE      AURNO,URNO
          MOVE      DADMNO,ADMISS
          ADD       ONE,TMPCOUNT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patients name
          MOVE      PACFNAME,PATNAME
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format discharge date
          MOVE      CPDATE,DSPDSDTE
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1                 * Read the doctors file
          IF        OVRCD<>1
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME               * Format attending doctors name
            MOVE      PACFNAME,ADOCNAME
          ENDIF
          MOVE      ADATE,FROMDATE
          MOVE      DDATE,TODATE
          CALL      RTIODAYS                * Calculate no. of days in hospital
          MOVE      NBRDAYS,LOS
          MOVE      PTDSDMDC,MDC
          PACK      KEY20,DRG,URNO,ADMISS,SP20
          CALL      WRTEMP1                 * Write to the 1st tempfile
          DISPLAY   *P35:24,*V2LON,ADMISS;
          GOTO      LOAD1000
.
LOAD9000  MOVE      ONE,EXIT
LOAD9999  RETURN
+
+
.****************************************************************************
.*                              GDRG0000               Called by: PDIS0000  *
.*                     Get DRG Code from patpgraf                           *
.*    Steps followed:  Get user's choice of DRG version                     *
.*                     IF HF DRG(choice 13),use AFUNDH & AFNDTB to get      *
.*                     PTHFGRPV and then read patpgraf                      *
.*                     IF HF DRG not set up, use Cat CL with ACLAIM to read *
.*                     PTCDGRPV                                             *
.*                     IF PTCDGRPV for claim code not set up,use system     *
.*                     parameter PTCNDGPV                                   *
.*                                                                          *
.****************************************************************************
GDRG0000  COMPARE  TEN3,VERSNUMB                 * 13 = HF DRG        *C-290423
          GOTO     GDRG1000 IF NOT EQUAL
          PACK     KEY8,DDADMNO,SP10
          CALL     RDMISS1
          BRANCH   OVRCD,GDRG9999
          MATCH    SP6,AFUNDH
          GOTO     GDRG0500 IF EQUAL
          PACK     KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
          CALL     RDFUND1
          BRANCH   OVRCD,GDRG0500
          MATCH    SP3,PTHFGRPV
          GOTO     GDRG0500 IF EQUAL
          MOVE     PTHFGRPV,VERSNDRG
          GOTO     GDRG1050
GDRG0500  MATCH    SP3,ACLAIM
          GOTO     GDRG0700 IF EQUAL
          PACK     KEY5,ANSC,ANSL,ACLAIM,SP10
          CALL     RDCODE1
          BRANCH   OVRCD,GDRG0700
          MATCH    SP3,PTCDGRPV
          GOTO     GDRG0700 IF EQUAL
          MOVE     PTCDGRPV,VERSNDRG
          GOTO     GDRG1050
GDRG0700  MOVE     PTCNDGPV,VERSNDRG
          GOTO     GDRG1050
.                                                                     *C-137125
GDRG1000  LOAD     VERSNDRG,VERSNUMB,DRG031,DRG041,DRG042,DRG050:
                                     DRG051,DRG052,DRG060,DRG062:     *C-269384
                                     DRG070,DRG080,DRG090,DRG100      *C-290423
GDRG1050  PACK     KEY13,DDADMNO,SP1,ONE,VERSNDRG,SP10    * episodeno =1
          CALL     RDPTPGR1
          BRANCH   OVRCD,GDRG9950
          MOVE     PTPGNDRG,CALCDRGN
          GOTO     GDRG9999
GDRG9950  MOVE     SP4,CALCDRGN
GDRG9999  RETURN
+
.******************************************************************************
.*                                 CLER0000               Called by: LOAD0000 *
.*                         Clear Tempfile Variables                           *
.******************************************************************************
CLER0000  UNPACK    SP70,DRG,URNO,ADMISS,PATNAME,ADOCNAME,MDC
          MOVE      ZERO,LOS
CLER9999  RETURN
+
.******************************************************************************
.*                                 PRNT0000               Called by: ML0000   *
.*                             Print The Report                               *
.******************************************************************************
PRNT0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"Printing : ";
          CALL      HEDA0000                * Print the main report heading
          PACK      KEY20,SP20
          CALL      RSTEMP1                 * Position at the start of the
          CALL      RKTEMP1                   tempfile & read the next record
          BRANCH    OVRCD,PRNT9000
.
          CALL      HEDB0000                * Print the DRG heading
          PACK      KEY20,SP20
          CALL      RSTEMP1                 * Position at the start of the
PRNT1000  CALL      RKTEMP1                   tempfile & read the next record
          BRANCH    OVRCD,PRNT9100
.
          MATCH     DRG,SAVDRG
          IF        !@EQUAL
            IF        CLNO>46
              CALL      TALB0000            * Print the DRG tail
              CALL      HEDA0000            * Print the main report heading
              CALL      HEDB0000            * Print the DRG heading
            ELSE
              CALL      TALB0000            * Print the DRG tail
              CALL      HEDB0000            * Print the DRG heading
            ENDIF
          ENDIF
.
          IF        CLNO>55
            CALL      UND132                * Print an underline
            CALL      HEDA0000              * Print the main report heading
            CALL      HEDB0000              * Print the DRG heading
          ENDIF
.
          CALL      GCOD0000                * Get the disease & operation codes
.
          MOVE      DSPDSDTE,DIM8
          MOVE      OPRLINE[1],DIM27
          PRINT     *1,"|",*2,URNO,*10,"|",*11,ADMISS,*19,"|",*20,PATNAME:
                    *37,"|",*38,DIM8,*46,"|",*47,ADOCNAME,*63,"|",*64,LOS:
                    *69,"|",*70,MDC,*74,"|",*75,DISLINE[1]:
                    *104,"|",*105,DIM27,*132,"|"
          ADD       ONE,CLNO
          ADD       ONE,PATCOUNT
          IF        DISCOUNT>1 | OPRCOUNT>1
            IF        DISCOUNT>OPRCOUNT
              MOVE      DISCOUNT,CODECONT
            ELSE
              MOVE      OPRCOUNT,CODECONT
            ENDIF
            MOVE      TWO,FORM2
            REPEAT
              MOVE      OPRLINE[FORM2],DIM27
              PRINT     *1,"|",*10,"|",*19,"|",*37,"|",*46,"|",*63,"|",*69,"|":
                        *74,"|",DISLINE[FORM2],*104,"|",DIM27:
                        *132,"|"
              ADD       ONE,CLNO
              ADD       ONE,FORM2
            UNTIL     FORM2>CODECONT
          ENDIF
          DISPLAY   *P12:24,*V2LON,ADMISS;
          GOTO      PRNT1000
.
PRNT9000  CALL      HEDB0000                * Print the DRG heading
          PRINT     *N                                            
          GOTO      PRNT9200
.
PRNT9100  CALL      TALB0000                * Print the DRG tail
PRNT9200  PRINT     *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete. ";
          CALL      HITENTER
PRNT9999  RETURN
+
.******************************************************************************
.*                                 HEDA0000               Called by: PRNT0000 *
.*                          Print The Main Heading                            *
.******************************************************************************
HEDA0000  CALL      HEAD132                 * Print main report heading
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME,*N
            ADD       "2",CLNO
          ENDIF
.
          PRINT     *40,"Starting Discharge Date : ",DSPSTDTE,*N:
                    *40,"Ending Discharge Date   : ",DSPEDDTE,*N
          ADD       "3",CLNO
.
          IF        COPTION=1
            PRINT     *40,"Starting DRG Code : ",DSPSTDRG,*N:
                      *40,"Ending DRG Code   : ",DSPEDDRG:            *C-117741
                      *80,"DRG Version : ",VERSDESC,*N                *I-117741
            ADD       "3",CLNO
          ELSE
            PRINT     *40,"Discharged Patients Without DRG Codes",*N
            ADD       "2",CLNO
          ENDIF
HEDA9999  RETURN
+
.******************************************************************************
.*                                 HEDB0000               Called by: PRNT0000 *
.*                           Print The DRG Heading                            *
.******************************************************************************
HEDB0000  MOVE      ZERO,EXIT
.******************************************** start of addition       *I-137125
          MOVE      SP70,PTDWDESC
          MATCH     SP3,VERSNDRG
          IF        @EQUAL
.                                                * a HF DRG listing
            PACK      KEY7,DRG,ZED4
            CALL      RSPTDGW1
            CALL      RPPTDGW1
            MATCH     PTDWDRGC,DRG
          ELSE
            PACK      KEY7,DRG,VERSNDRG,SP10
            CALL      RDPTDGW1                * Read the DRG file
            IF        OVRCD = 1
              PACK      KEY7,DRG,ZED4
              CALL      RSPTDGW1
              CALL      RPPTDGW1
            ENDIF
          ENDIF
          MATCH     PTDWDRGC,DRG
          IF        !@EQUAL
            MOVE      "Unknown DRG Code ",PTDWDESC
          ENDIF
.********************************************   end of addition       *I-137125
.
HEDB1000  MOVE      DRG,SAVDRG
          IF        COPTION=1
            MATCH     SP4,DRG
            IF        @EQUAL
              PRINT     *1,"DRG Code : No DRG Allocated"
            ELSE
              PRINT     *1,"DRG Code : ",DRG,SP2,PTDWDESC
            ENDIF
            ADD       "1",CLNO
          ENDIF
          CALL      UND132                  * Print an underline
          PRINT     *1,"|U/R No",*10,"|Admin.No",*19,"|Patient Name":
                    *37,"|Dis.Date",*46,"|Attending Doctor",*63,"| LOS":
                    *69,"|MDC",*74,"|Disease Codes",*104,"|Operation Codes":
                    *132,"|"
          ADD       ONE,CLNO
          CALL      UND132                  * Print an underline
HEDB9999  RETURN
+
.******************************************************************************
.*                                 TALB0000               Called by: PRNT0000 *
.*                            Print The DRG Tail                              *
.******************************************************************************
TALB0000  CALL      UND132                  * Print an underline
          IF        COPTION=1
            MATCH     SP4,SAVDRG
            IF        @EQUAL
              PRINT     *1,"Total Number of Patient(s) where No DRG Code ":
                        "was Allocated is ",PATCOUNT,*N,*N
            ELSE
              PRINT     *1,"Total Number of Patient(s) for DRG Code ",SAVDRG:
                        " is ",PATCOUNT,*N,*N
            ENDIF
          ELSE
            PRINT     *1,"Total Number of Patient(s) without DRG Codes ":
                      PATCOUNT,*N,*N
          ENDIF
          ADD       "3",CLNO
          MOVE      ZERO,PATCOUNT
TALB9999  RETURN
+
.******************************************************************************
.*                                 GCOD0000               Called by: PRNT0000 *
.*                      Get The Disease & Operation Codes                     *
.******************************************************************************
GCOD0000  MOVE      ADMISS,AADMNO
          MOVE      ONE,DISCOUNT
          REPEAT
            CLEAR     DISLINE[DISCOUNT]     * Clearing all dis. line positions
            ADD       ONE,DISCOUNT
          UNTIL     DISCOUNT>10
          MOVE      ONE,DISCOUNT
          PACK      KEY16,AADMNO,SP20                                 *C-240107
          CALL      RSPTECD2                * Position on & read a patient
GCOD1000  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,GCOD2000
.
          MATCH     AADMNO,PTEDADMN
          GOTO      GCOD2000 IF NOT EQUAL   * Different admission number
.
          MOVELPTR  DISLINE[DISCOUNT],DISPNTER * Find disease line position
          STRIP     PTEDCODE
          MOVELPTR  PTEDCODE,NEWPNTER       * Find disease code position
          IF        (DISPNTER+NEWPNTER+1)>29
            APPEND    SP30,DISLINE[DISCOUNT]
            RESET     DISLINE[DISCOUNT]     * Setup disease line for printing
            ADD       ONE,DISCOUNT          * Get next disease line
            MOVE      ZERO,DISPNTER         * Reset disease line position
            IF        DISCOUNT>10
              MOVE      TEN,DISCOUNT        * Reset to max disease lines
              GOTO      GCOD2500            * Maximum disease lines
            ENDIF
          ENDIF
          IF        DISPNTER<>0
            APPEND    SP1,DISLINE[DISCOUNT] * No space before 1st disease code
          ENDIF
          APPEND    PTEDCODE,DISLINE[DISCOUNT]
          GOTO      GCOD1000
.
GCOD2000  APPEND    SP30,DISLINE[DISCOUNT]
          RESET     DISLINE[DISCOUNT]
.
. ----- Get The Operation Codes -----
.
GCOD2500  MOVE      ONE,OPRCOUNT
          REPEAT
            CLEAR     OPRLINE[OPRCOUNT]     * Clearing all opr. line positions
            ADD       ONE,OPRCOUNT
          UNTIL     OPRCOUNT>10
          MOVE      ONE,OPRCOUNT
          PACK      KEY16,AADMNO,SP20                                 *C-240107
          CALL      RSPTECO2                * Position on & read a patient
GCOD3000  CALL      RKPTECO2                  epsiode operation file record
          BRANCH    OVRCD,GCOD4000
.
          MATCH     AADMNO,PTEOADMN
          GOTO      GCOD4000 IF NOT EQUAL   * Different admission number
.
          MOVELPTR  OPRLINE[OPRCOUNT],OPRPNTER * Find operation line position
          STRIP     PTEOCODE
          MOVELPTR  PTEOCODE,NEWPNTER       * Find operation code position
          IF        (OPRPNTER+NEWPNTER+1)>29
            APPEND    SP30,OPRLINE[OPRCOUNT]
            RESET     OPRLINE[OPRCOUNT]     * Setup operation line for printing
            ADD       ONE,OPRCOUNT          * Get next operation line
            MOVE      ZERO,OPRPNTER         * Reset operation line position
            IF        OPRCOUNT>10
              MOVE      TEN,OPRCOUNT        * Reset to max operation lines
              GOTO      GCOD9999            * Maximum operation lines
            ENDIF
          ENDIF
          IF        OPRPNTER<>0
            APPEND    SP1,OPRLINE[OPRCOUNT] * No space before 1st operation code
          ENDIF
          APPEND    PTEOCODE,OPRLINE[OPRCOUNT]
          GOTO      GCOD3000
.
GCOD4000  APPEND    SP30,OPRLINE[OPRCOUNT]
          RESET     OPRLINE[OPRCOUNT]
GCOD9999  RETURN
+
.******************************************************************************
.*                                 OPEN0000               Called by: LOAD0000 *
.*                             Open The Tempfile                              *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    FILELINE,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID          * Create the tempfile
          OPEN      TEMP1,FNAME
OPEN9999  RETURN
+
.******************************************************************************
.*                                 KILL0000               Called by: ML0000   *
.*                            Delete The Tempfile                  & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID          * Delete the tempfile
KILL9999  RETURN
+
.******************************************************************************
.*                        I/O Routines For The Tempfiles                      *
.******************************************************************************
RSTEMP1   RESET     KEY20
          READ      TEMP1,KEY20;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;DRG,URNO,ADMISS,TMPCOUNT,PATNAME,DSPDSDTE,ADOCNAME:
                          LOS,MDC
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY20
          WRITE     TEMP1,KEY20;DRG,URNO,ADMISS,TMPCOUNT,PATNAME,DSPDSDTE:
                                ADOCNAME,LOS,MDC
          RETURN
.
.==============================================================================
.
          INC       STD001IO
          INC       EXCLNEWB
          INC       RTIODAYS
          INC       PATDGWKY
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATDGWIO/INC            * DRG description & weightings file
          INC       PATDO1IO/INC            * Doctors file
          INC       PATDSCIO/INC            * Discharge file
          INC       PATCODIO/INC            * Codes file
          INC       PATECDIO/INC            * Patient episode disease file
          INC       PATECOIO/INC            * Patient episode operation file
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC            * Patient master file
          INC       PATMI1IO/INC            * Patient admission file
          INC       PATTRNIO/INC            * Patient transfer file
          INC       PATPGRIO/INC            * patpgraf    
          INC       PATFN1IO/INC            * patfundf    
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
+
.


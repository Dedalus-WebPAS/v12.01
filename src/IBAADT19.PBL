.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   IBAADT19                                                  *
.* Desc      :   Overnight Fee Master Listing                              *
.***************************************************************************
.* Date      :   31/07/96                                                  *
.* Author    :   J.Tan                                                     *
.* Modifications  :                                                        *
.*        V10.01.02 08/02/2011  Davin          CAR 233055                  *
.*                  Removed option 2 (new files)                           *
.*        V10.01.01 28/01/2011  Ebon Clements  CAR 233050                  *
.*                  Recompiled for changes to PATCMXFD                     *
.*        V9.12.03  14/12/09 J.Tan        CAR 212419                       *
.*                  Mods to allow selecting Contract ID from New file      *
.*        V9.12.02  25/11/09 J.Tan        CAR 211002                       *
.*                  Mods for range of contract ID                          *
.*        V9.12.01  09/09/2009 WeeLee Koo    CAR 205303                    *
.*                  Added Contract ID                                      *
.*        V9.09.01  10/01/2008 Ebon Clements CAR 149251                    *
.*                  Moved PTHLLREB and PTHLLPAT to form 8.2 before print   *
.*        V9.04.01  03/08/05 J.Tan    CAR 62750                            *
.*                  Removed redefined amount variables                     *
.*        V9.03.02  24/09/2003  CAR 42191   Mike Laming                    *
.*                  Correct Option 5 Revenue/Invoice B'down - rehash the   *
.*                  routine PREB0000                                       *
.*        V9.03.01  30/05/2003  Dean Cramer    CAR 38200                   *
.*                  Fixed web heading overlap when specific funds entered  *
.*        V9.02.02  23/04/2003  Dean Cramer  CAR 38200                     *
.*                  Fixed print overlaps for web                           *
.*                  Fixed heading details for entered ranges               *
.*                  Fixed heading lineup for additional charges            *
.*        V9.02.01  31/10/2002  J.Tan                                      *
.*                  Fixed printing print Rev. & Inv. Breakdown details     *
.*        V5.09.B02 11/12/2000  Tonii  509 mods                            *
.*                  Mods for option to print Rev. & Inv. Breakdown details *
.***************************************************************************
.*        V6.06.01  10/02/1999  Jill Habasque                              *
.*                  Fixed DISPHEAD display                                 *
.***************************************************************************
          INC       STD001FD
          INC       PATAFEFD/INC
          INC       PATCMXFD/INC
          INC       PATHDFFD/INC
          INC       PATLDFFD/INC  
          INC       PATHLFFD/INC  
          INC       PATCODFD/INC
          INC       PATOVBFD/INC
          INC       PATIOVFD/INC
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
ACLAIM    DIM       3
ACLAIMA   DIM       3
AFUNDH    DIM       6
AFUNDHA   DIM       6
CODE      DIM       2
COUNT     FORM      2
CONTID    DIM       6                         * Contract ID (db)
DIM15     DIM       15
DIM15A    DIM       15
DIM20     DIM       20
DIM20A    DIM       20
ECOL1     FORM      2
FOPTION   FORM      1
FRCLM     DIM       5
FRMCONTR  DIM       6
FORM8P2   FORM      8.2
FORM8P2A  FORM      8.2
FORM8P2B  FORM      8.2
OVRFLG    FORM      1
OVROVB    FORM      1                                                  *I-90302
OVRINV    FORM      1                                                  *I-90302
TOCLM     DIM       5
ENDCONTR  DIM       6
TEMPAMNT  DIM       15                              * was FORM 12.2    *C-90302
TMPAMNT   DIM       15                              * was FORM 12.2    *C-90302
STRCLM    DIM       3
ENDCLM    DIM       3
STRHF     DIM       6
ENDHF     DIM       6
PDSFLAG   FORM      1
SAVCONTR  DIM       6
.
Z2        INIT      "zz"
.
PRGNAM    INIT      "Overnight Fee Master Listing"
PRGID     INIT      "IBAADT19"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * initialization
.                                       * open files
.                                       * display header
.
ML1000    CALL      MOPT0000            * Display main options
          BRANCH    EXIT,ML9999
          GOTO      ML2000
.
ML1500    CALL      PUTSCR00            * Redisplay screen
ML2000    CALL      OPTN0000            * select option
          BRANCH    EXIT,ML1000
.
          CALL      GETSCR00            * Save current screen
          CALL      OPFL0000            * Open file
          BRANCH    EXIT,ML1500
.
          CALL      KCLM0000            * Keyin range of claim code
          BRANCH    EXIT,ML1500
.
          CALL      KFUN0000            * Keyin range of health fund
.
          CALL      CNID0000            * Select Contract ID
          CALL      DOPT0000            * print full description ?
.
ML2400    CALL      CONTQST             * OK to continue ?
          BRANCH    CEXIT,ML3000,ML1500,ML9999
          BEEP
          GOTO      ML2400
.
ML3000    CALL      PRIN0000            * print all details
          GOTO      ML1500
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P50:24,"Opening ";
.
          DISPLAY   *P60:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patcmxaf";
          OPEN      PATCMXA2,"patcmxaf"
.
INIT9999  RETURN
+
.**********************************************************************
.*                           MOPT0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
MOPT0000  MOVE       FALSE,EXIT
          CALL       DISPHEAD
          DISPLAY    *P45:2:
                     *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF," = Lumpsum ":
                     *P1:6,*V2LON,TWO,*HOFF," = Low Outliers ":
                     *P1:7,*V2LON,THREE,*HOFF," = High Outliers ":
                     *P1:8,*V2LON,FOUR,*HOFF," = Additional ":
                     *P1:9,*V2LON,FIVE,*HOFF," = Revenue and Invoice ":
                                              "Breakdown ";
.
MOPT1000  KEYIN      *P1:11,*EL,"Please Select : ",*V2LON,FOPTION;
          COMPARE    ZERO,FOPTION
          GOTO       MOPT9000 IF EQUAL
.
          BRANCH     FOPTION,MOPT2000,MOPT3000,MOPT4000,MOPT5000,MOPT6000
          BEEP
          GOTO       MOPT1000
.
MOPT2000  DISPLAY    *P54:2,*V2LON,*+," - Lumpsum";
          GOTO       MOPT9999
.
MOPT3000  DISPLAY    *P54:2,*V2LON,*+," - Low Outliers";
          GOTO       MOPT9999
.
MOPT4000  DISPLAY    *P54:2,*V2LON,*+," - High Outliers";
          GOTO       MOPT9999
.
MOPT5000  DISPLAY    *P54:2,*V2LON,*+," - Additional";
          GOTO       MOPT9999
.
MOPT6000  DISPLAY    *P15:2,SP1,*+,PRGNAM,*V2LON:
                                *+," - Revenue and Invoice Breakdown";
          GOTO       MOPT9999
.
MOPT9000  MOVE       TRUE,EXIT
MOPT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          DISPLAY    *P1:13,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:14,*V2LON,ONE,*HOFF," = Claim Code Seq. ";
.
OPTN1000  KEYIN      *P1:17,*EL,"Please Select : ",*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9000 IF EQUAL
.
          BRANCH     OPTION,OPTN9999
          BEEP
          GOTO       OPTN1000
.
OPTN9000  MOVE       TRUE,EXIT
OPTN9999  RETURN
+
.**********************************************************************
.*                             OPFL0000                               *
.*                Open Lumpsum overnight file                         *
.**********************************************************************
OPFL0000  IF        FOPTION = 1
            OPEN      PATHLFA1,"pathlfaf"      * Lumpsum charge      
          ELSE
            IF        FOPTION = 2
              OPEN      PATLDFA1,"patldfaf"    * Low outliers         
            ELSE
              IF        FOPTION = 3
                OPEN      PATHDFA1,"pathdfaf"  * High outliers          
              ELSE
                IF        FOPTION = 4
                  OPEN      PATAFEA1,"patafeaf"  * Additional charge
                ELSE
                  OPEN      PATHLFA1,"pathlfaf"      * Lumpsum charge
                  OPEN      PATOVBA1,"patovbaf"  * Revenue Breakdown
                  OPEN      PATIOVA1,"patiovaf"  * Invoice Breakdown
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          MOVE      ZERO,EXIT
OPFL9999  RETURN
+
.**********************************************************************
.*                             KCLM0000                               *
.*                keyin range of claim codes                          *
.**********************************************************************
KCLM0000  MOVE       ZERO,EXIT
          UNPACK     SP10,FRCLM,TOCLM
          MOVE       SP20,STRCLM
          MOVE       SP20,ENDCLM
.
          DISPLAY    *P1:4,*EF,"From Claim Code   :":
                     *P1:5,"To   Claim Code   :";
.
          MOVE      "4",EVERT
          MOVE      "20",ECOL               * keyin position
          MOVE      "30",ECOL1              * Display description position
          MOVE      SP3,ACODE
          MOVE      "CL",CODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM1000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,STRCLM  
          MOVE      STRCLM,FRCLM
          GOTO      KCLM5000
.
KCLM1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          MOVE      "START",FRCLM
          MOVE      ZERO,EXIT
.
KCLM5000  MOVE      "5",EVERT
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM6000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,ENDCLM  
          MOVE      ENDCLM,TOCLM
          GOTO      KCLM9999
.
KCLM6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          MOVE      "END  ",TOCLM
          MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.**********************************************************************
.*                             KFUN0000                               *
.*                Keyin range of health funds                         *
.**********************************************************************
KFUN0000  MOVE      SP6,STRHF
          KEYIN     *P1:7,*EL,"Starting Health Fund : ",*V2LON,STRHF
          ENDSET    STRHF
          APPEND    SP6,STRHF
          RESET     STRHF
.
          MATCH     SP6,STRHF
          IF        @EQUAL
            DISPLAY   *P24:7,*V2LON,"Start"
          ELSE
            DISPLAY   *P24:7,*V2LON,STRHF
          ENDIF
.
          MOVE      SP6,ENDHF
          KEYIN     *P1:8,*EL,"Ending   Health Fund : ",*V2LON,ENDHF
          ENDSET    ENDHF
          APPEND    SP6,ENDHF
          RESET     ENDHF
.
          MATCH     SP6,ENDHF
          IF        @EQUAL
            DISPLAY   *P24:8,*V2LON,"End"
            MOVE      "ZZZZZZ",ENDHF
          ELSE
            DISPLAY   *P24:8,*V2LON,ENDHF
          ENDIF
.
          MATCH     STRHF,ENDHF
          GOTO      KFUN9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending H.F. < Starting H.F. **";
          CALL      HITENTER
          GOTO      KFUN0000
.
KFUN9999  RETURN
+
.******************************************************************************
.*                                 CNID0000              Called By : ML0000   *
.*                          Select Contract ID                                *
.******************************************************************************
.
CNID0000  MOVE      SP6,FRMCONTR
          KEYIN     *P1:10,*EL,"Keyin Contract ID : ",*V2LON,FRMCONTR
          ENDSET    FRMCONTR
          APPEND    SP70,FRMCONTR
          RESET     FRMCONTR
.
          MATCH     SP70,FRMCONTR
          IF        @EQUAL
            DISPLAY   *P23:12,*EL,"Start";
            GOTO      CNID2000
          ENDIF
.
.         PACK      KEY24,FRMCONTR,SP70
.         CALL      RSPTCMX2
.         CALL      RKPTCMX2
.         BRANCH    OVRCD,CNID9000
.
.         MATCH     FRMCONTR,PTCMCNID
.         GOTO      CNID9000 IF NOT EQUAL
.
CNID2000  MOVE      SP6,ENDCONTR
          KEYIN     *P1:12,*EL,"Ending Contract ID : ",*V2LON,ENDCONTR
          ENDSET    ENDCONTR
          APPEND    SP70,ENDCONTR
          RESET     ENDCONTR
.
          MATCH     SP70,ENDCONTR
          IF        @EQUAL
            DISPLAY   *P23:12,*EL,"End";
            MOVE      "ZZZZZZ",ENDCONTR
            GOTO      CNID9999
          ENDIF
.
.         PACK      KEY24,ENDCONTR,SP70
.         CALL      RSPTCMX2
.         CALL      RKPTCMX2
.         BRANCH    OVRCD,CNID9000
.
.         MATCH     ENDCONTR,PTCMCNID
.         GOTO      CNID9000 IF NOT EQUAL
.
          GOTO      CNID9999
.
CNID9000  DISPLAY   *P1:24,*EL,*B,"Invalid Contract ID. ";
          CALL      HITENTER
          GOTO      CNID0000
.
CNID9999  RETURN
+
.******************************************************************************
.*                                 DOPT0000              Called By : ML0000   *
.*                         Print full description ?                           *
.******************************************************************************
DOPT0000  MOVE      ZERO,PDSFLAG
          MOVE      "N",ANS
          KEYIN     *P1:10,*EL,"Print full description (Y/N) ? ",*RV,*V2LON,ANS;
          REP       "yYnN",ANS
          CMATCH    "N",ANS
          GOTO      DOPT9999 IF EQUAL
          MOVE      ONE,PDSFLAG
          CMATCH    "Y",ANS
          GOTO      DOPT9999 IF EQUAL
          BEEP
          GOTO      DOPT0000
.
DOPT9999  RETURN
+
.******************************************************************************
.*                                 KCOD0000              Called By : ML0000   *
.*                         Keyin codes                                        *
.******************************************************************************
KCOD0000  CALL      KEYINCOD
          BRANCH    EXIT,KCOD1000:          * Nothing entered
                         KCOD2000           * Exitchar entered
.
KCOD1000  DISPLAY   *PECOL1:EVERT,*V2LON,TDESC;
KCOD2000  RETURN
+
.**********************************************************************
.*                             PRIN0000                               *
.*                Read file and print all details                     *
.**********************************************************************
PRIN0000  MOVE       ZERO,CPAGENO
          CLOCK      TIME,CTIMEIS
          MOVE       "60",CLNO
          MOVE       SP70,SAVCONTR
.
          DISPLAY    *P1:24,*EL,"Printing Claim Code : ";
.
          CALL       RSTEMP1                       * position to start of file
PRIN1000  CALL       RKTEMP1 
          BRANCH     OVRCD,PRIN8000
.
          DISPLAY    *P35:24,*EL,*V2LON,ACLAIM,*P42:24,"- ",AFUNDH;
.
          MATCH      SP70,FRMCONTR
          IF         !@EQUAL
            MATCH      FRMCONTR,CONTID
            GOTO       PRIN8000 IF LESS
          ENDIF
.
          MATCH      SP70,ENDCONTR
          IF         !@EQUAL
            MATCH      CONTID,ENDCONTR
            GOTO       PRIN8000 IF LESS
          ENDIF
.
          MATCH      SP70,STRCLM
          IF         !@EQUAL
            MATCH      STRCLM,ACLAIM
            GOTO       PRIN1000 IF LESS
          ENDIF
.
          MATCH      SP70,ENDCLM
          IF         !@EQUAL
            MATCH      ACLAIM,ENDCLM              * within selected range?
            GOTO       PRIN1000 IF LESS           * no.
          ENDIF
.
          MATCH     SP6,STRHF                     * check if hf entered ?
          IF        !@EQUAL
            MATCH     STRHF,AFUNDH
            GOTO      PRIN1000 IF LESS
          ENDIF
.
          MATCH     SP6,ENDHF
          IF        !@EQUAL
            MATCH     AFUNDH,ENDHF
            GOTO      PRIN1000 IF LESS
          ENDIF
.
          MATCH      CONTID,SAVCONTR
          GOTO       PRIN6000 IF EQUAL
.
          MATCH      SP70,SAVCONTR
          IF         !@EQUAL
            CALL       UND132
          ENDIF
          MOVE       CONTID,SAVCONTR
          CALL       HEAD0000
.
PRIN6000  COMPARE    ONE,FOPTION
          CALL       PREC0000 IF EQUAL             * print a lumpsum Record  
.
          COMPARE    TWO,FOPTION
          CALL       PREL0000 IF EQUAL             * print low outliers
.
          COMPARE    THREE,FOPTION
          CALL       PREH0000 IF EQUAL             * print high outliers
.
          COMPARE    FOUR,FOPTION
          CALL       PREA0000 IF EQUAL             * print additional charge
.
          COMPARE    FIVE,FOPTION
          CALL       PREB0000 IF EQUAL             * print Rev. & Inv. Brkdwn
.
          GOTO       PRIN1000
.
PRIN8000  COMPARE    ZERO,CPAGENO
          GOTO       PRIN9000 IF EQUAL
.
          CALL       UND132
          PRINT      *N,"*** END OF REPORT  ***";
          GOTO       PRIN9999
.
PRIN9000  DISPLAY   *P1:24,*EL,*B,"No Data Available. ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.**********************************************************************
.*                                HEAD0000                            *
.*                          Print page header                         *
.**********************************************************************
HEAD0000  CALL      HEAD132
.
          IF        FOPTION = 1
            PRINT     *1,"** Lumpsum Charge";
          ELSE
            IF        FOPTION = 2
              PRINT     *1,"** Low Outliers";
            ELSE
              IF        FOPTION = 3
                PRINT     *1,"** High Outliers";
              ELSE
                IF        FOPTION = 4
                  PRINT     *1,"** Additional Charge";
                ELSE
                  PRINT     *1,"** Revenue and Invoice Breakdown";
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PRINT         *40,"Claim Code from  : ",FRCLM:
                        *65," to : ",TOCLM,*N;
.
          PRINT     *40,"Health Fund from :";
.
          MATCH     SP6,STRHF
          GOTO      HEAD0100 IF EQUAL
.
          PRINT     *59,STRHF;
          GOTO      HEAD0200
.
HEAD0100  PRINT     *59,"START";
.
HEAD0200  PRINT     *65," to : ";
          MATCH     "ZZZZZZ",ENDHF
          GOTO      HEAD0300 IF EQUAL
.
          PRINT     *71,ENDHF,*N;
          GOTO      HEAD0400
.
HEAD0300  PRINT     *71,"END",*N;
.
HEAD0400  PRINT     *40,"Contract from :";
.
          MATCH     SP6,FRMCONTR
          GOTO      HEAD0600 IF EQUAL
.
          PRINT     *59,FRMCONTR;
          GOTO      HEAD0700
.
HEAD0600  PRINT     *59,"START";
.
HEAD0700  PRINT     *65," to : ";
          MATCH     "ZZZZZZ",ENDCONTR
          GOTO      HEAD0800 IF EQUAL
.
          PRINT     *71,ENDCONTR,*N;
          GOTO      HEAD0900
.
HEAD0800  PRINT     *71,"END",*N;
.
HEAD0900  PRINT     *3,"Contract ID      : ",SAVCONTR,*N;
.
          CALL      UND132
.
          BRANCH    FOPTION,HEAD1000,HEAD2000,HEAD3000,HEAD4000,HEAD5000
.
.         Lumpsum
.
HEAD1000  PRINT     *1,"| Claim Code":
                    *26,"| Health":
                    *35,"| Table Type":
                    *50,"| Casemix":
                    *63,"| Description":
                    *100,"|        Lumpsum":
                    *124,"|Outlier",*132,"|":
                    *N,*1,"|",*26,"| Fund",*35,"|":
                    *50,"|  Code    ",*63,"|":
                    *100,"|  Rebate",*112,"|  Patient":
                    *124,"|Cutoff",*132,"|"
          GOTO      HEAD9000
.
.         Low outliers
.
HEAD2000  PRINT     *1,"| Claim Code":
                    *26,"| Health":
                    *35,"| Table Type":
                    *48,"| Casemix":
                    *61,"|Ending":
                    *69,"| Description":
                    *107,"|        Amount ",*132,"|":
                    *N,*1,"|",*26,"| Fund",*35,"|":
                    *48,"|  Code    ",*61,"| Day",*69,"|":
                    *107,"|  Rebate",*119,"|  Patient",*132,"|"
          GOTO      HEAD9000
.
.         High outliers
.
HEAD3000  PRINT     *1,"| Claim Code":
                    *26,"| Health":
                    *34,"| Table Type":
                    *46,"| Casemix":
                    *58,"| End":
                    *64,"| Day":
                    *70,"| Description":
                    *108,"|        Amount ",*132,"|":
                    *N,*1,"|",*26,"| Fund",*34,"|":
                    *46,"|  Code",*58,"| Day",*64,"|Type",*70,"|":
                    *108,"|  Rebate",*120,"|  Patient",*132,"|"
          GOTO      HEAD9000
.
.         Additional
.
HEAD4000  PRINT     *1,"|Claim Code":
                    *26,"| Health":
                    *34,"| Table Type":
                    *47,"| Casemix":
                    *58,"| Bed ":
                    *63,"| End":
                    *70,"| Description":
                    *108,"|        Amount ",*132,"|":
                    *N,*1,"|",*26,"| Fund",*34,"|":
                    *47,"|  Code",*58,"|Type",*63,"| Day",*70,"|":
                    *108,"|  Rebate",*120,"|  Patient",*132,"|"
          GOTO      HEAD9000
.
.         Revenue and Invoice Breakdown
.
HEAD5000  PRINT     *1,"| Claim Code":
                    *25,"| Health":
                    *34,"| Table":
                    *42,"| Casemix":
                    *53,"|   Revenue Breakdown":
                    *77,"|    Revenue":
                    *93,"|   Invoice Breakdown":
                    *116,"|    Invoice":
                    *132,"|":
                    *N,*1,"|",*25,"| Fund",*34,"| Type",*42,"|  Code":
                       *53,"|",*77,"|    Amount",*93,"|":
                       *116,"|    Amount",*132,"|"
.
HEAD9000  ADD       FIVE,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.**********************************************************************
.*                                PREC0000                             *
.*                   Routine to print a record                         *
.**********************************************************************
PREC0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTHLCLMN
          CALL      RDCODE1
          PACK      DIM15,TDESC
.
          MOVE      SP20,DIM15A
          MOVE      "HT",TCODE
          PACK      KEY5,TCODE,PTHLTABT
          CALL      RDCODE1
          PACK      DIM15A,TDESC
.
          MOVE      PTHLLREB,FORM8P2A
          MOVE      PTHLLPAT,FORM8P2B
.
          PRINT     *1,"| ",PTHLCLMN," -",DIM15:
                    *26,"| ",PTHLHFND:
                    *35,"| ",PTHLTABT:
                    *50,"| ",PTHLCASM:
                    *63,"| ",PTHLDES1:
                    *100,"|",FORM8P2A,*112,"|",FORM8P2B:
                    *124,"|",PTHLCOFF,*132,"|"
          ADD       ONE,CLNO
.
          IF        PDSFLAG = 1
             PRINT     *1,"|",*26,"|",*35,"|":
                       *50,"|",*63,"| ",PTHLDES2:
                       *100,"|",*112,"|":
                       *124,"|",*132,"|"
             ADD       ONE,CLNO
          ENDIF
.
PREC9999   RETURN
+
.**********************************************************************
.*                                PREL0000                             *
.*                   Routine to print a record                         *
.**********************************************************************
PREL0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTLDCLMN
          CALL      RDCODE1
          PACK      DIM15,TDESC
.
          MOVE      SP20,DIM15A
          MOVE      "HT",TCODE
          PACK      KEY5,TCODE,PTLDTABT
          CALL      RDCODE1
          PACK      DIM15A,TDESC
.
          MOVE      PTLDDREB,FORM8P2A
          MOVE      PTLDDPAT,FORM8P2B
.
          PRINT     *1,"| ",PTLDCLMN," -",DIM15:
                    *26,"| ",PTLDHFND:
                    *35,"| ",PTLDTABT:
                    *48,"| ",PTLDCASM:
                    *61,"| ",PTLDEDAY:
                    *69,"| ",PTLDDES1:
                    *107,"|",FORM8P2A,*119,"|",FORM8P2B,*132,"|"
          ADD       ONE,CLNO
.
          IF        PDSFLAG = 1
             PRINT     *1,"|",*26,"|",*35,"|":
                       *48,"|",*61,"|",*69,"| ",PTLDDES2:
                       *107,"|",*119,"|",*132,"|"
             ADD       ONE,CLNO
          ENDIF
.
PREL9999   RETURN
+
.**********************************************************************
.*                                PREH0000                             *
.*                   Routine to print a record                         *
.**********************************************************************
PREH0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTHDCLMN
          CALL      RDCODE1
          PACK      DIM15,TDESC
.
          MOVE      SP20,DIM15A
          MOVE      "HT",TCODE
          PACK      KEY5,TCODE,PTHDTABT
          CALL      RDCODE1
          PACK      DIM15A,TDESC
.
          MOVE      PTHDDREB,FORM8P2A
          MOVE      PTHDDPAT,FORM8P2B
.
          PRINT     *1,"| ",PTHDCLMN," -",DIM15:
                    *26,"| ",PTHDHFND:
                    *34,"| ",PTHDTABT:
                    *46,"| ",PTHDCASM:
                    *58,"|",PTHDEDAY:
                    *64,"| ",PTHDDTYP:
                    *70,"| ",PTHDDES1:
                    *108,"|",FORM8P2A,*120,"|",FORM8P2B,*132,"|"
.
          ADD       ONE,CLNO
.
          IF        PDSFLAG = 1
             PRINT     *1,"|",*26,"|",*34,"|":
                       *46,"|",*58,"|",*64,"|",*70,"| ",PTHDDES2:
                       *108,"|",*120,"|",*132,"|"
             ADD       ONE,CLNO
          ENDIF
.
PREH9999   RETURN
.
.**********************************************************************
.*                                PREA0000                             *
.*                   Routine to print a record                         *
.**********************************************************************
PREA0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTFECLMN
          CALL      RDCODE1
          PACK      DIM15,TDESC
.
          MOVE      SP20,DIM15A
          MOVE      "HT",TCODE
          PACK      KEY5,TCODE,PTFETABT
          CALL      RDCODE1
          PACK      DIM15A,TDESC
.
          MOVE      PTFEDREB,FORM8P2
          PRINT     *1,"| ",PTFECLMN," -",DIM15:
                    *26,"| ",PTFEHFND:
                    *34,"| ",PTFETABT:
                    *47,"| ",PTFECASM:
                    *58,"| ",PTFEBTYP:
                    *63,"|",PTFEEDAY:
                    *70,"| ",PTFEDES1:
                    *108,"|",FORM8P2;
.
          MOVE      PTFEDPAT,FORM8P2
          PRINT     *120,"|",FORM8P2,*132,"|"
          ADD       ONE,CLNO
.
          IF        PDSFLAG = 1
             PRINT     *1,"|",*26,"|",*34,"|":
                       *47,"|",*58,"|",*63,"|",*70,"| ",PTFEDES2:
                       *108,"|",*120,"|",*132,"|"
             ADD       ONE,CLNO
          ENDIF
.
PREA9999   RETURN
+
.***********************************************************************
.*                                PREB0000                             *
.*                   Routine to print a record (Rev. & Inv. Brkdwn)    *
.***********************************************************************
PREB0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.* ****************************************** Start of bulk changes    *C-90302
          MOVE      ZERO,COUNT
          MOVE      ZERO,OVROVB
          MOVE      ZERO,OVRINV
          UNPACK    SP70,DIM20,TEMPAMNT
          UNPACK    SP70,DIM20A,TMPAMNT
.
          PACK      KEY30,FRMCONTR,PTHLCLMN,PTHLHFND,PTHLTABT,PTHLCASM,SP10
          CALL      RSPTIOV1
          CALL      RSPTOVB1
.
PREB1000  BRANCH    OVROVB,PREB1500         * if no more "patsrbaf"
.
          CALL      RKPTOVB1                * read next Revenue B'down
          IF        OVRCD = 1
            MOVE      ONE,OVROVB
            UNPACK    SP30,PTOBCODE,PTOBFUND,PTOBTBLT,PTOBCSCD
          ENDIF
.
          CALL      VALD0000                * make sure code is valid
.
PREB1500  BRANCH    OVRINV,PREB2000         * if no more "pmssinaf"
.
          CALL      RKPTIOV1                * read next Invoice B'down
          IF        OVRCD = 1
            MOVE      ONE,OVRINV
            UNPACK    SP30,PTIOCODE,PTIOFUND,PTIOTBLT,PTIOCSCD
          ENDIF
.
          CALL      VLCD0000           * make sure code is valid
.
PREB2000  IF        OVROVB = 1 & OVRINV = 1
            GOTO      PREB9000              * End of both B'downs
          ENDIF
.* ****************************************** End of bulk changes      *C-90302
.
          ADD       ONE,COUNT
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTOBCODE
          CALL      RDCODE1
          PACK      DIM15,TDESC
.
          IF        COUNT = 1
            PRINT     *1,"| ",PTHLCLMN," -",DIM15:
                      *25,"| ",PTHLHFND:
                      *34,"| ",PTHLTABT:
                      *42,"| ",PTHLCASM:
                      *53,"| ",DIM20:
                      *77,"|",TEMPAMNT:
                      *93,"| ",DIM20A:
                      *116,"|",TMPAMNT:
                      *132,"|"
          ELSE
            PRINT       *1,"|",*25,"|",*34,"|",*42,"|":
                        *53,"| ",DIM20,*77,"|",TEMPAMNT:
                        *93,"| ",DIM20A,*116,"|",TMPAMNT:
                        *132,"|"
          ENDIF
.
          ADD       ONE,CLNO
          GOTO      PREB1000
.
.
.
PREB9000  COMPARE   ZERO,COUNT
          IF        !@EQUAL
            PRINT       *1,"|",*25,"|",*34,"|",*42,"|":
                        *53,"|",*77,"|",*93,"|",*116,"|",*132,"|"
            ADD       ONE,CLNO                                         *I-90302
          ENDIF
.
PREB9999  RETURN
+
.******************************************************************************
.*                             VALD0000                                       *
.*      Check if the Overnight Revenue Breakdown record is valid              *
.******************************************************************************
VALD0000  MOVE      ZERO,EXIT
.
          MATCH     PTOBCNID,FRMCONTR
          GOTO      VALD9500 IF NOT EQUAL
.
          MATCH     PTOBCODE,PTHLCLMN
          GOTO      VALD9500 IF NOT EQUAL
.
          MATCH     PTOBFUND,PTHLHFND
          GOTO      VALD9500 IF NOT EQUAL
.
          MATCH     PTOBTBLT,PTHLTABT
          GOTO      VALD9500 IF NOT EQUAL
.
          MATCH     PTOBCSCD,PTHLCASM
          GOTO      VALD9500 IF NOT EQUAL
.
.
VALD9000  MOVE      SP20,DIM20
          MOVE      "FI",TCODE
          PACK      KEY5,TCODE,PTOBBRCD      * Breakdown Code
          CALL      RDCODE1
          PACK      DIM20,TDESC,SP10
          MOVE      PTOBAMNT,TEMPAMNT
          GOTO      VALD9999
.
VALD9500  MOVE      SP20,DIM20
.....     MOVE      "0.00",TEMPAMNT                                     D-90302
          MOVE      SP15,TEMPAMNT                                      *C-90302
          MOVE      ONE,OVROVB              * end of this Rev. B'down  *I-90302
          MOVE      ONE,EXIT
.
VALD9999  RETURN
.
+
.******************************************************************************
.*                             VLCD0000                                       *
.*      Check if the Overnight Invoice Breakdown record is valid              *
.******************************************************************************
VLCD0000  MOVE      ZERO,EXIT
.
          MATCH     PTIOCNID,FRMCONTR
          GOTO      VLCD9500 IF NOT EQUAL
.
          MATCH     PTIOCODE,PTHLCLMN
          GOTO      VLCD9500 IF NOT EQUAL
.
          MATCH     PTIOFUND,PTHLHFND
          GOTO      VLCD9500 IF NOT EQUAL
.
          MATCH     PTIOTBLT,PTHLTABT
          GOTO      VLCD9500 IF NOT EQUAL
.
          MATCH     PTIOCSCD,PTHLCASM
          GOTO      VLCD9500 IF NOT EQUAL
.
VLCD9000  MOVE      SP20,DIM20A
          MOVE      "FI",TCODE
          PACK      KEY5,TCODE,PTIOBRCD      * Breakdown Code
          CALL      RDCODE1
          PACK      DIM20A,TDESC,SP10
          MOVE      PTIOAMNT,TMPAMNT
          GOTO      VLCD9999
.
VLCD9500  MOVE      SP20,DIM20A
.....     MOVE      "0.00",TMPAMNT                                      D-90302
          MOVE      SP15,TMPAMNT                                       *C-90302
          MOVE      ONE,OVRINV              * end of this Inv. B'down  *I-90302
          MOVE      ONE,EXIT
.
VLCD9999  RETURN
.
+
.**********************************************************************
.*                                I/O                                  *
.**********************************************************************
RSTEMP1   IF         FOPTION = 1 | FOPTION = 5
            MATCH      SP3,STRCLM
            IF         @EQUAL
              PACK       KEY27,FRMCONTR,STRCLM,SP20,SP10
            ELSE
              PACK       KEY27,FRMCONTR,STRCLM,STRHF,SP20
            ENDIF
            CALL       RSPTHLF1             * position read on lumpsum
          ELSE
.
            IF         FOPTION = 2
              MATCH      SP3,STRCLM
              IF         @EQUAL
                PACK       KEY31,FRMCONTR,STRCLM,SP20,SP10
              ELSE
                PACK       KEY31,FRMCONTR,STRCLM,STRHF,SP20
              ENDIF
              CALL       RSPTLDF1           * position read on low outliers
            ELSE
.
              IF         FOPTION = 3
                MATCH      SP3,STRCLM
                IF         @EQUAL
                  PACK       KEY31,FRMCONTR,STRCLM,SP20,SP10
                ELSE
                  PACK       KEY31,FRMCONTR,STRCLM,STRHF,SP20
                ENDIF
                CALL       RSPTHDF1         * position read on high outliers
              ELSE
                IF         FOPTION = 4
                  MATCH      SP3,STRCLM
                  IF         @EQUAL
                    PACK       KEY34,FRMCONTR,STRCLM,SP20,SP10
                  ELSE
                    PACK       KEY34,FRMCONTR,STRCLM,STRHF,SP20
                  ENDIF
                  CALL       RSPTAFE1      * position read on additional chrge
                ENDIF
                CALL       RSPTAFE1         * position read on additional chrge
              ENDIF
            ENDIF
          ENDIF
          RETURN
.
RKTEMP1   IF         FOPTION = 1 | FOPTION = 5
            CALL       RKPTHLF1             * read next record of lumpsum
            MOVE       PTHLCLMN,ACLAIM
            MOVE       PTHLHFND,AFUNDH
            MOVE       PTHLCNID,CONTID
          ELSE
            IF         FOPTION = 2
              CALL       RKPTLDF1           * read next record of low outliers
              MOVE       PTLDCLMN,ACLAIM
              MOVE       PTLDHFND,AFUNDH
              MOVE       PTLDCNID,CONTID
            ELSE
              IF         FOPTION = 3
                CALL       RKPTHDF1         * read next record of high outliers
                MOVE       PTHDCLMN,ACLAIM
                MOVE       PTHDHFND,AFUNDH
                MOVE       PTHDCNID,CONTID
              ELSE
                IF         FOPTION = 4
                  CALL       RKPTAFE1         * read next record of additional
                  MOVE       PTFECLMN,ACLAIM
                  MOVE       PTFEHFND,AFUNDH
                  MOVE       PTFECNID,CONTID
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          RETURN
+
.******************************************************************************
.
          INC       STD001IO
          INC       PATDSCOD
          INC       KEYINCOD
          INC       PATAFEIO/INC
          INC       PATCMXIO/INC
          INC       PATHDFIO/INC
          INC       PATLDFIO/INC  
          INC       PATHLFIO/INC
          INC       PATCODIO/INC
          INC       PATOVBIO/INC
          INC       PATIOVIO/INC


.*******************************************************************************
.* System         : Operating Room System                                      *
.* Program        : IBAOPR22                                                   *
.* Desc           : Session Enquiry                                            *
.*******************************************************************************
.* Date           : 31/01/1990                                                 *
.* Author         : MMO                                                        *
.* Mods.          :                                                            *
.*******************************************************************************
.*        V11.03.02 20/04/2023  Thanh T        TSK 0909393                     *
.*                  Changed EXTR7000 to setup KEY26 with OPSEHOSP              *
.*        V11.03.01 20/03/2023  PJ Le Febour   TSK 0909393                     *
.*                  Amended KEY19 to KEY22 for theatre index changes           *
.*                          PACK     KEY22 to include OPDAHOSP                 *
.*        V10.01.01 03/02/2011  Jill Parkinson CAR 233088                      *
.*                  Added PMSVX1FD                                             *
.*        V5.08.01  28/08/2000  Caleb Sharman                                  *
.*                  Changed Health Fund variables to be 8 chars                *
.*        V5.07.01  25/02/1999  Jill Habasque                                  *
.*                  Added test for active/inactive codes                       *
.*        V5.07.00  27/10/1998  Jim Stathopoulos                               *
.*                  507 Changes                                                *
.*******************************************************************************
.*        V5.04.02  26/06/1997  Howellsy         505                           *
.*                  Use PATDOCKY instead of PATDOCDS                           *
.*        V5.04.01  27/06/1997  Steve Armstrong     SRF 642946                 *
.*                  Increased PREVARRY array size from 50 to 100               *
.*******************************************************************************
.*        V5.03.02  12/03/1996  Whirlpool   Srf 641299                         *
.*                  Fixed printing of sessions for option 3 - by surg/clinic   *
.*        V5.03.01  16/08/1994 Matt         SRF 640592                         *
.*                  Fixed I*C on controlf. Caused by trying to update the date *
.*                  without opening the control file.                          *
.*******************************************************************************
.*                : Paul Duncan (6/3/90)                                       *
.*                  period not mandatory entry - now displays                  *
.*                  records for all time periods if no time                    *
.*                  period entered                                             *
.*                : Paul Duncan (27/3/90)                                      *
.*                  set up default for 'to' date                               *
.*                : Peter Eddey (16/10/90)                                     *
.*                  added fields to static details display                     *
.*                  added minimum % free processing                            *
.*                  added alternative format for the booking details screen    *
.*        V5.01.01  23/10/1990 Justin Coates                                   *
.*                  Fixing of minor bugs, display & print format               *
.*                                                                             *
.*        V5.00     18/12/1990    Justin Coates                                *
.*                  Converted to Version 5                                     *
.*        V5.01.01  21/01/91  ROD                                              *
.*                  Converted to Release 5.01                                  *
.*                  08/02/91  ROD                                              *
.*                  Changed line 24 options to include Select item             *
.*        V5.01.02  22/05/91  Steve O'Gorman                                   *
.*                  Fixed Errors found by the New Compiler                     *
.*        V5.01.03  26/06/1991    Justin Coates  SRF 108932                    *
.*                  Removed use of OPCNBDSF and replaced with OPCNSUMM         *
.*        V5.01.04  25/07/91      ROD       SRF 109233                         *
.*                  Recompiled for OPRSESFD.INC                                *
.*        V5.01.05  24/09/91      J.Tan     SRF 110752                         *
.*                  Fixed displaying clinic description                        *
.*        V5.01.06  10/10/1991    Justin Coates  (User Group Changes)          *
.*                  Add display of cancelled sessions.                         *
.*        V5.01.07  24/12/1991    Justin Coates  (Modbury Changes)             *
.*                  Add display of cancelled bookings if desired (OPCNCBOK)    *
.*        V5.01.08  15/05/92      DIG                             SRF  113183  *
.*                  Fixed problem with Cancelled session variable not being    *
.*                  cleared when printing.                                     *
.*        V5.01.09  06/07/92      DIG                             SRF  123610  *
.*                  Added ability to choose Previous screen when doing enquiry *
.*        V5.01.10  06/12/93      Rob Leonard      SRF 600702                  *
.*                  Fixed printing of times on reports                         *
.*        V5.01.11  24/05/1995  Matt Surridge                                  *
.*                  Fixed bugs for global recompile.                           *
.*        V5.01.12  09/09/1994 Whirlpool     Hawkes Bay changes                *
.*                  Added "By Service" option                                  *
.*                  Chnage session summary display                             *
.*******************************************************************************
.
.$$$$$
.         IBAOPR22.PBL       Session Enquiry
.         ------------       ---------------
.
.$$$$$
          INC       STD001FD
          INC       BOKDIAFD/INC            * booking diagnosis file
          INC       BOKRC1FD/INC
          INC       OPRCLIFD/INC
          INC       OPRCONFD/INC
          INC       OPRDEAFD/INC
          INC       OPROPRFD/INC
          INC       OPRSESFD/INC
          INC       PATCODFD/INC
          INC       PATDHEAD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATPR1FD/INC            * pre-admission file
+
.**********************************************************************
.*                       CONSTANTS                                    *
.**********************************************************************
CODESP    INIT      "SP"
CODEST    INIT      "ST"
CODEOA    INIT      "OA"
CODEOC    INIT      "OC"
.
DATOPT    INIT      "Date"
.HUNDRED   FORM      "100"
SP55      INIT      "                                                       "
.
NORM      INIT      "Normal"
EXTRA     INIT      "Extra Session"
CANC      INIT      "Cancelled"
SERVOPT   INIT      "Service"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
ADMISSNO  DIM       8
ANAESCD   DIM       3
BIRTHDTE  DIM       10
CASENO    DIM       3
CHKSTAT   FORM      1         * status to check for
CHKDISP   DIM       1         * status to display
CLDESC    DIM       22
CLINCODE  DIM       6
CLINDATE  DIM       10
CLINDESC  DIM       20
CODE      DIM       2
COUNT     FORM      3
DCOUNT    DIM       3
DESC      DIM       7
DESC7     DIM       7 
DOCCODE   DIM       6
DIM3      DIM       3
DIM8      DIM       8
DIM18     DIM       18
DIM22     DIM       22
DIM24     DIM       24        * for patients name
DIM26     DIM       26        * for patients name
DIM30     DIM       30
DISPOPTL  DIM       20
DISEDATE  DIM       10
DISSDATE  DIM       10
DOCNAME   DIM       40
ENDDATE   DIM       8
ENDTIME   DIM       5
FORM3     FORM      3
FORM7P2   FORM      7.2
KEYOPT    DIM       2
MINPFREE  FORM      3
OPDESC    DIM       23
OPERDESC  DIM       24
OPRDESC   DIM       14
OPRROOM   DIM       6
OPTNF     FORM      2
PATNAME   DIM       24
PERD      DIM       3
PREVARRY  DIM       22[100]
PREVCNT   FORM      3
PTCNDCQS  FORM      1
RECCOUNT  FORM      2
SAVECDSC  DIM       15        * save value to shorten
SERVICE   DIM       3         * Service (session type) required 
SERVDESC  DIM       20
SESSKEY   DIM       22        * current session
SESS1     DIM       22
SESS2     DIM       22
SESS3     DIM       22
SESS4     DIM       22
SESS5     DIM       22
SESS6     DIM       22
SESS7     DIM       22
SESS8     DIM       22
SESS9     DIM       22
SESS10    DIM       22
SESS11    DIM       22
SESS12    DIM       22
SESS13    DIM       22
SESS14    DIM       22
SESS15    DIM       22
SESS16    DIM       22
STARTDTE  DIM       10
SSTATUS   DIM       13
STDATE    DIM       8
STTIME    DIM       5
SURGCODE  DIM       6
SURGNAME  DIM       40
THETCODE  DIM       6
YESDFLTS  FORM      1
.
PRGID     INIT      "IBAOPR22"
PRGNAM    INIT      "Session Enquiry"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
. ------- main processing -------
.
ML1000    CALL      SCRA0000               * display screen
          CALL      CLRA0000               * clear variables
.
          BRANCH    COPTION,ML5000,ML2000,ML3000,ML4000
.
ML2000    CALL      ROOM0000               * option 2, specific room
          BRANCH    EXIT,ML0100
          GOTO      ML5000
.
ML3000    BRANCH    OPCNCLSU,ML3500        * using docs or clinics ???
          CALL      CLIN0000               * option 3, specific clinic
          BRANCH    EXIT,ML0100
          GOTO      ML5000
.
ML3500    CALL      SURG0000               * option 3, surgeon code (clinic)
          BRANCH    EXIT,ML0100
          GOTO      ML5000
.
ML4000    CALL      SERV0000               * option 4 Service (session type)
          BRANCH    EXIT,ML0100
.
.         common to all options
.
ML5000    CALL      PARM0000               * keyin date range, period
          BRANCH    EXIT,ML7000
.
          CALL      EXTR0000               * extract records accord to range
          BRANCH    EXIT,ML0100,ML1000,ML0100,ML1000 
.
          DISPLAY   *P1:1,*EL
          CALL      HITENTER
          GOTO      ML9999
.
ML7000    BRANCH    COPTION,ML0100,ML1000,ML1000,ML1000
.
ML9999    CHAIN     PGM          
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P50:24,*EL,"Opening ":
                    *P60:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      IBACLOCK
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,FORTY;*81,OPCNODSC,*96,OPCNCDSC,*111,OPCNCLSU
.
.         remove blanks from Clinic/Surgeon desc
.
          MOVE      OPCNCDSC,DESC
          ENDSET    OPCNCDSC                * set FP to logical length
.
INIT1000  CMATCH    SP1,OPCNCDSC
          GOTO      INIT1100 IF NOT EQUAL   * no more blanks
.
          BUMP      OPCNCDSC,-1
          GOTO      INIT1000 IF NOT EOS     * get previous char
.
INIT1100  LENSET    OPCNCDSC                * set logical length to FP
          RESET     OPCNCDSC                * reset FP to start
.
.         remove blanks from operting room/Theatre desc
.
          ENDSET    OPCNODSC                * set FP to logical length
.
INIT2000  CMATCH    SP1,OPCNODSC
          GOTO      INIT2100 IF NOT EQUAL   * no more blanks
.
          BUMP      OPCNODSC,-1
          GOTO      INIT2000 IF NOT EOS     * get previous char
.
INIT2100  LENSET    OPCNODSC                * set logical length to FP
          RESET     OPCNODSC                * reset FP to start
.
          DISPLAY   *P60:24,"oprcliaf";
          OPEN      OPRCLIA1,"oprcliaf"
.
          DISPLAY   *P60:24,"oprcliaf";
          OPEN      OPRCLIA2,"oprcliaf"
.
          DISPLAY   *P60:24,"oprdetaf";
          OPEN      OPRDETA1,"oprdetaf"
.
          DISPLAY   *P60:24,"opropraf";
          OPEN      OPROPRA1,"opropraf"
.
          DISPLAY   *P60:24,"opropraf";
          OPEN      OPROPRA2,"opropraf"
.
          DISPLAY   *P60:24,"oprsesaf";
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
.
          DISPLAY   *P60:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P60:24,"patdo1af";
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P60:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patma1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P60:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P60:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          MOVE      ONE,CNOUNDLN
          MOVE      ONE,CNEWDS
          READ      CONTROLF,FORTY2;*31,OPCNSUMM,*104,OPCNCBOK
.
INIT9999  RETURN
+
.****************************************************************************
.*                               OPTN0000                                   *
.*                       select option                                      *
.****************************************************************************
.
OPTN0000  HLINE     *G37,2,48,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = By Session Date":
                    *P1:6,*V2LON,TWO,*HOFF," = By ",OPCNODSC:
                    *P1:7,*V2LON,THREE,*HOFF," = By ",OPCNCDSC:
                    *P1:8,*V2LON,FOUR,*HOFF," = By Service"
.
          KEYIN     *P1:10,*EL,"Select Option : _",*P17:10,*V2LON,COPTION
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    COPTION,OPTN8000,OPTN8000,OPTN8000,OPTN8000
          BEEP
          GOTO      OPTN0000
.
OPTN8000  LOAD      DISPOPTL WITH COPTION OF DATOPT,OPCNODSC,OPCNCDSC,SERVOPT
          DISPLAY   *P48:2,*V2LON,"- by ",*+,DISPOPTL,SP1
          MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                               SCRA0000                                   *
.*                       display initial screen                             *
.****************************************************************************
SCRA0000  DISPLAY   *P1:3,*EF
          BRANCH    COPTION,SCRA4000,SCRA1000,SCRA2000,SCRA3000
.
SCRA1000  DISPLAY   *P1:4,*+,OPCNODSC," : "
          GOTO      SCRA4000
.
SCRA2000  DISPLAY   *P1:4,*EL,*+,OPCNCDSC," : "
          GOTO      SCRA4000
.
SCRA3000  DISPLAY   *P1:4,*EL,*+,"Service : "
.
SCRA4000  DISPLAY   *P1:5,*EL,"From Date : ",*P25:5,"To Date : ":
                    *P47:5,"Period : ",*P61:5,"Min % Free : "
   
.
SCRA9999  RETURN
+
.****************************************************************************
.*                               CLRA0000                                   *
.*                       clear variables                                    *
.****************************************************************************
CLRA0000  MOVE      SP6,CLINCODE
          MOVE      SP10,STDATE
          MOVE      SP10,ENDDATE
          MOVE      SP3,PERD
          MOVE      SP6,OPRROOM
          MOVE      SP3,SERVICE
          MOVE      SP20,SERVDESC
.
CLRA1000  MOVE      ONE,FORM2
.
CLRA2000  STORE     SP70 USING FORM2 INTO SESS1,SESS2,SESS3,SESS4,SESS5:
                         SESS6,SESS7,SESS8,SESS9,SESS10,SESS11,SESS12:
                         SESS13,SESS14,SESS15,SESS16
          ADD       ONE,FORM2
          COMPARE   TWENTY,FORM2
          GOTO      CLRA2000 IF NOT EQUAL
.
CLRA9999  RETURN
+
.****************************************************************************
.*                               CLRP0000                                   *
.*                       clear the previous screen array                    *
.****************************************************************************
CLRP0000  MOVE      ZERO,PREVCNT
          REPEAT    
            ADD       ONE,PREVCNT
            MOVE      SP70,PREVARRY[PREVCNT]
          UNTIL     PREVCNT = 100
.
CLRP9999  RETURN
+
.******************************************************************************
.                   GURN0000
.    Given the admission number in KEY8/KEY6 this gets the UR number from
.    either the admission or booking master file.
.    Returns : AURNO
.    Routine by Bill Dew 14/03/90
.*******************************************************************************
.
GURN0000  CALL      RDMISS1                 * read admission file
          BRANCH    OVRCD,GURN5555
          GOTO      GURN9000
.
GURN5555  CALL      RDBKREC1                * read booking records file
          BRANCH    OVRCD,GURN9990
          MOVE      BKREURNO,AURNO
.
GURN9000  MOVE      ZERO,EXIT               * a valid admission number
          GOTO      GURN9999
.
GURN9990  MOVE      ONE,EXIT                * an invalid admission number
.
GURN9999  RETURN
+
.****************************************************************************
.*                  ROOM0000                    Called by : ML2000          *
.*        Input operating room                                              *
.****************************************************************************
ROOM0000  
          MOVELPTR  OPCNODSC,CCOL
          ADD       "4",CCOL
          KEYIN     *PCCOL:4,*DV,UNDLN6,*EL:
                    *PCCOL:4,*V2LON,OPRROOM
.
          CALL      VALO0000                  * validate operating room
          BRANCH    EXIT,ROOM8000,ROOM2000,ROOM9000
.                        invalid ,?       ,nothing
          GOTO      ROOM5000
.
ROOM2000  CALL      OPROPRDS                    * "?" on operating rooms
.
ROOM2500  DISPLAY   *P1:24,*EL,*+,OPCNODSC," code : ______"
          MOVELPTR  OPCNODSC,CCOL
          ADD       "9",CCOL
          KEYIN     *PCCOL:24,*V2LON,OPRROOM,*HOFF
.
          CALL      VALO0000                    * validate opr room
          BRANCH    EXIT,ROOM8100,ROOM2000,ROOM9000
.                        invalid ,?       ,nothing
.
          CALL      REDI0000                   * redisplay screen
.
.         valid item entered
.
ROOM5000  MOVELPTR  OPCNODSC,CCOL
          ADD       "4",CCOL
          DISPLAY   *PCCOL:4,*V2LON,OPRROOM,*HOFF,*P30:4,OPRMDESC
          MOVE      ZERO,EXIT
          GOTO      ROOM9999
.
ROOM8000  DISPLAY   *P1:24,*EL,*B,*+,OPCNODSC," code not on file - ";
          CALL      HITENTER
          GOTO      ROOM0000
.
ROOM8100  DISPLAY   *P1:24,*EL,*B,*+,OPCNODSC," code not on file - ";
          CALL      HITENTER
          GOTO      ROOM2500
.
ROOM9000  MOVE      TRUE,EXIT
.
ROOM9999  RETURN
+
.****************************************************************************
.*                               VALO0000                                   *
.*                       validate operating room                            *
.*        Returns : EXIT = 0      valid code entered                        *
.*                  EXIT = 1      invalid code entered                      *
.*                  EXIT = 2      ? entered                                 *
.*                  EXIT = 3      nothing entered                           *
.****************************************************************************
VALO0000  ENDSET    OPRROOM
          APPEND    SP6,OPRROOM
          RESET     OPRROOM
.
          MATCH     SP6,OPRROOM
          GOTO      VALO1000 IF NOT EQUAL
.
          MOVE      THREE,EXIT              * nothing entered
          GOTO      VALO9999
.
VALO1000  MATCH     QUEST,OPRROOM
          GOTO      VALO2000 IF NOT EQUAL
.
          MOVE      TWO,EXIT                * ? entered
          GOTO      VALO9999
.
VALO2000  PACK      KEY6,OPRROOM
          CALL      RDOPOPR1
          BRANCH    OVRCD,VALO3000          * invalid code
.
          COMPARE   ONE,OPRMSTAT            * active/inactive
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*V2LON,"Inactive Code ",*HOFF;
            CALL      HITENTER
            GOTO      ROOM2500
          ENDIF
          GOTO      VALO9500
.
VALO3000  MOVE      ONE,EXIT
          GOTO      VALO9999
.
VALO9500  MOVE      ZERO,EXIT                   * valid room
.
VALO9999  RETURN
+
.****************************************************************************
.*                               CLIN0000                                   *
.*                       input clinic / session                             *
.****************************************************************************
CLIN0000  
          MOVELPTR  OPCNCDSC,CCOL
          ADD       "4",CCOL
          KEYIN     *PCCOL:4,*DV,UNDLN6,*EL:
                    *PCCOL:4,*V2LON,CLINCODE,*HOFF
.
          CALL      VALC0000                  * validate clinic / session
          BRANCH    EXIT,CLIN8000,CLIN2000,CLIN9000
          GOTO      CLIN5000
.
CLIN2000  CALL      OPRCLIDS                    * "?" on clinic / session
.
CLIN2500  DISPLAY   *P1:24,*EL,*+,OPCNCDSC," code : ______"
          MOVELPTR  OPCNCDSC,CCOL
          ADD       "9",CCOL
          KEYIN     *PCCOL:24,*V2LON,CLINCODE,*HOFF
.
          CALL      VALC0000                    * validate clinic / session
          BRANCH    EXIT,CLIN8100,CLIN2000,CLIN9000
.
          CALL      REDI0000                   * redisplay screen
.
.         valid code entered
.
CLIN5000  MOVELPTR  OPCNCDSC,CCOL
          ADD       "4",CCOL
          DISPLAY   *PCCOL:4,*V2LON,CLINCODE,*HOFF,*P30:4,OPCLDESC
          MOVE      ZERO,EXIT
          GOTO      CLIN9999
.
CLIN8000  DISPLAY   *P1:24,*EL,*B,*+,OPCNCDSC," code not on file - ";
          CALL      HITENTER
          GOTO      CLIN0000
.
CLIN8100  DISPLAY   *P1:24,*EL,*B,*+,OPCNCDSC," code not on file - ";
          CALL      HITENTER
          GOTO      CLIN2500
.
CLIN9000  MOVE      TRUE,EXIT
.
CLIN9999  RETURN
+
.****************************************************************************
.*                               VALC0000                                   *
.*                       validate clinic / session                          *
.*        Returns : EXIT = 0      valid code entered                        *
.*                  EXIT = 1      invalid code entered                      *
.*                  EXIT = 2      ? entered                                 *
.*                  EXIT = 3      nothing entered                           *
.****************************************************************************
VALC0000  ENDSET    CLINCODE
          APPEND    SP6,CLINCODE
          RESET     CLINCODE
.
          MATCH     SP6,CLINCODE
          GOTO      VALC1000 IF NOT EQUAL
          MOVE      THREE,EXIT
          GOTO      VALC9999
.
VALC1000  MATCH     QUEST,CLINCODE
          GOTO      VALC2000 IF NOT EQUAL
          MOVE      TWO,EXIT
          GOTO      VALC9999
.
VALC2000  PACK      KEY6,CLINCODE
          CALL      RDOPCLI1
          BRANCH    OVRCD,VALC3000
.
          MATCH     SP6,OPCLDOCT
          GOTO      VALC9500 IF EQUAL       * using clinic codes
          MOVE      OPCLDOCT,KEY6           * using doctor code
.
          CALL      RDDOCT1                 * read doctor file
          BRANCH    OVRCD,VALC3000          * invalid doctors code
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OPCLDESC
          GOTO      VALC9500
.
VALC3000  MOVE      ONE,EXIT
          GOTO      VALC9999
.
VALC9500  MOVE      ZERO,EXIT                   * valid clinic
.
VALC9999  RETURN
+
.****************************************************************************
.*                               SURG0000                                   *
.*                       input surgeon details                              *
.****************************************************************************
SURG0000  MOVELPTR  OPCNCDSC,CCOL
          ADD       "4",CCOL
.
          MOVE      SP10,SURGCODE
          MOVE      CCOL,ECOL
          MOVE      "04",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,SURG9000,SURG9000
.
          MOVE      DCODE,SURGCODE
.
SURG5000  MOVE      SURGCODE,KEY6
.
          CALL      DCNM0000
          MOVE      DOCNAME,SURGNAME
          MOVE      SURGNAME,OPCLDESC
          MOVE      SURGCODE,CLINCODE
.
          MOVELPTR  OPCNCDSC,CCOL
          ADD       "4",CCOL
          DISPLAY   *PCCOL:4,*EL,*V2LON,SURGCODE,*HOFF,*P26:4,OPCLDESC
          MOVE      FALSE,EXIT
          GOTO      SURG9999
.
SURG9000  MOVE      TRUE,EXIT
.
SURG9999  RETURN
+
.****************************************************************************
.*                               VALS0000                                   *
.*                       validate surgeon code                              *
.*        Returns : EXIT = 0      valid code entered                        *
.*                  EXIT = 1      nothing entered                           *
.*                  EXIT = 2      ? entered                                 *
.*                  EXIT = 3      invalid code entered                      *
.****************************************************************************
VALS0000  ENDSET    SURGCODE
          APPEND    SP6,SURGCODE
          RESET     SURGCODE
.
          MATCH     SP6,SURGCODE
          GOTO      VALS9000 IF EQUAL       * nothing entered
.
          MATCH     QUEST,SURGCODE
          GOTO      VALS8000 IF NOT EQUAL
.
          MOVE      TWO,EXIT                * ? entered
          GOTO      VALS9999
.
VALS8000  MOVE      SURGCODE,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,VALS8500
          MOVE      FALSE,EXIT              * valid entry
          GOTO      VALS9999
.
VALS8500  MOVE      THREE,EXIT              * invalid entry
          GOTO      VALS9999
.
VALS9000  MOVE      TRUE,EXIT               * nothing entered
.
VALS9999  RETURN
+
.****************************************************************************
.*                               DCNM0000                                   *
.*                         get doctors name into dim 40                     *
.****************************************************************************
DCNM0000  PACK      DOCNAME,SP30,SP30
          MOVE      SURGCODE,KEY6                    
          CALL      RDDOCT1
          BRANCH    OVRCD,DCNM9999
.
          MOVE      DSNAME,DOCNAME                                      
          ENDSET    DOCNAME
          CALL      DCNM0250                       * get rid of spaces
          LENSET    DOCNAME
          APPEND    ", ",DOCNAME
          APPEND    DGNAME,DOCNAME
          RESET     DOCNAME
          GOTO      DCNM9999
.
DCNM0250  CMATCH    SP1,DOCNAME                    * get rid of spaces
          RETURN    IF NOT EQUAL                   * in doctors desc
          BUMP      DOCNAME BY -1
          RETURN    IF EOS
          GOTO      DCNM0250
.
DCNM9999  RETURN
+
+
.****************************************************************************
.*                               SERV0000                                   *
.*                       Enter the service (Session Type) Cat ST            *
.****************************************************************************
.
SERV0000  MOVE      CODEST,CODE                  * category
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,YESDFLTS
          MOVE      TEN1,ECOL
          MOVE      FOUR,EVERT
          DISPLAY   *PECOL:EVERT,*EL
.
          CALL      PATCODKY                     * keyin a codes field
          BRANCH    EXIT,SERV9999
          COMPARE   TWO,EXIT
          GOTO      SERV9000 IF EQUAL            * exitchar entered
          MOVE      ACODE,SERVICE
          MOVE      TDESC,SERVDESC
          DISPLAY   *P16:4,SERVDESC
          MOVE      ZERO,EXIT
          GOTO      SERV9999
.
SERV9000  MOVE      ONE,EXIT                     * EXITCHAR
.
SERV9999  RETURN
+
.****************************************************************************
.*                               PARM0000                                   *
.*                       enter start and end date, period                   *
.****************************************************************************
PARM0000  CALL      STRT0000                  * start date
          BRANCH    EXIT,PARM9999
.
          CALL      ENDD0000                  * end date
          BRANCH    EXIT,PARM0000
.
          CALL      PRDT0000                  * time period
          CALL      MIPF0000                  * minimum % free
.
PARM9999  RETURN
+
.****************************************************************************
.*                               STRT0000                                   *
.*                       enter start date                                   *
.****************************************************************************
STRT0000  MOVE      TEN3,CCOL
          MOVE      FIVE,CVERT
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      STRT9000 IF EQUAL
          BRANCH    OVRCD,STRT9000
          PACK      STDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STDATE
          CALL      PACDATE
          MOVE      CPCDATE,DISSDATE
          REP       " 0",DISSDATE
          MOVE      DISSDATE,STARTDTE
          MOVE      FALSE,EXIT
          GOTO      STRT9999
.
STRT9000  MOVE      TRUE,EXIT
          GOTO      STRT9999
.
STRT9999  RETURN
+
.****************************************************************************
.*                               ENDD0000                                   *
.*                       enter end date                                     *
.****************************************************************************
ENDD0000  MOVE      THIRTY5,CCOL
          MOVE      FIVE,CVERT
          MOVE      CCC,CCENT
          UNPACK    STDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      ENDD5000 IF NOT EQUAL
          MOVE      TRUE,EXIT
          GOTO      ENDD9999
.
ENDD5000  PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE
          MOVE      CPCDATE,DISEDATE
          REP       " 0",DISEDATE
          MATCH     STDATE,ENDDATE
          GOTO      ENDD9200 IF LESS
          GOTO      ENDD9999
.
ENDD9000  DISPLAY   *P1:24,*EL,*B,"Invalid date - ";
          CALL      HITENTER
          GOTO      ENDD0000
.
ENDD9200  DISPLAY   *P1:24,*EL,*B,"TO date cannot be before FROM date. ";
          CALL      HITENTER
          GOTO      ENDD0000
.
ENDD9999  RETURN
+
.****************************************************************************
.*                               PRDT0000                                   *
.*                       enter the period of the session                    *
.****************************************************************************
PRDT0000  DISPLAY   *P56:5,UNDLN3
          KEYIN     *P56:5,*V2LON,PERD,*HOFF
.
          CALL      VALP0000                       * validate period
          BRANCH    EXIT,PRDT8000,PRDT2000,PRDT9550
.                        Invalid  ?        Nothing
          GOTO      PRDT5000
.
PRDT2000  MOVE      CODESP,CODE
          CALL      PATCODDS                    * "?" on period           
.
PRDT2500  DISPLAY   *P1:24,*EL,"Period code : ___"
          KEYIN     *P15:24,*V2LON,PERD,*HOFF
.
          CALL      VALP0000                    * validate period           
          BRANCH    EXIT,PRDT8100,PRDT2000,PRDT9500
.                        Invalid  ?        Nothing
.
          CALL      REDI0000                   * redisplay screen
.
PRDT5000  DISPLAY   *P56:5,*V2LON,PERD
          GOTO      PRDT9999
.
PRDT8000  DISPLAY   *P1:24,*EL,*B,"Period code not on file - ";
          CALL      HITENTER
          GOTO      PRDT0000
.
PRDT8100  DISPLAY   *P1:24,*EL,*B,"Period code not on file - ";
          CALL      HITENTER
          GOTO      PRDT2500
.
.         nothing entered, so display All
.
PRDT9500  CALL      REDI0000
.
PRDT9550  DISPLAY   *P56:5,*V2LON,"All"
.
PRDT9999  RETURN
+
.****************************************************************************
.*                               VALP0000                                   *
.*                       validate period                                    *
.*        Returns : EXIT = 0      valid code entered                        *
.*                  EXIT = 1      invalid code entered                      *
.*                  EXIT = 2      ? entered                                 *
.*                  EXIT = 3      nothing entered                           *
.****************************************************************************
VALP0000  ENDSET    PERD
          APPEND    SP3,PERD
          RESET     PERD
.
          MATCH     SP6,PERD
          GOTO      VALP1000 IF NOT EQUAL
          MOVE      THREE,EXIT              * nothing entered
          GOTO      VALP9999
.
VALP1000  MATCH     QUEST,PERD
          GOTO      VALP2000 IF NOT EQUAL
          MOVE      TWO,EXIT                * ? entered
          GOTO      VALP9999
.
VALP2000  PACK      KEY5,CODESP,PERD
          CALL      RDCODE1 
          BRANCH    OVRCD,VALP3000          * invalid code
          GOTO      VALP9500
.
VALP3000  MOVE      ONE,EXIT                * invalid code
          GOTO      VALP9999
.
VALP9500  MOVE      ZERO,EXIT               * valid period
.
VALP9999  RETURN
+
.****************************************************************************
.                             MIPF0000                                      *
.                Keyin the minimum percent free                             *
.****************************************************************************
MIPF0000  MOVE      ZERO,MINPFREE
.
          KEYIN     *P74:5,*DV,UNDLN3:
                    *P74:5,*V2LON,MINPFREE:
                    *P74:5,*DV,MINPFREE
.
          COMPARE   ZERO,MINPFREE
          GOTO      MIPF9000 IF LESS
. 
          COMPARE   MINPFREE,HUNDRED
          GOTO      MIPF9000 IF LESS
.
          GOTO      MIPF9999
.
MIPF9000  DISPLAY   *P1:24,*EL,*B,"Must be a Number from 0 to 100. ";
          CALL      HITENTER
          GOTO      MIPF0000
.
MIPF9999  RETURN
+
.*********************************************************************
.*                  VMPF0000                                         *
.*        Validate minimum percentage free                           *
.*        Returns : EXIT = 1      invalid percentage                 *
.*********************************************************************
.
VMPF0000  COMPARE   ZERO,MINPFREE
          GOTO      VMPF8000 IF EQUAL       * all sessions are valid
.
          MOVE      OPSETIMB,FORM7P2        * time actually booked 
          MOVE      OPSEBRKT,FORM3          * break time
          MOVE      OPSEDURA,FORM4          * duration
.
          ADD       FORM3,FORM7P2           * TAB + break time -> time used
          DIV       FORM4,FORM7P2           * fraction of duration used
          MULT      HUNDRED,FORM7P2         * % of duration actually used
.
          COMPARE   "101",FORM7P2
          GOTO      VMPF9000 IF NOT LESS    * invalid %-age
.
          MOVE      "100",FORM3             * set up %-age free
          SUB       FORM7P2,FORM3           * the %-age free
.
          COMPARE   MINPFREE,FORM3
          GOTO      VMPF9000 IF LESS        * % used is less than that entered
.
VMPF8000  MOVE      FALSE,EXIT
          GOTO      VMPF9999
.
VMPF9000  MOVE      ONE,EXIT
.
VMPF9999  RETURN
+
.****************************************************************************
.*                               REDI0000                                   *
.*                       redisplay screen after "?" routine                 *
.****************************************************************************
REDI0000  CALL      SCRA0000                    * display screen
.
          BRANCH    COPTION,REDI3000,REDI1000,REDI2000,REDI2500
.
REDI1000  MOVELPTR  OPCNODSC,CCOL
          ADD       "4",CCOL
          DISPLAY   *PCCOL:4,*V2LON,OPRROOM,*HOFF,*P30:4,OPRMDESC
          GOTO      REDI3000
.
REDI2000  MOVELPTR  OPCNODSC,CCOL
          ADD       "4",CCOL
          DISPLAY   *PCCOL:4,*V2LON,CLINCODE,*HOFF,*P30:4,OPCLDESC
          GOTO      REDI3000
.
REDI2500  DISPLAY   *P11:4,*V2LON,SERVICE,*HOFF,*P16:4,SERVDESC
.
REDI3000  MATCH     SP8,STDATE
          GOTO      REDI9999 IF EQUAL
          DISPLAY   *P13:5,*V2LON,DISSDATE,*HOFF:
                    *P35:5,*V2LON,DISEDATE,*HOFF
.
REDI4000  MATCH     SP3,PERD
          GOTO      REDI5000 IF EQUAL
          DISPLAY   *P56:5,*V2LON,PERD,*HOFF
REDI5000 
          DISPLAY   *P74:5,*V2LON,MINPFREE
.
REDI9999  RETURN
+
.*********************************************************************
.*                  EXTR0000                    Called by : ML4000   *
.*        Display all the sessions satisfying the entered values     *
.*********************************************************************
EXTR0000  MOVE      EIGHT,CVERT
          MOVE      ZERO,FORM2
          CALL      SCRH0000                * display heading
          CALL      CLRP0000                * clear previous keys array
          MOVE      ONE,PREVCNT
          PACK      KEY22,STDATE,SP20
          CALL      RSOPSES7                * positional read at starting date
.
EXTR0100  CALL      RKOPSES7                * next read on session file
          BRANCH    OVRCD,EXTR8000          * no more data
.
          MATCH     OPSEDATE,ENDDATE
          GOTO      EXTR8000 IF LESS        * session date is after end date
.
.------ entry point for previous screen option ------
.
EXTR0200  CALL      VMPF0000 
          BRANCH    EXIT,EXTR0100           * not a valid percentage free
.
          MATCH     PERD,SP3
          GOTO      EXTR0500 IF EQUAL       * using all time periods
.
          MATCH     PERD,OPSETPER
          GOTO      EXTR0100 IF NOT EQUAL   * not the correct time period
.
EXTR0500  BRANCH    COPTION,EXTR3000,EXTR1000,EXTR2000,EXTR2500
.
.         Option 2 : Check on operating room
.
EXTR1000  MATCH     OPRROOM,OPSETHET
          GOTO      EXTR0100 IF NOT EQUAL   * not a valid operating room
          GOTO      EXTR3000
.
.         Option 3 : Check on clinic/doctor
.
EXTR2000  MATCH     CLINCODE,OPSECLIN
          GOTO      EXTR0100 IF NOT EQUAL   * not a valid clinic
          GOTO      EXTR3000
.
.         OPTION 4 : Check on service (Session Type)
.
EXTR2500  MATCH     SERVICE,OPSETYPE
          GOTO      EXTR0100 IF NOT EQUAL
.
.         have a valid item so go about displaying it
.
EXTR3000  COMPARE   TWENTY3,CVERT
          GOTO      EXTR3800 IF LESS        * will fit on current page
.
          CALL      SELA0000                    * next screen ? 
          BRANCH    EXIT,EXTR9999,EXTR9999,EXTR3050,EXTR9500,EXTR3500
.                        Exit     Next Dte Search   Next session  Previous
.
EXTR3050  MOVE      SESS1,PREVARRY[PREVCNT]
          CALL      CLRA1000                * clear storage variables
          MOVE      ZERO,FORM2              * clear counter
          MOVE      EIGHT,CVERT             * set line number
          DISPLAY   *P1:CVERT,*EF           * clear the screen
          ADD       ONE,PREVCNT
.
          GOTO      EXTR3800
.
.------ previous screen chosen ------
.
EXTR3500  CALL      CLRA1000                * clear storage variables
          MOVE      ZERO,FORM2              * clear counter
          MOVE      EIGHT,CVERT             * set line number
          DISPLAY   *P1:CVERT,*EF           * clear the screen
          MOVE      PREVARRY[PREVCNT],KEY22
          CALL      RDOPSES1                * read the session file
.
          GOTO      EXTR0200
.
.         display the item
.
EXTR3800  MOVE      OPSECLIN,KEY6           * set up key
          BRANCH    OPCNCLSU,EXTR4000       * using doctor code
.
          CALL      RDOPCLI1
          BRANCH    OVRCD,EXTR0100          * invalid clinic code
.
          MOVE      OPCLDESC,CLDESC
.
          MATCH     SP6,OPCLDOCT
          GOTO      EXTR5000 IF EQUAL       * using clinic codes
.
          MOVE      OPCLDOCT,KEY6           * use doctor code
.
.         read doctors file
.
EXTR4000  CALL      RDDOCT1
          BRANCH    OVRCD,EXTR0100          * invalid doctors code
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CLDESC
.
.         obtain theatre description or cancellation reason
.
EXTR5000  PACK      DIM8,OPSETHET,SP8
          COMPARE   ZERO,OPSESTAT
          GOTO      EXTR5500 IF NOT EQUAL   * not cancelled
.
.         Set field for display
.
          MOVE      "Cancel'd",DIM8    
.
.
EXTR5500  UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISSDATE
          REP       " 0",DISSDATE
.
          ADD       ONE,FORM2               * increment counter
.
          MATCH     SP5,OPSEACTS
          IF        @EQUAL
            MOVE      OPSETIME,STTIME
            MOVE      OPSEENDT,ENDTIME
          ELSE
            MOVE      OPSEACTS,STTIME
            MOVE      OPSEACTE,ENDTIME
          ENDIF
.
          PACK      KEY26,OPSEHOSP,OPSEDATE,OPSETIME,OPSECLIN,ZERO,Z70
          CALL      RSOPDEA1
          CALL      RPOPDEA1          * read last record to get no. of cases
          PACK      KEY23,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,ZERO
          MATCH     KEY23,KEY23       * test if same session
          GOTO      EXTR6000 IF EQUAL  * if it is keep value else set 0
          MOVE      "  0",OPDACASE      * set no. of cases to zero
EXTR6000  
          MOVE      OPSEDURA,FORM4      * calculate time left
          SUB       OPSETIMB,FORM4      * duration - time booked
          SUB       OPSEBRKT,FORM4      * duration - time for breaks
.
          BRANCH    OPSESUSP,EXTR6200   * check if session is suspended
          MOVE      "No ",KEY3
          GOTO      EXTR6500
.
EXTR6200  MOVE      "Yes",KEY3
.
.         display using actual start/end times
.
EXTR6500  DISPLAY   *P1:CVERT,*V2LON,FORM2,*HOFF,". ",*P5:CVERT,DISSDATE:
                    *P17:CVERT,CLDESC,*P40:CVERT,STTIME,*P46:CVERT,ENDTIME:
                    *P52:CVERT,OPDACASE,*P60:CVERT,FORM4,*P69:CVERT,KEY3:
                    *P73:CVERT,DIM8     
.
.         store the key for the displayed data
.
EXTR7000  PACK      SESSKEY,OPSEHOSP,OPSEDATE,OPSETIME,OPSECLIN,SP70
          STORE     SESSKEY USING FORM2 INTO SESS1,SESS2,SESS3,SESS4,SESS5:
                            SESS6,SESS7,SESS8,SESS9,SESS10,SESS11,SESS12:
                            SESS13,SESS14,SESS15,SESS16
.
          ADD       ONE,CVERT               * inc line counter
          GOTO      EXTR0100                * get next item
.
.         have reached the end of the range
.
EXTR8000  COMPARE   ZERO,FORM2
          GOTO      EXTR9000 IF NOT EQUAL   * have data displayed
.
          DISPLAY   *P1:24,*EL,*B,"No records on file - ";
          CALL      HITENTER
          MOVE      FOUR,EXIT
          GOTO      EXTR9999
.
.         prompt when have reached the end of the range -> no Search option
.
EXTR9000  CALL      SELA0000
          BRANCH    EXIT,EXTR9999,EXTR9999,EXTR9050,EXTR9500,EXTR9300
.                        Exit     Next     Search   Next     Previous
.
EXTR9050  BEEP
          DISPLAY   *P71:24,*EL
          GOTO      EXTR9000                * invalid selection
.
.------ previous screen chosen ------
.
EXTR9300  CALL      CLRA1000                * clear storage variables
          MOVE      ZERO,FORM2              * clear counter
          MOVE      EIGHT,CVERT             * set line number
          DISPLAY   *P1:CVERT,*EF           * clear the screen
          MOVE      PREVARRY[PREVCNT],KEY22
          CALL      RDOPSES1                * read the session file
.
          GOTO      EXTR0200
.
.         Next selected so redisplay screen and select new session
.
EXTR9500  MOVE      STARTDTE,DISSDATE       * redisplay selected
          CALL      REDI0000                * redisplay entered items
          GOTO      EXTR0000                * display the sessions
.
EXTR9999  RETURN
+
.*********************************************************************
.*                  SCRH0000                    Called by : EXTR0000 *
.*        Display screen heading                                     *
.*********************************************************************
SCRH0000  PACK      DESC7,OPCNODSC,SP10
          DISPLAY   *P5:7,*V2LON,*ULON,"Date      ":
                    *P17:7,OPCNCDSC,SP7:     
                    *P40:7,"Start":
                    *P46:7,"End  ":
                    *P53:7,"Pats":
                    *P58:7,"Time Left":
                    *P68:7,"Susp":
                    *P73:7,DESC7 
.
SCRH9999  RETURN
+
.*********************************************************************
.*                  SELA0000                    Called by : EXTR3000 *
.*        select exit,print,continue,next                   EXTR9000 *
.*        Returns : EXIT = 1      exit selected                      *
.*                  EXIT = 2      next selected (date ranges)        *
.*                  EXIT = 3      search more selected               *
.*                  EXIT = 4      next selected (session)            *
.*                  EXIT = 5      previous selected (date ranges)    *
.*********************************************************************
SELA0000  DISPLAY   *P1:24,"Select session, (",*V2LON:
                    ANSP,*HOFF,")rint sessions, (",*V2LON,ANSN,*HOFF:
                    ")ext, P(",*V2LON,ANSR,*HOFF,")evious, (":
                    *V2LON,ANSM,*HOFF,")ore, (",*V2LON,ANSE,*HOFF,")xit ? ";
.
          KEYIN     *V2LON,*JR,KEYOPT,*HOFF;
.
          ENDSET    KEYOPT
          APPEND    SP2,KEYOPT
          RESET     KEYOPT
.
          REP       UPPLOW,KEYOPT                 * allow for lower case
.
          TYPE      KEYOPT                        * was a numeric entered ?
          GOTO      SELA0500 IF EQUAL
.
. a character was entered
.
          REP       "E1P2N3M4R5",KEYOPT  
.
          TYPE      KEYOPT                        * valid letter entered ?
          GOTO      SELA0300 IF NOT EQUAL         * error in input
.
          MOVE      KEYOPT,OPTNF                  * move to form field
          BRANCH    OPTNF,SELA1000,SELA2000,SELA4000,SELA5000,SELA7000
.                         Exit     Print    Next     Search   Previous
SELA0300  BEEP
          GOTO      SELA0000 
.
. a numeric was entered so validate
.
SELA0500  MOVE      KEYOPT,COUNT                  * move to form field
          COMPARE   FORM2,COUNT                   * valid selection?
          GOTO      SELA0700 IF LESS
          GOTO      SELA0700 IF EQUAL
          BEEP
          GOTO      SELA0000
.
SELA0700  CALL      CONT0000
          BRANCH    EXIT,SELA9999,SELA6000,SELA0000
.                        Exit     Next
          GOTO      SELA0000
.
SELA1000  MOVE      TRUE,EXIT
          GOTO      SELA9999
.
SELA2000  CALL      PRNT0000              * print details
          GOTO      SELA0000
.
SELA4000  MOVE      TWO,EXIT              * next session enquiry (date range)
          GOTO      SELA9999
.
SELA5000  MOVE      THREE,EXIT            * search more
          MOVE      EIGHT,CVERT
          GOTO      SELA9999
.
SELA6000  MOVE      FOUR,EXIT             * next selected (session)
          GOTO      SELA9999
.
SELA7000  SUB       ONE,PREVCNT
.
          COMPARE   PREVCNT,ZERO          * see if previous screen is valid
          IF        @EQUAL
            ADD       ONE,PREVCNT
            GOTO      SELA0300
          ENDIF
.
          MOVE      FIVE,EXIT             * previous sess. enquiry (date range)
.
SELA9999  RETURN
+
.*********************************************************************
.*                  PRNH0000                    Called by : PRNT3000 *
.*        Print table heading                               PRNT9100 *
.*********************************************************************
PRNH0000  PACK      DESC7,OPCNODSC,SP10
          PRINT     *N,*5,"Date      ":
                    *17,OPCNCDSC,SP7:     
                    *40,"Start":
                    *46,"End  ":
                    *53,"Pats":
                    *58,"Time Left":
                    *68,"Susp":
                    *73,DESC7 
.
          PRINT     *5,"----------":
                    *17,"----------------------":
                    *40,"-----":
                    *46,"-----":
                    *53,"----":
                    *58,"---------":
                    *68,"----":
                    *73,"-------"
+
          ADD       THREE,CLNO
.
PRNH9999  RETURN
+
.*********************************************************************
.*                  PRNT0000                    Called by : SELA2000 *
.*        Print Details of sessions within a date range              *
.*********************************************************************
PRNT0000  MOVE      ONE,FORM2               * set item counter
          CALL      IBACLOCK                * get current date and time
          MOVE      SIXTY,CLNO              * set line number
          MOVE      " - Session Details",CPHDROPT
          PACK      KEY22,STDATE,SP20 
          CALL      RSOPSES7                * position at starting date entered
.
PRNT0100  CALL      RKOPSES7                * next read on session file
          BRANCH    OVRCD,PRNT9500
.
          MATCH     OPSEDATE,ENDDATE
          GOTO      PRNT9500 IF LESS        * session date is after end date
.
          CALL      VMPF0000
          BRANCH    EXIT,PRNT0100           * invalid percent free
.
          MATCH     SP3,PERD
          GOTO      PRNT0500 IF EQUAL       * using all periods
.
          MATCH     PERD,OPSETPER
          GOTO      PRNT0100 IF NOT EQUAL   * incorrect period
.
PRNT0500  BRANCH    COPTION,PRNT3000,PRNT1000,PRNT2000,PRNT2500
.
.         Option 2 : check on operating room
.
PRNT1000  MATCH     OPRROOM,OPSETHET
          GOTO      PRNT0100 IF NOT EQUAL   * not the correct operating room
          GOTO      PRNT3000
.
.         Option 3 : check on clinic/doctor
.
PRNT2000  MATCH     CLINCODE,OPSECLIN
          GOTO      PRNT0100 IF NOT EQUAL   * not the correct clinic
          GOTO      PRNT3000
.
.         Option 4 : check on service (SESSION TYPE)
.
PRNT2500  MATCH     SERVICE,OPSETYPE 
          GOTO      PRNT0100 IF NOT EQUAL   * not the correct clinic
.
.         have a valid item so go about displaying it
.
PRNT3000  COMPARE   SIXTY,CLNO
          GOTO      PRNT4000 IF NOT EQUAL   * will fit on current page
.
          CALL      HEAD80                  * print new page heading
          CALL      PRNH0000                * print new table heading
.
.         obtain the clinic/doctor description
.
PRNT4000  MOVE      OPSECLIN,KEY6           * set up clinic/doctor
.
          BRANCH    OPCNCLSU,PRNT4100       * using doctor file
.
          CALL      RDOPCLI1
          BRANCH    OVRCD,PRNT0100          * invalid clinic code
.
          MOVE      OPCLDESC,CLDESC         * set up description
          MATCH     SP6,OPCLDOCT
          GOTO      PRNT4200 IF EQUAL
.
          MOVE      OPCLDOCT,KEY6           * Use doctor code
.
PRNT4100  CALL      RDDOCT1
          BRANCH    OVRCD,PRNT0100          * invalid doctor code
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CLDESC         * set up doctors name
.
.         obtain the operating room description or cancellation reason
.
PRNT4200  PACK      DIM8,OPSETHET,SP8
          COMPARE   ZERO,OPSESTAT
          GOTO      PRNT4300 IF NOT EQUAL   * not cancelled
.  
          MOVE      "Cancel'd",DIM8    
.
.         obtain the session date
.
PRNT4300  UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISSDATE
          REP       " 0",DISSDATE
.
          MATCH     SP5,OPSEACTS
          IF        @EQUAL
            MOVE      OPSETIME,STTIME
            MOVE      OPSEENDT,ENDTIME
          ELSE
            MOVE      OPSEACTS,STTIME
            MOVE      OPSEACTE,ENDTIME
          ENDIF
.
          PACK      KEY26,OPSEHOSP,OPSEDATE,OPSETIME,OPSECLIN,ZERO,Z70
          CALL      RSOPDEA1
          CALL      RPOPDEA1          * read last record to get no. of cases
          PACK      KEY20,OPDADATE,OPDATIME,OPDACLIN,ZERO
          MATCH     KEY20,KEY23       * test if same session
          GOTO      PRNT5000 IF EQUAL  * if it is keep value else set 0
          MOVE      "  0",OPDACASE      * set no. of cases to zero
PRNT5000  
          MOVE      OPSEDURA,FORM4      * calculate time left
          SUB       OPSETIMB,FORM4      * duration - time booked
          SUB       OPSEBRKT,FORM4      * duration - time for breaks
.
          BRANCH    OPSESUSP,PRNT6000   * check if session is suspended
          MOVE      "No ",KEY3
          GOTO      PRNT6500
.
PRNT6000  MOVE      "Yes",KEY3
.
PRNT6500  PRINT     *1,FORM2,". ",*5,DISSDATE:
                    *17,CLDESC,*40,STTIME,*46,ENDTIME:
                    *52,OPDACASE,*60,FORM4,*69,KEY3:
                    *73,DIM8    
.
PRNT7600  ADD       ONE,FORM2               * increment item counter
          COMPARE   "55",CLNO
          GOTO      PRNT9100 IF NOT LESS    * need a new page
          ADD       ONE,CLNO                * increment line number
          GOTO      PRNT0100                * get next item
.
.         need a new page
.
PRNT9100  CALL      HEAD80                  * print page heading
          CALL      PRNH0000                * print table heading
          GOTO      PRNT0100                * get next item
.
.         end of report
.
PRNT9500  PRINT     *N,*N,"*** End of Report ***"
          MOVE      FALSE,EXIT
          DISPLAY   *P1:24,*EL,"Printing has been completed."
          COMPARE   ONE,FORM2
          GOTO      PRNT9999 IF NOT EQUAL   * have displayed items
.
          DISPLAY   *P1:24,*EL,*B,"No records on file - ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
.
PRNT9999  RETURN
+
.*********************************************************************
.*                  CONT0000                    Called by : SELA3000 *
.*        Continue enquiry selected so select a displayed session    *
.*        Returns : EXIT = 1      exit selected                      * 
.*                  EXIT = 2      next selected                      * 
.*********************************************************************
CONT0000  MOVE      SP70,SESSKEY            * clear the key
          LOAD      SESSKEY FROM COUNT FROM SESS1,SESS2,SESS3,SESS4,SESS5:
                    SESS6,SESS7,SESS8,SESS9,SESS10,SESS11,SESS12,SESS13:
                    SESS14,SESS15,SESS16
.
          MATCH     SP70,SESSKEY
          GOTO      CONT1000 IF NOT EQUAL
.
          BEEP
          GOTO      CONT9200
.
CONT1000  CALL      DIST0000                   * display static details
          BRANCH    EXIT,CONT0000              * invalid session
.
          CALL      SELB0000                   * select next details
          BRANCH    EXIT,CONT9000,CONT9100
.                        Exit     Next
          MOVE      FALSE,EXIT
          GOTO      CONT9999
.
CONT8000  DISPLAY   *P1:24,*EL,*B,"Selection not found - ";
          CALL      HITENTER
          GOTO      CONT0000
.
CONT9000  MOVE      TRUE,EXIT               * exit chosen
          GOTO      CONT9999
.
CONT9100  MOVE      TWO,EXIT                * Next chosen
          GOTO      CONT9999
.
CONT9200  MOVE      THREE,EXIT              * invalid selection
.
CONT9999  RETURN
+
.*********************************************************************
.*                  DIST0000                    Called by : CONT1000 *
.*        Display static details                            SELB1000 *
.*        Returns : EXIT = 1      invalid session                    * 
.*********************************************************************
DIST0000  MOVE      SESSKEY,KEY22                                 
          CALL      RDOPSES1
          BRANCH    OVRCD,DIST8000
.
          CALL      STAS0000                     * display screen
          MOVE      FALSE,EXIT                   * set exit status
.
          MOVE      OPSECLIN,SURGCODE
          BRANCH    OPCNCLSU,DIST0500
          MOVE      OPSECLIN,KEY6
          CALL      RDOPCLI1
          BRANCH    OVRCD,DIST0800
          MATCH     SP6,OPCLDOCT
          GOTO      DIST0700 IF EQUAL
          MOVE      OPCLDOCT,SURGCODE
.
DIST0500  CALL      DCNM0000                    * get doc and format name
          MOVE      DOCNAME,OPCLDESC
.
DIST0700  DISPLAY   *P23:4,*V2LON,OPSECLIN,*HOFF,*P37:4,OPCLDESC
.
DIST0800  MATCH     SP6,OPSEANAE
          GOTO      DIST1000 IF EQUAL
.
          MOVE      OPSEANAE,SURGCODE
          CALL      DCNM0000
          DISPLAY   *P23:5,*V2LON,OPSEANAE,*HOFF,*P37:5,DOCNAME  
.
DIST1000  MATCH     SP6,OPSETHET
          GOTO      DIST1500 IF EQUAL
          MOVE      OPSETHET,KEY6
          CALL      RDOPOPR1
          BRANCH    OVRCD,DIST1400
          DISPLAY   *P23:6,*V2LON,OPSETHET,*HOFF,*P37:6,OPRMDESC
.
DIST1400  COMPARE   ONE,OPSESUSP
          GOTO      DIST1500 IF NOT EQUAL
          DISPLAY   *P23:7,*EL,*V2LON,*BLINKON,"*** SUSPENDED ***"
.
DIST1500  UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISSDATE
          REP       " 0",DISSDATE
          DISPLAY   *P23:8,*V2LON,DISSDATE,*HOFF
.
          COMPARE   ZERO,OPSESTAT
          GOTO      DIST2000 IF NOT EQUAL
          MOVE      CANC,SSTATUS
.
DIST2000  LOAD      SSTATUS USING OPSESTAT FROM NORM,EXTRA
          DISPLAY   *P37:8,*V2LON,SSTATUS,*HOFF
.
.         check if actual start times
.
          PACK      OPSEACTS,OPSEACTS,SP5
          MATCH     SP5,OPSEACTS
          GOTO      DIST2500 IF NOT EQUAL
.
.         display expected times and duration
.
          DISPLAY   *P23:9,*V2LON,OPSETIME,*HOFF:
                    *P24:10,*V2LON,OPSEDURA,*HOFF:
                    *P48:10,*V2LON,OPSEENDT,*HOFF
          GOTO      DIST4000
.
.         display actual times and duration
.
DIST2500  DISPLAY   *P23:9,*V2LON,OPSEACTS,*HOFF:
                    *P24:10,*V2LON,OPSEACTD,*HOFF:
                    *P48:10,*V2LON,OPSEACTE,*HOFF
.
DIST4000  DISPLAY   *P26:12,*V2LON,OPSEPREP,*HOFF,*P29:12,"mins":
                    *P25:13,*V2LON,OPSEBRKT,*HOFF,*P29:13,"mins"
.
DIST5000  PACK      KEY5,CODESP,OPSETPER
          CALL      RDCODE1
          BRANCH    OVRCD,DIST6000
.
          DISPLAY   *P23:15,*V2LON,OPSETPER,*HOFF,*P37:15,TDESC
.
DIST6000  PACK      KEY5,CODEST,OPSETYPE
          CALL      RDCODE1
          BRANCH    OVRCD,DIST7000
.
          DISPLAY   *P23:16,*V2LON,OPSETYPE,*HOFF,*P37:16,TDESC
.
DIST7000  COMPARE   ZERO,OPSESTAT
          GOTO      DIST9999 IF NOT EQUAL
.
          PACK      KEY5,CODEOC,OPSECANC
          CALL      RDCODE1
          BRANCH    OVRCD,DIST9999
.
          DISPLAY   *P23:17,*V2LON,OPSECANC,*HOFF,*P37:17,TDESC
          GOTO      DIST9999
.
DIST8000  DISPLAY   *P1:24,*EL,*B,"Session not on file - ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
.
DIST9999  RETURN
+
.****************************************************************************
.*                               STAS0000                                   *
.*                       display static screen                              *
.****************************************************************************
STAS0000  DISPLAY   *P1:3,*EF,*P5:4,OPCNCDSC,*P21:4,":":
                    *P66:2,*V2LON," Static Details ",*HOFF:
                    *P5:5,"Anaesthetist",*P21:5,":":
                    *P5:6,OPCNODSC,*P21:6,":":
                    *P5:8,"Date",*P21:8,":":
                    *P5:9,"Start Time",*P21:9,":":
                    *P5:10,"Duration (Mins)",*P21:10,":":
                    *P37:10,"End Time : ":
                    *P5:12,"Preparation time",*P21:12,":":
                    *P5:13,"Time for breaks",*P21:13,":":
                    *P5:15,"Time Period",*P21:15,":":
                    *P5:16,"Session Type",*P21:16,":"
.
          PACK      OPSECANC,OPSECANC,SP3
          MATCH     SP3,OPSECANC
          GOTO      STAS9999 IF EQUAL
          DISPLAY   *P5:17,"Cancellation",*P21:17,":"
.
STAS9999  RETURN
+
.****************************************************************************
.*                  BOOK0000                          Called by : SELB2000  *
.*        display booking details                                           *
.****************************************************************************
BOOK0000  CALL      BOKS0000                * display the heading
          MOVE      ZERO,FORM2              * set patient counter
          MOVE      ZERO,CHKSTAT            * the status to check
          MOVE      SP1,CHKDISP             * set status to display
          MOVE      SEVEN,CVERT             * set first row
          PACK      KEY26,SESSKEY,ZERO,SP20
          CALL      RSOPDEA1
.
BOOK1000  CALL      RKOPDEA1                * next read on details file
          BRANCH    OVRCD,BOOK8000          * end of session
.
BOOK1050  PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     SESSKEY,KEY22
          GOTO      BOOK8000 IF NOT EQUAL   * different sessio
.
          COMPARE   CHKSTAT,OPDASTAT
          GOTO      BOOK8000 IF NOT EQUAL   * end of session
.
.         validate visit and UR number
.
          MOVE      OPDAADMN,KEY8           
          CALL      GURN0000                * get UR number
          BRANCH    EXIT,BOOK1000           * invalid visit number
.
          MATCH     ZEROUR,AURNO
          GOTO      BOOK1100 IF NOT EQUAL   * have a UR number
.
.         on pre-admission file
.
          MOVE      OPDAADMN,KEY8           * visit number
          CALL      RDPRAM1
          BRANCH    OVRCD,BOOK1000          * invalid visit number
.
          MOVE      ZEROUR,PURNO
          GOTO      BOOK1200                * valid patient
.
.         on master file
.
BOOK1100  MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,BOOK1000          * invalid UR number
.
.         have a valid item to display
.         increment counters
.
BOOK1200  ADD       ONE,CVERT               * increment line counter
.
          COMPARE   "23",CVERT
          GOTO      BOOK5000 IF NOT LESS    * need a new page
.
          ADD       ONE,FORM2               * add to patient counter
.
.         display the item
.
BOOK1500  COMPARE   ONE,OPCNSUMM
          GOTO      BOOK2000 IF NOT EQUAL   * display full details
.
. --------------------------------------------------------------
. ------- screen format one                              -------
. ------- just display one line of operation description -------
. --------------------------------------------------------------
.
          CALL      FORP0000                * format patient name
          MOVE      PATHDNAM,DIM26          * shorten name
          MOVE      OPDADES1,OPERDESC       * use first description line
.
          COMPARE   ONE,OPCNCBOK
          GOTO      BOOK1600 IF NOT EQUAL   * not displaying cancelled bookings
.
          COMPARE   THREE,OPDASTAT
          GOTO      BOOK1600 IF NOT EQUAL   * not a cancelled booking
.
          MOVE      "Invalid Code",TDESC
          PACK      KEY5,ANSS,ANSC,OPDACANC
          CALL      RDCODE1
          PACK      OPERDESC,OPDACANC,SP1,TDESC,SP30
.
BOOK1600  DISPLAY   *P1:CVERT,OPDAADMN,*P11:CVERT,OPDACASE,*V2LON,CHKDISP,*HOFF:
                    *P15:CVERT,DIM26,*P44:CVERT,OPDAEXPS:
                    *P50:CVERT,OPDAANAE,*P56:CVERT,OPERDESC
          GOTO      BOOK1000                * get next item
.
. ----------------------------------------------------------------------------
. ------- screen format two                                            -------
. ------- display all lines of operation description if they are there -------
. ----------------------------------------------------------------------------
.
BOOK2000  MOVE      ZERO,RECCOUNT           * check if enough room to display
          MATCH     SP55,OPDADES1
          GOTO      BOOK2100 IF EQUAL       * no desc 1
.
          ADD       ONE,RECCOUNT
.
BOOK2100  MATCH     SP55,OPDADES2
          GOTO      BOOK2200 IF EQUAL       * no desc 2
.
          ADD       ONE,RECCOUNT
.
BOOK2200  MATCH     SP55,OPDADES3
          GOTO      BOOK2300 IF EQUAL       * no desc 3
.
          ADD       ONE,RECCOUNT
.
.         reccount will hold the line number that the final description will
.         be displayed on so make sure this will fit on the page
.
BOOK2300  ADD       CVERT,RECCOUNT          * get the line number
          COMPARE   TWENTY3,RECCOUNT
          GOTO      BOOK5000 IF NOT LESS    * will need a new page
.
.         display the item
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY * format the date of birth
          CALL      PACDATE
          MOVE      CPCDATE,BIRTHDTE
          REP       " 0",BIRTHDTE
.
          MATCH     "0000000000",BIRTHDTE
          GOTO      BOOK2400 IF NOT EQUAL   * have a birth date
.
          MOVE      SP10,BIRTHDTE           * clear date
.
BOOK2400  CALL      FORP0000                * format patients name
.
.         obtain anaesthetic description
.
          MOVE      SP20,DIM18              * clear the description
          MATCH     SP3,OPDAANAE
          GOTO      BOOK2500 IF EQUAL       * no code to display
.
          PACK      KEY5,CODEOA,OPDAANAE
          CALL      RDCODE1
          BRANCH    OVRCD,BOOK2500          * invalid code
          MOVE      TDESC,DIM18
.
BOOK2500  COMPARE   ONE,OPCNCBOK
          GOTO      BOOK2600 IF NOT EQUAL   * not displaying cancelled bookings
.
          COMPARE   THREE,OPDASTAT
          GOTO      BOOK2600 IF NOT EQUAL   * not a cancelled booking
.
          MOVE      "Invalid Code",TDESC
          PACK      KEY5,ANSS,ANSC,OPDACANC
          CALL      RDCODE1
          PACK      DIM18,OPDACANC,SP1,TDESC
.
BOOK2600  DISPLAY   *P1:CVERT,OPDAADMN,*P11:CVERT,OPDACASE,*V2LON,CHKDISP,*HOFF:
                    *P15:CVERT,PATHDNAM,*P43:CVERT,BIRTHDTE:
                    *P54:CVERT,OPDAEXPS,*P60:CVERT,DIM18
.
.         display the descriptions if they are present
.
          MATCH     SP55,OPDADES1
          GOTO      BOOK3000 IF EQUAL       * no description to display
.
          ADD       ONE,CVERT
          DISPLAY   *P15:CVERT,OPDADES1     * display description 1
.
BOOK3000  MATCH     SP55,OPDADES2
          GOTO      BOOK4000 IF EQUAL       * no description to display
.
          ADD       ONE,CVERT
          DISPLAY   *P15:CVERT,OPDADES2     * display description 2
.
BOOK4000  MATCH     SP55,OPDADES3
          GOTO      BOOK1000 IF EQUAL       * no description to display
.
          ADD       ONE,CVERT
          DISPLAY   *P15:CVERT,OPDADES3     * display description 3
          GOTO      BOOK1000                * get next item
.
.         need a new screen
.
BOOK5000  DISPLAY   *P1:24,"( )ext screen, ( )ontinue enquiry ?": 
                    *P2:24,*V2LON,"N",*P17:24,"C"
.
BOOK6000  KEYIN     *P37:24,*DV,UNDLN1,*P37:24,*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANS,ANSN
          GOTO      BOOK7000 IF EQUAL
.
          MATCH     ANS,ANSC
          GOTO      BOOK9999 IF EQUAL
.
          BEEP
          GOTO      BOOK6000
.
BOOK7000  MOVE      ONE,FORM2               * Next selected
          MOVE      EIGHT,CVERT
          DISPLAY   *P1:CVERT,*EF 
          GOTO      BOOK1500                * display the item
.
.         end of current status displayed
.
BOOK8000  COMPARE   ONE,OPCNCBOK
          GOTO      BOOK8100 IF NOT EQUAL   * not displaying canc. bookings
.
          COMPARE   THREE,CHKSTAT
          GOTO      BOOK8100 IF EQUAL       * already displayed cancelled
.
.         set up to display cancelled bookings
.
          MOVE      THREE,CHKSTAT           * set status
          MOVE      ANSC,CHKDISP            * display C for cancelled
          GOTO      BOOK1050
.
BOOK8100  COMPARE   ZERO,FORM2              * Continue selected
          GOTO      BOOK9999 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"No patients for this session - ";
          CALL      HITENTER
.
BOOK9999  RETURN 
+
.****************************************************************************
.*                               FORP0000                                   *
.*                       format patient name                                *
.****************************************************************************
.
. ----- format patient name -----
.
FORP0000  PACK      PATHDNAM WITH SP30,SP30
.
FORP0100  MOVE      PSNAME,PATHDNAM
          ENDSET    PATHDNAM
          CALL      FORP0150                        * get rid of spaces
          APPEND    ", ",PATHDNAM
          APPEND    PTITL,PATHDNAM
          CALL      FORP0150                        * get rid of spaces
          APPEND    SP1,PATHDNAM
          APPEND    PGNAME,PATHDNAM
          RESET     PATHDNAM
          GOTO      FORP0999
.
FORP0150  CMATCH    SP1,PATHDNAM                      * get rid of spaces
          GOTO      FORP0999 IF NOT EQUAL             * in patient name
          BUMP      PATHDNAM BY -1
          GOTO      FORP0999 IF EOS
          GOTO      FORP0150
.
FORP0999  RETURN
+
.****************************************************************************
.*                               BOKS0000                                   *
.*                       display booking screen                             *
.****************************************************************************
BOKS0000  DISPLAY   *P5:4,*EF,OPCNCDSC,*P21:4,":":
                    *P66:2,*V2LON," Booking Details ",*HOFF:
                    *P5:5,"Date",*P21:5,":":
                    *P37:5,"Start Time : "
.
          COMPARE   ONE,OPCNSUMM
          GOTO      BOKS0200 IF NOT EQUAL   * display full details
.
.         single line format
.
          DISPLAY   *P1:7,*V2LON,*ULON,"Adm. No ",*P10:7,"Case":
                    *P15:7,"Patients Name              ",*P44:7,"Start":
                    *P50:7,"Anaes",*P56:7,"Operation Description   "
          GOTO      BOKS0900
.
.         multi line format
.
BOKS0200  DISPLAY   *P1:7,*V2LON,*ULON,"Adm. No ",*P10:7,"Case":
                    *P15:7,"Patients Name             ",*P43:7,"D.O.B     ":
                    *P54:7,"Start",*P60:7,"Anaesthetic      "        
.
.         get clinic/doctor name
.
BOKS0900  MOVE      OPSECLIN,SURGCODE
          BRANCH    OPCNCLSU,BOKS1000       * using doctor code
          MOVE      OPSECLIN,KEY6
          CALL      RDOPCLI1
          MATCH     SP6,OPCLDOCT
          GOTO      BOKS2000 IF EQUAL
          MOVE      OPCLDOCT,SURGCODE
.
BOKS1000  CALL      DCNM0000                * read doctors file and format
          MOVE      DOCNAME,OPCLDESC
.
BOKS2000  DISPLAY   *P23:4,*V2LON,OPSECLIN,*HOFF,*P37:4,OPCLDESC
.
          UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          DISPLAY   *P23:5,*V2LON,CPCDATE,*P50:5,OPSETIME
.
BOKS9999  RETURN
+
.****************************************************************************
.*                               NEXS0000                                   *
.*                       select end display, or display more                *
.****************************************************************************
NEXS0000  DISPLAY   *P1:24,*EL,"(",*V2LON,ANSE,*HOFF,")nd Display, ":
                    *V2LON,ANSD,*HOFF,")isplay more ? "
.
          KEYIN     *P30:24,*V2LON,ANS,*HOFF
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          REP       "E1D2",ANS
          MOVE      ANS,FORM2
          BRANCH    FORM2,NEXS1000,NEXS2000
          BEEP
          GOTO      NEXS0000
.
NEXS1000  MOVE      ONE,EXIT
          GOTO      NEXS9999
.
NEXS2000  MOVE      TWO,EXIT
.
NEXS9999  RETURN
+
.*********************************************************************
.*                  SELB0000                    Called by : CONT1000 *
.*        select static detail,booking detail,exit,print,next        *
.*        Returns : EXIT = 1      exit selected                      * 
.*                  EXIT = 2      next selected                      * 
.*********************************************************************
SELB0000  DISPLAY   *P1:24,"(",*V2LON,ANSS,*HOFF,")tatic Detail, (",*V2LON:
                    ANSB,*HOFF,")ooking Detail, (",*V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSP,*HOFF,")rint, (",*V2LON,ANSN,*HOFF,")ext ? "
SELB0100  KEYIN     *P62:24,*V2LON,ANS,*HOFF
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          REP       UPPLOW,ANS
          REP       "S1B2E3P4N5 01020304050",ANS   * Cannot enter numbers
          MOVE      ZERO,FORM2
          MOVE      ANS,FORM2
          BRANCH    FORM2,SELB1000,SELB2000,SELB3000,SELB4000,SELB5000
.                         Static   Booking  Exit     Print    Next
          BEEP
          GOTO      SELB0100              * invalid option
.
SELB1000  CALL      DIST0000              * display static detail
          GOTO      SELB0000
.
SELB2000  CALL      BOOK0000              * booking details
          GOTO      SELB0000
.
SELB3000  MOVE      TRUE,EXIT             * exit program
          GOTO      SELB9999
.
SELB4000  CALL      PRNN0000              * print static and booking details
          GOTO      SELB1000
.
SELB5000  MOVE      TWO,EXIT              * next session
.
SELB9999  RETURN
+
.*********************************************************************
.*                  PRNN0000                    Called by : SELB4000 *
.*        Print Static and Booking details                           *
.*********************************************************************
PRNN0000  MOVE      " - Static Details",CPHDROPT
          DISPLAY   *P1:24,*EL:
                    *P1:24,*V2LON,*BLINKON,"*** Printing session details ***"
.         
. ------------------------------------
. ------- print static details -------
. ------------------------------------
.
          CALL      HEAD80
          PRINT     *N,*N
          MOVE     SESSKEY,KEY22                                 
          CALL     RDOPSES1
          BRANCH   OVRCD,PRNN8000           * invalid session
.
.         print session doctor
.
          MOVE     OPSECLIN,SURGCODE        * set up doc code
          BRANCH   OPCNCLSU,PRNN0500        * using doctor code
.
          MOVE     "Invalid Code",OPCLDESC
          MOVE     OPSECLIN,KEY6            * clinic/doctor as key
          CALL     RDOPCLI1
          BRANCH   OVRCD,PRNN0700           * invalid clinic code
.
          MATCH    SP6,OPCLDOCT
          GOTO     PRNN0700 IF EQUAL        * have clinic descrption
.
          MOVE     OPCLDOCT,SURGCODE
.
PRNN0500  CALL     DCNM0000                 * get doc and format name
          MOVE     DOCNAME,OPCLDESC         * set up description
.
PRNN0700  PRINT    *5,OPCNCDSC,*21,": ",OPSECLIN,*37,OPCLDESC
.
.         print usual anaesthetist
.
          MOVE     OPSEANAE,SURGCODE
          CALL     DCNM0000                 * get name
.
          PRINT    *5,"Usual Anaesth",*21,": ",OPSEANAE,*37,DOCNAME
.
.         print operating room
.
PRNN1000  MOVE     OPSETHET,KEY6
          PACK     OPRMDESC,SP20,SP20       * clear desc
          CALL     RDOPOPR1
          PRINT    *5,OPCNODSC,*21,":":
                   *23,OPSETHET,*37,OPRMDESC
.
.         print session date
.
PRNN1500  UNPACK   OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL     PACDATE
          MOVE     CPCDATE,DISSDATE
          REP      " 0",DISSDATE
          PRINT    *N,*5,"Date",*21,":":
                   *23,DISSDATE;
.
.         print session status
.
          MOVE     CANC,SSTATUS
          LOAD     SSTATUS,OPSESTAT,NORM,EXTRA
.
          MATCH    SP5,OPSEACTS
          GOTO     PRNN1550 IF NOT EQUAL    * display actual times
.
.         display expected times
.
          PRINT    *37,SSTATUS:
                   *N,*5,"Start Time",*21,":":
                   *23,OPSETIME:
                   *N,*5,"Duration (Mins)",*21,":":
                   *24,OPSEDURA:
                   *37,"End Time : ":
                   *48,OPSEENDT
          GOTO     PRNN1560
.
.         display actual times
.
PRNN1550  PRINT    *37,SSTATUS:
                   *N,*5,"Start Time",*21,":":
                   *23,OPSEACTS:
                   *N,*5,"Duration (Mins)",*21,":":
                   *24,OPSEACTD:
                   *37,"End Time : ":
                   *48,OPSEACTE
.
.         print preparation time and time for breaks
.
PRNN1560  PRINT    *N,*5,"Pat. Prep. Time",*21,": ",*26,OPSEPREP,*29,"mins"
          PRINT    *5,"Time for Breaks",*21,": ",*25,OPSEBRKT,*29,"mins"
.
.         print time period
.
          MOVE     SP20,TDESC               * clear description
.
          MATCH    SP3,OPSETPER
          GOTO     PRNN1590 IF EQUAL        * no code to display
.
          PACK     KEY5,CODESP,OPSETPER
          CALL     RDCODE1
.
PRNN1590  PRINT    *N,*5,"Time Period",*21,":":
                   *23,OPSETPER,*37,TDESC
.
.         print session type
.
PRNN1600  MOVE     SP20,TDESC
.
          MATCH    SP3,OPSETYPE
          GOTO     PRNN1610 IF EQUAL        * no code to display
.
          PACK     KEY5,CODEST,OPSETYPE
          CALL     RDCODE1
.
PRNN1610  PRINT    *5,"Session Type",*21,":":
                   *23,OPSETYPE,*37,TDESC
.
.         print cancellation reason
.
PRNN1700  COMPARE  ZERO,OPSESTAT
          GOTO     PRNN2000 IF NOT EQUAL
.
          MOVE     SP20,TDESC
.
          MATCH    SP3,OPSECANC
          GOTO     PRNN1710 IF EQUAL        * no code to display
.
          PACK     KEY5,CODEOC,OPSECANC
          CALL     RDCODE1
.
PRNN1710  PRINT    *N,*5,"Cancellation",*21,":":
                   *23,OPSECANC,*37,TDESC
.
. -----------------------------------------------------------
. ------- Print all bookings for the selected session -------
. -----------------------------------------------------------
.
PRNN2000  DISPLAY   *P1:24,*EL:
                    *P1:24,*V2LON,*BLINKON,"*** Printing booking details ***"
.
          CALL      PRHH0000                      * print heading
          MOVE      ONE,FORM2               * counter
          MOVE      ZERO,CHKSTAT            * set status to check for
          MOVE      SP1,CHKDISP             * set status to print
          PACK      KEY26,SESSKEY,ZERO,SP20
          CALL      RSOPDEA1
.
PRNN3000  CALL      RKOPDEA1
          BRANCH    OVRCD,PRNN8000          * end of session
.
PRNN3050  PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     SESSKEY,KEY22
          GOTO      PRNN8000 IF NOT EQUAL   * different session
.
          COMPARE   CHKSTAT,OPDASTAT
          GOTO      PRNN8000 IF NOT EQUAL   * end of session
.
          MOVE      OPDAADMN,KEY8
          CALL      GURN0000
          BRANCH    EXIT,PRNN3000           * invalid patient
.
          MATCH     ZEROUR,AURNO
          GOTO      PRNN3100 IF NOT EQUAL   * has a UR number
.
.         on pre-admission file
.
          MOVE      OPDAADMN,KEY8           * visit number
          CALL      RDPRAM1
          BRANCH    OVRCD,PRNN3000          * invalid patient
.
          MOVE      ZEROUR,PURNO            * clear UR number
          GOTO      PRNN3200
.
.         on master file
.
PRNN3100
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PRNN3000          * invalid patient
.
PRNN3200  CALL      FORP0000
          MOVE      PATHDNAM,DIM24
.
PRNN4000  MOVE      OPDADES1,OPERDESC
.
          COMPARE   ONE,OPCNCBOK
          GOTO      PRNN4100 IF NOT EQUAL   * not printing cancelled
.
          COMPARE   THREE,OPDASTAT
          GOTO      PRNN4100 IF NOT EQUAL   * not a cancelled booking
.
          MOVE      "Invalid Code",TDESC
          PACK      KEY5,ANSS,ANSC,OPDACANC
          CALL      RDCODE1
          PACK      OPERDESC,OPDACANC,SP1,TDESC,SP30
.
PRNN4100  PRINT     *1,FORM2,*5,OPDAADMN,*15,OPDACASE,CHKDISP:
                    *19,DIM24,*44,OPDAEXPS:
                    *50,OPDAANAE,*56,OPERDESC
.
          ADD       ONE,FORM2
          ADD       ONE,CLNO
.
          COMPARE   SIXTY,CVERT
          GOTO      PRNN3000 IF NOT EQUAL
          CALL      PRHH0000
          GOTO      PRNN3000
.
.         displayed all of the current status
.
PRNN8000  COMPARE   ONE,OPCNCBOK
          GOTO      PRNN8100 IF NOT EQUAL   * not displaying cancelled bookings
.
          COMPARE   THREE,CHKSTAT
          GOTO      PRNN8100 IF EQUAL       * already printed cancelled
.
          MOVE      THREE,CHKSTAT           * check for cancelled
          MOVE      ANSC,CHKDISP            * print variable
          GOTO      PRNN3050
.
PRNN8100  PRINT     *N,*N,"**** END OF REPORT ****"
          DISPLAY   *P1:24,"Report generated.  ",*EL;
.
PRNN9999  RETURN 
+
.****************************************************************************
.*                  PRHH0000                    Called by : PRNN2000        *
.*        print booking heading details                     PRNN4000        *
.****************************************************************************
PRHH0000  MOVE      " - Booking Detail",CPHDROPT
          CALL      HEAD80
          MOVE      OPSECLIN,SURGCODE
          BRANCH    OPCNCLSU,PRHH1000
          MOVE      OPSECLIN,KEY6
          CALL      RDOPCLI1
          MATCH     SP6,OPCLDOCT
          GOTO      PRHH2000 IF EQUAL
          MOVE      OPCLDOCT,SURGCODE
.
PRHH1000  CALL      DCNM0000                    * read doctors file and format
          MOVE      DOCNAME,OPCLDESC
.
PRHH2000  PRINT     *N,*5,OPCNCDSC,*21,":":
                    *23,OPSECLIN,*37,OPCLDESC
.
          UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISSDATE
          REP       " 0",DISSDATE
          PRINT     *5,"Date",*21,":":
                    *23,DISSDATE:
                    *37,"Start Time : ":
                    *50,OPSETIME
.
          PRINT     *N,*1,"Cnt",*5,"Adm. No ",*14,"Case":
                    *19,"Patients Name           ",*44,"Start":
                    *50,"Anaes",*56,"Operation Description   "
.
          PRINT     *1,"---",*5,"--------",*14,"----":
                    *19,"------------------------",*44,"-----":
                    *50,"-----",*56,"------------------------"
          ADD       SIX,CLNO
.
PRHH9999  RETURN
+
.
. ------- I/O includes needed -------
.
          INC       STD001IO
          INC       BOKDIAIO/INC            * booking diagnois
          INC       BOKRC1IO/INC
          INC       OPRCLIDS
          INC       OPRCLIIO/INC
          INC       OPRDEAIO/INC
          INC       OPROPRDS
          INC       OPROPRIO/INC
          INC       OPRSESIO/INC
          INC       PATCODKY
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATPR1IO/INC            * pre-admission file
................................................................................

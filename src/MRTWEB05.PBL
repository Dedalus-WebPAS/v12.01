.-------------------------------------------------------------------------------
. Program       : MRTWEB05 
.
. Function      : Patient Coding Audit                  
.                 Report 1 - Coding Audit
.                 Report 2 - Coding Comments
.                 Report 3 - Coding Comments (using 'vismdtaf' & 'vismtxaf')
.
. Modifications  :
.-----------------------------------------------------------------------------
. V11.04.03 13/08/2024  Thanh T        TSK 0856783
.           Changed DELNOT00 to check for MRCNACDA patameter (Allow User,
.           Date/Time stamped Coding Comments to be deleted by another user) 
. V11.04.02 06/08/2024  Ebon Clements  TSK 0950175
.           Added pmspx2af read - CODCMT80 and CDCM0800
. V11.04.01 27/11/2023  Ebon Clements  TSK 0940104
.           Added read of pmspx2af - COAU0000
. V11.03.04 24/10/2023  Bella Turco    TSK 0901388
.           Fixed traps around mrtcahaf & mrtcadaf opens                      
.           Changed format of CAULDTTM for Table Sort
. V11.03.03 02/10/2023  Bella Turco    TSK 0938207
.           Added traps around mrtcahaf & mrtcadaf opens                      
. V11.03.02 12/09/2023  Bella Turco    TSK 0901388
.           Fixed table reads in CAUV0000 & CAUP0000 to stop writing incorrect
.           records
. V11.03.01 01/08/2023  Bella Turco    TSK 0901388
.           Added Report 04 options - CAUL0000, CAUV0000, CAUP0000 for Coding 
.           Audit Outputting Rows
. V11.02.02 10/02/2022  Alina Bhari    TSK 0902432
.           Validation check if comment has been codded or not .
.           And if is coded redirect to ICD Coding template 
.           otherwise redirect to Add screen.
.           NXTPAG70,PPAN0000
. V11.02.01 10/02/2022  Thanh T        TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.00.04 05/02/2021  Davin          TSK 0902458 CrossBrowser
.           Added ; to nbsp outputs (CCTB0000)           
. V11.00.03 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V11.00.02 16/07/2020  Sunny          TSK 0891641  
.           Added RDPMPX21 for patient folder color
. V11.00.01 16/03/2020  Tracey Nguyen  TSK 0889496
.           Added CATaO for MRTAUCTM
.------------------------------------------------------------------------------
. V10.14.01 18/02/2019  Ebon Clements  TSK 0829856
.           Recompiled for GETEFDRG - Contract from effective date
. V10.11.04 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.03 28/07/2017  J.Tan             TSK 0838634
.           Recompiled for MRTAUCTM - DRG Description
.           28/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.02 26/07/2017  Thanh T.       TSK 0821710
.           Recompiled PASMISTM"
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
.------------------------------------------------------------------------------
. V10.10.01 07/03/2017  Richa Phogat      TSK 0834533
.           Added a different format date field in display list (TABAUC00)
. V10.08.02 26/08/2016  Don Nguyen        TSK 0312570
.           Recompiled for GETDRG (GTDRG000) - Call GTEDRG00 when PTCNDGED=1
. V10.08.01 08/07/2016  Jill Parkinson    TSK 0822002
.           Recompiled for PATMASTM
. V10.06.01 27/03/2015  J.Tan             313342
.           Changed DEFLTMAN to DIM 2 - added default tick box for 'No Change'
. V10.05.01 18/09/2014  Davin         CAR 304204                      *
.           Recompiled for GETDRG - get drg vers based on disch date  *
. V10.04.04 03/07/2014  Don Nguyen      CAR 302919
.           Called CLMRAC00 before MVMRAC00 for Add form action
. V10.04.03 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.02 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
. V10.04.01 27/03/2014  Peter Vela      CAR 294526
.           Recompiled for MRTAUCTD, MRTAUCTM
.------------------------------------------------------------------------------
. V10.03.05 28/11/2013  Don Nguyen      CAR 294325
.           Added DEFLTMAN - default mandatory fields (for Add template)
. V10.03.04 31/07/2013  Ania P          CAR 249291
.           Added NEXTPAGE "6" - CLSNOR00
. V10.03.03 03/05/2013  Ebon Clements   CAR 261630
.           Removed port number from temp file name
. V10.03.02 15/04/2013  Don Nguyen      CAR 249291
.           Added CDCM0000 to process Coding Comments ('vismdtaf' & 'vismtxaf')
. V10.03.01 16/01/2012  Sandra Barcham  CAR 256563
.           Recompiled for PATMASTM
. V9.12.01  17/12/2009  Peter McMullen CAR 210130
.           Convert to newer Dr name format routine DOCNM000
. V9.08.02  08/05/2007  Mike Laming  CAR 137125 HDP 2007 DRG 5.2
.           Mods to PATDGWFD - Add field PTDWDRGV (add to Keys)
. V9.08.01  25/01/2007  Peter Vela CAR 127930
.           Recompiled for MRTMASFD
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
. V9.04.05  21/11/2005  Peter Vela    CAR 68675
.           Recompiled for PATDSCTM
. V9.04.04  01/03/2005  Peter Vela    CAR 57547
.           Recompiled for PATVADFD and PATVADIO
. V9.04.03  15/12/2004 Davin                     Healthsmart
.           Changed new wies to old wies on coding audit table   
. V9.04.02  07/12/2004 Lina Vo                   CAR 55659
.           Added Coding Comments.                               
. V9.04.01  25/11/2004 Lina Vo                   CAR 34301
.           Recompile for MRTAUCTM                               
. V9.04.00  19/11/2004 Lina Vo                   CAR 34301
.           Created Program                                      
.                    
.-------------------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       APSMASFD/INC
.
          INC       MRTAUCFD/INC
          INC       MRTAUCTD/INC
          INC       MRTMASFD/INC
          INC       MRTCAHFD/INC
          INC       MRTCAHTD/INC
          INC       MRTCADFD/INC
          INC       MRTCADTD/INC
          INC       MRTCONFD/INC
.
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
.
          INC       OUTSITFD/INC
.
          INC       PATALRFD/INC
          INC       PATASFFD/INC
          INC       PATCFAFD/INC
          INC       PATCALAG/INC
          INC       PATCMCFD/INC
          INC       PATCONFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATDADFD/INC
          INC       PATDGWFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATHSPFD/INC
          INC       PATIN1FD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPGRFD/INC
          INC       PATPR1FD/INC
          INC       PATRE1FD/INC
          INC       PATVADFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       PATICDFD/INC
          INC       PATICOFD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCGTD/INC
          INC       PMSVX1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
.
          INC       PRSDTLFD/INC
          INC       TFILEVAR/INC
.
          INC       VISCMTFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PMSEDGFD/INC       * Effective DRG Dates
          INC       PATMISTD/INC
.
          INC       PATECDFD/INC
          INC       PATECOFD/INC
.
.---------------------------------------------------------------------------
.  CGI Variables
.---------------------------------------------------------------------------
DEFLTMAN  DIM       2       * 1st char -Default mandatory fields(Add template)
.                           * 2nd char -Default tick box for 'No Change' field
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
STARTKEY  DIM       10      * Starting Key
CURRDATE  DIM       8       * Current Date
VALDCODE  DIM       70      * Validation Code
VALDTYPE  FORM      1       * Validation Type 1,Disease 2,Operation
.
.---------------------------------------------------------------------------
.  Variables
.---------------------------------------------------------------------------
.
AUDITDTE  DIM       12
.
COMFILAF  FILE      ASCII, FIXED=127
COMFILNM  DIM       8
COMFILNV  DIM       8
COMFILVF  FILE      ASCII, FIXED=127        * For visit Notes
CURRTIME  DIM       8        
.
DIM10     DIM       10
DMDCCODE  DIM       4
DOCTCODE  DIM       10
DDRGCODE  DIM       4
DRGVER    DIM       3
.
FORM8     FORM      8
.
LASTITEM  FORM      3
.
BLNKFLAG  FORM      1
CTR       FORM      1                       * Counter
MBSDESC   DIM       40
NEWDRGDS  DIM       45
NOTEDATE  DIM       12                      * Display Date
NOTEFILT  DIM       3                       * Note display filter
NOTENUMB  DIM       6                       * Note number
NOTEORDR  FORM      1                       * Display notes order
NOTETYPE  DIM       3                       * Note type
STRIKETG  DIM       10                      * Strike tag
STRENDTG  DIM       11                      * Strike end tag
SUPERLEV  DIM       1                       * Supervisor flag
AUDITDAT  DIM       8                       * Audit Date
AUDITTIM  DIM       8                       * Audit Time
.
PSAGECON  DIM       3
PSAGETYP  DIM       3
.
CFPAYCOD  DIM       3        * 3M Codefinder PAY Code
.
CAULDTTM  DIM       16       * Date/Time var for CAUL0000
CAULWEBU  DIM       50       * User ID var for CAUL0000
CAULNDRG  DIM       50       * National DRG/Version for CAUL0000
CAULPDCD  DIM       70       * Primary Diagnosis Code for CAUL0000
CAULPPCD  DIM       70       * Primary Procedure Code for CAUL0000
CAULHFCD  DIM       9        * Health Fund Code for CAUL0000
CAULCLTY  DIM       9        * Claim Type Code for CAUL0000
CAULHDRG  DIM       70       * Health Fund DRG for CAUL0000
CAUPSURG  DIM       84       * Operating Surgeon for CAUP0000
COAUFLAG  FORM      1        * do Coding Audit tables exist mrtcahaf & mrtcadaf
.
CATD1     INIT      "D1"
CATD2     INIT      "D2"
CATD3     INIT      "D3"
CATD4     INIT      "D4"
CATD5     INIT      "D5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
CATaO     INIT      "aO"
CODEYA    INIT      "YA"
CODEYB    INIT      "YB"
DASH      INIT      " - "
LAST      INIT      "999"
AT        INIT      "at"
CAUDTITLE INIT      "ICD Coding Audit for Visit: "
NBSP      INIT      "&nbsp;"
.---------------------------------------------------------------------------
PRGID     INIT      "MRTWEB05"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Coding Audit"
.---------------------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      CODAUD00        
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      CODCMT00        
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      CDCM0000        
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      COAU0000        
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40,NXTPAG50:
                       NXTPAG60,NXTPAG70
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages 
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG50  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG60  CALL      CLSNOR00      * Close - no parent refresh.
          GOTO      NXTPAG99
. 
NXTPAG70  CALL      PPAN0000      * Redirect As per conditional check
          CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);": 
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
.                             " alert(#"",WEBTITLE,"#");":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
.
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
CLSNOR00  CALL      OPENHTML
          BRANCH    EXIT,CLSNOR99
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#">":
                             " if (self.name.match(/PopUpFrame/)) {":
     " parent.document.getElementById('PopUpScreen').style.display=#"none#";":
                             "} else { ":
                             "  self.close(); }":
                             "</script>"
          CALL      CLOSHTML
.
CLSNOR99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.-------------------------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM 
.
          OPEN      MRTAUCA1,"mrtaucaf"
.
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATDADA1,"patdadaf"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATPGRA1,"patpgraf"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATVADA1,"patvadaf"
.
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      PRSDTLA2,"prsdtlaf"
.
          OPEN      VISCMTA1,"viscmtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      VISMDTA1,"vismdtaf"
.
          OPEN      WEBSECA1,"websecaf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,NINTY7;*248,MRCNEACA,*250,MRCNACDA
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          MOVE      ZERO,COAUFLAG
          MATCH     "2",MRCNEACA
          IF        @EQUAL
            MOVE      ONE,COAUFLAG
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      MRTCAHA1,"mrtcahaf"
            TRAPCLR   IO
            IF        OVRCD<>0
              MOVE      ZERO,COAUFLAG
            ENDIF
.
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      MRTCADA1,"mrtcadaf"
            TRAPCLR   IO
            IF        OVRCD<>0
              MOVE      ZERO,COAUFLAG
            ENDIF
          ENDIF
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,COMFILNM
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,COMFILNV
.
          OPEN      PATECDA1,"patecdaf"
          OPEN      PATECOA1,"patecoaf"
.
          CALL      OPICD1                  * Open ICD Disease files
          CALL      OPICO1                  * Open ICD Disease files
.
INIT9999  RETURN
.-------------------------------------------------------------------------------
. Clear Parameters
.-------------------------------------------------------------------------------
CLRPAR00  MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
          MOVE      SP70,ADMISSNO
          MOVE      SP70,URNUMBER
. 
          CALL      CPMRAC00
          CALL      CLCCAH00
          CALL      CLRCAH00
          CALL      CLCCAD00
          CALL      CLRCAD00 
.
          MOVE      ZERO,LASTITEM
.
          MOVE      SP70,NOTENUMB
          MOVE      SP70,NOTETYPE
          MOVE      SP70,SUPERLEV           * Clear supervisor flag
          MOVE      SP70,AUDITDAT
          MOVE      SP70,AUDITTIM
          MOVE      SP70,CAULDTTM
          MOVE      SP70,CAULWEBU
          MOVE      SP70,CAULNDRG
          MOVE      SP70,CAULPDCD
          MOVE      SP70,CAULPPCD
          MOVE      SP70,CAULHFCD
          MOVE      SP70,CAULCLTY
          PACK      CAUPSURG,SP70,SP70
.
          MOVE      SP70,DEFLTMAN
.
CLRPAR99  RETURN
.--------------------------------------------------------------------------
. Set Parameters
.--------------------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF  
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
. 
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mrauc",QRYNAME          
          IF        @EQUAL
            CALL      SPMRA000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "codcmmnt=",QRYNAME
          IF        @EQUAL
            CALL      GETCCM00           * Get coding comments
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "notetype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NOTETYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "notenumb=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NOTENUMB
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "superlev=",QRYNAME                                *I-50359
          IF        @EQUAL
            MOVE      QRYDATA,SUPERLEV
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "visnotes=",QRYNAME
          IF        @EQUAL
            CALL      GETNOT00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "defltman=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEFLTMAN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "auditdat=",QRYNAME                                        
          IF        @EQUAL
            MOVE      QRYDATA,AUDITDAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "audittim=",QRYNAME                                        
          IF        @EQUAL
            MOVE      QRYDATA,AUDITTIM
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------
. GETCCM00 - Get coding comments
.------------------------------------------------------------
GETCCM00  PREP      COMFILAF,COMFILNM
          MOVE      ONE,F3
          WRITE     COMFILAF,SEQ;QRYDATA
.
GETCCM10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA
          GOTO      GETCCM90 IF OVER
          MATCH     SP70,QRYNAME
          GOTO      GETCCM80 IF NOT EQUAL
          ADD       ONE,F3
          WRITE     COMFILAF,SEQ;QRYDATA
          GOTO      GETCCM10
.
GETCCM80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
GETCCM90  MOVE      F3,LASTITEM
          OPEN      COMFILAF,COMFILNM
GETCCM99  RETURN
+
.-------------------------------------------------------------------------------
. Patient Coding Audit            
.-------------------------------------------------------------------------------
CODAUD00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      CODAUD80 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      CODAUD10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      CODAUD20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      CODAUD30 IF EQUAL
.
          GOTO      CODAUD93
.
.         ************ ADD FORMACTION ***********
.
CODAUD10  PACK      KEY10,ADMISSNO,Z70      * get next episode number
          CALL      RSMRAUC1
          CALL      RPMRAUC1
          BRANCH    OVRCD,CODAUD12
.
          MATCH     ADMISSNO,MRACVISN
          GOTO      CODAUD12 IF NOT EQUAL
.
          MOVE      MRACEPSN,F2
          ADD       ONE,F2
          MOVE      F2,MRAUC002
          GOTO      CODAUD14
.
CODAUD12  MOVE      ONE,F2
          MOVE      F2,MRAUC002
.
CODAUD14  MOVE      ADMISSNO,MRAUC001
          PACK      KEY10,MRAUC001,MRAUC002,SP10
          CALL      RDMRAUC1
          COMPARE   ZERO,OVRCD
          GOTO      CODAUD95 IF EQUAL
.
          CALL      CLMRAC00           * clear prev RS/RP read record
          CALL      MVMRAC00
.
          CALL      IBACLOCK
          PACK      MRACUDTE,CCC,CYY,CMM,CDD       * Current Date
          REP       " 0",MRACUDTE
          MOVE      CTIMEIS,MRACUTIM               * Current Time
          MOVE      USERID,MRACUUSR                * Current WebID
.
          MOVE      MRACUDTE,MRACCDTE
          MOVE      MRACUTIM,MRACCTIM
          MOVE      MRACUUSR,MRACCUSR
          MOVE      MRACUDSC,MRACCDSC
          CALL      WRMRAUC1
.
          GOTO      CODAUD99
.
.         ************ UPDATE FORMACTION ***********
.
CODAUD20  MOVE      ADMISSNO,MRAUC001
          PACK      KEY10,MRAUC001,MRAUC002,SP10
          CALL      RDMRAUC1
          BRANCH    OVRCD,CODAUD94
.
          CALL      MVMRAC00
.
          CALL      IBACLOCK
          PACK      MRACUDTE,CCC,CYY,CMM,CDD       * Current Date
          REP       " 0",MRACUDTE
          MOVE      CTIMEIS,MRACUTIM               * Current Time
          MOVE      USERID,MRACUUSR                * Current WebID
.
          CALL      UPMRAUC1
          GOTO      CODAUD99
.
.         ************ DELETE FORMACTION ***********
.
CODAUD30  MOVE      ADMISSNO,MRAUC001
          PACK      KEY10,MRAUC001,MRAUC002,SP10
          CALL      RDMRAUC1
          BRANCH    OVRCD,CODAUD94
.
          CALL      DEMRAUC1
          GOTO      CODAUD99
.
.         ************ DISPLAY FORMACTION **********
.
CODAUD80  MOVE      ADMISSNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,CODAUD92
.
          MOVE      AURNO,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,CODAUD91
.
          MOVE      AURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          MOVE      ADMISSNO,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,CODAUD96
.
          MOVE      SP10,KEY3
          CALL      GTDRG000       * Get Grouper Version - returns it in KEY3
          MOVE      ONE,F2
          PACK      KEY13,ADMISSNO,F2,KEY3,SP70
          CALL      RDPTPGR1   * Read Grouper Details
          IF        OVRCD=1
            PACK      KEY10,ADMISSNO,F2
            PACK      KEY13,KEY10,SP70
            CALL      RSPTPGR1   * Read Grouper Details
            CALL      RKPTPGR1
            IF        OVRCD=0
              PACK      DIM10,PTPGADMN,PTPGEPNO
              MATCH     KEY10,DIM10
              GOTO      CODAUD83 IF EQUAL
            ENDIF
            CALL      CLRGRP00
          ENDIF
.
CODAUD83  CALL      CLMRAC00
          MATCH     Z70,MRAUC002
          GOTO      CODAUD85 IF EQUAL
          MATCH     SP70,MRAUC002
          GOTO      CODAUD85 IF EQUAL
.
          PACK      KEY10,ADMISSNO,MRAUC002,SP10
          CALL      RDMRAUC1
          BRANCH    OVRCD,CODAUD94
.
CODAUD85  CLEAR     WEBTITLE
          MOVE      "Coding Audit",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,CODAUD99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      CODAUD99
.
CODAUD91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CODAUD99
.
CODAUD92  MOVE      "Can't find patient Admission Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CODAUD99
.
CODAUD93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CODAUD99
.
CODAUD94  MOVE      "Can't find Coding Audit Record.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CODAUD99
.
CODAUD95  MOVE      "Coding Audit Record already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CODAUD99
.
CODAUD96  MOVE      "Patient has not been Discharged. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CODAUD99
.
CODAUD99  RETURN
+
.------------------------------------------------------------
.  Coding Comments
.------------------------------------------------------------
.
CODCMT00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      CODCMT80 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Add formaction
          GOTO      CODCMT10 IF EQUAL
.
          GOTO      CODAUD93
.
.         ************ UPDATE FORMACTION ***********
.         copy the comments back to file
.         first delete existing comments
.
CODCMT10  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      CTIMEIS,CURRTIME
.
          MOVE      "004",KEY3          * Coding Comments
          PACK      KEY14,ADMISSNO,KEY3,SP70
          CALL      RSVSCMT1
CODCMT15  CALL      RKVSCMT1
          BRANCH    OVRCD,CODCMT20          * end of file
          MATCH     VSCTVIST,ADMISSNO       * same Admission Number?
          GOTO      CODCMT20 IF NOT EQUAL
          MATCH     VSCTTYPE,KEY3
          GOTO      CODCMT20 IF NOT EQUAL
.
          PACK      KEY14,VSCTVIST,VSCTTYPE,VSCTLINE
          CALL      DEVSCMT1
          GOTO      CODCMT15
.
.         now write the new comments back
.
CODCMT20  MOVE      ADMISSNO,VSCTVIST          * set Admission Number
          MOVE      KEY3,VSCTTYPE
          MOVE      ZERO,F3
.
CODCMT25  ADD       ONE,F3
          READ      COMFILAF,SEQ;VSCTDATA
.
          MATCH     SP70,VSCTDATA
          IF        @EQUAL
            IF        F3>=LASTITEM
              GOTO      CODCMT99
            ELSE
              GOTO      CODCMT25
            ENDIF
          ENDIF
.
          MOVE      F3,VSCTLINE
          PACK      VSCTUKEY,CURRDATE,CURRTIME,USERID,SP70
          PACK      KEY14,VSCTVIST,VSCTTYPE,VSCTLINE
          CALL      WRVSCMT1
          IF        F3>=LASTITEM
            GOTO      CODCMT99
          ENDIF
          GOTO      CODCMT25
.
.         ************ DISPLAY FORMACTION **********
.
CODCMT80  PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,CODCMT92
.
          MOVE      AURNO,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,CODCMT91
.
          MOVE      AURNO,KEY8   * Read Master Extn 2 File
          CALL      RDPMPX21
          BRANCH    OVRCD,CODCMT91
.
          CLEAR     WEBTITLE
          MOVE      "Coding Comment",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,CODCMT99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      CODCMT99
.
CODCMT91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CODCMT99
.
CODCMT92  MOVE      "Can't find patient Admission Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CODCMT99
.
CODCMT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CODCMT99
.
CODCMT99  CLOSE     COMFILAF,DELETE
          RETURN
+
.------------------------------------------------------------
.  Coding Comments (NEW) - report processing
.------------------------------------------------------------
.
CDCM0000  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      CDCM0800 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add formaction
          GOTO      CDCM0100 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update formaction
          GOTO      CDCM0200 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete formaction
          GOTO      CDCM0300 IF EQUAL
.
          GOTO      CDCM9300
.
.
.         ************ ADD FORMACTION ***********
.
CDCM0100  CALL      CHKVIS00                * Checks mandatory fields for blanks
          BRANCH    EXIT,CDCM9999
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,CDCM9200
.
          CALL      ADDNOT00                * Add Coding Comments/Notes
          GOTO      CDCM9999
.
.         ************ UPDATE FORMACTION ***********
.
CDCM0200  
          GOTO      CDCM9999
.
.         ************ DELETE FORMACTION ***********
.
CDCM0300  PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,CDCM9200
.
          CALL      DELNOT00
          GOTO      CDCM9999
.
.         ************ DISPLAY FORMACTION **********
.
CDCM0800  PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,CDCM9200
.
          MOVE      AURNO,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,CDCM9100
.
          MOVE      AURNO,KEY8   * Read Master Extn 2 File
          CALL      RDPMPX21
          BRANCH    OVRCD,CDCM9100
.
          PACK      KEY17,ADMISSNO,NOTETYPE,NOTENUMB,SP70
          CALL      RDVSMDT1
          IF        OVRCD=1
            CALL      CLVNOT00     * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Coding Comments",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,CDCM9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      CDCM9999
.
CDCM9100  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CDCM9999
.
CDCM9200  MOVE      "Can't find patient Admission Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CDCM9999
.
CDCM9300  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CDCM9999
.
CDCM9999  CLOSE     COMFILVF,DELETE
          RETURN
+
.------------------------------------------------------------
.  ICD Coding Audit - report processing         
.------------------------------------------------------------
.
.         ************ DISPLAY FORMACTION **********
.
COAU0000  
COAU0800  PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,COAU9200
.
          MOVE      AURNO,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,COAU9100
.
          MOVE      AURNO,KEY8   * Read Master Extn 2 File
          CALL      RDPMPX21
          BRANCH    OVRCD,COAU9100
.
          CLEAR     WEBTITLE
          PACK      WEBTITLE,CAUDTITLE,ADMISSNO,SP70
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,COAU9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      COAU9999
.
COAU9100  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      COAU9999
.
COAU9200  MOVE      "Can't find patient Admission Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      COAU9999
.
COAU9300  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      COAU9999
.
COAU9999  CLOSE     COMFILVF,DELETE
          RETURN
+
.------------------------------------------------------------
.  Write the Visit Notes for a patient
.------------------------------------------------------------
ADDNOT00  MOVE      ZERO,F6
.
          PACK      KEY17,ADMISSNO,NOTETYPE,Z70
          CALL      RSVSMDT1
          CALL      RPVSMDT1                * read back to get last record
          IF        OVRCD=1
            MOVE      ONE,F6
            MOVE      F6,VSMDNOTE           * Note Number
          ELSE
            MATCH     ADMISSNO,VSMDVISN     * Same visit
            IF        @EQUAL
              MATCH     NOTETYPE,VSMDTYPE   * Same note type
              IF        @EQUAL
                MOVE      VSMDNOTE,F6
                ADD       ONE,F6
                MOVE      F6,VSMDNOTE
              ELSE
                MOVE      ONE,F6
                MOVE      F6,VSMDNOTE       * Note Number
              ENDIF
            ELSE
              MOVE      ONE,F6
              MOVE      F6,VSMDNOTE         * Note Number
            ENDIF
          ENDIF
.
          MOVE      ADMISSNO,VSMDVISN       * Visit number
          MOVE      NOTETYPE,VSMDTYPE       * Notes Type
.
          PACK      VSMDDATE,CCC,CYY,CMM,CDD
          REP       " 0",VSMDDATE          * Date
          CLOCK     TIME,VSMDTIME
          MOVE      USERID,VSMDUSER        * User
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,ADDNOT99
.
          MOVE      WBSEOCG,VSMDOCCG        * Occupation Group
          MOVE      "0",VSMDDELT            * Delete Indicator
          MOVE      SP70,VSMDDDAT           * Delete Date
          MOVE      SP70,VSMDDTIM           * Delete Time
          MOVE      SP70,VSMDDUSE           * Delete User
          MOVE      SP70,VSMDDOCC           * Delete User Occupation group
          PACK      VSMDSPAR,SP70,SP70      * Spare
.
          PACK      KEY17,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RDVSMDT1
          IF        OVRCD=1
            CALL      WRVSMDT1              * Write Record
          ELSE
            GOTO      ADDNOT99
          ENDIF
.
.         now write the visit notes
.
          MOVE      VSMDVISN,VSMTVISN
          MOVE      VSMDTYPE,VSMTTYPE
          MOVE      VSMDNOTE,VSMTNOTE
          MOVE      ZERO,BLNKFLAG
          MOVE      ZERO,F3
.
ADDNOT90  ADD       ONE,F3
          MOVE      F3,VSMTUNIQ
.
          READ      COMFILVF,SEQ;VSMTCMNT
          GOTO      ADDNOT99 IF OVER
.
          MATCH     SP70,VSMTCMNT
          IF        @EQUAL
            BRANCH    BLNKFLAG,ADDNOT90
            MOVE      ONE,BLNKFLAG               * Blank line yes
            GOTO      ADDNOT70                   * don't write second blank line
          ENDIF
          IF        @EOS
            BRANCH    BLNKFLAG,ADDNOT90          * don't write second blank line
            MOVE      ONE,BLNKFLAG               * Blank line yes
            GOTO      ADDNOT70
          ENDIF
.
          MOVE      ZERO,BLNKFLAG
.
ADDNOT70  PACK      KEY20,VSMTVISN,VSMTTYPE,VSMTNOTE,VSMTUNIQ,SP70
          CALL      WRVSMTX1
          IF        F3>=LASTITEM
            GOTO     ADDNOT99
          ENDIF
          GOTO      ADDNOT90
.
ADDNOT99  RETURN
+
.-----------------------------------------------------------
. Delete a Visit Note for a Patient
.-----------------------------------------------------------
DELNOT00  MATCH     SP70,ADMISSNO
          GOTO      DELNOT90 IF EQUAL
.
          MOVE      ADMISSNO,VSMDVISN   *  Visit Number
.
          PACK      KEY17,ADMISSNO,NOTETYPE,NOTENUMB,SP70
          CALL      RDVSMDT1
          BRANCH    OVRCD,DELNOT99
.
.          MATCH     "1",SUPERLEV
.          IF          !@EQUAL
.            MATCH     USERID,VSMDUSER
.            GOTO      DELNOT91 IF NOT EQUAL
.          ENDIF
.
          MATCH     "1",SUPERLEV
          GOTO      DELNOT30 IF EQUAL
.
          MATCH     "1",MRCNACDA
          GOTO      DELNOT30 IF EQUAL
.
          MATCH     USERID,VSMDUSER
          GOTO      DELNOT91 IF NOT EQUAL
.
DELNOT30  PACK      VSMDDDAT,CCC,CYY,CMM,CDD
          REP       " 0",VSMDDDAT
          CLOCK     TIME,VSMDDTIM
          MOVE      USERID,VSMDDUSE
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,DELNOT99
.
          MOVE      WBSEOCG,VSMDDOCC
.
          MOVE      "1",VSMDDELT           * Delete Indicator
          PACK      KEY17,ADMISSNO,NOTETYPE,NOTENUMB,SP70
          CALL      UPVSMDT1               * Write Record
          GOTO      DELNOT98
.
DELNOT90  MOVE      "Admission Number should not be blank.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELNOT99
.
DELNOT91  MOVE      "User ID must be the same to strikeout this entry.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELNOT99
.
DELNOT98  MOVE      ZERO,EXIT
.
DELNOT99  RETURN
+
.-----------------------------------------------------------
. * Checks mandatory fields, Checks admissno :cannot be blank
.------------------------------------------------------------
CHKVIS00  RESET     ADMISSNO
          MATCH     SP70,ADMISSNO
          GOTO      CHKVIS90 IF EQUAL
          GOTO      CHKVIS98
.
CHKVIS90  MOVE      "Admission number should not be blank.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKVIS99
.
CHKVIS98  MOVE      ZERO,EXIT
.
CHKVIS99  RETURN
+
.-------------------------------------------------------------------------------
. Process Fields called from WEBBOD00 
.-------------------------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40
          GOTO      PROFLD99
.
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13,PROFLD14,PROFLD15
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000                * Patient Master Details
          GOTO      PROFLD99
.
PROFLD12  CALL      MISF0000                * Patient Admission Details
          GOTO      PROFLD99
.
PROFLD13  CALL      DSCF0000                * Patient Discharge Details
          GOTO      PROFLD99
.
PROFLD14  CALL      MRAC0000                * Coding Audit         
          GOTO      PROFLD99
.
PROFLD15  CALL      MISC0000                * Misc tags            
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22,PROFLD23
          GOTO      PROFLD99
.
PROFLD21  CALL      MASF0000                * Patient Master Details
          GOTO      PROFLD99
.
PROFLD22  CALL      MISF0000                * Patient Admission Details
          GOTO      PROFLD99
.
PROFLD23  CALL      CCMT0000                * Coding Comments         
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32,PROFLD33
          GOTO      PROFLD99
.
PROFLD31  CALL      MASF0000                * Patient Master Details
          GOTO      PROFLD99
.
PROFLD32  CALL      MISF0000                * Patient Admission Details
          GOTO      PROFLD99
.
PROFLD33  CALL      CCTP0000                * Coding Comments (with header data)
          GOTO      PROFLD99
.
PROFLD40  BRANCH    FLDSETNO,PROFLD41,PROFLD42,PROFLD43,PROFLD44,PROFLD45
          GOTO      PROFLD99
.
PROFLD41  CALL      MASF0000                * Patient Master Details
          GOTO      PROFLD99
.
PROFLD42  CALL      MISF0000                * Patient Admission Details
          GOTO      PROFLD99
.
PROFLD43  CALL      CDAD0000                * Coding Audit
          GOTO      PROFLD99
.
PROFLD44  COMPARE   ZERO,COAUFLAG         * if Coding Audit tables don't exist
          GOTO      PROFLD99 IF EQUAL 
          PACK      KEY24,ADMISSNO,AUDITDAT,AUDITTIM,SP70
          CALL      RDMRCAH1
          BRANCH    OVRCD,PROFLD99
          CALL      MCAH0000                * Write mrtcahaf fields to html
          GOTO      PROFLD99
.
PROFLD45  CALL      MCAD0000                * Write mrtcadaf fields to html
          GOTO      PROFLD99
.
PROFLD99  RETURN
.-----------------------------------------------------------
. Miscellaneous Tags                                    
.-----------------------------------------------------------
MISC0000  BRANCH    FLDITMNO,MISC0100,MISC0200,MISC0300,MISC0400,MISC0500
          GOTO      MISC9999
.
MISC0100  CALL      TABAUC00
          GOTO      MISC9999
.
MISC0200  MOVE      PTPGSDRG,DDRGCODE   * State DRG
          CALL      DISDRG00            * Display Drg Info
          GOTO      MISC9999
.
MISC0300  WRITE     HTMLFILE;PTPGWTOL;  * WIES value total
          GOTO      MISC9999
.
MISC0400  PACK      KEY18,ADMISSNO,Z70
          CALL      RSP2DTL2
MISC0410  CALL      RPP2DTL2
          BRANCH    OVRCD,MISC9999
.
          MATCH     ADMISSNO,P2DTADMN
          GOTO      MISC9999 IF NOT EQUAL
.
          MATCH     SP10,P2DTEPSD
          GOTO      MISC0410 IF EQUAL
.
          UNPACK    P2DTEPSD,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      MISC9999
.
MISC0500  WRITE     HTMLFILE;DEFLTMAN;  * Default mandatory Add screen fields
          GOTO      MISC9999
.
MISC9999  RETURN
+
.------------------------------------------------------------
. Table of Coding Audit                        
.------------------------------------------------------------
TABAUC00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          PACK      KEY10,ADMISSNO,SP70
          CALL      RDMRAUC1
TABAUC10  CALL      RKMRAUC1
          BRANCH    OVRCD,TABAUC99
.
          MATCH     ADMISSNO,MRACVISN
          GOTO      TABAUC99 IF NOT EQUAL
.
          UNPACK    MRACADTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      AUDITDTE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      MRACNDRG,KEY4
          PACK      KEY7,KEY4,LAST,SP10     * last DRG version        *I-137125
          CALL      RSPTDGW1                                          *I-137125
          CALL      RPPTDGW1                                          *C-137125
.
          PACK      NEWDRGDS,MRACNDRG,SP70
          IF        OVRCD=0         
            MATCH     PTDWDRGC,KEY4         * same DRG code ?         *I-137125
            IF        @EQUAL
              PACK      NEWDRGDS,MRACNDRG,DASH,PTDWDESC,SP70
            ENDIF
          ENDIF
.
          PACK      KEY5,CODEYA,MRACTYPE
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      MRACTYPE,TDESC
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateReport('",HTMLLINK
          APPEND    ADMISSNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    MRACEPSN,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",AUDITDTE,"#",":
                             "#"",NEWDRGDS,"#",":
                             "#"",MRACNWIE,"#",":
                             "#"",MRACODRG,"#",":
                             "#"",MRACOWIE,"#",":
                             "#"",MRACDWIE,"#",":
                             "#"",MRACUDSC,"#",":
                             "#"",TDESC,"#",":
                             "#"",MRACADTE,"#");"
.
          GOTO      TABAUC10
.
TABAUC99  RETURN
+
.------------------------------------------------------------
.                     DRG Details
.      0.Description, 1.Code, 2.Low Trim, 3.High Trim
.      INPUT PARAMETER : DDRGCODE
.------------------------------------------------------------
DISDRG00  UNPACK    DIM127,DIM1,KEY1,DIM127
          MOVE      KEY1,F1
.
          PACK      KEY4,DDRGCODE,SP70
          PACK      KEY7,KEY4,LAST,SP10     * last DRG version        *I-137125
          CALL      RSPTDGW1                                          *I-137125
          CALL      RPPTDGW1                                          *C-137125
          BRANCH    OVRCD,DISDRG99
          MATCH     PTDWDRGC,KEY4           * same DRG code ?         *I-137125
          GOTO      DISDRG99 IF NOT EQUAL                             *I-137125
.
          BRANCH    F1,DISDRG10,DISDRG20,DISDRG30
          WRITE     HTMLFILE;PTDWDESC; * Description
          GOTO      DISDRG99
.
DISDRG10  WRITE     HTMLFILE;PTDWDRGC;  * Code
          GOTO      DISDRG99
.
DISDRG20  WRITE     HTMLFILE;PTDWLOWT;  * Low Trim
          GOTO      DISDRG99
.
DISDRG30  WRITE     HTMLFILE;PTDWHIGH;  * High Trim
          GOTO      DISDRG99
.
DISDRG99  RETURN
+
.-------------------------------------------------------------------------------
.                        ****** CLRGRP00 ******
.-------------------------------------------------------------------------------
CLRGRP00  MOVE      SP70,DPTPGADM   * Admission no
          MOVE      SP70,DPTPGEPN   * Episode no
          MOVE      SP70,PTPGGPVR   * Grouper version
          MOVE      SP70,PTPGNDRG   * National DRG
          MOVE      SP70,PTPGNMDC   * National MDC
          MOVE      ZERO,PTPGNWGT   * National weight
          MOVE      ZERO,PTPGNALS   * National average LOS
          MOVE      SP70,PTPGSDRG   * State DRG
          MOVE      SP70,PTPGSMDC   * State MDC
          MOVE      ZERO,PTPGSWGT   * State weight
          MOVE      ZERO,PTPGSALS   * State average LOS
          MOVE      SP70,PTPGLDRG   * Local DRG
          MOVE      SP70,PTPGLMDC   * Local MDC
          MOVE      ZERO,PTPGLWGT   * Local weight
          MOVE      ZERO,PTPGLALS   * Local average LOS
          MOVE      SP70,PTPGPCCL   * Patient CC class level
          MOVE      ZERO,PTPGWFIX   * WIES value fixed
          MOVE      ZERO,PTPGWVAR   * WIES value variable
          MOVE      ZERO,PTPGWTOL   * WIES value total
          MOVE      ZERO,PTPGWWTU   * WIES weight used
          MOVE      SP70,PTPGWWTD   * WIES weight description
          MOVE      SP70,PTPGSPAR   * Spare variable
.
CLRGRP99  RETURN
+
.------------------------------------------------------------
. Coding Comments
.------------------------------------------------------------
CCMT0000  BRANCH    FLDITMNO,CCMT1000
          GOTO      CCMT9999
.
CCMT1000  CALL      DCMT0000                * Display coding comments
          GOTO      CCMT9999
.
CCMT9999  RETURN
+
.------------------------------------------------------------
. Display Coding Comments
.------------------------------------------------------------
DCMT0000  MOVE      "004",KEY3
          PACK      KEY14,ADMISSNO,KEY3,SP70
          CALL      RSVSCMT1
DCMT1000  CALL      RKVSCMT1
          BRANCH    OVRCD,DCMT9999          * end of file
          MATCH     VSCTVIST,ADMISSNO       * same Admission Number?
          GOTO      DCMT9999 IF NOT EQUAL
          MATCH     VSCTTYPE,KEY3
          GOTO      DCMT9999 IF NOT EQUAL
          WRITE     HTMLFILE;VSCTDATA
          GOTO      DCMT1000
.
DCMT9999  RETURN
+
.------------------------------------------------------------
. Coding Comments - NEW tag processing routines ('vismdtaf' & 'vismtxaf')
.------------------------------------------------------------
CCTP0000  BRANCH    FLDITMNO,CCTP0100,CCTP0200,CCTP0300,CCTP0400,CCTP0500:
                             CCTP0600,CCTP0700,CCTP0800
          GOTO      CCTP9999
.
CCTP0100  WRITE     HTMLFILE;VSMDTYPE;      * Visit Note Type
          GOTO      CCTP9999
.
CCTP0200  WRITE     HTMLFILE;VSMDNOTE;      * Visit Note Number
          GOTO      CCTP9999
.
CCTP0300  MATCH     SP70,VSMDDATE           * Visit Note Date
          GOTO      CCTP9999 IF EQUAL
          UNPACK    VSMDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,F2
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      NOTEDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          WRITE     HTMLFILE;NOTEDATE;
          GOTO      CCTP9999
.
CCTP0400  WRITE     HTMLFILE;VSMDTIME;      * Visit Note Time
          GOTO      CCTP9999
.
CCTP0500  WRITE     HTMLFILE;VSMDDELT;      * Delete Indicator (0=No,1=Yes)
          GOTO      CCTP9999
.
CCTP0600  CALL      CCDP0000                * Display Coding Notes/Comments
          GOTO      CCTP9999
.
CCTP0700  MOVE      ONE,NOTEORDR
          CALL      CCTB0000                * Coding Comments List (table)
          GOTO      CCTP9999
.
CCTP0800  WRITE     HTMLFILE;SUPERLEV;      * Supervisor Flag
          GOTO      CCTP9999
.
CCTP9999  RETURN
+
.------------------------------------------------------------
. Display Coding Comments - NEW
.------------------------------------------------------------
CCDP0000  PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
CCDP0100  CALL      RKVSMTX1
          BRANCH    OVRCD,CCDP9999
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      CCDP9999 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      CCDP0100 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      CCDP0100 IF NOT EQUAL
.
          STRIP     VSMTCMNT
          MOVELPTR  VSMTCMNT,LENGTH
.
          COMPARE   LENGTH,ZERO
          IF        @EQUAL
            MOVE      ONE,LENGTH
          ENDIF
.
          SFORMAT   VAR,LENGTH
          MOVE      VSMTCMNT,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
          GOTO      CCDP0100
.
CCDP9999  RETURN
+
.------------------------------------------------------------
. Output Coding Comments Table rows
.------------------------------------------------------------
CCTB0000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MOVE      SP70,NOTEFILT
          UNPACK    DIM127,D1,NOTEFILT,DIM127
.
          CALL      CCNH0000      * Output Table Heading
.
          MOVE      ADMISSNO,VSMDVISN
.
           IF        NOTEORDR=0
            PACK      KEY17,ADMISSNO,NOTEFILT,Z70
          ELSE
            PACK      KEY17,ADMISSNO,NOTEFILT,SP70
          ENDIF
.
          CALL      RSVSMDT1
CCTB0100  IF        NOTEORDR=0
            CALL      RPVSMDT1
          ELSE
            CALL      RKVSMDT1
          ENDIF
          BRANCH    OVRCD,CCTB9999
.
          MATCH     ADMISSNO,VSMDVISN
          GOTO      CCTB9999 IF NOT EQUAL
.
          MATCH     NOTEFILT,VSMDTYPE
          GOTO      CCTB9999 IF NOT EQUAL
.
          MATCH     VSMDDATE,SP70
          IF        @EQUAL
            MOVE     "&nbsp;",NOTEDATE
            GOTO     CCTB0400
          ENDIF
.
          UNPACK    VSMDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      NOTEDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MATCH     "1",VSMDDELT
          IF        @EQUAL
            MOVE       "<strike>",STRIKETG
            MOVE       "</strike>",STRENDTG
          ELSE
            MOVE       "",STRIKETG
            MOVE       "",STRENDTG
          ENDIF
.
          REP       " +",ADMISSNO
          REP       " +",VSMDTYPE
          REP       " +",VSMDNOTE
.
CCTB0400  WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail03(":
                             "#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&admissno=",ADMISSNO:
                             "&superlev=",SUPERLEV:
                             "&notetype=",VSMDTYPE:
                             "&notenumb=",VSMDNOTE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             STRIKETG,NOTEDATE,SP1,VSMDTIME,STRENDTG,"</td>";
.
          PACK      KEY10,VSMDUSER,SP70
          CALL      RDWBSE1
          IF        OVRCD=0
            WRITE     HTMLFILE;"<td>",STRIKETG,WBSENAM," - ",VSMDOCCG,"&nbsp;":
                               STRENDTG,"</td>":
                               "<td>",STRIKETG;
          ELSE
            WRITE     HTMLFILE;"<td>",STRIKETG,VSMDUSER," - ",VSMDOCCG,"&nbsp;":
                               STRENDTG,"</td>":
                               "<td>",STRIKETG;
          ENDIF
.
          REP       "+ ",ADMISSNO
          REP       "+ ",VSMDTYPE
          REP       "+ ",VSMDNOTE
.
          MOVE      ZERO,CTR
          CALL      VSNOTE00
.
          PACK      KEY10,VSMDDUSE,SP70
          CALL      RDWBSE1
          IF        OVRCD=0
            WRITE     HTMLFILE;"&nbsp;",STRENDTG,"</td>":
                               "<td>",WBSENAM," - ",VSMDDOCC,"&nbsp;":
                               "</td>":
                               "</tr>"
          ELSE
            WRITE     HTMLFILE;"&nbsp;",STRENDTG,"</td>":
                               "<td>",VSMDDUSE," - ",VSMDDOCC,"&nbsp;":
                               "</td>":
                               "</tr>"
          ENDIF
.
          GOTO      CCTB0100
.
CCTB9999  RETURN
+
.--------------------------------------------------------------------
. Option 4 - ICD Coding Audit Maintenance
.--------------------------------------------------------------------
CDAD0000  BRANCH    FLDITMNO,CDAD0100,CDAD0200,CDAD0300
          GOTO      CDAD9999
.
CDAD0100  CALL      CAUL0000              * Coding Audit Header List
          GOTO      CDAD9999
.
CDAD0200  CALL      CAUV0000              * Output Coding Audit Diagnosis Row
          GOTO      CDAD9999
.
CDAD0300  CALL      CAUP0000              * Output Coding Audit Procedure Row
          GOTO      CDAD9999
.
CDAD9999  RETURN
+
.--------------------------------------------------------------------
. Option 4 - ICD Coding Audit Maintenance - Output Coding Audit Header Row
.--------------------------------------------------------------------
CAUL0000  COMPARE   ZERO,COAUFLAG         * if Coding Audit tables don't exist
          GOTO      CAUL9999 IF EQUAL
          PACK      KEY24,ADMISSNO,SP70
          CALL      RSMRCAH1                     * read mrtcahaf Header file
CAUL1000  CALL      RKMRCAH1
          BRANCH    OVRCD,CAUL9999
.
          MATCH     ADMISSNO,MRCHADMN
          IF        !@EQUAL
            GOTO      CAUL9999
          ENDIF
.
          MOVE      SP70,CAULDTTM                 * Audit Date/Time
          MATCH     SP70,MRCHADAT
          IF        !@EQUAL
            PACK      CAULDTTM,MRCHADAT,MRCHATIM,SP70
          ENDIF
.
          MATCH     MRCHPDCD,SP70              * Primary Diagnosis Code
          IF        !@EQUAL
            MOVE      DDATE,ICDRDDTE
            PACK      KEY9,MRCHPDCD,SP70
            CALL      RDPTICD1
            PACK      CAULPDCD,MRCHPDCD,NBSP,NBSP,NBSP,DDESC
          ENDIF
.
          MATCH     MRCHPPCD,SP70              * Primary Procedure Code
          IF        !@EQUAL
            MOVE      DDATE,ICDRDDTE
            PACK      KEY9,MRCHPPCD,SP70
            CALL      RDPTICO1
            PACK      CAULPPCD,MRCHPPCD,NBSP,NBSP,NBSP,ODESC
          ENDIF
.
          MOVE      SP70,CAULNDRG
          UNPACK    MRCHDRGV,D1,DIM1,ANS
          MATCH     SP70,MRCHNDRG              * National DRG
          IF        !@EQUAL
            MATCH     "0",D1
            IF        @EQUAL
              PACK      CAULNDRG,MRCHNDRG,SLASH,DIM1,DOT,ANS
            ELSE
              PACK      CAULNDRG,MRCHNDRG,SLASH,D1,DIM1,DOT,ANS
            ENDIF
          ENDIF
.
          PACK      KEY7,MRCHHDRG,MRCHHDGV,SP70
          CALL      RDPTDGW1
          IF        OVRCD=1
            PACK      CAULHDRG,SP70
          ELSE
            MOVE      SP70,CAULHDRG
            MOVE      SP70,D1
            MOVE      SP70,DIM1
            MOVE      SP70,ANS
            UNPACK    MRCHHDGV,D1,DIM1,ANS
            MATCH     SP70,MRCHHDGV                * Health Fund DRG
            IF        !@EQUAL
              MATCH     "0",D1
              IF        @EQUAL
                PACK      CAULHDRG,MRCHHDRG,SLASH,DIM1,DOT,ANS,NBSP,NBSP,NBSP,PTDWDESC,SP70
              ELSE
                PACK      CAULHDRG,MRCHHDRG,SLASH,D1,DIM1,DOT,ANS,NBSP,NBSP,NBSP,PTDWDESC,SP70
              ENDIF  
            ENDIF
          ENDIF
.
          PACK      KEY27,MRCHADMN,MRCHADAT,MRCHATIM,SP1,SP1,ONE,SP70
          CALL      RSMRCAD1
CAUL3000  CALL      RKMRCAD1
          MATCH     MRCHADMN,MRCDADMN
          GOTO      CAUL3050 IF NOT EQUAL
          MATCH     "04",MRCDRTYP
          GOTO      CAUL3000 IF NOT EQUAL
          PACK      CAULHFCD,MRCDDCOD,SP70           * Health Fund
.
CAUL3050  PACK      KEY27,MRCHADMN,MRCHADAT,MRCHATIM,SP1,SP1,ONE,SP70
          CALL      RSMRCAD1
CAUL4000  CALL      RKMRCAD1
          MATCH     MRCHADMN,MRCDADMN
          GOTO      CAUL4050 IF NOT EQUAL
          MATCH     "03",MRCDRTYP
          GOTO      CAUL4000 IF NOT EQUAL
          PACK      CAULCLTY,MRCDDCOD,SP70           * Claim Type
.
CAUL4050  MOVE      SP70,CAULWEBU
          MATCH     SP70,MRCHWEBU
          IF        !@EQUAL
            PACK      KEY10,MRCHWEBU
            CALL      RDWBSE1
            COMPARE   OVRCD,ZERO
            IF        @EQUAL
              PACK      CAULWEBU,WBSENAM,SP70        * User ID 
            ENDIF
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkPatient('",HTMLLINK
          APPEND    MRCHADAT,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    MRCHATIM,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":  
                             "#"",CAULDTTM,"#",":            * 0
                             "#"",CAULPDCD,"#",":                        * 1
			     "#"",CAULPPCD,"#",":                     * 2
                             "#"",CAULNDRG,"#",":                     * 3
                             "#"",CAULHDRG,"#",":                     * 4
                             "#"",MRCHNWAU,"#",":                     * 5
                             "#"",CAULHFCD,"#",":                     * 6
                             "#"",CAULCLTY,"#",":                     * 7
                             "#"",CAULWEBU,"#");"                     * 9 
          GOTO      CAUL1000
.
CAUL9999  RETURN
+
.--------------------------------------------------------------------
. ICD Coding Audit: Output Audit View Diagnosis Code Row 
.--------------------------------------------------------------------
CAUV0000  COMPARE   ZERO,COAUFLAG         * if Coding Audit tables don't exist
          GOTO      CAUV9999 IF EQUAL
          PACK      KEY27,ADMISSNO,AUDITDAT,AUDITTIM,SP1,SP1,ONE,SP70
          CALL      RDMRCAD1                  * read mrtcadaf Audit Detail file
          GOTO      CAUV1050
CAUV1000  CALL      RKMRCAD1
CAUV1050  BRANCH    OVRCD,CAUV9999
.
          MATCH     ADMISSNO,MRCDADMN
          IF        !@EQUAL
            GOTO      CAUV9999
          ENDIF
. 
          MATCH     AUDITDAT,MRCDADAT
          IF        !@EQUAL
            GOTO      CAUV9999
          ENDIF
.
          MATCH     AUDITTIM,MRCDATIM
          IF        !@EQUAL
            GOTO      CAUV9999
          ENDIF
.
          MATCH     "01",MRCDRTYP             * check if Diagnosis Code
          IF        !@EQUAL
            GOTO      CAUV1000
          ENDIF
.
          WRITE     HTMLFILE;"<tr>":
                             "<td>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;":
                             MRCDPRFX,"&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;":
                             MRCDDCOD,"</td>":
                             "<td>",MRCDDDES,"</td>":
                             "<td>",MRCDOFLG,"</td>":
                             "</tr>";
          GOTO      CAUV1000
.
CAUV9999  RETURN
+
.--------------------------------------------------------------------
. ICD Coding Audit: Output Audit View Procedure Code Row 
.--------------------------------------------------------------------
CAUP0000  COMPARE   ZERO,COAUFLAG         * if Coding Audit tables don't exist
          GOTO      CAUP9999 IF EQUAL
          PACK      KEY27,ADMISSNO,AUDITDAT,AUDITTIM,SP1,SP1,ONE,SP70
          CALL      RDMRCAD1
          GOTO      CAUP1050
CAUP1000  CALL      RKMRCAD1
CAUP1050  BRANCH    OVRCD,CAUP9999
.
          MATCH     ADMISSNO,MRCDADMN
          IF        !@EQUAL
            GOTO      CAUP9999
          ENDIF
.
          MATCH     AUDITDAT,MRCDADAT
          IF        !@EQUAL
            GOTO      CAUP9999
          ENDIF
.
          MATCH     AUDITTIM,MRCDATIM
          IF        !@EQUAL
            GOTO      CAUP9999
          ENDIF
.
          MATCH     "02",MRCDRTYP     * check if Procedure Code
          IF        !@EQUAL
            GOTO      CAUP1000
          ENDIF
.
          MOVE      SP70,KEY11
          MATCH     SP70,MRCDODAT
          IF        !@EQUAL
            UNPACK    MRCDODAT,CCENT,CYEAR,CMON,CDAY
            MOVE      ZERO,F2
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          PACK      KEY10,MRCDSURG,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            PACK      CAUPSURG,PMHCSNAM,SP1,PMHCGNAM,SP1,LBRACK,PMHCTITL,RBRACK
          ELSE
            PACK      CAUPSURG,SP70,SP70
          ENDIF
.
          WRITE     HTMLFILE;"<tr>":
                             "<td>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;":
                             MRCDPRFX,"&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;":
                             MRCDPCOD,"</td>":
                             "<td>",MRCDPDES,"</td>":
                             "<td>",KEY11,"</td>":
                             "<td>",MRCDOSTM,"</td>":
                             "<td>",MRCDOETM,"</td>":
                             "<td>",CAUPSURG,"</td>":
                             "</tr>";
          GOTO      CAUP1000
.
CAUP9999  RETURN
.--------------------------------------------------------------------
. Visit note display
.--------------------------------------------------------------------
VSNOTE00  PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
VSNOTE10  CALL      RKVSMTX1
          BRANCH    OVRCD,VSNOTE99
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      VSNOTE99 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      VSNOTE99 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      VSNOTE99 IF NOT EQUAL
.
          COMPARE   TWO,CTR
          IF        @EQUAL
            MATCH     "1",VSMDDELT
            IF        !@EQUAL
              WRITE     HTMLFILE;"...";
            ENDIF
            GOTO      VSNOTE99
          ENDIF
.
          ADD       ONE,CTR
          WRITE     HTMLFILE;VSMTCMNT,"<br>";
.
          GOTO      VSNOTE10
.
VSNOTE99  RETURN
+
.------------------------------------------------------------
. Get visit Comments
.------------------------------------------------------------
GETNOT00  PREP      COMFILVF,COMFILNV
          MOVE      ONE,F3
          WRITE     COMFILVF,SEQ;QRYDATA
.
GETNOT10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA
          GOTO      GETNOT90 IF OVER
          MATCH     SP70,QRYNAME
          GOTO      GETNOT80 IF NOT EQUAL
          ADD       ONE,F3
          WRITE     COMFILVF,SEQ;QRYDATA
          GOTO      GETNOT10
.
GETNOT80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
GETNOT90  MOVE      F3,LASTITEM
          OPEN      COMFILVF,COMFILNV
GETNOT99  RETURN
+
.------------------------------------------------------------
. Coding Comments Table Heading      
.------------------------------------------------------------
CCNH0000  WRITE     HTMLFILE;"<tr>":
                              "<td class=HeadingCell width=20%>Date</td>":
                              "<td class=HeadingCell width=20%>Inputed By</td>":
                              "<td class=HeadingCell width=40%>":
                              "Notes</td>":
                              "<td class=HeadingCell width=20%>Deleted By</td>":
                             "</tr>"
.
CCNH9999  RETURN
+
.-----------------------------------------------------------
. PATECD & PATECO Table Check for Admission No
. NEXPAG70  - Redirect to Add/Update ICD coding screen
.-----------------------------------------------------------
PPAN0000  PACK      KEY13,ADMISSNO,SP70
          CALL      RSPTECD1
          CALL      RKPTECD1              * Episode disease file record
          BRANCH    OVRCD,PPAN2000
.
          MATCH     ADMISSNO,DPTEDADM     * Disiease code exists
          GOTO      PPAN7000 IF EQUAL
.
PPAN2000  PACK      KEY13,ADMISSNO,SP70
          CALL      RSPTECO1
          CALL      RKPTECO1              * Intervention code
          BRANCH    OVRCD,PPAN8000
.
          MATCH     ADMISSNO,DPTEDADM     * No interventions exist
          GOTO      PPAN8000 IF NOT EQUAL
.
PPAN7000  REP       " +",URNUMBER
          REP       " +",ADMISSNO
.
          CLEAR     REDIRECT
          APPEND    "mrtweb02.pbl?",REDIRECT  * Redirect to Update ICD Coding
          APPEND    "&reportno=03",REDIRECT
          APPEND    "&template=001",REDIRECT
          APPEND    "&urnumber=",REDIRECT
          APPEND    URNUMBER,REDIRECT
          APPEND    "&admissno=",REDIRECT
          APPEND    ADMISSNO,REDIRECT
          RESET     REDIRECT
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          GOTO      PPAN9999
.
PPAN8000  REP       " +",URNUMBER
          REP       " +",ADMISSNO
.
          CLEAR     REDIRECT
          APPEND    "mrtweb02.pbl?",REDIRECT  * Redirect to Add ICD Coding
          APPEND    "&reportno=01",REDIRECT
          APPEND    "&template=001",REDIRECT
          APPEND    "&urnumber=",REDIRECT
          APPEND    URNUMBER,REDIRECT
          APPEND    "&admissno=",REDIRECT
          APPEND    ADMISSNO,REDIRECT
          RESET     REDIRECT
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          GOTO      PPAN9999
.
PPAN9999  RETURN
+
.-----------------------------------------------------------
.         Clears the visit notes file variables
.-----------------------------------------------------------
CLVNOT00  MOVE      SP70,VSMDVISN      * Visit Number
          MOVE      SP70,VSMDTYPE      * Comment Type
          MOVE      SP70,VSMDNOTE      * Note Number
          MOVE      SP70,VSMDDATE      * Input Date (CCYYMMDD)
          MOVE      SP70,VSMDTIME      * Input Time (HH:MM:SS)
          MOVE      SP70,VSMDUSER      * Input by WEB User ID (websecaf)
          MOVE      SP70,VSMDOCCG      * Occupational Group WEB User ID
          MOVE      SP70,VSMDDELT      * Delete Indicator (0=No,1=Yes)
          MOVE      SP70,VSMDDDAT      * Delete Date (CCYYMMDD)
          MOVE      SP70,VSMDDTIM      * Delete Time (HH:MM:SS)
          MOVE      SP70,VSMDDUSE      * Delete by WEB User ID (websecaf)
          MOVE      SP70,VSMDDOCC      * Occupational Group WEB User ID Dele
          MOVE      SP70,VSMDSPAR      * Spare Variable
.
          MOVE      SP70,VSMTVISN      * Visit Number
          MOVE      SP70,VSMTTYPE      * Comment Type
          MOVE      SP70,VSMTNOTE      * Note Number
          MOVE      SP70,VSMTUNIQ      * Line Number
          MOVE      SP70,VSMTCMNT      * Comment
          MOVE      SP70,VSMTSPAR      * Spare Variable
.
CLVNOT99  RETURN
+
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       APSMASIO/INC
.
          INC       MRTAUCIO/INC
          INC       MRTMASIO/INC
          INC       MRTCAHIO/INC
          INC       MRTCADIO/INC
.
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
.         
          INC       OUTSITIO/INC
.
          INC       PATALRIO/INC
          INC       PATCMCIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATCFAIO/INC
          INC       PATDADIO/INC
          INC       PATDGWIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATHSPIO/INC
          INC       PATIN1IO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPGRIO/INC
          INC       PATPR1IO/INC
          INC       PATRE1IO/INC
          INC       PATVADIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PATICDIO/INC
          INC       PATICOIO/INC
.
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSHCPIO/INC
          INC       PMSHCGIO/INC
          INC       PMSVX1IO/INC
          INC       PMSPX2IO/INC
.
          INC       PRSDTLIO/INC
.
          INC       VISCMTIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PMSEDGIO/INC       * Effective DRG Dates
.
          INC       CLNHIMAS
          INC       CLPMSPX2
          INC       MRTAUCTM
          INC       MRTCAHTM
          INC       MRTCADTM
          INC       PATDOCCD
          INC       PATDOCTM
          INC       PATDSCTM
          INC       PATMASTM
          INC       PATMISTM
          INC       PATMSXTM
          INC       PMSHCPTM
          INC       PMSHCGTM
          INC       PMSPX2TM
          INC       TFILENAM
          INC       CALCAGE
          INC       GETDRG
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       GETEFDRG
.
          INC       PATECDIO/INC  * disease code
          INC       PATECOIO/INC  * operation code

.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:     IBABOK80                                             *
.*                                                                            *
.*     FUNCTION:         MONTHLY STATS UPDATE                                 *
.*                                                                            *
.*     DATE WRITTEN:     07/06/87                                             *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*        V10.01.01 03/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added PMSVX1FD                                            *
.*        V9.03.00  02/07/2003  Tonii  CAR 40346 (THC64)                      *
.*                  Ported from r5.10                                         *
.******************************************************************************
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADAIO MATADBFD & MATADBIO             *
.*        V5.07.01  26/02/1999  Jim Stathopoulos SRF 645361                   *
.*                  Fixed Date Display                                        *
.*                  22/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*        V5.01.10  29/08/1990 Justin Coates                                  *
.*                  Fixed date formats for booking stats                      *
.*                  Standardized the code                                     *
.*        V5.01.11  24/01/1991 Peter Eddey                                    *
.*                  changed program to use BOKRC1FD                           *
.*                  instead of BOKMASFD                                       *
.*  Bubble means trouble man !                                                *
.*                                                                            *
.*                                                                            *
.******************************************************************************
.
. ------- FD includes needed -------
.
          INC       STD001FD
.
          INC       BOKCONFD/INC            * booking control file
          INC       BOKRC1FD/INC            * booking master file
          INC       BOKSTAFD/INC            * booking stats file
.
          INC       PATCOMM/INC
          INC       PATCODFD/INC            * codes file
          INC       PATCONFD/INC            * control file
          INC       PATDRGFD/INC            * date range file
          INC       PATMI1FD/INC            * admission file
          INC       PMSVX1FD/INC            * admission file
          INC       PATDSCFD/INC            * discharge file
.
. ------- program variables -------
.
CODE      DIM       3         * the current options code
CURDATE   DIM       8         * current date
FROMDATE  DIM       8         * starting date for the period
MONTH     DIM       9
TODATE    DIM       8         * ending date for the period
WDATE     DIM       8         * entered period CCYYMM
.
.
. ------- program constants -------
.
CODEBK    INIT      "BK"
CODEMAT   INIT      "MAT"       *** CODES FILE CODEBK..THCSCOD="MAT"
CODENOR   INIT      "NOR"       *** CODES FILE CODEBK..THCSCOD="NOR"
CODETHE   INIT      "THE"       *** CODES FILE CODEBK..THCSCOD="THE"
.
PRGID     INIT      "IBABOK80"
PRGNAM    INIT      "Monthly Statistical Update"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  MAINLINE CODE                                    *
.*********************************************************************
ML0000    CALL      INIT0000                * initialization
.
ML1000    CALL      KOPT0000                * enter option type
          BRANCH    COPTION OF ML3000,ML2000,ML3000
          GOTO      ML9999                  * exit chosen
.
ML2000    BRANCH    CMATRN,ML3000           * maternity system is on
.
          DISPLAY   *P1:24,*B,"Maternity System is off.  ",*EL;
          CALL      HITENTER
          GOTO      ML1000
.
ML3000    CALL      KCNT0000                * continue or not
          BRANCH    EXIT,ML1000             * dont continue
.
ML4000    CALL      KPER0000                * enter the period to start from
.
ML5000    CALL      CONTQST                 * OK to continue
          BRANCH    CEXIT,ML6000,ML4000,ML1000
.                         Yes    No     Cancel
.
ML6000    CALL      UPDT0000                * do the updates
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
.
          DISPLAY   *P64:24,"bokstaaf";
          OPEN      BOKBSTA1,"bokstaaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      IBACLOCK
          READ      CONTROLF,TEN5;*188,CMATRN
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
INIT9999  RETURN
+
.*********************************************************************
.*                  KOPT0000                    Called by : ML1000   *
.*        Enter the type for the updates                             *
.*        Returns : COPTION       option selected                    *
.*********************************************************************
KOPT0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Normal":
                    *P1:6,*V2LON,TWO,*HOFF," = Maternity":
                    *P1:7,*V2LON,THREE,*HOFF," = Theatre"
.
          KEYIN     *P1:9,*EL,"Please Select : ",*V2LON,COPTION
.
          COMPARE   ZERO TO COPTION
          GOTO      KOPT9999 IF EQUAL
.
          BRANCH    COPTION,KOPT9999,KOPT9999,KOPT9999
.
          BEEP
          GOTO      KOPT0000
.
KOPT9999  RETURN
+
.*********************************************************************
.*                  KCNT0000                    Called by : ML3000   *
.*        Enter whether to continue the report or not                *
.*        Returns : EXIT = 1      dont continue                      *
.*********************************************************************
KCNT0000  DISPLAY   *P1:13,*EF:
                    *P1:14,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:15,*V2LON,ONE,*HOFF:
                    " = Create Usage figures FROM a Month to today":
                    *P1:18,*EL,"Select Option : "
.
KCNT1000  KEYIN     *P17:18,*DV,UNDLN1:
                    *P17:18,*V2LON,MOPTION:
                    *P17:18,*DV,MOPTION
.
          COMPARE   ZERO TO MOPTION
          GOTO      KCNT9000 IF EQUAL
.
          BRANCH    MOPTION,KCNT8000
.
          BEEP
          GOTO      KCNT1000
.
KCNT8000  MOVE      FALSE,EXIT
          GOTO      KCNT9999
.
KCNT9000  MOVE      TRUE,EXIT
.
KCNT9999  RETURN
+
.*********************************************************************
.*                  KPER0000                    Called by : ML4000   *
.*        Keyin the from period                                      *
.*        Returns : WDATE         the from period                    *
.*                  FROMDATE      the from date of the period        *
.*********************************************************************
KPER0000  DISPLAY   *P1:20,*EF,"Usage Month/Year: ",UNDLN2,SLASH,UNDLN2
.
KPER1000  UNPACK    SP4,CYEAR,CMON          * set up for period input
          MOVE      CCC,CCENT
          MOVE      TEN9,CCOL
          MOVE      TWENTY,CVERT
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYPER                  * enter period
.
          PACK      KEY6,ICENT,IYEAR,IMON   * validate the period entered
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,KPER7000          * invlaid period
.
          MOVE      DRGMDSC,MONTH           * display the period that it is
          DISPLAY   *P30:20,*V2LON,MONTH,SP1,CCENT,CYEAR
.
          PACK      WDATE,DRGYR,DRGNUM      * set up the period CCYYMM
          REP       " 0",WDATE              * zero fill
          UNPACK    DRGFDTE,FROMDATE   * set up form date YYMMDD
          UNPACK    DRGTDTE,TODATE     * set up to   date YYMMDD
.
          GOTO      KPER9999
.
KPER7000  DISPLAY    *P40:24,*V2LON,*B,"** Invalid ",CPERMTH,"/Year **",*W2;
          GOTO       KPER1000
.
KPER9999  RETURN
+
.*********************************************************************
.*                  UPDT0000                    Called by : ML6000   *
.*        Update the stats files from WDATE to current date          *
.*********************************************************************
UPDT0000  CALL      RSET0000                * reset all the values
.
          DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"** Re-Calculating **",*HOFF,*W2;
.
.         WE MUST GO THRU WHOLE FILE ...BKREEDAT IS USED
.            ALSO HAVE TO FIND       ...ADATE & DATE OF OTHER BOOKINGS
.
.         BKRESTAT = 0  CHECK BKREEDAT       *** BOOKED
.         BKRESTAT = 1  CHECK ADATE         *** PRE-ADMITTED
.         BKRESTAT = 2  CHECK BKREEDAT       *** CANCELLED
.         BKRESTAT = 3  CHECK ADATE         *** ADMITTED
.         BKRESTAT = 4  CHECK DDATE         *** DISCHARGED
.
.         ONLY GET BOOKINGS,UNDELIVERED & BOOKING CANCELLATIONS,DEPOSITS
.         ie CALCULATE BSBOOKS,BSUNDLV,BSCANCB,BSDEPOS
.
UPDT1000  CALL      UBOK0000                * do initial updates
UPDT2000  CALL      UADM0000                * do admissions updates
UPDT3000  CALL      UDSC0000                * do discharges updates
.
          DISPLAY   *P1:24,*B,"Statistics have been updated.  ",*EL;
          CALL      HITENTER
.
UPDT9999  RETURN
+
.*********************************************************************
.*                  RSET0000                    Called by : UPDT0000 *
.*        Reset all the stats in the correct range and of the        *
.*        same type as the option entered (normal/maternity/theatre) *
.*********************************************************************
RSET0000  DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"** Re-Setting Usage Records **",*HOFF,*W2;
          MOVE      SP20,KEY9
          CALL      RDSBSTA1                * positional read 
          DISPLAY   *P1:24,*EL,"Statistics Usage : ";
.
RSET1000  CALL      RDKBSTA1             *** BOTH MUM & BABY MUST BE DONE
          BRANCH    OVRCD,RSET9999          * end of booking file, continue
.
          MATCH     WDATE,BSDATE            * is booking date < entered date ??
          GOTO      RSET1000 IF LESS        * yes, so dont update
.
          CALL      VTYP0000                * check if a valid booking type
          BRANCH    OVRCD,RSET1000          * not correct type
.
          MOVE      ZERO,BSBOOKS            * reset all the values
          MOVE      ZERO,BSADMTS
          MOVE      ZERO,BSDELVY
          MOVE      ZERO,BSUNDLV
          MOVE      ZERO,BSCANCB
          MOVE      ZERO,BSCANCP
          MOVE      ZERO,BSDSCHS
          MOVE      ZERO,BSDEPOS
.
          CALL      UPBSTA1                 * update the data
          DISPLAY   *P20:24,BSDATE,SP5,BSTYPE;
          GOTO      RSET1000
.
RSET9999  RETURN
+
.*********************************************************************
.*                  UBOK0000                    Called by : UPDT1000 *
.*        Update all the valid booking stats records                 *
.*********************************************************************
UBOK0000  PACK      KEY16,FROMDATE,SP10
          CALL      RSBKREC4              *** FROM WDATE TO LAST BOOKING
.
          DISPLAY   *P1:24,*EL,"Booking   : ";
.
. ------- read next record of booking master file -------
.
UBOK1000  CALL      RKBKREC4
          BRANCH    OVRCD,UBOK9999          * end of booking file
.
          REP       " 0",BKREEDAT            * zero fill date
          MOVE      BKRETYPE,BSTYPE           * set up booking type on stats file
          CALL      VTYP0000                * validate booking option type
          BRANCH    OVRCD,UBOK1000          * invalid option type
.
          PACK      CPTDATE,BKREEDAT     * set up the input date
          REP       " 0",CPTDATE            * zero fill
          CALL      GPERD                   * get the period and year
          BRANCH    EXIT,UBOK1000           * period not on file
          PACK      BSDATE,DRGYR,DRGNUM     * set up year and period, CCYYMM
.
          DISPLAY   *P20:24,BSDATE,*P30:24,BKREBOOK;
.
.         BKRESTAT = 0  CHECK BKREEDAT       *** BOOKED
.         BKRESTAT = 2  CHECK BKREEDAT       *** CANCELLED
.
          PACK      KEY9,BSTYPE,BSDATE      **** BSTYPE CHANGES
          CALL      RDBSTA1
          COMPARE   ONE,OVRCD               * is it on the file ??
          CALL      NEWB0000 IF EQUAL       * no, so write new record
.
          DISPLAY   *P20:24,BKREBOOK,*P30:24,ANSB,*P50:24,BKREEDAT;
.
          ADD       ONE,BSBOOKS             * add to # of bookings
          ADD       ONE,BSUNDLV             * add to # of undelivered
.
          COMPARE   TWO,BKRESTAT              * is booking cancelled ??
          GOTO      UBOK1200 IF NOT EQUAL   * no, so dont add
.
          ADD       ONE,BSCANCB             * add to # cancelled bookings
.
          COMPARE   ONE,CMATRN              * is maternity interface on ??
          GOTO      UBOK1500 IF NOT EQUAL   * no
.
          SUB       ONE,BSUNDLV         **** IF CANCEL REDUCE UNDEL W2.04
          GOTO      UBOK1500
.
. ------- record is not cancelled ------
.
UBOK1200  COMPARE   ONE,CMATRN              * is mat i/face on ??
          GOTO      UBOK1500 IF NOT EQUAL   * no
.
. ------- maternity i/face on ( based on BKREEDAT ) -------
.
          MATCH     ANSY,BKREPREA
          GOTO      UBOK1500 IF NOT EQUAL
.
          ADD       ONE,BSDEPOS             * add to # of deposits paid
.    
. ------- update the booking stats record -------
.
UBOK1500  CALL      UPBSTA1                 * update booking stats file
          GOTO      UBOK1000                * read next record of file
.
UBOK9999  RETURN
+
.*********************************************************************
.*                  UADM0000                    Called by : UPDT2000 *
.*        Update all admissions stats for a booking                  *
.*********************************************************************
UADM0000  PACK      KEY16,FROMDATE,SP30     * start at entered date
          CALL      RDSMISS3                * read in date order
.
          DISPLAY   *P1:24,*EL,"Admission : ";
.
. ------- next read on the admission file -------
.
UADM1000  CALL      RDKMISS3
          BRANCH    OVRCD,UADM9999          * end of admisssion file
.
          MATCH     ADATE,CURDATE           * are we after current date ??
          GOTO      UADM9999 IF LESS        * yes, so finished
.
          DISPLAY   *P20:24,ADATE,*P30:24,AADMNO;
.
          BRANCH    ASTAT OF UADM1000,UADM2000,UADM2000,UADM2000,UADM2000
          GOTO      UADM1000                * ignore pre-admitted
.
UADM2000  MOVE      AADMNO,BKREBOOK           * move admission # to book #
.
. ------- read the booking master file -------
.
          MOVE      BKREBOOK,KEY8             * booking number as key
          CALL      RDBKREC1                 * read bookings file
          BRANCH    OVRCD,UADM1000          * invalid booking number
.
          DISPLAY   *P20:24,ADATE,*P30:24,AADMNO;
.
          PACK      CPTDATE,ADATE       * set up the input date
          REP       " 0",CPTDATE            * zero fill
          CALL      GPERD                   * get the period and year
          BRANCH    EXIT,UADM1000           * period not on file
          PACK      BSDATE,DRGYR,DRGNUM     * set up year and period, CCYYMM
.
          MOVE      BKRETYPE,BSTYPE           * set up the booking type
          CALL      VTYP0000                * is it a valid booking type
          BRANCH    OVRCD,UADM1000          * no, so get next record
.
          PACK      KEY9,BSTYPE,BSDATE      * BSTYPE CHANGES
          CALL      RDBSTA1                 * read booking stats file
          COMPARE   ONE,OVRCD               * is there a record present ??
          CALL      NEWB0000 IF EQUAL       * no, write a new record
.
          DISPLAY   *P20:24,BSDATE,*P30:24,AADMNO;
.
          ADD       ONE,BSADMTS             * add to # pre-admissions
.
          COMPARE   FIVE,ASTAT              * is record cancelled ??
          GOTO      UADM4000 IF NOT EQUAL   * no
.
          ADD       ONE,BSCANCP             * add to # pre-adm cancellations
.
UADM4000  CALL      UPBSTA1                 * update the stats file
          GOTO      UADM1000                * read next admission file
.
UADM9999  RETURN
+
.*********************************************************************
.*                  UDSC0000                    Called by : UPDT3000 *
.*        Update discharge stats                                     *
.*********************************************************************
UDSC0000  PACK      KEY16,FROMDATE,SP30     * start at entered date
          CALL      RDSDSCH2                * read in date sequence
.
          DISPLAY   *P1:24,*EL,"Discharge : ";
.
. ------- read next record of discharge file -------
.
UDSC1000  CALL      RDKDSCH2
          BRANCH    OVRCD,UDSC9999          * end of discharge file so done
.
          DISPLAY   *P20:24,DDATE,*P30:24,DADMNO;
.
          MATCH     DDATE,CURDATE           * is date after current date ?
          GOTO      UDSC9999 IF LESS        * yes so finished
.
          MOVE      DADMNO,BKREBOOK           * use discharge number as key
.
. ------- read the booking master file -------
.
          MOVE      BKREBOOK,KEY8             * booking number as key
          CALL      RDBKREC1                 * read the booking file
          BRANCH    OVRCD,UDSC1000          * invalid booking number
.
          DISPLAY   *P20:24,DDATE,*P30:24,DADMNO;
.
          PACK      CPTDATE,DDATE       * set up the input date
          REP       " 0",CPTDATE            * zero fill
          CALL      GPERD                   * get the period and year
          BRANCH    EXIT,UDSC1000              * period not on file
          PACK      BSDATE,DRGYR,DRGNUM     * set up year and period, CCYYMM
.
          MOVE      BKRETYPE,BSTYPE           * set up the booking type
          CALL      VTYP0000                * validate the booking type
          BRANCH    OVRCD,UDSC1000          * not the correct type
.
          PACK      KEY9 WITH BSTYPE,BSDATE        **** BSTYPE CHANGES
          CALL      RDBSTA1                 * read the stats file
          COMPARE   ONE,OVRCD               * is a record present ?
          CALL      NEWB0000 IF EQUAL       * no, so write a new one
.
          DISPLAY   *P20:24,BSDATE,*P30:24,DADMNO;
.
. ------- update the booking stats file -------
.
          ADD       ONE,BSDSCHS             * add to # discharges
          CALL      UPBSTA1                 * update the booking stats file
          GOTO      UDSC1000                * read next discharge record
.
UDSC9999  RETURN
+
.*********************************************************************
.*                  NEWB0000                    Called by : lots     *
.*        write a new booking stats file record                      *
.*********************************************************************
NEWB0000  MOVE      ZERO,BSBOOKS
          MOVE      ZERO,BSADMTS
          MOVE      ZERO,BSDELVY
          MOVE      ZERO,BSUNDLV
          MOVE      ZERO,BSCANCB
          MOVE      ZERO,BSCANCP
          MOVE      ZERO,BSDSCHS
          MOVE      ZERO,BSDEPOS
          PACK      KEY9,BSTYPE,BSDATE
          CALL      WRBSTA1
          CALL      RDBSTA1
.
NEWB9999  RETURN
+
.*********************************************************************
.*                  VTYP0000                                          *
.*        Validates that the selected booking type has the correct   *
.*        THSCOD value for the option selected                       *
.*        Returns : OVRCD = 1     invalid type                       *
.*********************************************************************
VTYP0000  MATCH     SP3,BSTYPE              * is there a booking type ?
          GOTO      VTYP1000 IF NOT EQUAL   * yes there is
.
          MOVE      "UNK",BSTYPE            * no booking type
          GOTO      VTYP8000
.
. ------- validate the booking type -------
.
VTYP1000  PACK      KEY5,CODEBK,BSTYPE      * key of booking type
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,OVERCOND          * invalid type
.
          LOAD      CODE,COPTION,CODENOR,CODEMAT,CODETHE * get correct type
.
          MATCH     THCSCOD,CODE            * is it the correct thscod value
          GOTO      OVERCOND IF NOT EQUAL   * no, so invalid
.
VTYP8000  MOVE      ZERO,OVRCD              * valid booking type
.
VTYP9999  RETURN
+
.
. ------- I/O includes needed -------
.
          INC       BOKRC1IO/INC
          INC       BOKSTAIO/INC
.
          INC       PATCODIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDSCIO/INC
          INC       PATDRGIO/INC
.
          INC       PATCOMN3
          INC       STD001IO
................................................................................

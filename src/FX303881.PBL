.******************************************************************************
.*    Program      : FX303881                                                 *
.*    Description  : Loop through wattr1af and update certain fields with     *
.*                   new values (keyed in).                                   *
.*                                                                            *
.*    Author       : Don Nguyen                                               *
.*    Date         : 29/08/2014                                               *
.*                                                                            *
.*    Modifications:                                                          *
.*        V10.06.02  17/06/2015  Don Nguyen      CAR 303881                   *
.*                   Added option '9' for 'All' Procedure Status codes match  *
.*        V10.06.01  14/04/2015  Don Nguyen      CAR 315295                   *
.*                   Added Oracle option                                      *
.*        V10.05.01  07/10/2014  Don Nguyen      CAR 303881                   *
.*                   Modified KPAR0000 to call PATCODKY & PATHSPKY            *
.*        V10.05.00  29/08/2014  Don Nguyen      CAR 303881                   *
.*                   Created program                                          *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATHSPFD/INC
          INC       WATTR1FD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
CODE      DIM       2
.
DEFPATH   DIM       60
DIM70     DIM       70
DISNUM    FORM      5
.
NEWPATH   DIM       60
OLDPATH   DIM       60
RECNUM    FORM      8
RECNDISP  DIM       8
UPDTCNTR  FORM      8
UPDTDISP  DIM       8
USINGORC  DIM       1
.
WLSTIHOS  DIM       3             * Waiting List Intended Hospital (pathspaf)
WLSTSTAT  DIM       1             * Waiting List Procedure Status to process
.                                     1 - Unscheduled
.                                     2 - Scheduled
.                                     3 - Pre-Admitted
.                                     4 - Admitted
.                                     6 - Removed from Waiting List
.                                     9 - All
.
WLSTUNIT  DIM       3             * Waiting List Unit (Category WU) to process
.
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
CATWU     INIT      "WU"
.
PRGNAM    INIT      "FIXIT for wattr1af - populate empty WTWMIHSP with default"
PRGID     INIT      "FX303881"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                           MAIN0000                                         *
.*                  Program Mainline                                          *
.******************************************************************************
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN0100  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000:             * run c-isam fixit
                            MAIN1100              * run oracle fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN1100  CALL      KPAR0000                      * keyin processing parameters
          BRANCH    EXIT,MAIN0100,MAIN0100
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000:             * c-isam fixit
                            MAIN7000              * oracle fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
          GOTO      MAIN9999
.
.         Running oracle fix
.
MAIN7000  CALL      READ0000                      * run oracle fix
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                           INIT0000                 Called by: MAIN0000     *
.*        Display heading and check that the files needed exists & open them  *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      SP70,RECNDISP
          MOVE      ZERO,UPDTCNTR
          MOVE      SP70,UPDTDISP
          MOVE      ZERO,DISNUM
          MOVE      SP70,WLSTIHOS
          MOVE      SP70,WLSTSTAT
          MOVE      SP70,WLSTUNIT
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      WATTR1A1,"wattr1af"
.
INIT9999  RETURN
+
.******************************************************************************
.*                           OPTN0000                 Called by: MAIN0000     *
.*                  Get user options or exit                                  *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                            *
.*                        TRUE  (1)  Exit option selected                     *
.*              COPTION - option selected                                     *
.******************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit (Oracle only)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run c-isam fixit
                            OPTN9200             * run oracle fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9200  DISPLAY   *P1:10,*EF,*V2LON,"** Warning: Please backup wattr1af ":
                    "before continuing! **"
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                           KDIR0000                 Called by: MAIN0000     *
.*                  Keyin directory paths to save original file               *
.******************************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    *V2LON,ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP70,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,*V2LON,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP70,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP70,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM70,OLDPATH,SP70
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP70,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,*V2LON,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP70,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP70,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM70,NEWPATH,SP70
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.******************************************************************************
.*                           KPAR0000                 Called by: MAIN0000     *
.*                  Keyin parameters for processing records                   *
.******************************************************************************
KPAR0000  MOVE      ZERO,EXIT
          MOVE      SP70,WLSTUNIT
          DISPLAY   *P1:18,*EL,"Keyin Waiting List Unit to process : ",SP30
          MOVE      "38",ECOL
          MOVE      "18",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,CATWU
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,KPAR9100,KPAR9200
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P42:EVERT,TDESC
          PACK      WLSTUNIT,ACODE
.
KPAR0100  MOVE      SP1,WLSTSTAT
          KEYIN     *P1:20,*EL,"Keyin Procedure Status to process  : ":
                    *P3:21,"(1-Unscheduled, 2-Scheduled, 3-Pre Admitted,":
                    " 4-Admitted, 6-Removed, 9-All): ":
                    *V2LON,WLSTSTAT
.
          PACK      WLSTSTAT,WLSTSTAT,SP1
          MATCH     SP1,WLSTSTAT
          GOTO      KPAR0100 IF EQUAL
.
          MOVE      ZERO,F1
          MOVE      WLSTSTAT,F1
.
          COMPARE   NINE,F1            * All Status codes?
          GOTO      KPAR0200 IF EQUAL
.
          IF        F1=1 | F1=2 | F1=3 | F1=4 | F1=6
            GOTO      KPAR0200         * Valid status code; continue
          ENDIF
          GOTO      KPAR0100           * Invalid Procedure Status code
.
KPAR0200  MOVE      ZERO,EXIT
          MOVE      SP70,WLSTIHOS
.
          MOVE      "44",ECOL
          MOVE      "23",EVERT
          DISPLAY   *P1:23,*EL,"Keyin new default Intended Hospital code : ":
                    SP30
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATHSPKY
          BRANCH    EXIT,KPAR9100,KPAR9200
.
          DISPLAY   *PECOL:EVERT,*V2LON,KEY3,*HOFF,*P48:EVERT,PTHSNAME
          PACK      WLSTIHOS,KEY3
.
          GOTO      KPAR9999
.
KPAR9100  MOVE      ONE,EXIT
          GOTO      KPAR9999
.
KPAR9200  MOVE      TWO,EXIT           * EXITCHAR entered (\)
          GOTO      KPAR9999
.
KPAR9999  RETURN
+
.******************************************************************************
.*                           PREP0000                 Called by: MAIN0000     *
.*                  Prepare files for updating                                *
.******************************************************************************
PREP0000  DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cp `ldat wattr1af.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "svwttraf.dat",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cp `ldat wattr1af.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "svwttraf.idx",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,*EF,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.******************************************************************************
.*                           READ0000                 Called by: MAIN0000     *
.*                  Loop thru old file and update each record accordingly     *
.******************************************************************************
READ0000  DISPLAY   *P1:20,*EF,"Record No. : ";
.
          PACK      KEY19,SP70
          CALL      RDSTREA1                      * position at start
READ1000  CALL      RDKTREA1                      * read next record
          BRANCH    OVRCD,READ2000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 10000  | RECNUM = 1
            MOVE      ZERO,DISNUM
          ENDIF
.
          MOVE      RECNUM,RECNDISP
          LJUSTIFY  RECNDISP
          PACK      RECNDISP,RECNDISP,SP70
          STRIP     RECNDISP
          DISPLAY   *P15:20,*V2LON,*+,RECNDISP,*-,*HOFF;

          MATCH     WLSTUNIT,WMUNIT         * matching Unit?
          GOTO      READ1000 IF NOT EQUAL   * no; get next record
.
          MATCH     "9",WLSTSTAT            * match all Procedure Status codes?
          GOTO      READ1100 IF EQUAL
.
          MATCH     WLSTSTAT,DWMSTAT        * matching Procedure Status?
          GOTO      READ1000 IF NOT EQUAL   * no; get next record
.
READ1100  MATCH     SP70,WTWMIHSP           * Intended Hospital is blank?
          GOTO      READ1000 IF NOT EQUAL   * no; get next record
.
          MOVE      WLSTIHOS,WTWMIHSP       * save new hosp code for blank field
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      UPTREA1
          TRAPCLR   IO
.
          ADD       ONE,UPDTCNTR            * update counter for records updated
.
          GOTO      READ1000                * get next record
.
READ2000  CALL      IBACLOCK
          REP       " 0",CDATE
          MOVE      UPDTCNTR,UPDTDISP
          LJUSTIFY  UPDTDISP
          PACK      UPDTDISP,UPDTDISP,SP70
          STRIP     UPDTDISP
          DISPLAY   *P1:22,*EF,"Ended  : ",CDATE,SP1,CTIMEIS," ... ":
                    *P35:22,*V2LON,*+,UPDTDISP,*-,*HOFF," / ":
                    *V2LON,*+,RECNDISP,*-,*HOFF," records updated !!":
                    *P1:24;
          CALL      HITENTER
.
          GOTO      READ9999 
.
READ9999  RETURN
+
.******************************************************************************
.*                           VALD0000                 Called by: KDIR0000     *
.*                  Check if it's a valid directory                           *
.******************************************************************************
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM70
          ENDSET    DIM70
          CMATCH    SLASH,DIM70
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM70
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM70,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.******************************************************************************
.*                           ENDP0000                 Called by: MAIN0000     *
.*                  Remove or zip save file                                   *
.******************************************************************************
ENDP0000  MOVE      SP70,DIM70
          CLEAR     DIM70
          APPEND    OLDPATH,DIM70        * set up the saved file with path
          APPEND    "svwttraf*",DIM70
          APPEND    SP70,DIM70
          RESET     DIM70
          SQUEEZE   DIM70
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip ",CMDLINE
            APPEND    DIM70,CMDLINE
            APPEND    SP70,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM70,CMDLINE
            APPEND    SP70,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not zipped or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.******************************************************************************
.*                           DEFT0000                 Called by: KDIR0000     *
.*                  Get the default directory                                 *
.******************************************************************************
DEFT0000  EXECUTE   "ldat wattr1af.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'wattr1af'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.------------------------------------------------------------------------------
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATHSPKY
.
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       WATTR1IO/INC

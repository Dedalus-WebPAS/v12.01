. **************************************************************************
. *              P R O G R A M  S U M M A R Y                              *
. *              -------------  -------------                              *
. *                                                                        *
. * Program Name:            IBAPAT87                                      *
. *                                                                        *
. * Function:                (DAY CASES REPORT)                            *
. *                              SAME DAY ADMISSION AND DISCHARGE          *
. *                                                                        *
. * Date Written:            22/08/85                                      *
. *                                                                        *
. * Author:                  MALCOLM HAYSE (I.B.A)                         *
. *                                                                        *
. * Modifications:                                                         *
. *      V12.00.01  25/05/2025 Nikitha B      TSK 0955096                  *
. *                 ALphanumeric changes                                   *
. *      V10.11.01  19/09/2017 Richa Phogat   TSK 0843785                  *
. *                 Added code to print empty report even when there is    *
. *                 No Day Cases for a Date/time period                    *
. *      V9.08.01   20/03/2007 Sandra Barcham 136330                       *
. *                 Fix ward/bed print                                     *
. *      V9.04.01   02/09/2004  Davin   CAR 53145                          *
. *                 Added Multi-Hospital processing                        *
. *      V9.02.01   05/12/2001  Dean Cramer                                *
. *                 Fix totaling not correct                               *
. *                 01/12/2001  Dean Cramer                                *
. *                 Include admission date on print line                   *
. *      V5.08.01   28/08/2000  Caleb Sharman                              *
. *                 Changed Health Fund variables to be 8 chars            *
. *      V5.07.02   16/07/1999  Jill Habasque SRF 632928                   *
. *                 Added total, fixed date range to accept single day     *
. *      V5.07.01   03/05/1999  Jill Habasque                              *
. *                 Changed to enter a date range instead of a single date *
. *      V5.07.B02  28/01/1999  Jim Stathopoulos                           *
. *                 Report Modification                                    *
.                   09/11/1998  Glenn Berry      5.07 changes
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
.
. ----- Extra Variables -----
.
A5TIME    DIM       5
ACTDATE   DIM       8
ASDATE    DIM       10       * Formatted Admission Date
.
COUNT     FORM      5
CURDATE   DIM       8       * Current Date
.
D5TIME    DIM       5
DALERG    DIM       3
DANS      DIM       3
DDDEST    DIM       18
DDSTAT    DIM       18
DILLN     DIM       3
DHHMM     DIM       5
DHOSP     DIM       3
DNAME     DIM       30
DSEX      DIM       6
.
ENDDTE    DIM       8        * End date
.
FSDATE    DIM       10       * Formatted Start Date
FROMDATE  DIM       8
IBCNMHOS  FORM      1        * Multi Hospital Indicator
KEYEDATE  DIM       10       * Keyed in date (DD/MM/YY)
MIN1      FORM      1
.
PNAME     DIM       30
PAGENO    FORM      3
.
SAVBED    DIM       3
SAVWRD    DIM       3
SDATE     DIM       8        * Start date
.
TODATE    DIM       8
.
XDAY1     DIM       2
XDAY2     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
.
CODED     INIT      "D "
CODEDD    INIT      "DD"
.
PRGID     INIT      "IBAPAT87"
PRGNAM    INIT      "Day Cases For A Day Report"
VERSION   INIT      "V12.01.00"
+
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
+
.
. ----- Start Of Program -----
.
.
START     CALL      OPTN0000               * select options
          BRANCH    EXIT,ENDIT             * EXIT = 1 if 0 chosen in menu
.
ML0200    CALL      SDAT0000               * Enter the start date for the report
          BRANCH    OVRCD,START 
          CALL      EDAT0000               * Enter the end date
          BRANCH    OVRCD,ML0200
.
ML0300    CALL       KHOS0000
          BRANCH     EXIT,ML0200
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ADMNNO:          * yes
                          ML0300:          * no
                          ENDIT            * cancel
.
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Print Report"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               SDAT0000              Called by: ML0000  *
.*                     Enter the starting date                            *
.**************************************************************************
.
SDAT0000  DISPLAY   *P1:4,*EF,"Starting Date : "
          UNPACK    SP10,CCENT,CYEAR,CMON,CDAY
          MOVE      TEN7,CCOL
          MOVE      FOUR,CVERT
          MOVE      ZERO,CCANLDTE
          CALL      KEYDATE
          BRANCH    OVRCD,SDAT9999               * nothing entered
.
          PACK      SDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",SDATE
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
          UNPACK    SDATE,CCENT,CYEAR,CMON,CDAY  * format sdate for report
          CALL      PACDATE
.         MOVE      CPCDATE,FSDATE
.
          MATCH     SDATE,CURDATE                * greater than current date?
          GOTO      SDAT9999 IF NOT LESS
.
          DISPLAY   *P1:24,"Starting date cannot be in the future ";
          CALL      HITENTER
          GOTO      SDAT0000
.
SDAT9999  RETURN
+
.**************************************************************************
.*                               EDAT0000              Called by: ML0000  *
.*                           Enter ending date                            *
.**************************************************************************
.
EDAT0000  DISPLAY   *P1:6,*EF,"Ending Date   : "
          UNPACK    SP10,CCENT,CYEAR,CMON,CDAY
          MOVE      TEN7,CCOL
          MOVE      SIX,CVERT
          MOVE      ZERO,CCANLDTE
          CALL      KEYDATE
          BRANCH    OVRCD,EDAT9999               * nothing entered
.
          PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
          MATCH     ENDDTE,CURDATE                * after current date?
          GOTO      EDAT9000 IF LESS
.
          MATCH     ENDDTE,SDATE                  * after start date?
          GOTO      EDAT9999 IF LESS
.
          MATCH     ENDDTE,SDATE                  * after start date?
          GOTO      EDAT9999 IF EQUAL
.
          DISPLAY   *P1:24,"Ending date must be greater than starting date ";
          CALL      HITENTER
          GOTO      EDAT0000
.
EDAT9000  DISPLAY   *P1:24,"Ending date cannot be in the future ";
          CALL      HITENTER
          GOTO      EDAT0000
.
EDAT9999  RETURN
+
.
. ----- Print Via Admission Number -----
.
ADMNNO    CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      SP20,CPHDROPT
          MOVE      ZERO,COUNT
.   
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,"Scanning : ",*P25:24,"Admission :";
	         PACK      KEY16,SDATE,SP8
          CALL      RDSDSCH2
NEXTD     CALL      RDKDSCH2
          BRANCH    OVRCD,ENDP
.
          DISPLAY   *P12:24,*V2LON,DADMNO;
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      ACTDATE,XCENT,XYEAR,XMON,XDAY
.          DISPLAY   *P1:24,*EL,*B,"Discharge Date ",TODATE," Now ",ACTDATE;
          MATCH     SDATE,ACTDATE
          GOTO      ENDP IF LESS
.
          MATCH     ACTDATE,ENDDTE
          GOTO      ENDP IF LESS
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,NEXTD
.
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
.          DISPLAY   *P1:22,"Admission Date ",TODATE," Now ",ACTDATE,*W3
          MATCH     ACTDATE,ADATE
          GOTO      NEXTD IF NOT EQUAL
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,NEXTD
.
          COMPARE   ONE,IBCNMHOS
          GOTO      NEXTH IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      NEXTH IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,NEXTD
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      NEXTD IF NOT EQUAL
.
NEXTH     MOVE      SP30,DNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
.
          MOVE      SP3,SAVWRD
          MOVE      SP3,SAVBED
          MOVE      DTIME,D5TIME
.
       	  PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
.
NEXTT     CALL      RDKTRAN2
          BRANCH    OVRCD,GOTBED
.
          MATCH     AADMNO,TADMN
          GOTO      GOTBED IF NOT EQUAL
.
          MOVE      TWARD,SAVWRD
          MOVE      TBED,SAVBED
.
	  MATCH     SDATE,TDATE
          GOTO      NEXTT IF NOT EQUAL
.
	  MATCH     TTIME,D5TIME
          GOTO      NEXTT IF NOT EQUAL
.
          MATCH     "D",TMOVE
          GOTO      GOTBED IF EQUAL
.
          GOTO      NEXTT
.
GOTBED    CALL      DISPDA
          DISPLAY   *P37:24,*V2LON,DADMNO;
          GOTO      NEXTD
.
NOADMN    CALL      NOREC
          CALL      PAGEND
          PRINT     *1,"Total Number of Daycases = ",COUNT
          PRINT     *N,"*** END OF REPORT ***"
          DISPLAY   *P1:24,*B,*EL,"No Day Cases for this Date. ";
          CALL      HITENTER
          GOTO      START
.
ENDP      COMPARE    ZERO,PAGENO
          GOTO       NOADMN IF EQUAL
.
          CALL       PAGEND
          PRINT      *1,"Total Number of Daycases = ",COUNT
          PRINT      *N,"*** END OF REPORT ***"
          GOTO       START
+
.
. ----- Print Actual Data -----
.
DISPDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      SP20,DDSTAT
          PACK      KEY5,CODED,DSTAT
          CALL      RDCODE1
.
          MOVE      TDESC,DDSTAT
          MOVE      SP20,DDDEST
          PACK      KEY5,CODEDD,DDEST
          CALL      RDCODE1
.
          MOVE      TDESC,DDDEST
          MOVE      DTIME,D5TIME
          MOVE      ATIME,A5TIME
          MOVE      PGNAME,ANS
          PACK      PNAME,ANS,DOT,PSNAME
          MOVE      DGNAME,ANS
          PACK      DNAME,ANS,DOT,DSNAME
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ASDATE
          PRINT     *1,"!",AADMNO,*10,"!",AURNO,*20,"!",ASDATE:
                    *31,"!",*34,A5TIME,*41,"!",*44,D5TIME:
                    *51,"! ",PNAME:
                    *81,"! ",DDSTAT,*105,"! ",DSNAME,*132,"!":
                    *N,"!",*10,"!",*20,"! ",*31,"!",*41,"!":
                    *51,"! WARD : ",SAVWRD,"   BED : ",SAVBED:
                    *81,"! ",DDDEST,*105,"!",*132,"!":
                    *N,"!",*10,"!",*20,"! ",*30,"!",*41,"!":
                    *51,"!",*81,"!",*105,"!",*132,"!"
          ADD       THREE,CLNO
          ADD       ONE,COUNT
DISPEND   RETURN
+
.
. ----- Print when there is no Day Cases for this Date  ---
.
NOREC     CALL      HEAD
.
          PRINT     *1,"!",*10,"!",*20,"! ",*30,"!",*41,"!":
                    *51,"!",*81,"!",*105,"!",*132,"!"
          ADD       ONE,CLNO
NORECEND  RETURN
+
.
. ----- Last Line On The Page -----
.
PAGEND    PRINT     "*-------------------------------------------":
                    "--------------------------------------------":
                    "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
. ----- Headings For Printing -----
.
HEAD      ADD       ONE,PAGENO
       	  COMPARE   ONE,PAGENO
          CALL      PAGEND IF NOT EQUAL
.
          UNPACK    SDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FSDATE
.
          UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          CALL      HEAD132
          IF        IBCNMHOS =1
            PRINT     *40,"Day Cases From: ",FSDATE," to ",CPCDATE
            PRINT     *40,"Hospital : ",PTHSNAME
            ADD       THREE,CLNO
          ELSE
            PRINT     *40,"Day Cases From: ",FSDATE," to ",CPCDATE
            ADD       TWO,CLNO
          ENDIF
          CALL      PAGEND
          PRINT     *1,"! Admn no",*10,"!  U/R no",*20,"! Admn Date":
                    *31,"!Admn Time":
                    *41,"!Dsch Time",*51,"! Patients Name / Ward-Bed":
                    *81,"! Status / Destination":
                    *105,"! Attending Doctor",*132,"!"
          ADD       ONE,CLNO
          CALL      PAGEND
          RETURN
+
ENDIT     CHAIN     PGM
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:8,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      EIGHT,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:8,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.
          INC       STD001IO
.
          INC       PATHSPKY
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
+
.

.******************************************************************************
.* System         : theatre                                                   *
.* Program        : IBAOPR61                                                  *
.* Description    : Ward Report                                               *
.******************************************************************************
.* Programmer     : Paul Duncan                                               *
.* Date           : 28/02/90                                                  *
.* Modifications  : 23/10/90. J.Ceniza.  Fixed up minor bugs.                 *
.*        V10.04.01 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V5.00     18/12/90  Rod Knowles                                     *
.*                  Converted to Version 5                                    *
.*        V5.01     02/01/91  Peter Eddey                                     *
.*                  added description of Total after end of report            *
.*        V5.02     03/07/1991    Justin Coates  SRF 109236                   *
.*                  Due to changes in opr92, must allow for blank wards       *
.*                                                                            *
.*        V5.01.02  21/01/1991    Justin Coates                               *
.*                  Converted back to Release 5.01                            *
.*        V5.01.03  03/07/1991    Justin Coates  SRF 109236                   *
.*                  Due to changes in opr92, must allow for blank wards       *
.*        V5.01.04  18/06/92   ROD                                            *
.*                  Display/Print date range for period                       *
.*----------------------------------------------------------------------------*
.*        V5.02.00  19/10/94  Rob Leonard    SRF 131348                       *
.*                  Fixed header displaying incorrect date                    *
.******************************************************************************
.
.$$$$$
.
.  Ward Report                 
.  -----------             
.
.  - Initialisation
.
.       - Display Header
.       - Open Files
.          OPRCSCA1
.
.  - Processing Options
.
.       - 0 = Exit
.       - 1 = By Ward                 
.
.           - DTRN0000 - enter date range required.
.      
.               - O.K to Continue ?
.       
.           - PROC0000 - loop thru oprwaraf file, writing to tempfile for 
.                    each ward and period.              
.
.           - TEMP0000 - print the entire contents of the tempfile.
.
.                - FINI0000 - print each clinic heading and cancellation code 
.                             headings and totals.
.           - Print end of report message
.
.
.  - END
.
.$$$$$$

.************************************************************************
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRCONFD/INC
          INC       OPRCLIFD/INC
          INC       OPRWARFD/INC
          INC       PATDRGFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATWR1FD/INC
.************************************************************************
.*                           CONSTANTS                                  *
.************************************************************************ 
.
MINUS6    INIT      "------"
MINUS24   INIT      "------------------------"
.
OC        INIT      "OC"
.
.************************************************************************
.*                           VARIABLES                                  *
.************************************************************************
CMDLINE   DIM       70
CPTDATE   DIM       8
.
DATEFR    DIM       8
DATETO    DIM       8
DAY       FORM      2
DESC      DIM       3
DESCFR    DIM       20
DESCTO    DIM       20
DIM2A     DIM       2
DIM6      DIM       6
DIM7      DIM       7
.
ERMESS    DIM       30
.
FIRST     FORM      1
FORM6     FORM      6
.
HEAD      FORM      1
.
.
LAST      FORM      1
LINE      FORM      2
.
NEXTPD    DIM       6
.
OPRTEMP1  DIM       8
OPRTEMP2  DIM       8
.
POS       FORM      1
PRDT      DIM       8
PRDTFR    DIM       8
PRDTTO    DIM       8
PRNTFR    DIM       7
PRNTTO    DIM       7
PERFR     DIM       6
PERTO     DIM       6
PREVCANC  DIM       3
PREVWARD  DIM       3
PRTTDTE   DIM       10
PRTFDTE   DIM       10
.
REQPER    DIM       6
REQPR     DIM       7
.
SECFLAG   FORM      1
SVTFDTE   DIM       10
.
TEMP      DIM       22
TEMP6     FORM      6
.
TEMPFIL1  IFILE     SQL, FIXED=15, KEY="u1-3,4-9,10-14"
TEMPWARD  DIM       3
TEMPPERD  DIM       6
TEMPCNT   FORM      7
TEMPFIL2  IFILE     SQL, FIXED=12, KEY="u1-6"
TEMPDATE  DIM       6
TEMPTOTL  FORM      7
TOTAL     FORM      7
TOTLCANC  FORM      7
TPCENT    DIM       2
TPDAY     DIM       2
TPMON     DIM       2
TPYEAR    DIM       2
.
PRGID     INIT      "IBAOPR61"
PRGNAM    INIT      "Ward Report"
VERSION   INIT      "V12.01.00"
.
.************************************************************************
.*                              MAIN LINE                               *
.************************************************************************
ML0000    CALL      INIT0000            
.
ML1000    CALL      SCRN0000                * Display main screen
          CALL      SLCT0000                * Get selection
.
          BRANCH    EXIT,ML9999             * exit?
.
ML3000    CALL      DTRN0000                * get date range
          BRANCH    EXIT,ML1000
.
          CALL      PROC0000                * process and write to tempfile
.
          CALL      TEMP0000                * output tempfile
          GOTO      ML1000
.
ML9999    CALL      KILL0000
          CHAIN     PGM
.
.************************************************************************
.*                              INIT0000                                *
.*            Display head, open file and read control file             *
.************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CNOUNDLN
.
INIT1000  DISPLAY   *P50:24,"opening oprwaraf"
          OPEN      OPRWARA1,"oprwaraf"
.
          DISPLAY   *P50:24,"Opening oprcliaf"
          OPEN      OPRCLIA1,"oprcliaf"
.
          DISPLAY   *P50:24,"Opening patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P50:24,"Opening patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P50:24,"Opening patdrgaf"
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P50:24,"Opening patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P50:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FIFTY9;*27,CPERMTH 
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OPRTEMP1
.
INIT9999  RETURN
.
.************************************************************************
.*                              SCRN0000                                *
.*                  Display main options screen                         *
.************************************************************************
SCRN0000  HLINE     *G37,2,46,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = By Ward"
.
SCRN9999  RETURN
.
.************************************************************************
.*                              SLCT0000                                *
.*                        Get main selection                            *
.************************************************************************
SLCT0000  KEYIN     *P1:7,*EL,"Select Option : _":
                    *P17:7,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      SLCT9000 IF EQUAL
.
          DISPLAY   *P17:7,*EL,*V2LON,OPTION
.
          BRANCH    OPTION,SLCT8000
          BEEP
          GOTO      SLCT0000
.
SLCT8000  MOVE      ZERO,EXIT               * don't exit
          GOTO      SLCT9999
.
SLCT9000  MOVE      ONE,EXIT                * chose exit
.
SLCT9999  RETURN
.
.*************************************************************************
.*                           DTRN0000                                    *
.*            Get date range to be printed                               *
.*                     called by : ML2000                                *
.*************************************************************************
DTRN0000  DISPLAY   *P3:9,*EF,"Period from : ":
                    *P3:10,"Period to   : "
          MOVE      NINE,LINE
          CALL      GTPD0000                * get 'from' period
          MOVE      PRTFDTE,SVTFDTE
          BRANCH    EXIT,DTRN9000           * none entered
          DISPLAY   *P30:LINE,*V2LON,DRGLDSC:
                    *P50:LINE,*HOFF,"Start date  : ",*V2LON,PRTFDTE
.
          MOVE      REQPER,PERFR
          MOVE      REQPR,PRNTFR
          MOVE      DRGLDSC,DESCFR
.
DTRN0500  MOVE      TEN,LINE
          CALL      GTPD0000                * get 'to' period
          MOVE      SVTFDTE,PRTFDTE
          BRANCH    EXIT,DTRN0000           * none entered
          DISPLAY   *P30:LINE,*V2LON,DRGLDSC:
                    *P50:LINE,*HOFF,"Finish date : ",*V2LON,PRTTDTE
.
          MOVE      REQPER,PERTO
          MOVE      REQPR,PRNTTO
          MOVE      DRGLDSC,DESCTO
.
          MATCH     PERFR,PERTO             * from must be less than to 
          GOTO      DTRN1000 IF LESS
.
          PACK      KEY6,PERFR
          CALL      RDDRGA3                 * read first period from file
          MOVE      ONE,FORM6
.
DTRN0700  CALL      RDKDRGA3                * read next period
          BRANCH    OVRCD,DTRN0800
.
          PACK      DIM6,DRGCYR,DRGNUM
.
          MATCH     DIM6,PERTO              * period less than to period
          GOTO      DTRN0800 IF LESS
.
          ADD       ONE,FORM6
          GOTO      DTRN0700 
.
DTRN0800  COMPARE   TEN5,FORM6              * more than thirteen periods?
          GOTO      DTRN1000 IF NOT LESS
.
          GOTO      DTRN2000
.
DTRN1000  DISPLAY   *P1:24,*EL,*B,"Invalid ",CPERMTH,".  ";
          CALL      HITENTER
          GOTO      DTRN0500
.
DTRN2000  CALL      CONTQST                 * OK to continue?
          BRANCH    CEXIT,DTRN9999,DTRN0000,DTRN9000
.
DTRN9000  MOVE      TRUE,EXIT
          GOTO      DTRN9999
.
DTRN9500  MOVE      FALSE,EXIT
.
DTRN9999  RETURN
.************************************************************************
.*                           GTPD0000                                   *
.*                 Get requested REQPER from user                       *
.*                   called by : ML2000                                 *
.************************************************************************
GTPD0000  
.
.*** Set up KEYPER parametres and default NEXTPD
.
GTPD0050  DISPLAY   *P17:LINE,*EL           * set up default date, gperd and 
          MOVE      LINE,CVERT              * keydate parametres
          MOVE      TEN7,CCOL
          MOVE      TWENTY4,EVERT
          MOVE      ONE,ECOL 
          MOVE      ZERO,CHIGHLT   
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD                   * get current period
          BRANCH    EXIT,GTPD0100
.
          PACK      KEY6,DRGCYR,DRGCNUM
          CALL      RDSDRGA3
          CALL      RDPDRGA3
          BRANCH    OVRCD,GTPD0100
.
          PACK      REQPER,DRGCYR,DRGCNUM   * set up requested period
          UNPACK    REQPER,CCENT,CYEAR,CMON
          CALL      KEYPER                  * keyin period
          BRANCH    OVRCD,GTPD9000
.
          GOTO     GTPD1000
.
.*** NEXTPD not on file error
.
GTPD0100  DISPLAY   *P1:24,*B,"Fatal error - NEXTPD not on file.  ",*EL;
          CALL      HITENTER
          MOVE      TRUE,EXIT
          GOTO      GTPD9999
.
GTPD1000  PACK      KEY6,CCENT,CYEAR,CMON
          CALL      RDDRGA3                      * valid REQPER?
          BRANCH    OVRCD,GTPD3000
          PACK      REQPER,DRGYR,DRGNUM
          PACK      REQPR,DRGCNUM,SLASH,DRGCYR
          
.
.*** set up to and from dates to print
.
          UNPACK    DRGFDTE,TPCENT,TPYEAR,TPMON,TPDAY
          PACK      PRTFDTE,TPDAY,SLASH,TPMON,SLASH,TPCENT,TPYEAR
.
          UNPACK    DRGTDTE,TPCENT,TPYEAR,TPMON,TPDAY
          PACK      PRTTDTE,TPDAY,SLASH,TPMON,SLASH,TPCENT,TPYEAR
.
.*** display long desciption and from and to dates
.
........  DISPLAY   *P30:LINE,*V2LON,PRTFDTE," to ",PRTTDTE
.
          GOTO      GTPD9999
.
GTPD3000  DISPLAY   *P1:24,*B,"Invalid ",CPERMTH,".  ",*EL;
          CALL      HITENTER
          GOTO      GTPD0050
.
.*** no REQPER entered or cancel so exit = true
.
GTPD9000  MOVE      TRUE,EXIT
.
GTPD9999  RETURN          
.**********************************************************************
.*                              CREA0000                              *
.*                   Create the Indexed Temp File                     *
.**********************************************************************
CREA0000  CLEAR     OPRTEMP2
          APPEND    "oprtr2",OPRTEMP2
          APPEND    PORT,OPRTEMP2
          RESET     OPRTEMP2
          REP       " 0",OPRTEMP2
.
.
          CALL      KILL0000                       * kill existing temp file
.
.------ Build new indexed temp file 1 ------
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OPRTEMP1,CMDLINE
          APPEND    " 15 U1-3,4-9,10-14",CMDLINE
          APPEND    SP30,CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL1,OPRTEMP1
.
.------ Build new indexed temp file 2 ------
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OPRTEMP2,CMDLINE
          APPEND    " 12 u1-6",CMDLINE
          APPEND    SP30,CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL2,OPRTEMP2
.
CREA9999  RETURN
+
.**********************************************************************
.*                              KILL0000                              *
.*                  Kill the Tempfile if it Already Exists            *
.**********************************************************************
KILL0000  CLEAR     CMDLINE
          CLOSE     TEMPFIL1
          APPEND    "iserase ",CMDLINE
          APPEND    OPRTEMP1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          CLOSE     TEMPFIL2
          APPEND    "iserase ",CMDLINE
          APPEND    OPRTEMP2,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+

.************************************************************************
.*                           I/O                                        *
.*               I/O routines for temp file                             *
.************************************************************************
.
.------ I/O Routines for temp file 1 ------
.
RAOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPFIL1,KEY9;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPFIL1,KEY9;;
          RETURN
.
RDOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPFIL1,KEY9;TEMPWARD,TEMPPERD,TEMPCNT    
          GOTO      OVERCOND IF OVER
          RETURN
.
RKOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READKS    TEMPFIL1;TEMPWARD,TEMPPERD,TEMPCNT    
          GOTO      OVERCOND IF OVER
          RETURN
.
RPOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READKP    TEMPFIL1;TEMPWARD,TEMPPERD,TEMPCNT    
          GOTO      OVERCOND IF OVER
          RETURN
.
WROPTP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          WRITE     TEMPFIL1,KEY9;KEY9,TEMPCNT    
          RETURN
.
UPOPTP1   UPDATE    TEMPFIL1;TEMPWARD,TEMPPERD,TEMPCNT    
          RETURN
.
DEOPTP1   RESET     KEY9
          DELETE    TEMPFIL1,KEY9
          RETURN
.
.------ I/O Routines for temp file 1 ------
.
RAOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY6 
          READ      TEMPFIL2,KEY6;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY6 
          READ      TEMPFIL2,KEY6;;
          RETURN
.
RDOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY6 
          READ      TEMPFIL2,KEY6;TEMPDATE,TEMPTOTL
          GOTO      OVERCOND IF OVER
          RETURN
.
RKOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY6 
          READKS    TEMPFIL2;TEMPDATE,TEMPTOTL
          GOTO      OVERCOND IF OVER
          RETURN
.
RPOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY6 
          READKP    TEMPFIL2;TEMPDATE,TEMPTOTL
          GOTO      OVERCOND IF OVER
          RETURN
.
WROPTP2   MOVE      ZERO,OVRCD
          RESET     KEY6 
          WRITE     TEMPFIL2,KEY6;KEY6,TEMPTOTL   
          RETURN
.
UPOPTP2   UPDATE    TEMPFIL2;TEMPDATE,TEMPTOTL
          RETURN
.
DEOPTP2   RESET     KEY6 
          DELETE    TEMPFIL2,KEY6 
          RETURN
.
.*************************************************************************
.*                           PROC0000                                    *
.*          Process data and write to the tempfile                       *
.*                     called by : ML3000                                *
.*************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,*P30:24,*EL,*V2LON,"*** Processing ***";
          CALL      CREA0000
          PACK      KEY9,PERFR,SP20 
          CALL      RDOPWAR1
          COMPARE   ZERO,OVRCD
          GOTO      PROC1100 IF EQUAL       * record has a blank ward
.
PROC1000  CALL      RKOPWAR1                * read next record
          BRANCH    OVRCD,PROC9999
.
PROC1100  MATCH     OPWADATE,PERTO          * read too far?
          GOTO      PROC9999 IF LESS
.
          MOVE      OPWAWARD,TEMPWARD       * set up tempfile variables
          MOVE      OPWADATE,TEMPPERD
.
          PACK      KEY9,TEMPWARD,TEMPPERD
          CALL      RDOPTP1                 * read tempfile
          BRANCH    OVRCD,PROC2000
.
          ADD       OPWANCAS,TEMPCNT
.
          CALL      UPOPTP1                 * update tempfile
          SUB       TEMPCNT,TEMPCNT
          GOTO      PROC1000
.
PROC2000  SUB       TEMPCNT,TEMPCNT
          ADD       OPWANCAS,TEMPCNT
.
          PACK      KEY9,TEMPWARD,TEMPPERD
          CALL      WROPTP1                 * write to tempfile
          GOTO      PROC1000
.
PROC9999  RETURN
.*************************************************************************
.*                           TEMP0000                                    *
.*             Print the contents of the tempfile                        *
.*                     called by : ML4000                                *
.*************************************************************************
TEMP0000  DISPLAY   *P1:24,*EL,*P29:24,*V2LON,"*** Printing ***";
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO

          CALL      HEAD0000                * print header
.
          CALL      SET0000                 * write all tempfil2 values to 0
          MOVE      ONE,FIRST
          MOVE      ZERO,LAST
          PACK      KEY9,SP20              * first = yes
          CALL      RSOPTP1
.
TEMP1000  CALL      RKOPTP1                 * read next record
          BRANCH    OVRCD,TEMP8000
.
          MATCH     PREVWARD,TEMPWARD       * new clinic?
          CALL      FINI0000 IF NOT EQUAL   * if so, print previous totals and
                                            * heading for new clinic
.
          MATCH     PREVWARD,TEMPWARD       * same clinic and.
          GOTO      TEMP1100 IF EQUAL       *                .
.                                                            .
          GOTO      TEMP1300                *                .
.                                                            .
TEMP1100 
          MOVE      ONE,POS
          MOVE      ZERO,SECFLAG
          GOTO      TEMP2000
.
TEMP1300  MOVE      ZERO,SECFLAG            * print both headings
          MOVE      ZERO,POS                * re-pos in period file
.
TEMP2000  CALL      NXTP0000                * get next period
          BRANCH    EXIT,TEMP9000
          MOVE      ONE,POS
.
          MATCH     TEMPPERD,NEXTPD         * period on tempfile same as next
                                            * cronological period
          GOTO      TEMP3000 IF EQUAL       * if so, print values
.
          PACK      TEMP,TEMPWARD,TEMPPERD,TEMPCNT * save values
          MOVE      NEXTPD,TEMPPERD
          SUB       TEMPCNT,TEMPCNT         * clear count
.
          CALL      PRNT0000                * zero fill output
.
          UNPACK    TEMP,TEMPWARD,TEMPPERD,DIM7 * reset values  
          MOVE      DIM7,TEMPCNT
          GOTO      TEMP2000
.
TEMP3000  
          CALL      PRNT0000                * print value 
          MOVE      ZERO,TEMPCNT            * clear count
          MOVE      TEMPWARD,PREVWARD       * set prevclin
          GOTO      TEMP1000
.
TEMP8000  MOVE      ONE,LAST                * last = yes 
          CALL      FINI0000                * finish current code and print 
.                                           * totals for current clinnic
TEMP9000  PRINT     *N,*N,"*** End of Report ***",*N,*N:
                    "Total = Number of cases performed on patients from each ward."
.
TEMP9999  RETURN
. 
.*************************************************************************
.*                           FINI0000                                    *
.*            Pad output for clinics with no data in last NEXTPD         *
.*                     called by : TEMP1000                              *
.*************************************************************************
FINI0000  MOVE      TEMPCNT,DIM7
          PACK      TEMP,TEMPWARD,TEMPPERD,DIM7   * save values 
FINI1000 
          CALL      NXTP0000                * get next period
          BRANCH    EXIT,FINI2000
.
          MOVE      TEMPCNT,DIM7
          MOVE      PREVWARD,TEMPWARD
          MOVE      NEXTPD,TEMPPERD
          SUB       TEMPCNT,TEMPCNT
.         
          PRINT     TEMPCNT;                * print zeros till reach last period
.
          GOTO      FINI1000
.
FINI2000  BRANCH    FIRST,FINI3600
          PRINT     TOTAL                   * print total
          SUB       TOTAL,TOTAL
          SUB       TEMPTOTL,TEMPTOTL
.
          UNPACK    TEMP,TEMPWARD,TEMPPERD,DIM7 *reset values
          MOVE      DIM7,TEMPCNT 
.
          BRANCH    LAST,FINI9000
.
FINI3500  COMPARE   "55",CLNO               * time for new page?
          CALL      HEAD0000 IF NOT LESS
. 
FINI3600  MOVE      "No Ward code",WBDESC * default desc
          MATCH     SP3,TEMPWARD
          GOTO      FINI3650 IF EQUAL       * no code
.
          MOVE      "Unknown Code",WBDESC   * default code
          PACK      KEY6,TEMPWARD,SP3       * get code desc    
          CALL      RDWARD1
.
FINI3650  PRINT     TEMPWARD,*5,WBDESC,*26;
.
          MOVE      ZERO,FIRST
          ADD       ONE,CLNO
          GOTO      FINI9999
.
FINI9000  CALL      TOTL0000                * print last totals
          MOVE      TRUE,EXIT 
.
FINI9999  RETURN 
.
.*************************************************************************
.*                           PRNT0000                                    *
.*                   Print a line of output                              *
.*                 called by : TEMP2000,TEMP3000                         *
.*************************************************************************
PRNT0000  PRINT     TEMPCNT;
          ADD       TEMPCNT,TOTAL
.
.** update to second tempfile for totals
.
          MOVE      TEMPPERD,TEMPDATE
          MOVE      TEMPDATE,KEY6
          CALL      RDOPTP2
          BRANCH    OVRCD,PRNT9999
          ADD       TEMPCNT,TEMPTOTL
          CALL      UPOPTP2
.      
PRNT9999  RETURN
.
.*************************************************************************
.*                           NXTP0000                                    *
.*               Get the next period from period file                    *
.*                     called by : TEMP2000                              *
.*************************************************************************
NXTP0000  BRANCH    POS,NXTP1000            * want to re-position at start
.        
          PACK      KEY6,PERFR              * position at from date
          CALL      RDDRGA1
          BRANCH    OVRCD,NXTP9000
.
          PACK      NEXTPD,DRGYR,DRGNUM     * set up next period
.
          MOVE      DRGSDSC,DESC            * set up desc
          GOTO      NXTP8000
.
NXTP1000  CALL      RDKDRGA1                * read next period
          BRANCH    OVRCD,NXTP9000
.
          PACK      NEXTPD,DRGYR,DRGNUM
.
          MATCH     NEXTPD,PERTO            * read too far?
          GOTO      NXTP9000 IF LESS
.
          MOVE      DRGSDSC,DESC            * set up desc
          MOVE      ONE,POS
.
NXTP8000  MOVE      FALSE,EXIT
          GOTO      NXTP9999
.
NXTP9000  MOVE      TRUE,EXIT
.
NXTP9999  RETURN 
        
.*************************************************************************
.*                           HEAD0000                                    *
.*                     Print the headings                                *
.*                     called by : TEMP0000                              *
.*************************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *N,*45,"From : ",PRNTFR,SP2,PRTFDTE:
                    *N,*45,"To   : ",PRNTTO,SP2,PRTTDTE:
                    *N,*N,"WARD DESCRIPTION",*25; 
.
          MOVE      ZERO,POS
.
HEAD1000  CALL      NXTP0000
          BRANCH    EXIT,HEAD2000
.
          UNPACK    DRGCYR,DIM2,YEAR
.
          MOVE      ONE,POS
          PRINT     SP2,DRGCNUM,SLASH,YEAR;
          GOTO      HEAD1000
.
HEAD2000  PRINT     "  Total",*N:
                    MINUS24,*26;
.
          MOVE      ZERO,POS
.
HEAD3000  CALL      NXTP0000
          BRANCH    EXIT,HEAD4000
.
          MOVE      ONE,POS
          PRINT     SP1,MINUS6;
          GOTO      HEAD3000
.

HEAD4000  PRINT     SP1,MINUS6
.
HEAD9999  RETURN
.

.*************************************************************************
.*                           SET0000                                     *
.*                     set the totals tempfile                           *
.*                     called by : TOTL0000                              *
.*************************************************************************
SET0000   MOVE      ZERO,POS
          CALL      NXTP0000
.
          MOVE      NEXTPD,TEMPDATE
          MOVE      TEMPDATE,KEY6
          MOVE      ZERO,TEMPTOTL
          CALL      WROPTP2
          MOVE      ONE,POS
.
SET1000   CALL      NXTP0000
          BRANCH    EXIT,SET9999
.
          MOVE      NEXTPD,TEMPDATE      
          MOVE      TEMPDATE,KEY6
          MOVE      ZERO,TEMPTOTL
          CALL      WROPTP2
.
          GOTO      SET1000
.
SET9999   PACK      KEY6,NEXTPD
          CALL      RDSDRGA1
          RETURN
.
.*************************************************************************
.*                           TOTL0000                                    *
.*                   Print the totals for clinic                         *
.*                     called by : FINI2000                              *[
.*************************************************************************
TOTL0000  COMPARE   "55",CLNO               * new page?
          CALL      HEAD0000 IF NOT LESS
          MOVE      SP6,KEY6
          CALL      RSOPTP2                 * position at start of file
          PRINT     MINUS24,*27;
.
TOTL1000  CALL      RKOPTP2                 * read next record
          BRANCH    OVRCD,TOTL2000
.
          PRINT     MINUS6,SP1;             * print total minus signs
.
          GOTO      TOTL1000
.
TOTL2000  PRINT     MINUS6,*N:
                    "Total Number of Cases",*26;
          MOVE      SP6,KEY6
          CALL      RSOPTP2
.
TOTL3000  CALL      RKOPTP2
          BRANCH    OVRCD,TOTL4000
.
          PRINT     TEMPTOTL;               * print totals
          ADD       TEMPTOTL,TOTLCANC       * add to total of totals
.
          GOTO      TOTL3000
.
TOTL4000  PRINT     TOTLCANC,*N,MINUS24,*27;
          SUB       TOTLCANC,TOTLCANC
.
TOTL5000  MOVE      SP6,KEY6
          CALL      RSOPTP2
.
TOTL6000  CALL      RKOPTP2
          BRANCH    OVRCD,TOTL9000
.
          PRINT     MINUS6,SP1;             * print minus signs again
.
          GOTO      TOTL6000
.
TOTL9000  PRINT     MINUS6
.
TOTL9999  RETURN 
.
.*************************************************************************
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       OPRWARIO/INC
          INC       OPRCLIIO/INC
          INC       PATCOMN3
          INC       PATDRGIO/INC
          INC       PATWR1IO/INC
          INC       PATCODIO/INC
................................................................................

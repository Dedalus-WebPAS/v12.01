.*****************************************************************************
.* System    :   Patient Management System                                   
.* Program   :   FX874693                                                    
.* Desc      :   Add missing outbokcf record
.----------------------------------------------------------------------------
. Date      :   02/07/2019                                                  
. Author    :   Thanh T.                                                    
. Function  :   This program will loop through alllnkaf table and checking 
.               for the existence of outbokcf record with the same outpatient
.               number. If it does not exist, insert a new outbokcf record
.               with outpatient number and referral date. Otherwise, if the
.               referral date is blank, update it with the referral date.
.----------------------------------------------------------------------------
. V10.14.00 :   02/07/2019  Thanh T.           TSK 0874693
.----------------------------------------------------------------------------
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
          INC       ALLLNKFD/INC
          INC       OUTSITFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBOCFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CNTREAD   FORM      8
CNTADD    FORM      8
CNTUPD    FORM      8
CNTERR    FORM      8
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
SAVESITE  DIM       6
.
PRGID     INIT      "FX874693"
PRGNAM    INIT      "Add missing outbokcf record"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening "
          DISPLAY   *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"
.
          DISPLAY   *P64:24,"alllnkaf";
          OPEN      ALLLNKA1,"alllnkaf" 
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          MOVE      ZERO,CNTREAD
          MOVE      ZERO,CNTADD
          MOVE      ZERO,CNTUPD            
          MOVE      ZERO,CNTERR            
          MOVE      SP70,SAVESITE
.
INIT9999  RETURN
+
.*****************************************************************************
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION  1 = Run Fixit (Report Only)                         *
.*                       2 = Run Fixit                                       *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (Report Only)":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit" 
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run fixit (report only)
                            OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.* Loop through alllnkaf and checking for the existence of outbokaf record   *
.* with the same outpatient number. If it does not exist, insert a new       *
.* outbokcf record with referral number, outpatient number and referral date.*
.* Otherwise, if the referral date is blank, update it with the referral     *
.* date.                                                                     *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,CNTREAD                 * initialise record counter
.
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      HEAD0000                     * print report header
.
          MOVE      SP70,KEY16
PROC0500  CALL      RSALLNK1                     * position at start of file
PROC1000  CALL      RKALLNK1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,ALLNVISN
          ADD       ONE,CNTREAD
.
          MATCH     SP70,ALLNVISN
          GOTO      PROC1000 IF EQUAL
.
          MATCH     SP70,ALLNLNKV
          GOTO      PROC1000 IF EQUAL
.
          MATCH     SAVESITE,ALLNSITE
          GOTO      PROC2000 IF EQUAL
.
          CALL      OUTFIL00
          IF        EXIT=1
            CALL      PRTERR00
            GOTO      PROC1000
          ENDIF
.
PROC2000  MOVE      ALLNLNKV,KEY8
          CALL      RDOTBOC1
          IF        OVRCD=1
            CALL      ADDREC00
          ELSE
            MATCH     SP70,OTBCRDAT
            IF        @EQUAL 
              CALL      UPDREC00
            ENDIF
          ENDIF
.
          GOTO      PROC1000                     * get next record
.
PROC9000  CALL      LINE0000
.
          IF        COPTION = 1
            PRINT     *N,CNTREAD,*HOFF," alllnkaf records read. "
            PRINT     *N,CNTERR,*HOFF," alllnkaf records in error. "
            PRINT     *N,CNTADD,*HOFF," outbokcf records will be added. "
            PRINT     *N,CNTUPD,*HOFF," outbokcf records will be updated. "
          ELSE
            PRINT     *N,CNTREAD,*HOFF," alllnkaf records read. "
            PRINT     *N,CNTERR,*HOFF," alllnkaf records in error. "
            PRINT     *N,CNTADD,*HOFF," outbokcf records added. "
            PRINT     *N,CNTUPD,*HOFF," outbokcf records updated. "
          ENDIF
          PRINT     *N,*1,"*** End of Report ***"
.
          DISPLAY   *P1:13,*EF,"Fixit completed."
          IF        COPTION = 1
            DISPLAY   *P1:15,*V2LON,CNTREAD,*HOFF," alllnkaf records read. "
            DISPLAY   *P1:17,*V2LON,CNTERR,*HOFF:
                      " alllnkaf records in error. "
	    DISPLAY   *P1:19,*V2LON,CNTADD,*HOFF:
                      " outbokcf records will be added. "
            DISPLAY   *P1:21,*V2LON,CNTUPD,*HOFF:
                      " outbokcf records will be updated. "
          ELSE
            DISPLAY   *P1:15,*V2LON,CNTREAD,*HOFF," alllnkaf records read. "
            DISPLAY   *P1:17,*V2LON,CNTERR,*HOFF:
                      " alllnkaf records in error. "
	    DISPLAY   *P1:19,*V2LON,CNTADD,*HOFF:
                      " outbokcf records added. "
            DISPLAY   *P1:21,*V2LON,CNTUPD,*HOFF:
                      " outbokcf records updated. "
          ENDIF
          CALL      HITENTER
.
PROC9999  RETURN
.
.*****************************************************************************
. Process adding new record                                                  *
.*****************************************************************************
ADDREC00  PACK      KEY8,ALLNVISN,SP70      * Patient Referral Details
          CALL      RDALREF1
          BRANCH    OVRCD,ADDREC99
.
          ADD       ONE,CNTADD
          CALL      DISPAD00  
.
          IF        COPTION = 2
            MOVE      ALLNLNKV,DOBAOUTN 
            MOVE      ALLNLNKV,KEY8
            MOVE      ALRERDAT,OTBCRDAT
            MOVE      SP70,OTBCSPAR
            CALL      WROTBOC1
          ENDIF
.
ADDREC99  RETURN
.
.*****************************************************************************
. Process updating new record                                                *
.*****************************************************************************
UPDREC00  PACK      KEY8,ALLNVISN,SP70      * Patient Referral Details
          CALL      RDALREF1
          BRANCH    OVRCD,UPDREC99
.
          MATCH     OTBCRDAT,ALRERDAT
          GOTO      UPDREC99 IF EQUAL 
.
          ADD       ONE,CNTUPD
          CALL      DISPUP00  
. 
          IF        COPTION = 2
            MOVE      ALRERDAT,OTBCRDAT
            MOVE      SP70,OTBCSPAR
            CALL      UPOTBOC1
          ENDIF
.
UPDREC99  RETURN
.
.******************************************************************************
.                     Check Correct Files are Open for System                 *
.******************************************************************************
OUTFIL00  MOVE      ALLNSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,OUTFIL90
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open Outpatient Files
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OUTFIL99
.
OUTFIL90  MOVE      ONE,EXIT
.
OUTFIL99  RETURN
.
.****************************************************************************
.                    Open Outpateint Files                                  *
.****************************************************************************
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBOKC1  * Get the name of the BOKC1 file
          CLOSE     OUTBOKC1
          OPEN      OUTBOKC1,CFNAME           * Open the file
.
OPNOUT99  RETURN
+
.****************************************************************************
.* Print a line with add details                                            *
.****************************************************************************
PRTERR00  COMPARE   FIFTY5,CLNO                  * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          PRINT     *1,PIPE,*3,ALLNVISN,*19,PIPE:
                    *21,ALLNLNKV,*36,PIPE:
                    *38,"",*52,PIPE:
                    *54,"Error",*62,PIPE:
                    *64,"Outpatient Site Invalid":
                    *132,PIPE  
.
          ADD       ONE,CLNO                     * increment line count
          ADD       ONE,CNTERR
.
PRTERR99  RETURN
+
.****************************************************************************
.* Print a line with add details                                            *
.****************************************************************************
DISPAD00  COMPARE   FIFTY5,CLNO                  * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          PRINT     *1,PIPE,*3,ALLNVISN,*19,PIPE:
                    *21,ALLNLNKV,*36,PIPE:
                    *38,ALRERDAT,*52,PIPE:    
                    *54,"Added",*62,PIPE:
                    *132,PIPE  
.
          ADD       ONE,CLNO                     * increment line count
.
DISPAD99  RETURN
+
.****************************************************************************
.* Print a line with update details                                         *
.****************************************************************************
DISPUP00  COMPARE   FIFTY5,CLNO                  * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          PRINT     *1,PIPE,*3,ALLNVISN,*19,PIPE:
                    *21,ALLNLNKV,*36,PIPE:
                    *38,ALRERDAT,*52,PIPE:    
                    *54,"Updated",*62,PIPE:
                    *132,PIPE  
.
          ADD       ONE,CLNO                     * increment line count
.
DISPUP99  RETURN
+
.****************************************************************************
.*                       Print page heading                                 *
.****************************************************************************
HEAD0000  CALL      HEAD132                      * display page header
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Referral Number",*19,PIPE:
                    *21,"Booking Number",*36,PIPE:
                    *38,"Referral Date",*52,PIPE:
                    *54,"Action ",*62,PIPE,*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SIX,CLNO                     * increment line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                      Draw line across page                               *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLREFIO/INC
          INC       ALLLNKIO/INC
          INC       OUTBOCIO/INC
          INC       OUTSITIO/INC

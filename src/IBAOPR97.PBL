.******************************************************************************
.* System         :  Theatre                                                  *
.* Program        :  IBAOPR97                                                 *
.* Description    :  Implants Upload                                          *
.******************************************************************************
.* Date           :  17/01/2017                                               *
.* Author         :  Peter Vela                                               *
.* Function       :  Upload Implany/Consumable Codes                          *
.* Modifications  :                                                           *
.*                   V11.04.02  20/02/2024  Alina Bhari    TSK 0942349        *
.*                              Checks if multi-hopital and based on          *
.*                              logged in user info,the default claim code is *
.*                              read from the hopital detail file. And is     *
.*                              validated againts the Misc charge code        *
.*                              - VFLD0000, VFLD0100                          *
.*                   V11.04.01  29/01/2024  Alina Bhari    TSK 0942349        *
.*                              Allowed miscellaneous item field to set to    *
.*                              blank in the upload spreadsheet -VFLD0000     *
.*                   V10.10.04  14/03/2017  Peter Vela     TSK 0829089        *
.*                              Added new validations in VFLD0000             *
.*                   V10.10.03  10/03/2017  Peter Vela     TSK 0829089        *
.*                              Added barcode validation in VFLD0000          *
.*                   V10.10.02  10/03/2017  Peter Vela     TSK 0829089        *
.*                              Added VALDPASS functionality                  *
.*                              Added validations for Category OW             *
.*                              Added validations for Misc Charge             *
.*                   V10.10.01  02/03/2017  Peter Vela     TSK 0829089        *
.*                              Added Lot/Serial Number                       *
.*                              Changed index to oprthiaf                     *
.*                   V10.09.01  17/02/2017  Peter Vela     TSK 0829089        *
.*                              Created program                               *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCONFD/INC
          INC       PATCODFD/INC            * Codes file
          INC       PATMCHFD/INC      * Misc Charge File
          INC       OPRCONFD/INC
          INC       OPRTHIFD/INC
          INC       PATHSPFD/INC      * Hospital Detail
          INC       WEBSECFD/INC      * Web Security / Web User
.
.         Implant/Consumable Codes Upload File
.
IMPLNTXT  FILE       HL7, FIXED=1000        * Upload file for journals
.
.Name     Type     Size     Start
.----     ----     ----     -----
XICODE    DIM        15     Implant Code/EAN No.
XIDESC    DIM        60     Description    
XIMCHG    DIM         9     Miscellaneous Charge Code
XITYIM    DIM         3     Type of Implant (Category OW)
XIMANU    DIM        60     Manufacturer
XISIZE    DIM        10     Size
XISUPP    DIM        30     Supplied Name
XICOST    DIM        15     Cost
XICOPC    DIM        10     Commonwealth Prosthetic Code
XISTAT    DIM         1     Status  1=Inactive
XIREGI    DIM         1     Prompt For Joint Registry Info
.                                         Blank or 0 = No
.                                         1 = Yes
XIBCOD    DIM        60     Barcode
.XIAIMP    DIM         1     Ad-hoc Implant
.                           Blank or 0 = No
.                                    1 = Yes
XILOTN    DIM        20     Lot/Serial Number
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      8
BATCHNO   FORM      8
CRETURN   INIT      015                * carriage return character
CMDLINE   DIM       100
CODEJ     INIT      "J "
DIM10     DIM       10
DIM15     DIM       15
DIM55     DIM       55
DIM60     DIM       60
ERRCOUNT  FORM      8
ERRFLAG   FORM      1
ERRORMSG  DIM       50
FORM8     FORM      8
FNAMEI    DIM       150
FNAMEB    DIM       8
FGSTFLAG  FORM      1
FORM12P2  FORM      12.2
GSTAMT    FORM      12.2
HEADSEC   FORM      5
IBCNMHOS  FORM      1
JOURAMTT  FORM      12.2
JOURDATE  DIM        8
JTYPE     DIM        1
LINECNTR  FORM      8
MAXGSTJ   FORM      12.2
USERID    DIM       10
PATHNAME  DIM       100
PNAME     DIM       25
SEC1      FORM      "00001"
TIMEIS    DIM       8
TOTAMNT   FORM      12.2
UPDCOUNT  FORM      8
VALDPASS  FORM      1
.
XFLD0001  DIM       70
XFLD0002  DIM       70
XFLD0003  DIM       70
XFLD0004  DIM       70
XFLD0005  DIM       70
XFLD0006  DIM       70
XFLD0007  DIM       70
XFLD0008  DIM       70
XFLD0009  DIM       70
XFLD0010  DIM       70
XFLD0011  DIM       70
XFLD0012  DIM       70
.XFLD0013  DIM       70
XFLD0014  DIM       70
.
IMPEAMSG  INIT      "Implant Code/EAN No. is Mandatory. "
IMPELMSG  INIT      "Implant Code/EAN No. max. Length is 15. "
IMPMEMSG  INIT      "Type of Implant is Mandatory for Item: "
IMPERMSG  INIT      "Invalid Type of Implant: Code - "
IMDEMMSG  INIT      "Implant Description is Mandatory. "
MISCHGCD  INIT      "Invalid Misc. Charge: "
BARCODER  INIT      "Barcode already exists for Item: "
BARCODEM  INIT      "Barcode is Mandatory  for Item: "
BARCODEL  INIT      "Barcode max. length is 60. Item: "
.
PRGID     INIT      "IBAOPR97"
PRGNAM    INIT      "Upload Implant/Consumable Codes"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000              * Initailize variables & open files
          CALL      KOPT0000              * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          CALL      KASC0000              * Keyin ascii file
          BRANCH    EXIT,ML9900
.
          CALL      KUSR0000              * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000              * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      PRHD0000              * Print header
.
          CALL      CTOT0000              * Clear total vars
.
          MOVE      ONE,VALDPASS            * Validation Pass Set to Yes
          CALL      POST0000              * Updating file
.
          IF        ERRCOUNT=0
            MOVE      ZERO,VALDPASS         * If no errors proceed
            CALL      POST0000              * with Updating file
          ENDIF
.
          CALL      PTOT0000                * Print report totals
.
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"oprthiaf"
          OPEN      OPRTHIA1,"oprthiaf"
          OPEN      OPRTHIA3,"oprthiaf"
.
          DISPLAY   *P64:24,"patmchgf"
          OPEN      PATMCHG1,"patmchgf"
.
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,HUND13;*48,OPCNSIVB
.
          OPEN      PATHSPA1,"pathspaf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          OPEN      WEBSECA1,"websecaf"
.
          MOVE      ZERO,ERRFLAG
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Upload Journals                                 *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                   *P1:6,*V2LON,"0",*HOFF," = Exit":
                   *P1:7,*V2LON,"1",*HOFF," = Upload Implant/Consumable Codes":
                   *P1:10,"Select Option : ";
.
KOPT1000  KEYIN     *P17:10,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"Implant/Consumable Codes Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      IMPLNTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
.
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P1:16,*EL,"Keyin Username: ",*P26:16,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P1:17,*EL,"Keyin Pathname: ",*P26:17,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.*                        Post record                                         *
.******************************************************************************
POST0000  MOVE      ZERO,LINECNTR
.
          OPEN      IMPLNTXT,FNAMEI
POST1000  READ      IMPLNTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011,XFLD0012,XFLD0014
          GOTO      POST9000 IF OVER
.
          ADD       ONE,LINECNTR            * Text file line counter for error
.
          PACK      XICODE,XFLD0001,SP70
          PACK      XIDESC,XFLD0002,SP70
          PACK      XIMCHG,XFLD0003,SP70
          PACK      XITYIM,XFLD0004,SP70
          PACK      XIMANU,XFLD0005,SP70
          PACK      XISIZE,XFLD0006,SP70
          PACK      XISUPP,XFLD0007,SP70
.
          MOVE      ZERO,FORM12P2 
          PACK      KEY15,XFLD0008
          SQUEEZE   KEY15
          MOVE      KEY15,FORM12P2
          MOVE      FORM12P2,XICOST
.
          PACK      XICOPC,XFLD0009,SP70
          PACK      XISTAT,XFLD0010,SP70
          PACK      XIREGI,XFLD0011,SP70
          PACK      XIBCOD,XFLD0012,SP70
.          PACK      XIAIMP,XFLD0013,SP70 
          PACK      XILOTN,XFLD0014,SP70
.
          IF        VALDPASS=1
            CALL      VFLD0000                * Validation pass so
            BRANCH    EXIT,POST1000           * validate fields
          ENDIF
.
          CALL      UIMP0000              * Update Implant/Consumable Record
.
          GOTO      POST1000
.
POST9000  CLOSE     IMPLNTXT
.
POST9999  RETURN
+
.----------------------------------------------------------------------
. Subroutine to post implant/consumable record 
.----------------------------------------------------------------------
UIMP0000  CALL      CLRTHI00
.
          MOVE      XICODE,OPTICODE
          MOVE      XIDESC,OPTIDESC
          MOVE      XIMCHG,OPTIMCHG
          MOVE      XITYIM,OPTITYIM
          MOVE      XIMANU,OPTIMANU
          MOVE      XISIZE,OPTISIZE
          MOVE      XISUPP,OPTISUPP
          MOVE      XICOST,OPTICOST
          MOVE      XICOPC,OPTICOPC
          MOVE      XISTAT,OPTISTAT
          MOVE      XIREGI,OPTIREGI
          MOVE      XIBCOD,OPTIBCOD
.          MOVE      XIAIMP,OPTIAIMP
          MOVE      XILOTN,OPTILOTN
          MOVE      SP70,OPTISPAR
.
          PACK      KEY75,OPTICODE,OPTIBCOD,SP70
          CALL      RAOPTHI1
          IF        OVRCD=0
            IF        VALDPASS=0
              ADD       ONE,UPDCOUNT        * Total records updated
              CALL      UPOPTHI1
            ENDIF
          ELSE
            IF        VALDPASS=0
              ADD       ONE,ADDCOUNT        * Total records added
              CALL      WROPTHI1
            ENDIF
          ENDIF
.
UIMP9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Date               : ",CDATE:
                    *N,"Time               : ",CTIMEIS:
                    *N,"User ID            : ",USERID:
                    *N,"Implant/Consumable  Load file : ",PATHNAME
.
          CALL      UND132
.
          PRINT     *1,"| Line",*12,"| Error ",*70,"| Record Detail":
                    *132,"|"
.
          CALL      UND132
.
PRHD9999  RETURN
+
.******************************************************************************
.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaopr97.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
.         Clear OPRTHIFD variables               
.******************************************************************************
CLRTHI00  MOVE      SP70,OPTICODE
          MOVE      SP70,OPTIDESC
          MOVE      SP70,OPTIMCHG
          MOVE      SP70,OPTITYIM
          MOVE      SP70,OPTIMANU
          MOVE      SP70,OPTISIZE
          MOVE      SP70,OPTISUPP
          MOVE      ZERO,OPTICOST
          MOVE      SP70,OPTICOPC
          MOVE      SP70,OPTISTAT
          MOVE      SP70,OPTIREGI
          MOVE      SP70,OPTIBCOD
          MOVE      SP70,OPTIAIMP
          MOVE      SP70,OPTILOTN
          MOVE      SP70,OPTISPAR
.
CLRTHI99  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ADDCOUNT
          MOVE      ZERO,UPDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
. Print total line
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
          PRINT     *1,"| Total Updated : ",UPDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
+
.******************************************************************************
. Validate bed fee input text file fields
.******************************************************************************
VFLD0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL | @EOS
            PACK      ERRORMSG,IMPEAMSG,SP70
            CALL      PERR0000
            MOVE      ONE,EXIT
          ELSE
            MOVE        SP70,DIM15
            MOVE        SP70,DIM55 
            UNPACK      XFLD0001,DIM15,DIM55
            MATCH       SP70,DIM55
            IF          !@EQUAL & !@EOS
              PACK      ERRORMSG,IMPELMSG,SP70
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF          
.
          MATCH     SP70,XFLD0002
          IF        @EQUAL | @EOS
            PACK      ERRORMSG,IMDEMMSG,SP70
            CALL      PERR0000
            MOVE      ONE,EXIT 
          ENDIF
.
          MATCH     SP70,XFLD0004
          IF        !@EQUAL
            PACK      KEY5,ANSO,ANSW,XITYIM,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              PACK      ERRORMSG,IMPERMSG,XITYIM,SP70
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ELSE
            PACK      ERRORMSG,IMPMEMSG,XICODE,SP70
            CALL      PERR0000
            MOVE      ONE,EXIT 
          ENDIF
.
          MATCH     SP70,XFLD0003
          IF        !@EQUAL & !@EOS
.         Checks if multi-hopital and based on logged in user info,
.         the default claim code is read from the hospital detail file.
.         And is validated againts the Misc charge code
            IF        IBCNMHOS=1
              PACK      KEY10,USERID,SP70             
              CALL      RDWBSE1
              BRANCH    OVRCD,VFLD0100
. 
              PACK      KEY3,WBSEHOSP,SP70
              CALL      RDPTHSP1
              BRANCH    OVRCD,VFLD0100
. 
              PACK      KEY29,PTHSDCLM,SP6,SP3,XIMCHG,SP70        
              CALL      RDSMCHG1
              CALL      RDKMCHG1
              BRANCH    OVRCD,VFLD1000
.
              MATCH     PTHSDCLM,MCLMCOD
              GOTO      VFLD1000 IF NOT EQUAL
.
              MATCH     SP70,MHFUND
              GOTO      VFLD1000 IF NOT EQUAL
.
              MATCH     SP70,PTMCHFTT
              GOTO      VFLD1000 IF NOT EQUAL
.
              MATCH     XIMCHG,MCHARGE
              GOTO      VFLD1000 IF NOT EQUAL
.
              GOTO      VFLD2000
.
            ELSE
              GOTO      VFLD0100
            ENDIF
            
.
VFLD0100    PACK      KEY29,PTCNDCLM,SP6,SP3,XIMCHG,SP70 
            CALL      RDSMCHG1
            CALL      RDKMCHG1
            BRANCH    OVRCD,VFLD1000
.
            MATCH     PTCNDCLM,MCLMCOD
            GOTO      VFLD1000 IF NOT EQUAL
.
            MATCH     SP70,MHFUND
            GOTO      VFLD1000 IF NOT EQUAL
.
            MATCH     SP70,PTMCHFTT
            GOTO      VFLD1000 IF NOT EQUAL
. 
            MATCH     XIMCHG,MCHARGE
            GOTO      VFLD1000 IF NOT EQUAL
.
            GOTO      VFLD2000
.
VFLD1000    PACK      ERRORMSG,MISCHGCD,PTCNDCLM,SP2,XIMCHG,SP70
            CALL      PERR0000
            MOVE      ONE,EXIT
          ENDIF
.
VFLD2000  MATCH     SP70,XFLD0012
          IF        !@EQUAL
.
.            MATCH     SP70,XFLD0001
.            GOTO      VFLD3000 IF EQUAL
.            GOTO      VFLD3000 IF EOS
.
.            PACK      KEY75,XIBCOD,SP70
.            CALL      RSOPTHI3
.VFLD2100    CALL      RKOPTHI3
.            BRANCH    OVRCD,VFLD3000
.         
.            MATCH     XIBCOD,OPTIBCOD
.            GOTO      VFLD3000 IF NOT EQUAL
.
.            MATCH     XICODE,OPTICODE  
.            GOTO      VFLD2100 IF EQUAL
.
.            PACK      ERRORMSG,BARCODER,XICODE,SP70
.            CALL      PERR0000
.            MOVE      ONE,EXIT
          ELSE
            MATCH     "1",OPCNSIVB
            IF        @EQUAL
              PACK      ERRORMSG,BARCODEM,XICODE,SP70
              CALL      PERR0000
              MOVE      ONE,EXIT 
            ENDIF
          ENDIF          
.
VFLD3000  MATCH     "1",OPCNSIVB
          IF        @EQUAL
            MATCH     SP70,XFLD0012
            IF        !@EQUAL
              MOVE      SP70,DIM60
              MOVE      SP70,DIM10
              UNPACK    XFLD0012,DIM60,DIM10
              MATCH     SP70,DIM10
              IF        !@EQUAL & !@EOS
                PACK      ERRORMSG,BARCODEL,XICODE,SP70
                CALL      PERR0000
                MOVE      ONE,EXIT
              ENDIF
            ENDIF
          ENDIF
.
VFLD9999  RETURN
+
.******************************************************************************
. Print error line
.******************************************************************************
PERR0000  ADD       ONE,ERRCOUNT            * Total error count
.
          UNPACK    XFLD0001,KEY15
          UNPACK    XFLD0003,KEY9
          UNPACK    XFLD0004,KEY3
.
          PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*70,"|":
                    KEY15,SP2,KEY9,SP2,KEY3,SP2;
.
          PRINT     *132,"|"
.
PERR9999  RETURN
+
.******************************************************************************
          INC       STD001IO
.
          INC       PATCODIO/INC            * Codes file
          INC       PATMCHIO/INC            * Misc Charge File
          INC       OPRTHIIO/INC
          INC       PATHSPIO/INC            * Hospital Detail
          INC       WEBSECIO/INC            * Web Security / Web Users
.
.
.
.

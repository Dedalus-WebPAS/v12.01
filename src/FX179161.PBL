.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   FX179169                                                    *
.* Desc      :   Fixit for moving PMPXMSNO (Medicare safety net number)      *
.*               into the concession card file.                              *
.*****************************************************************************
.* Date      :   14/11/2008                                                  *
.* Author    :   Jill Habasque  CAR 179161                                   *
.* Function  :   This program will loop through pmspx2af and for every       *
.*               record with a safety net number (non-blank pmpxmsno), a     *
.*               record will be written to the health concession card table  *
.*               (pmsccdaf), if one doesn't already exist.                   *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSPX2FD/INC
          INC       PMSCCDFD/INC
          INC       PATCODFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CODE      DIM       2
COUNTER   FORM      8
.
DIM4      DIM       4
EXPRDATE  DIM       8
.
FMON      FORM      2
FYEAR     FORM      4
.
.
. PROGRAM CONSTANTS
. -----------------
.
CATLOWCT  INIT      "ct"
.
.
PRGID     INIT      "FX179161"
PRGNAM    INIT      "Fixit for Medicare Safety Net Number"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      KCOD0000               * get concession card type code
          BRANCH    EXIT,MAIN0100          * nothing/exitchar entered
          CALL      KEXP0000               * get the expiry date to be created
          BRANCH    EXIT,MAIN0100          * nothing/exitchar entered
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsccdaf";
          OPEN      PMSCCDA1,"pmsccdaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               KCOD0000              Called by: MAIN0000   *
.*         Get the user to select the category "ct" code to be used          *
.*         for pension number.                                               *
.* Returns:  EXIT  0 = Valid code entered                                    *
.*                 1 = Nothing entered or exitchar entered                   *
.*****************************************************************************
.
KCOD0000  DISPLAY   *P1:10,*EF,"Concession Card Type code:"
.
          MOVE      TWENTY8,ECOL                 * set keyin column
          MOVE      TEN,EVERT                    * set keyin row
          MOVE      CATLOWCT,CODE                * set category "ct"
          MOVE      SP3,CKYIDEF3                 * set blank default
          MOVE      ZERO,CKYIMAND                * set non-mandatory
.
          CALL      PATCODKY                     * get code for safety net no
          BRANCH    EXIT,KCOD9100:               * nothing entered
                         KCOD9100                * exitchar input
.
.         A valid code has been input, so check that it has indicator 1
.         set to "2" (saftey net).
.
          MATCH     "2",TCINDC1                  * pension card code ?
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Concession card type is not for a ":
                      "safety net type.";
            CALL      HITENTER
            GOTO      KCOD0000
          ENDIF
.
          DISPLAY   *P40:10,TDESC                * display description
          MOVE      ZERO,EXIT
          GOTO      KCOD9999
.
KCOD9100  MOVE      ONE,EXIT
.
KCOD9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through the master ext 2   file (pmspx2af) and for every      *
.*        record where PMPXMSNOis NOT blank, write a record to the health    *
.*        concession card table (pmsccdaf) using the selected concession     *
.*        card type.                                                         *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,COUNTER                 * initialise counter
.
          MOVE      SP8,KEY8
          CALL      RSPMPX21                     * position at start of file
PROC0500  CALL      RKPMPX21                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          MATCH     SP10,PMPXMSNO                * blank saftety net number?
          GOTO      PROC0500 IF EQUAL            * yes - ignore record
.
.         This record has a value in pension number, so check if this patient
.         has a pmsccdaf record for the selected concession card type
.
          PACK      KEY19,PMPXURNO,SP20
          CALL      RSPMCCD1                     * position on U/R
PROC1000  CALL      RKPMCCD1                     * read next record
          BRANCH    OVRCD,PROC5000               * eof - no existing record
.
          MATCH     PMPXURNO,PMCDURNO            * same U/R still ?
          GOTO      PROC5000 IF NOT EQUAL        * no - no existing record
.
          MATCH     ACODE,PMCDCTYP               * pension card type ?
          GOTO      PROC1000 IF NOT EQUAL        * no - get next record  
          MATCH     PMPXMSNO,PMCDCNUM
          GOTO      PROC0500 IF EQUAL            * no - get next record
.
.         This patient doesn't have an existing concession card type for
.         pension number, so create a health concession card record in pmsccdaf
.
PROC5000  MOVE      PMPXURNO,PMCDURNO            * load U/R number
          MOVE      ACODE,PMCDCTYP               * load card type (cat ct)
          MOVE      PMPXMSNO,PMCDCNUM            * concession card number
          MOVE      EXPRDATE,PMCDEXDT            * load expiry date
          CALL      IBACLOCK                     * get current date/time
          PACK      PMCDCDAT,CCC,CYY,CMM,CDD     * load create date
          REP       " 0",PMCDCDAT
          MOVE      CTIMEIS,PMCDCTIM             * load create time
          REP       " 0",PMCDCTIM
          MOVE      "FX179161  ",PMCDCUID        * load create operator id
          MOVE      SP8,PMCDUDAT                 * load update date
          MOVE      SP8,PMCDUTIM                 * load update time
          MOVE      SP10,PMCDUUID                * load update operator id
          MOVE      SP30,PMCDSPAR
.
          CALL      WRPMCCD1                     * write new record
          ADD       ONE,COUNTER                  * increment counter
.
.         Now clear the PMPXMSNO field on the pmspx2af record
.
          MOVE      SP20,PMPXMSNO
          CALL      UPPMPX21                     * update pmspx2af record
.
          GOTO      PROC0500                     * get next pmi record
.
.         Processing completed
.
PROC9000  DISPLAY   *P1:24,*EL,*V2LON,COUNTER,*HOFF," records created.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.********************************************************************
.*                          KEXP0000                                *
.*  Keyin the medicare safety net expiry date                       *
.********************************************************************
KEXP0000  MOVE      SP70,EXPRDATE
          DISPLAY   *P1:11,"Expiry Date :";
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          MOVE      "28",CCOL
          MOVE      "11",CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,KEXP9000
.
          PACK      EXPRDATE,CCENT,CYEAR,CMON,CDAY,SP70
          REP       " 0",EXPRDATE
          MOVE      ZERO,EXIT
          GOTO      KEXP9999
.
KEXP9000  MOVE      ONE,EXIT
KEXP9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCODKY
.
          INC       PMSPX2IO/INC
          INC       PMSCCDIO/INC
          INC       PATCODIO/INC

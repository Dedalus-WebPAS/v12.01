.*******************************************************************************
.*                                                                             *
.*   Program        :    IBAPMS34                                              *
.*                                                                             *
.*   Description    :    Patient Invoice message register                      *
.*                                                                             *
.*   Author         :    Peter Eddey                                           *
.*                                                                             *
.*   Date           :    07/01/91                                              *
.*                                                                             *
.*   Modifications  :    RAD DUDE WAS HERE !                                   *
.*                                                                             *
.*                  V9.02.01 24/03/2003 Dean Cramer   CAR 35618                *
.*                           Ported from 6.10 and altered for 9.02             *
.*                  V5.01    27/03/1991 Peter Eddey                            *
.*                            fixed up my errors                               *
.*                                                                             *
.*******************************************************************************
.
          INC       STD001FD
          INC       PATINMFD/INC
          INC       PATCODFD/INC
.
PRGID     INIT      "IBAPMS34"
PRGNAM    INIT      "PATIENT INVOICE MESSAGE REGISTER"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                       Variables Required                          *
.*********************************************************************
.
CODE      DIM       2
DIM20     DIM       20
DISPFLAG  FORM      1
ECLAIM    DIM       3
ECLAIMDS  DIM       20
ENDSYST   FORM      1
ESYSTEM   FORM      1
FINISH    INIT      "Finish"
SCLAIM    DIM       3
SCLAIMDS  DIM       20
SSYSTEM   FORM      1
START     INIT      "Start "
STRTSYST  FORM      1
SYSDESC1  DIM       20
SYSDESC2  DIM       20

.
.*********************************************************************
.*                         ML0000                                    *
.*                    Mainline of Program                            *
.*********************************************************************
.
ML0000
          CALL      INIT0000             * initialisation routine
.
ML0100    
          MOVE      ZERO,DISPFLAG
          CALL      MENU0000             * handle the menu
          BRANCH    EXIT,ML9999
.
ML1000   
          MOVE      ONE,DISPFLAG
          CALL      CVAR0000             * clear the variables
.
          CALL      SCRN0000             * display the screen
.
          CALL      KFSY0000             * starting system
          BRANCH    EXIT,ML0100
.
          CALL      KESY0000             * ending system
          BRANCH    EXIT,ML1000
.
          CALL      KSCC0000             * Starting claim code
ML2000    CALL      KECC0000             * ending claim code
.
          CALL      VLCC0000             * validate start code <= end 
          BRANCH    EXIT,ML2000            code
.
          CALL      CONTQST              * ok to continue ?
          BRANCH    CEXIT,ML5000,ML1000,ML0100
.
ML5000
          CALL       PRPT0000            * print the report 
          GOTO       ML0100
.
ML9999    CHAIN     PGM
.
.*********************************************************************
.*                         INIT0000                                  *
.*                    Initialisation Section                         *
.*********************************************************************
.
INIT0000
          CALL      DISPHEAD
          MOVE      "CL",CODE
          MOVE      ONE,CNEWDS
          MOVE      ONE,CNOUNDLN
.
          DISPLAY   *P50:24,"Opening "
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P58:24,"patinmaf"
          OPEN      PATINMA1,"patinmaf"
.
INIT9999  RETURN
.
.*********************************************************************
.*                         MENU0000                                  *
.*                 Handle the program menu                           *
.*********************************************************************
.
MENU0000 
          MOVE      ZERO,EXIT
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print Register ":
                    *P1:7,"Select Option :"
. 
          BRANCH    DISPFLAG,MENU9999    * for redisplays
.
MENU1000
          KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION:
                    *P17:7,*DV,COPTION
.
          COMPARE   ZERO,COPTION         * test for exit
          GOTO      MENU9000 IF EQUAL
.
          BRANCH    COPTION,MENU9999     *branch on valid entry
          BEEP                            else invalid entry
          GOTO      MENU1000
MENU9000
          MOVE      ONE,EXIT             * exit was selected
.
MENU9999  RETURN
                   
.
.*********************************************************************
.*                         SCRN0000                                  *
.*                   Display the screen                              *
.*********************************************************************
.
SCRN0000
          DISPLAY   *P1:8,*EF,*P1:9,"Starting System      :":
                    *P1:11,"Ending System        :":
                    *P1:14,"Starting Claim Code  :":
                    *P1:16,"Ending Claim Code    :"
SCRN9999  RETURN
.
.********************************************************************
.*                         KFSY0000                                 *
.*               Keyin the system required                          *
.********************************************************************
.
KFSY0000
          CALL      DSOP0000             * display valid systems
          MOVE      ZERO,EXIT
          MOVE      ZERO,STRTSYST
          CLEAR     SYSDESC1
.
KFSY1000  KEYIN     *P24:9,*EL,*DV,UNDLN1:
                    *P24:9,*V2LON,SSYSTEM:
                    *P24:9,*DV,SSYSTEM
.
          COMPARE   ZERO,SSYSTEM
          GOTO      KFSY9000 IF EQUAL
.
          BRANCH    SSYSTEM,KFSY2000,KFSY3000,KFSY4000,KFSY5000
          BEEP 
          GOTO      KFSY1000
.
KFSY2000
          MOVE      "Accident & Emergency",SYSDESC1
          DISPLAY   *P28:9,SYSDESC1
          GOTO      KFSY9999 
KFSY3000
          MOVE      "Outpatients",SYSDESC1
          DISPLAY   *P28:9,SYSDESC1
          GOTO      KFSY9999 
KFSY4000
          MOVE      "Inpatients",SYSDESC1
          DISPLAY   *P28:9,SYSDESC1
          GOTO      KFSY9999 
KFSY5000
          MOVE      "Other Financial",SYSDESC1
          DISPLAY   *P28:9,SYSDESC1
          GOTO      KFSY9999 
.
KFSY9000 
          DISPLAY   *P24:9,*V2LON,START
          MOVE      ONE,STRTSYST
.
KFSY9999  RETURN
.
.*********************************************************************
.*                         KSCC0000                                  *
.*                   Keyin the claim code                            *
.*********************************************************************
.
KSCC0000
          MOVE      ZERO,EXIT
.
KSCC1000
          KEYIN     *P24:14,*EL,*DV,UNDLN3:
                    *P24:14,*V2LON,SCLAIM:
                    *P24:14,*DV,SCLAIM 
.
          PACK      SCLAIM,SCLAIM,SP3
          MATCH     SP3,SCLAIM          * test if nothing entered
          GOTO      KSCC4500 IF EQUAL
.
          MATCH     QUEST,SCLAIM        * test for question mark
          GOTO      KSCC5000 IF EQUAL
.
          PACK      KEY5,CODE,SCLAIM
          CALL      RDCODE1              * validate claim code
          BRANCH    OVRCD,KSCC4000
.
          MOVE      TDESC,SCLAIMDS
          DISPLAY   *P28:14,SCLAIMDS
          GOTO      KSCC9999
.
KSCC4000
          BEEP
          DISPLAY   *P1:24,*EL,"Invalid Claim code. ";
          CALL      HITENTER
          GOTO      KSCC1000
KSCC4500
          DISPLAY   *P24:14,*V2LON,START
          MOVE      SP3,SCLAIM
          MOVE      SP20,SCLAIMDS
          GOTO      KSCC9999
.
KSCC5000
          CALL      PATCODDS             * question mark display
KSCC6000
          DISPLAY   *P1:24,*EL,"Claim Code :"
          KEYIN     *P14:24,*DV,UNDLN3:
                    *P14:24,*V2LON,SCLAIM:
                    *P14:24,*DV,SCLAIM 
.
          PACK      SCLAIM,SCLAIM,SP3
          MATCH     SP3,SCLAIM         * test if nothing entered
          GOTO      KSCC8500 IF EQUAL
.
          MATCH     QUEST,SCLAIM       * test for a ? mark
          GOTO      KSCC5000 IF EQUAL
.
          PACK      KEY5,CODE,SCLAIM
          CALL      RDCODE1             * validate the claim code
          BRANCH    OVRCD,KSCC8000
          MOVE      TDESC,SCLAIMDS
          GOTO      KSCC8500
.
KSCC8000
          BEEP
          DISPLAY   *P1:24,*EL,"Invalid Claim code. ";
          CALL      HITENTER
          GOTO      KSCC6000
.
.*** Redisplay routine - valid entry ***
KSCC8500
          CALL      MENU0000
          CALL      SCRN0000
          CALL      DDAT0000             * display the data  
.
KSCC9999  RETURN
.
.********************************************************************
.*                         KESY0000                                 *
.*               Keyin the system required                          *
.********************************************************************
.
KESY0000
          MOVE      ZERO,ENDSYST
          MOVE      ZERO,EXIT
          CLEAR     SYSDESC2
.
KESY1000  KEYIN     *P24:11,*EL,*DV,UNDLN1:
                    *P24:11,*V2LON,ESYSTEM:
                    *P24:11,*DV,ESYSTEM
.
          COMPARE   ZERO,ESYSTEM
          GOTO      KESY9000 IF EQUAL
.
          BRANCH    ESYSTEM,KESY2000,KESY3000,KESY4000,KESY5000
          BEEP 
          GOTO      KESY1000
.
KESY2000
          MOVE      "Accident & Emergency",SYSDESC2
          DISPLAY   *P28:11,SYSDESC2
          GOTO      KESY8000 
KESY3000
          MOVE      "Outpatients",SYSDESC2
          DISPLAY   *P28:11,SYSDESC2
          GOTO      KESY8000 
KESY4000
          MOVE      "Inpatients",SYSDESC2
          DISPLAY   *P28:11,SYSDESC2
          GOTO      KESY9999 
KESY5000
          MOVE      "Other Financial",SYSDESC2
          DISPLAY   *P28:11,SYSDESC2
          GOTO      KESY8000 
.
.--- Test start <= finish ----
.
KESY8000
          COMPARE   SSYSTEM,ESYSTEM
          GOTO      KESY8500 IF LESS
          GOTO      KESY9999
KESY8500
          BEEP
          DISPLAY   *P1:24,"Ending system must be >= Starting system. ";
          CALL      HITENTER
          GOTO      KESY0000
.
KESY9000  
          MOVE      ONE,ENDSYST
          DISPLAY   *P24:11,*EL,*V2LON,FINISH
          MOVE      NINE,ESYSTEM
.
KESY9999  
          DISPLAY   *P1:24,*EL
          RETURN
.
          
.---- Display valid systems ----
.
DSOP0000
          DISPLAY   *P1:24,*EL:
                    *V2LON,ONE,*HOFF:
                    " = A & E, ":
                    *V2LON,TWO,*HOFF:
                    " = Outpatients, ":
                    *V2LON,THREE,*HOFF:
                    " = Inpatients, ":
                    *V2LON,FOUR,*HOFF:
                    " = Other Financial"
DSOP9999  RETURN
.
.*********************************************************************
.*                         KECC0000                                  *
.*                   Keyin the claim code                            *
.*********************************************************************
.
KECC0000
          MOVE      ZERO,EXIT
.
KECC1000
          KEYIN     *P24:16,*EL,*DV,UNDLN3:
                    *P24:16,*V2LON,ECLAIM:
                    *P24:16,*DV,ECLAIM 
.
          PACK      ECLAIM,ECLAIM,SP3
          MATCH     SP3,ECLAIM          * test if nothing entered
          GOTO      KECC4500 IF EQUAL
.
          MATCH     QUEST,ECLAIM        * test for question mark
          GOTO      KECC5000 IF EQUAL
.
          PACK      KEY5,CODE,ECLAIM
          CALL      RDCODE1              * validate claim code
          BRANCH    OVRCD,KECC4000
          MOVE      TDESC,ECLAIMDS
.
          DISPLAY   *P28:16,ECLAIMDS
          GOTO      KECC9999
.
KECC4000
          BEEP
          DISPLAY   *P1:24,*EL,"Invalid Claim code. ";
          CALL      HITENTER
          GOTO      KECC1000
KECC4500
          MOVE      "zzz",ECLAIM
          CLEAR     ECLAIMDS
          DISPLAY   *P24:16,*V2LON,FINISH
          GOTO      KECC9999
.
KECC5000
          CALL      PATCODDS             * question mark display
KECC6000
          DISPLAY   *P1:24,*EL,"Claim Code :"
          KEYIN     *P14:24,*DV,UNDLN3:
                    *P14:24,*V2LON,ECLAIM:
                    *P14:24,*DV,ECLAIM 
.
          PACK      ECLAIM,ECLAIM,SP3
          MATCH     SP3,ECLAIM         * test if nothing entered
          GOTO      KECC8600 IF EQUAL
.
          MATCH     QUEST,ECLAIM       * test for a ? mark
          GOTO      KECC5000 IF EQUAL
.
          PACK      KEY5,CODE,ECLAIM
          CALL      RDCODE1             * validate the claim code
          BRANCH    OVRCD,KECC8000
          MOVE      TDESC,ECLAIMDS
          GOTO      KECC8500
.
KECC8000
          BEEP
          DISPLAY   *P1:24,*EL,"Invalid Claim code. ";
          CALL      HITENTER
          GOTO      KECC6000
.
.*** Redisplay routine - valid entry ***
KECC8500
          CALL      MENU0000
          CALL      SCRN0000
          CALL      DDAT0000             * display the data  
          GOTO      KECC9999
.
KECC8600
          CALL      MENU0000
          CALL      SCRN0000
          MOVE      "zzz",ECLAIM
          MOVE      SP20,SYSDESC2
          CALL      DDAT0000             * display the data
.
KECC9999  RETURN
.
.*********************************************************************
.*                         DDAT0000                                  *
.*                      Display the data                             *
.*********************************************************************
.
DDAT0000
          BRANCH    STRTSYST,DDAT0100
          DISPLAY   *P24:9,*V2LON,SSYSTEM,*HOFF,*P28:9,SYSDESC1 
          GOTO      DDAT0200
DDAT0100  DISPLAY   *P24:9,*V2LON,START                         
DDAT0200  BRANCH    ENDSYST,DDAT0300
          DISPLAY   *P24:11,*V2LON,ESYSTEM,*HOFF,*P28:11,SYSDESC2
          GOTO      DDAT0400
DDAT0300
          DISPLAY   *P24:11,*V2LON,FINISH                        
.
DDAT0400  MATCH     SP3,SCLAIM
          GOTO      DDAT1000 IF EQUAL
.
          DISPLAY   *P24:14,*V2LON,SCLAIM,*HOFF,*P28:14,SCLAIMDS
          GOTO      DDAT2000
.
DDAT1000
          DISPLAY   *P24:14,*V2LON,START
.
DDAT2000
          MATCH     "zzz",ECLAIM
          GOTO      DDAT3000 IF EQUAL
.
          DISPLAY   *P24:16,*V2LON,ECLAIM,*HOFF,*P28:16,ECLAIMDS
          GOTO      DDAT9999
.
DDAT3000
          DISPLAY   *P24:16,*V2LON,FINISH 
.
DDAT9999  RETURN
.
.*********************************************************************
.*                         CVAR0000                                  *
.*                    Clear the variables                            *
.*********************************************************************
.
CVAR0000
          MOVE      ZERO,SSYSTEM
          MOVE      ZERO,ESYSTEM
          CLEAR     SCLAIM
          CLEAR     ECLAIM
          CLEAR     SCLAIMDS
          CLEAR     ECLAIMDS
          CLEAR     SYSDESC1
          CLEAR     SYSDESC2
.
CVAR9999  RETURN
.
.*********************************************************************
.*                         VLCC0000                                  *
.*             Validate end code >=  start code                      *
.*********************************************************************
.
VLCC0000  MOVE      ZERO,EXIT
.
          MATCH     SCLAIM,ECLAIM
          GOTO      VLCC5000 IF LESS
.
          GOTO      VLCC9999
.
VLCC5000
          MOVE      ONE,EXIT
.
          BEEP
          DISPLAY   *P1:24,"Ending code must be >= to starting code. ";
          CALL      HITENTER
.
VLCC9999  RETURN
.
.*********************************************************************
.*                        PRPT0000                                   *
.*                    Print the report                               *
.*********************************************************************
.
PRPT0000 
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*V2LON,"*** Printing ***"
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000
.

          PACK      KEY4,SSYSTEM,SP3             * position in the file
          CALL      RSPTINM1
          CALL      RKPTINM1
          BRANCH    OVRCD,PRPT9000
.
          PACK      KEY4,PTIMSYS,SP3             * position in the file
          CALL      RSPTINM1
PRPT1000
          CALL      RKPTINM1             * read next record
          BRANCH    OVRCD,PRPT9000
.
          COMPARE   PTIMSYS,ESYSTEM      * test within system range
          GOTO      PRPT9000 IF LESS
.
          MATCH     SCLAIM,PTIMCLM       * test >= start claim code
          GOTO      PRPT1000 IF LESS
.
          MATCH     PTIMCLM,ECLAIM       * test <= end code
          GOTO      PRPT1000 IF LESS
.
          CLEAR     DIM20
          CLEAR     TDESC
.
          PACK      KEY5,CODE,PTIMCLM
          CALL      RDCODE1
.
          BRANCH   PTIMSYS,PRPT2000,PRPT3000,PRPT4000,PRPT5000
PRPT2000
          MOVE      "Accident & Emergency",DIM20
          GOTO      PRPT6000 
PRPT3000
          MOVE      "Outpatients",DIM20
          GOTO      PRPT6000 
PRPT4000
          MOVE      "Inpatients",DIM20
          GOTO      PRPT6000 
PRPT5000
          MOVE      "Other Financial",DIM20
.
PRPT6000  PRINT     "|",*3,PTIMSYS,*7,DIM20,*30,"| ",PTIMCLM,*37,TDESC:
                    *60,"| ",PTIMMES1,*132,"|",*N:
                    "|",*30,"|",*60,"| ",PTIMMES2,*132,"|"
          CALL      UND132
.
          ADD       THREE,CLNO
          COMPARE   CLNO,FIFTY5
          GOTO      PRPT7000 IF LESS
.
          GOTO      PRPT1000
PRPT7000
          CALL      HEAD0000
          GOTO      PRPT1000
PRPT9000
          PRINT     *N:
                    "****  End of Report ****"
.
PRPT9999  RETURN
.
.*********************************************************************
.*                       HEAD0000                                    *
.*                   Print the report heading                        *
.********************************************************************* 
.
HEAD0000
          CALL      HEAD132
.
          BRANCH   STRTSYST,HEAD0100
          PRINT    *40,"Starting System     : ",SSYSTEM,SP5,SYSDESC1
          GOTO     HEAD0200
.
HEAD0100  PRINT    *40,"Starting System     : ",START                   
.
HEAD0200 
          BRANCH    ENDSYST,HEAD0300
          PRINT     *40,"Ending System       : ",ESYSTEM,SP5,SYSDESC2
          GOTO      HEAD0400
.
HEAD0300  PRINT     *40,"Ending System       : ",FINISH
.
HEAD0400
          PRINT     " "
          MATCH     SP3,SCLAIM
          GOTO      HEAD1000 IF EQUAL
          PRINT     *40,"Starting Claim Code : ",SCLAIM,SP2,SCLAIMDS
          GOTO      HEAD3000
.
HEAD1000
          PRINT     *40,"Starting Claim Code : ",START
.
HEAD3000  MATCH     "zzz",ECLAIM
          GOTO      HEAD5000 IF EQUAL
          PRINT     *40,"Ending Claim Code   : ",ECLAIM,SP2,ECLAIMDS,*N
          GOTO      HEAD6000
.
HEAD5000
          PRINT      *40,"Ending Claim Code   : ",FINISH,*N
.
HEAD6000  CALL       UND132
          PRINT      "| System",*30,"| Claim Code",*60,"| Message",*132,"|"
          CALL       UND132
          ADD        NINE,CLNO
.
HEAD9999  RETURN
.
.*********************************************************************
.*                  I/O and other includes required                  *
.*********************************************************************
.
          INC       PATCODDS
.
          INC       PATINMIO/INC
          INC       PATCODIO/INC
          INC       STD001IO

.***************************************************************************
.* System    :   PMI                                                       *
.* Program   :   FIXALERT                                                  *
.* Desc      :   This program will loop through each master file record    *
.*               to calculate and update ptmxsin1                          *
.***************************************************************************
.* Date      :   27/06/2008                                                *
.* Author    :   Jill Habasque                                             *
.* Modifications :                                                         *
.* V11.04.01        07/05/2024  Ebon Clements     TSK 0946433              *
.*                  Fixed end dated alerts test                            *
.* V10.08.01        08/08/2016  Davin             TSK 0321234              *
.*                  Added chronic condition alert (pmpxsn11)               *
.* V10.05.02        09/01/2015 Jill Parkinson CAR 306129                   *
.*                  Moved WA Health functionality to be first to stop      *
.*                  setting pmpxsin7, 8 and 9 as well as ptmxsin1          *
.* V10.05.01        21/10/2014 Sandra Barcham CAR 306129                   *
.*                  Added new alert for car                                *
.* V10.03.01        29/11/2011 Jill Parkinson CAR 249362                   *
.*                  Changed alert key                                      *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATALRFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
SAVESIN1  DIM       1
SAVESIN7  DIM       1
SAVESIN8  DIM       1
SAVESIN9  DIM       1
SAVESN11  DIM       1
DIM8      DIM       8
.
PRGID     INIT      "FIXALERT"
PRGNAM    INIT      "Fix master file alert indicator"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PATALRT1,"patalrtf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER
.
          DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY8,SP70
          CALL      RDSMAST1
PROC1000  CALL      RDKMAST1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,COUNTER
          IF        (COUNTER%100) = 0
            DISPLAY   *P15:20,*V2LON,COUNTER
          ENDIF
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC1000
.
          MOVE      PTMXSIN1,SAVESIN1
          MOVE      SP70,PTMXSIN1
          MOVE      PMPXSIN7,SAVESIN7
          MOVE      SP70,PMPXSIN7
          MOVE      PMPXSIN8,SAVESIN8
          MOVE      SP70,PMPXSIN8
          MOVE      PMPXSIN9,SAVESIN9
          MOVE      SP70,PMPXSIN9
          MOVE      PMPXSN11,SAVESN11
          MOVE      SP70,PMPXSN11
.
          CALL      CHKALR00
.
          MATCH     SAVESIN1,PTMXSIN1
          GOTO      PROC2000 IF NOT EQUAL
          MATCH     SAVESIN7,PMPXSIN7
          GOTO      PROC2000 IF NOT EQUAL
          MATCH     SAVESIN8,PMPXSIN8
          GOTO      PROC2000 IF NOT EQUAL
          MATCH     SAVESIN9,PMPXSIN9
          GOTO      PROC2000 IF NOT EQUAL
          MATCH     SAVESN11,PMPXSN11
          GOTO      PROC1000 IF EQUAL
.
PROC2000  CALL      UPMAST1
          CALL      UPPMPX21
          GOTO      PROC1000
.
PROC9999  DISPLAY   "Finished processsing ";
          CALL      HITENTER
          RETURN
+
.**************************************************************************
.*                               CHKALR00              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
CHKALR00  PACK      KEY16,PURNO,SP70
          CALL      RSPTALR1
CHKALR10  CALL      RKPTALR1
          BRANCH    OVRCD,CHKALR99
.
          MATCH     PURNO,PTALURNO
          GOTO      CHKALR99 IF NOT EQUAL
.
          PACK      KEY5,PTALCATG,PTALCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKALR10
.
          MATCH     SP8,PTALDTIN
          GOTO      CHKALR10 IF NOT EQUAL
.
          CALL       IBACLOCK
          PACK       DIM8,CCC,CYY,CMM,CDD,SP70
          REP        " 0",DIM8
.
          MATCH     SP70,PTALEDAT
          IF        !@EQUAL
            MATCH     PTALEDAT,DIM8                * future end date
            GOTO      CHKALR20 IF LESS
.
            GOTO      CHKALR10                     * show alert as inactive
          ENDIF
.
CHKALR20  MATCH     "M",TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSIN7
            GOTO      CHKALR10
          ENDIF
          MATCH     "B",TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSIN8
            GOTO      CHKALR10
          ENDIF
          MATCH     "R",TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSIN9
            GOTO      CHKALR10
          ENDIF
          MATCH     "C",TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSN11
            GOTO      CHKALR10
          ENDIF
.
          REP       "S2 1",TCINDC4
.
          MATCH     "2",TCINDC4        * check for security alert, if yes stop
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4
            GOTO      CHKALR99
          ENDIF
.
          MATCH     PTMXSIN1,TCINDC4
          IF        @LESS
            PACK      PTMXSIN1,TCINDC4
          ENDIF
.
          MATCH     SP70,PTMXSIN1
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4
          ENDIF
.
          GOTO      CHKALR10
.
CHKALR99  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PATALRIO/INC

.*********************************************************************
.* System    :   Outpatient System                                   *
.* Program   :   IBAOUT95                                            *
.* Function  :   Outpatient Units Listing                            *
.*********************************************************************
.* Date      :   15.04.91                                            *
.* Author    :   Peter Eddey                                         *
.* Mods      :                                                       *
.*     V10.07.01   04/11/2015  Ebon Clements  CAR 268970             *
.*                 Change to use OUTCLIFD index 1 instead of index 2 *
.*        V5.08.01  19/09/2000  Steve Armstrong   srf 647263         *
.*                  Recompiled for changes to OUTDSCLN               *
.*        V5.08.B01 16/03/2000  Steve Armstrong     5.08 Mods        *
.*                  Mods for changes to OUTCLIFD (effective date)    *
.*********************************************************************
.
          INC       STD001FD
          INC       OUTUNIFD/INC       * Inpatient Units
          INC       OUTCLIFD/INC
          INC       OUTCTYFD/INC
          INC       OUTSITFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATTMAFD/INC
.
.*********************************************************************
.*                       Data Variables Required                     *
.*********************************************************************
.
CODE      DIM       2
CODEAC    INIT      "AC"    
CLINDESC  DIM       25
CLINFILE  DIM       8
CLIAF     INIT      "cliaf"
CTYAF     INIT      "ctyaf"
DIM3      DIM       3
DIM14     DIM       14
DIM29     DIM       29
DIM30     DIM       30
DIM40     DIM       40
ENDCTYP   DIM       6
ENDDATE   DIM       10
ENDCID   DIM       6
ENDTEAM   DIM       6
ECTYDESC  DIM       20
ECIDDESC  DIM       30
ETEADESC  DIM       30
ENDFLAG   FORM      1
FINISH    INIT      "Finish"
MNUOPT    FORM      1
OLDCTYP   DIM       6
OLDCID   DIM       6
OLDTEAM   DIM       6
ROW       FORM      2
CTYPIALT  DIM       20
SCTYDESC  DIM       20
SCIDDESC  DIM       30
START     INIT      "Start"
STRTDATE  DIM       10
STEADESC  DIM       30
STRTFLAG  FORM      1
STRTCTYP  DIM       6
STRTCID   DIM       6
STRTTEAM  DIM       6
TEAMDESC  DIM       25
TYPEDESC  DIM       25
TYPEFILE  DIM       8
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT95"
PRGNAM    INIT      "Outpatient Units Listing"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                         ML0000                                    *
.*                     Mainline Code                                 *
.*********************************************************************
.
ML0000    CALL      INIT0000             * initialization routine
          BRANCH    EXIT,ML9999            (Site is not set)
ML0500    CALL      MENU0000             * program menu
          BRANCH    MNUOPT,ML1000,ML2000
          GOTO      ML9999
.
.***** Print listing by Clinic Type *****
.
ML1000    CALL      CVAR0000             * clear variables
          CALL      DSCR0000             * display the screen
          CALL      ICTY0000             * input clinic types
.
          CALL      CONTQST
          BRANCH    CEXIT,ML1100,ML1000,ML0500
.
ML1100    CALL      PRNT0000             * print the report
          GOTO      ML0500
.
.***** Print listing by Clinic Id ******
.
ML2000    CALL      CVAR0000             * clear variables
          CALL      DSCR0000             * display the screen
          CALL      ICID0000             * input clinic id details
.
          CALL      CONTQST
          BRANCH    CEXIT,ML2100,ML2000,ML0500
.
ML2100    CALL      PRNT0000             * print the report
          GOTO      ML0500
.
ML9999    CHAIN     PGM
.
.********************************************************************
.*                         INIT0000                                 *
.*                    Initialization routine                        *
.********************************************************************
.
INIT0000  MOVE      ONE,CNEWDS
          CALL      DISPHEAD
.
          MOVE      ZERO,EXIT
.
.**** Test if the site has been passed via common - error if not ****
.
          PACK      IDDD,IDDD,SP6
          MATCH     SP6,IDDD
          GOTO      INIT1000 IF NOT EQUAL
.
INIT0500  BEEP
          DISPLAY   *P1:24,"Site has not been set up. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      INIT9999
.
INIT1000  DISPLAY   *P50:24,*EL,"Opening : ",*P61:24,"pattmaaf"
          OPEN      PATTMAA1,"pattmaaf"
.
          OPEN      OUTAUDUI,"outaudui"
          DISPLAY   *P61:24,*EL,"outsitaf"
          OPEN      OUTSITA1,"outsitaf"
          PACK      KEY6,IDDD
          CALL      RDSITA1
          BRANCH    OVRCD,INIT0500
.
          PACK      CLINFILE,OSTFILE,CLIAF
          PACK      TYPEFILE,OSTFILE,CTYAF
          OPEN      OUTCLIA1,CLINFILE  
          OPEN      OUTCTYA1,TYPEFILE  
          OPEN      OUTCTYA2,TYPEFILE  
.
          DISPLAY   *P61:24,*EL,"outuniaf"
          OPEN      OUTUNIA1,"outuniaf"
          OPEN      OUTUNIA2,"outuniaf"
.
          DISPLAY   *P61:24,*EL,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P61:24,*EL,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
.
.********************************************************************
.*                         DMEN0000                                 *
.*                  Display the programs menu                       *
.********************************************************************
.
DMEN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0 ",*HOFF,"= Exit":
                    *P1:5,*V2LON,"1 ",*HOFF,"= By Clinic Type":
                    *P1:6,*V2LON,"2 ",*HOFF,"= By Clinic Id":
                    *P1:8,"Select Option ? "
.
DMEN9999  RETURN
.********************************************************************
.*                         MENU0000                                 *
.*                  Handle the programs menu                        *
.********************************************************************
.
MENU0000  HLINE     *G37,2,52,80
          CALL      DMEN0000             * display the menu
MENU1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MNUOPT:
                    *P17:8,*DV,MNUOPT
.
          COMPARE   ZERO,MNUOPT          * test if exit selected
          GOTO      MENU9999 IF EQUAL
.
.**** Test for valid options 1 or 2 ****
.
          BRANCH    MNUOPT,MENU1100,MENU2000
.
          BEEP                           * invalid option so re-keyin
          GOTO      MENU1000
.
MENU1100  DISPLAY   *P52:2,*V2LON,"- By Clinic Type "   
          GOTO      MENU9999
.
MENU2000  DISPLAY   *P52:2,*V2LON,"- By Clinic Id "                
          GOTO      MENU9999
.
.
MENU9999  RETURN
.
.
.*********************************************************************
.*                         ICTY0000                                  *
.*                  Input Clinic Type details                        *
.*********************************************************************
.
ICTY0000
          MOVE      ZERO,STRTFLAG
          MOVE      ZERO,ENDFLAG
.
          MOVE      TEN1,ROW
          CALL      KCTY0000
          BRANCH    EXIT,ICTY1000
          MOVE      OCTDESC,SCTYDESC
          MOVE      OTUITYPE,STRTCTYP
          DISPLAY   *P20:11,*V2LON,STRTCTYP,*HOFF,*P28:11,SCTYDESC
          GOTO      ICTY3000
.
.**** Use default starting clinic type ****
.
ICTY1000
          MOVE      ONE,STRTFLAG
          MOVE      SP10,STRTCTYP
          DISPLAY   *P20:11,*V2LON,START
ICTY3000
          MOVE      TEN3,ROW
          CALL      KCTY0000
          BRANCH    EXIT,ICTY4000
.
.***** Test start code is < or = end code *****
.
          MATCH     STRTCTYP,OTUITYPE
          GOTO      ICTY9000 IF LESS
.
          MOVE      OCTDESC,ECTYDESC
          MOVE      OTUITYPE,ENDCTYP
          DISPLAY   *P20:13,*V2LON,ENDCTYP,*HOFF,*P28:13,ECTYDESC
          GOTO      ICTY9999
.
.**** Use default ending clinic type ****
.
ICTY4000
          MOVE      ONE,ENDFLAG
          MOVE      "zzzzzzz",ENDCTYP
          DISPLAY   *P20:13,*V2LON,FINISH
          GOTO      ICTY9999
.
ICTY9000
          BEEP
          DISPLAY   *P1:24,"Ending code must be > or = Starting code. ";
          CALL      HITENTER
          GOTO      ICTY3000
          
ICTY9999
          RETURN
.
.*********************************************************************
.*                         ICID0000                                  *
.*                  Input Clinic Id details                          *
.*********************************************************************
.
ICID0000
          MOVE      ZERO,STRTFLAG
          MOVE      ZERO,ENDFLAG
.
          MOVE      TEN1,ROW
          CALL      KCID0000
          BRANCH    EXIT,ICID1000
          MOVE      OCADESC,SCIDDESC
          MOVE      OTUICLIN,STRTCID
          DISPLAY   *P18:11,*V2LON,STRTCID,*HOFF,*P26:11,SCIDDESC
          GOTO      ICID3000
.
.**** Use default starting clinic id ****
.
ICID1000
          MOVE      ONE,STRTFLAG
          MOVE      SP10,STRTCID
          DISPLAY   *P18:11,*V2LON,START
ICID3000
          MOVE      TEN3,ROW
          CALL      KCID0000
          BRANCH    EXIT,ICID4000
.
.***** Test start code is < or = end code *****
.
          MATCH     STRTCID,OTUICLIN
          GOTO      ICID9000 IF LESS
.
          MOVE      OCADESC,ECIDDESC
          MOVE      OTUICLIN,ENDCID
          DISPLAY   *P18:13,*V2LON,ENDCID,*HOFF,*P26:13,ECIDDESC
          GOTO      ICID9999
.
.**** Use default ending clinic id ****
.
ICID4000
          MOVE      ONE,ENDFLAG
          MOVE      "zzzzzzz",ENDCID
          DISPLAY   *P18:13,*V2LON,FINISH
          GOTO      ICID9999
.
ICID9000
          BEEP
          DISPLAY   *P1:24,"Ending code must be > or = Starting code. ";
          CALL      HITENTER
          GOTO      ICID3000
          
ICID9999
          RETURN
.
.*********************************************************************
.*                         KCTY0000                                  *
.*                     Keyin the Clinic Type                         *
.*********************************************************************
.
KCTY0000
          MOVE      ZERO,EXIT
.
          KEYIN     *P20:ROW,*EL,*DV,UNDLN6:
                    *P20:ROW,*V2LON,OTUITYPE:
                    *P20:ROW,*DV,OTUITYPE
.
          PACK      OTUITYPE,OTUITYPE,SP6
          MATCH     SP6,OTUITYPE          * test if noting entered
          GOTO      KCTY9000 IF EQUAL
.
          MATCH     QUEST,OTUITYPE        * test for a ?
          GOTO      KCTY5000 IF EQUAL
.
          PACK      KEY12,IDDD,OTUITYPE 
          CALL      RDCTYA1               * read the clinic type file
          BRANCH    OVRCD,KCTY4000
          GOTO      KCTY9999
.
KCTY4000  BEEP
          DISPLAY   *P1:24,*EL,"Clinic Type is not on file, ";
          CALL      HITENTER
          GOTO      KCTY0000
.
.***** Handle the question mark option *****
.
KCTY5000
          CALL      OUTDSCTY
.
KCTY6000
          DISPLAY   *P1:24,"Clinic Type :"
.
          KEYIN     *P15:24,*DV,UNDLN6:
                    *P15:24,*V2LON,OTUITYPE:
                    *P15:24,*DV,OTUITYPE
.
          PACK      OTUITYPE,OTUITYPE,SP6
          MATCH     SP6,OTUITYPE          * test if noting entered
          GOTO      KCTY8500 IF EQUAL
.
          MATCH     QUEST,OTUITYPE       * test for a ?
          GOTO      KCTY5000 IF EQUAL
.
          PACK      KEY12,IDDD,OTUITYPE
          CALL      RDCTYA1               * read the clinic type file
          BRANCH    OVRCD,KCTY7000
          CALL      DMEN0000             * redisplay the menu
          CALL      DSCR0000             * redisplay the screen
          GOTO      KCTY9999
.
KCTY7000  BEEP
          DISPLAY   *P1:24,*EL,"Clinic Type is not on file, ";
          CALL      HITENTER
          GOTO      KCTY6000
.
KCTY8500  
          CALL      DMEN0000             * redisplay the menu
          CALL      DSCR0000             * redisplay the screen
KCTY9000
          MOVE      ONE,EXIT
.
KCTY9999  RETURN
.
.*********************************************************************
.*                         KCID0000                                  *
.*                   Keyin the Clinic Id                             *
.*********************************************************************
.
KCID0000  MOVE      ZERO,EXIT            * set exit flag
.
          KEYIN     *P18:ROW,*EL,*DV,UNDLN6:
                    *P18:ROW,*V2LON,OTUICLIN:
                    *P18:ROW,*DV,OTUICLIN
.
          PACK      OTUICLIN,OTUICLIN,SP6
          MATCH     SP6,OTUICLIN          * test if noting entered
          GOTO      KCID9000 IF EQUAL
.
          MATCH     QUEST,OTUICLIN       * test for a ?
          GOTO      KCID5000 IF EQUAL
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,IDDD,OTUICLIN,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTUICLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,KCID4000
          CALL      GCLD0000             * gets correct clinic desc
          GOTO      KCID9999
.
KCID4000  BEEP
          DISPLAY   *P1:24,*EL,"Clinic Id is not on file, ";
          CALL      HITENTER
          GOTO      KCID0000
.
.***** Handle the question mark option *****
.
KCID5000  CALL      OUTDSCLN
.
KCID6000  DISPLAY   *P1:24,"Clinic Id :"         
.
          KEYIN     *P13:24,*DV,UNDLN6:
                    *P13:24,*V2LON,OTUICLIN:
                    *P13:24,*DV,OTUICLIN
.
          PACK      OTUICLIN,OTUICLIN,SP6
          MATCH     SP6,OTUICLIN          * test if noting entered
          GOTO      KCID8500 IF EQUAL
.
          MATCH     QUEST,OTUICLIN       * test for a ?
          GOTO      KCID5000 IF EQUAL
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,IDDD,OTUICLIN,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTUICLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,KCID7000
          CALL      GCLD0000             * gets correct clinic desc
          CALL      DMEN0000             * redisplay the menu
          CALL      DSCR0000             * redisplay the screen
          GOTO      KCID9999
.
KCID7000  BEEP
          DISPLAY   *P1:24,*EL,"Clinic Id is not on file, ";
          CALL      HITENTER
          GOTO      KCID6000
.
KCID8500  CALL      DMEN0000             * redisplay the menu
          CALL      DSCR0000             * redisplay the screen
KCID9000  MOVE      ONE,EXIT
.
KCID9999  RETURN
.
.*********************************************************************
.*                         CVAR0000                                  *
.*                   Clear the variables                             *
.*********************************************************************
.
CVAR0000   MOVE     ZERO,STRTFLAG
           MOVE     ZERO,ENDFLAG
           CLEAR    SCTYDESC
           CLEAR    ECTYDESC
           CLEAR    STRTCTYP
           CLEAR    ENDCTYP
           CLEAR    SCIDDESC
           CLEAR    ECIDDESC
           CLEAR    STRTCID
           CLEAR    ENDCID
.
CVAR9999   RETURN
.
.*********************************************************************
.*                         DSCR0000                                  *
.*                 display the required screen                       *
.*********************************************************************
.*
DSCR0000  BRANCH    MNUOPT,DSCR1000,DSCR4000
          GOTO      DSCR9999
.
.
.***** Display the screen for speciality option *****
.
DSCR1000  DISPLAY   *P1:11,*EF:
                    "From Clinic Type :":
                    *P1:13,"  To Clinic Type :"
          BRANCH    STRTFLAG,DSCR1100
          DISPLAY   *P20:11,*EL,*V2LON,STRTCTYP,*HOFF,*P28:11,SCTYDESC
          GOTO      DSCR1200
.
DSCR1100  DISPLAY   *P20:11,*EL,*V2LON,START                            
.
DSCR1200  BRANCH    ENDFLAG,DSCR1300
          DISPLAY   *P20:13,*EL,*V2LON,ENDCTYP,*HOFF,*P28:13,ECTYDESC
          GOTO      DSCR9999
DSCR1300
          DISPLAY   *P20:13,*EL,*V2LON,FINISH                           
          GOTO      DSCR9999
          
.
.***** Display the screen for doctor option *****
.
DSCR4000  DISPLAY   *P1:11,*EF:
                    "From Clinic Id :":
                    *P1:13,"  To Clinic Id :"
          BRANCH    STRTFLAG,DSCR4100
          DISPLAY   *P18:11,*EL,*V2LON,STRTCID,*HOFF,*P26:11,SCIDDESC
          GOTO      DSCR4200
DSCR4100  DISPLAY   *P18:11,*EL,*V2LON,START                            
DSCR4200  BRANCH    ENDFLAG,DSCR4300
          DISPLAY   *P18:13,*EL,*V2LON,ENDCID,*HOFF,*P26:13,ECIDDESC
          GOTO      DSCR9999
DSCR4300  DISPLAY   *P18:13,*EL,*V2LON,FINISH                           
.
DSCR9999  RETURN
.*********************************************************************
.*                         FDOC0000                                  *
.*                 Format the doctors name                           *
.*********************************************************************
.
FDOC0000  MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
.
          CALL      PACNAME
FDOC9999  RETURN
.
.*********************************************************************
.*                        PRNT0000                                   *
.*                   Print the master listing                        *
.*********************************************************************
.
PRNT0000  MOVE      ZERO,CPAGENO
          DISPLAY   *P1:24,*EL,*V2LON,"***** Printing Report *****"
          CALL      HEAD0000             * print the heading
          CALL      SPOS0000             * position in file
PRNT1000  CALL      NEXT0000             * get next record
          BRANCH    OVRCD,PRNT8000
.
          MATCH     IDDD,OTUISITE
          GOTO      PRNT8000 IF NOT EQUAL
.
          CALL      TEST0000             * test if in range
          BRANCH    EXIT,PRNT8000
.
          ADD       ONE,CLNO             * Increment the row
.
          CALL      SPVR0000             * set up variables for print
.
          CALL      CLIN0000             * choose which line to print
          BRANCH    EXIT,PRNT2000
.
          CALL      LIN10000             * print line type 1
          GOTO      PRNT3000
.
PRNT2000  CALL      LIN20000             * print line type 3
.
PRNT3000  COMPARE   "55",CLNO
          GOTO      PRNT1000 IF LESS 
.
          CALL      HEAD0000
          GOTO      PRNT1000
.
PRNT8000  CALL      UND132
          PRINT     " ",*N:
                    "***** End of Report *****"
          DISPLAY   *P1:24,*EL
.
PRNT9999  RETURN
.
.
.*********************************************************************
.*                         SPOS0000                                  *
.*                   Set the position in the file                    *
.*********************************************************************
.
SPOS0000  BRANCH    MNUOPT,SPOS1000,SPOS2000
          GOTO      SPOS9999
SPOS1000  PACK      KEY18,IDDD,STRTCTYP,SP10
          CALL      RSOTUNI1
          GOTO      SPOS9999
SPOS2000  PACK      KEY18,IDDD,STRTCID,SP10
          CALL      RSOTUNI2
          GOTO      SPOS9999
.
SPOS9999  RETURN
.

.*********************************************************************
.*                         NEXT0000                                  *
.*             Get the next record in the file                       *
.*********************************************************************
.
NEXT0000  BRANCH    MNUOPT,NEXT1000,NEXT2000
          GOTO      NEXT9999
NEXT1000  CALL      RKOTUNI1
          GOTO      NEXT9999
NEXT2000  CALL      RKOTUNI2
          GOTO      NEXT9999
.
NEXT9999  RETURN
.
.*********************************************************************
.*                         TEST0000                                  *
.*                 Test for end of required range                    *
.*********************************************************************
.
TEST0000  MOVE      ZERO,EXIT
          BRANCH    MNUOPT,TEST1000,TEST2000
          GOTO      TEST9999
TEST1000  MATCH     OTUITYPE,ENDCTYP
          GOTO      TEST9000 IF LESS
          GOTO      TEST9999
TEST2000  MATCH     OTUICLIN,ENDCID
          GOTO      TEST9000 IF LESS
          GOTO      TEST9999
.
TEST9000  MOVE      ONE,EXIT
.
TEST9999  RETURN
.
.*********************************************************************
.*                         CLIN0000                                  *
.*                 Choose which line to print                        *
.*********************************************************************
.
CLIN0000  MOVE      ZERO,EXIT
          BRANCH    MNUOPT,CLIN1000,CLIN2000
          GOTO      CLIN9999
CLIN1000
          MATCH     OLDCTYP,OTUITYPE
          GOTO      CLIN9000 IF EQUAL
          MOVE      OTUITYPE,OLDCTYP
          GOTO      CLIN9999
CLIN2000
          MATCH     OLDCID,OTUICLIN
          GOTO      CLIN9000 IF EQUAL
          MOVE      OTUICLIN,OLDCID
          GOTO      CLIN9999
.
CLIN9000  MOVE      ONE,EXIT
.
CLIN9999  RETURN
.
.*********************************************************************
.*                         LIN10000                                  *
.*                 Print line with key decription                    *
.*********************************************************************
.
LIN10000  MOVE      ZERO,EXIT
          BRANCH    MNUOPT,LIN11000,LIN12000
          GOTO      LIN19999
LIN11000  PRINT     "|",*3,OTUITYPE,*10,TYPEDESC,*36,"|":
                    *38,OTUICLIN,*45,CLINDESC,*71,"|",*73,OTUITEAM:
                    *79,TEAMDESC,*106,"|",*108,STRTDATE,*119,"|":
                    *121,ENDDATE,*132,"|"
          GOTO      LIN19999
LIN12000  PRINT     "|",*3,OTUICLIN,*10,CLINDESC,*36,"|":
                    *38,OTUITYPE,*45,TYPEDESC,*71,"|",*73,OTUITEAM:
                    *79,TEAMDESC,*106,"|",*108,STRTDATE,*119,"|":
                    *121,ENDDATE,*132,"|"
          GOTO      LIN19999
.
LIN19999  RETURN

.*********************************************************************
.*                         LIN20000                                  *
.*                 Print line without  key decription                *
.*********************************************************************
.
LIN20000  MOVE      ZERO,EXIT
          BRANCH    MNUOPT,LIN21000,LIN22000
          GOTO      LIN29999
LIN21000  PRINT     "|",*36,"|":
                    *38,OTUICLIN,*45,CLINDESC,*71,"|",*73,OTUITEAM:
                    *79,TEAMDESC,*106,"|",*108,STRTDATE,*119,"|":
                    *121,ENDDATE,*132,"|"
          GOTO      LIN29999
LIN22000  PRINT     "|",*36,"|":
                    *38,OTUITYPE,*45,TYPEDESC,*71,"|",*73,OTUITEAM:
                    *79,TEAMDESC,*106,"|",*108,STRTDATE,*119,"|":
                    *121,ENDDATE,*132,"|"
          GOTO      LIN29999
.
LIN29999  RETURN
.
.*********************************************************************
.*                         HEAD0000                                  *
.*                   Print the required heading                      *
.*********************************************************************
.*
HEAD0000  CALL      IBACLOCK
          CALL      HEAD132
          BRANCH    MNUOPT,HEAD1000,HEAD4000
          GOTO      HEAD9999
.
.
.***** Print the heading for speciality option *****
.
HEAD1000  PRINT     " ",*N:    
                    *40,"From Clinic Type :";    
          BRANCH    STRTFLAG,HEAD1100
          PRINT     *60,STRTCTYP,*66,SCTYDESC
          GOTO      HEAD1200
HEAD1100  PRINT     *60,START                            
HEAD1200  PRINT     *40,"  To Clinic Type :";;
          BRANCH    ENDFLAG,HEAD1300
          PRINT     *60,ENDCTYP,*66,ECTYDESC
          GOTO      HEAD1500
HEAD1300  PRINT     *60,FINISH                           
.
.***** Print the coloumn headings *****
.
HEAD1500  CALL      UND132
          PRINT     "|",*3,"Clinic Type",*36,"|":
                    *38,"Clinic Id",*71,"|",*73,"Team":
                    *106,"|",*108,"Start date",*119,"|":
                    *121,"End date",*132,"|"
          CALL      UND132
          GOTO      HEAD9999
          
.
.***** Print the heading for doctor option *****
.
HEAD4000  PRINT     " ",*N:    
                    *40,"From Clinic Id :";
          BRANCH    STRTFLAG,HEAD4100
          PRINT     *58,STRTCID,*64,SCIDDESC
          GOTO      HEAD4200
HEAD4100  PRINT     *58,START                            
HEAD4200  PRINT     *40,"  To Clinic Id :";
          BRANCH    ENDFLAG,HEAD4300
          PRINT     *58,ENDCID,*64,ECIDDESC
          GOTO      HEAD4500
HEAD4300  PRINT     *58,FINISH                           
.
.***** Print the coloumn headings *****
.
HEAD4500  CALL      UND132
          PRINT     "|",*3,"Clinic Id",*36,"|":
                    *38,"Clinic Type",*71,"|",*73,"Team":
                    *106,"|",*108,"Start date",*119,"|":
                    *121,"End date",*132,"|"
          CALL      UND132
          GOTO      HEAD9999
          
HEAD9999  ADD       NINE,CLNO
          RETURN
.
.*********************************************************************
.*                         SPVR0000                                  *
.*                   set up print variavles                          *
.*********************************************************************
.
SPVR0000  CLEAR     STRTDATE
          CLEAR     ENDDATE
          CLEAR     PTTADESC
          CLEAR     TEAMDESC
          CLEAR     CLINDESC
          CLEAR     TYPEDESC
.
          PACK      KEY12,IDDD,OTUITYPE
          CALL      RDCTYA1
          BRANCH    OVRCD,SPVR1000
.
          MOVE      OCTDESC,TYPEDESC
SPVR1000  PACK      KEY6,OTUITEAM
          CALL      RDPTTMA1
          MOVE      PTTADESC,TEAMDESC
.
          PACK      KEY20,IDDD,OTUICLIN,OTUISDAT
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTUICLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
            MOVE      OCADESC,CLINDESC
          ENDIF
          BRANCH    OVRCD,SPVR2000
          CALL      GCLD0000
          MOVE      OCADESC,CLINDESC
.
SPVR2000  UNPACK    OTUISDAT,CCENT,CYEAR,CMON,CDAY
          PACK      STRTDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",STRTDATE
.
          PACK      OTUIEDAT,OTUIEDAT,SP8
          MATCH     SP8,OTUIEDAT
          GOTO      SPVR9999 IF EQUAL
.
          UNPACK    OTUIEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      ENDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",ENDDATE
.
SPVR9999  RETURN
.
.
.********************************************************************
.*                         GCLD0000                                 *
.*                    Get clinic description                        *
.********************************************************************
.
GCLD0000  PACK      OCADOCT,OCADOCT,SP6
          MATCH     SP6,OCADOCT
          GOTO      GCLD9999 IF EQUAL
.
          PACK      KEY6,OCADOCT
          CALL      RDDOCT1
          BRANCH    OVRCD,GCLD9999
.
          CALL      FDOC0000
          MOVE      PACFNAME,OCADESC
GCLD9999  RETURN
.
.*********************************************************************
.*                 I/O and other includes required                   *
.*********************************************************************
.
          INC       OUTDSCLN
          INC       OUTDSCTY
          INC       STD001IO
          INC       PATCODKY
          INC       PATTMADS
          INC       OUTUNIIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATTMAIO/INC

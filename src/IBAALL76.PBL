.******************************************************************************
.*  System        :  Allied Health                                            *
.*  Program       :  IBAALL76                                                 *
.*  Description   :  Allied Health Set Department Details                     *
.******************************************************************************
.*  Date          :  23/05/2012                                               *
.*  Author        :  Don Nguyen                                               *
.*  Function      :  This character program accepts user option settings      *
.*                   (defaults) and sets them for all department records in   *
.*                   the Allied Health Department table.                      *
.*                                                                            *
.*  Modifications :                                                           *
.******************************************************************************
.*       V11.02.01   07/02/2022  Thanh T       TSK 0896905                    *
.*                   Recompiled for ALLDEPFD changes                          *
.*       V10.03.01   04/06/2013  Ania P           CAR 285833                  *
.*                   Added XUSRUAHP
.*       V10.03.00   23/05/2012  Don Nguyen       CAR 264682                  *
.*                   Created program                                          *
.******************************************************************************
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLDEPFD/INC
.
. PROGRAM CONSTANTS
. -----------------
PRGID     INIT      "IBAALL76"
PRGNAM    INIT      "Allied Health Set Dept Defaults "
VERSION   INIT      "V12.01.00"
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
COLNUMBR  FORM      2
ROWNUMBR  FORM      2
XUSRUERF  DIM       1         * Using External Referrals
XUSRURAD  DIM       1         * Using Residential Admission
XUSRUASS  DIM       1         * Using Assessments
XUSRURAP  DIM       1         * Using Request Appointments
XUSRDLAS  DIM       1         * Display Last 5 Contacts
XUSRDCLI  DIM       1         * Display Clinic Documentation
XUSRDRAA  DIM       1         * Display Referral Action Audit
XUSRDLET  DIM       1         * Display Letter History
XUSRUEME  DIM       1         * Using Encounter Medications
XUSRUEIN  DIM       1         * Using Encounter Interventions
XURSUESU  DIM       1         * Using Encounter Supplies
XUSRUAHP  DIM       1         * Using Additional HCPs
.
+
.******************************************************************************
.*                              MAIN0000                                      *
.*                  Controlling Logic (Mainline code)                         *
.******************************************************************************
.
MAIN0000  CALL      INIT0000            * initialisation and open files
.
MAIN0100  CALL      OPTN0000            * select options
          BRANCH    EXIT,MAIN9999       * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000    * proceed with clean up
          BRANCH    EXIT,MAIN9999
.
MAIN1000  CALL      PROC0000            * process
          CALL      CONTQST             * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:     * yes
                          MAIN0100:     * no
                          MAIN0100      * cancel
.
MAIN2000  CALL      SAVE0000
.
MAIN9999  CHAIN     PGM                 * chain back to program
+
.******************************************************************************
.*                                INIT0000               Called by: MAIN0000  *
.*                             Initialisation                                 *
.*                  Display program headings and open files                   *
.******************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,"Opening alldepaf";
          OPEN      ALLDEPA1,"alldepaf"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*        get user's option selection                   Called by : MAIN0000  *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                    *
.*                        TRUE  (1)  Exit option selected                     *
.*                                                                            *
.*        valid options     --->  (0) Exit                                    *
.*                                (1) Run                                     *
.******************************************************************************
OPTN0000  HLINE     *G37,2,57,80
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*P1:5,ONE,*HOFF:
                    *P2:4," = Exit":
                    *P2:5," = Run":
                    *P1:7,"Select Option :";
.
OPTN1000  KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
          COMPARE   ZERO,OPTION               * exit ?
          GOTO      OPTN9100 IF EQUAL         * yes
.
          BRANCH    OPTION,OPTN9000
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9100  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                               GETA0000              Called by: PROC0000    *
.*                  Get user's Yes/No responses and sets EXIT value.          *
.******************************************************************************
.
GETA0000  MOVE      SP1,ANS
GETA1000  KEYIN     *PCOLNUMBR:ROWNUMBR,*EL,*DV,UNDLN1:
                    *PCOLNUMBR:ROWNUMBR,*V2LON,ANS;
          TYPE      ANS
          GOTO      GETA1000 IF EQUAL
.
          REP       UPPLOW,ANS
          MATCH     ANS,ANSY
          IF        @EQUAL
            DISPLAY   *PCOLNUMBR:ROWNUMBR,*EL,*V2LON,"Yes",*HOFF
            GOTO      GETA9100
          ELSE
            MATCH     ANS,ANSN
            IF        @EQUAL
              DISPLAY   *PCOLNUMBR:ROWNUMBR,*EL,*V2LON,"No",*HOFF
              GOTO      GETA9000
            ENDIF
          ENDIF
.
          GOTO      GETA1000
.
GETA9000  MOVE      ZERO,EXIT
          GOTO      GETA9999
.
GETA9100  MOVE      ONE,EXIT
.
GETA9999  RETURN
+
.******************************************************************************
.*                               PROC0000              Called by: MAIN0000    *
.*                  Get option settings from user.                            *
.******************************************************************************
.
PROC0000  MOVE      FORTY3,COLNUMBR
          MOVE      FOUR,ROWNUMBR
.
PROC0100  DISPLAY   *P1:3,*EF,*P1:ROWNUMBR:
                    "Using External Referrals? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC0101
.
          MOVE      "0",XUSRUERF
          GOTO      PROC0200
.
PROC0101  MOVE      "1",XUSRUERF
.
PROC0200  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Using Residential Admission? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC0201
.
          MOVE      "0",XUSRURAD
          GOTO      PROC0300
.
PROC0201  MOVE      "1",XUSRURAD
.
PROC0300  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Using Assessments? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC0301
.
          MOVE      "0",XUSRUASS
          GOTO      PROC0400
.
PROC0301  MOVE      "1",XUSRUASS
.
PROC0400  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Using Request Appointments? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC0401
.
          MOVE      "0",XUSRURAP
          GOTO      PROC0500
.
PROC0401  MOVE      "1",XUSRURAP
.
PROC0500  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Display Last 5 Contacts? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC0501
.
          MOVE      "0",XUSRDLAS
          GOTO      PROC0600
.
PROC0501  MOVE      "1",XUSRDLAS
.
PROC0600  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Display Clinical Documentation? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC0601
.
          MOVE      "0",XUSRDCLI
          GOTO      PROC0700
.
PROC0601  MOVE      "1",XUSRDCLI
.
PROC0700  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Display Referral Action Audit? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC0701
.
          MOVE      "0",XUSRDRAA
          GOTO      PROC0800
.
PROC0701  MOVE      "1",XUSRDRAA
.
PROC0800  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Display Letter History? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC0801
.
          MOVE      "0",XUSRDLET
          GOTO      PROC0900
.
PROC0801  MOVE      "1",XUSRDLET
.
PROC0900  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Using Encounter Medications? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC0901
.
          MOVE      "0",XUSRUEME
          GOTO      PROC1000
.
PROC0901  MOVE      "1",XUSRUEME
.
PROC1000  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Using Encounter Interventions? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC1001
.
          MOVE      "0",XUSRUEIN
          GOTO      PROC1100
.
PROC1001  MOVE      "1",XUSRUEIN
.
PROC1100  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Using Encounter Supplies? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC1101
.
          MOVE      "0",XURSUESU
          GOTO      PROC9999
.
PROC1101  MOVE      "1",XURSUESU
.
PROC1200  ADD       ONE,ROWNUMBR
          DISPLAY   *P1:ROWNUMBR,"Using Additional HCPs? (Yes/No): ";
          CALL      GETA0000
          BRANCH    EXIT,PROC1201
.
          MOVE      "0",XUSRUAHP
          GOTO      PROC9999
.
PROC1201  MOVE      "1",XUSRUAHP
.
PROC9999  RETURN
+
.******************************************************************************
.*                               SAVE0000              Called by: MAIN0000    *
.*                  Updates all department records with the option values.    *
.******************************************************************************
.
SAVE0000  PACK      KEY3,SP70
          CALL      RSALDEP1
SAVE1000  CALL      RKALDEP1
          BRANCH    OVRCD,SAVE9999
.
          CALL      SETV0000            * copy user settings to file variables 
          CALL      UPALDEP1            * update record
          GOTO      SAVE1000
.
SAVE9999  RETURN
+
.******************************************************************************
.*                               SETV0000              Called by: SAVE0000    *
.*                  Copies all the user option values to file variables.      *
.******************************************************************************
.
SETV0000  MOVE      XUSRUERF,ALDEUERF
          MOVE      XUSRURAD,ALDEURAD
          MOVE      XUSRUASS,ALDEUASS
          MOVE      XUSRURAP,ALDEURAP
          MOVE      XUSRDLAS,ALDEDLAS
          MOVE      XUSRDCLI,ALDEDCLI
          MOVE      XUSRDRAA,ALDEDRAA
          MOVE      XUSRDLET,ALDEDLET
          MOVE      XUSRUEME,ALDEUEME
          MOVE      XUSRUEIN,ALDEUEIN
          MOVE      XURSUESU,ALDEUESU
          MOVE      XUSRUAHP,ALDEUAHP
.
SETV9999  RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       ALLDEPIO/INC

.-------------------------------------------------------------------------------
. Program       : CLIWEB11 
.
. Function      : Result Load Server
.
. Modifications : 
.-------------------------------------------------------------------------------
. V11.02.02 02/08/2022  Bella Turco    TSK 0921957
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V11.02.01 09/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V10.11.03 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.02 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
. V10.06.01 12/03/2015  Steve Armstrong CAR 314108
.           Removed reference to PATCGPFD (No Longer in use)
.-------------------------------------------------------------------------------
. V10.04.02 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.-------------------------------------------------------------------------------
. V10.03.05 30/06/2013  Davin             CAR 283202
.           Recompiled for changes to hl7 messaging
. V10.03.04 01/05/2013  Steve Armstrong  CAR 284376
.           Recompiled for changes to DGCLICUP.
. V10.03.03 23/04/2013  Ebon Clements  CAR 261630
.           Removed port number from temp file name
. V10.03.02 03/04/2013  Davin          CAR 270648
.           Recompiled for changes to HL7 messaging includes
. V10.03.01 16/01/2012  Sandra Barcham  CAR 256563
.           Recompiled for PATMASTM
. V10.02.01 31/03/2011  Jill Parkinson
.           Fixed ENCDCHAR value from ! to ~
. V9.11.02  28/11/2008  Jill Habasque   CAR 181097
.           Fixed child/adult ref ranges and decimal numeric results
. V9.11.01  05/11/2008  Peter McMullen CAR 174725
.           convert internal javascript to W3C standards
. V9.10.01  11/09/2008  Jill Habasque   CAR 174243
.           Changed tickboxes to radio buttons
. V9.10.00  21/07/2008  Jill Habasque   CAR 174243
.           New Program
.-------------------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       WEBDATDF
.
          INC       RESIDTTD/INC 
          INC       PMSALAFD/INC 
          INC       APSMASFD/INC
          INC       PATFN1FD/INC 
          INC       EMRUNKFD/INC      * Unknowns Patient File
          INC       PATALRFD/INC      * Patient Alerts Code File
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PMSVX1FD/INC
          INC       PATIN1FD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCGTD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PMSQPTFD/INC
          INC       PATCALAG/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC
          INC       PATPR1FD/INC      * Pre-admission master file
          INC       PATVISFD/INC
          INC       PATICDFD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       PATVADFD/INC
          INC       PATASFFD/INC
          INC       PATDADFD/INC
          INC       PATCMCFD/INC
          INC       PATITMFD/INC
          INC       PMSALNFD/INC      * alert comment file
          INC       PMSCLPFD/INC      * Patient Clinical Pathways
          INC       PMSCCDFD/INC      * Concession Card Details
.
          INC       RESIDTFD/INC
          INC       RESLNKFD/INC
          INC       PATMI1FD/INC
          INC       PATNIDFD/INC
          INC       PATWR1FD/INC
          INC       PATDO1FD/INC
          INC       TFILEVAR/INC
          INC       MRTMASFD/INC
          INC       OUTSITFD/INC      * Outpatient Site File 
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       OBSPCOFD/INC
          INC       OBSPCTFD/INC
.
.  CGI Variables
.
ABNMFLAG  DIM       1
BLNKFLAG  FORM      1
NEXTPAGE  FORM      1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
STARTKEY  DIM       65      * Starting Key
CURRDATE  DIM       8       * Current Date
COUNT     FORM      3
SCOUNT    FORM      3
OBXCOUNT  FORM      3
REFRANGE  DIM       20
.
CATdg     INIT      "dg"
CATWI     INIT      "wi"
CSEC      DIM       2
CMDLINE   DIM       127       * Command line instruction
DASH      INIT      "-"
DATOFRES  DIM       14
DATPROCD  DIM       14
DATTAKEN  DIM       14
DELETKEY  DIM       16
DIM70     DIM       70
DATETIME  DIM       16
DISPDATE  DIM       11
DIM3      DIM       3
DIM5      DIM       5
DIM10     DIM       10
DOCTCODE  DIM       6
EMRFLAG   FORM      1
F12       FORM      12
FORM32    FORM      3.2
FORM8     FORM      8
HEADERTY  DIM       1
.
HEADA001  DIM       20        * Specimen
HEADA002  DIM       20        * Fluids
HEADA003  DIM       8         * Date taken
HEADA004  DIM       8         * Time taken
HEADA005  DIM       8         * Date processed 
HEADA006  DIM       8         * Time processed
HEADA007  DIM       8         * Date of result
HEADA008  DIM       8         * Time of result
HEADA009  DIM       10        * Ordered by
HEADA010  DIM       20        * Collector
HEADA011  DIM       100       * Clinical Details
HEADA012  DIM       20        * Lab reference number
.
HEADB001  DIM       20        * Examination Type
HEADB002  DIM       50        * Region
HEADB003  DIM       8         * Date taken
HEADB004  DIM       8         * Time taken
HEADB005  DIM       1         * Abnormal result
HEADB006  DIM       10        * Ordered by
HEADB007  DIM       100       * Clinical Details
HEADB008  DIM       8         * LMP Date
HEADB009  DIM       8         * EDD Date
HEADB010  DIM       2         * Gestation in weeks from EDD
HEADB011  DIM       8         * Dates Sure/Unsure 
HEADB012  DIM       2         * Gestation today in weeks
.
ITEMNUM   FORM      3
LINECNT   FORM      1
LASTITEM  FORM      3
MBSDESC   DIM       70
NMPNUMB   DIM       20
ORDRPROV  DIM       100
OUTVAR    DIM       127
PACKLINK  DIM       70      * Test
PSAGETYP  DIM       6                        * age type description
PSAGECON  DIM       3                        * age converted value
SQUOTE    INIT      "'"
REFRDESC  DIM       14
RESULTTY  DIM       3
RESVALUE  DIM       20
RESTFILE  FILE      ASCII, FIXED=500        * result text file
SERVICID  DIM       100
.
OBXARRAY  DIM       50[99]
OBXITEMA  DIM       3[99]
.
ENCDCHAR  INIT      "^~\&"
AA        INIT      "AA"
BLANK     INIT      ""
CARAT     INIT      "^"
CLININFO  INIT      "Clinical Info:"
CLINGATD  INIT      "Clinical GA today:"
DATES     INIT      "Dates:"
DET       INIT      "Detected"
EDD       INIT      "EDD from LMP:"
EXAMTYPE  INIT      "Examination Type:"
FT        INIT      "FT"
GA        INIT      "GA:"
LMP       INIT      "LMP:"
LONGTEXT  INIT      "LONGTEXT"
MSA       INIT      "MSA"
MSH       INIT      "MSH"
NTE       INIT      "NTE"
OBR       INIT      "OBR"
OBX       INIT      "OBX"
ORU       INIT      "ORU"
PID       INIT      "PID"
PIPE      INIT      "|"
SETID     INIT      "0001"
SETID2    INIT      "0002"
SETID3    INIT      "0003"
SLASHBR   INIT      "\.br\"
SPECTYPE  INIT      "Specimen Type:"
ST        INIT      "ST"
WKS       INIT      "wks"
LONGTFIL  FILE      ASCII,FIXED=128
LONGTFNM  DIM       8
.---------------------------------------------------------------------------
PRGID     INIT      "CLIWEB11"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Result Load"
.---------------------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      RIDM0000         * Maintain residtaf
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      CRTRES00         * Create a result for a UR/Visit
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40,NXTPAG50
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      PREDIR00      * Redirect content URL
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages 
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG50  CALL      WINLS000
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------
. Refresh Parent and Close Window
.------------------------------------------------------------
WINLS000  CALL      OPENHTML
          BRANCH    EXIT,WINCLS99
          WRITE     HTMLFILE;"<script type=#"text/javascript#" ":
                             "src=#"../js/Standard.js#"></script>":
                             "<script type=#"text/javascript#" ":
                             "src=#"../js/Custom.js#"></script>":
                             "<script type=#"text/javascript#"> ":
                             " if (self.name.match(/PopUpFrame/)) {":
                             "  reloadParentFrame();}":
                             "</script>"
          CALL      CLOSHTML
WINLS999  RETURN
.------------------------------------------------------------------------------
.  Redirect Content URL
.------------------------------------------------------------------------------ 
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#"}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);": 
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.-------------------------------------------------------------------------------
. Open Files and Initialize Variables
.-------------------------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open Routine in PATMASTM
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      WEBSECA1,"websecaf"
          OPEN      WEBSECA3,"websecaf"
.
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          READ      CONTROLF,SEVENTY7;*96,PTCNASEC,PTCNLIFE,PTCNLFCH
          READ      CONTROLF,TEN;*250,CAUDA1
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND14;*87,PTCNRSAP,*95,PTCNRSFC,*103,PTCNRRAP:
                                    *111,PTCNRRFC,*119,PTCNRMIC,*131,PTCNRHLV
          READ      CONTROLF,TWENTY1;*238,PTCNAGEC
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          OPEN      RESIDTA1,"residtaf"
          OPEN      RESIDTA2,"residtaf"
          OPEN      RESIDTA3,"residtaf"
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FILENAME
.
INIT9999  RETURN
.-------------------------------------------------------------------------------
. Clear Parameters
.-------------------------------------------------------------------------------
CLRPAR00  MOVE      ZERO,STARTNUM 
          MOVE      ZERO,NORECORD
          MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,ADMISSNO
          MOVE      SP70,URNUMBER 
          MOVE      ZERO,NEXTPAGE
          MOVE      SP70,RESULTTY
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
.
          PACK      HEADA001,Z70
          PACK      HEADA002,Z70
          PACK      HEADA003,Z70
          PACK      HEADA004,Z70
          PACK      HEADA005,Z70
          PACK      HEADA006,Z70
          PACK      HEADA007,Z70
          PACK      HEADA008,Z70
          PACK      HEADA009,Z70
          PACK      HEADA010,Z70
          PACK      HEADA011,Z70,Z70
          PACK      HEADA012,Z70
.
          PACK      HEADB001,Z70
          PACK      HEADB002,Z70
          PACK      HEADB003,Z70
          PACK      HEADB004,Z70
          PACK      HEADB005,Z70
          PACK      HEADB006,Z70
          PACK      HEADB007,Z70,Z70
          PACK      HEADB008,Z70
          PACK      HEADB009,Z70
          PACK      HEADB010,Z70
          PACK      HEADB011,Z70
          PACK      HEADB012,Z70
.
          CALL      CREID000
.
          MOVE      ZERO,SCOUNT
          MOVE      ONE,COUNT
          WHILE     COUNT < 100
            PACK      OBXARRAY[COUNT],SP70
            PACK      OBXITEMA[COUNT],SP70
            ADD       ONE,COUNT
          DO
.
CLRPAR99  RETURN
.-------------------------------------------------------------------------------
. Set Parameters
.-------------------------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.          
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.   
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF  
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
. 
          MATCH     "keywords=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEYWORDS
            GOTO      SETPAR99
          ENDIF
. 
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
. 
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "deletkey=",QRYNAME
          IF        @EQUAL
            PACK      DELETKEY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "resultty=",QRYNAME
          IF        @EQUAL
            PACK      RESULTTY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "reidt",QRYNAME
          IF        @EQUAL
            CALL      SREID000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "heada",QRYNAME
          IF        @EQUAL
            CALL      SHEDA000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "headb",QRYNAME
          IF        @EQUAL
            CALL      SHEDB000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "obxit",QRYNAME
          IF        @EQUAL
            UNPACK    QRYNAME,DIM5,DIM3
            ADD       ONE,SCOUNT
            PACK      OBXARRAY[SCOUNT],QRYDATA
            PACK      OBXITEMA[SCOUNT],DIM3
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "longt",QRYNAME
          IF        @EQUAL
            UNPACK    QRYNAME,DIM5,DIM3
            CALL      SETACM00
            ADD       ONE,SCOUNT
            PACK      OBXARRAY[SCOUNT],LONGTEXT
            PACK      OBXITEMA[SCOUNT],DIM3
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.
.------------------------------------------------------------
.   Set Parameters for result header fields
.------------------------------------------------------------
SHEDA000  MOVE      ZERO,EXIT 
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SHEDA010,SHEDA020,SHEDA030,SHEDA040,SHEDA050:
                       SHEDA060,SHEDA070,SHEDA080,SHEDA090,SHEDA100:
                       SHEDA110,SHEDA120
.
          MOVE      ONE,EXIT
          GOTO      SHEDA999
.
SHEDA010  PACK      HEADA001,QRYDATA,SP70
          GOTO      SHEDA999
.
SHEDA020  PACK      HEADA002,QRYDATA,SP70
          GOTO      SHEDA999
.
SHEDA030  MATCH     SP70,QRYDATA
          GOTO      SHEDA999 IF EQUAL
          GOTO      SHEDA999 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00              * Set CMON from MTHNAM3
          PACK      HEADA003,CCENT,CYEAR,CMON,CDAY
          GOTO      SHEDA999
.
SHEDA040  PACK      HEADA004,QRYDATA,SP70
          GOTO      SHEDA999
.
SHEDA050  MATCH     QRYDATA,SP70
          GOTO      SHEDA999 IF EQUAL
          GOTO      SHEDA999 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00              * Set CMON from MTHNAM3
          PACK      HEADA005,CCENT,CYEAR,CMON,CDAY
          GOTO      SHEDA999
.
SHEDA060  PACK      HEADA006,QRYDATA,SP70
          GOTO      SHEDA999
.
SHEDA070  MATCH     QRYDATA,SP70
          GOTO      SHEDA999 IF EQUAL
          GOTO      SHEDA999 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00              * Set CMON from MTHNAM3
          PACK      HEADA007,CCENT,CYEAR,CMON,CDAY
          GOTO      SHEDA999
.
SHEDA080  PACK      HEADA008,QRYDATA,SP70
          GOTO      SHEDA999
.
SHEDA090  PACK      HEADA009,QRYDATA,SP70
          GOTO      SHEDA999
.
SHEDA100  PACK      HEADA010,QRYDATA,SP70
          GOTO      SHEDA999
.
SHEDA110  PACK      HEADA011,QRYDATA,SP70
          GOTO      SHEDA999
.
SHEDA120  PACK      HEADA012,QRYDATA,SP70
          GOTO      SHEDA999
.
SHEDA999  RETURN
+
.------------------------------------------------------------
.   Set Parameters for result header b fields
.------------------------------------------------------------
SHEDB000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SHEDB010,SHEDB020,SHEDB030,SHEDB040,SHEDB050:
                       SHEDB060,SHEDB070,SHEDB080,SHEDB090,SHEDB100:
                       SHEDB110,SHEDB120
.
          MOVE      ONE,EXIT
          GOTO      SHEDB999
.
SHEDB010  PACK      HEADB001,QRYDATA,SP70
          GOTO      SHEDB999
.
SHEDB020  PACK      HEADB002,QRYDATA,SP70
          GOTO      SHEDB999
.
SHEDB030  MATCH     QRYDATA,SP70
          GOTO      SHEDB999 IF EQUAL
          GOTO      SHEDB999 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00              * Set CMON from MTHNAM3
          PACK      HEADB003,CCENT,CYEAR,CMON,CDAY
          GOTO      SHEDB999
.
SHEDB040  PACK      HEADB004,QRYDATA,SP70
          GOTO      SHEDB999
.
SHEDB050  PACK      HEADB005,QRYDATA,SP70
          GOTO      SHEDB999
.
SHEDB060  PACK      HEADB006,QRYDATA,SP70
          GOTO      SHEDB999
.
SHEDB070  PACK      HEADB007,QRYDATA,SP70
          GOTO      SHEDB999
.
SHEDB080  MATCH     QRYDATA,SP70
          GOTO      SHEDB999 IF EQUAL
          GOTO      SHEDB999 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00              * Set CMON from MTHNAM3
          PACK      HEADB008,CCENT,CYEAR,CMON,CDAY
          GOTO      SHEDB999
.
SHEDB090  MATCH     QRYDATA,SP70
          GOTO      SHEDB999 IF EQUAL
          GOTO      SHEDB999 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00              * Set CMON from MTHNAM3
          PACK      HEADB009,CCENT,CYEAR,CMON,CDAY
          GOTO      SHEDB999
.
SHEDB100  PACK      HEADB010,QRYDATA,SP70
          GOTO      SHEDB999
.
SHEDB110  PACK      HEADB011,QRYDATA,SP70
          GOTO      SHEDB999
.
SHEDB120  PACK      HEADB012,QRYDATA,SP70
          GOTO      SHEDB999
.
SHEDB999  RETURN
+
.-------------------------------------------------------------------------------
. Result Item Details Maintenance
.-------------------------------------------------------------------------------
RIDM0000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
. 
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      RIDM0400 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      RIDM0100 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      RIDM0200 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      RIDM0300 IF EQUAL
.
          GOTO      RIDM0910
.
.         ************ ADD FORMACTION ***********
.
RIDM0100  PACK      KEY6,REIDT001,REIDT002,SP70
          CALL      RDREIDT1
          IF        OVRCD<>1
            GOTO      RIDM0920
          ENDIF
.
          CALL      CLREID00
.                                           
          CALL      MVREID00
          PACK      KEY6,REIDRTYP,REIDIORD,SP70
          CALL      RAREIDT1
          IF        OVRCD=1
            CALL      WRREIDT1
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      RIDM9999
.
.         ************ UPDATE FORMACTION **********
.
RIDM0200  PACK      KEY6,REIDT001,REIDT002,SP70
          CALL      RDREIDT1                
          BRANCH    OVRCD,RIDM0930   
.
          CALL      MVREID00
          CALL      UPREIDT1
.
          MOVE      ZERO,EXIT
          GOTO      RIDM9999
.
.         ************ DELETE FORMACTION **********
.
RIDM0300  PACK      KEY6,REIDT001,REIDT002,SP70
          CALL      RDREIDT1                
          BRANCH    OVRCD,RIDM0930   
.
          CALL      DEREIDT1
.
          MOVE      ZERO,EXIT
          GOTO      RIDM9999
.
.         ************ DISPLAY FORMACTION **********
.
RIDM0400  PACK      KEY6,REIDT001,REIDT002,SP70
          CALL      RDREIDT1 
          IF        OVRCD=1
            CALL      CLREID00
          ENDIF
. 
          CLEAR     WEBTITLE
          APPEND    "Result Item Detail Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00
          BRANCH    EXIT,RIDM9999
.
          CALL      WEBBOD00
          CALL      WEBEND00
.
          MOVE      ONE,EXIT
          GOTO      RIDM9999
.
RIDM0910  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RIDM9999
.
RIDM0920  MOVE      "Result Item detail record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RIDM9999
.
RIDM0930  MOVE      "Result Item detail record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RIDM9999
.
RIDM9999  RETURN 
.
.-------------------------------------------------------------------------------
. Process Fields called from WEBBOD00 
.-------------------------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20
          GOTO      PROFLD99
.
.      Patient Alert Details
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      REID0000
          GOTO      PROFLD99
.
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22,PROFLD23
          GOTO      PROFLD99
.
PROFLD21  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD22  CALL      CRTM0000
          GOTO      PROFLD99
.
PROFLD23  CALL      REID0000
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. Table of Patient Clinical Pathways
.------------------------------------------------------------
CLPTB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          PACK      KEY19,ADMISSNO,SP70
          CALL      RSPMCLP1
CLPTB100  CALL      RKPMCLP1
          BRANCH    OVRCD,CLPTB999
.
          MATCH     ADMISSNO,PMCPADMN        * Admission No
          GOTO      CLPTB999 IF NOT EQUAL
.
          PACK      PACKLINK,PMCPADMN,SQUOTE,COMMA,SQUOTE,PMCPCLPT,SQUOTE:
                             COMMA,SQUOTE,PMCPUNIQ,SQUOTE,COMMA,SQUOTE:
                             PMCPACTV
.
          CLEAR     HTMLLINK
          APPEND    "javascript:ShowDetail('",HTMLLINK
          APPEND    PACKLINK,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      "PW",CATEGORY        * Clinical Pathway
          MOVE      PMCPCLPT,CODE
          CALL      GETCOD00
.
          PACK      KEY9,PMCPDIG1,SP70   * Diagnosis 1
          CALL      RDPTICD1
          IF        OVRCD=1 | ICDRDVER=1
            MOVE      "Invalid Diagnosis Code",DDESC
          ENDIF

          UNPACK    PMCPDDT1,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      PMCPACTV,F1      * 1 = Active
          BRANCH    F1,CLPTB200
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                     "#"<strike>",TDESC,"</strike>#",":    * Clinical Pathway
                     "#"<strike>",DDESC,"</strike>#",":    * Diagnosis 1 Desc
                     "#"<strike>",KEY11,"</strike>#");"    * Diagnosis 1 Date
.
          GOTO      CLPTB100 
.
CLPTB200  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",TDESC,"#",":        * Clinical Pathway
                             "#"",DDESC,"#",":        * Diagnosis 1 Desc
                             "#"",KEY11,"#");"        * Diagnosis 1 Date
.
          GOTO      CLPTB100 
.
CLPTB999  RETURN
.
.------------------------------------------------------------
. Clear Result Item Details Table
.------------------------------------------------------------
CLREID00  PACK      REIDRTYP,SP70
          PACK      REIDIORD,SP70
          PACK      REIDITEM,SP70
          PACK      REIDIDES,SP70
          PACK      REIDRRAN,SP70
          PACK      REIDRRTY,SP70
          MOVE      ZERO,REIDLRRM
          MOVE      ZERO,REIDHRRM
          MOVE      ZERO,REIDLRRF
          MOVE      ZERO,REIDHRRF
          PACK      REIDRRDE,SP70
          PACK      REIDUMEA,SP70
          PACK      REIDWEBC,SP70
          PACK      REIDDATC,SP70
          PACK      REIDTIMC,SP70
          PACK      REIDWEBU,SP70
          PACK      REIDDATU,SP70
          PACK      REIDTIMU,SP70
          PACK      REIDSPAR,SP70
.
          RETURN
+
.------------------------------------------------------------
. Create Result for a UR/Visit
.------------------------------------------------------------
CRTRES00  CALL      CLREID00
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,CRTRES90
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          MATCH     SP70,ADMISSNO
          IF        !@EQUAL
            PACK      KEY8,ADMISSNO,SP20
            CALL      RDPMVX11
            IF        OVRCD=1
              CALL      CLPMSVX1
            ENDIF
          ENDIF
.
          MATCH     ANSA,UPDTTYPE
          GOTO      CRTRES10 IF EQUAL
          GOTO      CRTRES80
.
CRTRES10  CALL      ADRE0000                * Add new result
          GOTO      CRTRES99
.
CRTRES80  CALL      PATNAA00                * format name without bold tags
.
          CLEAR     WEBTITLE
          APPEND    "Create Result for ",WEBTITLE
          APPEND    PATFNAME,WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,CRTRES99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      CRTRES99
.
CRTRES90  MOVE      "Invalid Patient U/R",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CRTRES99
.
CRTRES99  RETURN
+
.------------------------------------------------------------
. Output result template
.------------------------------------------------------------
CRTM0000  MATCH     SP70,RESULTTY
          GOTO      CRTM9999 IF EQUAL
.
          MOVE      ZERO,LINECNT
          MOVE      ZERO,ITEMNUM
.
          PACK      KEY6,RESULTTY,SP70     * output result details fields
          CALL      RSREIDT1
CRTM1000  CALL      RKREIDT1
          BRANCH    OVRCD,CRTM9999
.
          MATCH     RESULTTY,REIDRTYP
          GOTO      CRTM9000 IF NOT EQUAL
.
          TYPE      REIDRITY
          GOTO      CRTM1000 IF NOT EQUAL
          MOVE      REIDRITY,F1
.
          ADD       ONE,LINECNT
          IF        LINECNT>2
            MOVE      ONE,LINECNT
          ENDIF
.                                        * text fields only allow 1 to a line
          IF        F1=3|F1=4
            MOVE      ONE,LINECNT
          ENDIF
.
          IF        LINECNT=1
            WRITE     HTMLFILE;"<tr>";
          ENDIF
.
          WRITE     HTMLFILE;"<td>",REIDIDES,"</td>";
.
          ADD       ONE,ITEMNUM
          MOVE      ITEMNUM,DIM3
          REP       " 0",DIM3
.
          BRANCH    F1,CRTM1100,CRTM1200,CRTM1300,CRTM1400,CRTM1500:
                       CRTM1600,CRTM1700,CRTM1800,CRTM1900
          GOTO      CRTM1000
.
CRTM1100  WRITE     HTMLFILE;"<td><input type=text class=Number name=":
                             "obxit",REIDIORD," min=0 max=999.99 ":
                             "onblur=#'RoundNumber(this,2);#'>":
                             SP1,REIDUMEA;
          MATCH     SP70,REIDRRDE
          IF        !@EQUAL
            WRITE     HTMLFILE;SP1,LBRACK,REIDRRDE,RBRACK,"</td>" 
          ENDIF
.
          IF        LINECNT=2
            WRITE     HTMLFILE;"</tr>"
          ENDIF
          GOTO      CRTM1000
.
CRTM1200  WRITE     HTMLFILE;"<td><input type=radio name=obxit",REIDIORD:
                             " value=#'Yes#'>Yes":
                             "<input type=radio name=obxit",REIDIORD:
                             " value=#'No#'>No":
                             "</td>"
          IF        LINECNT=2
            WRITE     HTMLFILE;"</tr>"
          ENDIF
          GOTO      CRTM1000
.
CRTM1300  WRITE     HTMLFILE;"<td colspan=3><input type=text name=":
                             "obxit",REIDIORD," size=80></td></tr>"
          MOVE      TWO,LINECNT
          GOTO      CRTM1000
.
CRTM1400  WRITE     HTMLFILE;"<td colspan=3><textarea wrap=hard name=longt":
                             REIDIORD," rows=4 cols=70></textarea></td></tr>"
          MOVE      TWO,LINECNT
          GOTO      CRTM1000
.
CRTM1500  WRITE     HTMLFILE;"<td><input type=radio name=obxit",REIDIORD:
                             " value=#'Sensitive#'>Sensitive":
                             "<input type=radio name=obxit",REIDIORD:
                             " value=#'Resistant#'>Resistant":
                             "<input type=radio name=obxit",REIDIORD:
                             " value=#'Intermediate#'>Intermediate":
                             "</td>"
          IF        LINECNT=2
            WRITE     HTMLFILE;"</tr>"
          ENDIF
          GOTO      CRTM1000
.
CRTM1600  WRITE     HTMLFILE;"<td><input type=radio name=obxit",REIDIORD:
                             " value=#'Positive#'>Positive":
                             "<input type=radio name=obxit",REIDIORD:
                             " value=#'Negative#'>Negative</td>"
          IF        LINECNT=2
            WRITE     HTMLFILE;"</tr>"
          ENDIF
          GOTO      CRTM1000
.
CRTM1700  WRITE     HTMLFILE;"<td><input type=radio name=obxit",REIDIORD:
                             " value=#'Detected#'>Detected":
                             "<input type=radio name=obxit",REIDIORD:
                             " value=#'Not Detected#'>Not Detected</td>"
          IF        LINECNT=2
            WRITE     HTMLFILE;"</tr>"
          ENDIF
          GOTO      CRTM1000
.
CRTM1800  WRITE     HTMLFILE;"<td><input type=radio name=obxit",REIDIORD:
                             " value=#'Reactive#'>Reactive":
                             "<input type=radio name=obxit",REIDIORD:
                             " value=#'Non-Reactive#'>Non-Reactive</td>"
          IF        LINECNT=2
            WRITE     HTMLFILE;"</tr>"
          ENDIF
          GOTO      CRTM1000
.
CRTM1900  WRITE     HTMLFILE;"<td><select name=obxit",REIDIORD,">":
                             "<option></option>"
          MOVE      "BO",CATEGORY
          MOVE      SP70,CODE
          CALL      CODSEL00
          WRITE     HTMLFILE;"</select></td>"
          IF        LINECNT=2
            WRITE     HTMLFILE;"</tr>"
          ENDIF
          GOTO      CRTM1000
.
CRTM9000  
CRTM9999  RETURN
+
.------------------------------------------------------------
. Add new result to a patient
.------------------------------------------------------------
ADRE0000  CALL      CREAT000                * Create text file
.
          CALL      ADDMES00                * Add message details
          BRANCH    EXIT,ADRE9999
.
          CALL      MVMES000                * Execute us1 script to load result
          GOTO      ADRE9999
.
ADRE9000  MOVE      "Unable to create text file",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ADRE9999
.
ADRE9999  CLOSE     RESTFILE,DELETE
          RETURN
+
.------------------------------------------------------------
. Move result by executing cliweb11.us1 <filename> <message id>
.------------------------------------------------------------
MVMES000  CLEAR     CMDLINE
          APPEND    "cliweb11.us1 ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    PTCNRMIC,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVMES999  RETURN
+
.------------------------------------------------------------
. Create text file for result
.------------------------------------------------------------
CREAT000  CLOSE     RESTFILE
          PREP      RESTFILE,FILENAME
.
CREATE99  RETURN
+
.------------------------------------------------------------
. Add message details          
.------------------------------------------------------------
ADDMES00  MOVE      ZERO,EXIT
.
          CALL      WRMSH000           * Write MSH line
          CALL      WRMSA000           * Write MSA line
          CALL      WRPID000           * Write PID line
          CALL      WROBR000           * Write OBR line
          CALL      WRNTE000           * Write NTE lines
          CALL      WROBX000           * Write OBX lines
.
          WRITE     RESTFILE,SEQ;"^\"
.
ADDMES99  RETURN
+
.------------------------------------------------------------
. Write MSH line
.------------------------------------------------------------
WRMSH000  CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          UNPACK    CTIMEIS,CHOUR,DIM1,CMIN,DIM1,CSEC
          PACK      DATETIME,CCC,CYY,CMM,CDD,CHOUR,CMIN,CSEC
          REP       " 0",DATETIME
.
          CALL      GMESID00     * generate next message id
.
          CALL      STRMSH00     * strip trailing spaces for MSH
.
          WRITE     RESTFILE,SEQ;*+,MSH,PIPE,ENCDCHAR,PIPE,*+,PTCNRSAP,PIPE:
                                 *+,PTCNRSFC,PIPE,*+,PTCNRRAP,PIPE,*+,PTCNRRFC:
                                 PIPE,DATETIME,PIPE,PIPE,ORU,PIPE,*+,PTCNRMIC:
                                 PIPE,ANSP,PIPE,*+,PTCNRHLV
.
WRMSH999  RETURN
+
.------------------------------------------------------------
. Write MSA line
.------------------------------------------------------------
WRMSA000  WRITE     RESTFILE,SEQ;*+,MSA,PIPE,AA,PIPE,PTCNRMIC
.
WRMSA999  RETURN
+
.------------------------------------------------------------
. Write PID line
.------------------------------------------------------------
WRPID000  SQUEEZE   PURNO
          STRIP     PURNO
          STRIP     PSNAME
          STRIP     PGNAME
          REP       " ^",PGNAME
.
          WRITE     RESTFILE,SEQ;*+,PID,PIPE,PIPE,PURNO,PIPE,PURNO,PIPE:
                                    PIPE,PSNAME,CARAT,PGNAME,PIPE,PIPE:
                                    PBDATE,PIPE,PSEX
.
WRPID999  RETURN
+
.------------------------------------------------------------
. Write OBR line
.------------------------------------------------------------
WROBR000  MOVE      ZERO,EXIT
          MOVE      ZERO,HEADERTY
          STRIP     PTCNRMIC
          PACK      HEADA001,HEADA001,SP70
          MATCH     HEADA001,Z70
          IF        @EQUAL
            PACK      HEADA001,SP70
          ENDIF
          STRIP     HEADA001
          PACK      HEADA012,HEADA012,SP70
          MATCH     HEADA012,Z70
          IF        @EQUAL
            PACK      HEADA012,SP70
          ENDIF
          MATCH     SP70,HEADA012
          IF        @EQUAL
            PACK      HEADA012,PTCNRMIC,SP70
          ENDIF
          STRIP     HEADA012
.
          MOVE      SP70,SERVICID
          PACK      KEY5,CATdg,RESULTTY,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,WROBR900
.
          MOVE      TCINDC1,HEADERTY
          PACK      SERVICID,THCSCOD,CARAT,TDESC
          STRIP     SERVICID
.
          MATCH     "2",HEADERTY
          GOTO      WROBR300 IF EQUAL
.
          MATCH     "3",HEADERTY
          GOTO      WROBR300 IF EQUAL
.
          UNPACK    HEADA003,CCENT,CYEAR,CMON,CDAY
          UNPACK    HEADA004,CHOUR,DIM1,CMIN,DIM1,CSEC
          PACK      DATTAKEN,CCENT,CYEAR,CMON,CDAY,CHOUR,CMIN,CSEC
          REP       " 0",DATTAKEN
.
          STRIP     HEADA010
.
          UNPACK    HEADA005,CCENT,CYEAR,CMON,CDAY
          UNPACK    HEADA006,CHOUR,DIM1,CMIN,DIM1,CSEC
          PACK      DATPROCD,CCENT,CYEAR,CMON,CDAY,CHOUR,CMIN,CSEC
          REP       " 0",DATPROCD
.
          STRIP     HEADA001
.
          MOVE      SP70,ORDRPROV               * format ordering provider
          MATCH     SP70,HEADA009
          GOTO      WROBR100 IF EQUAL
.
          PACK      KEY10,HEADA009,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,WROBR100
.
          STRIP     PMHCHCPC
          STRIP     PMHCSNAM
          STRIP     PMHCGNAM
          REP       " ^",PMHCGNAM
          PACK      ORDRPROV,PMHCHCPC,CARAT,PMHCSNAM,CARAT,PMHCGNAM

.
WROBR100  STRIP     ORDRPROV
.
          UNPACK    HEADA007,CCENT,CYEAR,CMON,CDAY
          UNPACK    HEADA008,CHOUR,DIM1,CMIN,DIM1,CSEC
          PACK      DATOFRES,CCENT,CYEAR,CMON,CDAY,CHOUR,CMIN,CSEC
          REP       " 0",DATOFRES
.
          STRIP     RESULTTY
.
          WRITE     RESTFILE,SEQ;*+,OBR,PIPE,SETID,PIPE,PTCNRMIC,PIPE:
                                    HEADA012,PIPE,SERVICID,PIPE,ANSR,PIPE:
                                    PIPE,DATTAKEN,PIPE,PIPE,PIPE:
                                    HEADA010,PIPE,PIPE,PIPE,PIPE:
                                    DATPROCD,PIPE,HEADA001,PIPE:
                                    ORDRPROV,PIPE,PIPE,PIPE,PIPE:
                                    PIPE,PIPE,DATOFRES,PIPE,PIPE:
                                    RESULTTY,PIPE,ANSF
.
          PACK      RESULTTY,RESULTTY,SP70
          GOTO      WROBR999
.
WROBR300  STRIP     ORDRPROV
.
          UNPACK    HEADB003,CCENT,CYEAR,CMON,CDAY
          UNPACK    HEADB004,CHOUR,DIM1,CMIN,DIM1,CSEC
          PACK      DATTAKEN,CCENT,CYEAR,CMON,CDAY,CHOUR,CMIN,CSEC
          REP       " 0",DATTAKEN
.
          MOVE      SP70,ORDRPROV               * format ordering provider
          MATCH     HEADB009,SP70
          GOTO      WROBR400 IF EQUAL
.
          PACK      KEY10,HEADB006,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,WROBR400
.
          STRIP     PMHCHCPC
          STRIP     PMHCSNAM
          STRIP     PMHCGNAM
          REP       " ^",PMHCGNAM
          PACK      ORDRPROV,PMHCHCPC,CARAT,PMHCSNAM,CARAT,PMHCGNAM
.
WROBR400  STRIP     RESULTTY
.
          WRITE     RESTFILE,SEQ;*+,OBR,PIPE,SETID,PIPE,PTCNRMIC,PIPE:
                                    PTCNRMIC,PIPE,SERVICID,PIPE,ANSR,PIPE:
                                    PIPE,DATTAKEN,PIPE,PIPE,PIPE:
                                    PIPE,PIPE,PIPE,PIPE:
                                    DATPROCD,PIPE,PIPE:
                                    ORDRPROV,PIPE,PIPE,PIPE,PIPE:
                                    PIPE,PIPE,DATOFRES,PIPE,PIPE:
                                    RESULTTY,PIPE,ANSF
.
          PACK      RESULTTY,RESULTTY,SP70
          GOTO      WROBR999
.
WROBR900  MOVE      "Invalid Universal Service ID (Category dg)",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      WROBR999
.
WROBR999  RETURN
+
.------------------------------------------------------------
. Write NTE line
.------------------------------------------------------------
WRNTE000  MATCH     "2",HEADERTY
          GOTO      WRNTE100 IF EQUAL
.
          MATCH     "3",HEADERTY
          GOTO      WRNTE100 IF EQUAL
.
WRNTE050  STRIP     HEADA001
          MATCH     HEADA002,Z70
          IF        @EQUAL
            REP       "~ ",HEADA002
          ENDIF
          STRIP     HEADA002
          MATCH     HEADA011,Z70
          IF        @EQUAL
            REP       "~ ",HEADA011
          ENDIF
          STRIP     HEADA011
.
          WRITE     RESTFILE,SEQ;*+,NTE,PIPE,SETID,PIPE,PIPE:
                                 *+,SPECTYPE,HEADA001,SP1,HEADA002
.
          WRITE     RESTFILE,SEQ;*+,NTE,PIPE,SETID2,PIPE,PIPE:
                                 *+,CLININFO,*+,HEADA011
.
          GOTO      WRNTE999
.
WRNTE100  MATCH     Z70,HEADB001
          GOTO      WRNTE200 IF EQUAL
.
          STRIP     HEADB001
          MATCH     Z70,HEADB002
          IF        @EQUAL
            REP       "~ ",HEADB002
          ENDIF
          STRIP     HEADB002
          MATCH     Z70,HEADB007
          IF        @EQUAL
            REP       "~ ",HEADB007
          ENDIF
          STRIP     HEADB007
.
          WRITE     RESTFILE,SEQ;*+,NTE,PIPE,SETID,PIPE,PIPE:
                                 *+,EXAMTYPE,HEADB001,SP1,*+,HEADB002
.
          WRITE     RESTFILE,SEQ;*+,NTE,PIPE,SETID2,PIPE,PIPE:
                                 *+,CLININFO,*+,HEADB007
.
WRNTE200  MATCH     Z70,HEADB008
          GOTO      WRNTE999 IF EQUAL
.
          UNPACK    HEADB008,CCENT,CYEAR,CMON,CDAY
          PACK      KEY10,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",KEY10
.
          MATCH     Z70,HEADB009
          IF        @EQUAL
            REP       "~ ",HEADB009
          ENDIF
          STRIP     HEADB009
.
          MOVE      SP70,DIM10
          MATCH     HEADB009,SP70
          IF        !@EQUAL
            UNPACK    HEADB009,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
            REP       " 0",DIM10
          ENDIF
.
          MATCH     Z70,HEADB010
          IF        @EQUAL
            REP       "~ ",HEADB010
          ENDIF
          STRIP     HEADB010
.
          MATCH     Z70,HEADB011
          IF        @EQUAL
            REP       "~ ",HEADB011
          ENDIF
          STRIP     HEADB011
.
          WRITE     RESTFILE,SEQ;*+,NTE,PIPE,SETID3,PIPE,PIPE:
                                 *+,LMP,KEY10,SP1,EDD,SP1,DIM10:
                                 *+,SP1,GA,SP1,HEADB010,SP1,WKS,SP1:
                                    DATES,SP1,HEADB011,SP1:
                                    CLINGATD,HEADB012,WKS
.
WRNTE999  RETURN
+
.------------------------------------------------------------
. Write OBX line
.------------------------------------------------------------
WROBX000  MOVE      ZERO,COUNT
          MOVE      ONE,OBXCOUNT
.
WROBX100  ADD       ONE,COUNT
          IF        COUNT>SCOUNT
            GOTO      WROBX999
          ENDIF
.
          PACK      KEY6,RESULTTY,OBXITEMA[COUNT],SP70
          CALL      RDREIDT1
          BRANCH    OVRCD,WROBX100
.
          MATCH     OBXARRAY[COUNT],Z70
          GOTO      WROBX100 IF EQUAL
.
          MOVE      OBXCOUNT,DIM3
          REP       " 0",DIM3
.
          STRIP     REIDITEM
          STRIP     REIDIDES
.
          PACK      OBXARRAY[COUNT],OBXARRAY[COUNT],SP70
.
          MATCH     SP70,OBXARRAY[COUNT]
          GOTO      WROBX100 IF EQUAL
.
          MATCH     "0   ",OBXARRAY[COUNT]
          GOTO      WROBX100 IF EQUAL
.
          TYPE      REIDRITY
          GOTO      WROBX100 IF NOT EQUAL
          MOVE      REIDRITY,F1
.
          BRANCH    F1,WROBX200,WROBX300,WROBX400,WROBX500:
                       WROBX300,WROBX300,WROBX300,WROBX300:
                       WROBX300
          GOTO      WROBX100
.
.     --- numeric result ---
WROBX200  MOVE      SP70,REFRANGE
          MOVE      SP70,ABNMFLAG
          MOVE      ZERO,FORM32
          SQUEEZE   OBXARRAY[COUNT]
          MOVE      OBXARRAY[COUNT],FORM32
.
          MATCH     "1",REIDRRAN
          IF        @EQUAL
            PACK      REFRANGE,REIDLRRM,DASH,REIDHRRM
            IF        FORM32<REIDLRRM
              MOVE      ANSL,ABNMFLAG
            ENDIF
            IF        FORM32>REIDHRRM
              MOVE      ANSH,ABNMFLAG
            ENDIF
          ENDIF
.
          MATCH     "2",REIDRRAN
          IF        @EQUAL
            MATCH     ANSM,PSEX
            IF        @EQUAL
              IF        FORM32<REIDLRRM
                MOVE      ANSL,ABNMFLAG
              ENDIF
              IF        FORM32>REIDHRRM
                MOVE      ANSH,ABNMFLAG
              ENDIF
              PACK      REFRANGE,REIDLRRM,DASH,REIDHRRM
            ELSE
              IF        FORM32<REIDLRRF
                MOVE      ANSL,ABNMFLAG
              ENDIF
              IF        FORM32>REIDHRRF
                MOVE      ANSH,ABNMFLAG
              ENDIF
              PACK      REFRANGE,REIDLRRF,DASH,REIDHRRF
            ENDIF
          ENDIF
          MATCH     "3",REIDRRAN
          IF        @EQUAL
            PACK      AGEDATE,CCC,CYY,CMM,CDD
            CALL      CALCAGE                   * CALCAGE uses PBDATE
            COMPARE   PTCNAGEC,PSAGEYRS         * Child?
            IF        @LESS
              IF        FORM32<REIDLRRF
                MOVE      ANSL,ABNMFLAG
              ENDIF
              IF        FORM32>REIDHRRF
                MOVE      ANSH,ABNMFLAG
              ENDIF
              PACK      REFRANGE,REIDLRRF,DASH,REIDHRRF
            ELSE
              IF        FORM32<REIDLRRM
                MOVE      ANSL,ABNMFLAG
              ENDIF
              IF        FORM32>REIDHRRM
                MOVE      ANSH,ABNMFLAG
              ENDIF
              PACK      REFRANGE,REIDLRRM,DASH,REIDHRRM
            ENDIF
          ENDIF
          STRIP     REFRANGE
.
          WRITE     RESTFILE,SEQ;*+,OBX,PIPE,DIM3,PIPE,ST,PIPE:
                                 *+,REIDITEM,CARAT,*+,REIDIDES:
                                 PIPE,PIPE,*+,OBXARRAY[COUNT]:
                                 PIPE,*+,REIDUMEA,PIPE,*+,REFRANGE,PIPE:
                                 ABNMFLAG,PIPE,PIPE,PIPE,ANSF
          ADD       ONE,OBXCOUNT
          GOTO      WROBX100
.
.--- yes/no, pos/neg, det/not det, react/non reac, sensitivity, blood result ---
WROBX300  MATCH     "9",REIDRITY
          IF        @EQUAL
            PACK      KEY5,ANSB,ANSO,OBXARRAY[COUNT],SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      OBXARRAY[COUNT],TDESC,SP70
            ENDIF
          ENDIF
.
          STRIP     OBXARRAY[COUNT]
          WRITE     RESTFILE,SEQ;*+,OBX,PIPE,DIM3,PIPE,ST,PIPE:
                                 *+,REIDITEM,CARAT,*+,REIDIDES,PIPE:
                                 PIPE,*+,OBXARRAY[COUNT]:
                                 PIPE,PIPE,PIPE,PIPE,PIPE,PIPE,ANSF
          ADD       ONE,OBXCOUNT
          GOTO      WROBX100
.
.     --- text result ---
WROBX400  MATCH     HEADB005,Z70
          IF        @EQUAL
            MOVE      SP70,HEADB005
          ENDIF
          STRIP     HEADB005
.
          PACK      OBXARRAY[COUNT],OBXARRAY[COUNT],SP70
          MATCH     SP70,OBXARRAY[COUNT]
          GOTO      WROBX100 IF EQUAL
.
          WRITE     RESTFILE,SEQ;*+,OBX,PIPE,DIM3,PIPE,FT,PIPE:
                                 *+,REIDITEM,CARAT,*+,REIDIDES,PIPE:
                                 PIPE,*+,OBXARRAY[COUNT]:
                                 PIPE,PIPE,PIPE,*+,HEADB005,PIPE,PIPE,PIPE:
                                 ANSF
          ADD       ONE,OBXCOUNT
          GOTO      WROBX100
.
.     --- long text result ---
WROBX500  MATCH     HEADB005,Z70
          IF        @EQUAL
            MOVE      SP70,HEADB005
          ENDIF
          STRIP     HEADB005
.
          CLEAR     LONGTFNM
          APPEND    "LGT",LONGTFNM
          APPEND    PORT,LONGTFNM
          APPEND    REIDIORD,LONGTFNM
          RESET     LONGTFNM
          REP       " 0",LONGTFNM
          OPEN      LONGTFIL,LONGTFNM
.
          READ      LONGTFIL,SEQ;DIM70
          GOTO      WROBX999 IF OVER
.
          MATCH     SP70,DIM70
          GOTO      WROBX100 IF EQUAL
.
          WRITE     RESTFILE,SEQ;*+,OBX,PIPE,DIM3,PIPE,FT,PIPE:
                                 *+,REIDITEM,CARAT,*+,REIDIDES,PIPE,PIPE:
                                 DIM70,SLASHBR;
.
WROBX550  READ      LONGTFIL,SEQ;DIM70
          GOTO      WROBX600 IF OVER
.
          WRITE     RESTFILE,SEQ;DIM70,SLASHBR;
          GOTO      WROBX550
.
WROBX600  WRITE     RESTFILE,SEQ;PIPE,PIPE,PIPE,*+,HEADB005,PIPE,PIPE,PIPE:
                                 ANSF
          ADD       ONE,OBXCOUNT
          GOTO      WROBX100
WROBX999  RETURN
+
.------------------------------------------------------------
. Generate next message ID
.------------------------------------------------------------
GMESID00  MOVE      ZERO,F12
          MOVE      "114",PRXCODE   * System Lock Sector 114
          CALL      GETSLK00        * Get System Lock of Sector 114
          READ      CONTROLF,HUND14;*119,PTCNRMIC
          MOVE      PTCNRMIC,F12
          ADD       ONE,F12
          MOVE      F12,PTCNRMIC
          WRITAB    CONTROLF,HUND14;*119,PTCNRMIC
          CALL      RELSLK00        * Release System Lock of Sector 10
          SUB       ONE,F12
          MOVE      F12,PTCNRMIC
          REP       " 0",PTCNRMIC
.
          COMPARE   ZERO,F12
          GOTO      GMESID00 IF EQUAL       * invalid number
.      
GMESID99  RETURN
+
.------------------------------------------------------------
. Strip trailing spaces from MSH fields
.------------------------------------------------------------
STRMSH00  STRIP     PTCNRSAP
          STRIP     PTCNRSFC
          STRIP     PTCNRRAP
          STRIP     PTCNRRFC
          STRIP     PTCNRMIC
          STRIP     PTCNRHLV
STRMSH99  RETURN
+
.------------------------------------------------------------
. Clear Inputs
.------------------------------------------------------------
CLRACM00  CLEAR     LONGTFNM
          UNPACK    QRYNAME,DIM5,DIM3
          APPEND    "LGT",LONGTFNM
          APPEND    PORT,LONGTFNM
          APPEND    DIM3,LONGTFNM
          RESET     LONGTFNM
          REP       " 0",LONGTFNM
          PREP      LONGTFIL,LONGTFNM
          RETURN
+
.------------------------------------------------------------
. Set Inputs
.------------------------------------------------------------
SETACM00  CALL      CLRACM00
          MOVE      ZERO,BLNKFLAG
          UNPACK    QRYNAME,KEY5,KEY3
          PACK      QRYDATA,QRYDATA,SP70,SP70
          WRITE     LONGTFIL,SEQ;QRYDATA
.
SETACM10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA * Read CONFFILE sequentially
          GOTO      SETACM99 IF OVER             * If end of file
          MATCH     SP70,QRYNAME                 * If next CGI parameter
          GOTO      SETACM80 IF NOT EQUAL
.
          PACK      QRYDATA,QRYDATA,SP70,SP70
.
          MATCH     SP70,QRYDATA
          IF        @EQUAL
            BRANCH    BLNKFLAG,SETACM10
            MOVE      ONE,BLNKFLAG               * Blank line yes
            SQUEEZE   QRYDATA
            GOTO      SETACM70                   * don't write second blank line
          ENDIF
          IF        @EOS
            BRANCH    BLNKFLAG,SETACM10          * don't write second blank line

            MOVE      ONE,BLNKFLAG               * Blank line yes
            SQUEEZE   QRYDATA
            GOTO      SETACM70
          ENDIF
.
          MOVE      ZERO,BLNKFLAG                * Blank line no
.
SETACM70  WRITE     LONGTFIL,SEQ;QRYDATA
          GOTO      SETACM10
.
SETACM80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Para

SETACM99  RETURN
+
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
.
.------------------------------------------------------------------------------
.
          INC       IBAWEBCD
.
          INC       APSMASIO/INC
          INC       EMRUNKIO/INC            * Unknowns Patient File IO
          INC       PATALRIO/INC            * Patient Alerts Code File IO
          INC       PATPR1IO/INC            * Pre-admission master file
          INC       PATVISIO/INC 
          INC       PATIN1IO/INC
          INC       PATMA1IO/INC
          INC       PMSVX1IO/INC
          INC       PMIGTNID
          INC       PMSHCPIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPTM
          INC       PMSHCGTM
          INC       PMSPX2IO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATMASTM
          INC       RESIDTTM
          INC       CLPMSPX2
          INC       CLPMSVX1
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       DAYOFWEK
          INC       CALCAGE
          INC       NAMSTRCD
          INC       WEBDATCD
.
          INC       RESIDTIO/INC
          INC       RESLNKIO/INC
          INC       PATMI1IO/INC
          INC       PATWR1IO/INC
          INC       OUTSITIO/INC            * Outpatient Site File 
          INC       PATDO1IO/INC
          INC       PATICDIO/INC
          INC       MRTMASIO/INC
          INC       PATFN1IO/INC 
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       PATVADIO/INC
          INC       PATASFIO/INC
          INC       PATHSPIO/INC
          INC       PATITMIO/INC
          INC       PATCMCIO/INC
          INC       PATDADIO/INC
          INC       PATNIDIO/INC
          INC       PMSALNIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PMSALAIO/INC 
          INC       PMSCLPIO/INC      
          INC       PMSCCDIO/INC            * Concession Card Details
.
          INC       OBSPCOIO/INC
          INC       OBSPCTIO/INC
          INC       CLPATMAS                * Clear nhimasaf file variables
          INC       CLNHIMAS                * Clear nhimasaf file variables
          INC       DGCLICUP
          INC       PATDOCTM
          INC       PATDOCCD
          INC       TFILENAM
.

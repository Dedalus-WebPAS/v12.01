.***************************************************************************
.* System    :   Check Emergency Incomplete tables                         *
.* Program   :   CHKEMINC                                                  *
.* Desc      :   Fix emergency incomplete counts                           *
.***************************************************************************
.* Date      :   15/07/2004                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will check emrincaf has the correct records  *
.* Modifications :                                                         *
.*           :   V11.02.01 08/08/2022  Ebon Clements TSK 0922052           *
.*           :             Added total and column headings                 *
.*           :   V9.09.01  26/10/2007  Peter Vela    CAR 151159            *
.*           :             Added new Emergency Cancellation status 4       *
.***************************************************************************
.
          INC       STD001FD
          INC       EMRVISFD/INC
          INC       EMRINCFD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ERRORCNT  FORM      8
PRGID     INIT      "CHKEMINC"
PRGNAM    INIT      "Check Emergency Incomplete Tables "
VERSION   INIT      "V12.01.00"
+
SAVKEY8   DIM       8
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
          GOTO      ML9999
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      EMRVISA2,"emrvisaf"
          OPEN      EMRINCA1,"emrincaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Print only":
                    *P1:6,*V2LON,TWO,*HOFF,". Print and Update"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,ERRORCNT      * Total errors
          CALL      HEAD0000           * Print heading
.
          CALL      PRINC000           * Print incomplete errors
          CALL      PRVIS000           * Print emrvisaf errors
.
          CALL      ENDD0000           * Print end of report
.
PROC9999  RETURN
+
.**************************************************************************
.*                               HEAD0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD80
.
          IF        COPTION=2
            PRINT     *20,"Print and Update"
          ELSE
            PRINT     *20,"Print Only"
          ENDIF
.
          ADD       ONE,CLNO
          CALL      UND80
.
HEAD9999  RETURN
+
.**************************************************************************
.*                               ENDD0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
ENDD0000  CALL      UND80
.
          PRINT     *1,"| Total",*12,"| ",ERRORCNT,*80,"|"
.
          CALL      UND80
.
          PRINT     *N,*1,"*** End of Report ***"
.
ENDD9999  RETURN
+
.**************************************************************************
.*                               PRINC000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
PRINC000  PRINT     *1,"| Visit",*12,"| Incomplete file errors",*80,"|"
          ADD       ONE,CLNO
          CALL      UND80
.
          PACK      KEY8,SP70
          CALL      RSEMINC1
PRINC100  CALL      RKEMINC1
          BRANCH    OVRCD,PRINC999
.
          PACK      KEY8,EMIPADMN,SP70
          CALL      RDEMVIS1
          BRANCH    OVRCD,PRINC200
.
          COMPARE   ONE,EMVISTAT
          GOTO      PRINC100 IF EQUAL
.
          COMPARE   TWO,EMVISTAT
          GOTO      PRINC100 IF EQUAL
.
          GOTO      PRINC300
.
PRINC200  IF        CLNO >= 55
            CALL      HEAD0000
          ENDIF
.  
          PRINT     *1,"|",*3,EMIPADMN,*12,"| Missing visit record",*80,"|"
          ADD       ONE,ERRORCNT            * Total errors
          GOTO      PRINC900
.
PRINC300  IF        CLNO >= 55
            CALL      HEAD0000
          ENDIF
.
          PRINT     *1,"|",*3,EMIPADMN:
                    *12,"| Invalid visit record, not incomplete",*80,"|"
          ADD       ONE,ERRORCNT            * Total errors
          GOTO      PRINC900
.
PRINC900  IF        COPTION=2
            PACK      SAVKEY8,EMIPADMN
            PACK      KEY8,EMIPADMN
            CALL      DEEMINC1
            PACK      KEY8,EMIPADMN
            CALL      RSEMINC1
          ENDIF
          GOTO      PRINC100
.
PRINC999  RETURN
+
.**************************************************************************
.*                               PRVIS000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
PRVIS000  CALL      UND80
          ADD       ONE,CLNO
          PRINT     *1,"| Visit",*12,"| Visit file errors ",*80,"|"
          CALL      UND80
.
          PACK      KEY9,SP70
          CALL      RSEMVIS2
PRVIS100  CALL      RKEMVIS2
          BRANCH    OVRCD,PRVIS999
.
          COMPARE   THREE,EMVISTAT
          GOTO      PRVIS999 IF NOT LESS
.
          PACK      KEY8,EMVIADMN,SP70
          CALL      RDEMINC1
          BRANCH    OVRCD,PRVIS200
.
          GOTO      PRVIS100
.
PRVIS200  IF        CLNO >= 55
            CALL      HEAD0000
          ENDIF
.
          PRINT     *1,"|",*3,EMVIADMN,*12,"| Missing incomplete record",*80,"|"
          ADD       ONE,ERRORCNT            * Total errors
          GOTO      PRVIS900
.
PRVIS900  IF        COPTION=2
            CALL      ADDINC00
          ENDIF
          GOTO      PRVIS100
.
PRVIS999  RETURN
+
.------------------------------------------------------------
. Add New Patient to Incomplete Table
.------------------------------------------------------------
ADDINC00  MOVE      "2",EMIPDCOM           * Set to two untill discharge
          MOVE      "2",EMIPNCOM
          MOVE      "0",EMIPCCOM
          MOVE      EMVIDOCT,EMIPDOCT
          MOVE      EMVITRNS,EMIPNURS
          MOVE      SP70,EMIPSPAR
          MOVE      EMVIADMN,EMIPADMN
          PACK      KEY8,EMVIADMN,SP70
          CALL      RDEMINC1
          IF        OVRCD=1
            CALL      WREMINC1
          ENDIF
          RETURN
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       EMRVISIO/INC
          INC       EMRINCIO/INC

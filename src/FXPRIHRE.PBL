.***************************************************************************
.* System    :   Private Practice System                                   *
.* Program   :   FXPRIHRE                                                  *
.***************************************************************************
.* Date      :   12/02/2010                                                *
.* Author    :   Davin Sloan                                               *
.* Mods      :                                                             *
.*              V9.12.00  12/02/2010  Davin         CAR 207040             *
.*                        Created program - Update prihreff.prhrbcod with  *
.*                                          patma1af.pfundh                *
.***************************************************************************
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PATMA1FD/INC
          INC       PRIHDBFD/INC
          INC       PRIHREFD/INC
.
COUNTER   FORM      5
.
PRGID     INIT      "FXPRIHRE"
PRGNAM    INIT      "Write PMI fund to prihreff "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select FROM and TO
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PROC0000               * process records for update
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"          * Master file
          OPEN      PATMX1A1,"patmx1af"          * Master file
.
          DISPLAY   *P64:24,"prihreff";
          OPEN      PRIHREF1,"prihreff"          * PRI Doctor referral details
.
          DISPLAY   *P64:24,"prihdbtf";
          OPEN      PRIHDBT2,"prihdbtf"          * PRI Debtors holding table
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Update prihreff with pfundh"
.
          DISPLAY   *P1:11,*EF,*V2LON,"WARNING: PLEASE BACKUP prihreff BEFORE RUNNING THIS PROGRAM."
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,OPTN9000,OPTN1000,OPTN9500 * Yes, no, cancel
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                PROC0000            Called by: ML0000     *
.*              read through prihreff                                       *
.****************************************************************************
PROC0000  MOVE      ZERO,COUNTER
          DISPLAY   *P1:24,*EL,"Processing : ";
          PACK      KEY23,SP70
          CALL      RSPRHR1                      * position at start of file
PROC1000  CALL      RKPRHR1                      * read next record
          BRANCH    OVRCD,PROC9000               * end of file
.
          DISPLAY   *P14:24,*EL,*V2LON,PRHRUNIQ,PRHRPRAC,PRHRDOCT,PRHRPIND;
.
.         read prihdbtf to get debtor number
.
          PACK      KEY8,PRHRUNIQ,SP70
          CALL      RDPRHD2
          BRANCH    OVRCD,PROC1000               * no record
.
          COMPARE   ONE,PRHDSCAN
          GOTO      PROC1000 IF NOT EQUAL        * PMI ?
.
          CALL      UDDR0000                     * update doc ref details file
.
          GOTO      PROC1000                     * get next record
.
PROC9000  DISPLAY   *P1:20,*EL,COUNTER," prihreff records updated.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.****************************************************************************
.*                                UDDR0000                                  *
.*              update prihreff with patma1af.pfundh                        *
.****************************************************************************
UDDR0000  PACK        KEY8,PRHDDEBT,SP70
          CALL        RDMAST1
          BRANCH      OVRCD,UDDR9999
.
          MATCH       SP70,PFUNDH
          GOTO        UDDR9999 IF EQUAL
.
UDDR5000  MATCH       SP70,PRHRBCOD
          GOTO        UDDR9999 IF NOT EQUAL
.
          MOVE        PFUNDH,PRHRBCOD
          CALL        UPPRHR1
          ADD         ONE,COUNTER
UDDR9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATMA1IO/INC
          INC       PRIHDBIO/INC
          INC       PRIHREIO/INC

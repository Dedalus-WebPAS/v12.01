.----------------------------------------------------------------------
. Program       : PRIWEB03
.
. Function      : Private Practice Patient Server
.
. Modifications : 
.----------------------------------------------------------------------
. V12.00.01 24/07/2025  J.Tan          TSK 0962500
.           Mod GCSH1000 to check for U/R number
. V11.04.03 29.01.2024 dasarkies     TSK 0916369
.           Changed code so that the name as opposed to the User Id is displayed
.           Fixed up some other issues such as introducing a clear statement
.           before the add.
. V11.04.02 28.11.2023 dasarkies     TSK 0916369
.           Added New columns to display Future Action completed date
.           Added future action completed, created, and update details to
.           Future Action update/delete
. V11.04.01 27/11/2023 Thanh T       TSK 0916369
.           Added PRFADTCP, PRFACDAT, PRFACTIM, PRFACUID, PRFAUDAT
.                  PRFAUTIM and PRFAUUID fields
. V11.02.01 10/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.00.01 09/12/2020  Thanh T          TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V10.14.01 20/03/2019  J.Tan            TSK 0857392
.           Changed RCP Transaction count from DIM3 to DIM5
. V10.13.01 06/09/2018  J.Tan         TSK 0848506
.           Fixed up key for prihreaf
. V10.11.03 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.02 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
.----------------------------------------------------------------------
. V10.08.01 18.04.2016  Peter Vela   TSK 0294177
.           Changed read to prihdb to KEY27
. V10.04.02 07.04.2014  Peter Vela   CAR 286158
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
. V10.03.03 27/05/2013  Peter Vela     CAR 261630
.           Removed port number from temp file name
. V10.03.02 23/08/2012  J.Tan          CAR 261574
.           Added a column of PP Balance on Future action screen
. V10.03.01 16/01/2012  Sandra Barcham  CAR 256563
.           Recompiled for PATMASTM
.----------------------------------------------------------------------
. V9.11.01  03/04/2009  J.Tan          CAR 190941
.           Added Medical Practice to Future Action file
.----------------------------------------------------------------------
. V9.08.01  31/01/2007  Peter Vela     CAR 127930
.           Recompiled for MRTMASFD
.----------------------------------------------------------------------
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B02 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
. V9.04.03  21/11/2005  Peter Vela    CAR 68675
.           Recompiled for PATDSCTM
. V9.04.02  01/03/2005  Peter Vela    CAR 57547
.           Recompiled for PATVADFD and PATVADIO
. V9.04.01  16/12/2004 Ebon Clements    CAR 55406
.           Added URNUMBER and ADMISSNO to CLRPAR00
. V9.03.04  20/07/2004 J.Tan   CAR 50220
.           Added PP Comments
. V9.03.03  07/06/2004 Peter Vela CAR 49016
.           2004 Statutory Changes - recompiled for PMSPX2TM
. V9.03.02  02/03/2003  Lina Vo                  CAR 47921
.           Removed use of PRIDBTFD & IO.
. V9.03.01  27/01/2003  Pat Dirito               CAR 44114
.           Modified to now use PRIDBTFD as now not using DEBDBTFD
. V9.03.00  03/12/2003  Pat Dirito               CAR 44114
.           Created Web Server.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       PATDO1FD/INC   * Doctor File
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       OUTSITFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATHSPFD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       PRICMNFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       VARWISCM/INC
.
          INC       PRIFACFD/INC
          INC       PRIHDBFD/INC                 * holding header file
          INC       PRIHREFD/INC                 * referral details
          INC       PRIPRAFD/INC                 * medical practice file
          INC       PRIINVFD/INC                 * invoice file
          INC       RCPDTEFD/INC
          INC       RCPBNKFD/INC
...          INC       PRIDBTFD/INC
.
          INC       TFILEVAR/INC
.
.----------------------------------------------------------------------
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
STARTKEY  DIM       10      * Starting Key
CURRDATE  DIM       8       * Current Date
FORM8     FORM      8 
FORM3     FORM      3 
FORM12    FORM      12
FORM12P2  FORM      12.2
DOCTCODE  DIM       8
PSAGETYP  DIM       3
MBSDESC   DIM       70
PSAGECON  DIM       3
CATU1     INIT      "U1"
CATU2     INIT      "U2"
CATU3     INIT      "U3"
CATU4     INIT      "U4"
CATU5     INIT      "U5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
CATVI     INIT      "VI"
COMFILAF  FILE      ASCII, FIXED=127
COMFILNM  DIM       8
COMFILPR  INIT      "PCOMNT"
INVOICEN  DIM       12
OSTDAMNT  FORM      12.2
DATECOMP  DIM       11
CREATDAT  DIM       11
.
LASTITEM  FORM      3
STATUS    DIM       20
SQUOTE    INIT      "'"
PACKLINK  DIM       70      * Test
PPCOMMNT  DIM       70[35]
.
PRIFA001  DIM       8       * Future Action Date
PRIFA002  DIM       8       * Debtor Number
PRIFA003  FORM      2       * Scan Flag
PRIFA004  DIM       3       * Future Action Code
PRIFA005  DIM       60      * Comments
PRIFA006  DIM       6       * Medical Practice
PRIFA007  DIM       8       * Date Action Completed
.
.----------------------------------------------------------------------
PRGID     INIT      "PRIWEB03"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Private Practice Patient Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      PRIFAC00        * Future Action Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      FUTACT00        * Future Actions by Date - Private Practice
          GOTO      MAIN1000
.
MAIN1300  CALL      PPCMN000        * PP Comments
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM 
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PRICMNT1,"pricmntf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATDSCH1,"patdschf"
.
          OPEN      PRIFACT1,"prifactf"
          OPEN      PRIFACT2,"prifactf"
          OPEN      PRIPRAC1,"pripracf"          * Medical Practice file
          OPEN      PRIHDBT1,"prihdbtf"          * Holding Header file
          OPEN      PRIHREF1,"prihreff"
          OPEN      PRIINVO3,"priinvof"
          OPEN      RCPDTEA3,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,COMFILNM
.
...          OPEN      PRIDEBT1,"pridebtf"
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,VIEWTYPE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,DATECOMP
          MOVE      SP70,CREATDAT
.
          MOVE      SP70,PRIFA001
          MOVE      SP70,PRIFA002
          MOVE      ZERO,PRIFA003
          MOVE      SP70,PRIFA004
          MOVE      SP70,PRIFA005
          MOVE      SP70,PRIFA006
          MOVE      SP70,PRIFA007
.
          MOVE      ZERO,LASTITEM
.         PACK      COMFILNM,COMFILPR,PORT
.         REP       " 0",COMFILNM
.         PREP      COMFILAF,COMFILNM
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS:
                                  *85,IBCNUGST
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prifa",QRYNAME
          IF        @EQUAL
            CALL      STFAC000
            GOTO      SETPAR99 
          ENDIF
.
          MATCH     "ppcommnt=",QRYNAME
          IF        @EQUAL
            CALL      GETPCM00           * Get private prac.comments
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 1 - Future Actions
.-------------------------------------------------------------------------------
STFAC000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STFAC001,STFAC002,STFAC003,STFAC004,STFAC005:
                       STFAC006,STFAC007
.
          GOTO      STFAC999
.
STFAC001  MOVE      QRYDATA,KEY11   * Change Date as format is (DD MON CCYY)
          PACK      KEY11,KEY11,SP70
          UNPACK    KEY11,KEY8,KEY3
          MATCH     SP70,KEY3
          IF        !@EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00
            PACK      PRIFA001,CCENT,CYEAR,CMON,CDAY
            REP       " 0",PRIFA001
          ELSE
            MOVE      QRYDATA,PRIFA001
          ENDIF
          GOTO      STFAC999
.
STFAC002  MOVE      QRYDATA,PRIFA002
          GOTO      STFAC999
.
STFAC003  MOVE      QRYDATA,PRIFA003
          GOTO      STFAC999
.
STFAC004  MOVE      QRYDATA,PRIFA004
          GOTO      STFAC999
.
STFAC005  MOVE      QRYDATA,PRIFA005
          GOTO      STFAC999
.
STFAC006  MOVE      QRYDATA,PRIFA006
          GOTO      STFAC999
.
STFAC007  MOVE      QRYDATA,KEY11   * Change Date as format is (DD MON CCYY)
          PACK      KEY11,KEY11,SP70
          UNPACK    KEY11,KEY8,KEY3
          MATCH     SP70,KEY3
          IF        !@EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00
            PACK      PRIFA007,CCENT,CYEAR,CMON,CDAY
            REP       " 0",PRIFA007
          ELSE
            MOVE      QRYDATA,PRIFA007
          ENDIF
          GOTO      STFAC999
.
STFAC999  RETURN
.
.------------------------------------------------------------
.         Get private practice billing
.------------------------------------------------------------
GETPCM00  PREP      COMFILAF,COMFILNM
          MOVE      ONE,F3
          WRITE     COMFILAF,SEQ;QRYDATA
.
GETPCM10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA
          GOTO      GETPCM90 IF OVER
          MATCH     SP70,QRYNAME
          GOTO      GETPCM80 IF NOT EQUAL
          ADD       ONE,F3
          WRITE     COMFILAF,SEQ;QRYDATA
          GOTO      GETPCM10
.
GETPCM80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
GETPCM90  MOVE      F3,LASTITEM
          OPEN      COMFILAF,COMFILNM
GETPCM99  RETURN
.------------------------------------------------------------
. Future Actions Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------
PRIFAC00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      PRIFAC40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      PRIFAC10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      PRIFAC20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      PRIFAC30 IF EQUAL
.
          GOTO      PRIFAC93
.
.         ************ ADD FORMACTION ***********
.
PRIFAC10  MOVE      URNUMBER,KEY8   * Read Master File to get Date of Birth
          CALL      RDMAST1
          BRANCH    OVRCD,PRIFAC94 
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,PRIFAC94 
.
          MOVE      ONE,PRFASCAN     * 1 =PMI
          PACK      KEY18,PRIFA001,URNUMBER,PRFASCAN,SP70
          CALL      RDPRFA1
          COMPARE   ZERO,OVRCD
          GOTO      PRIFAC92 IF EQUAL
.
          CALL      CLPRIFAC
          CALL      FACSAV00      * Moves input variables to file variables
.
          PACK      PRFACDAT,CCC,CYY,CMM,CDD
          REP       " 0",PRFACDAT
          CLOCK     TIME,PRFACTIM
          MOVE      USERID,PRFACUID
.
          CALL      WRPRFA1       * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      PRIFAC99
.
.         ************ UPDATE FORMACTION **********
.
PRIFAC20  MOVE      URNUMBER,KEY8   * Read Master File to get Date of Birth
          CALL      RDMAST1
          BRANCH    OVRCD,PRIFAC94
.
          MOVE      ONE,PRFASCAN     * 1 =PMI
          PACK      KEY18,PRIFA001,URNUMBER,PRFASCAN,SP70
          CALL      RDPRFA1
          BRANCH    OVRCD,PRIFAC91

          CALL      FACSAV00      * Moves input variables to file variables
.
          PACK      PRFAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PRFAUDAT
          CLOCK     TIME,PRFAUTIM
          MOVE      USERID,PRFAUUID
.
          CALL      UPPRFA1       * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      PRIFAC99
.
.         ************ DELETE FORMACTION **********
.
PRIFAC30  MOVE      URNUMBER,KEY8   * Read Master File to get Date of Birth
          CALL      RDMAST1
          BRANCH    OVRCD,PRIFAC94
.
          MOVE      ONE,PRFASCAN     * 1 =PMI
          PACK      KEY18,PRIFA001,URNUMBER,PRFASCAN,SP70
          CALL      RDPRFA1
          BRANCH    OVRCD,PRIFAC91
.
          CALL      DEPRFA1
.
          MOVE      ZERO,EXIT
          GOTO      PRIFAC99
.
.         ************ DISPLAY FORMACTION **********
.
PRIFAC40  MOVE      URNUMBER,KEY8   * Read Master File to get Date of Birth
          CALL      RDMAST1
          BRANCH    OVRCD,PRIFAC94
.
          PACK      KEY18,PRIFA001,PRIFA002,PRIFA003,SP70
          CALL      RDPRFA1
          IF        OVRCD=1
            CALL      CLRFAC00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Future Actions Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PRIFAC99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      PRIFAC99
.
PRIFAC91  MOVE      "Future Actions Code record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRIFAC99
.
PRIFAC92  MOVE      "Future Actions Code record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRIFAC99
.
PRIFAC93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRIFAC99
.
PRIFAC94  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRIFAC99
.
PRIFAC99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRFAC00  MOVE      SP70,PRFADATE  * Future Action Date
          MOVE      SP70,PRFADEBT  * Debtor Number
          MOVE      ZERO,PRFASCAN  * Scan Flag
          MOVE      SP70,PRFAACTN  * Future Action Code
          MOVE      SP70,PRFACOMM  * Comments
          MOVE      SP70,PRFAMPRA  * Medical Practice
.
          MOVE      SP70,PRFADTCP
          MOVE      SP70,PRFACDAT
          MOVE      SP70,PRFACTIM
          MOVE      SP70,PRFACUID
          MOVE      SP70,PRFAUDAT
          MOVE      SP70,PRFAUTIM
          MOVE      SP70,PRFAUUID
.
          MOVE      SP70,PRFASPAR  * Spare Variable
.
CLRFAC99  RETURN
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
FACSAV00  MOVE      PRIFA001,PRFADATE  * Future Action Date
          MOVE      URNUMBER,PRFADEBT  * Debtor Number
          MOVE      ONE,PRFASCAN       * Scan Flag 1=PMI
          MOVE      PRIFA004,PRFAACTN  * Future Action Code
          MOVE      PRIFA005,PRFACOMM  * Comments
          MOVE      PRIFA006,PRFAMPRA  * Medical Practice
          MOVE      PRIFA007,PRFADTCP  * Date Action Completed
          MOVE      SP70,PRFASPAR      * Spare Variable
.
FACSAV99  RETURN
.------------------------------------------------------------
. Future Actions by Date
.------------------------------------------------------------
FUTACT00  CALL      CURPER00
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          MOVE      "Future Actions",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,FUTACT99
.
          CALL      WEBBOD00
          CALL      WEBEND00
.
FUTACT99  RETURN
.
.------------------------------------------------------------
.  Private practice comments
.------------------------------------------------------------
.         copy the comments back to file
.         first delete existing comments
.
PPCMN000  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      PPCMN600 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      PPCMN200 IF EQUAL
          GOTO      PPCMN920
.
PPCMN200  PACK      KEY13,URNUMBER,SP70
          CALL      RSPRCM1
          CALL      RKPRCM1 
          BRANCH    OVRCD,PPCMN500          * end of file
          MATCH     PRCMDEBT,URNUMBER       * same U/R Number?
          GOTO      PPCMN500 IF NOT EQUAL
.
          PACK      KEY13,PRCMDEBT,PRCMSCAN,PRCMCLNO
          CALL      DEPRCM1 
          GOTO      PPCMN200
.
.         now write the new comments back
.
PPCMN500  MOVE      URNUMBER,PRCMDEBT          * set U/R Number
          MOVE      "1",PRCMSCAN
          MOVE      ZERO,F3
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      COMFILAF,COMFILNM
          TRAPCLR   IO
          BRANCH    OVRCD,PPCMN999
.
PPCMN510  ADD       ONE,F3
          PACK      PRCMLINE,SP70
          READ      COMFILAF,SEQ;PRCMLINE
          ENDSET    PRCMLINE
          APPEND    SP70,PRCMLINE
          RESET     PRCMLINE
.
          MATCH     SP70,PRCMLINE
          IF        @EQUAL
            IF        F3>=LASTITEM
              MOVE      ZERO,EXIT
              GOTO      PPCMN999
            ELSE
              GOTO      PPCMN510
            ENDIF
          ENDIF
.
          MOVE      F3,PRCMCLNO
          PACK      KEY13,PRCMDEBT,PRCMSCAN,PRCMCLNO
          CALL      WRPRCM1  
          IF        F3>=LASTITEM
            MOVE      ZERO,EXIT
            GOTO      PPCMN999
          ENDIF
          GOTO      PPCMN510
.
PPCMN600  MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PPCMN990
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,PPCMN990
.
          CLEAR     WEBTITLE
          MOVE      "Comments Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PPCMN999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      PPCMN999
.
PPCMN990  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PPCMN999
.
PPCMN920  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PPCMN999
.
PPCMN999  CLOSE     COMFILAF,DELETE
          RETURN
+
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD12  CALL      PRFA0000      * Future Actions 
          GOTO      PROFLD99
.
PROFLD20  CALL      FUTR0000      * Future Actions List
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32
          GOTO      PROFLD99
.
PROFLD31  CALL      MASF0000      * Master File
          GOTO      PROFLD99
.
PROFLD32  CALL      PPCM0000      * PP Comments File
          GOTO      PROFLD99
.
PROFLD99  RETURN
.-----------------------------------------------------------
. Option 1 - Future Actions
.-----------------------------------------------------------
PRFA0000  BRANCH    FLDITMNO,PRFA0100,PRFA0200,PRFA0300,PRFA0400,PRFA0500:
                             PRFA0600,PRFA0700,PRFA0800,PRFA0900,PRFA1000
          GOTO      PRFA9999
.
PRFA0100  UNPACK    PRFADATE,CCENT,CYEAR,CMON,CDAY  * Future Action Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR; 
          GOTO      PRFA9999
.
PRFA0200  WRITE     HTMLFILE;PRFADEBT;  * Debtor Number
          GOTO      PRFA9999
.
PRFA0300  WRITE     HTMLFILE;PRFASCAN;  * Scan Flag
          GOTO      PRFA9999
.
PRFA0400  MOVE      "FA",CATEGORY       * Future Action Code
          MOVE      PRFAACTN,CODE
          CALL      CODITM00
          GOTO      PRFA9999
.
PRFA0500  MATCH     SP70,PRFACOMM
          GOTO      PRFA9999 IF EQUAL
          WRITE     HTMLFILE;PRFACOMM;  * Comments
          GOTO      PRFA9999
.
PRFA0600  CALL      FACTB000     * Table of Future Actions
          GOTO      PRFA9999
.
PRFA0700  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,PRFA0710,PRFA0720
          WRITE     HTMLFILE;PRFAMPRA;
          GOTO      PRFA9999
.
PRFA0710  PACK      KEY6,PRFAMPRA,SP70
          CALL      RDPRPR1
          IF        OVRCD=0
            WRITE     HTMLFILE;PRPRDESC;
          ELSE
            WRITE     HTMLFILE;PRFAMPRA;
          ENDIF
          GOTO      PRFA9999
.
PRFA0720  MOVE      ONE,PRHDSCAN
          PACK      KEY27,URNUMBER,PRHDSCAN,SP70
          CALL      RSPRHD1
PRFA0722  CALL      RKPRHD1
          BRANCH    OVRCD,PRFA9999
          MATCH     PRHDDEBT,URNUMBER       * Same U/R number ?
          GOTO      PRFA9999 IF NOT EQUAL
.
          PACK      KEY27,PRHDUNIQ,SP70
          CALL      RSPRHR1
PRFA0725  CALL      RKPRHR1
          BRANCH    OVRCD,PRFA0722
.
          COMPARE   PRHRUNIQ,PRHDUNIQ
          GOTO      PRFA0722 IF NOT EQUAL   * different unique number
.
          PACK      KEY6,PRHRPRAC,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRFA0725
.
          PACK      KEY8,QUOTE,PRPRCODE,QUOTE
.
          MATCH     PRFAMPRA,PRPRCODE
          IF        @EQUAL
             WRITE     HTMLFILE;"<option value=",KEY8," selected>":
                       "(",PRPRCODE,") ",PRPRDESC,"</option>"
          ELSE
             WRITE     HTMLFILE;"<option value=",KEY8,">":
                       "(",PRPRCODE,") ",PRPRDESC,"</option>"
          ENDIF
          GOTO      PRFA0725
.
PRFA0800  MATCH     SP70,PRFADTCP
          GOTO      PRFA0899 IF EQUAL 
          UNPACK    PRFADTCP,CCENT,CYEAR,CMON,CDAY  * Future Action Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR; 
PRFA0899  GOTO      PRFA9999
.
PRFA0900  PACK      KEY10,PRFACUID,SP70
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      PRFACUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          MATCH     SP70,PRFACDAT
          IF        !@EQUAL
            UNPACK    PRFACDAT,CCENT,CYEAR,CMON,CDAY  * Created Date      
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            WRITE     HTMLFILE;SP1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1,"at"; 
            WRITE     HTMLFILE;SP1,PRFACTIM;
          ENDIF
          GOTO      PRFA9999
.
PRFA1000  PACK      KEY10,PRFAUUID,SP70
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      PRFAUUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          MATCH     SP70,PRFAUDAT
          IF        !@EQUAL
            UNPACK    PRFAUDAT,CCENT,CYEAR,CMON,CDAY  * Updated Date      
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            WRITE     HTMLFILE;SP1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1,"at"; 
            WRITE     HTMLFILE;SP1,PRFAUTIM;
          ENDIF
          GOTO      PRFA9999
.

PRFA9999  RETURN
.------------------------------------------------------------
. Table of Future Actions      
.------------------------------------------------------------
FACTB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      FACHD000      * Output Table Heading
.
..          IF        NORECORD=0
..            MOVE      "13",NORECORD       * Set Default No Records to Display
..          ENDIF
..          MOVE      ZERO,RECNUMB
..          MOVE      ZERO,DISNUMB
.
          PACK      KEY18,URNUMBER,Z70
          CALL      RSPRFA2
FACTB100  CALL      RPPRFA2
          BRANCH    OVRCD,FACTB999
.
          MATCH     URNUMBER,PRFADEBT
          GOTO      FACTB999 IF NOT EQUAL
.
..          ADD       ONE,RECNUMB
..          IF        STARTNUM>=RECNUMB
..            GOTO      FACTB100
..          ENDIF
.
.
          MOVE      SP70,DATECOMP
          MOVE      SP70,CREATDAT
.
          MATCH     SP70,PRFADTCP
          IF        !@EQUAL
            UNPACK    PRFADTCP,CYEAR,CCENT,CMON,CDAY
            MOVE      " ",SP1
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DATECOMP,CDAY,SP1,MTHNAM3,SP1,CYEAR,CCENT
          ENDIF
.
          MATCH     SP70,PRFACDAT
          IF        !@EQUAL
            UNPACK    PRFACDAT,CYEAR,CCENT,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      CREATDAT,CDAY,SP1,MTHNAM3,SP1,CYEAR,CCENT
          ENDIF
.
          CALL      FACRW000      * Display Future Actions Table Details Row
..          ADD       ONE,DISNUMB
..          COMPARE   NORECORD,DISNUMB
..          GOTO      FACTB100 IF NOT EQUAL
          GOTO      FACTB100
.
FACTB999  RETURN
.------------------------------------------------------------
. Future Actions Table Details (TABLE HEADING)
.------------------------------------------------------------
FACHD000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=15%>":
                               "Future Action Date</td>":
                               "<td class=HeadingCell width=15%>":
                               "Action Type</td>":
                               "<td class=HeadingCell width=15%>":
                               "Medical Practice</td>":
                               "<td class=HeadingCell width=30%>":
                               "Comment</td>":
                               "<td class=HeadingCell width=15%>":
                               "Date Completed</td>":
                               "<td class=HeadingCell width=10%>":
                               "Created By</td>":
                             "</tr>"
.
FACHD999  RETURN
.------------------------------------------------------------
. Future Actions Table (TABLE ROW)
.------------------------------------------------------------
FACRW000  UNPACK    PRFADATE,CCENT,CYEAR,CMON,CDAY  * Future Action Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MATCH     SP70,PRFAACTN
          IF        @EQUAL 
            MOVE      "&nbsp;",TDESC
          ELSE
            MOVE      "FA",CATEGORY     * Get Claim Code descirption
            MOVE      PRFAACTN,CODE
            CALL      GETCOD00
          ENDIF
.
          MATCH     SP70,PRFACOMM
          IF        @EQUAL 
            MOVE      "&nbsp;",PRFACOMM
          ENDIF
.
          MOVE      SP70,PRPRDESC
          PACK      KEY6,PRFAMPRA,SP70
          CALL      RDPRPR1
          IF        OVRCD=1
            MOVE      "&nbsp;",PRPRDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&prifa001=",PRFADATE:
                             "&prifa002=",PRFADEBT:
                             "&prifa003=",PRFASCAN,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             KEY11,"</td>":
                             "<td>",TDESC,"</td>":
                             "<td>",PRPRDESC,"</td>":
                             "<td>",PRFACOMM,"</td>":
                             "<td>",DATECOMP,"</td>" 
         PACK       KEY10,USERID,SP70
         CALL       RDWBSE1
         IF         OVRCD=1
           MOVE       USERID,WBSENAM 
         ENDIF
         WRITE      HTMLFILE;"<td>",WBSENAM,"<br>" 
         MATCH      SP70,CREATDAT
         IF         !@EQUAL
           WRITE      HTMLFILE;CREATDAT," at ",PRFACTIM,"</td></tr>"
         ELSE
           WRITE      HTMLFILE;"</td></tr>"
         ENDIF
.
FACRW999 RETURN
.------------------------------------------------------------
. Future Actions List by Date
.------------------------------------------------------------
FUTR0000  BRANCH    FLDITMNO,FUTR0100,FUTR0200,FUTR0300,FUTR0400,FUTR0500:
                             FUTR0600,FUTR0700,FUTR0800,FUTR0900,FUTR1000:
                             FUTR1100
          GOTO      FUTR9999
.
FUTR0100  CALL      NEXT0000
          GOTO      FUTR9999
.
FUTR0200  CALL      PREV0000
          GOTO      FUTR9999
.
FUTR0300  CALL      CURSTD00
          GOTO      FUTR9999
.
FUTR0400  CALL      CUREND00
          GOTO      FUTR9999
.
FUTR0500  CALL      CURYR000
          GOTO      FUTR9999
.
FUTR0600  CALL      CURMON00
          GOTO      FUTR9999
.
FUTR0700  CALL      SELV0000
          GOTO      FUTR9999
.
FUTR0800  CALL      FTABP000  * Table Future Actions - Private Practice
          GOTO      FUTR9999
.
FUTR0900  WRITE     HTMLFILE;TEMPLATE;
          GOTO      FUTR9999
.
FUTR1000  WRITE     HTMLFILE;REPORTNO;
          GOTO      FUTR9999
.
FUTR1100  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      FUTR9999
.
FUTR9999  RETURN
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          WRITE     HTMLFILE;"priweb03.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&viewtype=",VIEWTYPE:
                             "&frstdate=",NXTFDATE:
                             "&lastdate=",NXTLDATE;
.
NEXT9999  RETURN
.------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          WRITE     HTMLFILE;"priweb03.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&viewtype=",VIEWTYPE:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE;
.
PREV9999  RETURN
.------------------------------------------------------------
. Displays Private Practice Future Actions Table
.------------------------------------------------------------
FTABP000  PACK      KEY18,FRSTDATE,SP70
          CALL      RSPRFA1
FTABP100  CALL      RKPRFA1
          BRANCH    OVRCD,FTABP999
.
          MATCH     PRFADATE,LASTDATE
          GOTO      FTABP999 IF LESS
.
          CALL      FROWP000   * Display Future Actions Row
          GOTO      FTABP100
.
FTABP999  RETURN
.------------------------------------------------------------
. Private Practice Future Actions
.------------------------------------------------------------
FROWP000  BRANCH    PRFASCAN,FROWP050
.
...          PACK      KEY8,PRFADEBT,SP70  * Private Practice
...          CALL      RDPRDB1             * Read Debtor record
...          BRANCH    OVRCD,FROWP999 
.
.  * Set up PMI vars with Debitor vars for CALCAGE & PATLN000
.
...          MOVE      PRDBSNAM,PSNAME     * Surname
...          MOVE      PRDBGNAM,PGNAME     * Given Name
...          MOVE      PRDBTITL,PTITL      * Title
...          MOVE      PRDBDOBH,PBDATE     * Date of Birth  
.
...          GOTO      FROWP100
.
FROWP050  PACK      KEY8,PRFADEBT,SP70  * PMI
          CALL      RDMAST1
          BRANCH    OVRCD,FROWP999 

FROWP100  MATCH     SP70,PRFAACTN
          IF        @EQUAL 
            MOVE      "&nbsp;",TDESC
          ELSE
            MOVE      "FA",CATEGORY     * Get Claim Code descirption
            MOVE      PRFAACTN,CODE
            CALL      GETCOD00
          ENDIF
.
.   Output formatted date(link) and set FACTDATE as a CGI parameter
.
          UNPACK    PRFADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MATCH     SP70,PRFACOMM
          IF        @EQUAL
            MOVE      "&nbsp;",PRFACOMM
          ENDIF
.
          MOVE      PRFADEBT,URNUMBER
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          CALL      CALCAGE
.
          MOVE      SP70,PRPRDESC
          PACK      KEY6,PRFAMPRA,SP70
          CALL      RDPRPR1
          IF        OVRCD=1
            MOVE      "&nbsp;",PRPRDESC
          ENDIF
.
          CALL      GBAL0000             * Calculate Total Invoice outstanding
.
          CALL      PATLN000
          WRITE     HTMLFILE;"<tr><td>":
                             "<img src=../images/ActionIcon.gif ":
                             "class=ListIcon onclick='linkAction(#"":
                             URNUMBER,"#",#"",PRFASCAN,"#")'>":
                             KEY11,"</td>":
                             "<td>",FORMATNM,"</td>":
                             "<td>",TDESC,"</td>":
                             "<td>",PRPRDESC,"</td>":
                             "<td>",PRFACOMM,"</td>":
                             "<td>",OSTDAMNT,"</td>":
                             "</tr>"
.
FROWP999 RETURN
.------------------------------------------------------------
.         Total invoice outstanding
.------------------------------------------------------------
GBAL0000  MOVE      ZERO,OSTDAMNT
          PACK      KEY18,URNUMBER,SP70
          CALL      RSPRIN3
GBAL1000  CALL      RKPRIN3
          BRANCH    OVRCD,GBAL9999
          MATCH     URNUMBER,PRINDEBT         * Same debtor no?
          GOTO      GBAL9999 IF NOT EQUAL
.
          CALL      GCSH0000                     * Get cash amount
          ADD       PRINITOT,OSTDAMNT
          ADD       PRINPAMT,OSTDAMNT
          ADD       PRINHAMT,OSTDAMNT
          ADD       PRINIAMT,OSTDAMNT
          ADD       PRINMAMT,OSTDAMNT
          ADD       PRINVAMT,OSTDAMNT
          ADD       PRINOTHA,OSTDAMNT
          ADD       PRINJAMT,OSTDAMNT
          ADD       PRINCNTT,OSTDAMNT
          IF        IBCNUGST=2 | IBCNUGST=3
            ADD       PRINGSTJ,OSTDAMNT
          ENDIF
          GOTO      GBAL1000
.
GBAL9999  RETURN
+
.**********************************************************************
.*        Get Cash and Unreleased cash                                *
.**********************************************************************
GCSH0000  MOVE      ZERO,PRINPAMT
          MOVE      ZERO,PRINHAMT
          MOVE      ZERO,PRINOTHA
.
          MOVE      SP70,INVOICEN
          MOVE      ZERO,FORM12
          MOVE      ZERO,FORM8
          MOVE      PRINNUMB,FORM8
          MOVE      FORM8,FORM12
          MOVE      FORM12,INVOICEN
.
          PACK      KEY29,INVOICEN,SP70
          CALL      RSRCDTE3
GCSH1000  CALL      RKRCDTE3
          BRANCH    OVRCD,GCSH9999
.
          MATCH     INVOICEN,RCDTINVN
          GOTO      GCSH9999 IF NOT EQUAL
.
          MATCH     URNUMBER,RCDTURNO         * Same U/R no?
          GOTO      GCSH1000 IF NOT EQUAL
.
.         Get payment type
.
          MOVE      RCDTRECN,KEY12
          CALL      RDRCBNK1
          BRANCH    OVRCD,GCSH1000
.
          MATCH     "1",RCBNSTAT                * Cancelled
          GOTO      GCSH1000 IF EQUAL
.
          MOVE      RCDTAMNT,FORM12P2
          MULT      "-1",FORM12P2
          MOVE      ZERO,F1
          MOVE      RCBNTTYP,F1
          BRANCH    F1,GCSH1100,GCSH1200,GCSH1200,GCSH1100
.
          ADD       FORM12P2,PRINPAMT
          GOTO      GCSH1000
.
GCSH1100  ADD       FORM12P2,PRINHAMT
          GOTO      GCSH1000
.
GCSH1200  ADD       FORM12P2,PRINOTHA
          GOTO      GCSH1000
.
GCSH9999  RETURN
+
.------------------------------------------------------------
.        Comments       
.------------------------------------------------------------
PPCM0000  BRANCH    FLDITMNO,PPCM0100
          GOTO      PPCM9999
.
PPCM0100  CALL      DSCM0000                * Display comments
          GOTO      PPCM9999
.
PPCM9999  RETURN
+
.------------------------------------------------------------
.         Display comments
.------------------------------------------------------------
DSCM0000  PACK      KEY13,URNUMBER,SP70
          CALL      RSPRCM1
DSCM1000  CALL      RKPRCM1 
          BRANCH    OVRCD,DSCM9999          * end of file
          MATCH     PRCMDEBT,URNUMBER       * same U/R Number?
          GOTO      DSCM9999 IF NOT EQUAL
          WRITE     HTMLFILE;PRCMLINE
          GOTO      DSCM1000
.
DSCM9999  RETURN
+
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       PATDO1IO/INC   * Doctor File
          INC       PATHSPIO/INC
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PATALRIO/INC
          INC       OUTSITIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATFN1IO/INC
          INC       PATWR1IO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       PRICMNIO/INC
          INC       PATITMIO/INC
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATCMCIO/INC
          INC       PATASFIO/INC
.
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       APSMASIO/INC
          INC       PATIN1IO/INC
          INC       PATDSCIO/INC
.
          INC       PRIFACIO/INC
          INC       PRIPRAIO/INC                 * medical practice file
          INC       PRIINVIO/INC                 * invoice file
          INC       RCPDTEIO/INC
          INC       RCPBNKIO/INC
          INC       PRIHDBIO/INC                 * holding header file
          INC       PRIHREIO/INC                 * referral details
...          INC       PRIDBTIO/INC
.
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       CLPRIFAC
          INC       PATMASTM
          INC       PATDOCTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PATDSCTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATDOCCD
          INC       PATNAMCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       TFILENAM

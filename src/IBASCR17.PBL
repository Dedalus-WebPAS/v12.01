.*****************************************************************************
.*  System       :  Screen Painter                                           *
.*  Program      :  IBASCR17                                                 *
.*  Function     :  Maintain Field File Program Id                           *
.*****************************************************************************
.*  Date         :  24/11/92                                                 *
.*  Author       :  Whirlpool                                                *
.*  Modifactions :                                                           *
.*                                                                           *
.*  C2.00.01 09.09.1999 Sandra Barcham                                       *
.*           Change ITEMNO to XITEMNO for PATITMFD                           *
.*                                                                           *
.*****************************************************************************
.
          INC       SYSSTDDF
          INC       PAINTDDF
          INC       SCRMASFD/INC
          INC       SCRPRGFD/INC
.
ACCEPT    FORM      1
PROGRAM   DIM       8
SCREEN    DIM       2

PRGID     INIT      "IBASCR17"
PRGNAM    INIT      "Maintain Field File Program Id"
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              ML0000                                       *
.*                             Mainline                                      *
.*****************************************************************************
.
ML0000    CALL      INIT0000                * initialistion
.
ML1000    CALL      KPID0000                * keyin the program id
          BRANCH    EXIT,ML9999
.
ML1100    CALL      KSID0000                * keyin screen id
          BRANCH    EXIT,ML1000
.
          CALL      DSCR0000
.
          CALL      SIPC0000                * Select Item Post Cancel
          BRANCH    EXIT,ML1100
.
          CALL      UPSCMA1
          GOTO      ML1100 
.
ML9999    CHAIN     PGM
.
.*****************************************************************************
.*                              INIT0000                                     *
.*                        Initialisation section                             *
.*****************************************************************************
.
INIT0000
          CALL      DISPHEAD
.
          OPEN      SCRMASA1,"scrmasaf"
          OPEN      SCRPRGA1,"scrprgaf"
.
INIT9999  RETURN
.
.
.*****************************************************************************
.*                              DSCR0000                                     *
.*                        Display the screen                                 *
.*****************************************************************************
.
DSCR0000 
          DISPLAY   *P1:7,*EL,*V2LON,"1",*HOFF:
                    ". Field File Program Id : ":
                    *V2LON,SCMAFPR
DSCR9999  RETURN
.
.
.*****************************************************************************
.*                              KPID0000                                     *
.*                       Keyin the program id                                *
.*****************************************************************************
.
KPID0000
          MOVE      ZERO,EXIT
          DISPLAY   *P1:3,*EF:
                    *P4:4,"Program Id            :"
.
          MOVE      ONE,ACCEPT
          MOVE      ZERO,EXIT
          MOVE      FOUR,CVERT
          MOVE      "28",CCOL
          MOVE      ZERO,CKEYTYP
          MOVE      "6",HTOP
          MOVE      "24",HBOT
          MOVE      "40",HLEF
          MOVE      "80",HRIG
          CALL      KSCPRA00
          BRANCH    EXIT,KPID9000,KPID9000
.
          MOVE      ZERO,EXIT
          GOTO      KPID9999
.
KPID9000  MOVE      ONE,EXIT
KPID9999  RETURN
.
.
.*****************************************************************************
.*                              KSID0000                                     *
.*                       Keyin the program id                                *
.*****************************************************************************
.
KSID0000
          MOVE      ZERO,EXIT
          DISPLAY   *P1:5,*EF:
                    *P4:5,"Screen Id             :"
.
          MOVE      ONE,ACCEPT
          MOVE      ZERO,EXIT
          MOVE      FIVE,CVERT
          MOVE      "28",CCOL
          MOVE      TWO,CKEYTYP             * insert/change
          MOVE      "6",HTOP
          MOVE      "24",HBOT
          MOVE      "40",HLEF
          MOVE      "80",HRIG
          MOVE      SCPRPID,SCMAPID
          CALL      KSCMAA00
          BRANCH    EXIT,KSID9000,KSID9000
.
KSID1000  PACK      KEY10,SCMAPID,SCMASID
          CALL      RDSCMA1
          BRANCH    OVRCD,KSID5000          * insert mode
.
          MOVE      ZERO,EXIT
          GOTO      KSID9999
.
KSID5000
          DISPLAY   *P1:23,*EL,"Record is not on file .";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      KSID0000
.
KSID9000  MOVE      ONE,EXIT
+
KSID9999  RETURN
.
.*****************************************************************************
.*                            SIPC0000                                       *
.*                     Select Item Post Cancel                               *
.*****************************************************************************
.
SIPC0000
          MOVE      ZERO,EXIT
          CALL      MAINQST
          IF        CCITEM = -1
            MOVE      ONE,EXIT
            GOTO      SIPC9999
          ENDIF
          IF       CCITEM = 0
            GOTO     SIPC9999
          ENDIF
          BRANCH   CCITEM,SIPC1000
          BEEP
          GOTO     SIPC0000
.
SIPC1000  CALL     KFFP0000                 * keyin field file program id
          GOTO     SIPC0000
.
        
SIPC9999  RETURN
.
.*****************************************************************************
.*                            KFFP0000                                       *
.*                   Keyin the field file program id                         *
.*****************************************************************************
.
KFFP0000
          MOVE      SCPRPID,PROGRAM
.
          MOVE      ONE,ACCEPT
          MOVE      ZERO,EXIT
          MOVE      SEVEN,CVERT
          MOVE      "28",CCOL
          MOVE      ZERO,CKEYTYP
          MOVE      "6",HTOP
          MOVE      "24",HBOT
          MOVE      "40",HLEF
          MOVE      "80",HRIG
          CALL      KSCPRA00
.
          MOVE      SCPRPID,SCMAFPR
          MOVE      PROGRAM,SCPRPID
.
          IF        EXIT = 1
            MOVE      ZERO,EXIT
            MOVE      SP8,SCMAFPR
            GOTO      KFFP9999
          ENDIF
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KFFP9999
          ENDIF
.
          MOVE      ZERO,EXIT
.
          MATCH     SCPRPID,SCMAFPR
          IF        @EQUAL
            BEEP
            DISPLAY   *P1:24,*EL:
                      "Program Id & Field File Program Id cannot be the same.";
            CALL      HITENTER
            GOTO      KFFP0000
          ENDIF
.
KFFP9999  RETURN
.
.*****************************************************************************
.*                            I/O Section                                    *
.*****************************************************************************
.
          INC       SCRMASIO/INC
          INC       SCRPRGIO/INC
          INC       SCRPRGKY
          INC       SCRMASKY
.
          INC       PAINTDCD
          INC       SYSSTDCD

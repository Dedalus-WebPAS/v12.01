.*****************************************************************************
.*    Program      : FXNZPEXT                                                *
.*    Description  : Set up the Start date for NZ Priv.G/L Interface         *
.*    Author       : J.Tan                                                   *
.*    Date         : 25/07/2007                                              *
.*    Modifications:                                                         *
.*****************************************************************************
.
          INC       STD001FD
          INC       NZPEXTFD/INC
          INC       PATINVFD/INC
          INC       PATVISFD/INC
          INC       PATDTRFD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
          INC       AAEDTRFD/INC
.
RECNUM    FORM      8
STRTDATE  DIM       8
.
PRGNAM    INIT      "NZ Private G/L Interface"
PRGID     INIT      "FXNZPEXT"
VERSION   INIT      "V12.01.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KSTR0000                      * Keyin start date
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      REXT0000                      * Remove extraction records
          CALL      RINV0000                      * Loop through invoice file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
.
          OPEN      NZPEXTA1,"nzpextaf"
          OPEN      PATINVR2,"patinvrf"
          OPEN      PATDTRA5,"patdtraf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      AAEDTRE4,"aaedtref"
          OPEN      OUTSITA1,"outsitaf"
          MOVE      ZERO,RECNUM
INIT9999  RETURN
+
.********************************************************************
.*                          KSTR0000                                *
.*  Keyin start date for G/L Interface                              *
.********************************************************************
KSTR0000  MOVE      SP70,STRTDATE
          DISPLAY   *P1:10,"Start Date for G/L Interface :";
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          MOVE      "37",CCOL
          MOVE      "10",CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,KSTR9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY,SP70
          REP       " 0",STRTDATE
          MOVE      ZERO,EXIT
          GOTO      KSTR9999
.
KSTR9000  MOVE      ONE,EXIT
KSTR9999  RETURN
+
.**********************************************************************
.*                               RINV0000                             *
.*        Remove DTR records for Invoice raised prior to Start date   *
.**********************************************************************
RINV0000  DISPLAY   *P1:20,*EL,"Records Updated : ";
.
          PACK      KEY16,SP70
          CALL      RDSINV2 
RINV0200  CALL      RDKINV2                       * loop thru patdtraf file
          BRANCH    OVRCD,RINV9000                * finish
.
          MATCH     SP70,PINVDTE
          GOTO      RINV0200 IF EQUAL
          MATCH     STRTDATE,PINVDTE
          GOTO      RINV9000 IF NOT LESS
.
          MOVE      PINVADM,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,RINV0200
.
          IF        (RECNUM%500) = 0
            DISPLAY   *P19:20,*V2LON,RECNUM,PINVNO
          ENDIF
.
          BRANCH    PINVSYS,RINV1000,RINV2000,RINV3000
          GOTO      RINV0200                      * check next code record
.
.         Emergency
.
RINV1000  PACK      KEY23,PINVNO,SP70
          CALL      RDSDTRE4
RINV1200  CALL      RDKDTRE4
          BRANCH    OVRCD,RINV0200
.
          MATCH     PINVNO,ATINVNO         * Same invoice number?
          GOTO      RINV0200 IF NOT EQUAL
.
          MOVE      "9999999999999999",ATDTBTCH    * set up batch number
          CALL      UPDTRE4
          ADD       ONE,RECNUM
          GOTO      RINV1200
.
.         Outpatient
.
RINV2000 MOVE       "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
          PACK      CFNAME,OSTFILE,FILDTRO4
          OPEN      OUTDTRO4,CFNAME
.
          PACK      KEY23,PINVNO,SP70
          CALL      RDSDTRO4
RINV2200  CALL      RDKDTRO4
          BRANCH    OVRCD OF RINV0200
.
          MATCH     PINVNO,OTINVNO         * Same invoice number?
          GOTO      RINV0200 IF NOT EQUAL
.
          MOVE      "9999999999999999",OTDTBTCH    * set up batch number
          CALL      UPDTRO4
          ADD       ONE,RECNUM
          GOTO      RINV2200
.
.         Inpatient
.
RINV3000  PACK      KEY23,PINVNO,SP70
          CALL      RDSDTRN5
RINV3200  CALL      RDKDTRN5
          BRANCH    OVRCD,RINV0200
.
          MATCH     PINVNO,TREF            * Same invoice number?
          GOTO      RINV0200 IF NOT EQUAL
.
          MOVE      "9999999999999999",PTDTBTCH    * set up batch number
          CALL      UPDTRA5
          ADD       ONE,RECNUM
          GOTO      RINV3200
.
RINV9000  CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:20,*EL,"Records Updated  : ",RECNUM
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
RINV9999  RETURN
+
.**********************************************************************
.         Remove any existing extraction records
.**********************************************************************
REXT0000  PACK      KEY49,SP70
          CALL      RSNZEXT1
          CALL      RKNZEXT1
          BRANCH    OVRCD,REXT9999
.
          MATCH     SP70,NZEXBTID
          GOTO      REXT9999 IF NOT EQUAL
.
         PACK   KEY49,NZEXBTID,NZEXHCOD,NZEXGLBT,NZEXADMN,NZEXINVN,NZEXTRNO,SP70
          CALL      DENZEXT1
          GOTO      REXT0000
.
REXT9999  RETURN
+
.**********************************************************************
.         Update DTR records without updating changed date/time
.**********************************************************************
UPDTRE4   MOVE      ATNUMB,DATNUMB
          MOVE      ATTRANS,DATTRANS
          MOVE      ATRECTYP,DATRECTY
          MOVE      ATNINVS,DATNINVS
.
          UPDATE    AAEDTRE4;DATNUMB,ATINVNO,DATTRANS,ATDDESC,ATPATAMT:
                            ATDDATE,ATITEMNO,ATTYPE,ATPAYTYP,ATRECEPT:
                            ATINVPRT,DATRECTY,ATCHGDTE,ATCHGTIM:
                            ATMISGRP,ATDEPTYP,ATBATCHN:
                            ATPATPOR,ATREBPOR,DATNINVS,ATSERVS:
                            ATDTEFFD,ATSPARE1,ATSPARE2:
                            ATDTGSTA,ATDTGSTC,ATDTGSTM:
                            ATDTCRST,ATDTBTCH,ATDTSUBN,ATDTEDAD:
                            ATDTSVTM,ATSPARE
          RETURN
.
UPDTRO4   MOVE      OTNUMB,DOTNUMB
          MOVE      OTTRANS,DOTTRANS
          MOVE      OTRECTYP,DOTRECTY
          MOVE      OTNINVS,DOTNINVS
.
          UPDATE    OUTDTRO4;DOTNUMB,OTINVNO,DOTTRANS,OTDDESC,OTPATAMT:
                            OTDDATE,OTITEMNO,OTTYPE,OTPAYTYP,OTRECEPT:
                            OTINVPRT,DOTRECTY,OTCHGDTE,OTCHGTIM:
                            OTMISGRP,OTDEPTYP,OTBATCHN:
                            OTPATPOR,OTREBPOR,DOTNINVS,OTSERVS,OTDTGSTA:
                            OTDTGSTC,OTDTGSTM,OTDTEFFD:
                            OTDTCRST,OTDTBTCH,OTSPARE
          RETURN
.
.
UPDTRA5   MOVE      TADMNO,DTADMNO
          MOVE      TTRANSN1,DTTRANSN1
          MOVE      TFCENT,DTFCENT
          MOVE      TFYEAR,DTFYEAR
          MOVE      TFMONTH,DTFMONTH
          MOVE      TFDAY,DTFDAY
          MOVE      TRECTYPE,DTRECTYP
.
          UPDATE    PATDTRA5;DTADMNO,DTTRANSN1,TTYPEDEB,TDCODE,TPATAMTT:
                                 TPATAMTG,TPATAMTH,TPATAMTI,TPATAMTP:
                                 DTFCENT,DTFYEAR,DTFMONTH,DTFDAY:
                                 TTCENT,TTYEAR,TTMONTH,TTDAY:
                                 TTYPE,TITEMNO,TREF,TPAYTYPE,TRECEIPT:
                                 TDDESC,TINVPRT,DTRECTYP:
                                 TNODAYS,TCLAIM,TREBATE:
                                 TDOCTA,TSERVS,TDOCTO,TCHGDTE,TCHGTIM:
                                 TSESSION,TMISGRP,TDEPTYP:
                                 TDWARD,TCLMTYP,TADMTYP,TBATCHN:
                                 TNINVS,PTDTLSPT,PTDTLSRB,PTDTDTYP:
                                 PTDTDES2,PTDTEPSD,PTDTCCU:
                                 PTDTGSTA,PTDTGSTC,PTDTGSTM:
                                 PTDTEFFD,PTDTGSTL,PTDTTIME:
                                 PTDTCRST,PTDTUNIQ,PTDTBTYP:
                                 PTDTCNTR,PTDTCPRC,PTDTCONT:
                                 PTDTSUNQ,PTDTBTCH,TSPARE
.
          RETURN
.
.**********************************************************************
.
          INC       STD001IO
          INC       NZPEXTIO/INC
          INC       PATINVIO/INC
          INC       PATVISIO/INC
          INC       PATDTRIO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       AAEDTRIO/INC
.

.******************************************************************************
.*  Program       : MAKELABL                                                  *
.*  Function      : This program converts the screen painter file entries     *
.*                  for use with the labels screen painting.                  *
.*  Author        : MATT                                                      *
.*                                                                            *
.******************************************************************************
.*                                                                            *
.*  Modifications :                                                           *
.*  V10.03.01 13/12/2012  Steve Armstrong   CAR 277958                        *
.*            Mods to load all screen numbers, not just "01".                 *
.******************************************************************************
.*  V9.09.01  20/09/2007  Steve Armstrong   CAR 150691                        *
.*            Added in Allied Health Labels                                   *
.******************************************************************************
.*  V5.09.B02 30/01/2001  Ebon Clements          5.09 Mods                    *
.*            Added in Doctors Labels                                         *
.*  V5.09.B01 05/10/2000  Caleb Sharman          5.09 Mods                    *
.*            Added in Referral Labels                                        *
.******************************************************************************
.*  V5.07.01  07/12/1998  Glenn Berry            5.07 (Y2K) Changes           *
.*            For date fields on labels, set the minimum screen length to 8   *
.******************************************************************************
.*  V5.05.02  02/01/1998  Steve Armstrong                                     *
.*            Added in code for Emergency Fields (EMR)                        *
.*  V5.05.01  03/10/1997  Howellsy                                            *
.*            Mods to update all fields in SCRFLDFD if record exists already  *
.******************************************************************************
.*  V5.04.01  01/04/1997  Howellsy                                            *
.*            Move "01" to SCMAFSC to fix items being available               *
.*  V5.04.00  05/02/1997  Howellsy                                            *
.*            Ported for NZ                                                   *
.*  V5.03.01  10/10/1995  MATT                                                *
.*            Program written.                                                *
.*                                                                            *
.******************************************************************************
.
+
.
.         Data File Includes.
.
          INC       STD001FD
          INC       SCRFLDFD/INC
          INC       SCRPRGFD/INC
          INC       SCRMASFD/INC
.
.         Program Variables
.
FLDNO     FORM      5
FLDSET    FORM      3
.
INITSCR   INIT      "01"
.
LABPROG   DIM       8
OLDID     DIM       8
.
SAVDES    DIM       35            * description
SAVFLD    DIM       8             * field
SAVFLN    FORM      3.1           * field length
SAVK15    DIM       15
SAVMIN    FORM      3             * minimum length
SAVMAX    FORM      3             * maximum length
SAVFTY    FORM      1             * field type
.
.
.         Program Constants
.
MASDESC   INIT      "PMI Labels"
MISDESC   INIT      "Inpatient Labels"
OUTDESC   INIT      "Outpatient Labels"
WATDESC   INIT      "Waiting List Labels"
AAEDESC   INIT      "A&E Labels"
BOKDESC   INIT      "Booking Labels"
EMRDESC   INIT      "Emergency Labels"
REFDESC   INIT      "Referral Labels"
DOCDESC   INIT      "Doctors Labels"
ALLDESC   INIT      "Allied Health Labels"
.
MASID     INIT      "PATMA1FD"
MISID     INIT      "PATMI1FD"
OUTID     INIT      "OUTBB1FD"
WATID     INIT      "WATTR1FD"
AAEID     INIT      "AAEDE1FD"
BOKID     INIT      "OPRDEAFD"
EMRID     INIT      "EMRVISFD"
REFID     INIT      "OUTRF1FD"
DOCID     INIT      "PATDO1FD"
ALLID     INIT      "ALLREFFD"
.
LABEL01   INIT      "LABELPMI"
LABEL02   INIT      "LABELINP"
LABEL03   INIT      "LABELOUT"
LABEL04   INIT      "LABELWAT"
LABEL05   INIT      "LABELAAE"
LABEL06   INIT      "LABELBOK"
LABEL07   INIT      "LABELEMR"
LABEL08   INIT      "LABELREF"
LABEL09   INIT      "LABELDOC"
LABEL10   INIT      "LABELALL"
.
TILDA15   INIT      "~~~~~~~~~~~~~~~"
.
.

PRGID     INIT      "MAKELABL"
PRGNAM    INIT      "Convert Screen Painter Fields for Labels"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*        ML0000 - Program Logic Control                                      *
.******************************************************************************
.
ML0000    CALL      INIT0000                   * Initalise program.
.
ML1000    CALL      OPTN0000                   * Get option.
          COMPARE   ZERO,OPTION
          GOTO      ML9999 IF EQUAL
.
          LOAD      LABPROG,OPTION,LABEL01,LABEL02,LABEL03,LABEL04:
                                   LABEL05,LABEL06,LABEL07,LABEL08:
                                   LABEL09,LABEL10
.
          CALL      MAKE0000                   * Add the master file
.
          BRANCH    OPTION,ML2000
          CALL      MOTH0000
.
ML2000    CALL      WRHD0000
          CALL      WRMS0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM                        * Exit.
+
.******************************************************************************
.*        INIT0000 - Program initialisation                                   *
.******************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P54:24,"Opening scrfldaf";
          OPEN      SCRFLDA1,"scrfldaf"           * Open screen master file.
.
          DISPLAY   *P62:24,"scrprgaf";
          OPEN      SCRPRGA1,"scrprgaf"           * Open screen master file.
.
          DISPLAY   *P62:24,"scrmasaf";
          OPEN      SCRMASA1,"scrmasaf"           * Open screen master file.
.
INIT9999  RETURN
+
.******************************************************************************
.*        OPTN0000 - Get program option.                                      *
.******************************************************************************
.
OPTN0000  DISPLAY    *P1:4,*EF,*P2:4,*V2LON," 0",*HOFF,". Exit.":
                     *P2:5,*V2LON," 1",*HOFF,". Make PMI Labels.":
                     *P2:6,*V2LON," 2",*HOFF,". Make Admission Labels.":
                     *P2:7,*V2LON," 3",*HOFF,". Make Outpatient Labels.":
                     *P2:8,*V2LON," 4",*HOFF,". Make Waiting List Labels.":
                     *P2:9,*V2LON," 5",*HOFF,". Make A&E Labels.":
                     *P2:10,*V2LON," 6",*HOFF,". Make Booking Labels.":
                     *P2:11,*V2LON," 7",*HOFF,". Make Emergency (EMR) Labels.":
                     *P2:12,*V2LON," 8",*HOFF,". Make Referral Labels.":
                     *P2:13,*V2LON," 9",*HOFF,". Make Doctors Labels.":
                     *P2:14,*V2LON,"10",*HOFF,". Make Allied Health Labels.":
                     *P2:16,"Select Option : ";
OPTN1000  KEYIN      *P18:16,*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9999 IF EQUAL
.
          BRANCH     OPTION,OPTN9999,OPTN9999,OPTN9999,OPTN9999,OPTN9999:
                            OPTN9999,OPTN9999,OPTN9999,OPTN9999,OPTN9999
.
          BEEP
          GOTO       OPTN1000
.
OPTN9999  RETURN
+
.******************************************************************************
.*        MAKE0000 - Get the masterfile fields and add them to the label      *
.*                   fields.                                                  *
.******************************************************************************
.
MAKE0000  MOVE      ZERO,FLDSET
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON,"Program : ",*P30:24,"Screen : ":
                    *P55:24,"Item : "; 
.
          PACK      KEY15,MASID,SP20
          CALL      RSSCFL1
MAKE1000  CALL      RKSCFL1
          BRANCH    OVRCD,MAKE9999
.
          MATCH     MASID,SCFLPID                * Check if using old FD.
          GOTO      MAKE9999 IF NOT EQUAL
.
          DISPLAY   *P30:24,SCFLPID,*P39:24,SCFLSID,*P62:24,SCFLFNO;
.
.         Save the relevant variables for updating later
.
          MOVE      SCFLDES,SAVDES
          MOVE      SCFLFLD,SAVFLD
          MOVE      SCFLMIN,SAVMIN
          MOVE      SCFLMAX,SAVMAX
          MOVE      SCFLFTY,SAVFTY
          MOVE      SCFLFLN,SAVFLN
.
          CALL      WRIT0000                       * Update the position file.
.
          GOTO      MAKE1000
.
MAKE9999  RETURN
+
.******************************************************************************
.*        MOTH0000 - Get the existing fields from the selected system and     *
.*                   add them for the labels.                                 *
.******************************************************************************
.
MOTH0000  MOVE      "500",FLDSET
.
          LOAD      OLDID,OPTION,MASID,MISID,OUTID,WATID,AAEID,BOKID,EMRID:
                                 REFID,DOCID,ALLID
.
          PACK      KEY15,OLDID,SP20
          CALL      RSSCFL1
MOTH1000  CALL      RKSCFL1
          BRANCH    OVRCD,MOTH9999
.
          MATCH     OLDID,SCFLPID                * Check if using old FD.
          GOTO      MOTH9999 IF NOT EQUAL
.
          DISPLAY   *P30:24,SCFLPID,*P39:24,SCFLSID,*P62:24,SCFLFNO;
.
.         Save the relevant variables for updating later
.
          MOVE      SCFLDES,SAVDES
          MOVE      SCFLFLD,SAVFLD
          MOVE      SCFLMIN,SAVMIN
          MOVE      SCFLMAX,SAVMAX
          MOVE      SCFLFTY,SAVFTY
          MOVE      SCFLFLN,SAVFLN
.
          CALL      WRIT0000                       * Update the position file.
.
          GOTO      MOTH1000
.
MOTH9999  RETURN
+
.******************************************************************************
.*        WRIT0000 - Write the new Field records.                             *
.******************************************************************************
.
WRIT0000  PACK      SAVK15,SCFLPID,SCFLSID,SCFLFNO
          MOVE      LABPROG,SCFLPID
          MOVE      SCFLFNO,FLDNO
          ADD       FLDSET,FLDNO
          MOVE      FLDNO,SCFLFNO
.
          PACK      KEY15,SCFLPID,SCFLSID,SCFLFNO,SP10
          CALL      RDSCFL1
          BRANCH    OVRCD,WRIT5000
.
WRIT1000  MOVE      SAVDES,SCFLDES          * reload saved variables
          MOVE      SAVFLD,SCFLFLD
          MOVE      SAVMIN,SCFLMIN
          MOVE      SAVMAX,SCFLMAX
          MOVE      SAVFTY,SCFLFTY
          MOVE      SAVFLN,SCFLFLN
.
          MOVE      ZERO,SCFLMUL
          MOVE      ZERO,SCFLMAN
          MOVE      TWO,SCFLTYP 
.
.         Labels may use dates without centuries, so set minimum display length
.
WRIT4000  IF        SCFLFLN=8.0 & SCFLMAX=10
             MOVE      EIGHT,SCFLMIN
          ENDIF
.
          CALL      UPSCFL1
          GOTO      WRIT7000 
.
WRIT5000  MOVE      ZERO,SCFLMUL
          MOVE      ZERO,SCFLMAN
          MOVE      TWO,SCFLTYP 
.
.         Labels may use dates without centuries, so set minimum display length
.
          IF        SCFLFLN=8.0 & SCFLMAX=10
             MOVE      EIGHT,SCFLMIN
          ENDIF
.
          CALL      WRSCFL1
.
.         Check if there is another screen id for the same program id
.
WRIT7000  PACK      KEY15,SCFLPID,SCFLSID,TILDA15
          CALL      RSSCFL1                      * posn at end of current screen
          CALL      RKSCFL1                      * read next record
          BRANCH    OVRCD,WRIT9000               * eof - finished
.
          MATCH     LABPROG,SCFLPID              * same program id still ?
          GOTO      WRIT9000 IF NOT EQUAL        * no - finished
.
.         There is another screen id for the same program id
.
          PACK      KEY15,SCFLPID,SCFLSID,FLDNO,SP10
          CALL      RDSCFL1                      * field on file ?
          IF        OVRCD
            MOVE      FLDNO,SCFLFNO
            MOVE      SAVDES,SCFLDES
            MOVE      SAVFLD,SCFLFLD
            MOVE      SAVMIN,SCFLMIN
            MOVE      SAVMAX,SCFLMAX
            MOVE      SAVFTY,SCFLFTY
            MOVE      SAVFLN,SCFLFLN
            GOTO      WRIT5000                   * no - write new record
          ELSE
            GOTO      WRIT1000                   * yes - update record
          ENDIF
.
WRIT9000  MOVE      SAVK15,KEY15
          CALL      RDSCFL1
.
WRIT9999  RETURN
+
.******************************************************************************
.*        WRMS0000 - Write the new Program Master Record.                     *
.******************************************************************************
.
WRMS0000  PACK      KEY10,LABPROG,INITSCR
          CALL      RDSCMA1
          COMPARE   ZERO,OVRCD
          GOTO      WRMS5000 IF EQUAL
.
          MOVE      LABPROG,SCMAPID
          MOVE      INITSCR,SCMASID
          LOAD      SCMADES,OPTION,MASDESC,MISDESC,OUTDESC,WATDESC:
                                   AAEDESC,BOKDESC,EMRDESC,REFDESC:
                                   DOCDESC,ALLDESC
          MOVE      EIGHT,SCMATOP
          MOVE      ONE,SCMALEF
          MOVE      TWENTY2,SCMABOT
          MOVE      EIGHTY,SCMARIG
          MOVE      ZERO,SCMASEX
          MOVE      "01",SCMAFSC
.
          CALL      WRSCMA1
          GOTO      WRMS9999
.
WRMS5000  MOVE      "01",SCMAFSC
          CALL      UPSCMA1
.
WRMS9999  RETURN
+
.******************************************************************************
.*        WRHD0000 - Write the new Program Record.                            *
.******************************************************************************
.
WRHD0000  PACK      KEY8,LABPROG
          CALL      RDSCPR1
          COMPARE   ZERO,OVRCD
          GOTO      WRHD9999 IF EQUAL
.
          MOVE      LABPROG,SCPRPID
          LOAD      SCPRNAM,OPTION,MASDESC,MISDESC,OUTDESC,WATDESC:
                            AAEDESC,BOKDESC,EMRDESC,REFDESC:
                            DOCDESC,ALLDESC
          CALL      WRSCPR1
.
WRHD9999  RETURN
+
.
.         Program Includes.
.
          INC       STD001IO
          INC       SCRFLDIO/INC
          INC       SCRPRGIO/INC
          INC       SCRMASIO/INC
+

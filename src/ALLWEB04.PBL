.-----------------------------------------------------------------------
. Program       : ALLWEB04
.
. Function      : Equipment Loans Web Server
.
. Modifications : 
.----------------------------------------------------------------------
. V11.03.01 01/12/2022  Bella Turco    TSK 0921957
.           Added NOMORE00 & PTCNNRTD to MAITB000 routine           
. V11.02.04 01/08/2022  Bella Turco    TSK 0921957
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V11.02.03 09/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.02.02 07/02/2022  Thanh T       TSK 0896905
.           Recompiled for ALLDEPFD changes
. V11.02.01 24/01/2022  Jacob Jackson  TSK 0864505
.           Added preferred name label options -
.           MRPRN612, MRPRN613, MRPRN614, MRPRN615, MRPRN616
.           Recompiled ALLN0000
. V11.01.01 27/04/2021  Ebon Clements  TSK 0904025
.           Changed equipment loans list to use a new index by loan status
.           EQPTB000
. V11.00.02 09/12/2020  Thanh T        TSK 0872840
.           Recompiled as PMSPX2TM changes
. V11.00.01 06/04/2020  Thanh T        TSK 0879163                    
.           Changed MRPRN568 for Sex category.                        
.           Added MRPRN611 for sex long description                   
.----------------------------------------------------------------------
. V10.14.02 09/05/2019  Thanh T         TSK 0873242
.           Changed EROW0000 to fix the issue with equipment status not
.           showing correctly in Equipment status list  
. V10.14.01 08/05/2019  Thanh T         TSK 0873127
.           Fixed the equipment status is not update correctly when it is
.           changed from Inactive/written off and Needs Maintenance   
.           (ALLMAI10)
. V10.13.02 05/12/2018  Don Nguyen      TSK 0838558
.           Deleted temp file (ALCSTFIL) after processing (SCTTMP00)
. V10.13.01 30/07/2018  Ebon Clements   TSK 0815359
.           Don't display IP department in selection lists
. V10.12.01 16/01/2018  Thanh T.        TSK 0850311
.           Fixed loan equipment status is not displayed correctly when an
.           item has been returned with a status of written off
. V10.11.06 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.05 11/09/2017  Thanh T.        TSK 0821710
.           Audit Amission/outpatient visit comments. Removed PATCOMFD
. V10.11.04 21/08/2017  Ebon Clements   TSK 0843761
.           Added read of PMI master file for loan notes - LONNOT40
. V10.11.03 30/07/2017  Thanh T.        TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.02 26/07/2017  Thanh T.        TSK 0821710
.           Recompiled PASMISTM 
. V10.11.01 14/07/2017  Thanh T.        TSK 0821710
.           Audit Admission/outpatient visit comments. PATMISTM changed.
. V10.10.01 28/03/2017  Thanh T.        TSK 0832066
.           Changed DVA PREPAT/PMPXDVAC fields to use the new
.           concession card PMCDCNUM/PMCDDVAC fields
. V10.08.01 08/07/2016  Jill Parkinson  TSK 0822002
.           Recompiled for PATMASTM
. V10.05.01 03/10/2014  Ebon Clements   CAR 268970
.           Change to use OUTCLIFD index 1 instead of index 2
. V10.04.02 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
. V10.03.06 19/03/2013  Jill Parkinson CAR 282814
.           Recompiled for PATMASTM - Disability alert DSAL0000 change
. V10.03.05 10/12/2012  Ebon Clements CAR 261630
.           Removed port number from temp file name
. V10.03.04 02/10/2012  Mike Laming   CAR 273880
.           Corrected "STATUS" in EQPTB000 (at EQPTB100 - 200)
. V10.03.03 25/07/2012  B.G.Ackland
.           New Tags for
.           01.133 output patient loans with date/time of loan and no highlights
.           01.134 equipment by department table sort list
.           01.135 department filter select list
. V10.03.02 11/05/2012  Ebon Clements    CAR 264296
.           Changed loan created by tag to display the users name - ALLN2000
. V10.03.01 16/01/2012  Sandra Barcham  CAR 256563
.           Recompiled for PATMASTM
. V10.02.03 11/10/2011  Mark Coupland    CAR 252429
.           Recompiled for PMSPX2TM and PATMASTM
. V10.02.02 13/09/2011 Jill Parkinson  CAR 248652
.           Recompiled for PMSPX2TM - Added read of pmsvx1af in GETCURIP
. V10.02.01 04/04/2011  Jill Parkinson CAR 239574 
.           Removed open of ibaprntf            
. V10.00.04 20/10/2010  Mike Laming   CAR 192466
.           Added Report 08 - Equipment Loan Notes (LONNOT00 +)
. V10.00.03 12/10/2010  Mike Laming   CAR 178888
.           Added "Returned Condition" test to set "Equip.Status" at ALLLON45
.           Changes to Equip.Maintenance at ALLMAI10 (Add) ALLMAI20 (Update) 
.           Add "write Maintenance record" in ALLLON45
.           Added a second "Equipment Loan List" (Hosp.level) at EROW5000
. V10.00.02 06/07/2010  Ebon Clements CAR 217645
.           Added update of loan type to print loan info sheet - ALLLON30
. V10.00.01 26/07/2010  Ebon Clements CAR 225970
.           Added URNUMBER and ADMISSNO to CLRPAR00
.           Added loan number to redirect for add new loan ALLLON00
. V9.12.02  09/03/2010  Mike Laming   CAR 217476
.           Added Dates in "ccyymmdd" format for Equip.Sort List at EQPTB200/300
. V9.12.01  05/01/2010  Ebon Clements CAR 152047
.           Added print selected form option to equipment loans
.           Added vars 101 - 110 to read/print loaned equipment  1 to 10        
. V9.11.07  22/04/2009  Mike Laming   CAR 194450
.           Fix Status display at ALLTB110
. V9.11.06  03/04/2009  Saroeun Soeur CAR 192750
.           Fixed consumable ALLOEDAT problem
. V9.11.05  22/01/2009  Ebon Clements CAR 182024
.           Added equipment type filter to the equipment list
. V9.11.04  31/10/2008  Peter McMullen CAR 174725
.           convert internal javascript to W3C standards
. V9.11.03  31/10/2008  Peter Vela    CAR 181821
.           If EQITYPE is spaces don't filter equipment loans list.
. V9.11.02  17/10/2008  Ebon Clements CAR 178881
.           Don't update equipment balance when updating a return - ALLLON40
. V9.11.01  16/10/2008  Ebon Clements CAR 178882
.           Added HCP filter to the equipment loans list - EQPTB000 
. V9.10.01  24/08/2008  Peter Vela    CAR 151951
.           Added PMI variables to Loan Equipment Template variables type 17
. V9.09.03  24/08/2007  Ebon Clements CAR 133923
.           Changed department selection lists to be in alphabetical order
. V9.09.02  31/07/2007  Davin         CAR 76341
.           Added option 6 for SCTT
. V9.09.01  26/07/2007  Mike Laming   CAR 143939
.           Change "Expected Return Date" when "Next Review Date" is entered
. V9.05.02  27/03/2006  Peter Vela    CAR 61299
.           Increased field no size from DIM3 to DIM5 
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B05 21/02/2006  Mike Laming   CAR 87666
.           Changed ALLOTHER to use PMSHCPAF  instead of WEBSECAF
. V9.05.B04 07/02/2006  Ebon Clements CAR 87670
.           Added report 7 equipment search
. V9.05.B03 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
. V9.05.B02 21/12/2005  Ebon Clements  CAR 76341
.           Added report 5 patient care team detail maintenance
. V9.04.07  21/11/2005  Peter Vela     CAR 68675
.           Recompiled for PATDSCTM
. V9.04.06  07/09/2005  Ebon Clements  CAR 73702
.           Added active status check to department selection list ARFD0000
. V9.04.05  05/09/2005  Peter Vela    CAR 72693
.           Fixed lower casing for new patient name format
. V9.04.04  24/08/2005  Peter Vela    CAR 72693
.           Added new format for patient name
.           Stationery template type 17 Allied Health Forms MRPRN000
. V9.04.03  01/03/2005  Peter Vela    CAR 57547
.           Recompiled for PATVADFD and PATVADIO
. V9.04.02  17/12/2004  Ebon Clements CAR 55406
.           Added report 4 equipment status list
. V9.04.01  21/09/2004  Lina Vo       CAR 52582
.           Re-read websecaf with userid after displaying Theapist Field
.           in ALLN0700. This was causing the wrong printer list to be
.           displayed for the current user.
. V9.03.03  13/06/2004  Pat Dirito    CAR 50842                       
.           - CGI var allnprnt was not being assigned to SELPRINT
. V9.03.02  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      
.           - Add Sex Description routine SEXDSC00 with Code "R" - "Intersex"
. V9.03.01  09/06/2004  Pat Dirito               CAR 50552
.           Added Overdue record mark for Equipment lists   
.           07/06/2004  Peter Vela               CAR 49016
.           2004 Statutory Changes - recompiled for PMSPX2TM
. V9.03.00  27/11/2003  Pat Dirito               CAR 43997
.           Created Web Server.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
          INC       ALLCSTDF
.
          INC       ALLEQUTD/INC
          INC       ALLPCTFD/INC
          INC       ALLCASFD/INC
          INC       ALLCSTFD/INC   * SCTT comments file
          INC       ALLSCTFD/INC
          INC       ALLSCTTD/INC
          INC       PATDO1FD/INC   * Doctor File
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PMSCCDFD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       OUTSITFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCTYFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATWR1FD/INC
          INC       PRIITMFD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       TFILEVAR/INC
          INC       VARWISCM/INC
          INC       IBATMDFD/INC
.
          INC       ALLCONFD/INC
          INC       ALLLONFD/INC
          INC       ALLEQUFD/INC
          INC       ALLMAIFD/INC
          INC       ALLDEPFD/INC
          INC       ALLEDTFD/INC
          INC       ALLETXFD/INC
.
.----------------------------------------------------------------------
ALPCT001  DIM       8
ALPCT002  DIM       2
ALPCT003  DIM       10
ALPCT004  DIM       6
ALPCT005  DIM       1
.
BLNKFLAG  FORM      1
CFILEPRE  DIM       3
COND1     FORM      1
COND2     FORM      1
CTR       FORM      1       * Counter
DEPARTMN  DIM       3       * Department code
DEPTCODE  DIM       3       * Department code
DSPLDATE  DIM       12      * Display Date
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
UPDATETY  DIM       1       * another Update TypE
STARTKEY  DIM       10      * Starting Key
STATNCOD  DIM       6
NOTENUMB  DIM       6       * note number
NOTETYPE  DIM       3       * note type
NOTEFILT  DIM       3       * note display filter
NOTEORDR  FORM      1       * Display notes order
CURRDATE  DIM       8       * Current Date
CSEC      DIM       2
FILTTYPE  DIM       3
FORM8     FORM      8 
FORM3     FORM      3 
FORM5     FORM      5
CURRROW   FORM      3
LASTROW   FORM      3
LASTITEM  FORM      3                       * Last Item of FF Notes
LOANTYPE  DIM       20
DOCTCODE  DIM       8
FILTDEPT  DIM       3                       * Allied Health Department Code (CG)
FILTSTAT  DIM       1                       
FILTHCPC  DIM       10
DIM3      DIM       3
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8
DIM14     DIM       14
DIM35     DIM       35
PSAGETYP  DIM       3
MBSDESC   DIM       70
PSAGECON  DIM       3
CODERROR  DIM       125     * Output Web error
CATQM     INIT      "QM"
CATQO     INIT      "QO"
CATU1     INIT      "U1"
CATU2     INIT      "U2"
CATU3     INIT      "U3"
CATU4     INIT      "U4"
CATU5     INIT      "U5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
CATVI     INIT      "VI"
FIRSTCH   FORM      1 
LINE60    DIM       60 
LOANCNTR  FORM      2
SITEDESC  DIM       30
STATUS    DIM       20
STATDESC  DIM       30
STRIKETG  DIM       10                      * Strike tag
STRENDTG  DIM       11                      * Strike end tag
SUPRFLAG  DIM       1                       * Supervisor flag for deleted notes
SQUOTE    INIT      "'"
TEAMDESC  DIM       50
TOTAL     FORM      8.2
TYPEDESC  DIM       11
PACKLINK  DIM       70      * Test
PRNTSTRT  FORM      3
VARIABLE  DIM       127
LOANDATE  DIM       11
EXPRETRN  DIM       11
ACTRETRN  DIM       11
OPFLGSCT  FORM      1          * Open Flag for SCTT File
READLOAN  FORM      2
SAVESTAT  DIM       1
SCTTDESC  DIM       30                                * SCTT Description
SCTTDSC1  INIT      "Consumer Information"            * SCTT Description 1
SCTTDSC2  INIT      "Summary/Referral Information"    * SCTT Description 2
SCTTCNTR  DIM       8
FONTR     INIT      "<font color=red>"
FONTREND  INIT      "</font>"
.
ALLON001  DIM       8      * U/R Number
ALLON002  DIM       8      * Loan Number
ALLON003  DIM       3      * Loan Group (Category QC)
ALLON004  DIM       10     * Equipment Code (allequaf)
ALLON005  DIM       8      * Loan Date (CCYYMMDD)
ALLON006  DIM       8      * Loan Time (HH:MM:SS)
ALLON007  DIM       10     * Therapist WEB User ID (websecaf)
ALLON008  DIM       3      * Type of Loan (Category QL)
ALLON009  DIM       1      * Status
ALLON010  DIM       8      * Expected Return Date (CCYYMMDD)
ALLON011  DIM       8      * Review Date (CCYYMMDD)
ALLON012  DIM       8      * Quantity Issued
ALLON013  DIM       10     * Issuing WEB User ID (websecaf)
ALLON014  DIM       8      * Return Date (CCYYMMDD)
ALLON015  DIM       8      * Return Time (HH:MM:SS)
ALLON016  DIM       10     * Returning WEB User ID (websecaf)
ALLON017  DIM       3      * Condition of Return (Category QO)
ALLON018  DIM       8      * Date Record Created (CCYYMMDD)
ALLON019  DIM       8      * Time Record Created (HH:MM:SS)
ALLON020  DIM       10     * WEB User ID Created (websecaf)
ALLON021  DIM       8      * Date Record Updated (CCYYMMDD)
ALLON022  DIM       8      * Time Record Updated (HH:MM:SS)
ALLON023  DIM       10     * WEB User ID Updated (websecaf)
ALLON024  DIM       8      * Next Review Date 
ALLON025  DIM       3      * Reason for Review 
.
SETNPRNT  FORM      1      * Print Info Sheet
.
ALMAI001  DIM       10     * Equipment Code
ALMAI002  FORM      8      * Maintenance Number
ALMAI003  DIM       8      * Maintenance Date (CCYYMMDD)
ALMAI004  DIM       8      * Maintenance Time (HH:MM:SS)
ALMAI005  DIM       3      * Condition After Main (Cat QM)
ALMAI006  DIM       8      * Hazard Notice Date (CCYYMMDD)
ALMAI007  DIM       10     * Hazard Notice Ref No.
.
LOANNUMB  DIM       8      * Loan Number for loading details off intial List   
HOMELOCA  DIM       3      * Home Location CGI
EQIPTYPE  DIM       3      * Equipment Type CGI
EQIPSTAT  DIM       1      * Equipment Status CGI
.
. temporary Notes File
COMFILVF  FILE      ASCII, FIXED=127   * for Equipment Loan Notes 
COMFILNV  DIM       8
HIGHOVER  FORM      1
.
.----------------------------------------------------------------------
PRGID     INIT      "ALLWEB04"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Equipment Loans Web Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          CALL      OUTFIL00       * Check Correct Site Files Are Open
          BRANCH    EXIT,MAIN1000
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500:
                             MAIN1600,MAIN1700,MAIN1800
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      ALLLON00        * Patient Equipment Loans
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      ALLMAI00        * Patient Equipment Services Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      LONLST00        * Equipment Loans List
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      EQPLST00        * Equipment Lists
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1500  CALL      CARETM00        * Patient Care Team Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1600  IF        OPFLGSCT = 1
            MOVE      "Option Not Available!",WEBTITLE
            CALL      WEBERR00
            MOVE      ZERO,REPORTNO
            GOTO      MAIN1000
          ENDIF
          CALL      SCTTMP00        * Service Coordination Tool Templates
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1700  CALL      EQSRCH00        * Equipment Loans List
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1800  CALL      LONNOT00        *  Output Table of Equipment Loan Notes
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00        *  next page
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40,NXTPAG50
.
          CALL      SPCCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG50  CALL      WINCLS00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------
. Refresh Parent and Close Window
.------------------------------------------------------------
SPCCLS00  CALL      OPENHTML
          BRANCH    EXIT,SPCCLS99
          WRITE     HTMLFILE;"<script type=#"text/javascript#" ":
                               "src=#"../js/Standard.js#"></script>":
                               "<script type=#"text/javascript#" ":
                               "src=#"../js/Custom.js#"></script>":
                             "<script type=#"text/javascript#"> {":
                             " if (self.name==#"PopUpFrame#") {":
                             "  reloadParentFrame();} ":
                             " else if (self.name==#"PopUpFrame1#") {":
                             "  reload2ParentFrame();} ":
                             " else { ":
                             "  opener.history.go(0);":
                             "  self.close(); }":
                             "}":
                             "</script>"
          CALL      CLOSHTML     * End of Document
SPCCLS99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Output HTML Error File
.------------------------------------------------------------
CODERR00  CALL      OPENHTML
          BRANCH    EXIT,CODERR95
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
          " var PopUpScreen = parent.document.getElementById('PopUpScreen');":
                    "  alert(#"",CODERROR,"#");":
                    "  if (window.name.substr(0,4)=='Hide') {":
                    "    self.close();} else {":
                    "  if (PopUpScreen) {":
                    "    PopUpScreen.style.display='none'; } else {":
                    "    history.go(-1); } }":
                    "}":
                    "</script>",012
.
          CALL      CLOSHTML     * End of Document
          GOTO      CODERR99
.
CODERR95  DISPLAY   "*** Fifo Not Found - ",FILNAM," ***"
.
CODERR99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM 
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      IBATDET1,"ibatmdaf"
          OPEN      IBASPOL1,"ibaspolf"
          OPEN      IBATMHA1,"ibatmhaf"
.
          OPEN      PRIITEM1,"priitemf"
          OPEN      PRIITEM2,"priitemf"
          OPEN      ALLLONA1,"alllonaf"
          OPEN      ALLLONA4,"alllonaf"
          OPEN      ALLLONA5,"alllonaf"
          OPEN      ALLEQUA1,"allequaf"
          OPEN      ALLEQUA6,"allequaf"
          OPEN      ALLMAIA1,"allmaiaf"
          OPEN      ALLMAIA2,"allmaiaf"
          OPEN      APSMASA1,"apsmasaf"
          OPEN      ALLDEPA1,"alldepaf"
          OPEN      ALLPCTA1,"allpctaf"
          OPEN      ALLCASA1,"allcasaf"
          OPEN      ALLEDTA1,"alledtaf"
          OPEN      ALLETXA1,"alletxaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSCCDA1,"pmsccdaf"
.
          MOVE      ZERO,OPFLGSCT
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ALLSCTA1,"allsctaf"          * SCTT table
          TRAPCLR   IO
          MOVE      OVRCD,OPFLGSCT
.
          IF        OPFLGSCT = 0
            OPEN      ALLCSTA1,"allcstaf"        * SCTT Comments table
          ENDIF
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND06;*21,ALCNSCOD
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND03;*212,PTCNPFSN,PTCNPFGN
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          OPEN      OUTSITA1,"outsitaf"     * Open the site file
          MOVE      "out",CFILEPRE          * Save Current File Prefix
          CALL      OPNOUT00                * Open Outpatient Files
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,COMFILNV
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,DEPTCODE
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,UPDATETY
          MOVE      ZERO,SETNPRNT
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,LOANNUMB
          MOVE      SP70,HOMELOCA
          MOVE      SP70,EQIPTYPE
          MOVE      SP70,EQIPSTAT
          MOVE      SP70,SELPRINT
          MOVE      SP70,SCTTCNTR
          MOVE      SP70,STATNCOD
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,NOTENUMB
          MOVE      SP70,NOTETYPE
          MOVE      SP70,NOTEFILT
          MOVE      SP70,NOTEORDR
          MOVE      SP70,SUPRFLAG
. 
          MOVE      Z70,ALLON001
          MOVE      Z70,ALLON002
          MOVE      Z70,ALLON003
          MOVE      Z70,ALLON004
          MOVE      Z70,ALLON005
          MOVE      Z70,ALLON006
          MOVE      Z70,ALLON007
          MOVE      Z70,ALLON008
          MOVE      Z70,ALLON009
          MOVE      Z70,ALLON010
          MOVE      Z70,ALLON011
          MOVE      Z70,ALLON012
          MOVE      Z70,ALLON013
          MOVE      Z70,ALLON014
          MOVE      Z70,ALLON015
          MOVE      Z70,ALLON016
          MOVE      Z70,ALLON017
          MOVE      Z70,ALLON018
          MOVE      Z70,ALLON019
          MOVE      Z70,ALLON020
          MOVE      Z70,ALLON021
          MOVE      Z70,ALLON022
          MOVE      Z70,ALLON023
          MOVE      Z70,ALLON024
          MOVE      Z70,ALLON025
.
          MOVE      SP70,ALMAI001
          MOVE      ZERO,ALMAI002
          MOVE      SP70,ALMAI003
          MOVE      SP70,ALMAI004
          MOVE      SP70,ALMAI005
          MOVE      SP70,ALMAI006
          MOVE      SP70,ALMAI007
          MOVE      SP70,FILTDEPT
          MOVE      SP70,FILTHCPC
          MOVE      SP70,FILTSTAT
.
          MOVE      SP70,ALPCT001
          MOVE      SP70,ALPCT002
          MOVE      SP70,ALPCT003
          MOVE      SP70,ALPCT004
          MOVE      SP70,ALPCT005
          MOVE      SP70,FILTTYPE
.
          CALL      CPALSC00
          CALL      CLALEQ00
.
          CALL      CLRACM00           * Create comments temp file
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updatety=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDATETY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.  
          MATCH     "setnprnt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SETNPRNT
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "homeloca=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,HOMELOCA
            PACK      HOMELOCA,HOMELOCA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "loannumb=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LOANNUMB
            PACK      LOANNUMB,LOANNUMB,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "eqiptype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EQIPTYPE
            PACK      EQIPTYPE,EQIPTYPE,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "eqipstat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EQIPSTAT
            PACK      EQIPSTAT,EQIPSTAT,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "notetype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NOTETYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "notenumb=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NOTENUMB
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "noteordr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NOTEORDR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "notefilt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NOTEFILT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lonnotes=",QRYNAME                               *I-192466
          IF        @EQUAL
            CALL      GETNOT00              * common Notes
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "suprflag=",QRYNAME                               *I-192466
          IF        @EQUAL
            MOVE      QRYDATA,SUPRFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "allnprnt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SELPRINT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "allon",QRYNAME
          IF        @EQUAL
            CALL      STALN000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "almai",QRYNAME
          IF        @EQUAL
            CALL      STMAI000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtdept=",QRYNAME
          IF        @EQUAL
            PACK      FILTDEPT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filthcpc=",QRYNAME
          IF        @EQUAL
            PACK      FILTHCPC,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtstat=",QRYNAME
          IF        @EQUAL
            PACK      FILTSTAT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "alpct",QRYNAME
          IF        @EQUAL
            CALL      STALP000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "alsct",QRYNAME
          IF        @EQUAL
            CALL      SPALS000              * For SCTT table
            GOTO      SETPAR99
          ENDIF     
.
          MATCH     "alequ",QRYNAME
          IF        @EQUAL
            CALL      STEQU000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "deptcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEPTCODE
            PACK      DEPTCODE,DEPTCODE,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "scttcntr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCTTCNTR
            GOTO      SETPAR99
          ENDIF     
.
          MATCH     "filttype=",QRYNAME
          IF        @EQUAL
            PACK      FILTTYPE,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF     
.
          MATCH     "statncod=",QRYNAME
          IF        @EQUAL
            PACK      STATNCOD,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF     
.
          MATCH     "alcst",QRYNAME              * SCTT Comments
          IF        @EQUAL
            CALL      SETACM00
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 1 - Equipment Loans        
.-------------------------------------------------------------------------------
STALN000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STALN001,STALN002,STALN003,STALN004,STALN005:
                       STALN006,STALN007,STALN008,STALN009,STALN010:
                       STALN011,STALN012,STALN013,STALN014,STALN015:
                       STALN016,STALN017,STALN018,STALN019,STALN020:
                       STALN021,STALN022,STALN023,STALN024,STALN025
          GOTO      STALN999
.
STALN001  MOVE      QRYDATA,ALLON001
          PACK      ALLON001,ALLON001,SP70
          GOTO      STALN999
.
STALN002  MOVE      QRYDATA,ALLON002
          PACK      ALLON002,ALLON002,SP70
          GOTO      STALN999
.
STALN003  MOVE      QRYDATA,ALLON003
          GOTO      STALN999
.
STALN004  SQUEEZE   QRYDATA
          MOVE      QRYDATA,ALLON004
          PACK      ALLON004,ALLON004,SP70
          GOTO      STALN999
.
STALN005  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON005,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON005
          GOTO      STALN999
.
STALN006  MOVE      QRYDATA,ALLON006
          GOTO      STALN999
.
STALN007  MOVE      QRYDATA,ALLON007
          GOTO      STALN999
.
STALN008  MOVE      QRYDATA,ALLON008
          GOTO      STALN999
.
STALN009  MOVE      QRYDATA,ALLON009
          GOTO      STALN999
.
STALN010  RESET     QRYDATA 
          MATCH     SP70,QRYDATA
          GOTO      STALN999 IF EQUAL 
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON010,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON010
          GOTO      STALN999
.
STALN011  RESET     QRYDATA 
          MATCH     SP70,QRYDATA
          GOTO      STALN999 IF EQUAL 
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON011,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON011
          GOTO      STALN999
.
STALN012  MOVE      QRYDATA,ALLON012
          GOTO      STALN999
.
STALN013  MOVE      QRYDATA,ALLON013
          GOTO      STALN999
.
STALN014  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON014,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON014
          GOTO      STALN999
.
STALN015  MOVE      QRYDATA,ALLON015
          GOTO      STALN999
.
STALN016  MOVE      QRYDATA,ALLON016
          GOTO      STALN999
.
STALN017  MOVE      QRYDATA,ALLON017
          GOTO      STALN999
.
STALN018  MOVE      QRYDATA,ALLON018
          GOTO      STALN999
.
STALN019  MOVE      QRYDATA,ALLON019
          GOTO      STALN999
.
STALN020  MOVE      QRYDATA,ALLON020
          GOTO      STALN999
.
STALN021  MOVE      QRYDATA,ALLON021
          GOTO      STALN999
.
STALN022  MOVE      QRYDATA,ALLON022
          GOTO      STALN999
.
STALN023  MOVE      QRYDATA,ALLON023
          GOTO      STALN999
.
STALN024  RESET     QRYDATA
          MATCH     SP70,QRYDATA
          GOTO      STALN999 IF EQUAL 
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON024,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON024
          GOTO      STALN999
.
STALN025  MOVE      QRYDATA,ALLON025
          GOTO      STALN999
.
STALN999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 2 - Equipment Service Maintenance
.-------------------------------------------------------------------------------
STMAI000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STMAI001,STMAI002,STMAI003,STMAI004,STMAI005:
                       STMAI006,STMAI007
          GOTO      STMAI999
.
STMAI001  MOVE      QRYDATA,ALMAI001
          PACK      ALMAI001,ALMAI001,SP70
          GOTO      STMAI999
.
STMAI002  MOVE      QRYDATA,ALMAI002
          GOTO      STMAI999
.
STMAI003  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALMAI003,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALMAI003
          GOTO      STMAI999
.
STMAI004  MOVE      QRYDATA,ALMAI004
          GOTO      STMAI999
.
STMAI005  MOVE      QRYDATA,ALMAI005
          GOTO      STMAI999
.
STMAI006  RESET     QRYDATA 
          MATCH     SP70,QRYDATA
          GOTO      STMAI999 IF EQUAL 
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALMAI006,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALMAI006
          GOTO      STMAI999
.
STMAI007  MOVE      QRYDATA,ALMAI007
          GOTO      STMAI999
.
STMAI999  RETURN
.------------------------------------------------------------
. Patient Equipment Loans
.------------------------------------------------------------
ALLLON00  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE 
.
          MATCH     SP70,UPDTTYPE
          GOTO      ALLLON70 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add Loan        
          GOTO      ALLLON10 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete             
          GOTO      ALLLON20 IF EQUAL
.
          MATCH     "P",UPDTTYPE            * Print Information Sheet
          GOTO      ALLLON30 IF EQUAL
.
          MATCH     "R",UPDTTYPE            * Return Equipment from Patient
          GOTO      ALLLON40 IF EQUAL
.
          MATCH     "L",UPDTTYPE            * Place on Review 
          GOTO      ALLLON50 IF EQUAL
.
          MATCH     "F",UPDTTYPE            * Print Forms
          GOTO      ALLLON60 IF EQUAL
.
          GOTO      ALLLON90
.
.   ************ ADD FORMACTION ************
.
ALLLON10  MOVE      URNUMBER,KEY8           * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,ALLLON91
.
          PACK      KEY10,ALLON004,SP70     * Read Equipment File
          CALL      RDALEQU1                 
          BRANCH    OVRCD,ALLLON93
.
          MATCH     "0",ALEQSTAT            * Check Status
          GOTO      ALLLON94 IF NOT EQUAL
.
          COMPARE   ZERO,ALEQCBAL           * Check Current Balance
          GOTO      ALLLON95 IF EQUAL 
.                                           * Check Quantity <= to Current Bal
          MOVE      ZERO,FORM8
          MOVE      ALLON012,FORM8          * Quantity
          IF        FORM8 > ALEQCBAL
            GOTO      ALLLON96
          ENDIF
.
          CALL      GETLON00                * Generate Loan Number
          MOVE      F8,ALLON002
.
          CALL      CLRALN00                * Clear all vars as still sitting on
.                                           * last read record.
          PACK      KEY16,ALLON001,ALLON002,SP70
          CALL      RDALLON1                * Add into Current File
          COMPARE   ZERO,OVRCD
          GOTO      ALLLON92 IF EQUAL
.
          CALL      SAVALN00
          CALL      WRALLON1
.
          MATCH     "4",NEXTPAGE
          IF        @EQUAL
            STRIP     REDIRECT
            ENDSET    REDIRECT
            APPEND    "&allon002=",REDIRECT
            APPEND    ALLOLOAN,REDIRECT
            RESET     REDIRECT
          ENDIF
.
.
.    Set Equipment Status to Loaned if Current Bal = 0 
.    First subtract Quantity from Current Balance
.
          MOVE      ZERO,F8
          MOVE      ZERO,FORM8
          MOVE      ALLON012,FORM8          * Quantity   
          ASSIGN    (ALEQCBAL-FORM8),F8
          MOVE      F8,ALEQCBAL
.
          COMPARE   ZERO,ALEQCBAL           * Current Balance equal to zero?
          IF        @EQUAL
            MOVE      "1",ALEQSTAT          * Set to Currently on Loan
          ENDIF
.
          MOVE      CURRDATE,ALEQSDAT       * Date Status Set
.
          CALL      UPALEQU1                * Update Equipment Table
.
          COMPARE   ONE,SETNPRNT            * print Info Sheet
          GOTO      ALLLON11 IF NOT EQUAL
.
          PACK      KEY16,ALLON001,ALLON002,SP70   * Read Loan File
          CALL      RDALLON1                 
          BRANCH    OVRCD,ALLLON91
.
          MOVE      ALCNSCOD,STATNCOD
          CALL      MRPRN000                * Print Info Details
.
ALLLON11  MOVE      ZERO,EXIT
          GOTO      ALLLON99
.
.         ************ DELETE FORMACTION **********
.
ALLLON20  MOVE      URNUMBER,KEY8   * Read Master File 
          CALL      RDMAST1
          BRANCH    OVRCD,ALLLON91
.
          PACK      KEY16,ALLON001,ALLON002,SP70
          CALL      RDALLON1                * Add into Current File
          BRANCH    OVRCD,ALLLON92          * No, Display error message
.
          CALL      DEALLON1                * Delete record
.
          MOVE      ZERO,EXIT
          GOTO      ALLLON99
.
.         ************ PRINT INFORMATION SHEET FORMACTION **********
.
ALLLON30  MOVE      URNUMBER,KEY8   * Read Master File 
          CALL      RDMAST1
          BRANCH    OVRCD,ALLLON91
.
          PACK      KEY16,ALLON001,ALLON002,SP70
          CALL      RDALLON1                * Add into Current File
          BRANCH    OVRCD,ALLLON92          * No, Display error message
.
          MATCH     Z70,ALLON008
          IF        !@EQUAL
            MOVE      ALLON008,ALLOTYPE   * Type of Loan (Category QL)
          ENDIF
.
          CALL      UPALLON1
.
          PACK      KEY10,ALLOEQUI,SP70     * Read Equipment File
          CALL      RDALEQU1                 
          BRANCH    OVRCD,ALLLON93
.
          COMPARE   ONE,SETNPRNT            * print Info Sheet
          GOTO      ALLLON11 IF NOT EQUAL
.
          MOVE      ALCNSCOD,STATNCOD
          CALL      MRPRN000                * Print Info Details
.
          MOVE      ZERO,EXIT
          GOTO      ALLLON99
.
.         ************ RETURN EQUIPMENT FROM LOAN FORMACTION **********
.
ALLLON40  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,ALLLON91
.
          PACK      KEY16,ALLON001,ALLON002,SP70
          CALL      RDALLON1               * Add into Current File
          BRANCH    OVRCD,ALLLON92         * No, Display error message
.
          PACK      KEY10,ALLOEQUI,SP70    * Read Equipment File
          CALL      RDALEQU1                 
          BRANCH    OVRCD,ALLLON93
.
          MOVE      ALLOSTAT,SAVESTAT      * Save original loan status
.
          MOVE      ALLON014,ALLORDAT      * Date Returned  
          MOVE      ALLON015,ALLORTIM      * Time Returned  
          MOVE      ALLON017,ALLOCOND      * Condition on Return
          MOVE      ONE,ALLOSTAT           * Status = Returned
.
          CALL      UPALLON1               * Update record
.
.   First check if the equipment return record is being update. If so
.   we do not want to update the currect balance.
.   Set Current Balance by adding Quantity back to it. Also set Status if  
.   needed when Current Balance was is now greater than zero.
.   
          MATCH     SAVESTAT,ALLOSTAT       * Status has not changed
          GOTO      ALLLON49 IF EQUAL
.
.                                           * check Loan Status       *C-178888
ALLLON45  UNPACK    SP70,TCINDC1,TDESC
          PACK      KEY5,CATQO,ALLON017,SP10
          CALL      RDCODE1              
          MATCH     "0",TCINDC1             * check if Condition is OK
          IF        @EQUAL
            MOVE      ZERO,F8
            ASSIGN    (ALEQCBAL + ALLOQTYI),F8
            MOVE      F8,ALEQCBAL           * Set Current Balance
            MOVE      "0",ALEQSTAT          * Set to "Available For Loan"
          ELSE 
            CALL      CLRMAI00              * Clear all ALLMAIaf vars
            MOVE      ALLOEQUI,ALMAI001     * create a Maintenance record
            CALL      GETMAI00              * get new Maintenance No.
            MOVE      FORM8,ALMAI002
            CALL      SAVMAI00
            MOVE      CURRDATE,ALMAMDAT     * Date Record Returned(CCYYMMDD)
            PACK      KEY18,ALLOEQUI,ALMAI002,SP20
            CALL      WRALMAI1              * write Maintenance record
            MOVE      "3",ALEQSTAT          * default to "needs maintenance"
            MATCH     "2",TCINDC1           * check if Condition is OK
            IF        @EQUAL
              MOVE      TCINDC1,ALEQSTAT
            ENDIF
          ENDIF
.                                                              * end  *C-178888
.
          COMPARE   ZERO,ALEQCBAL           * Current Balance Not equal to zero?
          IF        !@EQUAL
            MOVE      "0",ALEQSTAT          * Set to "Available For Loan"
          ENDIF
.
          MOVE      CURRDATE,ALEQSDAT       * Date Status Set
.
          CALL      UPALEQU1                * Update Equipment Table
.
ALLLON49  MOVE      ZERO,EXIT
          GOTO      ALLLON99
.
.         ************ PLACE EQUIPMENT ON REVIEW FORMACTION **********
.
ALLLON50  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,ALLLON91
.
          PACK      KEY16,ALLON001,ALLON002,SP70
          CALL      RDALLON1               * Add into Current File
          BRANCH    OVRCD,ALLLON92         * No, Display error message
.
          PACK      KEY10,ALLOEQUI,SP70    * Read Equipment File
          CALL      RDALEQU1                 
          BRANCH    OVRCD,ALLLON93
.
          MOVE      ALLON011,ALLOVDAT      * Review Date 
          MOVE      ALLON024,ALLONDAT      * Next Review Date
          MOVE      ALLON024,ALLOEDAT      * new Expected Return Date *I-143939
          MOVE      ALLON007,ALLOTHER      * Therapist
          MOVE      ALLON025,ALLOREAS      * Reason For Review
.
          CALL      UPALLON1               * Update record
.
          MOVE      ZERO,EXIT
          GOTO      ALLLON99
.
.         ************ PRINT FORMS FORMACTION **********
.         
ALLLON60  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,ALLLON91
.
          PACK      KEY16,ALLON001,ALLON002,SP70
          CALL      RDALLON1                * Add into Current File
          IF        OVRCD=1
            CALL      CLRALN00
          ENDIF
.
          PACK      KEY10,ALLOEQUI,SP70     * Read Equipment File
          CALL      RDALEQU1
          IF        OVRCD=1
            CALL      CLALLEQU
          ENDIF
.
          PACK      KEY6,STATNCOD,SP70
          CALL      RDIBTH1
          BRANCH    OVRCD,ALLLON97
.
          MATCH     " 17",IBTHSTYP          * Equipment Loans
          GOTO      ALLLON97 IF NOT EQUAL
.
          CALL      MRPRN000                * Print Info Details
.
          MOVE      ZERO,EXIT
          GOTO      ALLLON99
.
.         ************ DISPLAY FORMACTION **********
.
ALLLON70  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,ALLLON91
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          PACK      KEY16,ALLON001,ALLON002,SP70
          CALL      RDALLON1                * Add into Current File
          IF        OVRCD=1
            CALL      CLRALN00
          ELSE 
            PACK      KEY10,ALLOEQUI,SP70     * Read Equipment File
            CALL      RDALEQU1                 
            BRANCH    OVRCD,ALLLON93
          ENDIF
.
          CLEAR     WEBTITLE
          MATCH     "1",SUPRFLAG
          IF        @EQUAL
            MOVE      "Supervisor Patient Equipment Loans",WEBTITLE
          ELSE
            MOVE      "Patient Equipment Loans",WEBTITLE
          ENDIF
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ALLLON99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      ALLLON99
.
ALLLON90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLLON99
.
ALLLON91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLLON99
.
ALLLON92  MOVE      "Equipment Loan record already exists. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLLON99
.
ALLLON93  MOVE      "Equipment Code record does not exists. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLLON99
.
ALLLON94  MOVE      "Equipment Code is not available for loan. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLLON99
.
ALLLON95  MOVE      "Equipment Code current balance is 0. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLLON99
.
ALLLON96  MOVE      "Quantity must be less than or equal to the",WEBTITLE
          ENDSET    WEBTITLE
          APPEND    " Equipment Current Balance of ",WEBTITLE
          ENDSET    WEBTITLE
          MOVE      ALEQCBAL,DIM8
          SQUEEZE   DIM8
          STRIP     DIM8
          APPEND    DIM8,WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLLON99
.
ALLLON97  MOVE      "Invalid stationery code.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLLON99
.
ALLLON99  RETURN
.------------------------------------------------------------
. Generate New Loan Number   
.------------------------------------------------------------
GETLON00  MOVE      ZERO,F8
.
          PACK      KEY16,ALLON001,Z70
          CALL      RSALLON1                
          CALL      RPALLON1                
          BRANCH    OVRCD,GETLON90          
.
          MATCH     ALLON001,ALLOURNO       * Match on U/R
          GOTO      GETLON90 IF NOT EQUAL
.
          MOVE      ALLOLOAN,F8
          ADD       ONE,F8
          GOTO      GETLON99
.
GETLON90  MOVE      ONE,F8
.
GETLON99  RETURN
.------------------------------------------------------------
. Clear Equipment Loan record
.------------------------------------------------------------
CLRALN00  MOVE      SP70,ALLOURNO   * U/R Number
          MOVE      SP70,ALLOLOAN   * Loan Number
          MOVE      SP70,ALLOLGRP   * Loan Group (Category QC)
          MOVE      SP70,ALLOEQUI   * Equipment Code (allequaf)
          MOVE      SP70,ALLOLDAT   * Loan Date (CCYYMMDD)
          MOVE      SP70,ALLOLTIM   * Loan Time (HH:MM:SS)
          MOVE      SP70,ALLOTHER   * Therapist WEB User ID (websecaf)
          MOVE      SP70,ALLOTYPE   * Type of Loan (Category QL)
          MOVE      SP70,ALLOSTAT   * Status
          MOVE      SP70,ALLOEDAT   * Expected Return Date (CCYYMMDD)
          MOVE      SP70,ALLOVDAT   * Review Date (CCYYMMDD)
          MOVE      ZERO,ALLOQTYI   * Quantity Issued
          MOVE      SP70,ALLOIUID   * Issuing WEB User ID (websecaf)
          MOVE      SP70,ALLORDAT   * Return Date (CCYYMMDD)
          MOVE      SP70,ALLORTIM   * Return Time (HH:MM:SS)
          MOVE      SP70,ALLORUID   * Returning WEB User ID (websecaf)
          MOVE      SP70,ALLOCOND   * Condition of Return (Category QO)
          MOVE      SP70,ALLOCDAT   * Date Record Created (CCYYMMDD)
          MOVE      SP70,ALLOCTIM   * Time Record Created (HH:MM:SS)
          MOVE      SP70,ALLOCUID   * WEB User ID Created (websecaf)
          MOVE      SP70,ALLOUDAT   * Date Record Updated (CCYYMMDD)
          MOVE      SP70,ALLOUTIM   * Time Record Updated (HH:MM:SS)
          MOVE      SP70,ALLOUUID   * WEB User ID Updated (websecaf)
          MOVE      SP70,ALLONDAT   * Next Review Date (CCYYMMDD)
          MOVE      SP70,ALLOREAS   * Reason For Review (Category QE)
          MOVE      SP70,ALLOSPAR   * Spare Variable
.
CLRALN99  RETURN
.-----------------------------------------------------------
.         Clears the Equipment Loan notes file variables              *I-192466
.-----------------------------------------------------------
CLLNOT00  MOVE      SP70,ALEDURNO      * U/R Number
          MOVE      SP70,ALEDLOAN      * Loan Number
          MOVE      SP70,ALEDTYPE      * Comment Type
          MOVE      SP70,ALEDNOTE      * Note Number
          MOVE      SP70,ALEDDATE      * Input Date (CCYYMMDD)
          MOVE      SP70,ALEDTIME      * Input Time (HH:MM:SS)
          MOVE      SP70,ALEDUSER      * Input by WEB User ID (websecaf)
          MOVE      SP70,ALEDOCCG      * Occupational Group WEB User ID
          MOVE      SP70,ALEDDELT      * Delete Indicator (0=No,1=Yes)
          MOVE      SP70,ALEDDDAT      * Delete Date (CCYYMMDD)
          MOVE      SP70,ALEDDTIM      * Delete Time (HH:MM:SS)
          MOVE      SP70,ALEDDUSE      * Delete by WEB User ID (websecaf)
          MOVE      SP70,ALEDDOCC      * Occupational Group WEB User ID Dele
          MOVE      SP70,ALEDSPAR      * Spare Variable
.
          MOVE      SP70,ALETURNO      * U/R Number
          MOVE      SP70,ALETLOAN      * Loan Number
          MOVE      SP70,ALETTYPE      * Comment Type
          MOVE      SP70,ALETNOTE      * Note Number
          MOVE      SP70,ALETUNIQ      * Line Number
          MOVE      SP70,ALETCMNT      * Comment
          MOVE      SP70,ALETSPAR      * Spare Variable
.
CLENOT99  RETURN
.
.------------------------------------------------------------
. Get Equipment Loan Notes                                            *I-192466
.------------------------------------------------------------
GETNOT00  PREP      COMFILVF,COMFILNV
.
          MOVE      ONE,F3
          WRITE     COMFILVF,SEQ;QRYDATA
.
GETNOT10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA
          GOTO      GETNOT90 IF OVER
          MATCH     SP70,QRYNAME
          GOTO      GETNOT80 IF NOT EQUAL
          ADD       ONE,F3
          WRITE     COMFILVF,SEQ;QRYDATA
          GOTO      GETNOT10
.
GETNOT80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
GETNOT90  MOVE      F3,LASTITEM
          OPEN      COMFILVF,COMFILNV
GETNOT99  RETURN
.
.------------------------------------------------------------
. Save Patient Equipment Loan Data
.------------------------------------------------------------
SAVALN00  MATCH     Z70,ALLON001
          IF        !@EQUAL
            MOVE      ALLON001,ALLOURNO   * U/R Number
          ENDIF
.
          MATCH     Z70,ALLON002
          IF        !@EQUAL
            MOVE       ALLON002,ALLOLOAN   * Loan Number
          ENDIF
.
          MATCH     Z70,ALLON003
          IF        !@EQUAL
            MOVE      ALLON003,ALLOLGRP   * Loan Group (Category QC)
          ENDIF
.
          MATCH     Z70,ALLON004
          IF        !@EQUAL
            MOVE      ALLON004,ALLOEQUI   * Equipment Code (allequaf)
          ENDIF
.
          MATCH     Z70,ALLON005
          IF        !@EQUAL
            MOVE      ALLON005,ALLOLDAT   * Loan Date (CCYYMMDD)
          ENDIF
.
          MATCH     Z70,ALLON006
          IF        !@EQUAL
            MOVE      ALLON006,ALLOLTIM   * Loan Time (HH:MM:SS)
          ENDIF
.
          MATCH     Z70,ALLON007
          IF        !@EQUAL
            MOVE      ALLON007,ALLOTHER   * Therapist WEB User ID (websecaf)
          ENDIF
.
          MATCH     Z70,ALLON008
          IF        !@EQUAL
            MOVE      ALLON008,ALLOTYPE   * Type of Loan (Category QL)
          ENDIF
.
          MATCH     Z70,ALLON010
          IF        !@EQUAL
            MOVE      ALLON010,ALLOEDAT   * Expected Return Date (CCYYMMDD)
          ENDIF
.
          MATCH     Z70,ALLON011
          IF        !@EQUAL
            MOVE      ALLON011,ALLOVDAT   * Review Date (CCYYMMDD)
          ENDIF
.
          MATCH     Z70,ALLON012
          IF        !@EQUAL
            MOVE      ALLON012,ALLOQTYI   * Quantity Issued
          ENDIF
.
          MATCH     "A",UPDTTYPE    * Add Formaction 
          IF        @EQUAL
            MOVE      USERID,ALLOIUID   * Issuing WEB User ID (websecaf)
            MOVE      CURRDATE,ALLOCDAT * Date Record Created (CCYYMMDD)
            MOVE      CTIMEIS,ALLOCTIM  * Time Record Created (HH:MM:SS)
            MOVE      USERID,ALLOCUID   * WEB User ID Created (websecaf)
            MOVE      "0",ALLOSTAT      * Status set to Loaned
            MOVE      SP70,ALLOUDAT     * Date Record Updated (CCYYMMDD)
            MOVE      SP70,ALLOUTIM     * Time Record Updated (HH:MM:SS)
            MOVE      SP70,ALLOUUID     * WEB User ID Updated (websecaf)
          ENDIF
.
          MATCH     "U",UPDTTYPE    * Update Formaction 
          IF        @EQUAL
            MOVE      CURRDATE,ALLOUDAT * Date Record Updated (CCYYMMDD)
            MOVE      CTIMEIS,ALLOUTIM  * Time Record Updated (HH:MM:SS)
            MOVE      USERID,ALLOUUID   * WEB User ID Updated (websecaf)
          ENDIF
.
          MATCH     "R",UPDTTYPE    * Return Formaction
          IF        @EQUAL
            MOVE      USERID,ALLORUID   * Returning WEB User ID (websecaf)
            MOVE      "1",ALLOSTAT      * Status set to Returned
          ENDIF
.
          MATCH     Z70,ALLON014
          IF        !@EQUAL
            MOVE      ALLON014,ALLORDAT   * Return Date (CCYYMMDD)
          ENDIF
.
          MATCH     Z70,ALLON015
          IF        !@EQUAL
            MOVE      ALLON015,ALLORTIM   * Return Time (HH:MM:SS)
          ENDIF
.
          MATCH     Z70,ALLON017
          IF        !@EQUAL
            MOVE      ALLON017,ALLOCOND   * Condition of Return (Category QO)
          ENDIF
.
          MATCH     Z70,ALLON024
          IF        !@EQUAL
            MOVE      ALLON024,ALLONDAT   * Next Review Date (CCYYMMDD)
          ENDIF
.
          MATCH     Z70,ALLON025
          IF        !@EQUAL
            MOVE      ALLON025,ALLOREAS   * Reason for Review 
          ENDIF
.
          MOVE      SP70,ALLOSPAR   * Spare Variable
.
SAVALN99  RETURN
.------------------------------------------------------------
. Equipment Service
.------------------------------------------------------------
ALLMAI00  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE 
.
          MATCH     SP70,UPDTTYPE
          GOTO      ALLMAI70 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add Formaction  
          GOTO      ALLMAI10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update Formaction
          GOTO      ALLMAI20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete Formaction  
          GOTO      ALLMAI30 IF EQUAL
.
          GOTO      ALLMAI90
.
.   ************ ADD FORMACTION ************
.
ALLMAI10  PACK      KEY10,ALMAI001,SP70     * Read Equipment File
          CALL      RDALEQU1                 
          BRANCH    OVRCD,ALLMAI91
.
          CALL      GETMAI00                * Generate Service Number
          MOVE      FORM8,ALMAI002
.
          CALL      CLRMAI00                * Clear all vars as still sitting on
.                                           * last read record.
.
          PACK      KEY18,ALMAI001,ALMAI002,SP70
          CALL      RDALMAI1                * Add into Current File
          COMPARE   ZERO,OVRCD
          GOTO      ALLMAI92 IF EQUAL
.
          CALL      SAVMAI00
          CALL      WRALMAI1
.
          SUB       ONE,ALEQCBAL            * reduce "Current Bal" for new Maint
.
          UNPACK    SP20,TCINDC1            * check Maintenance Status *I-178888
          PACK      KEY5,CATQM,ALMAI005,SP70
          CALL      RDCODE1
          MATCH     "0",TCINDC1
          IF        @EQUAL
            ADD       ONE,ALEQCBAL          * add one to "Current balance"
            IF        ALEQCBAL > 0
              MOVE      "0",ALEQSTAT        * set to "Available for Loan"
            ENDIF
          ELSE
            IF      ALEQCBAL = 0
              MATCH   "3",TCINDC1
              IF      @EQUAL
                MOVE    "3",ALEQSTAT        * set to "Needs Maintenance"
              ELSE
                MATCH   "2",TCINDC1
                IF      @EQUAL
                  MOVE    "2",ALEQSTAT      * set to "Inactive/Written Off"
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          CALL      UPALEQU1                * update Equipment record
.                                                              * end  *I-178888
.
          MOVE      ZERO,EXIT
          GOTO      ALLMAI99
.
.         ************ UPDATE FORMACTION **********
.
ALLMAI20  PACK      KEY10,ALMAI001,SP70     * Read Equipment File
          CALL      RDALEQU1                 
          BRANCH    OVRCD,ALLMAI91
.
          PACK      KEY18,ALMAI001,ALMAI002,SP70
          CALL      RDALMAI1               
          BRANCH    OVRCD,ALLMAI93
.
          MOVE      ALMACOND,KEY3           * save previous Condition
.
          CALL      SAVMAI00
          CALL      UPALMAI1
.
          MATCH     KEY3,ALMACOND           * has Condition changed?  *I-178888
          IF        !@EQUAL
            UNPACK    SP20,TCINDC1          * check Maintenance Status
            PACK      KEY5,CATQM,KEY3,SP70
            CALL      RDCODE1
            MOVE      "9",COND1             * set default in form field
            MOVE      TCINDC1,COND1
.
            UNPACK    SP20,TCINDC1          * check Maintenance Status
            PACK      KEY5,CATQM,ALMACOND,SP70
            CALL      RDCODE1
            MOVE      "9",COND2             * set default in form field
            MOVE      TCINDC1,COND2
.
            IF        COND1 <> 0 & COND2 = 0
              ADD       ONE,ALEQCBAL        * TCINDC1 change to "0" - available
            ENDIF
            IF        COND1 = 0 & COND2 <> 0
              SUB       ONE,ALEQCBAL        * TCINDC1 changed to "maintenance"
            ENDIF
            MOVE      "3",ALEQSTAT          * set to "Needs Maintenance"
            IF        ALEQCBAL > 0
              MOVE      "0",ALEQSTAT        * set to "Available for Loan"
            ENDIF
            CALL      UPALEQU1              * update Equipment record
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      ALLMAI99
.
.         ************ DELETE FORMACTION **********
.
ALLMAI30  PACK      KEY10,ALMAI001,SP70     * Read Equipment File
          CALL      RDALEQU1                 
          BRANCH    OVRCD,ALLMAI91
.
          PACK      KEY18,ALMAI001,ALMAI002,SP70
          CALL      RDALMAI1                
          BRANCH    OVRCD,ALLMAI93          
.
          CALL      DEALMAI1                * Delete record
.
          MOVE      ZERO,EXIT
          GOTO      ALLMAI99
.
.         ************ DISPLAY FORMACTION **********
.
ALLMAI70  PACK      KEY10,ALMAI001,SP70     * Read Equipment File
          CALL      RDALEQU1                 
          BRANCH    OVRCD,ALLMAI91

          PACK      KEY18,ALMAI001,ALMAI002,SP70
          CALL      RDALMAI1                
          IF        OVRCD=1
            CALL      CLRMAI00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Equipment Service",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ALLMAI99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      ALLMAI99
.
ALLMAI90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLMAI99
.
ALLMAI91  MOVE      "Equipment Code does not exists. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLMAI99
.
ALLMAI92  MOVE      "Equipment Service record already exists. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLMAI99
.
ALLMAI93  MOVE      "Equipment Service record does not exists. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLMAI99
.
ALLMAI99  RETURN
.------------------------------------------------------------
. Generate New Loan Number   
.------------------------------------------------------------
GETMAI00  MOVE      ZERO,FORM8
.
          PACK      KEY18,ALMAI001,Z70
          CALL      RSALMAI1                
          CALL      RPALMAI1                
          BRANCH    OVRCD,GETMAI90          
.
          MATCH     ALMAI001,ALMAEQUI       * Match on Equipment Code
          GOTO      GETMAI90 IF NOT EQUAL
.
          MOVE      ALMAMNUM,FORM8
          ADD       ONE,FORM8
          GOTO      GETMAI99
.
GETMAI90  MOVE      ONE,FORM8
.
GETMAI99  RETURN
.------------------------------------------------------------
. Clear Equipment Service record
.------------------------------------------------------------
CLRMAI00  MOVE      SP70,ALMAEQUI   * Equipment Code
          MOVE      SP70,ALMAMNUM   * Maintenance Number
          MOVE      SP70,ALMAMDAT   * Maintenance Date (CCYYMMDD)
          MOVE      SP70,ALMAMTIM   * Maintenance Time (HH:MM:SS)
          MOVE      SP70,ALMACOND   * Condition After Main (Cat QM)
          MOVE      SP70,ALMAHDAT   * Hazard Notice Date (CCYYMMDD)
          MOVE      SP70,ALMAHNUM   * Hazard Notice Ref No.
          MOVE      SP70,ALMACDAT   * Date Record Created (CCYYMMDD)
          MOVE      SP70,ALMACTIM   * Time Record Created (HH:MM:SS)
          MOVE      SP70,ALMACUSR   * WEB User ID Created
          MOVE      SP70,ALMAUDAT   * Date Record Updated (CCYYMMDD)
          MOVE      SP70,ALMAUTIM   * Time Record Updated (HH:MM:SS)
          MOVE      SP70,ALMAUUSR   * WEB User ID Updated
          MOVE      SP70,ALMASPAR   * Spare
.
CLRMAI99  RETURN
.------------------------------------------------------------
. Save Equipment Service record
.------------------------------------------------------------
SAVMAI00  MOVE      ALMAI001,ALMAEQUI   * Equipment Code
          MOVE      ALMAI002,ALMAMNUM   * Maintenance Number
          MOVE      ALMAI003,ALMAMDAT   * Maintenance Date (CCYYMMDD)
          MOVE      ALMAI004,ALMAMTIM   * Maintenance Time (HH:MM:SS)
          MOVE      ALMAI005,ALMACOND   * Condition After Main (Cat QM)
          MOVE      ALMAI006,ALMAHDAT   * Hazard Notice Date (CCYYMMDD)
          MOVE      ALMAI007,ALMAHNUM   * Hazard Notice Ref No.
.
          MATCH     "A",UPDTTYPE    * Add Formaction  
          IF        @EQUAL 
            MOVE      CURRDATE,ALMACDAT * Date Record Created (CCYYMMDD)
            MOVE      CTIMEIS,ALMACTIM  * Time Record Created (HH:MM:SS)
            MOVE      USERID,ALMACUSR   * WEB User ID Created
            MOVE      SP70,ALMAUDAT     * Date Record Updated (CCYYMMDD)
            MOVE      SP70,ALMAUTIM     * Time Record Updated (HH:MM:SS)
            MOVE      SP70,ALMAUUSR     * WEB User ID Updated
          ELSE
            MOVE      CURRDATE,ALMAUDAT * Date Record Updated (CCYYMMDD)
            MOVE      CTIMEIS,ALMAUTIM  * Time Record Updated (HH:MM:SS)
            MOVE      USERID,ALMAUUSR   * WEB User ID Updated
          ENDIF
          MOVE      SP70,ALMASPAR   * Spare
.
SAVMAI99  RETURN
.------------------------------------------------------------
. Equipment Service
.------------------------------------------------------------
LONLST00  CLEAR     WEBTITLE
          MOVE      "Equipment Loan List",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LONLST99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      LONLST99
.
LONLST99  RETURN
.------------------------------------------------------------
. MRPRN000                           
. Routine to print the Equipment Loans Information Sheet       
.------------------------------------------------------------
MRPRN000  CALL      OPNPRINT                   * open the spool file
.
          PRINT     *N;
          MOVE      ONE,CURRROW
.
          PACK      KEY12,STATNCOD,SP70
          CALL      RSIBDET1                   * position on the details file
          CALL      RKIBDET1
          BRANCH    OVRCD,MRPRN990
.
          MATCH     STATNCOD,IBDTSCOD           * match stationary codes
          GOTO      MRPRN990 IF NOT EQUAL
.
          MOVE      IBDTROW,LASTROW
.
          COMPARE   CURRROW,IBDTROW            * compare rows
          GOTO      MRPRN200 IF EQUAL
.
.------ print new-lines until current row equals the one read in ------
.
MRPRN050  PRINT     *N;
          ADD       ONE,CURRROW
.
          COMPARE   CURRROW,IBDTROW            * compare rows
          GOTO      MRPRN200 IF EQUAL
.
          GOTO      MRPRN050
.
.------ read through the details file ------
.
MRPRN100  CALL      RKIBDET1
          BRANCH    OVRCD,MRPRN900
.
          MATCH     STATNCOD,IBDTSCOD           * match stationary codes
          GOTO      MRPRN900 IF NOT EQUAL
.
          MOVE      IBDTROW,LASTROW
.
          COMPARE   CURRROW,IBDTROW            * compare rows
          GOTO      MRPRN200 IF EQUAL
.
.------ print new-lines until current row equals the one read in ------
.
MRPRN110  PRINT     *N;
          ADD       ONE,CURRROW
.
          COMPARE   CURRROW,IBDTROW            * compare rows
          GOTO      MRPRN200 IF EQUAL
.
          GOTO      MRPRN110
.
.------ branch on the field type ------
.
MRPRN200  BRANCH    IBDTINDC,MRPRN300,MRPRN400
.
.------ the field is a text field ------
.
MRPRN300  RESET     IBDTTXFL,IBDTPLEN
          LENSET    IBDTTXFL
          RESET     IBDTTXFL
          PRINT     *IBDTCOL,*+,IBDTTXFL;
          GOTO      MRPRN100
.
.------ the field is a data field ------
.
MRPRN400  MOVE      IBDTTXFL,DIM5
          MOVE      DIM5,FORM5
.
          BRANCH    FORM5,MRPRN501,MRPRN502,MRPRN503,MRPRN504,MRPRN505: * 5
                          MRPRN506,MRPRN507,MRPRN508,MRPRN509,MRPRN510: * 10
                          MRPRN511,MRPRN512,MRPRN513,MRPRN514,MRPRN515: * 15
                          MRPRN516,MRPRN517,MRPRN518,MRPRN519,MRPRN520: * 20
                          MRPRN521,MRPRN522,MRPRN523,MRPRN524,MRPRN525: * 25
                          MRPRN526,MRPRN527,MRPRN528,MRPRN529,MRPRN530: * 30
                          MRPRN531,MRPRN532,MRPRN533,MRPRN534,MRPRN535: * 25
                          MRPRN536,MRPRN537,MRPRN538,MRPRN539,MRPRN540: * 40
                          MRPRN541,MRPRN542,MRPRN543,MRPRN544,MRPRN545: * 45
                          MRPRN546,MRPRN547,MRPRN548,MRPRN549,MRPRN550: * 50
                          MRPRN551,MRPRN552,MRPRN553,MRPRN554,MRPRN555: * 55
                          MRPRN556,MRPRN557,MRPRN558,MRPRN559,MRPRN560: * 60
                          MRPRN561,MRPRN562,MRPRN563,MRPRN564,MRPRN565: * 65
                          MRPRN566,MRPRN567,MRPRN568,MRPRN569,MRPRN570: * 70
                          MRPRN571,MRPRN572,MRPRN573,MRPRN574,MRPRN575: * 75
                          MRPRN576,MRPRN577,MRPRN578,MRPRN579,MRPRN580: * 80
                          MRPRN581,MRPRN582,MRPRN583,MRPRN584,MRPRN585: * 85
                          MRPRN586,MRPRN587,MRPRN588,MRPRN589,MRPRN590: * 90
                          MRPRN591,MRPRN592,MRPRN593,MRPRN594,MRPRN595: * 95
                          MRPRN100,MRPRN100,MRPRN598,MRPRN599,MRPRN600: * 100
                          MRPRN601,MRPRN602,MRPRN603,MRPRN604,MRPRN605: * 105
                          MRPRN606,MRPRN607,MRPRN608,MRPRN609,MRPRN610: * 110
                          MRPRN611,MRPRN612,MRPRN613,MRPRN614,MRPRN615: * 115
                          MRPRN616
.
.------ format U/R number ------
.
MRPRN501  MOVE      ALLOURNO,VARIABLE
          MOVE      EIGHT,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN750
.
.------ format Loan number ------
.
MRPRN502  MOVE      ALLOLOAN,VARIABLE
          MOVE      EIGHT,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN750
.
.------ Loan Group ------
.
MRPRN503  MOVE      "QC",CATEGORY
          MOVE      ALLOLGRP,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format Equipment Code ------
.
MRPRN504  MOVE      ALLOEQUI,VARIABLE
          MOVE      TEN,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN750
.
.------ format Loan Date ------
.
MRPRN505  UNPACK    ALLOLDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format Loan Time ------
.
MRPRN506  MOVE      ALLOLTIM,DIM5
          MOVE      DIM5,VARIABLE
          GOTO      MRPRN740
.
.------ Therapist Web User ID ------
.------ Therapist HCP Code    ------                                   *C-87666
.
MRPRN507  PACK      KEY10,ALLOTHER,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,MRPRN100
.
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
          MOVE      DOCFNAME,VARIABLE
          GOTO      MRPRN740
.
.------ Type Of Loan ------
.
MRPRN508  MOVE      "QL",CATEGORY
          MOVE      ALLOTYPE,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format Status ------
.
MRPRN509  MATCH     "0",ALLOSTAT
          IF        @EQUAL
            MOVE      "Loaned",VARIABLE
            GOTO      MRPRN740
          ENDIF
.
          MATCH     "1",ALLOSTAT
          IF        @EQUAL
            MOVE      "Returned",VARIABLE
            GOTO      MRPRN740
          ENDIF
.
          MATCH     "2",ALLOSTAT
          IF        @EQUAL
            MOVE      "Cancelled",VARIABLE
            GOTO      MRPRN740
          ENDIF
          GOTO      MRPRN740
.
.------ format Expected Return Date ------
.
MRPRN510  UNPACK    ALLOEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format Review Date ------
.
MRPRN511  UNPACK    ALLOVDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ Quantity Issued ------
.
MRPRN512  MOVE      SP70,VARIABLE
          MOVE      ALLOQTYI,VARIABLE
          GOTO      MRPRN740
.
.------ Issuing WEB User ID ------
.
MRPRN513  PACK      KEY10,ALLOIUID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,MRPRN100
.
          MOVE      WBSENAM,VARIABLE 
          GOTO      MRPRN740
.
.------ format Return Date ------
.
MRPRN514  UNPACK    ALLORDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format Return Time ------
.
MRPRN515  MOVE      ALLORTIM,DIM5
          MOVE      DIM5,VARIABLE
          GOTO      MRPRN740
.
.------ Returning WEB User ID ------
.
MRPRN516  PACK      KEY10,ALLORUID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,MRPRN100
.
          MOVE      WBSENAM,VARIABLE 
          GOTO      MRPRN740
.
.------ Condition Of Return ------
.
MRPRN517  MOVE      "QO",CATEGORY
          MOVE      ALLOCOND,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format Date Record Created ------
.
MRPRN518  UNPACK    ALLOCDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format Time Record Created ------
.
MRPRN519  MOVE      ALLOCTIM,DIM5
          MOVE      DIM5,VARIABLE
          GOTO      MRPRN740
.
.------ WEB User ID Created ------
.
MRPRN520  PACK      KEY10,ALLOCUID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,MRPRN100
.
          MOVE      WBSENAM,VARIABLE 
          GOTO      MRPRN740
.
.------ format Date Record Updated ------
.
MRPRN521  UNPACK    ALLOUDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format Time Record Updated ------
.
MRPRN522  MOVE      ALLOUTIM,DIM5
          MOVE      DIM5,VARIABLE
          GOTO      MRPRN740
.
.------ WEB User ID Updated ------
.
MRPRN523  PACK      KEY10,ALLOUUID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,MRPRN100
.
          MOVE      WBSENAM,VARIABLE 
          GOTO      MRPRN740
.
.------ format Next Review Date ------
.
MRPRN524  UNPACK    ALLONDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ Reason For Review ------
.
MRPRN525  MOVE      "QE",CATEGORY
          MOVE      ALLOREAS,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ Equipment Desc ------ 
.
MRPRN526  MOVE      SP70,VARIABLE
          MOVE      ALEQDESC,VARIABLE
          GOTO      MRPRN740
.
.------ Loan Group ------
.
MRPRN527  MOVE      "QG",CATEGORY
          MOVE      ALEQLOAN,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ Equipment Type ------
.
MRPRN528  MOVE      "QT",CATEGORY
          MOVE      ALEQTYPE,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ Equipment Sub-Type ------
.
MRPRN529  MOVE      "QY",CATEGORY
          MOVE      ALEQSUBT,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ Home Location ------
.
MRPRN530  MOVE      "QH",CATEGORY
          MOVE      ALEQHOME,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ Department ------
.
MRPRN531  MOVE      "CG",CATEGORY
          MOVE      ALEQDEPT,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ Item Code ------
.
MRPRN532  MOVE      ALEQITEM,VARIABLE
          MOVE      NINE,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN750
.
.------ Supplier Code ------
.
MRPRN533  MOVE      ALEQSUPP,VARIABLE
          MOVE      FIVE,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN740
.
.------ Supplier Product Code ------
.
MRPRN534  MOVE      ALEQPROD,VARIABLE
          MOVE      TWENTY,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN740
.
.------ Serial Number ------
.
MRPRN535  MOVE      ALEQSERI,VARIABLE
          MOVE      TWENTY,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN740
.
.------ format Status ------
.
.                                                            * start  *C-178888
MRPRN536  IF        ALEQCBAL > 0
            MOVE      "0",ALEQSTAT          * some still available
          ELSE
            MATCH   "1",ALEQCONS            * is it a Consumable?
            IF      @EQUAL 
              MOVE    "1",ALEQSTAT
            ENDIF
          ENDIF
.
          MATCH     "0",ALEQSTAT
          IF        @EQUAL
            MOVE    "Available for Loan",VARIABLE
            MATCH   "1",ALEQCONS
            IF      @EQUAL
              MOVE    "Available",VARIABLE
            ENDIF
            GOTO      MRPRN740
          ENDIF
.
          MATCH     "1",ALEQSTAT
          IF        @EQUAL
            MOVE      "Currently on Loan",VARIABLE
            MATCH   "1",ALEQCONS
            IF      @EQUAL
              MOVE    "Not available",VARIABLE
            ENDIF                                            * end    *C-178888
            GOTO      MRPRN740
          ENDIF
.
          MATCH     "2",ALEQSTAT
          IF        @EQUAL
            MOVE      "Inactive/Written Off",VARIABLE
            GOTO      MRPRN740
          ENDIF
.
          MATCH     "3",ALEQSTAT
          IF        @EQUAL
            MOVE      "Needs Maintenance",VARIABLE
            GOTO      MRPRN740
          ENDIF
.
          MATCH     "4",ALEQSTAT
          IF        @EQUAL
            MOVE      "Deleted - Strike out",VARIABLE
            GOTO      MRPRN740
          ENDIF
          GOTO      MRPRN740
.
.------ format Date Of Status ------
.
MRPRN537  UNPACK    ALEQSDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format Purchase Date ------
.
MRPRN538  UNPACK    ALEQPDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ Maintenance Period (Months) ------ 
.
MRPRN539  MOVE      SP70,VARIABLE
          MOVE      ALEQMAIN,VARIABLE
          GOTO      MRPRN740
.
.------ Unit Cost ------ 
.
MRPRN540  MOVE      SP70,VARIABLE
          MOVE      ALEQUNIT,VARIABLE
          GOTO      MRPRN740
.
.------ Deposit Amount ------ 
.
MRPRN541  MOVE      SP70,VARIABLE
          MOVE      ALEQDEPO,VARIABLE
          MOVE      TEN1,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN750
.
.------ format Special funding ------
.
MRPRN542  MATCH     "0",ALEQSPEC
          IF        @EQUAL
            MOVE      "No",VARIABLE
            GOTO      MRPRN740
          ENDIF
.
          MATCH     "1",ALEQSPEC
          IF        @EQUAL
            MOVE      "Yes",VARIABLE
            GOTO      MRPRN740
          ENDIF
          GOTO        MRPRN100
.
.------ Loan Authorisation Level ------ 
.
MRPRN543  MOVE      SP70,VARIABLE
          MOVE      ALEQLOAU,VARIABLE
          GOTO      MRPRN740
.
.------ format Date Of Recondition ------
.
MRPRN544  UNPACK    ALEQRDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ Consumable Indicator ------
.
MRPRN545  MATCH     "0",ALEQCONS
          IF        @EQUAL
            MOVE      "No",VARIABLE
            GOTO      MRPRN740
          ENDIF
.
          MATCH     "1",ALEQCONS
          IF        @EQUAL
            MOVE      "Yes",VARIABLE
            GOTO      MRPRN740
          ENDIF
          GOTO        MRPRN100
.
.------ Current Balance ------
.
MRPRN546  MOVE      SP70,VARIABLE
          MOVE      ALEQCBAL,VARIABLE
          GOTO      MRPRN740
.
.------ Re-order Level ------
.
MRPRN547  MATCH     "1",ALEQCONS
          GOTO      MRPRN100 IF NOT EQUAL
.
          MOVE      SP70,VARIABLE
          MOVE      ALEQREOR,VARIABLE
          GOTO      MRPRN740
.
.------ Re-order Quantity ------
.
MRPRN548  MATCH     "1",ALEQCONS
          GOTO      MRPRN100 IF NOT EQUAL
.
          MOVE      SP70,VARIABLE
          MOVE      ALEQREQT,VARIABLE
          GOTO      MRPRN740
.
.------ Register Number ------ 
.
MRPRN549  MOVE      SP70,VARIABLE
          MOVE      ALEQREGN,VARIABLE
          GOTO      MRPRN740
.
.------ Asset Number ------ 
.
MRPRN550  MOVE      SP70,VARIABLE
          MOVE      ALEQASSN,VARIABLE
          GOTO      MRPRN740
.
.------ format Date Record Created ------
.
MRPRN551  UNPACK    ALEQCDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format Time Record Created ------
.
MRPRN552  MOVE      ALEQCTIM,DIM5
          MOVE      DIM5,VARIABLE
          GOTO      MRPRN740
.
.------ WEB User ID Created ------
.
MRPRN553  PACK      KEY10,ALEQCUID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,MRPRN100
.
          MOVE      WBSENAM,VARIABLE 
          GOTO      MRPRN740
.
.------ format Date Record Updated ------
.
MRPRN554  UNPACK    ALEQUDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format Time Record Updated ------
.
MRPRN555  MOVE      ALEQUTIM,DIM5
          MOVE      DIM5,VARIABLE
          GOTO      MRPRN740
.
.------ WEB User ID Updated ------
.
MRPRN556  PACK      KEY10,ALEQUUID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,MRPRN100
.
          MOVE      WBSENAM,VARIABLE 
          GOTO      MRPRN740
.
.------ Record Status ------
.
MRPRN557  MATCH     "0",ALEQACTV
          IF        @EQUAL
            MOVE      "Active",VARIABLE
            GOTO      MRPRN740
          ENDIF
.
          MATCH     "1",ALEQACTV
          IF        @EQUAL
            MOVE      "Inactive",VARIABLE
            GOTO      MRPRN740
          ENDIF
          GOTO        MRPRN100
.
.------ Fee To Be Paid ------ 
.
MRPRN558  MOVE      SP70,VARIABLE
          MOVE      ALEQPFEE,VARIABLE
          MOVE      TEN1,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN750
.
.------ Total ------ 
.
MRPRN559  MOVE      SP70,VARIABLE
          ASSIGN    ALEQDEPO + ALEQPFEE,TOTAL
          MOVE      TOTAL,VARIABLE
          MOVE      TEN1,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN750
.
.------ format the patients name ------
.
MRPRN560  MOVE      TWO,PACFRMT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME                      * pack the patients name
          MOVE      PACFNAME,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients surname ------
.
MRPRN561  MOVE      PSNAME,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients given name ------
.
MRPRN562  MOVE      PGNAME,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients title ------
.
MRPRN563  MOVE      PTITL,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients address line 1 ------
.
MRPRN564  MOVE      PADD1,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients address line 2 ------
.
MRPRN565  MOVE      PADD2,VARIABLE
          GOTO      MRPRN740
.
.------ format the suburb ------
.
MRPRN566  MOVE      PSUBRB,VARIABLE
          GOTO      MRPRN740
.
.------ format the post code ------
.
MRPRN567  MOVE      PPOST,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients sex description ------
.
MRPRN568  MOVE      SP8,VARIABLE
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSP00                
          MOVE      DSEXASC1,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients date of birth ------
.
MRPRN569  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients age ------
.
MRPRN570  MOVE      PSAGE,VARIABLE
          MOVE      THREE,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      MRPRN750
.
.------ format Current Date ------
.
MRPRN571  UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients name (Title Firstname Surname)------
.
MRPRN572  MOVE      ONE,PACFRMT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME                      * pack the patients name
          MOVE      PACFNAME,VARIABLE
          MOVE      VARIABLE,LINE60
          CALL      CASE0000
          MOVE      LINE60,VARIABLE
          GOTO      MRPRN740
.
.
.------ bad debt indicator ------
.
MRPRN573  PACK      DIM3,DNO,SP70
          MOVE      ZERO,FORM1
          MOVE      PBDEBT,FORM1
          LOAD      DIM3,FORM1,DYES
          MOVE      DIM3,VARIABLE
          GOTO      MRPRN740
.
.-------- format previous surname --------------
.
MRPRN574  MOVE      PPSNAME,VARIABLE
          GOTO      MRPRN740
.
.------ format the patients address line 4 ------
.
MRPRN575  MOVE      PADD4,VARIABLE
          GOTO      MRPRN740
.
.------ format patients private telephone------
.
MRPRN576  MOVE      PTELEP,VARIABLE          * Private telephone
          GOTO      MRPRN740
.
.------ format patients business telephone------
.
MRPRN577  MOVE      PTELEB,VARIABLE          * Private telephone
          GOTO      MRPRN740
.
.------ Marital Status Desc------
.         
MRPRN578  MOVE      "M ",CATEGORY
          MOVE      PMSTAT,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format the country of birth description ------
.
MRPRN579  MOVE      "C ",CATEGORY
          MOVE      PCONT,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format patient's religion description ------
.
MRPRN580  MOVE      "R ",CATEGORY              * Religion
          MOVE      PREG,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format patient type description ------
.
MRPRN581  MOVE      "T ",CATEGORY              * Type
          MOVE      PTYPE,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format patient occupation ------
.
MRPRN582  MOVE      POCCUP,VARIABLE          * Occupation
          GOTO      MRPRN740
.
.------ format medicare number ------
.
MRPRN583  MOVE      PMEDI,VARIABLE           * Medicare number
          GOTO      MRPRN740
.
.-------- format Medicare Reference Number -------------
.
MRPRN584  MOVE      PTMXMCCD,VARIABLE
          GOTO      MRPRN740
.
.-------- format Pension Number -------------
.         
MRPRN585  MOVE      PPENNO,VARIABLE
          GOTO      MRPRN740
.
.------ format pension number expiry date ------
.
MRPRN586  MATCH     SP70,PPNDTE
          GOTO      MRPRN740 IF EQUAL
          GOTO      MRPRN740 IF EOS
          REP       " 0",PPNDTE
          UNPACK    PPNDTE,XCENT,XYEAR,XMON
          PACK      VARIABLE,XMON,SLASH,XCENT,XYEAR
          GOTO      MRPRN740
.
.------ format the repatriation number ------
.
MRPRN587  MOVE      THREE,DIM1
          CALL      GCCARD00
          MOVE      PMCDCNUM,VARIABLE
.          MOVE      PURNO,VARIABLE
          GOTO      MRPRN740
.
.------ format the smoker indicator ------
.
MRPRN588  MOVE      SP70,VARIABLE
          LOAD      VARIABLE,PSMOK,DYES,DNO
          GOTO      MRPRN740
.
.----- format Microfilm Address ------
.
MRPRN589  MOVE      PMICRO,VARIABLE
          GOTO      MRPRN740
.
.------ format Death --------
.
MRPRN590  COMPARE   "1",PCEASE
          IF        @EQUAL
            MOVE      "YES",VARIABLE
          ELSE
            MOVE      "NO ",VARIABLE
          ENDIF
          GOTO      MRPRN740
.
.------ format Autopsy Indicator--------
.
MRPRN591  COMPARE   "1",PAUTOPY
          IF        @EQUAL
            MOVE      "YES",VARIABLE
          ELSE
            MOVE      "NO ",VARIABLE
          ENDIF
          GOTO      MRPRN740
.
.
.------ format Highlight Code 1 Description------
.
MRPRN592  MOVE      "H1",CATEGORY              * Highlight Code 1
          MOVE      PHIGH1,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format Highlight Code 2 Description------
.
MRPRN593  MOVE      "H2",CATEGORY              * Highlight Code 2
          MOVE      PHIGH2,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format Highlight Code 3 Description------
.
MRPRN594  MOVE      "H3",CATEGORY              * Highlight Code 3
          MOVE      PHIGH3,CODE
          CALL      GETCOD00
          MOVE      TDESC,VARIABLE
          GOTO      MRPRN740
.
.------ format Doctors Name --------
.
MRPRN595  MOVE      PLOCDOC,VARIABLE
          GOTO       MRPRN740
.
.------ format the health fund ------
.
MRPRN598  MOVE      PFUNDH,VARIABLE
          GOTO      MRPRN740
.
.------ format health fund table ------
.
MRPRN599  MOVE      PFNDTB,VARIABLE
          GOTO      MRPRN740
.
.------ format fund membership number ------
.
MRPRN600  MOVE      PFNDMM,VARIABLE
          GOTO      MRPRN740
.
MRPRN601  MOVE      ONE,READLOAN                 * Read Loan 1
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.
MRPRN602  MOVE      TWO,READLOAN                 * Read Loan 2
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.
MRPRN603  MOVE      THREE,READLOAN               * Read Loan 3
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.
MRPRN604  MOVE      FOUR,READLOAN                * Read Loan 4
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.
MRPRN605  MOVE      FIVE,READLOAN                * Read Loan 5
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.
MRPRN606  MOVE      SIX,READLOAN                 * Read Loan 6
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.
MRPRN607  MOVE      SEVEN,READLOAN               * Read Loan 7
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.
MRPRN608  MOVE      EIGHT,READLOAN               * Read Loan 8
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.
MRPRN609  MOVE      NINE,READLOAN                * Read Loan 9
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.
MRPRN610  MOVE      TEN,READLOAN                 * Read Loan 10
          CALL      LOAN0000
          MOVE      SP1,VARIABLE
          GOTO      MRPRN740
.	  
.----- format patient sex long description ------	  
.	  
MRPRN611  MOVE      PSEX,DSEXCHAR	  
          CALL      SEXDSP00	      
          MOVE      DSEXLDES,VARIABLE        * Get sex long description		
          GOTO      MRPRN740	      
.
MRPRN612  PACK      VARIABLE,PMPXPFSN,SP70   * Preferred Surname
          GOTO      MRPRN740
.
MRPRN613  PACK      VARIABLE,PMPXPFGN,SP70   * Preferred Given Name
          GOTO      MRPRN740
.
MRPRN614  PACK      VARIABLE,PSNAME,SP70     * Pref Surname instead of Actual SN
          MATCH     "1",PTCNPFSN
          IF        @EQUAL
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFSN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      MRPRN740
.
MRPRN615  PACK      VARIABLE,PGNAME,SP70     * Pref GN instead of Actual GN
          MATCH     "1",PTCNPFGN
          IF        @EQUAL
            MATCH     SP70,PMPXPFGN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFGN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      MRPRN740
.
MRPRN616  PACK      VARIABLE,PTMASNAM,SP70   * Pref Surname instead of 40char SN
          MATCH     "1",PTCNPFSN
          IF        @EQUAL
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFSN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      MRPRN740
.
MRPRN740  RESET     VARIABLE,IBDTPLEN
          LENSET    VARIABLE
          RESET     VARIABLE
          PRINT     *IBDTCOL,*+,VARIABLE;
          GOTO      MRPRN100
.
.------ formatting for Form fields and right justified variables ------
.
MRPRN750  COMPARE   ZERO,PRNTSTRT          * don't bump if printing full length
          GOTO      MRPRN760 IF EQUAL
.
          BUMP      VARIABLE,PRNTSTRT      * bump to starting position
.
.------ print the variable ------
.
MRPRN760  PRINT     *IBDTCOL,*+,VARIABLE;
          GOTO      MRPRN100
.
.------ end of medical record request form reached ------
.
MRPRN900  MOVE      FALSE,EXIT
.
.------ print new-lines until the end of page is reached ------
.
MRPRN910  PRINT     *N;
          ADD       ONE,LASTROW
.
          COMPARE   LASTROW,IBTHLENG          * test page length
          GOTO      MRPRN999 IF EQUAL
.
          GOTO      MRPRN910
.
.------ no details on file ------
.
MRPRN990  MOVE      TRUE,EXIT
.
MRPRN999  CALL      CLSPRINT                  * close the spool file
          RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40,PROFLD50:
                             PROFLD60,PROFLD70,PROFLD80
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD12  CALL      ALLN0000    * Patient Equipment Loan 
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21
          GOTO      PROFLD99
.
PROFLD21  CALL      ALMA0000    * Equipment Service Maintenance
          GOTO      PROFLD99
.
PROFLD30  CALL      LLST0000    * Equipment Loan List
          GOTO      PROFLD99
.
PROFLD40  CALL      EQPL0000    * Equipment Lists
          GOTO      PROFLD99
.
PROFLD50  BRANCH    FLDSETNO,PROFLD51,PROFLD52
          GOTO      PROFLD99
.
PROFLD51  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD52  CALL      ALLP0000    * Patient Care Team Details
          GOTO      PROFLD99
.
PROFLD60  BRANCH    FLDSETNO,PROFLD61,PROFLD62,PROFLD63,PROFLD64,PROFLD65
          GOTO      PROFLD99
.
PROFLD61  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD62  CALL      ALSC0000    * SCTT Referral Details
          GOTO      PROFLD99
.
PROFLD63  CALL      MSXF0000     * Master Extension Details
          GOTO      PROFLD99
.
PROFLD64  CALL      PMIX0000     * Master Extension Details Two
          GOTO      PROFLD99
.
PROFLD65  CALL      TABL0000     * General Tags
          GOTO      PROFLD99
.
PROFLD70  BRANCH    FLDSETNO,PROFLD71,PROFLD72
          GOTO      PROFLD99
.
PROFLD71  CALL      ALEQ0000     * Equipment Table
          GOTO      PROFLD99
.
PROFLD72  CALL      STAG0000     * General tags for equipment search
          GOTO      PROFLD99
.
PROFLD80  BRANCH    FLDSETNO,PROFLD81,PROFLD82
          GOTO      PROFLD99
.
PROFLD81  CALL      MASF0000     * Patient Master Template Enquiries
          GOTO      PROFLD99
.
PROFLD82  CALL      ELON0000     * Equipment Loan based comments
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------------------------
. Equipment List   
.------------------------------------------------------------------------------
LLST0000  BRANCH    FLDITMNO,LLST0100,LLST0200,LLST0300,LLST0400,LLST0500:
                             LLST0600
          GOTO      LLST9999
.
LLST0100  CALL      EQPTB000      * Equipment Loans List
          GOTO      LLST9999

LLST0200  MOVE      "QH",CATEGORY    * Home Location
          MOVE      HOMELOCA,CODE
          CALL      CODITM00
          GOTO      LLST9999

LLST0300  MOVE      "QT",CATEGORY    * Equipment Type
          MOVE      EQIPTYPE,CODE
          CALL      CODITM00
          GOTO      LLST9999
.
LLST0400  WRITE     HTMLFILE;EQIPSTAT;  * Equipment Status CGI
          GOTO      LLST9999
.
LLST0500  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,LLST0510
.
          PACK      KEY10,FILTHCPC,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,LLST9999
.
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
          WRITE     HTMLFILE;DOCFNAME;
          GOTO      LLST9999
.
LLST0510  WRITE     HTMLFILE;FILTHCPC; 
          GOTO      LLST9999
.
LLST0600  WRITE     HTMLFILE;FILTSTAT; 
          GOTO      LLST9999
.
LLST9999  RETURN
.------------------------------------------------------------
. Table of Patient Equipment Loans 
.------------------------------------------------------------
EQPTB000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE 
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MATCH     SP70,HOMELOCA
          GOTO      EQPTB999 IF EQUAL
.
.         MATCH     SP70,EQIPTYPE
.         GOTO      EQPTB999 IF EQUAL
.
          PACK      KEY25,FILTSTAT,SP70
          CALL      RSALLON5
EQPTB100  CALL      RKALLON5
          BRANCH    OVRCD,EQPTB999
.
          MATCH     SP70,FILTSTAT
          IF        !@EQUAL
            MATCH     FILTSTAT,ALLOSTAT
            GOTO      EQPTB999 IF NOT EQUAL * match status
          ENDIF
.
          MATCH     SP70,FILTHCPC
          IF        !@EQUAL
            MATCH     FILTHCPC,ALLOTHER
            GOTO      EQPTB100 IF NOT EQUAL
          ENDIF
.
          MOVE      ALLOURNO,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,EQPTB100
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          PACK      KEY10,ALLOEQUI,SP70
          CALL      RDALEQU1
          BRANCH    OVRCD,EQPTB100
.
          MATCH     HOMELOCA,ALEQHOME     * Home Location
          GOTO      EQPTB100 IF NOT EQUAL
.
          MATCH     SP70,EQIPTYPE
          IF        !@EQUAL & !@EOS
            MATCH     EQIPTYPE,ALEQTYPE     * Equipment Type
            GOTO      EQPTB100 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,EQIPSTAT
          IF        !@EQUAL
            MATCH     EQIPSTAT,ALEQSTAT     * Equipment Status Status  
            GOTO      EQPTB100 IF NOT EQUAL
          ENDIF
..
          UNPACK    PBDATE,CDAY,CMON,CYEAR,CCENT
          CALL      IBACLOCK
          IF        PCEASE=1
            MATCH     SP70,PDECDTE
            IF        @EQUAL
              PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
            ELSE
              MOVE      PDECDTE,AGEDATE            * Date of Death
            ENDIF
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
.
          CALL      PATLN000
.
          REP       " +",ALLOURNO
          REP       " +",ALLOLOAN
.
          CLEAR     HTMLLINK
          APPEND    "javascript: PatientLink('",HTMLLINK
          APPEND    LINKPRG,HTMLLINK
          APPEND    ".pbl?reportno=",HTMLLINK
          APPEND    LINKREP,HTMLLINK
          APPEND    "&template=",HTMLLINK
          APPEND    LINKTEM,HTMLLINK
          APPEND    "&urnumber=",HTMLLINK
          APPEND    ALLOURNO,HTMLLINK
          APPEND    "&loannumb=",HTMLLINK
          APPEND    ALLOLOAN,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          REP       "+ ",ALLOURNO
          REP       "+ ",ALLOLOAN
.
          MOVE      "QL",CATEGORY        * Loan Type
          MOVE      ALLOTYPE,CODE
          CALL      GETCOD00
          MOVE      TDESC,LOANTYPE
.
          PACK      KEY10,ALLOTHER,SP70
          PACK      DOCFNAME,ALLOTHER,SP70
          CALL      RDPMHCP1
          IF        OVRCD = 0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ENDIF
          CALL      PATFLD00
.
          MOVE      "&nbsp;",STATUS                                   *I-273880
          MATCH     "1",ALEQCONS            * is this a "Consumable"? *I-273880
          GOTO      EQPTB200 IF EQUAL                                 *I-273880
.
          MOVE      ZERO,F1
          MOVE      ALLOSTAT,F1
          BRANCH    F1,EQPTB110,EQPTB120 
          MOVE      "Loaned",STATUS
          GOTO      EQPTB200
EQPTB110  MOVE      "Returned",STATUS
          GOTO      EQPTB200
EQPTB120  MOVE      "Cancelled",STATUS
.
EQPTB200  UNPACK    ALLOLDAT,CCENT,CYEAR,CMON,CDAY   * Loan Date        
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      LOANDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      ZERO,F2    
          MOVE      SP3,MTHNAM3 
. 
          UNPACK    ALLOEDAT,CCENT,CYEAR,CMON,CDAY   * Expected Return Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      EXPRETRN,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MATCH     MTHNAM3,SP3
          IF        !@EQUAL
            PACK      EXPRETRN,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            MOVE      "&nbsp;",DIM6
....        MOVE      "&nbsp;",STATUS                                 *D-273880
            PACK      EXPRETRN,DIM6,SP8
....        MOVE      CURRDATE,ALLOEDAT
            MOVE      "99991231",ALLOEDAT
          ENDIF
.
          MATCH     CURRDATE,ALLOEDAT    * Overdue?
          IF        @LESS
            MATCH     "0",ALLOSTAT       * Still on loan?
            IF        @EQUAL
              GOTO      EQPTB300 
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",FORMATNM,"#",":     * Formatted Name
                             "#"",ALEQDESC,"#",":     * Equipment Desc
                             "#"",LOANDATE,"#",":     * Loan Date
                             "#"",EXPRETRN,"#",":     * Expected Return Date
                             "#"",LOANTYPE,"#",":     * Type of Loan 
                             "#"",DOCFNAME,"#",":     * Therapist 
                             "#"",STATUS,"#",":       * Status 
                             "#"",KEY3,"#",":         * Patient Folder 
                             "#"",ALLOLDAT,"#",":     * Loan Date (ccyymmdd)
                             "#"",ALLOEDAT,"#");"     * Exp Return Date (ccyymmd
.
          GOTO      EQPTB100 
.
EQPTB300  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                          "#"",FONTR,FORMATNM,FONTREND,"#",":  * Formatted Name
                          "#"",FONTR,ALEQDESC,FONTREND,"#",":  * Equipment Desc
                          "#"",FONTR,LOANDATE,FONTREND,"#",":  * Loan Date
                          "#"",FONTR,EXPRETRN,FONTREND,"#",":  * Exp Return Date
                          "#"",FONTR,LOANTYPE,FONTREND,"#",":  * Type of Loan 
                          "#"",FONTR,DOCFNAME,FONTREND,"#",":  * Therapist 
                          "#"",FONTR,STATUS,FONTREND,"#",":    * Status 
                          "#"",KEY3,"#",":                     * Patient Folder 
                          "#"",ALLOLDAT,"#",":        * Loan Date (ccyymmdd)
                          "#"",ALLOEDAT,"#");"        * Exp Return Date (ccyymmd
.
          GOTO      EQPTB100 
.
EQPTB999  RETURN
.------------------------------------------------------------------------------
. Equipment Service   
.------------------------------------------------------------------------------
ALMA0000  BRANCH    FLDITMNO,ALMA0100,ALMA0200,ALMA0300,ALMA0400,ALMA0500:
                             ALMA0600,ALMA0700,ALMA0800,ALMA0900,ALMA1000:
                             ALMA1100,ALMA1200,ALMA1300,ALMA1400,ALMA1500:
                             ALMA1600,ALMA1700,ALMA1800
          GOTO      ALMA9999
.
ALMA0100  WRITE     HTMLFILE;ALMAEQUI;    * Equipment Code
          GOTO      ALMA9999
.
ALMA0200  WRITE     HTMLFILE;ALMAMNUM;    * Maintenance Number
          GOTO      ALMA9999
.
ALMA0300  UNPACK    ALMAMDAT,CCENT,CYEAR,CMON,CDAY   * Maintenance Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALMA9999
.
ALMA0400  WRITE     HTMLFILE;ALMAMTIM;    * Maintenance Time
          GOTO      ALMA9999
.
ALMA0500  MOVE      ALMACOND,CODE         * Condition After Main (Cat QM)
          MOVE      "QM",CATEGORY
          CALL      CODITM00
          GOTO      ALMA9999
.
ALMA0600  MATCH     SP70,ALMAHDAT
          GOTO      ALMA9999 IF EQUAL
          UNPACK    ALMAHDAT,CCENT,CYEAR,CMON,CDAY   * Hazard Notice Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALMA9999
.
ALMA0700  MATCH     SP70,ALMAHNUM         * Hazard notice reference no.
          GOTO      ALMA9999 IF EQUAL
          WRITE     HTMLFILE;ALMAHNUM;    
          GOTO      ALMA9999
.
ALMA0800  WRITE     HTMLFILE;ALEQDESC;    * Equipment Desc
          GOTO      ALMA9999

ALMA0900  MOVE      "QG",CATEGORY         * Equipment Loan Group
          MOVE      ALEQLOAN,CODE
          CALL      CODITM00
          GOTO      ALMA9999
.
ALMA1000  WRITE     HTMLFILE;ALEQMAIN;    * Equipment Maintenance Period
          GOTO      ALMA9999
.
ALMA1100  UNPACK    DIM127,DIM1,KEY1,DIM127    * Supplier Code/Desc    
          MOVE      KEY1,F1 
          BRANCH    F1,ALMA1110
.
          PACK      KEY5,ALEQSUPP,SP70          
          CALL      RDAPMA1
          BRANCH    OVRCD,ALMA9999

          CLEAR     KEY60
          MOVE      APMACN1,KEY60
          ENDSET    KEY60
          APPEND    APMACN2,KEY60
          ENDSET    KEY60
          WRITE     HTMLFILE;KEY60;
          GOTO      ALMA9999
.
ALMA1110  WRITE     HTMLFILE;ALEQSUPP;
          GOTO      ALMA9999
.
ALMA1200  CALL      MAITB000     * Table of Equipment Code Services
          GOTO      ALMA9999
.
ALMA1300  CALL      NEXT0000
          GOTO      ALMA9999
.
ALMA1400  CALL      PREV0000
          GOTO      ALMA9999
.
ALMA1500  WRITE     HTMLFILE;ALEQEQUI;    * Equipment Code
          GOTO      ALMA9999
.
ALMA1600  WRITE     HTMLFILE;ALEQDEPT;    * Equipment Department Code
          GOTO      ALMA9999
.
ALMA1700  UNPACK    ALEQPDAT,CCENT,CYEAR,CMON,CDAY   * Purchase Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALMA9999
.
ALMA1800  WRITE     HTMLFILE;ALEQCBAL;    * Equipment Current Balance *I-178888
          GOTO      ALMA9999
.
ALMA9999  RETURN
.------------------------------------------------------------------------------
. Patient Equipment Loans     
.------------------------------------------------------------------------------
ALLN0000  BRANCH    FLDITMNO,ALLN0100,ALLN0200,ALLN0300,ALLN0400,ALLN0500:
                             ALLN0600,ALLN0700,ALLN0800,ALLN0900,ALLN1000:
                             ALLN1100,ALLN1200,ALLN1300,ALLN1400,ALLN1500:
                             ALLN1600,ALLN1700,ALLN1800,ALLN1900,ALLN2000:
                             ALLN2100,ALLN2200,ALLN2300,ALLN2400,ALLN2500:
                             ALLN2600,ALLN2700,ALLN2800,ALLN2900,ALLN3000:
                             ALLN3100,ALLN3200,ALLN3300,ALLN3400,ALLN3500
          GOTO      ALLN9999
.
ALLN0100  WRITE     HTMLFILE;ALLOURNO;    * U/R Number
          GOTO      ALLN9999
.
ALLN0200  WRITE     HTMLFILE;ALLOLOAN;    * Loan Number
          GOTO      ALLN9999
.
ALLN0300  MATCH     SP70,ALLOLGRP 
          GOTO      ALLN9999 IF EQUAL
          WRITE     HTMLFILE;ALLOLGRP;    * Loan Group (Category QC)
          GOTO      ALLN9999
.
ALLN0400  UNPACK    DIM127,DIM1,KEY1,DIM127     
          MOVE      KEY1,F1 
          BRANCH    F1,ALLN0410
.
          WRITE     HTMLFILE;ALEQDESC;    * Equipment Desc Description
          GOTO      ALLN9999
.
ALLN0410  WRITE     HTMLFILE;ALLOEQUI;    * Code
          GOTO      ALLN9999
.
ALLN0500  MATCH     SP70,ALLOLDAT         * Loan Date
          GOTO      ALLN9999 IF EQUAL
.
          UNPACK    ALLOLDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALLN9999
.
ALLN0600  MATCH     SP70,ALLOLTIM         * Loan Time
          GOTO      ALLN9999 IF EQUAL
          WRITE     HTMLFILE;ALLOLTIM;
          GOTO      ALLN9999
.
ALLN0700  UNPACK    DIM127,DIM1,KEY1,DIM127  * Therapist HCP Code      *C-87666
          MATCH     SP70,ALLOTHER         
          GOTO      ALLN9999 IF EQUAL
.
          MOVE      KEY1,F1
          BRANCH    F1,ALLN0710 
.
          PACK      KEY10,ALLOTHER,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,ALLN9999
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
.
          WRITE     HTMLFILE;DOCFNAME
          GOTO      ALLN9999
.
ALLN0710  WRITE     HTMLFILE;ALLOTHER;
          GOTO      ALLN9999
.
ALLN0800  MOVE      ALLOTYPE,CODE         * Type of Loan (Cat QL)
          MOVE      "QL",CATEGORY
          CALL      CODITM00
          GOTO      ALLN9999
.
ALLN0900  MATCH     SP70,ALLOSTAT         * Status         
          GOTO      ALLN9999 IF EQUAL
          WRITE     HTMLFILE;ALLOSTAT;
          GOTO      ALLN9999
.
ALLN1000  MATCH     SP70,ALLOEDAT         * Expected Return Date
          GOTO      ALLN9999 IF EQUAL
.
          UNPACK    ALLOEDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALLN9999
.
ALLN1100  MATCH     SP70,ALLOVDAT         * Review Date
          GOTO      ALLN9999 IF EQUAL
.
          UNPACK    ALLOVDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALLN9999
.
ALLN1200  WRITE     HTMLFILE;ALLOQTYI;    * Quantity Issued
          GOTO      ALLN9999
.
ALLN1300  MATCH     SP70,ALLOIUID         * Issuing web userid
          GOTO      ALLN9999 IF EQUAL
          WRITE     HTMLFILE;ALLOIUID;
          GOTO      ALLN9999
.
ALLN1400  MATCH     SP70,ALLORDAT         * Return Date
          GOTO      ALLN9999 IF EQUAL
          UNPACK    ALLORDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALLN9999
.
ALLN1500  MATCH     SP70,ALLORTIM         * Return Time
          GOTO      ALLN9999 IF EQUAL
          WRITE     HTMLFILE;ALLORTIM;
          GOTO      ALLN9999
.
ALLN1600  MATCH     SP70,ALLORUID         * Returning Web Userid
          GOTO      ALLN9999 IF EQUAL
          WRITE     HTMLFILE;ALLORUID;
          GOTO      ALLN9999
.
ALLN1700  MOVE      ALLOCOND,CODE         * Condition of Return (Cat QO)
          MOVE      "QO",CATEGORY
          CALL      CODITM00
          GOTO      ALLN9999
.
ALLN1800  MATCH     SP70,ALLOCDAT         * Date Record Created
          GOTO      ALLN9999 IF EQUAL
          UNPACK    ALLOCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALLN9999
.
ALLN1900  MATCH     SP70,ALLOCTIM         * Time Record Created
          GOTO      ALLN9999 IF EQUAL
          WRITE     HTMLFILE;ALLOCTIM;
          GOTO      ALLN9999
.
ALLN2000  MATCH     SP70,ALLOCUID         * WEB Userid Created
          GOTO      ALLN9999 IF EQUAL
.
          PACK      KEY10,ALLOCUID,SP70
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALLOCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      ALLN9999
.
ALLN2100  MATCH     SP70,ALLOUDAT         * Date Record Updated
          GOTO      ALLN9999 IF EQUAL
          UNPACK    ALLOUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALLN9999
.
ALLN2200  MATCH     SP70,ALLOUTIM         * Time Record Updated
          GOTO      ALLN9999 IF EQUAL
          WRITE     HTMLFILE;ALLOUTIM;
          GOTO      ALLN9999
.
ALLN2300  MATCH     SP70,ALLOUUID         * Web Userid Updated
          GOTO      ALLN9999 IF EQUAL
          WRITE     HTMLFILE;ALLOUUID;
          GOTO      ALLN9999
.
ALLN2400  MOVE      ZERO,HIGHOVER
          CALL      ALLTB000              * Table of Patient Equipment Loans 
          GOTO      ALLN9999
.
ALLN2500  MOVE      "CG",CATEGORY         * Department
          MOVE      ALEQDEPT,CODE
          CALL      CODITM00
          GOTO      ALLN9999
.
ALLN2600  UNPACK    DIM127,DIM1,KEY1,DIM127    * Consumable Indicator  
          MOVE      KEY1,F1 
          BRANCH    F1,ALLN2610
.
          MATCH     "1",ALEQCONS
          IF        @EQUAL
            WRITE     HTMLFILE;"Yes";
          ELSE
            WRITE     HTMLFILE;"No";
          ENDIF
          GOTO      ALLN9999
.
ALLN2610  WRITE     HTMLFILE;ALEQCONS;
          GOTO      ALLN9999
.
ALLN2700  MATCH     SP70,ALLONDAT      * Next Review Date
          GOTO      ALLN9999 IF EQUAL 
.
          UNPACK    ALLONDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALLN9999
.
ALLN2800  MOVE      ALLOREAS,CODE         * Reason For Review (Cat QE)  
          MOVE      "QE",CATEGORY
          CALL      CODITM00
          GOTO      ALLN9999
.
ALLN2900  MOVE      SP70,CODE       * Reason For Review Display only(Cat QE)  
          MOVE      "QE",CATEGORY
          CALL      CODITM00
          GOTO      ALLN9999
.
ALLN3000  WRITE     HTMLFILE;LOANNUMB; * Loan number if found will popup screen
          GOTO      ALLN9999
.
ALLN3100  PACK      KEY25,ALLOURNO,ALLOLOAN,SP70  * "Notes" Red-Button?
          CALL      RSALEDT1
          CALL      RKALEDT1
          BRANCH    OVRCD,ALLN9999
.
          MATCH     ALLOURNO,ALEDURNO
          GOTO      ALLN9999 IF NOT EQUAL
          MATCH     ALLOLOAN,ALEDLOAN
          GOTO      ALLN9999 IF NOT EQUAL
          MATCH     "001",ALEDTYPE
          GOTO      ALLN9999 IF NOT EQUAL
.
          WRITE     HTMLFILE;ONE;           * Notes exist
          GOTO      ALLN9999
.
ALLN3200  WRITE     HTMLFILE;SUPRFLAG;      * Supervisor flag for Loan Notes
          GOTO      ALLN9999
.
ALLN3300  MOVE      ONE,HIGHOVER
          CALL      ALLTB000              * Table of Patient Equipment Loans
          GOTO      ALLN9999
.
ALLN3400  CALL      ELST0000              * Table of Equipment
          GOTO      ALLN9999
.
ALLN3500  CALL      ARFD0000                * Department Filter
          GOTO      EQPL9999
.
ALLN9999  RETURN
.------------------------------------------------------------
. Table of Patient Equipment Loans 
.------------------------------------------------------------
ALLTB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          PACK      KEY16,URNUMBER,SP70
          CALL      RSALLON1
ALLTB100  CALL      RKALLON1
          BRANCH    OVRCD,ALLTB999
.
          MATCH     URNUMBER,ALLOURNO
          GOTO      ALLN9999 IF NOT EQUAL
.
          PACK      KEY10,ALLOEQUI,SP70
          CALL      RDALEQU1
          BRANCH    OVRCD,ALLTB100
.
          PACK      PACKLINK,URNUMBER,SQUOTE,COMMA,SQUOTE,ALLOLOAN
.
          CLEAR     HTMLLINK
          APPEND    "javascript:ShowDetail('",HTMLLINK
          APPEND    PACKLINK,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      "QH",CATEGORY        * Home Location
          MOVE      ALEQHOME,CODE
          CALL      GETCOD00
.
          MOVE      "&nbsp;",STATUS
          MATCH     "1",ALEQCONS            * is this a "Consumable"?
          GOTO      ALLTB200 IF EQUAL
.
          MOVE      ZERO,F1
          MOVE      ALLOSTAT,F1
          BRANCH    F1,ALLTB110,ALLTB120 
          MOVE      "Loaned",STATUS
          GOTO      ALLTB200
ALLTB110  MOVE      "Returned",STATUS
          GOTO      ALLTB200
ALLTB120  MOVE      "Cancelled",STATUS
.
ALLTB200  UNPACK    ALLOLDAT,CCENT,CYEAR,CMON,CDAY   * Loan Date        
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      LOANDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      ZERO,F2    
          MOVE      SP3,MTHNAM3  
.
          UNPACK    ALLOEDAT,CCENT,CYEAR,CMON,CDAY   * Expected Return Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC 
.
          MATCH     MTHNAM3,SP3
          IF        !@EQUAL
            PACK      EXPRETRN,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            MOVE      "&nbsp;",DIM6
            PACK      EXPRETRN,DIM6,SP8
            MOVE      CURRDATE,ALLOEDAT
          ENDIF
.
          MOVE      ZERO,F2    
          MOVE      SP3,MTHNAM3  
.
          UNPACK    ALLORDAT,CCENT,CYEAR,CMON,CDAY   * Return Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC 
.
          MATCH     MTHNAM3,SP3
          IF        !@EQUAL
            PACK      ACTRETRN,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            MOVE      "&nbsp;",DIM6
            PACK      ACTRETRN,DIM6,SP8
          ENDIF
.
          BRANCH    HIGHOVER,ALLTB250
.
          MATCH     CURRDATE,ALLOEDAT    * Overdue?
          IF        @LESS
            MATCH     "0",ALLOSTAT       * Still on loan?
            IF        @EQUAL
              GOTO      ALLTB300 
            ENDIF
          ENDIF
.
ALLTB250  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":                 * 0
                             "#"",ALLOEQUI,"#",":     * Equipment Code     * 1
                             "#"",ALEQDESC,"#",":     * Equipment Desc     * 2
                             "#"",TDESC,"#",":        * Home Location      * 3
                             "#"",LOANDATE,"#",":     * Loan Date          * 4
                             "#"",EXPRETRN,"#",":     * Expected Return Date
                             "#"",STATUS,"#",":       * Status             * 6
                             "#"",ACTRETRN,"#",":     * Actual Return Date * 7
                             "#"",ALLOLDAT,ALLOLTIM,"#");"     * Loan Date / Time   * 8
.
          GOTO      ALLTB100 
.
ALLTB300  MATCH     SP70,TDESC
          IF        @EQUAL
            PACK      KEY60,SP70
          ELSE 
            PACK      KEY60,FONTR,TDESC,FONTREND
          ENDIF
          
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                           "#"",FONTR,ALLOEQUI,FONTREND,"#",": * Equip Code
                           "#"",FONTR,ALEQDESC,FONTREND,"#",": * Equip Desc
                           "#"",KEY60,"#",":                   * Home Location
                           "#"",FONTR,LOANDATE,FONTREND,"#",": * Loan Date
                           "#"",FONTR,EXPRETRN,FONTREND,"#",": * Exp Return Date
                           "#"",FONTR,STATUS,FONTREND,"#",":   * Status 
                           "#"",FONTR,ACTRETRN,FONTREND,"#",": * Act Return Date
                           "#"",FONTR,ALLOLDAT,ALLOLTIM,FONTREND,"#");" * Loan Date / Time   * 8
.
          GOTO      ALLTB100 
.
ALLTB999  RETURN
.
.--------------------------------------------------------------------------
. Equipment Loan Notes                                                *I-192466
.--------------------------------------------------------------------------
ELON0000  BRANCH    FLDITMNO,ELON0100,ELON0200,ELON0300,ELON0400,ELON0500:
                             ELON0600,ELON0700,ELON0800,ELON0900,ELON1000:
                             ELON1100,ELON1200
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      ELON9999
.
ELON0100  MATCH     SP70,ALEDTYPE
          GOTO      ELON9999 IF EQUAL
          WRITE     HTMLFILE;ALEDTYPE;
          GOTO      ELON9999
.
ELON0200  MATCH     SP70,ALEDNOTE
          GOTO      ELON9999 IF EQUAL
          WRITE     HTMLFILE;ALEDNOTE;
          GOTO      ELON9999
.
ELON0300  MATCH     SP70,ALEDDATE
          GOTO      ELON9999 IF EQUAL
          UNPACK    ALEDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,F2
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DSPLDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          WRITE     HTMLFILE;DSPLDATE;
          GOTO      ELON9999
.
ELON0400  MATCH     SP70,ALEDTIME
          GOTO      ELON9999 IF EQUAL
          WRITE     HTMLFILE;ALEDTIME;
          GOTO      ELON9999
.
ELON0500  MATCH     SP70,ALEDUSER
          GOTO      ELON9999 IF EQUAL
          WRITE     HTMLFILE;ALEDUSER;
          GOTO      ELON9999
.
ELON0600  MATCH     SP70,ALEDOCCG
          GOTO      ELON9999 IF EQUAL
          WRITE     HTMLFILE;ALEDOCCG;
          GOTO      ELON9999
.
ELON0700  MATCH     SP70,ALEDDELT
          GOTO      ELON9999 IF EQUAL
          WRITE     HTMLFILE;ALEDDELT;
          GOTO      ELON9999
.
ELON0800  MATCH     SP70,ALETCMNT
          GOTO      ELON9999 IF EQUAL
          WRITE     HTMLFILE;ALETCMNT;
          GOTO      ELON9999
.
ELON0900  PACK      KEY28,ALEDURNO,ALEDLOAN,ALEDTYPE,ALEDNOTE,SP70
          CALL      RSALETX1
ELON0910  CALL      RKALETX1
          BRANCH    OVRCD,ELON9999
.
          MATCH     ALEDURNO,ALETURNO
          GOTO      ELON9999 IF NOT EQUAL
.
          MATCH     ALEDLOAN,ALETLOAN
          GOTO      ELON9999 IF NOT EQUAL
.
          MATCH     ALEDTYPE,ALETTYPE
          GOTO      ELON0910 IF NOT EQUAL
.
          MATCH     ALEDNOTE,ALETNOTE
          GOTO      ELON0910 IF NOT EQUAL
.
          STRIP     ALETCMNT
          MOVELPTR  ALETCMNT,LENGTH
.
          SFORMAT   VAR,LENGTH
          MOVE      ALETCMNT,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
          GOTO      ELON0910
.
ELON1000  MOVE      ZERO,NOTEORDR
          CALL      ELONTB00
          GOTO      ELON9999
.
ELON1100  MOVE      ONE,NOTEORDR
          CALL      ELONTB00
          GOTO      ELON9999
.
ELON1200  WRITE     HTMLFILE;LOANNUMB;
          GOTO      ELON9999
.
.
ELON9999  RETURN
.
.------------------------------------------------------------
. Display Table of Encounter Notes                                    *I-192466
.---------------------------------------------------------------------
ELONTB00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          MOVE      SP70,NOTEFILT
          UNPACK    DIM127,D1,NOTEFILT,DIM127
.
          CALL      NOTHD000      * Output Table Heading
.
          MOVE      URNUMBER,ALEDURNO
          MOVE      LOANNUMB,ALEDLOAN
.
          IF        NOTEORDR=0
            PACK      KEY25,URNUMBER,LOANNUMB,NOTEFILT,Z70
          ELSE
            PACK      KEY25,URNUMBER,LOANNUMB,NOTEFILT,SP70
          ENDIF
.
          CALL      RSALEDT1
ELONTB10  IF        NOTEORDR=0
            CALL      RPALEDT1
          ELSE
            CALL      RKALEDT1
          ENDIF
          BRANCH    OVRCD,ELONTB99
.
          MATCH     URNUMBER,ALEDURNO
          GOTO      ELONTB99 IF NOT EQUAL
.
          MATCH     LOANNUMB,ALEDLOAN
          GOTO      ELONTB99 IF NOT EQUAL
.
          MATCH     NOTEFILT,ALEDTYPE
          GOTO      ELONTB99 IF NOT EQUAL
.
          MATCH     ALEDDATE,SP70
          IF        @EQUAL
            MOVE     "&nbsp",DSPLDATE
            GOTO     ELONTB40
          ENDIF
.
          UNPACK    ALEDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DSPLDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MATCH     "1",ALEDDELT
          IF        @EQUAL
            MOVE       "<strike>",STRIKETG
            MOVE       "</strike>",STRENDTG
          ELSE
            MOVE       "",STRIKETG
            MOVE       "",STRENDTG
          ENDIF
.
          REP       " +",URNUMBER
          REP       " +",LOANNUMB
          REP       " +",ALEDTYPE
          REP       " +",ALEDNOTE
.
ELONTB40  WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&loannumb=",LOANNUMB:
                             "&notetype=",ALEDTYPE:
                             "&notenumb=",ALEDNOTE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             STRIKETG,DSPLDATE,SP1,ALEDTIME,"</td>":
                             "<td>",STRIKETG,ALEDUSER," - ",ALEDOCCG,"&nbsp":
                             STRENDTG,"</td>":
                             "<td>",STRIKETG;
.
          REP       "+ ",URNUMBER
          REP       "+ ",LOANNUMB
          REP       "+ ",ALEDTYPE
          REP       "+ ",ALEDNOTE
.
          MOVE      ZERO,CTR
          CALL      LONOTE00
          WRITE     HTMLFILE;"&nbsp",STRENDTG,"</td>":
                             "<td>",ALEDDUSE," - ",ALEDDOCC,"&nbsp":
                             "</td>":
                             "</tr>"
          GOTO      ELONTB10
.
ELONTB99  RETURN
.
.------------------------------------------------------------
. Loan Note Heading (TABLE HEADING)                                   *I-192466
.------------------------------------------------------------
NOTHD000  WRITE     HTMLFILE;"<tr>":
                              "<td class=HeadingCell width=20%>Date</td>":
                              "<td class=HeadingCell width=20%>Input By</td>":
                              "<td class=HeadingCell width=40%>":
                              "Notes</td>":
                              "<td class=HeadingCell width=20%>Delete By</td>":
                             "</tr>"
.
NOTHD999  RETURN
.
.--------------------------------------------------------------------
. Encounter note display                                              *I-192466
.--------------------------------------------------------------------
LONOTE00  PACK      KEY28,ALEDURNO,ALEDLOAN,ALEDTYPE,ALEDNOTE,SP70
          CALL      RSALETX1
LONOTE10  CALL      RKALETX1
          BRANCH    OVRCD,LONOTE99
.
          MATCH     ALEDURNO,ALETURNO
          GOTO      LONOTE99 IF NOT EQUAL
.
          MATCH     ALEDLOAN,ALETLOAN
          GOTO      LONOTE99 IF NOT EQUAL
.
          MATCH     ALEDTYPE,ALETTYPE
          GOTO      LONOTE99 IF NOT EQUAL
.
          MATCH     ALEDNOTE,ALETNOTE
          GOTO      LONOTE99 IF NOT EQUAL
.
          COMPARE   TWO,CTR
          GOTO      LONOTE99 IF EQUAL
          ADD       ONE,CTR
          WRITE     HTMLFILE;ALETCMNT,"<br>";
          GOTO      LONOTE10
.
LONOTE99  RETURN
.
.------------------------------------------------------------
. Table Equipment Code Services 
.------------------------------------------------------------
MAITB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      MAIHD000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY26,ALMAI001,Z70
          CALL      RSALMAI2
MAITB100  CALL      RPALMAI2
          BRANCH    OVRCD,MAITB900
.
          MATCH     ALMAI001,ALMAEQUI           * Equipment Code
          GOTO      MAITB900 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      MAITB100
          ENDIF
.
          CALL      MAIRW000      * Display Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      MAITB100 IF NOT EQUAL
          GOTO      MAITB999
.
MAITB900  CALL      NOMORE00      * Display No More Records Message
.
MAITB999  RETURN
.------------------------------------------------------------
. Equipment Service (TABLE HEADING)
.------------------------------------------------------------
MAIHD000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=10%>":
                               "Maintenance Date</td>":
                               "<td class=HeadingCell width=40%>":    *I-178888
                               "Comment</td>":                        *I-178888
                               "<td class=HeadingCell width=50%>":
                               "Condition After Maintenance</td>":
                             "</tr>"
.
MAIHD999  RETURN
.------------------------------------------------------------------
. Equipment Service (TABLE ROW)
.------------------------------------------------------------------
MAIRW000  UNPACK    ALMAMDAT,CCENT,CYEAR,CMON,CDAY  * Service Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
.                                        * for maintenance records created from
.                                        * Loan Returns needing repair I-178888
          MATCH     SP3,ALMACOND                                      *I-178888
          IF        @EQUAL
            MOVE      "Loan Return - needs maintenance",DIM35
          ELSE
            MOVE      "&nbsp;",DIM35
          ENDIF                                                 * end *I-178888
.
          MOVE      "QM",CATEGORY        * Condition After Maintenance
          MOVE      ALMACOND,CODE
          CALL      GETCOD00
.
          MATCH     SP20,ALMACOND                                     *I-178888
          IF        @EQUAL
            MOVE      "&nbsp;",TDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&almai001=",ALMAEQUI:
                             "&almai002=",ALMAMNUM,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             KEY11,"</td>":
                             "<td>",DIM35,"</td>":                    *I-178888
                             "<td>",TDESC,"</td>":
                             "</tr>"
.
MAIRW999  RETURN
.-----------------------------------------------------------
. Link to Next List Page
.-----------------------------------------------------------
NEXT0000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",ALMAI001
.
          WRITE     HTMLFILE;"allweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&almai001=",ALMAI001;
.
          REP       "+ ",ALMAI001
.
NEXT9999  RETURN
.-----------------------------------------------------------
. Link to Prev List Page
.-----------------------------------------------------------
PREV0000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",ALMAI001
.
          WRITE     HTMLFILE;"allweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&almai001=",ALMAI001;
.
          REP       "+ ",ALMAI001
.
PREV9999  RETURN
.
.------------------------------------------------------------
. Equipment lists
.------------------------------------------------------------
EQPLST00  MATCH     SP70,FILTDEPT
          IF        @EQUAL
            MOVE      WBSEDALL,FILTDEPT
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Equipment Status List",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EQPLST99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EQPLST99
.
EQPLST99  RETURN
.
.------------------------------------------------------------------------------
. Equipment List
.------------------------------------------------------------------------------
EQPL0000  BRANCH    FLDITMNO,EQPL0100,EQPL0200,EQPL0300
          GOTO      EQPL9999
.
EQPL0100  CALL      ETAB0000   
          GOTO      EQPL9999

EQPL0200  CALL      ARFD0000                * Department Filter
          GOTO      EQPL9999
.
EQPL0300  WRITE     HTMLFILE;FILTSTAT;      * Status Filter  
          GOTO      EQPL9999
.
EQPL9999  RETURN
.
.-------------------------------------------------------------------
.     writing here for allied health department drop down
.-------------------------------------------------------------------
ARFD0000  PACK      PTCDKEY2,ANSC,ANSG,SP70
          CALL      RDSCODE2                      * positional read
ARFD0100  CALL      RDKCODE2                      * read a record
          BRANCH    OVRCD,ARFD9999
.
          MATCH     "CG",TCODE
          GOTO      ARFD9999 IF NOT EQUAL
.
          MATCH     ANSI,TCINDC3                  * Skip IP departments
          GOTO      ARFD0100 IF EQUAL
.
          PACK      KEY3,ACODE,SP70
          CALL      RDALDEP1                      * Read department table
          BRANCH    OVRCD,ARFD0100
.
          MATCH     "1",ALDEACTV
          GOTO      ARFD0100 IF EQUAL       * Skip Inactive
.
          MATCH     FILTDEPT,ALDEDEPT
          IF        @EQUAL
            WRITE     HTMLFILE;"<option selected value=",ALDEDEPT,">":
                               TDESC,"</option>"
          ELSE
            IF        IBCNMHOS=1
              MATCH     WBSEHOSP,ALDEHOSP
              GOTO      ARFD0100 IF NOT EQUAL
            ENDIF
            WRITE     HTMLFILE;"<option value=",ALDEDEPT,">":
                               TDESC,"</option>"
          ENDIF     
          GOTO      ARFD0100
.
ARFD9999  RETURN
.
.-------------------------------------------------------------------
. Equipment Status List
.-------------------------------------------------------------------
ETAB0000  UNPACK    DIM127,D1,LINKPRG,DIM127                          *I-178888
          UNPACK    DIM127,D1,LINKREP,DIM127                          *I-178888
          UNPACK    DIM127,D1,LINKTEM,DIM127                          *I-178888
.
ETAB0500  MATCH     SP70,FILTDEPT           * Exit if not department
          GOTO      ETAB9999 IF EQUAL
.
          PACK      KEY53,FILTDEPT,SP70
          CALL      RSALEQU6                * positional read
ETAB1000  CALL      RKALEQU6                * read a record forward
          BRANCH    OVRCD,ETAB9999
.
          MATCH     FILTDEPT,ALEQDEPT
          GOTO      ETAB9999 IF NOT EQUAL
.
          MATCH     SP70,FILTSTAT
          IF        !@EQUAL
            MATCH     FILTSTAT,ALEQSTAT
            GOTO      ETAB1000 IF NOT EQUAL * match status
          ENDIF
.
          CALL      EROW0000                * Display Row
.
          GOTO      ETAB1000
.
ETAB9999  RETURN
.
.******************************************************************************
. Equipment Status List           
.******************************************************************************
.                                                            * start  *C-178888
EROW0000  MATCH     "0",ALEQSTAT
          IF        @EQUAL
            MOVE    "Available for Loan",STATDESC
            MATCH   "1",ALEQCONS
            GOTO    EROW1000 IF NOT EQUAL
            MOVE    "Available",STATDESC
          ENDIF
.
          MATCH     "1",ALEQSTAT
          IF        @EQUAL
            MOVE    "Currently on Loan",STATDESC
            MATCH   "1",ALEQCONS
            GOTO    EROW1000 IF NOT EQUAL
            MOVE    "Not available",STATDESC                   * end  *C-178888
          ENDIF
.
          MATCH     "2",ALEQSTAT
          IF        @EQUAL
            MOVE    "Inactive/Written Off",STATDESC
          ENDIF
.
          MATCH     "3",ALEQSTAT
          IF        @EQUAL
            MOVE    "Needs Maintenance",STATDESC
          ENDIF
.
EROW1000  MATCH     "1",ALEQACTV
          IF        @EQUAL
            MOVE       "<strike>",STRIKETG
            MOVE       "</strike>",STRENDTG
          ELSE
            MOVE       "",STRIKETG
            MOVE       "",STRENDTG
          ENDIF
.
          MOVE      ZERO,F3
          SQUEEZE   TEMPLATE
          MOVE      TEMPLATE,F3
.                       "001"    "002"                      * templare no.
          BRANCH    F3,EROW4000,EROW5000
.
EROW4000  WRITE     HTMLFILE;"t.addRow(#"",STRIKETG,ALEQEQUI,STRENDTG,"#",":
                             "#"",STRIKETG,ALEQDESC,STRENDTG,"#",":
                             "#"",STRIKETG,STATDESC,STRENDTG,"#",":
                             "#"",STRIKETG,ALEQCBAL,STRENDTG,"#");"
          GOTO      EROW9999
.
.                                           * List with Update for allweb0404002
EROW5000  WRITE     HTMLFILE;"t.addRow(#"":                           *I-178888
                             "javascript:ShowDetail":
                             "('",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&deptcode=",ALEQDEPT:
                             "&alequ001=",ALEQEQUI,"')#",":
                             "#"",STRIKETG,ALEQEQUI,STRENDTG,"&nbsp;#",":
                             "#"",STRIKETG,ALEQDESC,STRENDTG,"#",":
                             "#"",STRIKETG,STATDESC,STRENDTG,"#",":
                             "#"",STRIKETG,ALEQCBAL,STRENDTG,"#");"
          GOTO      EROW9999                                   * end  *I-178888
.
.
EROW9999  RETURN
.
.**********************************************************************
.*                             CASE0000                      *I-60700 *
.*  Routine to convert all except first character of each word to     *
.*        lower case                                                  *
.**********************************************************************
CASE0000  MOVE      ONE,FIRSTCH
          RESET     LINE60
.
CASE1000  CMATCH    SP1,LINE60
          GOTO      CASE5000 IF EQUAL            * match char to space
          BRANCH    FIRSTCH,CASE2000
          MOVE      LINE60,ANS
          RESET     UPPLOW
          SCAN      ANS,UPPLOW                   * test to see if char is
          GOTO      CASE1100 IF EQUAL            *   a letter
          MOVE      ONE,FIRSTCH
          GOTO      CASE3000                     * any char but a letter
          *         found
.
CASE1100  OR        040,LINE60                     * actual conversion to
          RESET     UPPLOW
          *         lower case (believe it
          *         or not!)
.
CASE2000  MOVE      ZERO,FIRSTCH                 * not on first char any
.                                                   *   more
CASE3000  BUMP      LINE60
          GOTO      CASE1000 IF NOT EOS          * move to next char
          RESET     LINE60                         * end of line reached
          GOTO      CASE9999
.
CASE5000  MOVE      ONE,FIRSTCH                  * first char was a space
          GOTO      CASE3000
.
CASE9999  RETURN
.
.------------------------------------------------------------
. Patient Care Team Maintenance
.------------------------------------------------------------
CARETM00  MATCH     SP70,UPDTTYPE
          GOTO      CARETM70 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add Formaction
          GOTO      CARETM10 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete Formaction
          GOTO      CARETM30 IF EQUAL
.
          GOTO      CARETM90
.
.   ************ ADD FORMACTION ************
.
CARETM10  PACK      KEY26,ALPCT001,ALPCT002,ALPCT003,ALPCT004,SP70
          CALL      RDALPCT1
          COMPARE   ZERO,OVRCD
          GOTO      CARETM93 IF EQUAL
.
          MOVE      ALPCT001,ALPCURNO
          MOVE      ALPCT002,ALPCTYPE
          MOVE      ALPCT003,ALPCCODE
          MOVE      ALPCT004,ALPCSITE
          MOVE      ALPCT005,ALPCADFL
.
          PACK      ALPCCDAT,CCC,CYY,CMM,CDD
          REP       " 0",ALPCCDAT
          CLOCK     TIME,ALPCCTIM
          MOVE      USERID,ALPCCUID
          MOVE      SP70,ALPCSPAR
.
          CALL      WRALPCT1
.
          MOVE      ZERO,EXIT
          GOTO      CARETM99
.
CARETM30  PACK      KEY26,ALPCT001,ALPCT002,ALPCT003,ALPCT004,SP70
          CALL      RDALPCT1
          BRANCH    OVRCD,CARETM92
.
          CALL      DEALPCT1
.
          MOVE      ZERO,EXIT
          GOTO      CARETM99
.
.         ************ DISPLAY FORMACTION **********
.
CARETM70  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,CARETM91
.
          MOVE      URNUMBER,KEY8   * Read Master Extn File 2
          CALL      RDPMPX21
          BRANCH    OVRCD,CARETM91
.
          PACK      KEY26,ALPCT001,ALPCT002,ALPCT003,ALPCT004,SP70
          CALL      RDALPCT1
          IF        OVRCD=1
            CALL      CLALPC00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Patient Care Team Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,CARETM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      CARETM99
.
CARETM90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CARETM99
.
CARETM91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CARETM99
.
CARETM92  MOVE      "Care Team Details Not Found. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CARETM99
.
CARETM93  MOVE      "Care Team Details Exist. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CARETM99
.
CARETM99  RETURN
.
.------------------------------------------------------------------------------
. Patient Care Team Details
.------------------------------------------------------------------------------
ALLP0000  BRANCH    FLDITMNO,ALLP0100,ALLP0200,ALLP0300,ALLP0400,ALLP0500
          GOTO      ALLP9999
.
ALLP0100  CALL      CARTB000           * Table of care team details
          GOTO      ALLP9999
.
ALLP0200  WRITE     HTMLFILE;ALPCURNO;
          GOTO      ALLP9999
.
ALLP0300  WRITE     HTMLFILE;ALPCTYPE;
          GOTO      ALLP9999
.
ALLP0400  WRITE     HTMLFILE;ALPCSITE;
          GOTO      ALLP9999
.
ALLP0500  UNPACK    DIM127,DIM1,KEY1,DIM127  
          MOVE      KEY1,F1
          BRANCH    F1,ALLP0510
.
          WRITE     HTMLFILE;ALPCCODE;  
          GOTO      ALLP9999
.
ALLP0510  MOVE      ALPCTYPE,F2           
          BRANCH    F2,ALLP0520,ALLP9999,ALLP9999,ALLP0520
.
ALLP0520  PACK      KEY10,ALPCCODE,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE    ALPCCODE,DOCFNAME
          ENDIF
          WRITE     HTMLFILE;DOCFNAME;  
          GOTO      ALLP9999
.
ALLP0530  PACK      KEY10,ALPCCODE,SP70
          CALL      RDALCAS1
          IF        OVRCD=1
            MOVE    ALPCCODE,ALCADESC
          ENDIF
.
          WRITE     HTMLFILE;ALCADESC;
          GOTO      ALLP9999
.
ALLP9999  RETURN
.
.------------------------------------------------------------
. Clear file vars for allpctaf
.------------------------------------------------------------
CLALPC00  MOVE      SP70,ALPCURNO
          MOVE      SP70,ALPCTYPE
          MOVE      SP70,ALPCCODE
          MOVE      SP70,ALPCSITE
          MOVE      SP70,ALPCADFL
          MOVE      SP70,ALPCCDAT
          MOVE      SP70,ALPCCTIM
          MOVE      SP70,ALPCCUID
          MOVE      SP70,ALPCSPAR

CLALPC99  RETURN
.
.-------------------------------------------------------------------------------
.   Set Parameters for Option 5 - Care team details                
.-------------------------------------------------------------------------------
STALP000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STALP001,STALP002,STALP003,STALP004,STALP005
          GOTO      STALP999
.
STALP001  MOVE      QRYDATA,ALPCT001
          PACK      ALPCT001,ALPCT001,SP70
          GOTO      STALP999
.
STALP002  MOVE      QRYDATA,ALPCT002
          PACK      ALPCT002,ALPCT002,SP70
          GOTO      STALP999
.
STALP003  MOVE      QRYDATA,ALPCT003
          PACK      ALPCT003,ALPCT003,SP70
          GOTO      STALP999
.
STALP004  MOVE      QRYDATA,ALPCT004
          PACK      ALPCT004,ALPCT004,SP70
          GOTO      STALP999
.
STALP005  MOVE      QRYDATA,ALPCT005
          PACK      ALPCT005,ALPCT005,SP70
          GOTO      STALP999
.
STALP999  RETURN
.
.------------------------------------------------------------
. Table of Patient Care Team Details
.------------------------------------------------------------
CARTB000  PACK      KEY26,URNUMBER,SP70
          CALL      RSALPCT1
CARTB100  CALL      RKALPCT1
          BRANCH    OVRCD,CARTB999
.
          MATCH     URNUMBER,ALPCURNO
          GOTO      ALLN9999 IF NOT EQUAL
.
          CLEAR     HTMLLINK
          APPEND    "javascript:ShowCare('",HTMLLINK
          APPEND    ALPCURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    ALPCTYPE,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    ALPCCODE,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    ALPCSITE,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      SP70,TYPEDESC
          MOVE      SP70,TEAMDESC
          MOVE      SP70,SITEDESC
.
          MOVE      ALPCTYPE,F2
          BRANCH    F2,CARTB200,CARTB300,CARTB400,CARTB500
.
CARTB200  MOVE      "Consultant",TYPEDESC
.
          PACK      KEY10,ALPCCODE,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE    SP70,DOCFNAME
          ENDIF
          MOVE      DOCFNAME,TEAMDESC
.
          MOVE      SP70,SITEDESC
          GOTO      CARTB600
.
CARTB300  MOVE      "Clinic Type",TYPEDESC
          CALL      COTFIL00              * Open correct outpatient files
.
          PACK      KEY12,ALPCSITE,ALPCCODE,SP70
          CALL      RDCTYA1
          IF        OVRCD=1
            MOVE      ALPCCODE,OCTDESC
          ENDIF
.
          MOVE      OCTDESC,TEAMDESC
          MOVE      OSTDESC,SITEDESC
.
          GOTO      CARTB600
.
CARTB400  MOVE      "Clinic Id",TYPEDESC
          CALL      COTFIL00              * Open correct outpatient files
.
          MOVE      ALPCCODE,DIM6     
          PACK      KEY20,ALPCSITE,DIM6,Z70
          CALL      RDSCLIA1          * Read Clinic Record
          CALL      RDPCLIA1          * Read Clinic Record
          IF        OVRCD=1
            MOVE      ALPCCODE,OCADESC
            GOTO      CART450
          ENDIF
.
          MATCH     ALPCSITE,OCASITE
          IF        !@EQUAL
            MOVE      ALPCCODE,OCADESC
            GOTO      CART450
          ENDIF
.
          MATCH     DIM6,OCACLIN
          IF        !@EQUAL
            MOVE      ALPCCODE,OCADESC
            GOTO      CART450
          ENDIF
.
CART450   MOVE      OCADESC,TEAMDESC
          MOVE      OSTDESC,SITEDESC
          GOTO      CARTB600
.
CARTB500  MOVE      "Case Team",TYPEDESC
.
          PACK      KEY10,ALPCCODE,SP70
          CALL      RDALCAS1
          IF        OVRCD=1
            MOVE    SP70,ALCADESC
          ENDIF
.
          MOVE      ALCADESC,TEAMDESC
.
          MOVE      SP70,SITEDESC
          GOTO      CARTB600
.
CARTB600  PACK      KEY10,ALPCCUID,SP70
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALPCCUID,WBSENAM
          ENDIF
.
          MATCH     "1",ALPCADFL
          IF         @EQUAL
            MOVE       "Yes",DIM3 
          ELSE
            MOVE       "No ",DIM3 
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":     *0
                             "#"",ALPCURNO,"#",":              *1
                             "#"",TYPEDESC,"#",":              *2
                             "#"",TEAMDESC,"#",":              *3
                             "#"",SITEDESC,"#",":              *4
                             "#"",DIM3,"#",":                  *5
                             "#"",ALPCCDAT,ALPCCTIM,"#",":     *6 
                             "#"",WBSENAM,"#");"               *7
.
          GOTO      CARTB100
.
CARTB999  RETURN
.
.------------------------------------------------------------
. SCCT Referrals
.------------------------------------------------------------
SCTTMP00  MATCH     SP70,UPDTTYPE
          GOTO      SCTTMP70 IF EQUAL
.         
          MATCH     "A",UPDTTYPE    * Add Formaction
          GOTO      SCTTMP10 IF EQUAL 
.         
          MATCH     "U",UPDTTYPE    * Update Formaction
          GOTO      SCTTMP20 IF EQUAL 
.         
          MATCH     "D",UPDTTYPE    * Delete Formaction
          GOTO      SCTTMP30 IF EQUAL 
.         
          GOTO      SCTTMP90
.         
.   ************ ADD FORMACTION ************
.   
SCTTMP10  MOVE      ZERO,F8
          PACK      KEY16,URNUMBER,Z70
          CALL      RSALSCT1
          CALL      RPALSCT1
          BRANCH    OVRCD,SCTTMP15
.         
          MATCH     URNUMBER,ALSCURNO
          GOTO      SCTTMP15 IF NOT EQUAL
.
          MOVE      ALSCCNTR,F8
.         
          COMPARE   "99999999",F8           * Max 99999999
          GOTO      SCTTMP93 IF EQUAL
.
SCTTMP15  CALL      CLALSC00                * clear allsct
          ADD       ONE,F8
.           
          COMPARE   "99999999",F8           * Max 99999999
          GOTO      SCTTMP93 IF EQUAL
.         
          CALL      MVALSC00
.
          MOVE      URNUMBER,ALSCURNO
          MOVE      F8,ALSCCNTR
.
          PACK      ALSCCDAT,CCC,CYY,CMM,CDD
          REP       " 0",ALSCCDAT
          CLOCK     TIME,ALSCCTIM
          MOVE      USERID,ALSCCUID
          PACK      ALSCUDAT,SP70
          PACK      ALSCUTIM,SP70
          PACK      ALSCUUID,SP70
          MOVE      SP70,ALSCSPAR
.
          PACK      KEY16,ALSCURNO,ALSCCNTR,SP70
          CALL      RAALSCT1
          COMPARE   ONE,OVRCD
          GOTO      SCTTMP15 IF NOT EQUAL
.
          CALL      WRALSCT1
.
          CALL      UPACM000               * Update SCTT Comments
.
          MOVE      ZERO,EXIT
          GOTO      SCTTMP99
.
SCTTMP20  PACK      KEY16,ALSCT001,ALSCT002,SP70
          CALL      RDALSCT1
          BRANCH    OVRCD,SCTTMP92
.
          CALL      MVALSC00
.
          PACK      ALSCUDAT,CCC,CYY,CMM,CDD
          REP       " 0",ALSCUDAT
          CLOCK     TIME,ALSCUTIM
          MOVE      USERID,ALSCUUID
.
          CALL      UPALSCT1
.
          CALL      UPACM000               * Update SCTT Comments
.
          MOVE      ZERO,EXIT
          GOTO      SCTTMP99
.
SCTTMP30  PACK      KEY16,ALSCT001,ALSCT002,SP70
          CALL      RDALSCT1
          BRANCH    OVRCD,SCTTMP92
.
          CALL      DEALSCT1
.
          MOVE      ZERO,EXIT
          GOTO      SCTTMP99
.
.         ************ DISPLAY FORMACTION **********
.
SCTTMP70  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,SCTTMP91
.
          MOVE      URNUMBER,KEY8   * Read Master Extn File 2
          CALL      RDPMPX21
          BRANCH    OVRCD,SCTTMP91
.
          PACK      KEY16,URNUMBER,SCTTCNTR,SP70
          CALL      RDALSCT1
          IF        OVRCD=1
            CALL      CLALSC00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "SCTT Referrals",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,SCTTMP99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SCTTMP99
.
SCTTMP90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SCTTMP99
.
SCTTMP91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SCTTMP99
.
SCTTMP92  MOVE      "SCTT Referral Details Not Found. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SCTTMP99
.
SCTTMP93  MOVE      "SCTT Referral Details Exist. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SCTTMP99
.
SCTTMP99  CLOSE     ALCSTFIL,DELETE    * Delete temp file createdy by CLRACM00
          RETURN
+
.------------------------------------------------------------------------------
. General tags
.------------------------------------------------------------------------------
TABL0000  BRANCH    FLDITMNO,TABL0010,TABL0020
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      TABL9999
.
TABL0010  CALL      SCTTRF00                * SCTT Referral Table Output
          GOTO      TABL9999
TABL0020  PACK      KEY3,WBSEHOSP,SP70
          CALL      RDPTHSP1
          BRANCH    OVRCD,TABL9999
.
          UNPACK    DIM127,DIM1,KEY1,DIM127
          MOVE      KEY1,F1
          BRANCH    F1,TABL0025,TABL0026
.
          WRITE     HTMLFILE;PTHSHOSP;      * hospital id
          GOTO      TABL9999
.
TABL0025  WRITE     HTMLFILE;PTHSNAME;      * hospital name
          GOTO      TABL9999
.
TABL0026  WRITE     HTMLFILE;PTHSTELE;      * hospital phone number
          GOTO      TABL9999
.
TABL9999  RETURN
+
.*******************************************************************************
.*                Display SCTT External Referrals List                         *
.*******************************************************************************
SCTTRF00  PACK      KEY16,URNUMBER,SP70     * Loop through SCTT referrals
          CALL      RSALSCT1                * Positional Read
SCTTRF10  CALL      RKALSCT1                * Sequential Read
          BRANCH    OVRCD,SCTTRF99
.
          MATCH     URNUMBER,ALSCURNO
          GOTO      SCTTRF99 IF NOT EQUAL
.
          CALL      SCTTNM00                * Setting SCTT Name
.
SCTTRF20  CALL      SCTROW00                * Displaying the Table Row
          GOTO      SCTTRF10
.
SCTTRF99  RETURN
.*******************************************************************************
.*                    Setting SCTT Name for Display                            *
.*******************************************************************************
SCTTNM00  MOVE      SP70,SCTTDESC
.>>>>     MOVE      "EV",CATEGORY
.>>>>     MOVE      PMEVEVNT,CODE
.>>>>     CALL      GETCOD00
.>>>>     MOVE      TDESC,SCTTDESC
.>>>>     MATCH     ANSI,TCINDC1
.>>>>     IF        @EQUAL
...............
.>>>>     ENDIF
.>>>>     MATCH     "001",ALSCTYPE
.>>>>     IF        @EQUAL
            MOVE      "003",LINKTEM
.>>>>       MOVE      SCTTDSC1,SCTTDESC
.>>>>     ENDIF
.>>>>     MATCH     "002",ALSCTYPE
.>>>>     IF        @EQUAL
.>>>>       MOVE      "005",LINKTEM
.>>>>       MOVE      SCTTDSC2,SCTTDESC
.>>>>     ENDIF
.
SCTTNM99  RETURN
.*******************************************************************************
.                           Display Of SCTT Row
.*******************************************************************************
SCTROW00  WRITE     HTMLFILE;"<tr>":
                    "<td nowrap>":
                    "<a HREF ='javascript:SCTTRefLink(#"",ALSCURNO,"#",#"":
                    ALSCCNTR,"#",#"",LINKTEM,"#")'>":
                    "<img src=../images/AppointmentIcon.gif hspace=4":
                    " border=0 align=absmiddle></a>":
                    ALSCCNTR,"</td>":
                    "</tr>"
SCTROW99  RETURN
+
.*******************************************************************************
. OUTFIL00 - Check if the user id has an outpatienty site. If they do open
.            the correct outpatient files according to the site. If they do not
.            have a site make sure the default prefix of out is is used to
.            open the files.
.*******************************************************************************
OUTFIL00  MATCH     SP70,WBSESIT             * Does the user have a site
          GOTO      OUTFIL50 IF NOT EQUAL
.
OUTFIL10  MATCH     "out",CFILEPRE           * Is out currently open
          IF        !@EQUAL
            MOVE      "out",OSTFILE          * open default prefix of out
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open out Outpatient Files
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OUTFIL99
.
OUTFIL50  MOVE      WBSESIT,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,OUTFIL90           * error invalid site code
.
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open prefixed Outpatient Files
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OUTFIL99
.
OUTFIL90  MOVE      "Outpatient Site Invalid",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
OUTFIL99  RETURN
.
.------------------------------------------------------------
. Open Outpatient Files
.------------------------------------------------------------
OPNOUT00  PACK      CFNAME,CFILEPRE,FILCLIA1  * Get the name of the CLIA1 file
          CLOSE     OUTCLIA1
          OPEN      OUTCLIA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCTYA1  * Get the name of the CLTY2 file
          CLOSE     OUTCTYA1
          OPEN      OUTCTYA1,CFNAME           * Open the file
.
OPNOUT99  RETURN
.
.*******************************************************************************
. COTFIL00 - Check that the correct outpatient site prefix files are open
.            according to the care team files site code. If the site is invalid
.            or blank just open the default site of out.

.*******************************************************************************
COTFIL00  MATCH     SP70,ALPCSITE            * Does the visit have a site
          GOTO      COTFIL50 IF NOT EQUAL
.
COTFIL10  MATCH     "out",CFILEPRE           * Is out currently open
          IF        !@EQUAL
            MOVE      "out",OSTFILE          * open default prefix of out
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open out Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
COTFIL50  MOVE      ALPCSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,COTFIL10           * error invalid site code
.
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open prefixed Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
COTFIL99  RETURN
.
.------------------------------------------------------------
. Equipment Search
.------------------------------------------------------------
EQSRCH00  MATCH     SP70,UPDTTYPE
          GOTO      EQSRCH70 IF EQUAL
.
          GOTO      EQSRCH90
.
EQSRCH70  PACK      KEY3,DEPTCODE,SP70
          CALL      RDALDEP1                * Read department table
          IF        OVRCD=1
            MOVE      SP70,DEPARTMN         * Save Department Variable
            CALL      CLRDEP00                    * Clear all the file variables
          ELSE
            MOVE      ALDEDEPT,DEPARTMN       * Save Department Variable
          ENDIF
.
          PACK      KEY10,ALEQU001,SP70
          CALL      RDALEQU1
          IF        OVRCD=1
            CALL      CLALLEQU                    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Equipment Search",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EQSRCH99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EQSRCH99
.
EQSRCH90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EQSRCH90
.
EQSRCH99  RETURN
.
.------------------------------------------------------------
. Equipment Loan Notes                                                *I-192466
.------------------------------------------------------------
LONNOT00  MOVE      ZERO,EXIT
          RESET     UPDATETY
          MATCH     SP70,UPDATETY
          GOTO      LONNOT40 IF EQUAL
.         
          MATCH     "A",UPDATETY            * Add Notes Formaction
          GOTO      LONNOT10 IF EQUAL       
.         
          MATCH     "D",UPDATETY            * Delete Notes Formaction
          GOTO      LONNOT30 IF EQUAL
.
          GOTO      LONNOT93
.
.         ************ ADD FORMACTION ***********
.
LONNOT10  PACK      KEY16,URNUMBER,LOANNUMB,SP70
          CALL      RDALLON1
          BRANCH    OVRCD,LONNOT90
.
          CALL      ADLNOT00                * Add Equip.Loan Note SubRoutine
.
          MOVE      ZERO,EXIT
          GOTO      LONNOT99
.
.         ************ DELETE FORMACTION **********
.
LONNOT30  PACK      KEY16,URNUMBER,LOANNUMB,SP70
          CALL      RDALLON1
          BRANCH    OVRCD,LONNOT90
.
          CALL      DELNOT00                * Delete Equip.Loan Note SubRoutine
          BRANCH    EXIT,LONNOT99
.
          MOVE      ZERO,EXIT
          GOTO      LONNOT99
.
.         ************ DISPLAY FORMACTION **********
.
LONNOT40  MOVE      URNUMBER,KEY8           * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,LONNOT94
.
          PACK      KEY16,URNUMBER,LOANNUMB,SP70
          CALL      RDALLON1
          BRANCH    OVRCD,LONNOT90
.
          PACK      KEY25,URNUMBER,LOANNUMB,NOTETYPE,NOTENUMB,SP70
          CALL      RDALEDT1
          IF        OVRCD=1
            CALL      CLLNOT00     * Clear all the file variables
          ENDIF
.
          MOVE      "Equipment Loan Note Maintenance",WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LONNOT99
          CALL      WEBBOD00                * Process Web Body
          CALL      WEBEND00                * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      LONNOT99
.
LONNOT90  MOVE      "Invalid Patient Loan ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LONNOT99
.
LONNOT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LONNOT99
.
LONNOT94  MOVE      "Invalid U/R Number ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LONNOT99
.
LONNOT99  CLOSE     COMFILVF,DELETE
          RETURN
.
.------------------------------------------------------------
.  Write Equip. Loan Notes                                            *I-192466
.------------------------------------------------------------
ADLNOT00  MOVE      ZERO,F6
.
          PACK      KEY25,URNUMBER,LOANNUMB,NOTETYPE,Z70
          CALL      RSALEDT1
          CALL      RPALEDT1                * read back to get last record
          BRANCH    OVRCD,ADLNOT40
.
          MATCH     URNUMBER,ALEDURNO       * Same UR No.
          GOTO      ADLNOT40 IF NOT EQUAL
.
          MATCH     LOANNUMB,ALEDLOAN       * Same Loan Number
          GOTO      ADLNOT40 IF NOT EQUAL
.
          MATCH     NOTETYPE,ALEDTYPE       * Same note type
          GOTO      ADLNOT40 IF NOT EQUAL
.
          MOVE      ALEDNOTE,F6
          ADD       ONE,F6
          MOVE      F6,ALEDNOTE             * Note Number
          GOTO      ADLNOT50
.
ADLNOT40  MOVE      ONE,F6
          MOVE      F6,ALEDNOTE             * Note Number
.
ADLNOT50  MOVE      URNUMBER,ALEDURNO       * U/R Number  
          MOVE      LOANNUMB,ALEDLOAN       * Equip.Loan number
          MOVE      NOTETYPE,ALEDTYPE       * Notes Type
.
          PACK      ALEDDATE,CCC,CYY,CMM,CDD
          REP       " 0",ALEDDATE           * Date
          CLOCK     TIME,ALEDTIME
          MOVE      USERID,ALEDUSER         * User
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,ADLNOT99
.
          MOVE      WBSEOCG,ALEDOCCG        * Occupation Group
          MOVE      "0",ALEDDELT            * Delete Indicator
          MOVE      SP70,ALEDDDAT           * Delete Date
          MOVE      SP70,ALEDDTIM           * Delete Time
          MOVE      SP70,ALEDDUSE           * Delete User
          MOVE      SP70,ALEDDOCC           * Delete User Occupation group
          PACK      ALEDSPAR,SP70,SP70      * Spare
.
          PACK      KEY25,ALEDURNO,ALEDLOAN,ALEDTYPE,ALEDNOTE,SP70
          CALL      RDALEDT1
          IF        OVRCD=1
            CALL      WRALEDT1              * Write Record
          ELSE
            GOTO      ADLNOT99
          ENDIF
.
.         now write the equipment loan notes
.
          MOVE      ALEDURNO,ALETURNO
          MOVE      ALEDLOAN,ALETLOAN
          MOVE      ALEDTYPE,ALETTYPE
          MOVE      ALEDNOTE,ALETNOTE
          PACK      ALETSPAR,SP70,SP70      * Spare
          MOVE      ZERO,F3
.
ADLNOT90  ADD       ONE,F3
          MOVE      F3,ALETUNIQ
.
          READ      COMFILVF,SEQ;ALETCMNT
.
          PACK      KEY28,ALETURNO,ALETLOAN,ALETTYPE,ALETNOTE,ALETUNIQ,SP70
          CALL      WRALETX1
          IF        F3 >= LASTITEM
            GOTO     ADLNOT99
          ENDIF
          GOTO      ADLNOT90
.
ADLNOT99  RETURN
.
.-----------------------------------------------------------
. Delete Equip.Loan Note for a Patient                                *I-192466
.-----------------------------------------------------------
DELNOT00  MATCH     SP70,URNUMBER
          GOTO      DELNOT90 IF EQUAL
.
          MATCH     SP70,LOANNUMB
          GOTO      DELNOT92 IF EQUAL
.
          MOVE      URNUMBER,ALEDURNO   *  Visit Number
          MOVE      LOANNUMB,ALEDLOAN   *  Encounter Number
.
          PACK      KEY25,URNUMBER,LOANNUMB,NOTETYPE,NOTENUMB,SP70
          CALL      RDALEDT1
          BRANCH    OVRCD,DELNOT99
.
          MATCH     "1",SUPRFLAG
          IF          !@EQUAL
            MATCH     USERID,ALEDUSER
            GOTO      DELNOT91 IF NOT EQUAL
          ENDIF
.
          PACK      ALEDDDAT,CCC,CYY,CMM,CDD
          REP       " 0",ALEDDDAT
          CLOCK     TIME,ALEDDTIM
          MOVE      USERID,ALEDDUSE
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,DELNOT99
.
          MOVE      WBSEOCG,ALEDDOCC
.
          MOVE      "1",ALEDDELT           * Delete Indicator
          PACK      KEY25,URNUMBER,LOANNUMB,NOTETYPE,NOTENUMB,SP70
          CALL      UPALEDT1               * Write Record
          GOTO      DELNOT98
.
DELNOT90  MOVE      "U/R Number should not be blank.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELNOT99
.
DELNOT91  MOVE      "User ID must be the same to strikeout this entry.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELNOT99
.
DELNOT92  MOVE      "Loan Number should not be blank.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELNOT99
.
DELNOT98  MOVE      ZERO,EXIT
.
DELNOT99  RETURN
.
.---------------------------------------------------------------
.
.---------------------------------------------------------------
GEEQNM00  MOVE      " 0",KEY2
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY22,KEY2,ALEQITEM,Z70
          CALL      RSPRIT1
GEEQNM10  CALL      RPPRIT1
          BRANCH    OVRCD,GEEQNM20
          COMPARE   ZERO,PRITFLAG
          GOTO      GEEQNM20 IF NOT EQUAL
          MATCH     ALEQITEM,PRITITMN
          GOTO      GEEQNM20 IF NOT EQUAL
          MATCH     KEY8,PRITDAT1
          GOTO      GEEQNM90 IF EQUAL
          GOTO      GEEQNM10 IF NOT LESS
          GOTO      GEEQNM90
.
GEEQNM20  MOVE      "&nbsp",PRITDESC
          MOVE      ONE,OVRCD
          GOTO      GEEQNM99
.
GEEQNM90  MOVE      ZERO,OVRCD
GEEQNM99  RETURN
.
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
.
.------------------------------------------------------------
. Dummy labels for ALLEQUTM
.------------------------------------------------------------
PREVGE00  RETURN
NEXTGE00  RETURN
EQTAB000  RETURN
.
.-----------------------------------------------------------
. Initialize's file data  (Department codes)
.-----------------------------------------------------------
CLRDEP00  MOVE      SP70,ALDEDEPT
          MOVE      SP70,ALDEREFE
          MOVE      SP70,ALDESECU
          MOVE      SP70,ALDEVACS
          MOVE      SP70,ALDEBILL
          MOVE      SP70,ALDEPRAC
          MOVE      SP70,ALDEUR1
          MOVE      SP70,ALDEUR2
          MOVE      SP70,ALDEUD1
          MOVE      SP70,ALDEUD2
          MOVE      SP70,ALDEUY1
          MOVE      SP70,ALDEUY2
          MOVE      SP70,ALDEUC1
          MOVE      SP70,ALDEUC2
          MOVE      SP70,ALDECDAT
          MOVE      SP70,ALDECTIM
          MOVE      SP70,ALDECUID
          MOVE      SP70,ALDEUDAT
          MOVE      SP70,ALDEUTIM
          MOVE      SP70,ALDEUUID
.
CLRDEP99  RETURN
.
.------------------------------------------------------------------------------
. General tags for equipment search
.------------------------------------------------------------------------------
STAG0000  BRANCH    FLDITMNO,STAG1000
.
          WRITE     HTMLFILE;"Invalid Tag";
          GOTO      STAG9999
.
STAG1000  CALL      SLON0000                * Currently on loan to
          GOTO      STAG9999
.
STAG9999  RETURN
.------------------------------------------------------------------------------
. Show who currently has a piece of equipment on loan
.------------------------------------------------------------------------------
SLON0000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          PACK      KEY27,ALEQU001,ZERO,SP70
          CALL      RSALLON4
SLON1000  CALL      RKALLON4
          BRANCH    OVRCD,SLON9999
.
          MATCH     ALEQU001,ALLOEQUI       * Correct Equipment Code
          GOTO      SLON9999 IF NOT EQUAL
.
          MATCH     "0",ALLOSTAT           * Currently on loan
          GOTO      SLON9999 IF NOT EQUAL
.
          PACK      KEY8,ALLOURNO,SP70       * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,SLON1000
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
          CALL      PATFLD00
.
          WRITE     HTMLFILE;"<tr><td nowrap>":
                             "Currently on loan to</td>":
                             "<td><img src=../images/PatientFolder",KEY3,".gif":
                             " class=ListIcon ":
                             " onclick='parent.location.href=#"",LINKPRG,".pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",ALLOURNO:
                             "&loannumb=",ALLOLOAN,"#";'>":
                             FORMATNM,"</td></tr>";
.
          GOTO      SLON1000
.
SLON9999  RETURN
.
.------------------------------------------------------------------------------
. Read Loan X (READLOAN) for this urnumber so all loan vars can be used 
. Loans are read in loan number order and must have a status of loaned.
.------------------------------------------------------------------------------
LOAN0000  MOVE      ZERO,LOANCNTR
.
          PACK      KEY16,URNUMBER,SP70
          CALL      RSALLON1
LOAN1000  CALL      RKALLON1 
          BRANCH    OVRCD,LOAN9000
.
          MATCH     URNUMBER,ALLOURNO            * U/R
          GOTO      LOAN9000 IF NOT EQUAL
.
          MATCH     "0",ALLOSTAT                 * Loaned
          GOTO      LOAN1000 IF NOT EQUAL
.
          ADD       ONE,LOANCNTR                 * Loan Count
.
          COMPARE   LOANCNTR,READLOAN            * Correct Loan
          GOTO      LOAN1000 IF NOT EQUAL
.
          PACK      KEY10,ALLOEQUI,SP70          * Read Equipment File
          CALL      RDALEQU1
          IF        OVRCD=1
            CALL      CLALLEQU
          ENDIF
.
          GOTO      LOAN9999
.
LOAN9000  CALL      CLRALN00
          CALL      CLALLEQU
.
LOAN9999  RETURN
.-------------------------------------------------------------------
. Active Equipment Search/List
.-------------------------------------------------------------------
ELST0000  MATCH     SP70,FILTDEPT           * Exit if not department
          GOTO      ELST9999 IF EQUAL
          PACK      KEY53,FILTDEPT,SP70
          CALL      RSALEQU6                * positional read
ELST1000  CALL      RKALEQU6                * read a record forward
          BRANCH    OVRCD,ELST9999
          MATCH     FILTDEPT,ALEQDEPT
          GOTO      ELST9999 IF NOT EQUAL
          MATCH     "0",ALEQSTAT
          GOTO      ELST1000 IF NOT EQUAL 
          MOVE      "CG",CATEGORY
          MOVE      ALEQDEPT,CODE
          CALL      GETCOD00
          MOVE      SP70,KEY10
          MATCH     "1",ALEQCONS            * is it a Consumable?
          IF        @EQUAL 
            MOVE      "Consumable",KEY10
          ENDIF
          WRITE     HTMLFILE;"t.addRow(#"",ALEQEQUI,"#",":
                             "#"",ALEQDESC,"#",":
                             "#"",ALEQDEPT,"#",":
                             "#"",TDESC,"#",":
                             "#"",ALEQCONS,"#",":
                             "#"",KEY10,"#",":
                             "#"",ALEQCBAL,"#");"
          GOTO      ELST1000
.
ELST9999  RETURN
.
.------------------------------------------------------------
.
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       GCCARD00
.
          INC       ALLCASIO/INC
          INC       ALLCSTIO/INC   * SCTT comments file
          INC       ALLSCTIO/INC
          INC       ALLPCTIO/INC
          INC       PATDO1IO/INC   * Doctor File
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PMSCCDIO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PATALRIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATFN1IO/INC
          INC       PATWR1IO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       PATITMIO/INC
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATCMCIO/INC
          INC       PATASFIO/INC
          INC       PRIITMIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       APSMASIO/INC
          INC       PATIN1IO/INC
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       IBATMDIO/INC
.
          INC       ALLLONIO/INC
          INC       ALLEQUIO/INC
          INC       ALLMAIIO/INC
          INC       ALLDEPIO/INC
          INC       ALLEDTIO/INC
          INC       ALLETXIO/INC
.
          INC       ALLEQUTM
          INC       ALLSCTTM
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLALLEQU
          INC       CLPMSPX2
          INC       PATMASTM
          INC       PATDOCTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PATDSCTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       ALLCSTCD
          INC       PATNAMCD
          INC       TFILENAM

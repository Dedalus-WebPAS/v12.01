. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBABOK71                         *
. *                                                        *
. *     FUNCTION:         BOOKING MONTHLY STATISTICS       *
. *                                                        *
. *     DATE WRITTEN:     10/02/88                         *
. *                                                        *
. *     AUTHOR:           J CLARK                          *
. *                                                        *
. *     MODIFICATIONS:                                     *
.*         V5.08.B01 12/01/2000  J Habasque  634273        *
.*                   Fixed no booking error                *
.*         V5.07.02  07/12/1999  Jill Habasque             *
.*                   Added over 14 periods error           *
.*         V5.07.01  06/10/1999  Jill Habasque SRF 634275  *
.*                   Fixed date ranges                     *
.*                        W2.01 16/06/88 M.HAYSE           *
. *                       CHECK FOR UNKNOWN CODES          *
. *                       V4.00 22/08/88 M.HAYSE           *
. *                       DEPOSIT ADDED                    *
. *                       V4.02 25/08/88 M.HAYSE           *
. *                       NEW FORMAT BREAKDOWN NOR,MAT,THE *
. *                       V4.03 M.HAYSE 29/09/88           *
. *    BOOKING  DELIVERIES=BSBOOK-BSUNDLV-BSCANCB          *
. *    ACTUAL   DELIVERIES=BSDELV                          *
. *                       V4.04 M.HAYSE 29/09/88           *
. *                             HEADING TO BOOK MNTH STAT  *
. *                       V5.00 21.02.89 S.Barcham         *
. *                             Fix Heading                *
. *                             SRF003419                  *
. *                       V5.01 10/06/89 M.Ohleiter        *
. *                             Fix date input             *
. *                             SRF 101032                 *
. *                    V5.01.01 27/06/89 M.Ohleiter        *
. *                             Updated for new release    *
. *                             09/07/89 J.Clark           *
. *                             Added KEY9 Variable        *
. *                             required by BOKIOSTA       *
. *                             10/07/89 K.Peak            *
. *                             Added 13x4 periods         *
. *                                                        *
. *       V5.01.10  23/08/1990 Justin Coates               *
. *                 Standardized code ie modularized it    *
. *                 and fixed the multiple bugs in it      *
. *                 Added display of date ranges for period*
. *       V5.01.11  10/10/1990 Karin Peak                  *
. *                 Fixed the keyin of period to use       *
. *                 calander date instead of financial date*
. **********************************************************
.
.$$$$$
.         IBABOK72.PBL        Booking Monthly Statistics
.         ============        ==========================
.
.         KOPT0000 - keyin the report type, Normal, Maternity, Theatre
.         KPER0000 - keyin the period range
.         REPT0000 - produce the report
.                    For each suboption (OPTDES/COPTION)
.                      Read a record of the booking stats file;
.                        For each booking type (BSTYPE) do
.                          If different booking type then
.                            print stats about prev. book type;
.                            add F1-13 to T1-13 and add FTOTAL to TTOTAL;
.                            set up new saved booking type, clear F1-13, FTOTAL;
.                          endif
.                          Load the appropriate stat. from the file according 
.                          to the value of OPTDES/COPTION & period => FORM6;
.                          Store FORM6 in the periods value F1-13;
.                          Add FORM6 to the booking type total FTOTAL;
.                        endfor
.                      endread
.                      Print stats for previous booking type;
.                      Print totals for sub-option;
.                    endfor
.$$$$$
.
          INC       STD001FD                * general variables
          INC       BOKSTAFD/INC            * booking stats file
          INC       PATCODFD/INC            * codes file
          INC       PATCOMM/INC             * contains KEYPER
          INC       PATCONFD/INC            * patient control file
          INC       PATDRGFD/INC            * date range file
.
. ------- program variables -------
.
COUNTR    FORM      2         * how many periods to do report for: max of 13
CODEXXX   DIM       3         * thscode for the option chosen
ENDMM     DIM       2         * end month
ENDYY     DIM       4         * end year ccyy
ENDYY2    DIM       2         * end year yy
ENDDATE   DIM       6         * end date
.
DATEA     DIM       10        * from date for from period
DATEB     DIM       10        * to   date for from period
DATEC     DIM       10        * from date for to   period
DATED     DIM       10        * to   date for to   period
DESCOPT   DIM       18        * holds sub-heading description
DIM4      DIM       4         * variable for year ccyy
FORM6     FORM      6         * holds the stats for a col
.
FTOTAL    FORM      7         * the total of the ranges for a booking type
F0A       FORM      6         * value of range to print
F1A       FORM      6         * value of range to print
F2A       FORM      6         * value of range to print
F3A       FORM      6         * value of range to print
F4A       FORM      6         * value of range to print
F5A       FORM      6
F6A       FORM      6
F7A       FORM      6
F8A       FORM      6
F9A       FORM      6
F10A      FORM      6
F11A      FORM      6
F12A      FORM      6
F13A      FORM      6
.
.
LFLAG     FORM      1         * if = 0, means table heading is printed
LIMIT     FORM      2         * how many sub-options to print
.
M0        DIM       2         * period to print
M1        DIM       2         * each progressive period
M2        DIM       2
M3        DIM       2
M4        DIM       2
M5        DIM       2
M6        DIM       2
M7        DIM       2
M8        DIM       2
M9        DIM       2
M10       DIM       2
M11       DIM       2
M12       DIM       2
M13       DIM       2
.
OPTDES    DIM       24        * holds the suboption description
OPT2      DIM       18        * as above but shorter version
.
STMM      DIM       2         * start month
STMMFORM  FORM      2         * start month as a form
STYY      DIM       4         * start year
STDATE    DIM       6         * start date
STORECNT  FORM      2         * which F1-13 value to store FORM6 in
SVTYPE    DIM       3         * the saved booking type (ie previous one)
.
TTOTAL    FORM      7         * overall total of the totals for each book. type
T0        FORM      6         * overall total to print for each OPTDES
T1        FORM      6         * overall total of individual ranges
T2        FORM      6
T3        FORM      6
T4        FORM      6
T5        FORM      6
T6        FORM      6
T7        FORM      6
T8        FORM      6
T9        FORM      6
T10       FORM      6
T11       FORM      6
T12       FORM      6
T13       FORM      6
.
XCC       DIM       2
XCOUNT    FORM      2         * counter
XYYMM     DIM       6         * used as a progressivve period to store
XMM       DIM       2
.
YMM       DIM       2         * the year only of the range
Y0        DIM       2         * year to print (only the year, not cent)
Y1        DIM       2         * each progressive year
Y2        DIM       2
Y3        DIM       2
Y4        DIM       2
Y5        DIM       2
Y6        DIM       2
Y7        DIM       2
Y8        DIM       2
Y9        DIM       2
Y10       DIM       2
Y11       DIM       2
Y12       DIM       2
Y13       DIM       2
.
YYMM1     DIM       6
YYMM2     DIM       6
YYMM3     DIM       6
YYMM4     DIM       6
YYMM5     DIM       6
YYMM6     DIM       6
YYMM7     DIM       6
YYMM8     DIM       6
YYMM9     DIM       6
YYMM10    DIM       6
YYMM11    DIM       6
YYMM12    DIM       6
YYMM13    DIM       6
.
. ------- prgoram constants -------
.
CODEBK    INIT      "BK"                       * booking type
CODEMAT   INIT      "MAT"                      * code for maternity report
CODENOR   INIT      "NOR"                      * code for normal report
CODETHE   INIT      "THE"                      * code for theatre report
DASH4     INIT      "----"                     * for lines under headings
DASH6     INIT      "------"                   * for lines under headings
DASH7     INIT      "-------"                  * for lines under headings
DASH20    INIT      "--------------------"     * for lines under headings
DESCNORM  INIT      "Normal Bookings   "       * for headings
DESCMAT   INIT      "Maternity Bookings"       * for headings
DESCTHE   INIT      "Theatre Bookings  "       * for headings
.
OPTDES1   INIT      "Booked                  " * normal, maternity, theatre
OPTDES2   INIT      "Pre-Admissions          " * normal, maternity, theatre
OPTDES3   INIT      "Deliveries              " * maternity
OPTDES4   INIT      "Un-Delivered            " * maternity
OPTDES5   INIT      "Booking Cancellations   " * normal, maternity, theatre
OPTDES6   INIT      "Pre-Admission Cancell'ns" * normal, maternity, theatre
OPTDES7   INIT      "Discharges              " * normal, theatre
OPTDES8   INIT      "Deposits Paid           " * maternity
.
OPTD1     INIT      "Booked           "        * as above but shorter length
OPTD2     INIT      "Pre-Admissions   "
OPTD3     INIT      "Deliveries       "
OPTD4     INIT      "Un-Delivered     "
OPTD5     INIT      "Booking Cancell'n"
OPTD6     INIT      "Pre-Adm Cancell'n"
OPTD7     INIT      "Discharges       "
OPTD8     INIT      "Deposits Paid    "
.
PRGID     INIT      "IBABOK71"
PRGNAM    INIT      "Booking Monthly Statistics"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  MAINLINE                                         *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000                * initialize & open files
.
ML1000    CALL      KOPT0000                * enter the options
          BRANCH    EXIT,ML9999             * exit chosen
.
ML2000    CALL      KPER0000                * enter the period
.
ML3000    CALL      CONTQST                 * OK to continue
          BRANCH    CEXIT,ML4000,ML2000,ML1000
.                         Yes    No     Cancel
.
ML4000    CALL      REPT0000                * produce the report
          GOTO      ML1000                  * re-enter options
.
ML9999    CHAIN     PGM 
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening bokstaaf";
          OPEN      BOKBSTA1,"bokstaaf"     * booking stats file
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"     * codes file
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA3,"patdrgaf"     * date range file
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,FIFTY9;*27,CPERMTH
INIT9999  RETURN
+
.*********************************************************************
.*                  KOPT0000                    Called by : ML1000   *
.*        Enter the search option                                    *
.*        Returns : MOPTION       choice                             *
.*                  EXIT = 1      exit chosen                        *
.*********************************************************************
KOPT0000  HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Normal":
                    *P1:6,*V2LON,TWO,*HOFF," = Maternity":
                    *P1:7,*V2LON,THREE,*HOFF," = Theatre":
                    *P1:9,"Please Select : _"
.
KOPT1000  KEYIN     *P17:9,*DV,UNDLN1:
                    *P17:9,*V2LON,MOPTION:
                    *P17:9,*DV,MOPTION
.
          COMPARE   ZERO TO MOPTION
          GOTO      KOPT9000 IF EQUAL
.
          BRANCH    MOPTION,KOPT2000,KOPT2000,KOPT2000
.
          BEEP                              * invalid selection
          GOTO      KOPT1000
.
KOPT2000  LOAD      CODEXXX,MOPTION,CODENOR,CODEMAT,CODETHE * thscod value
          LOAD      DESCOPT,MOPTION,DESCNORM,DESCMAT,DESCTHE * display
          LOAD      CPHDROPT,MOPTION,DESCNORM,DESCMAT,DESCTHE * report subhead
          STRIP     DESCOPT
          DISPLAY   *P53:2,*V2LON,"- ",*+,DESCOPT,SP1   * print sub-heading
          MOVE      FALSE,EXIT
          GOTO      KOPT9999
.
KOPT9000  MOVE      TRUE,EXIT               * exit chosen
.
KOPT9999  RETURN
+
.*********************************************************************
.*                  KPER0000                    Called by : ML2000   *
.*        Enter the periods of the report                            *
.*********************************************************************
.
KPER0000  DISPLAY   *P1:16,*EF,"From  Date  :":
                    *P1:18,*EL,"To    Date  :"
.
. ------- the from period ------
.
          UNPACK    SP4,CYEAR,CMON
          MOVE      CCC,CCENT
          MOVE      TEN5,CCOL
          MOVE      TEN6,CVERT
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYPER                  * keyin the period
          BRANCH    OVRCD,KPER9500
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3                 * is it a valid range ??
          BRANCH    OVRCD,KPER7100          * no
.
. ------- display the periods start & finish date -------
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATEA           * set up from date
          REP       " 0",DATEA
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATEB           * set up to date
          REP       " 0",DATEB
.
          DISPLAY   *P27:16,DRGMDSC:
                    *P27:17,DATEA," to ",DATEB
.
          PACK      STDATE,DRGYR,DRGNUM     * format the start period ccyymm
          REP       " 0",STDATE             * zero fill
          MOVE      DRGCYR,STYY              * set up start year
          MOVE      DRGCNUM,STMM             * set up start period
.
. ------- the to period ------
.
KPER2000  UNPACK    SP4,CYEAR,CMON
          MOVE      CCC,CCENT
          MOVE      TEN5,CCOL
          MOVE      TEN8,CVERT
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYPER                  * keyin the period
          BRANCH    OVRCD,KPER0000
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,KPER7100
.
. ------- display the periods start & finish date -------
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATEC           * set up from date
          REP       " 0",DATEC
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATED           * set up to date
          REP       " 0",DATED
.
          DISPLAY   *P27:18,DRGMDSC:
                    *P27:19,DATEC," to ",DATED
.
          PACK      ENDDATE,DRGYR,DRGNUM    * format the ending period ccyymm
          REP       " 0",ENDDATE            * zero fill
          MOVE      DRGCYR,ENDYY             * set up end year ccyy
          MOVE      DRGYR,FORM4             * set up end year as a form4
          MOVE      FORM4,IYEAR             * set up end year yy as a form2
          MOVE      DRGCNUM,ENDMM            * set up end period
.
. ------- validate the dates -------
.
          MATCH     STDATE,ENDDATE          * is end date before to date ??
          GOTO      KPER7000 IF LESS        * yes so invalid
.
.         A valid period range has been input, so we need to make sure
.         that the range covers no more than 13 periods
.
          MOVE      ZERO,COUNTR
          WHILE     COUNTR < 13
            ADD       ONE,COUNTR
            STORE     SP2,COUNTR,Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,Y10,Y11,Y12,Y13
            STORE     SP2,COUNTR,M1,M2,M3,M4,M5,M6,M7,M8,M9,M10,M11,M12,M13
          DO
.
          PACK      KEY6,STYY,STMM
          CALL      RDDRGA3                      * position on first period
          BRANCH    OVRCD,KPER7200
          MOVE      ZERO,COUNTR                  * initilaise counter
          GOTO      KPER6050
.
KPER6000  CALL      RDKDRGA3                     * get next period record
          BRANCH    OVRCD,KPER6500               * eof - no more periods
.
KPER6050  ADD       ONE,COUNTR                   * increment counter
.
          UNPACK    DRGYR,XCC,Y0
          STORE     Y0,COUNTR,Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,Y10,Y11,Y12,Y13
          STORE     DRGCNUM,COUNTR,M1,M2,M3,M4,M5,M6,M7,M8,M9,M10,M11,M12,M13
          PACK      XYYMM,DRGYR,DRGNUM
          STORE     XYYMM,COUNTR,YYMM1,YYMM2,YYMM3,YYMM4,YYMM5,YYMM6,YYMM7:
                                 YYMM8,YYMM9,YYMM10,YYMM11,YYMM12,YYMM13
.
          MATCH     DRGCYR,ENDYY                 * end year ?
          GOTO      KPER6000 IF NOT EQUAL        * no - get next period
.
          MATCH     DRGCNUM,ENDMM                * end period ?
          GOTO      KPER6000 IF NOT EQUAL        * no - get next period
.
KPER6500  IF        COUNTR > 13
            DISPLAY   *P1:24,*EL,*B,"Must be less than 14 Periods in range.  ";
            CALL      HITENTER
            GOTO      KPER0000
          ENDIF
.
          GOTO      KPER9000
.
. ------- ERROR: to date before from date -------
.
KPER7000  DISPLAY   *P1:24,*B,"TO date must be after the FROM date.  ",*EL;
          CALL      HITENTER
          GOTO      KPER0000
.
. ------- ERROR: period entered not on file -------
.
KPER7100  DISPLAY   *P1:24,*B,"** Invalid ",CPERMTH,"/Year **  ",*EL;
          CALL      HITENTER
          GOTO      KPER0000
.
KPER7200  DISPLAY   *P1:24,*EL,*B,CPERMTH," ",STMM,"/",STYY," not found.  ";
          CALL      HITENTER
          GOTO      KPER0000
.
KPER9000  MOVE      ZERO,EXIT
          GOTO      KPER9999
.
KPER9500  MOVE      ONE,EXIT
.
KPER9999  RETURN
+
.*********************************************************************
.*                  REPT0000                    Called by : ML4000   *
.*        Produce the report                                         *
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*BLINKON,*V2LON,"Please Wait.",*EL
.
          MOVE      ONE,LFLAG               * indicate tble head not printed
          MOVE      ZERO,CPAGENO            * init. page counter for report
          MOVE      "60",CLNO               * set page line counter
.
.         CALL      CLRV0000                * clear the month & year variables
.         CALL      SETA0000                * set each range value
          MOVE      ONE,COPTION             * start at the first option
.
. ------- start the processing -------
. ------- for each value or OPTDES, reads through the booking stats file ------
. ------- and loads the required values from the record using COPTION    ------
. ------- in the meantime, have to check on each booking type and when   ------
. ------- have a new one, print the details about the previous one       ------
. ------- at the end of the stats file, prints details about OPTDES      ------
. ------- gets the next OPTDES and does it all again until no more OPTDES------
.
. ---------------------------------------
. ------- get the next sub-option -------
. ---------------------------------------
.
REPT1000  CALL      SETB0000                * set up progressive value of OPTDES
.
          DISPLAY   *P1:24,*EL,"Printing Statistics for :",OPTDES:
                    *P60:24,"Type :";
.
          MOVE      SP6,SVTYPE              * clear save booking type
          MOVE      SP9,KEY9                * set up for initial read
          CALL      RDSBSTA1                * initial read on stats file
.
. ----------------------------------------------
. ------- next read on booking stats file ------
. ----------------------------------------------
.
REPT2000  CALL      RDKBSTA1                * read next record
          BRANCH    OVRCD,REPT6500          * end of file
.
          DISPLAY   *P67:24,BSTYPE,SP3,BSDATE; * display to show working
.
          PACK      KEY5,CODEBK,BSTYPE      * key of booking type
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,REPT2000          * invalid booking type
.
          MATCH     CODEXXX,THCSCOD         * is it of the correct type
          GOTO      REPT2000 IF NOT EQUAL   * no, so read next record
.
. ------- check that file date is in the required ranges -------
.
          MATCH     STDATE,BSDATE           * is file date before start date ??
          GOTO      REPT2000 IF LESS        * yes, so ignore
.
          MATCH     BSDATE,ENDDATE          * is file date after end date ??
          GOTO      REPT2000 IF LESS        * yes, so ignore
.
. ------- have a valid booking statistic record ------
.
          MATCH     SVTYPE,BSTYPE           * is it a diff. booking type ??
          CALL      NEWB0000 IF NOT EQUAL   * yes, so do updates
.
          UNPACK    BSDATE,DIM4,XMON        * unpack the booking date
          MOVE      XMON,IMON               * turn current period into a form
.
. ------- load the appropriate value from the file    -------
. ------- ie for booking type BSTYPE in period BSDATE -------
. -------    obtain the (eg) # of cancelled bookings  -------
.
          BRANCH    MOPTION OF REPT4000,REPT4500,REPT4000
.
. ------- for normal and theatre report ------
.
REPT4000  LOAD      FORM6,COPTION,BSBOOKS,BSADMTS,BSCANCB,BSCANCP,BSDSCHS
          GOTO      REPT6000
.
. ------- for maternity report -------
.
REPT4500  COMPARE   THREE,COPTION           * is it the deliveries sub-option ?
          GOTO      REPT5000 IF EQUAL       * yes, use diff values
.
          LOAD      FORM6,COPTION,BSBOOKS,BSDEPOS,BSDELVY,BSUNDLV,BSCANCB:
                    BSADMTS,BSCANCP,BSDSCHS,BSDELVY
          GOTO      REPT6000
.
. ------- delivered is total booked in a month (no delivered) -------
. ------- #delivered = #booked - #undel - #cancelled bookings -------
.
REPT5000  MOVE      BSBOOKS,FORM6           * number of bookings
          SUB       BSUNDLV,FORM6           * minus # of undelivered
          SUB       BSCANCB,FORM6           * minus # of bookings cancelled
          GOTO      REPT6000
.
. ------ store the particular amount in the app. range variable -------
.
REPT6000  MOVE      ZERO,XCOUNT                  * initialise counter
          WHILE     XCOUNT < COUNTR
            ADD       ONE,XCOUNT
            LOAD      XYYMM,XCOUNT,YYMM1,YYMM2,YYMM3,YYMM4,YYMM5,YYMM6,YYMM7:
                      YYMM8,YYMM9,YYMM10,YYMM11,YYMM12,YYMM13
.
.           If the same period is found, then load the new value into the
.           period count
.
            MATCH     XYYMM,BSDATE
            IF        @EQUAL
              LOAD      FORM6,XCOUNT,F1A,F2A,F3A,F4A,F5A,F6A,F7A,F8A:
                                     F9A,F10A,F11A,F12A,F13A
.             ADD       BCCANCL,FORM6             * increment period count
              STORE     FORM6,XCOUNT,F1A,F2A,F3A,F4A,F5A,F6A,F7A,F8A:
                                     F9A,F10A,F11A,F12A,F13A
              GOTO      REPT6100
            ENDIF
          DO
.
REPT6100  ADD       FORM6,FTOTAL            * add to the booking type total
          DISPLAY   *P67:24,*V2LON,BSTYPE;  * highlight to show done
          GOTO      REPT2000
.
. ------- at end of booking statistics file -------
. ------- for the subtype OPTDES            -------
.
REPT6500  CALL      NEWB0000                * print stats for book type
.
          COMPARE   ZERO,TTOTAL             * were there any values found ??
          GOTO      REPT8000 IF EQUAL       * no, try next section (OPTDES)
.
REPT7000  CALL      PTOT0000                * print the totals for the sub-opt
.
. ------- get the next sub-option -------
.
REPT8000  ADD       ONE,COPTION             * get the next sub-option
.
          BRANCH    MOPTION OF REPT9000,REPT8500,REPT9000
.
. ------- for maternity only, if pre-admission sub section, print new page -----
.
REPT8500  COMPARE   SIX,COPTION             * is it a pre-admission ??
          GOTO      REPT9000 IF NOT EQUAL   * no, so dont do new page
.
          MOVE      "60",CLNO               * make a new page be printed
.
REPT9000  COMPARE   COPTION,LIMIT           * are there any more options ??
          GOTO      REPT1000 IF NOT EQUAL   * yes there are
.
          COMPARE   ZERO,CPAGENO            * were any pages printed ??
          GOTO      REPT9995 IF EQUAL       * no
.
          PRINT     *N,*N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P15:24,*V2LON:
                    "*** Generation Completed ***";
          GOTO      REPT9999
.
. ------- no records found but at least print something -------
.
REPT9995  DISPLAY   *P1:24,*B,"*** No Bookings found ***",*EL,*W2;
          CALL      HEAD132                 * print a heading
          PRINT     *N,"No bookings in this period."
          PRINT     *N,"***  End of Report  ***"
.
REPT9999  RETURN
+
.*********************************************************************
.*                  CLRV0000                    Called by : REPT0000 *
.*        Clear the period and year variables                        *
.*********************************************************************
CLRV0000  MOVE      ONE,XCOUNT
.
CLRV1000  COMPARE   TEN3,XCOUNT
          GOTO      CLRV9999 IF EQUAL
.
          STORE     SP2 USING XCOUNT OF M1,M2,M3,M4,M5,M6,M7,M8,M9,M10,M11:
                                        M12,M13
          STORE     SP2 USING XCOUNT OF Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,Y10,Y11:
                                        Y12,Y13
          ADD       ONE,XCOUNT
          GOTO      CLRV1000
.
CLRV9999  RETURN
+
.*********************************************************************
.*                  SETA0000                    Called by : REPT0000 *
.*        Set up the period ranges for a year form start date        *
.*        (even though may not need them all)                        *
.*        Para's  : FORM4      starting year (form)                  *
.*                  XMM        starting period (dim)                 *
.*        Returns : COUNTR     the amount of months to print (max=13)*
.*********************************************************************
SETA0000  MOVE      ZERO,XCOUNT             * set counter to zero
          MOVE      STYY,FORM4              * set up start year as a form
          MOVE      FORM4,IYEAR             * year as a form 2
          MOVE      STMM,XMM                * set up start period
          REP       " 0",XMM                * zero fill
.
SETA1000  ADD       ONE,XCOUNT              * increment counter
.
          COMPARE   TEN4,XCOUNT             * have we finished yet ??
          GOTO      SETA9000 IF EQUAL       * yes
.
          MOVE      IYEAR,YMM               * set up the year
          REP       " 0",YMM                * zero fill
.
          STORE     XMM,XCOUNT,M1,M2,M3,M4,M5,M6,M7,M8,M9,M10,M11,M12,M13
          STORE     YMM,XCOUNT,Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,Y10,Y11,Y12,Y13
.
. ------- set the limit on how many to print ------
.
          MATCH     YMM,ENDYY2              * is the current year same as end ?
          GOTO      SETA8000 IF NOT EQUAL   * no, so not at end date.
.
          MATCH     XMM,ENDMM               * is it the end period
          GOTO      SETA8000 IF NOT EQUAL   * no, so not at end date
.
          MOVE      XCOUNT,COUNTR           * set up the limit for displays
.
SETA8000  CALL      PMTH0000                * increment period counter
          GOTO      SETA1000               
.
SETA9000  MOVE      "13",FORM2
          COMPARE   FORM2,COUNTR
          IF        @EQUAL
            MATCH     XMM,ENDMM
            IF        !@EQUAL
              DISPLAY   *P1:24,"Must be less than 14. ";
              CALL      HITENTER
            ENDIF
          ENDIF
SETA9999  RETURN
+
.*********************************************************************
.*                  SETB0000                    Called by : REPT0000 *
.*        loads the description for each sub-option to look at       *
.*********************************************************************
SETB0000  BRANCH    MOPTION OF SETB1000,SETB2000,SETB1000
.
. ------- for normal and theatre report -------
.
SETB1000  LOAD      OPTDES,COPTION,OPTDES1,OPTDES2,OPTDES5,OPTDES6,OPTDES7
          LOAD      OPT2,COPTION,OPTD1,OPTD2,OPTD5,OPTD6,OPTD7
          MOVE      SIX,LIMIT
          GOTO      SETB9999
.
. ------- for maternity report -------
.
SETB2000  LOAD      OPTDES,COPTION,OPTDES1,OPTDES8,OPTDES3,OPTDES4,OPTDES5:
                                   OPTDES2,OPTDES6,OPTDES7,OPTDES3
          LOAD      OPT2,COPTION,OPTD1,OPTD8,OPTD3,OPTD4,OPTD5,OPTD2:
                                 OPTD6,OPTD7,OPTD3
          MOVE      TEN,LIMIT
          GOTO      SETB9999
.
SETB9999  RETURN
+
.*********************************************************************
.*                  NEWB0000                    Called by : REPT0000 *
.*        Print the details when a change of booking type occurs     *
.*********************************************************************
NEWB0000  MATCH     SP3,SVTYPE              * is there a saved type ??
          GOTO      NEWB7000 IF EQUAL       * no, so set up initial values
.
          MOVE      "Unknown               ",TDESC
          PACK      KEY5 WITH CODEBK,SVTYPE * key of saved booking type
          CALL      RDCODE1                 * read the codes file
.
          COMPARE   ZERO,FTOTAL             * is there any stats for this type
          GOTO      NEWB7000 IF EQUAL       * no, so dont print
.
          COMPARE   "55",CLNO               * is a new table heading needed
          CALL      PTBH0000 IF NOT LESS    * yes
.
. ------- not sure what this is for yet !!!!!!!! -------
.
          COMPARE   LFLAG,ONE               *
          GOTO      NEWB1000 IF NOT EQUAL   *
.
          COMPARE   ZERO,CPAGENO            * is it the first page ??
          CALL      PTBH0000 IF NOT EQUAL   * print new table if not
.
NEWB1000  MOVE      ZERO,XCOUNT             * initialize counter
          PRINT     *1,SVTYPE,SP1,TDESC,SP1; * print the booking type
.
NEWB2000  ADD       ONE,XCOUNT              * increment counter
          LOAD      F0A,XCOUNT,F1A,F2A,F3A,F4A,F5A,F6A,F7A,F8A:
                               F9A,F10A,F11A,F12A,F13A
          PRINT     F0A,SP1;                 * print the ind. date range total
.
          COMPARE   COUNTR,XCOUNT           * have limit been printed
          GOTO      NEWB2000 IF NOT EQUAL   * no, print another record
.
          PRINT     SP1,FTOTAL              * print the total for the type
          ADD       ONE,CLNO                * increment line counter
.
. ------- add to the total counters -------
.
          ADD       F1A,T1
          ADD       F2A,T2
          ADD       F3A,T3
          ADD       F4A,T4
          ADD       F5A,T5
          ADD       F6A,T6
          ADD       F7A,T7
          ADD       F8A,T8
          ADD       F9A,T9
          ADD       F10A,T10
          ADD       F11A,T11
          ADD       F12A,T12
          ADD       F13A,T13
          ADD       FTOTAL,TTOTAL
.
. ------- either no save value or continuation -------
.
NEWB7000  MOVE      BSTYPE,SVTYPE           * set up new save values
          MOVE      ZERO,F1A                * clear the counters for each
          MOVE      ZERO,F2A                * booking type range & total
          MOVE      ZERO,F3A
          MOVE      ZERO,F4A
          MOVE      ZERO,F5A
          MOVE      ZERO,F6A
          MOVE      ZERO,F7A
          MOVE      ZERO,F8A
          MOVE      ZERO,F9A
          MOVE      ZERO,F10A
          MOVE      ZERO,F11A
          MOVE      ZERO,F12A
          MOVE      ZERO,F13A
          MOVE      ZERO,FTOTAL
.
NEWB9999  RETURN
+
.*********************************************************************
.*                  PUND0000                    Called by : lots     *
.*        Print 24 underlines the 13 lots of 6 underlines the 7      *
.*********************************************************************
PUND0000  PRINT     *1,DASH20,DASH4;        * 24 underlines
          MOVE      ZERO,XCOUNT             * initialize counter
.
PTBH2000  ADD       ONE,XCOUNT              * increment counter
          PRINT     SP1,DASH6;              * print underlines
.
          COMPARE   COUNTR,XCOUNT           * have limit been printed ??
          GOTO      PTBH2000 IF NOT EQUAL   * not yet
.
          PRINT     SP2,DASH7               * underline for Total
          ADD       ONE,CLNO                * increment line number
.
PUND9999  RETURN
+
.*********************************************************************
.*                  HEAD0000                    Called by :          *
.*        Prints a new page heading for the report                   *
.*********************************************************************
HEAD0000  CLOCK     TIME,CTIMEIS            * get current time
.
          MOVE      "BOOKING MONTHLY STATISTICS -",PRGNAM
          CALL      HEAD132                 * print basic heading
          MOVE      "BOOKING MONTHLY STATISTICS  ",PRGNAM
.
          PRINT     *40,"From : ",STMM,SLASH,STYY,SP5,DATEA," to ",DATEB
          PRINT     *40,"To   : ",ENDMM,SLASH,ENDYY,SP5,DATEC," to ",DATED,*N
.
          MOVE      SEVEN,CLNO              * next line is line 7
HEAD9999  RETURN
+
.*********************************************************************
.*                  PTBH0000                    Called by :          *
.*        Prints a new table heading                                 *
.*        ie when OPTDES changes, need to print this                 *
.*           if LFLAG = 0, means have already printed table heading  *
.*********************************************************************
PTBH0000  COMPARE   "55",CLNO               * is a new page needed ??
          CALL      HEAD0000 IF NOT LESS    * print page heading if so
.
          COMPARE   ONE,LFLAG               * if lflag = 0 means table head has 
          GOTO      PTBH9999 IF NOT EQUAL   * been printed for this book. type
.
. ------- print the first line, the period ranges -------
.
          MOVE      ZERO,XCOUNT             * initialize counter
          PRINT     *1,OPTDES,SP2;          * print which sub section
.
PTBH1000  ADD       ONE,XCOUNT
          LOAD      Y0,XCOUNT,Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,Y10,Y11,Y12,Y13
          LOAD      M0,XCOUNT,M1,M2,M3,M4,M5,M6,M7,M8,M9,M10,M11,M12,M13
          PRINT     M0,SLASH,Y0,SP2;        * print the range
.
          COMPARE   COUNTR,XCOUNT           * have limit been printed ??
          GOTO      PTBH1000 IF NOT EQUAL   * not yet
.
          PRINT     SP1,"Total"             * print total heading
          ADD       ONE,CLNO                * increment line counter
.
. ------- print the second line, the underlines -------
.
          CALL      PUND0000                * print the underlines
          MOVE      ZERO,LFLAG              * indicate table heading is printed
.
PTBH9999  RETURN
+
.*********************************************************************
.*                  PMTH0000                    Called by : SETA0000 *
.*        Increment the period counter                               *
.*        If a new year, reset period counter, increment year counter*
.*        Updates : IYEAR         the year                           *
.*                  XMM           the previous period                *
.*********************************************************************
PMTH0000  MOVE      XMM,IMON                * set up period as a form
          ADD       ONE,IMON                * increment period
          MOVE      IMON,XMM                * put back into dim
          REP       " 0",XMM                * zero fill
.
          COMPARE   TEN4,IMON               * is it a new year ??
          GOTO      PMTH9999 IF NOT EQUAL   * no
.
. ------- have changed to next year ------
.
          ADD       ONE,IYEAR               * increment year counter
          MOVE      ONE,IMON                * reset period counter
          MOVE      IMON,XMM                * turn to a dim
          REP       " 0",XMM                * zero fill
.
PMTH9999  RETURN
+
.*********************************************************************
.*                  PTOT0000                    Called by : REPT7000 *
.*        Print the total for the sub-option                         *
.*********************************************************************
PTOT0000  COMPARE   "55",CLNO                 * is a new page needed ??
          CALL      HEAD0000 IF NOT LESS      * yes
.
. ------- print the totals for the sub-option -------
.
          CALL      PUND0000                  * print the first underlines
          MOVE      ZERO,XCOUNT               * zero counter
          PRINT     *1,"Total ",OPT2,SP1;     * sub option description
.
PTOT1000  ADD       ONE,XCOUNT                * increment counter
          LOAD      T0,XCOUNT,T1,T2,T3,T4,T5,T6,T7,T8,T9,T10,T11,T12,T13
          PRINT     T0,SP1;                   * print book. type total
.
          COMPARE   COUNTR,XCOUNT             * have limit been printed
          GOTO      PTOT1000 IF NOT EQUAL     * not yet
.
          PRINT     SP1,TTOTAL                * print the totol for book. type
          ADD       ONE,CLNO                  * increment line number
.
          CALL      PUND0000                  * print the underlines
.
. ------- reset the total counters -------
.
          MOVE      ZERO,F1A                * clear the counters for each
          MOVE      ZERO,F2A                * booking type range & total
          MOVE      ZERO,F3A
          MOVE      ZERO,F4A
          MOVE      ZERO,F5A
          MOVE      ZERO,F6A
          MOVE      ZERO,F7A
          MOVE      ZERO,F8A
          MOVE      ZERO,F9A
          MOVE      ZERO,F10A
          MOVE      ZERO,F11A
          MOVE      ZERO,F12A
          MOVE      ZERO,F13A
          MOVE      ZERO,FTOTAL
.
          MOVE      ZERO,T1                 * clear the total counters
          MOVE      ZERO,T2
          MOVE      ZERO,T3
          MOVE      ZERO,T4
          MOVE      ZERO,T5
          MOVE      ZERO,T6
          MOVE      ZERO,T7
          MOVE      ZERO,T8
          MOVE      ZERO,T9
          MOVE      ZERO,T10
          MOVE      ZERO,T11
          MOVE      ZERO,T12
          MOVE      ZERO,T13
          MOVE      ZERO,TTOTAL
.
          MOVE      ONE,LFLAG               * indicate heading not printed
.
          PRINT     *N
          ADD       TWO,CLNO                * add two to line counter
.
PTOT9999  RETURN
+
.
. ------- I/O includes needed -------
.
          INC       STD001IO
          INC       BOKSTAIO/INC
          INC       PATCOMN3
          INC       PATDRGIO/INC
          INC       PATCODIO/INC
.
................................................................................

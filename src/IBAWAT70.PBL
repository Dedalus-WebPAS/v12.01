.******************************************************************************
.* System         : Waiting Lists                                             *
.* Program        : IBAWAT70.PBL                                              *
.* Name           : Waiting List Suspension                                   *
.******************************************************************************
.* Date           : 11/02/1993                                                *
.* Author         : Justin Coates                                             *
.* Function       : To allow suspension of unscheduled patient procedures.    *
.* Modifications  :                                                           *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.02.01 07/07/2011  Steve Armstrong  CAR 240722                   *
.*                  Added RDPMPX21 prior to calling PMIHEAD                   *
.******************************************************************************
.*        V9.02.01  13/02/2003  Lina Vo      SRF 22709                        *
.*                  Remove open of outpreaf - not used.                       *
.******************************************************************************
.*        V5.10.03  28/08/2002  Peter Vela   SRF 19540                        *
.*                  Recompiled for PATDINVS                                   *
.*                  21/08/2002  Guomin Fu    SRF 17469                        *
.*                  Recompiled for PATDINVS                                   *
.*        V5.10.02  14/08/2002  Mike Laming  SRF 17570                        *
.*                  Recompiled for Casemix port from V6.10 to V5.10           *
.*        V5.10.01  28/06/2002  Mike Laming  SRF 16830                        *
.*                  Recompiled for PATDINVS                                   *
.******************************************************************************
.*        V5.09.01  22/09/2001  Tonii SRF 13075                               *
.*                  Recompiled for PATDINVS                                   *
.******************************************************************************
.*        V5.08.01  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*        V5.07.B01 13/01/1999  Jim Stathopoulos                              * 
.*                  Fixed Global Compile Errors                               * 
.******************************************************************************
.*        V5.01.02  12/03/1993    Justin Coates                               *
.*                  highlight the display of selected procedure code/cnt/desc *
.*        V5.01.03  16/03/93   ROD                                            *
.*                  added security check on treatment record                  *
.*        V5.01.04  16/04/93   ROD                                            *
.*                  Update Suspended status (bug).  Allow > 3 suspensions.    *
.*        V5.01.05  22/04/93 DIG                                              *
.*                  Re-Compiled for changes to PATALERT.                      *
.*        V5.01.06  04/06/93  ROD                                             *
.*                  Keyin Date on List when re-activating a suspension        *
.*        V5.01.07  31/08/93  ROD                                             *
.*                  Recompile for NZHIS                                       *
.*        V5.01.08  13/01/1994  Stretch                                       *
.*                  Fixed global recompl. bugs                                *
.*        V5.01.09  17/03/1994  Paul Howells                                  *
.*                  Cannot suspend a planned patient.                         *
.*        V5.01.10  07/04/1994  Paul Howells       SRF440795                  *
.*                  Add a change suspension date option.                      *
.*                  Don't rearrange suspension count when deleting.           *
.*        V5.01.11  20/05/1994  Paul Howells       SRF440871                  *
.*                  Don't Allow Changing of date on list for delete option.   *
.*                  23/06/1994  Rob Leonard                                   *
.*                  Added includes etc to get to compile                      *
.*        V5.03.01  01/08/1995  Greg Horvat      SRF 610897                   *
.*                  Recompiled for PATDINVS                                   *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       BOKRC1FD/INC            * booking file
          INC       OUTPREFD/INC            * OP pre-attendance file
          INC       PATALRFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATDHEAD/INC
          INC       PATDINVS/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC            * admission file
          INC       PATNIDFD/INC
          INC       PATPR1FD/INC            * pre-admission file
          INC       PATGSRFD/INC            * surname file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       WATCONFD/INC
          INC       WATPROFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATSUSFD/INC
          INC       WATTR1FD/INC
.
.         program variables
.
CALLPOSN  FORM      1         * which line 24 prompt
CNTITEM   FORM      2         * item counter
CODE      DIM       2
CURRDATE  DIM       8         * current date
DATEFROM  DIM       8         * from date
DATETO    DIM       8         * to   date
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
SCNDGNAM  DIM       40
SAVFROM   DIM       8         * from date
SAVTO     DIM       8         * to   date
ITEMS     DIM       21[20]    * displayed procedures
NEWDOL    DIM       8        
NMPNUMB   DIM       70
PROCCNT   FORM      2         * procedure count
PROCCODE  DIM       9         * procedure code
PROCSCNT  FORM      2         * suspension count
SUSPREAS  DIM       3         * suspension reason
URNUMBER  DIM       8         * ur number
WTSPDTCK  DIM       8
WTSPSTOP  DIM       8
WTSPSTRT  DIM       8
.
.         program constants
.
CATWN     INIT      "WN"
MAXITEMS  FORM      "20"
STAT1     INIT      "Unscheduled  "
STAT2     INIT      "Scheduled    "
STAT3     INIT      "Pre-admitted "
STAT4     INIT      "Admitted     "
STAT5     INIT      "Discharged   "
STAT6     INIT      "Removed      "
STAT7     INIT      "Performed    "
STAT8     INIT      "Not Performed"
.
PRGID     INIT      "IBAWAT70"
PRGNAM    INIT      "Waiting List Suspensions"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * enter option
          BRANCH    EXIT,ML9999             * exit
.
          CALL      IDPP0000                * identify patient and procedure
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"opening ";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P58:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
          DISPLAY   *P58:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          DISPLAY   *P58:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf";
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P58:24,"patpraxf"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P58:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
.
          READ      CONTROLF,THIRTY7;*211,WTCNWLSC
          IF        WTCNWLSC=1
            DISPLAY   *P58:24,"watseaaf";
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P58:24,"watseiaf";
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
.
          READ      CONTROLF,FIFTY9;*231,PTCNRGNS
          OPEN      PATGSRN1,"patgsrnf"     * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ONE,CNEWDS
          MOVE      ONE,CDEFDTE
          MOVE      ONE,CCANLDTE
.
INIT9999  RETURN
+
.*********************************************************************
.         enter the option
.*********************************************************************
OPTN0000  HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF,*V2LON:
                    *P1:4,"0",*P1:5,"1",*P1:6,"2",*P1:7,"3",*HOFF:
                    *P2:4,". Exit":
                    *P2:5,". Suspend Procedure":
                    *P2:6,". Change Suspension Period":
                    *P2:7,". Delete Suspension":
                    *P1:9,"Select option : ";
OPTN1000  KEYIN     *P17:9,*V2LON,MOPTION
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL       * exit
          BRANCH    MOPTION,OPTN1500,OPTN2000,OPTN1800
          BEEP
          GOTO      OPTN1000
.
OPTN1500  DISPLAY   *P52:2,*V2LON,"- Suspend Procedure "
          GOTO      OPTN9000
.
OPTN1800  DISPLAY   *P52:2,*V2LON,"- Delete Suspension "
          GOTO      OPTN9000
.
OPTN2000  DISPLAY   *P52:2,*V2LON,"- Change Suspension Period "
.
OPTN9000  MOVE      ZERO,EXIT
.
OPTN9999  RETURN
+
.*********************************************************************
.         identify the patient & procedure
.*********************************************************************
IDPP0000  CALL      GURN0000                * get patient
          BRANCH    EXIT,IDPP9999           * exit
.
IDPP1000  PERFORM   MOPTION,GPRO0000,GSUS0000,GSUS0000    * get procedure code
          BRANCH    EXIT,IDPP0000
.
          DISPLAY   *P1:6,*EF:
                    *P5:7,"Procedure Code/Cnt : ",*V2LON,PROCCODE,"/",PROCCNT:
                    *HOFF,*P5:8,"Description        : ",*V2LON,WMDESC
.
          BRANCH    MOPTION,IDPP3000,IDPP5000,IDPP4000
.
IDPP3000  CALL      SUSP0000                * suspend a procedure
          GOTO      IDPP1000
.
IDPP4000  CALL      REAC0000                * Unsuspend a procedure
          GOTO      IDPP1000
.
IDPP5000  CALL      CHNG0000                * Change Suspension Period 
          GOTO      IDPP1000
.
IDPP9999  RETURN
+
.*********************************************************************
.         enter the suspension details
.*********************************************************************
SUSP0000  CALL      SCRB0000
.
          MOVE      SP8,SAVFROM
          MOVE      SP8,SAVTO
.
          CALL      FDAT0000                * enter from date
          BRANCH    EXIT,SUSP9999
.
          CALL      TDAT0000                * enter to date
          BRANCH    EXIT,SUSP9999
.
          CALL      REAS0000                * enter reason
          BRANCH    EXIT,SUSP9999           * exitchar
.
SUSP5000  CALL      MAINQST
          BRANCH    CCITEM,SUSP5100,SUSP5200,SUSP5300
.
          COMPARE   ZERO,CCITEM
          GOTO      SUSP8000 IF EQUAL       * post
          GOTO      SUSP9999 IF LESS        * cancel
.
          BEEP
          GOTO      SUSP5000
.
SUSP5100  CALL      FDAT0000                * enter from date
          BRANCH    EXIT,SUSP9999
.
SUSP5200  CALL      TDAT0000                * enter to date
          BRANCH    EXIT,SUSP9999
          GOTO      SUSP5000
.
SUSP5300  CALL      REAS0000                * enter reason
          BRANCH    EXIT,SUSP9999
          GOTO      SUSP5000
.
SUSP8000  CALL      POSU0000                * put on suspension
.
SUSP9999  RETURN
+
.*********************************************************************
.         get the U/R number
.*********************************************************************
GURN0000  DISPLAY   *P1:4,*EF,"U/R Number : "
          MOVE      "14",CCOL
          MOVE      "4",CVERT
.
          CALL      KURN
          BRANCH    EXIT,GURN9999           * nothing entered
          BRANCH    OVRCD,GURN9000          * invalid
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,GURN9000
.
          CALL      PMIHEAD
          MOVE      PURNO,URNUMBER          * set UR number
          MOVE      ZERO,EXIT
          GOTO      GURN9999
.
GURN9000  DISPLAY   *P1:24,*B,"Invalid U/R Number.  ",*EL;
          CALL      HITENTER
          GOTO      GURN0000
.
GURN9999  RETURN
+
.*********************************************************************
.*                  GPRO0000                    Called by : xxxx0000 *
.*        Display items from the wattr1af file                       *
.*********************************************************************
GPRO0000  CALL      CLRA0000                * clear storage variables
          MOVE      ONE,CPAGENO             * set page
          MOVE      ZERO,CNTITEM            * clear count
          DISPLAY   *P1:6,*EF,*V2LON,*ULON:
                    *P5:7,"  Code/Cnt  ":
                    *P18:7,"Description                   ":
                    *P50:7,"Status       ":
                    *P64:7,"Date on List";
          MOVE      "7",CVERT               * starting line
          PACK      KEY19,URNUMBER,SP20
          CALL      RDSTREA1
.
. ******* loop over file *******
.
GPRO1000  CALL      RDKTREA1
          BRANCH    OVRCD,GPRO5000          * end of file
.
          MATCH     URNUMBER,WMURNO
          GOTO      GPRO5000 IF NOT EQUAL   * diff. patient
.
          COMPARE   ONE,WMSTAT
          GOTO      GPRO1000 IF NOT EQUAL   * only unscheduled allowed
.
. check if user has security access to this record
.
          COMPARE   ONE,WTCNWLSC                  * using security?
          GOTO      GPRO2000 IF NOT EQUAL
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          COMPARE   ZERO,OVRCD                    * ALL Access
          GOTO      GPRO2000 IF EQUAL
          PACK      KEY13,PASSCODE,WMDOCTOR,WMUNIT
          CALL      RDWTSEI2
          BRANCH    OVRCD,GPRO1000                * no access
.
. ******* increment counters *******
.
GPRO2000  ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      GPRO4000 IF NOT LESS    * need a new page
          ADD       ONE,CNTITEM             * add to item counter
.
. ******* display data and store key *******
.
GPRO3000  UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          LOAD      KEY13,WMSTAT,STAT1,STAT2,STAT3,STAT4,STAT5,STAT6,STAT7,STAT8
.
          PACK      WTSPDTCK,CCC,CYY,CMM,CDD
          REP       " 0",WTSPDTCK
          CALL      WATONSUS
          IF        EXIT = ZERO
            MOVE      "Suspended",KEY13
          ENDIF
.
          SETLPTR   WMDESC,30
          DISPLAY   *P1:CVERT,*V2LON,CNTITEM,*HOFF,".":
                    *P5:CVERT,WMCODE,"/",WMCNT:
                    *P18:CVERT,*+,WMDESC:
                    *P50:CVERT,KEY13:
                    *P64:CVERT,CPCDATE
          PACK      ITEMS[CNTITEM],WMURNO,WMCODE,WMCNT,SP2
          GOTO      GPRO1000
.
. ******* new screen needed *******
.
GPRO4000  BRANCH    CPAGENO,GPRO4500
.
.         Middle pages : (N)ext & (P)revious
.
GPRO4100  MOVE      ONE,CALLPOSN            * set position
          CALL      KEYA0000
          BRANCH    EXIT,GPRO7000,GPRO0000,GPRO9100
.                        Next     First    Exit
          GOTO      GPRO6000
.
.         first page : (N)ext
.
GPRO4500  MOVE      TWO,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,GPRO7000,GPRO5900,GPRO9100
.                        Next     First    Exit
          GOTO      GPRO6000
.
. ******* no more data to display *******
.
GPRO5000  COMPARE   ZERO,CNTITEM
          GOTO      GPRO8900 IF EQUAL       * nothing on file
.
          BRANCH    CPAGENO,GPRO5500
.
.         last page : (P)revious
.
GPRO5100  MOVE      THREE,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,GPRO5900,GPRO0000,GPRO9100
.                        Next     First    Exit
          GOTO      GPRO6000
.
.         first page :
.
GPRO5500  MOVE      FOUR,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,GPRO5900,GPRO5900,GPRO9100
.                        Next     First    Exit
          GOTO      GPRO6000
.
GPRO5900  BEEP   
          BRANCH    CALLPOSN,GPRO4100,GPRO4500,GPRO5100,GPRO5500
.
. ******* item on screen selected *******
.
GPRO6000  MOVE      ITEMS[FORM2],KEY19
          CALL      RDTREA1
          BRANCH    OVRCD,GPRO5900
.
          MOVE      WMCODE,PROCCODE         * set code
          MOVE      WMCNT,PROCCNT           * set count
          MOVE      CURRDATE,DATEFROM       * set date from
          MOVE      CURRDATE,DATETO         * set date to
          MOVE      SP3,SUSPREAS            * clear reason
          MOVE      ZERO,EXIT
.
. cannot suspend a planned patient
.
          PACK      KEY5,ANSC,ANSC,WTWMEATY,SP5
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     ANSP,TCINDC3
            IF        @EQUAL
              DISPLAY   *P1:24,*B,*EL,"Cannot Suspend Planned Patients. ";
              CALL      HITENTER
              GOTO      GPRO5000
            ENDIF
          ENDIF
.
          GOTO      GPRO9999
.
. ******* set up for a new page *******
.
GPRO7000  CALL      CLRA0000                * clear storage variables
          ADD       ONE,CPAGENO             * add to page counter
          MOVE      ONE,CNTITEM             * reset item counter
          MOVE      "8",CVERT               * set row
          DISPLAY   *P1:CVERT,*EF           * clear screen
          GOTO      GPRO3000
.
GPRO8900  DISPLAY   *P1:24,*B,"No procedures found.  ",*EL;
          CALL      HITENTER
.
. ******* set exit flags *******
.
GPRO9100  MOVE      ONE,EXIT                * exit selected
.
GPRO9999  RETURN
+
.*********************************************************************
.                   GSUS0000
.         Display items from the watsusaf file
.*********************************************************************
GSUS0000  CALL      CLRA0000                * clear storage variables
          MOVE      ONE,CPAGENO             * set page
          MOVE      ZERO,CNTITEM            * clear count
          DISPLAY   *P1:6,*EF,*V2LON,*ULON:
                    *P5:7,"  Code/Cnt  ":
                    *P18:7,"Description                   ":
                    *P50:7,"From Date ":
                    *P61:7," To  Date ":
                    *P72:7,"Reason";
          MOVE      "7",CVERT               * starting line
          PACK      KEY21,URNUMBER,SP20
          CALL      RSWTSUS1
.
. ******* loop over file *******
.
GSUS1000  CALL      RKWTSUS1
          BRANCH    OVRCD,GSUS5000          * end of file
.
          MATCH     URNUMBER,WTSUURNO
          GOTO      GSUS5000 IF NOT EQUAL   * diff. patient
.
. ******* increment counters *******
.
          ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      GSUS4000 IF NOT LESS    * need a new page
          ADD       ONE,CNTITEM             * add to item counter
.
. ******* display data and store key *******
.
GSUS3000  MOVE      "Invalid Code",WPDESC
          MOVE      WTSUCODE,KEY9
          CALL      RDPROA1
.
          UNPACK    WTSUFDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          SETLPTR   WPDESC,30
          DISPLAY   *P1:CVERT,*V2LON,CNTITEM,*HOFF,".":
                    *P5:CVERT,WTSUCODE,"/",WTSUCNT:
                    *P18:CVERT,*+,WPDESC:
                    *P50:CVERT,CPCDATE;
          UNPACK    WTSUTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P61:CVERT,CPCDATE,*P72:CVERT,WTSUREAS
          PACK      ITEMS[CNTITEM],WTSUURNO,WTSUCODE,WTSUCNT,WTSUSCNT
          GOTO      GSUS1000
.
. ******* new screen needed *******
.
GSUS4000  BRANCH    CPAGENO,GSUS4500
.
.         Middle pages : (N)ext & (P)revious
.
GSUS4100  MOVE      ONE,CALLPOSN            * set position
          CALL      KEYA0000
          BRANCH    EXIT,GSUS7000,GSUS0000,GSUS9100
.                        Next     First    Exit
          GOTO      GSUS6000
.
.         first page : (N)ext
.
GSUS4500  MOVE      TWO,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,GSUS7000,GSUS5900,GSUS9100
.                        Next     First    Exit
          GOTO      GSUS6000
.
. ******* no more data to display *******
.
GSUS5000  COMPARE   ZERO,CNTITEM
          GOTO      GSUS8900 IF EQUAL       * nothing on file
.
          BRANCH    CPAGENO,GSUS5500
.
.         last page : (P)revious
.
GSUS5100  MOVE      THREE,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,GSUS5900,GSUS0000,GSUS9100
.                        Next     First    Exit
          GOTO      GSUS6000
.
.         first page :
.
GSUS5500  MOVE      FOUR,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,GSUS5900,GSUS5900,GSUS9100
.                        Next     First    Exit
          GOTO      GSUS6000
.
GSUS5900  BEEP   
          BRANCH    CALLPOSN,GSUS4100,GSUS4500,GSUS5100,GSUS5500
.
. ******* item on screen selected *******
.
GSUS6000  MOVE      ITEMS[FORM2],KEY21
          CALL      RDWTSUS1
          BRANCH    OVRCD,GSUS5900
.
          MOVE      WTSUCODE,PROCCODE       * set code
          MOVE      WTSUCNT,PROCCNT         * set count
          MOVE      WTSUSCNT,PROCSCNT       * set suspension count
          MOVE      WTSUFDAT,DATEFROM       * set date from
          MOVE      WTSUTDAT,DATETO         * set date to
          MOVE      WTSUREAS,SUSPREAS       * set reason
.
          MOVE      "Invalid Code",WPDESC
          MOVE      WTSUCODE,KEY9
          CALL      RDPROA1
          MOVE      WPDESC,WMDESC
.
          MOVE      "Invalid Code",TDESC
          PACK      KEY5,CATWN,SUSPREAS
          CALL      RDCODE1
.
          MOVE      ZERO,EXIT
          GOTO      GSUS9999
.
. ******* set up for a new page *******
.
GSUS7000  CALL      CLRA0000                * clear storage variables
          ADD       ONE,CPAGENO             * add to page counter
          MOVE      ONE,CNTITEM             * reset item counter
          MOVE      "8",CVERT               * set row
          DISPLAY   *P1:CVERT,*EF           * clear screen
          GOTO      GSUS3000
.
GSUS8900  DISPLAY   *P1:24,*B,"No Suspended Procedures to Delete.  ",*EL;
          CALL      HITENTER
.
. ******* set exit flags *******
.
GSUS9100  MOVE      ONE,EXIT                * exit selected
.
GSUS9999  RETURN
+
.*********************************************************************
.         put the procedure on suspension
.*********************************************************************
POSU0000  MOVE      "zz",KEY2
          MOVE      ZERO,FORM2
          PACK      KEY21,URNUMBER,PROCCODE,PROCCNT,KEY2
          CALL      RSWTSUS1
.
          CALL      RPWTSUS1
          BRANCH    OVRCD,POSU2000
          MATCH     URNUMBER,WTSUURNO
          GOTO      POSU2000 IF NOT EQUAL
          MATCH     PROCCODE,WTSUCODE
          GOTO      POSU2000 IF NOT EQUAL
          COMPARE   PROCCNT,WTSUCNT
          GOTO      POSU2000 IF NOT EQUAL
.
          MOVE      WTSUSCNT,FORM2
.
POSU2000  ADD       ONE,FORM2
.
.         suspend the procedure
.
POSU5000  MOVE      URNUMBER,WTSUURNO
          MOVE      PROCCODE,WTSUCODE
          MOVE      PROCCNT,WTSUCNT
          MOVE      FORM2,WTSUSCNT
.
          MOVE      DATEFROM,WTSUFDAT
          MOVE      DATETO,WTSUTDAT
          MOVE      SUSPREAS,WTSUREAS
          MOVE      SP20,WTSUSPAR
.
          PACK      KEY21,WTSUURNO,WTSUCODE,WTSUCNT,WTSUSCNT
          CALL      WRWTSUS1
.
POSU9999  RETURN
+
.*********************************************************************
.         Unsuspend the procedure
.*********************************************************************
REAC0000  CALL      SCRB0000                * display screen
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P26:10,*V2LON,CPCDATE
          UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P26:11,*V2LON,CPCDATE
          DISPLAY   *P26:12,*V2LON,SUSPREAS,*HOFF,SP3,TDESC
.
          CALL      DTOL0000                      * input date on list
.
          DISPLAY   *P1:24,*EL,"OK to Delete (",*V2LON,ANSY,*HOFF,"/":
                    *V2LON,ANSN,*HOFF,") ? ";
.
REAC1000  KEYIN     *P27:24,*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     ANSN,ANS
          GOTO      REAC9999 IF EQUAL
          MATCH     ANSY,ANS
          GOTO      REAC5000 IF EQUAL
          BEEP
          GOTO      REAC1000
.
.         Unsuspend the procedure
.
REAC5000  PACK      KEY21,URNUMBER,PROCCODE,PROCCNT,PROCSCNT
          CALL      DEWTSUS1
.
. update date on list
.
..          MATCH     SP8,NEWDOL                    * new date on list?
..          IF        !@EQUAL
..            MOVE      NEWDOL,WMDATE1
..            CALL      UPTREA1
..          ENDIF
.
. now have to re-adjust the current numbers
.
...          MOVE      ZERO,FORM2
...REAC6000  PACK      KEY21,URNUMBER,PROCCODE,PROCCNT,FORM2
...          CALL      RSWTSUS1
...          CALL      RKWTSUS1
...          BRANCH    OVRCD,REAC9999
...          PACK      KEY19,WTSUURNO,WTSUCODE,WTSUCNT,SP2
...          MATCH     KEY19,KEY21
...          GOTO      REAC9999 IF NOT EQUAL
.         
...          ADD       ONE,FORM2
...          MOVE      FORM2,WTSUSCNT
...          CALL      UPWTSUS1
...          GOTO      REAC6000
.
REAC9999  RETURN
+
.*********************************************************************
.         Change Suspension Period 
.*********************************************************************
CHNG0000  CALL      SCRB0000                * display screen
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P26:10,*V2LON,CPCDATE
          UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P26:11,*V2LON,CPCDATE
          DISPLAY   *P26:12,*V2LON,SUSPREAS,*HOFF,SP3,TDESC
.
          MOVE      DATEFROM,SAVFROM
          MOVE      DATETO,SAVTO
.
CHNG0500  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      CHNG7000 IF EQUAL
          GOTO      CHNG9999 IF LESS
.
          BRANCH    CCITEM,CHNG1000,CHNG2000,CHNG3000
          BEEP
          GOTO      CHNG0500
.
. keyin from date
.
CHNG1000  CALL      FDAT0000 
          BRANCH    EXIT,CHNG9999
.
          MATCH     DATEFROM,DATETO
          GOTO      CHNG0500 IF NOT LESS    * >= from date
.
          DISPLAY   *P1:24,*B,"From Date can't be before To date.  ",*EL;
          CALL      HITENTER
          GOTO      CHNG1000
.
. keyin to date
.
CHNG2000  CALL      TDAT0000
          BRANCH    EXIT,CHNG9999
          GOTO      CHNG0500
.
. keyin reason
.
CHNG3000  CALL      REAS0000
          BRANCH    EXIT,CHNG9999
          GOTO      CHNG0500
..
.
CHNG7000  MOVE      URNUMBER,WTSUURNO 
          MOVE      PROCCODE,WTSUCODE       * set code
          MOVE      PROCCNT,WTSUCNT         * set count
          MOVE      PROCSCNT,WTSUSCNT       * set suspension count
          MOVE      DATEFROM,WTSUFDAT
          MOVE      DATETO,WTSUTDAT
          MOVE      SUSPREAS,WTSUREAS 
          PACK      KEY21,WTSUURNO,WTSUCODE,WTSUCNT,WTSUSCNT
          CALL      RAWTSUS1
          BRANCH    OVRCD,CHNG9999
.         
          CALL      UPWTSUS1
          GOTO      CHNG9999
.
.
CHNG9999  RETURN
+
.*********************************************************************
.*  DTOL0000  :  DISPLAY Date on List                                  *
.*********************************************************************
DTOL0000  DISPLAY   *P2:13,*EL,*V2LON,FOUR,*HOFF,". Date on List       : ";
.
          PACK      KEY19,URNUMBER,PROCCODE,PROCCNT
          CALL      RDTREA1
          BRANCH    OVRCD,DTOL9999
.
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P26:13,*V2LON,CPCDATE
.
...          MOVE      "16",CCOL
...          MOVE      "14",CVERT
...          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
...          CALL      KEYDATE
...          BRANCH    CQUEST,DTOL0000
...          BRANCH    OVRCD,DTOL0000
.
...          PACK      NEWDOL,CCENT,CYEAR,CMON,CDAY        * new date on list
...          REP       " 0",NEWDOL
.
DTOL9999  RETURN
+
.*********************************************************************
.         display the input screen
.*********************************************************************
SCRB0000  DISPLAY   *P1:9,*EF,*V2LON:
                    *P1:10," 1",*P1:11," 2",*P1:12," 3",*HOFF:
                    *P3:10,". From Date          : ":
                    *P3:11,". To   Date          : ":
                    *P3:12,". Reason             : "
SCRB9999  RETURN
+
.*********************************************************************
.         enter the from date - DATEFROM
.*********************************************************************
FDAT0000  MOVE      "26",CCOL
          MOVE      "10",CVERT 
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,FDAT0000
          BRANCH    OVRCD,FDAT9100
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATEFROM
.
          MATCH     WMDATE1,DATEFROM
          GOTO      FDAT7000 IF NOT LESS    * >= date on list date
.
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:24,*B,"Date must be on or after ",*V2LON,CPCDATE,*HOFF:
                    ".  ",*EL;
          CALL      HITENTER
          MOVE      CURRDATE,DATEFROM
          GOTO      FDAT0000
.
. check that date doesn't clash with a previous suspended range
.
FDAT7000  PACK      KEY21,URNUMBER,PROCCODE,PROCCNT,SP20
          CALL      RSWTSUS1
FDAT7100  CALL      RKWTSUS1
          BRANCH    OVRCD,FDAT9000
          MATCH     URNUMBER,WTSUURNO
          GOTO      FDAT9000 IF NOT EQUAL
          MATCH     PROCCODE,WTSUCODE
          GOTO      FDAT9000 IF NOT EQUAL
          COMPARE   PROCCNT,WTSUCNT
          GOTO      FDAT9000 IF NOT EQUAL
.
          MATCH     WTSUFDAT,DATEFROM
          IF        !@LESS
            MATCH     WTSUTDAT,DATEFROM
            IF        @LESS | @EQUAL
.
. make sure (if in change option) that it is not period we are changing 
.
              MATCH     WTSUFDAT,SAVFROM
              IF        @EQUAL
                MATCH     WTSUTDAT,SAVTO
                IF        @EQUAL
                  GOTO      FDAT7100
                ENDIF
              ENDIF
.
              GOTO      FDAT8900
            ENDIF
          ENDIF
          GOTO      FDAT7100
.
FDAT8900 
          UNPACK    WTSUFDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:24,*EL,*B,"Suspension range ",*V2LON,CPCDATE,*HOFF:
                    " - ";
          UNPACK    WTSUTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *V2LON,CPCDATE,*HOFF," exists.  ";
          CALL      HITENTER
          GOTO      FDAT0000
.
FDAT9000  MOVE      ZERO,EXIT                     * valid from date!
          GOTO      FDAT9999
.
FDAT9100  MOVE      ONE,EXIT
.
FDAT9999  RETURN
+
.*********************************************************************
.         enter the to date - DATETO
.*********************************************************************
TDAT0000  MOVE      "26",CCOL
          MOVE      "11",CVERT 
          UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          MATCH     SP8,DATETO
          IF        @EQUAL
            UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          ENDIF
          CALL      KEYDATE
          BRANCH    CQUEST,TDAT0000
          BRANCH    OVRCD,TDAT9100
.
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATETO
.
          MOVE      ZERO,EXIT
          MATCH     DATEFROM,DATETO
          GOTO      TDAT7000 IF NOT LESS    * >= from date
.
          DISPLAY   *P1:24,*B,"Date can't be before From date.  ",*EL;
          CALL      HITENTER
          GOTO      TDAT0000
.
. check that date doesn't clash with a previous suspended range
.
TDAT7000  PACK      KEY21,URNUMBER,PROCCODE,PROCCNT,SP20
          CALL      RSWTSUS1
TDAT7100  CALL      RKWTSUS1
          BRANCH    OVRCD,TDAT9000
          MATCH     URNUMBER,WTSUURNO
          GOTO      TDAT9000 IF NOT EQUAL
          MATCH     PROCCODE,WTSUCODE
          GOTO      TDAT9000 IF NOT EQUAL
          COMPARE   PROCCNT,WTSUCNT
          GOTO      TDAT9000 IF NOT EQUAL
.
          MATCH     WTSUFDAT,DATEFROM
          GOTO      TDAT7100 IF NOT LESS
.
          MATCH     WTSUFDAT,DATETO
          GOTO      TDAT9000 IF LESS
.
TDAT8900  
.
. make sure (if in change option) that it is not period we are changing 
.
          MATCH     WTSUFDAT,SAVFROM
          IF        @EQUAL
            MATCH     WTSUTDAT,SAVTO
            IF        @EQUAL
              GOTO      TDAT7100
            ENDIF
          ENDIF
.
          UNPACK    WTSUFDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:24,*EL,*B,"Date must be less than ",*V2LON,CPCDATE:
                    *HOFF,".  ";
          CALL      HITENTER
          GOTO      TDAT0000
.
TDAT9000  MOVE      ZERO,EXIT
          GOTO      TDAT9999
.
TDAT9100  MOVE      ONE,EXIT
.
TDAT9999  RETURN
+
.*********************************************************************
.         enter the reason - SUSPREAS
.*********************************************************************
REAS0000  MOVE      "26",ECOL
          MOVE      "12",EVERT
          MOVE      ONE,CKYIMAND
          MOVE      SUSPREAS,CKYIDEF3
          MOVE      CATWN,CODE
. 
          CALL      PATCODKY
          COMPARE   TWO,EXIT
          GOTO      REAS9500 IF EQUAL       * exitchar
.
          MOVE      ACODE,SUSPREAS
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,SP3,TDESC
          MOVE      ZERO,EXIT
          GOTO      REAS9999
.
REAS9500  MOVE      ONE,EXIT
.
REAS9999  RETURN
+
.*********************************************************************
.*        Keyin response for line 24 question                        *
.*********************************************************************
KEYA0000  DISPLAY   *P1:24,*EL,"Select item, (":
                    *V2LON,ANSE,*HOFF,")xit";
          MOVE      "19",CCOL
          IF        CALLPOSN = ONE | CALLPOSN = TWO
            DISPLAY   ", (",*V2LON,"N",*HOFF,")ext";
            ADD       "8",CCOL
          ENDIF
          IF        CALLPOSN = ONE | CALLPOSN = THREE
            DISPLAY   ", (",*V2LON,"F",*HOFF,")irst";
            ADD       "9",CCOL
          ENDIF
          DISPLAY   " ? ",*EL;
          ADD       FOUR,CCOL
.
KEYA1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      KEYA5000 IF EQUAL       * number entered
.
.         letter entered
.
          MATCH     SP1,DIM2
          GOTO      KEYA1500 IF NOT EQUAL   * invalid entry
.
          UNPACK    DIM2,ANS,ANS            * get required letter in ANS
          REP       UPPLOW,ANS              * get as uppercase
          REP       "N1F2E3",ANS            * turn valid letters into number
          MOVE      ZERO,EXIT               * clear exit flag
          MOVE      ANS,EXIT                * set exit flag
          BRANCH    EXIT,KEYA9999,KEYA9999,KEYA9999
.
KEYA1500  BEEP
          GOTO      KEYA1000
.
.         number entered
.
KEYA5000  MOVE      ZERO,EXIT               * set exit flag
          MOVE      DIM2,FORM2              * get as a number
.
          COMPARE   FORM2,ZERO
          GOTO      KEYA1500 IF NOT LESS    * negative entered
.
          COMPARE   FORM2,CNTITEM
          GOTO      KEYA1500 IF LESS        * number too large
.
KEYA9999  RETURN
+
.*********************************************************************
.*                  CLRA0000                    Called by :          *
.*        Clear the storage variables                                *
.*********************************************************************
CLRA0000  MOVE      ONE,FORM2
          WHILE     FORM2 <= MAXITEMS
            MOVE      SP30,ITEMS[FORM2]
            ADD       ONE,FORM2
          DO
CLRA9999  RETURN
+
REDISPS   DISPLAY   *P1:4,*EF,"U/R Number : "
GETSVAR   RETURN
+
          INC       STD001IO
          INC       NZCOMPIO/INC                 * IO Include for NZHIS
          INC       BOKRC1IO/INC            * booking file
          INC       CALCAGE/PBL
          INC       OUTPREIO/INC            * OP pre-attendance file
          INC       PATALERT
          INC       PATALRIO/INC
          INC       PATCOMN1/PBL            * holds KURN
          INC       PATCODKY
          INC       PATCODIO/INC
          INC       PATMA1IO/INC            * patient master file
          INC       PATMI1IO/INC            * admission file
          INC       PATNIDIO/INC
          INC       PATPR1IO/INC            * pre-admission file
          INC       PATGSRIO/INC            * surname file
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATSNDX/PBL             * ? for surnames only
          INC       PATSNX2/PBL             * ? for surnames/given names
          INC       PMIHEAD
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATSUSIO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WATONSUS
AGECHK
CLPATMAS  RETURN
..............................................................................

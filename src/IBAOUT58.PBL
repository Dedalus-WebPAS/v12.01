+
.**********************************************************************
.* System   :  OUTPATIENTS                                            *
.* Program  :  IBAOUT58                                               *
.* Desc     :  BOOKING CANCELLATION REPORT                            *
.**********************************************************************
.* Date     :  29/12/89                                               *
.* Author   :  D.Di.Paola                                             *
.* Comments :  Provide a report showing all bookings within a specific*
.*          :  date range that have been cancelled.....               *
.*          :                                                         *
.* Mods     :                                                         *
.*        V9.02.01  24/10/2001  Dean Cramer                           *
.*                  Fix heading error.                                *
.*        V5.08.01  29/08/2000  Caleb Sharman                         *
.*                  Changed Health Fund variables to be 8 chars       *
.*          V5.08.B02  14/03/2000  Steve Armstrong  5.08 Mods         *
.*                       Mods for changes to OUTCLIFD (effective date)*
.*            V5.08.B01  08/01/2000  Steve Armstrong                  *
.*                       Mods to use PATSTRYR to determine start of   *
.*                       financial year date.                         *
.**********************************************************************
.*            V5.07.01   11/03/1999  Jim Stathopoulos                 *
.*                       Increased DATFORM1 and DATFORM2              *
.*            V5.07.B02  18/01/1999  Nicole Harrington 507 rebound 095*
.*                       Mods to use HEAD132                          *
.*            V5.07.B01  02/11/1998  Jim Stathopoulos                 *
.*                       507 Changes                                  *
.**********************************************************************
.*             V5.01.02  27/04/90  Steve O'Gorman  SRF 104781         *
.*                       Fixed Program not to terminate with a record *
.*                       passed the end date range                    *
.*             V5.01.121 27/11/90 Glen Hobby                          *
.*                       Recompiled for changes to                    *
.*                       PATMX1FD                                     *
.*             V5.01.122 18/11/93 Rob Leonard   SRF 600613            *
.*                       Fixed report to print details only for       *
.*                       selected site (was printing clinics from     *
.*                       other sites)                                 * 
.*             V5.01.123 21/06/94 Rob Leonard   SRF 130106            *
.*                       Fix Cancellation date displaying session date*
.**********************************************************************
.
.$$$$$
. Booking Cancellation Report (IBAOUT58)
.__________________________________________
.
. - Initialization
.        - Display Header
.        - Open Files 
.               PATMA1A1
.               PATCODE1
.               PATPR1A1
.               PATDO1A1
.               CONTROLF
.               OUTCLIA1
.               OUTCANA3
.
.        - Clear Variables
.
. - Processing Options
.        - 0. Exit
.        - 1. Date Sequence
.
.        - Determine Report Headers By Option 1 = Date Sequence
.
. - Processing Loop  
.
.        - Input Start Date
.              - if spaces or e.o.s. entered, Default to variable in controlf
.
.        - Input End Date 
.              - if spaces or e.o.s. entered, Default to current date
.              - if OK, continue
.
.        - Extraction Loop
.              - Read OUTCANA3
.                     - check if record exists within date ranges
.                     - if OK, extract relevant cancellation details
.                
.              - Read OUTCLIA1
.                     - check if record exists
.                     - if OK, extract clinic description
.                else 
.              - Read PATDO1A1
.                     - check if record exists
.                     - if OK, extract doctor description
.
.              - Test for Zero U/R Number 
.                     - If Zero U/R Number.....
.              - Read PATMA1A1
.                     - check if record exists
.                     - if OK, extract patient details   
.
.                     - If Valid U/R Number......
.              - Read PATPR1A1
.                     - check if record exists
.                     - if OK, extract patient details   
.
.              - Read PATCODE1
.                     - check if record exists
.                     - if OK, extract reason for cancellation description
.
.              - Call Print Module
.                     - Print Booking Cancellation Report
.
.        - END
.
.$$$$$
.
          INC       STD001FD
.
          INC       OUTCLIFD/INC        * outpatients clinic file
          INC       OUTCANFD/INC        * outpatients cancelled bookings file
.
          INC       PATCODFD/INC        * patient codes file
          INC       PATCONFD/INC        * patients control file
          INC       PATDO1FD/INC        * patient doctor file
          INC       PATDRGFD/INC
          INC       PATMA1FD/INC        * patient master file
          INC       PATPR1FD/INC        * patient pre-admission file
.
.**********************************************************************
.*                         CONSTANTS                                  *
.**********************************************************************
.
CODECB    INIT      "CB"
FINISH    INIT      "Finish    "
HEADER1   INIT      "Date Range Sequence"
START     INIT      "Start     "
.
.**********************************************************************
.*                         GLOBAL VARIABLES                           *
.**********************************************************************
.
BEGINCNT  FORM      1
CANCDATE  DIM       10
CLOSDATE  DIM       10
CSTRTYR   DIM       8                            * for PATSTRYR
.
DATFORM1  FORM      8
DATFORM2  FORM      8
DIM20     DIM       20
DIM30     DIM       30
.
FROMDATE  DIM       8 
.
.
LINECNT   FORM      2
.
NEWKEY    DIM       19
OPENDATE  DIM       10
.
PAGENO    FORM      8
PATNAME   DIM       29
PATSTCUR  DIM       4                            * for PATSTRYR
SAVKEY    DIM       19
SESNDATE  DIM       10
SDAY      DIM       2
SMON      DIM       2
SYEAR     DIM       2
SCENT     DIM       2
.
TIMEIS    DIM       8
TODATE    DIM       8  
.
PRGID     INIT      "IBAOUT58"
PRGNAM    INIT      "Booking Cancellation Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                      controlling logic                             *
.**********************************************************************
ML0000    CALL      INIT0000                      * intialization
ML1000    CALL      CLR0000                       * clear input variables
          CALL      OPTN0000                      * option screen
          BRANCH    EXIT,ML9999                   * exit program
ML2000    CALL      DATE0000                      * Date Ranges 
          BRANCH    EXIT,ML1000
          CALL      CONT0000                      * ok to continue
          BRANCH    EXIT,ML2000,ML1000            * "1" = No, "2" = Cancel
          CALL      EXTR0000                      * extraction routine
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*                   call header routine and open files               *
.**********************************************************************
INIT0000  CALL      DISPHEAD                        * display header
.
          DISPLAY   *P64:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P72:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"

          DISPLAY   *P64:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes" 
.
          DISPLAY   *P64:24,"Opening patpr1af";
          OPEN      PATPR1A1,"patpr1af"
.
          DISPLAY   *P64:24,"Opening patdo1af";
          OPEN      PATDO1A1,"patdo1af" 
.
          DISPLAY   *P64:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"             
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P72:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILCANA3
          DISPLAY   *P72:24,CFNAME;
          OPEN      OUTCANA3,CFNAME
.
INIT9999    RETURN
+
.**********************************************************************
.*                           CLR0000                                  *
.*                       clear input variables                        *
.**********************************************************************
CLR0000   MOVE      ZERO,PAGENO
          MOVE      SP30,PATNAME
          UNPACK    SP30,SDAY,SMON,SYEAR,SCENT
          MOVE      SP20,DIM20
          MOVE      ZERO,OPTION
          UNPACK    SP30,CANCDATE,SESNDATE
          MOVE      SP20,SAVKEY
          MOVE      SP20,NEWKEY
          MOVE      SP10,CLOSDATE
          MOVE      SP10,OPENDATE
.
CLR9999   RETURN
.
.**********************************************************************
.*                           OPTN0000                                 *
.*                    select option screen                            *
.**********************************************************************
OPTN0000  MOVE      FALSE,EXIT
          MOVE      "80",LINECNT
.
          HLINE     *G37,2,54,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0 ",*HOFF,"= Exit":
                    *P1:5,*V2LON,"1 ",*HOFF,"= Date Range Sequence":
                    *P1:7,"Please Select : ";
.
OPTN1000  KEYIN     *P17:7,*V2LON,OPTION,*HOFF
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          COMPARE   "2",OPTION   
          GOTO      OPTN0000 IF NOT LESS
.
.-------- determine header -----
.
          DISPLAY   *P54:2,*V2LON,"- ",*+,HEADER1,SP1;
          GOTO      OPTN9999
. 
OPTN9000  MOVE      TRUE,EXIT                * set exit flag
.
OPTN9999  RETURN
+
.***************************************************************************
.*                                 CONT0000                                *
.*                           Ok To Continue    Y/N/C                       *
.***************************************************************************
CONT0000    MOVE       FALSE,EXIT
            CALL       CONTQST
            MATCH      "1",ANS                       * 1 = yes 
            GOTO       CONT9999 IF EQUAL
            MATCH      "2",ANS                       * 2 = no
            GOTO       CONT9000 IF EQUAL
            MATCH      "3",ANS                       * 3 = cancel
            GOTO       CONT8000 IF EQUAL
            GOTO       CONT0000
.
CONT8000    MOVE       TWO,EXIT                     * cancel flag
            GOTO       CONT9999
.
CONT9000    MOVE      TRUE,EXIT                     * no
.
CONT9999    DISPLAY   *P1:24,*EL;
            RETURN
+
.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000    MOVE       FALSE,EXIT
            PACK       OPENDATE,SP10                   * move sp date range
            PACK       CLOSDATE,SP10
            MOVE       "17",CCOL                       * set up keyin date
            MOVE       CCC,CCENT
            MOVE       "9",CVERT
            MOVE       "3",CDEFDTE
            MOVE       "45",ECOL
            MOVE       "9",EVERT
.
            DISPLAY    *P1:9,*EF,"Date Range    :":
                       *P27:9," TO ";
            MOVE       SP2,CDAY                        
            MOVE       SP2,CMON
            MOVE       SP2,CYEAR
.
            CALL       KEYDATE                        * call keyin date routine
            MATCH      SP2,CDAY
            GOTO       DATE0500 IF NOT EQUAL
.
.---- if spaces entered for start date then default to start of fin. year -----
.
            OPEN       PATDRGA2,"patdrgaf"
            CALL       PATSTRYR                  * get start of year date
.
            DISPLAY    *P17:9,*V2LON,"Start     ",*HOFF;
            UNPACK     CSTRTYR,CCENT,CYEAR,CMON,CDAY
            PACK       FROMDATE,CCENT,CYEAR,CMON,CDAY      * move to form field
            PACK       OPENDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    
            MOVE       FROMDATE,DATFORM1                   * used for comparison
            GOTO       DATE1000
.
DATE0500
            PACK       OPENDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    
            PACK       FROMDATE,CCENT,CYEAR,CMON,CDAY      * move to form field
            REPLACE    " 0",FROMDATE                       * replace spaces 
            MOVE       FROMDATE,DATFORM1                   * used for comparison
.
.---------- keyin second date range -------
.
DATE1000    MOVE       "32",CCOL                       * set up keyin date 
            MOVE       "9",CVERT                       * parameters
            MOVE       CCC,CCENT
            MOVE       "3",CDEFDTE
            MOVE       "9",EVERT
            MOVE       "45",ECOL
.
            MOVE       SP2,CDAY
            MOVE       SP2,CMON
            MOVE       SP2,CYEAR
.
            CALL       KEYDATE                         * call keyin date routine
            MATCH      SP2,CDAY
            GOTO       DATE5000 IF NOT EQUAL
.
            DISPLAY    *P32:9,*V2LON,"Finish    ",*HOFF;
            PACK       TODATE,CCC,CYY,CMM,CDD
            PACK       CLOSDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
            REPLACE    " 0",CLOSDATE                   * replace spaces
            MOVE       TODATE,DATFORM2
            GOTO       DATE9999
.
DATE5000
            PACK       CLOSDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
            PACK       TODATE,CCENT,CYEAR,CMON,CDAY    * move to form field
            REPLACE    " 0",CLOSDATE                   * replace spaces
            MOVE       TODATE,DATFORM2                 * for comparison  
.
            COMPARE    DATFORM1,DATFORM2               * compare 1st range to
            GOTO       DATE8000 IF LESS                * 2nd range. 1st date
            GOTO       DATE9999                        * must be less than 2nd 
.
DATE8000    DISPLAY    *P1:24,*B,"Invalid entry for date range - Hit ":
                       "<",*V2LON,"RETURN",*HOFF,">";
            KEYIN      ANS;                            * error message
            GOTO       DATE0000
DATE9000    MOVE       TRUE,EXIT
.
DATE9999    RETURN
+
.************************************************************************
.*                           EXTR0000                                   *
.*                     Location Extraction Routine                      *
.*            Read necessary files and Extract relevant information     * 
.************************************************************************
EXTR0000  MOVE      FALSE,BEGINCNT         * initialise No-more-records flag
.
.-------- using 3rd key -----
.
EXTR1000  PACK      KEY33,IDDD,SP30  
          CALL      RSOTCAN3               * position on cancelled booking file
.
.--------- Read Outpatients Cancelled Bookings File -----
.
EXTR2000  CALL       RKOTCAN3                     * read key sequential
          BRANCH     OVRCD,EXTR9000
.
.---------- validate date ranges ------------
.
          MATCH      FROMDATE,OPCNDATE             * test if 1st date 
          GOTO       EXTR2000 IF LESS              * is within range 
.
          MATCH      OPCNDATE,TODATE               * check if 2nd date    
          GOTO       EXTR2000 IF LESS              * is within range
.
          MATCH      OPCNSITE,IDDD                 * check if correct
          GOTO       EXTR2100 IF EQUAL             * site
          GOTO       EXTR2000
.
.---------- unpack dates and retrieve relevant data ------
.
EXTR2100  UNPACK     OPCNDATE,SCENT,SYEAR,SMON,SDAY
          PACK       SESNDATE,SDAY,SLASH,SMON,SLASH,SCENT,SYEAR
          PACK       NEWKEY,OPCNCLIN,OPCNDATE,OPCNSTRT   * save key
.
          DISPLAY    *P1:24,*P20:24,"Date : ",*V2LON,SESNDATE,*HOFF;
.
.--------- Extract other relevant details  -----
.
          PACK      KEY20,IDDD,OPCNCLIN,OPCNDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OPCNSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OPCNCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          BRANCH    OVRCD,EXTR2500 
.
          MATCH     SP6,OCADOCT               * check doctor codes for spaces
          GOTO      EXTR2500 IF EQUAL
.
.------- extract additional info ----
.
          PACK      KEY6,OCADOCT              * read doctors file for
          CALL      RDDOCT1                   * description 
          BRANCH    OVRCD,EXTR2500 
          CALL      FDOC0000                  * format doctor's name routine
          GOTO      EXTR2600
.
.------- if doctor not on file, then get clinic description ------
.
EXTR2500  MOVE      OCADESC,DIM20             * move clinic desc to print var.
.
.-------  test for zero u/r number -----
.
EXTR2600  MATCH     ZEROUR,OPCNURNO           * test for zero u/r 
          GOTO      EXTR2800 IF NOT EQUAL
.
.------- if zero u/r then get patient details from pre-admission masterfile ---
.
          MOVE      SP4,PTITL                 * clear variables
          MOVE      SP20,PSNAME
          MOVE      SP30,PGNAME
          MOVE      OPCNOUTN,KEY8
          CALL      RDPRAM1                   * read patient master file
          BRANCH    OVRCD,EXTR3000
          CALL      FNAM0000                  * format patient name routine
          GOTO      EXTR3000
.
.------- if valid U/R No. extract details from patient master file -----
.
EXTR2800  MOVE      SP4,PTITL                 * clear variables
          MOVE      SP20,PSNAME
          MOVE      SP30,PGNAME
          MOVE      OPCNURNO,KEY8
          CALL      RDMAST1                   * read patient master file
          BRANCH    OVRCD,EXTR3000
          CALL      FNAM0000                  * format patient name routine
.
EXTR3000  MOVE      SP10,CANCDATE             * clear cancellation date
          UNPACK    OPCNRDTE,XCENT,XYEAR,XMON,XDAY
          PACK      CANCDATE,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          MOVE      SP20,TDESC
          PACK      KEY5,CODECB,OPCNREAS
          CALL      RDCODE1
          BRANCH    OVRCD,EXTR4000
.
EXTR4000  MOVE      TRUE,BEGINCNT            * set no-more-records-flag
          CALL      PRNT0000
          GOTO      EXTR2000
.
EXTR9000  BRANCH    BEGINCNT,EXTR9500
          DISPLAY   *P1:24,*B,*EL,"Details Not On File - Hit ":
                               "<",*V2LON,"ENTER",*HOFF,"> to continue ";
          KEYIN     ANS;
          CALL      HEAD0000
.
.-------- report details completed, now print total lines -----
.
EXTR9500  CALL      LNPR0000                         * print final line
          CALL      EXPR0000
.  
EXTR9999  RETURN
+
.*************************************************************************
.*                             PRNT0000                                  *
.*                  print movement reason details                        *
.*************************************************************************
PRNT0000  CLOCK     TIME,TIMEIS
          COMPARE   "50",LINECNT
          CALL      HEAD0000 IF NOT LESS
.
.-------- check for different clinic ------
.
          MATCH     NEWKEY,SAVKEY                     * check for different
          GOTO      PRNT2000 IF EQUAL
.
.------- print full details -----
.
PRNT1000  PRINT     *N,"|",*3,OPCNCLIN,*10,DIM20,*31,"|":
                      *34,SESNDATE,*46,"|",*49,OPCNSTRT:
                      *56,"|",*57,OPCNURNO,*67,PATNAME:
                      *97,"|",*99,CANCDATE,*111,TDESC,*132,"|";
.
          GOTO      PRNT3000
.
.-------- if same clinic, session date and time then print partial details ----
.
PRNT2000  PRINT     *N,"|",*31,"|",*46,"|":
                      *56,"|",*57,OPCNURNO,*67,PATNAME: 
                      *97,"|",*99,CANCDATE,*111,TDESC,*132,"|";
.
PRNT3000  MOVE       NEWKEY,SAVKEY               * move to save variable
          ADD        "1",LINECNT
PRNT9999  RETURN
.
EXPR0000  PRINT      *N,*N,"**** END OF REPORT ****";
          RETURN
.
LNPR0000  PRINT      *N,"*----------------------------------------":
                          "-----------------------------------------":
                          "--------------------------------------":
                          "-----------*";
          RETURN
+
.**********************************************************************
.*                         HEAD0000                                   *
.*                      Print Headings                                *
.**********************************************************************
.
HEAD0000  COMPARE   ZERO,PAGENO
          CALL      PGE0000 IF NOT EQUAL
.
          ADD       ONE,PAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      HEADER1,CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
          PRINT     *N,*40,"Cancellations from ",OPENDATE," To ",CLOSDATE;
.
          CALL      PGE0000                        * print line
.
          PRINT     *N,*1,"|",*31,"|",*35,"Session",*46,"|",*49,"Start",*56,"|":
                    *97,"|",*104,"Cancellation",*132,"|":
                    *N,*1,"|",*3,"Clinic",*12,"Description",*31,"|",*37,"Date":
                    *46,"|",*49,"Time",*56,"|",*59,"U/R":
                    *67,"Patient's Name",*97,"|",*102,"Date",*113,"Reason":
                    *132,"|";
.
          CALL      PGE0000                        * print line
          MOVE      "8",LINECNT
          MOVE      EIGHT,LINECNT
HEAD9999  RETURN
+
.**********************************************************************
.*                         PGE0000                                    *
.*                        Print Line                                  *
.**********************************************************************
.
PGE0000   PRINT     *N,*1,"*--------------------------------------------------":
                    "---------------------------------------------------":
                    "-----------------------------*"; 
          ADD       ONE,LINECNT
          RETURN
+
.**********************************************************************
.*                           FDOC0000                                 *
.*                 Routine to format doctors  name                    *
.**********************************************************************
FDOC0000  PACK      DIM20,SP20
          MATCH     SP6,DTITL
          GOTO      FDOC1200 IF EQUAL
          MOVE      DTITL,DIM20                  * get title
          ENDSET    DIM20                        * reset pointers
.
.-------- get Givename -----
.
FDOC0500  CMATCH    SP1,DIM20                    * character match for spaces
          GOTO      FDOC1000 IF NOT EQUAL
          BUMP      DIM20,-1                     * bump back by 1
          GOTO      FDOC0500                     * loop 
.
FDOC1000  APPEND    " ",DIM20                    * if no more spaces append
FDOC1200  MATCH     SP30,DGNAME
          GOTO      FDOC2100 IF EQUAL
          APPEND    DGNAME,DIM20                 * given name to field
.
.-------- get Surname -----
.
FDOC1500  CMATCH    SP1,DIM20                    * character match for spaces
          GOTO      FDOC2000 IF NOT EQUAL
          BUMP      DIM20,-1                     * bump back by 1
          GOTO      FDOC1500                     * loop 
.
FDOC2000  APPEND    " ",DIM20                    * if no more spaces append
FDOC2100  MATCH     SP20,DSNAME
          GOTO      FDOC9999 IF EQUAL
          APPEND    DSNAME,DIM20                 * given name to field
.
FDOC9999  RETURN
+
.**********************************************************************
.*                           FNAM0000                                 *
.*                 Routine to format patients name                    *
.**********************************************************************
FNAM0000  PACK      PATNAME,SP30
          MATCH     SP6,PTITL
          GOTO      FNAM1200 IF EQUAL
          MOVE      PTITL,PATNAME                * get title
          ENDSET    PATNAME                      * reset pointers
.
.-------- get givename -----
.
FNAM0500  CMATCH    SP1,PATNAME                  * character match for spaces
          GOTO      FNAM1000 IF NOT EQUAL
          BUMP      PATNAME,-1                   * bump back by 1
          GOTO      FNAM0500                     * loop 
.
FNAM1000  APPEND    " ",PATNAME                  * if no more spaces append
FNAM1200  MATCH     SP30,PGNAME
          GOTO      FNAM2100 IF EQUAL
          APPEND    PGNAME,PATNAME               * given name to field
.
.-------- get Surname -----
.
FNAM1500  CMATCH    SP1,PATNAME                  * character match for spaces
          GOTO      FNAM2000 IF NOT EQUAL
          BUMP      PATNAME,-1                   * bump back by 1
          GOTO      FNAM1500                     * loop 
.
FNAM2000  APPEND    " ",PATNAME                  * if no more spaces append
FNAM2100  MATCH     SP20,PSNAME
          GOTO      FNAM9999 IF EQUAL
          APPEND    PSNAME,PATNAME               * given name to field
.
FNAM9999  RETURN
+
.
.-------- io subroutine includes ------
.
          INC       STD001IO
.
          INC       PATSTRYR
.
          INC       OUTCLIIO/INC
          INC       OUTCANIO/INC
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDRGIO/INC
          INC       PATMA1IO/INC
          INC       PATPR1IO/INC

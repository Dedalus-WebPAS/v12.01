.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAHMS28                                             *
.*                                                                            *
.*     FUNCTION:         HEALTH FUND PATIENTS REPORT                          *
.*                                                                            *
.*     DATE WRITTEN:     23/07/1992                                           *
.*                                                                            *
.*     AUTHOR:           Peter Eddey (based on IBAHOS06 by Karen Twin Peaks)  *
.*                                                                            *
.*     MODIFICATIONS:                                                         * 
.*                                                                            * 
.*        V12.00.01 30/05/2025  Don Nguyen      TSK 0955096                   *
.*                  Alphanumeric visit number changes                         *
.*        V10.04.01 12/06/2014 Steve Armstrong  CAR 301639                    *
.*                  Moved call to TFILENAM to start of program and added call *
.*                  to KILL0000 on exit of program.                           *
.******************************************************************************
.*        V10.03.01 19/12/2012 Patrick Adair  CAR 261630                      *
.*                  Removed port number from temp file name                   *
.******************************************************************************
.*        V10.02.01 20/05/2011 Saroeun Soeur CAR 243270                       *
.*                  remove mandatory hospital id and now can accept All hos   *
.******************************************************************************
.*        V9.09.00  15/01/2008 Jill Habasque CAR 145292                       *
.*                  Ported from V9.07 to V9.08                                *
.******************************************************************************
.*        V9.07.00  13/07/2007 Ian Farnell                                    *
.*                  Ported from V9.06 to V9.07                                *
.******************************************************************************
.*        V9.06.01  22/12/2006  Ramaprasad                                    *
.*                  Included Hospital Id                                      *
.*        V9.06.00  23/11/2006  Neelkamal Pyla                                *
.*                  Ported from V6 to V9                                      *
.******************************************************************************
.*        V6.06.01  16/09/1999  Steve Downing                                 *
.*                  Redundant code removed                                    *
.*        V6.05.03  26/11/1998  Jill Habasque                                 *
.*                  Fixed To Date to display century                          *
.*        V6.05.02  17/11/1998  Nicole Harrington                             *
.*                  Changed to use century in call to JULCON and test for new *
.*                  century                                                   *
.*        V6.05.01  29/10/1998  Nicole Harrington                             *
.*                  Changed to use DISPHEAD, HEAD132 and include century      *
.******************************************************************************
.*        V6.03.02  27/10/1995  Greg Horvat      SRF 612064 612205 612206     *
.*                  Recompiled for changes to NHOSPDAY                        *
.*        V6.03.01  11/10/1995  Greg Horvat      SRF 611811                   *
.*                  Changed to use CALCDAYS instead of CALC when calculating  *
.*                  the difference between two dates                          *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       PATHSPFD/INC
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.
.         TEMP FILES DECLARATION
.
PATWORK1  IFILE     SQL, FIXED=116, KEY="U1-42"
.
.Name      Type     Length     Physical     Start
.----      ----     ------     --------     -----
.ACLAIM    DIM         3           3           1
.AFUNDH    DIM         6           6           4
XPSNAME    DIM        17          17          10
XDAURNO    DIM         8           8          27
XDAADMNO   DIM         8           8          35
XPGNAME    DIM        23          23          43
.AFNDTB    DIM         8           8          66
WRKDAY     FORM        6           4          74
.ADATE     DIM         8           8          78
.DDATE     DIM         8           8          86
.DFMBS     DIM         9           9          94
.ATYPE     DIM         3           3         103
.ACARE     DIM         3           3         106
WARDBED    DIM         7           7         109 
.End of Record                               116
.
.  redefine form fields from key
.  -----------------------------
.Name      Type     Length     Start
.----      ----     ------     -----
.AURNO     FORM        8         27
.AADMNO    FORM        8         33
.
.--------------------------------------------------------
.         WORK VARIABLES
.
CALDAYS   FORM      6
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CLMBED    FORM      7
CLMPAT    FORM      8
CMDLIN1   DIM       26
CMDLIN2   DIM       16
CODE      DIM       2                  * required by PATCODDS
CUDATE    DIM       8
.
DIM14     DIM       14
.
EDCLM     DIM       3
EDHFD     DIM       6
.
FCLMDESC  DIM       20                 * From Claim Code description
FHFDDESC  DIM       30                 * From Health Fund description
FROMDATE  DIM       8                  * From Date in "YYMMDD" format
.
HFDBED    FORM      7
HFDPAT    FORM      8
HOSCODE   DIM       3
.
JYEAR     FORM      2
NBRDAYS   FORM      6
.
PRTFCLM   DIM       6                  * PRinT From CLaiM code variable
PRTTCLM   DIM       6                  * PRinT To CLaiM code variable
PRTFHFD   DIM       6                  * PRinT From Health FunD variable
PRTTHFD   DIM       6                  * PRinT To Health FunD variable
QUESTFLG  FORM      1                  * "?" option flag (0=On, 1=Off)
.
SAVCLM    DIM       3
SAVHFD    DIM       6
SEPCLM    FORM      1
SEPHFD    FORM      1
STCLM     DIM       3
STHFD     DIM       6
.
TCLMDESC  DIM       20                 * To Claim Code description
THFDDESC  DIM       30                 * To Health Fund description
TEMPFILE  DIM       8
TODATE    DIM       8                  * To Date in "YYMMDD" format
TOTBED    FORM      7
TOTPAT    FORM      8
.
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WSTAT     FORM      1
.
XDAY1     DIM       2
XDAY2     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
.
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
.         CONSTANTS
.
CODECL    INIT      "CL"
ERASE     INIT      "iserase "
FINISH    INIT      "Finish"
ISBUILD   INIT      "isbuild "
LYEAR     FORM      "366"
OYEAR     FORM      "365"
START     INIT      "Start"
SP42      INIT      "                                          "
UKEY      INIT      " 116 U1-42"
ZERO8     INIT      "0000    "          * for patfndfd
.
PRGID     INIT      "IBAHMS28"
PRGNAM    INIT      "HEALTH FUND PATIENTS REPORT"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
.
          DISPLAY   *P64:24,"controlf"
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMX1A1,"patmx1af"
.
          
          DISPLAY   *P64:24,"pmsvx1af";      * PMSVX1FD
          OPEN      PMSVX1A1,"pmsvx1af"
. 
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF

          MOVE      ONE,CNEWDS              * set up new standard for "?" option
.
          PACK      CUDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
+
.
.         START OF PROGRAM
.
ML0050    CALL        KHOS0000                   * Keyin hospital
          BRANCH      EXIT,ML0050
.
START     DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Health Fund Patients Report":
                    *P1:7,"Select Option :";
.
START0    KEYIN     *P17:7,*EF,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF VALOPT
          DISPLAY   *P35:7,*EL,*B,*V2LON,"** Invalid Option **",*W2;
          GOTO      START0
+
.
VALOPT    DISPLAY   *P1:10,*EF,"From Date :":
                        *P1:11,"To   Date :";
.
.         Keyin From Date
.
KADAT     MOVE      ZERO,CHIGHLT
          MOVE      ONE,CDEFDTE
          MOVE      TEN,CVERT
          MOVE      TEN3,CCOL
          MOVE      TEN,EVERT
          MOVE      "40",ECOL
.
KADAT1    UNPACK    SP6 INTO CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KADAT1
          BRANCH    OVRCD OF START0
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          MATCH     FROMDATE,CUDATE
          GOTO      INVFDATE IF LESS
.
          UNPACK    FROMDATE,XCENT1,XYEAR1,XMON1,XDAY1
.
.         Keyin To Date
.
          MOVE      TEN1,CVERT
          MOVE      TEN1,EVERT
.
KADAT2    UNPACK    CUDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    CQUEST,KADAT2
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     TODATE,CUDATE
          GOTO      INVTDATE IF LESS
.
          MATCH     FROMDATE,TODATE
          GOTO      INVDATRG IF LESS
.
          UNPACK    TODATE,XCENT2,XYEAR2,XMON2,XDAY2
.
.         Add one to the TODATE. This is because initially the program did not
.         include the end date, so it looks at all the data upto but including
.         the TODATE.
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          PACK      TODATE WITH CC,YY,MM,DD
          REP       " 0",TODATE
          GOTO      KEYCLM
.
INVDATRG  DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** To Date must be greater than or equal From Date **  ";
          CALL      HITENTER
          GOTO      KADAT2
.
INVFDATE  DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** From Date must be less than or = current date **   ";
          CALL      HITENTER
          GOTO      KADAT1
.
INVTDATE  DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** To Date must be less than or equal current date **  ";
          CALL      HITENTER
          GOTO      KADAT2
.
.         KEYIN CLAIM CODE RANGE
.
.         --- From Claim Code ---
.
KEYCLM    MOVE      SP20,FCLMDESC
          MOVE      ONE,QUESTFLG
          MOVE      "13",CCOL1
          DISPLAY   *P1:13,*EF,"From Claim Code  :":
                    *P40:13,"( ",*V2LON,"?",*HOFF," for codes scan )";
.
KEYCLM1   MOVE      SP3,STCLM
          KEYIN     *P20:CCOL1,*DV,SP3,*P20:CCOL1,*V2LON,STCLM;
          RESET     STCLM
          GOTO      KEYCLM3 IF NOT EOS
          MOVE      SP3,STCLM
.
KEYCLM2   BRANCH    QUESTFLG,KEYCLM21
          CALL      RDSP0000
          DISPLAY   *P1:13,"From Claim Code  :";
.
KEYCLM21  DISPLAY   *P20:13,*EL,*V2LON,START;
          MOVE      START,PRTFCLM
          GOTO      KEYCLM5
.
KEYCLM3   ENDSET    STCLM
          APPEND    SP3,STCLM
          RESET     STCLM
.
          MATCH     SP3,STCLM
          GOTO      KEYCLM2 IF EQUAL
.
          CMATCH    QUEST,STCLM
          GOTO      KEYCLM4 IF NOT EQUAL
          MOVE      CODECL,CODE
          CALL      PATCODDS
          MOVE      ZERO,QUESTFLG
          MOVE      "24",CCOL1
.
KEYCLM31  DISPLAY   *P1:24,*EL,"From Claim Code  :";
          GOTO      KEYCLM1
.
KEYCLM4   PACK      KEY5,CODECL,STCLM
          CALL      RDCODE1
          BRANCH    OVRCD,KEYCLM42
          MOVE      TDESC,FCLMDESC
.
          BRANCH    QUESTFLG,KEYCLM41
          CALL      RDSP0000
          DISPLAY   *P1:13,"From Claim Code  :";
.
KEYCLM41  DISPLAY   *P20:13,*EL,*V2LON,STCLM,*HOFF,*P31:13,FCLMDESC;
          MOVE      STCLM,PRTFCLM
          GOTO      KEYCLM5
.
KEYCLM42  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Claim Code does not exist **    ";
          CALL      HITENTER
          BRANCH    QUESTFLG,KEYCLM1
          GOTO      KEYCLM31
.
.         --- To Claim Code ---
.
KEYCLM5   MOVE      SP20,TCLMDESC
          MOVE      ONE,QUESTFLG
          MOVE      "14",CCOL1
          DISPLAY   *P1:14,*EF,"To   Claim Code  :":
                    *P40:14,"( ",*V2LON,"?",*HOFF," for codes scan )";
.
KEYCLM51  MOVE      SP3,EDCLM
          KEYIN     *P20:CCOL1,*DV,SP3,*P20:CCOL1,*V2LON,EDCLM;
          RESET     EDCLM
          GOTO      KEYCLM6 IF NOT EOS
.
KEYCLM52  MOVE      "zzz",EDCLM
          BRANCH    QUESTFLG,KEYCLM53
          CALL      RDSP0000
          CALL      RDFC0000
          DISPLAY   *P1:14,"To   Claim Code  :";
.
KEYCLM53  DISPLAY   *P20:14,*EL,*V2LON,FINISH;
          MOVE      FINISH,PRTTCLM
          GOTO      KEYHFD
.
KEYCLM6   ENDSET    EDCLM
          APPEND    SP3,EDCLM
          RESET     EDCLM
.
          MATCH     SP3,EDCLM
          GOTO      KEYCLM52 IF EQUAL
.
          CMATCH    QUEST,EDCLM
          GOTO      KEYCLM7 IF NOT EQUAL
          MOVE      CODECL,CODE
          CALL      PATCODDS
          MOVE      ZERO,QUESTFLG
          MOVE      "24",CCOL1
.
KEYCLM61  DISPLAY   *P1:24,*EL," To Claim Code  :";
          GOTO      KEYCLM51
.
KEYCLM7   PACK      KEY5,CODECL,EDCLM
          CALL      RDCODE1
          BRANCH    OVRCD,KEYCLM8
.
          MATCH     STCLM,EDCLM
          GOTO      KEYCLM71 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Claim Code must be > or = to From Claim Code **  ";
          CALL      HITENTER
          BRANCH    QUESTFLG,KEYCLM51
          GOTO      KEYCLM61
.
KEYCLM71  MOVE      TDESC,TCLMDESC
          BRANCH    QUESTFLG,KEYCLM72
          CALL      RDSP0000
          CALL      RDFC0000
          DISPLAY   *P1:14,"To   Claim Code  :";
.
KEYCLM72  DISPLAY   *P20:14,*EL,*V2LON,EDCLM,*HOFF,*P31:14,TCLMDESC;
          MOVE      EDCLM,PRTTCLM
          GOTO      KEYHFD
.
KEYCLM8   DISPLAY   *P1:24,*EL,*B,*V2LON,"** Claim Code does not exist **    ";
          CALL      HITENTER
          BRANCH    QUESTFLG,KEYCLM51
          GOTO      KEYCLM61
.
.         KEYIN HEALTH FUND RANGE
.
.         --- From Health Fund ---
.
KEYHFD    MOVE      SP30,FHFDDESC
          MOVE      ONE,QUESTFLG
          MOVE      "16",CCOL1
          DISPLAY   *P1:16,*EF,"From Health Fund :":
                    *P40:16,"( ",*V2LON,"?",*HOFF," for funds scan )";
.
KEYHFD1   MOVE      SP6,STHFD
          KEYIN     *P20:CCOL1,*DV,SP6,*P20:CCOL1,*V2LON,STHFD;
          RESET     STHFD
          GOTO      KEYHFD3 IF NOT EOS
          MOVE      SP6,STHFD
.
KEYHFD2   BRANCH    QUESTFLG,KEYHFD21
          CALL      RDSP0000
          CALL      RDFC0000
          CALL      RDTC0000
          DISPLAY   *P1:16,"From Health Fund :";
.
KEYHFD21  DISPLAY   *P20:16,*EL,*V2LON,START;
          MOVE      START,PRTFHFD
          GOTO      KEYHFD5
.
KEYHFD3   ENDSET    STHFD
          APPEND    SP6,STHFD
          RESET     STHFD
.
          MATCH     SP3,STHFD
          GOTO      KEYHFD2 IF EQUAL
.
          CMATCH    QUEST,STHFD
          GOTO      KEYHFD4 IF NOT EQUAL
          CALL      PATDSFN1
          MOVE      ZERO,QUESTFLG
          MOVE      "24",CCOL1
.
KEYHFD31  DISPLAY   *P1:24,*EL,"From Health Fund :";
          GOTO      KEYHFD1
.
KEYHFD4   PACK      KEY14,STHFD,ZERO8
          CALL      RDFUND1
          BRANCH    OVRCD,KEYHFD42
          MOVE      HFNAME,FHFDDESC
.
          BRANCH    QUESTFLG,KEYHFD41
          CALL      RDSP0000
          CALL      RDFC0000
          CALL      RDTC0000
          DISPLAY   *P1:16,"From Health Fund :";
.
KEYHFD41  DISPLAY   *P20:16,*EL,*V2LON,STHFD,*HOFF,*P31:16,FHFDDESC;
          MOVE      STHFD,PRTFHFD
          GOTO      KEYHFD5
.
KEYHFD42  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Health Fund does not exist **    ";
          CALL      HITENTER
          BRANCH    QUESTFLG,KEYHFD1
          GOTO      KEYHFD31
.
.         --- To Health Fund ---
.
KEYHFD5   MOVE      SP30,THFDDESC
          MOVE      ONE,QUESTFLG
          MOVE      "17",CCOL1
          DISPLAY   *P1:17,*EF,"To   Health Fund :":
                    *P40:17,"( ",*V2LON,"?",*HOFF," for funds scan )";
.
KEYHFD51  MOVE      SP6,EDHFD
          KEYIN     *P20:CCOL1,*DV,SP6,*P20:CCOL1,*V2LON,EDHFD;
          RESET     EDHFD
          GOTO      KEYHFD6 IF NOT EOS
.
KEYHFD52  MOVE      "zzzzzz",EDHFD
          BRANCH    QUESTFLG,KEYHFD53
          CALL      RDSP0000
          CALL      RDFC0000
          CALL      RDTC0000
          CALL      RDFF0000
          DISPLAY   *P1:17,"To   Health Fund :";
.
KEYHFD53  DISPLAY   *P20:17,*EL,*V2LON,FINISH;
          MOVE      FINISH,PRTTHFD
          GOTO      SEPPAGE
.
KEYHFD6   ENDSET    EDHFD
          APPEND    SP6,EDHFD
          RESET     EDHFD
.
          MATCH     SP3,EDHFD
          GOTO      KEYHFD52 IF EQUAL
.
          CMATCH    QUEST,EDHFD
          GOTO      KEYHFD7 IF NOT EQUAL
          CALL      PATDSFN1
          MOVE      ZERO,QUESTFLG
          MOVE      "24",CCOL1
.
KEYHFD61  DISPLAY   *P1:24,*EL,"To Health Fund  :";
          GOTO      KEYHFD51
.
KEYHFD7   PACK      KEY14,EDHFD,ZERO8
          CALL      RDFUND1
          BRANCH    OVRCD,KEYHFD8
.
          MATCH     STHFD,EDHFD
          GOTO      KEYHFD71 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Health Fund must be > or = From Health Fund **   ";
          CALL      HITENTER
          BRANCH    QUESTFLG,KEYHFD51
          GOTO      KEYHFD61
.
KEYHFD71  MOVE      HFNAME,THFDDESC
          BRANCH    QUESTFLG,KEYHFD72
          CALL      RDSP0000
          CALL      RDFC0000
          CALL      RDTC0000
          CALL      RDFF0000
          DISPLAY   *P1:17,"To   Health Fund :";
.
KEYHFD72  DISPLAY   *P20:17,*EL,*V2LON,EDHFD,*HOFF,*P31:17,THFDDESC;
          MOVE      EDHFD,PRTTHFD
          GOTO      SEPPAGE
.
KEYHFD8   DISPLAY   *P1:24,*EL,*B,*V2LON,"** Health Fund does not exist **    ";
          CALL      HITENTER
          BRANCH    QUESTFLG,KEYHFD51
          GOTO      KEYHFD61
+
.*************************************************************************
.*                            RDSP0000                                   *
.*               Redisplay Screen after "?" option                       *
.*************************************************************************
RDSP0000  DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Health Fund Patients Report":
                    *P1:7,"Select Option : ",*V2LON,OPTION,*HOFF:
                    *P1:10,"From Date : ":
                           *V2LON,XDAY1,"/",XMON1,"/",XCENT1,XYEAR1,*HOFF:
                    *P1:11,"To   Date : ":
                           *V2LON,XDAY2,"/",XMON2,"/",XCENT2,XYEAR2;
RDSP9999  RETURN
+
.*************************************************************************
.*                            RDFC0000                                   *
.*                    ReDisplay From Claim code                          *
.*************************************************************************
RDFC0000  DISPLAY   *P1:13,"From Claim Code  :";
          MATCH     SP3,STCLM
          GOTO      RDFC1000 IF NOT EQUAL
          DISPLAY   *P20:13,*V2LON,START;
          GOTO      RDFC9999
.
RDFC1000  DISPLAY   *P20:13,*V2LON,STCLM,*HOFF,*P31:13,FCLMDESC;
.
RDFC9999  RETURN
+
.*************************************************************************
.*                            RDTC0000                                   *
.*                     ReDisplay To Claim code                           *
.*************************************************************************
RDTC0000  DISPLAY   *P1:14,"To   Claim Code  :";
          MATCH     "zzz",EDCLM
          GOTO      RDTC1000 IF NOT EQUAL
          DISPLAY   *P20:14,*V2LON,FINISH;
          GOTO      RDTC9999
.
RDTC1000  DISPLAY   *P20:14,*V2LON,EDCLM,*HOFF,*P31:14,TCLMDESC;
.
RDTC9999  RETURN
+
.*************************************************************************
.*                            RDFF0000                                   *
.*                   ReDisplay From health Fund                          *
.*************************************************************************
RDFF0000  DISPLAY   *P1:16,"From Health Fund :";
          MATCH     SP6,STHFD
          GOTO      RDFF1000 IF NOT EQUAL
          DISPLAY   *P20:16,*V2LON,START;
          GOTO      RDFF9999
.
RDFF1000  DISPLAY   *P20:16,*V2LON,STHFD,*HOFF,*P31:16,FHFDDESC;
.
RDFF9999  RETURN
+
.*************************************************************************
.
.         Check if each Health Fund is to be on separate page
.
SEPPAGE   MOVE      ZERO,SEPHFD
          MOVE      ZERO,SEPCLM
.
          KEYIN     *P1:19,*EF:
                    "Would you like each Health Fund on a separate page (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          DISPLAY   *P60:19,*V2LON,ANS;
.
          MATCH     "Y" TO ANS
          GOTO      SEPPAGEY IF EQUAL
          MATCH     "N" TO ANS
          GOTO      SEPPAGEN IF EQUAL
          BEEP
          GOTO      SEPPAGE
.
SEPPAGEY  MOVE      ONE,SEPHFD
          MOVE      ONE,SEPCLM
          GOTO      REKEY
.
SEPPAGEN  MOVE      TWO,SEPHFD
          MOVE      TWO,SEPCLM
+
.
.         Check if entries are okay or not
.
REKEY     CALL      CONTQST
          BRANCH    CEXIT,CNTCHK,VALOPT,START0
.                          Yes     No   Cancel
          BEEP
          GOTO      REKEY
+
.
.             PROCESS START
.
.         INITIALIZE CENSUS FILE
.
CNTCHK    DISPLAY   *P1:24,*EL,*P19:24,*V2LON:
                    "** Initializing Health Fund Report File **",*W2;
          CALL      CREA0000
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Processing Current Admission **",*W,*HOFF:
                    *P1:24,*EL,*P20:24,"Current Admission No :":
                    *P55:24,"Scanning :";
.
          MOVE      TWO,WSTAT
          MOVE      SP8,DDATE
          MOVE      SP9,DFMBS
          CALL      ADMFN
.
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Processing On Leave Admission **",*W,*HOFF:
                    *P1:24,*EL,*P20:24,"On Leave Admission No :":
                    *P55:24,"Scanning :";
.
          MOVE      FOUR,WSTAT
          MOVE      SP8,DDATE
          MOVE      SP9,DFMBS
          CALL      ADMFN
.
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Processing Discharge Admission **",*W,*HOFF:
                    *P1:24,*EL,*P20:24,"Discharged Admission No :":
                    *P55:24,"Scanning :";
.
          PACK      KEY16 WITH FROMDATE,SP8
          CALL      RDSDSCH2
.
NEXT3     CALL      RDKDSCH2
          BRANCH    OVRCD OF REPORT
          DISPLAY   *P66:24,DADMNO;
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF NEXT3
.
          COMPARE   ONE,IBCNMHOS
          GOTO      ALLHOS1 IF NOT EQUAL
.
          MATCH     SP70,PTHSHOSP
          GOTO      ALLHOS1 IF EQUAL
.
          PACK      KEY8,DADMNO,SP10
          CALL      RDPMVX11
          BRANCH    OVRCD,NEXT3
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      NEXT3 IF NOT EQUAL
.
ALLHOS1   UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      WDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",WDATE
.
          MATCH     TODATE,WDATE
          GOTO      NEXT3 IF NOT LESS
.
          DISPLAY   *P44:24,*V2LON,DADMNO;
          CALL      TRANS  
          GOTO      NEXT3
+
.
.         READ ADMISSIONS FILE
.
ADMFN     PACK      KEY9 WITH WSTAT,SP6
          CALL      RDSMISS2
NEXT5     CALL      RDKMISS2
          BRANCH    OVRCD OF CONT1
.
.
.         Filtering Records Based on Hospital ID
.
          COMPARE   ONE,IBCNMHOS
          GOTO      ALLHOS IF NOT EQUAL
.
          MATCH     SP70,PTHSHOSP 
          GOTO      ALLHOS IF EQUAL
.
          PACK      KEY8,DAADMNO,SP10
          CALL      RDPMVX11
          BRANCH    OVRCD,NEXT5
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      NEXT5 IF NOT EQUAL
.
ALLHOS    COMPARE   WSTAT,ASTAT
          GOTO      CONT1 IF NOT EQUAL
          DISPLAY   *P66:24,AADMNO;
.
          UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      WDATE WITH XCENT,XYEAR,XMON,XDAY
          RESET     WDATE
          REP       " 0",WDATE
          RESET     WDATE
.
          MATCH     TODATE,WDATE
          GOTO      NEXT5 IF NOT LESS
.
          DISPLAY   *P44:24,*V2LON,AADMNO;
          CALL      TRANS  
          GOTO      NEXT5
CONT1     RETURN
+
.
.         READ TRANSFERS FILE      
.
TRANS     MATCH     STCLM,ACLAIM
          RETURN    IF LESS
.
          MATCH     ACLAIM,EDCLM
          RETURN    IF LESS
.
          MATCH     STHFD,AFUNDH
          RETURN    IF LESS
.
          MATCH     AFUNDH,EDHFD
          RETURN    IF LESS
.
          CALL      NHOSPDAY
.
          COMPARE   ZERO,NBRDAYS
          RETURN    IF EQUAL
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NOUR
.
          MOVE      PSNAME,XPSNAME
          PACK      KEY42 WITH ACLAIM,AFUNDH,XPSNAME,AURNO,AADMNO
          CALL      RDWORK1
          BRANCH    OVRCD OF NEWREC2
          ADD       NBRDAYS,WRKDAY
          RETURN
.
NOUR      DISPLAY   *P1:22,*EL,*B,*V2LON:
                    "*** U/R Number ",AURNO," missing from Master file ***";
          RETURN
.
NEWREC2   MOVE      NBRDAYS,WRKDAY
          MOVE      PGNAME,XPGNAME
.
.------- Get the last ward and bed for the patient ------
.
          MOVE      SP7,WARDBED
          PACK      KEY30,AADMNO,ZEDS
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,WPOOL
.
          MATCH     TADMN,AADMNO
          GOTO      WPOOL IF NOT EQUAL
.
          PACK      WARDBED,TWARD,SLASH,TBED
.
WPOOL     PACK      KEY42 WITH ACLAIM,AFUNDH,XPSNAME,AURNO,AADMNO
          CALL      WRWORK1
          RETURN
+
.
.         PRINT REPORT
.
REPORT    MOVE      SP42,KEY42              * CHECK ANY RECORD IN FILE
          CALL      RDSWORK1
          CALL      RDKWORK1
          BRANCH    OVRCD OF NOPAT
.
          MOVE      AFUNDH,SAVHFD
          MOVE      ACLAIM,SAVCLM
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
          MOVE      ZERO,TOTBED
          MOVE      ZERO,HFDPAT
          MOVE      ZERO,HFDBED
          MOVE      ZERO,CLMPAT
          MOVE      ZERO,CLMBED
.
          DISPLAY   *P1:24,*EL,*P29:24,*V2LON,"** Generating Report **",*W2;
.
          MOVE      SP42,KEY42
          CALL      RDSWORK1
PLOOP     CALL      RDKWORK1
          BRANCH    OVRCD OF ENDP
.
CHKSUB    MATCH     ACLAIM,SAVCLM
          GOTO      PRTSUB IF NOT EQUAL
.
          MATCH     AFUNDH,SAVHFD
          GOTO      PRTCONT IF EQUAL
.
PRTSUB    CALL      PRTLN
.
          PRINT      *1," **  SUB TOTAL  **",*19,"| ",HFDPAT," Patients":
                    *62,"|",*123,"|",HFDBED,*132,"|",*N:
                    *19,"*------------------------------------------*":
                   *123,"*--------*"
          ADD       TWO,CLNO
.
          MOVE      AFUNDH,SAVHFD
          MOVE      ZERO,HFDPAT
          MOVE      ZERO,HFDBED
.
          MATCH     ACLAIM,SAVCLM
          GOTO      PRTTOT IF NOT EQUAL
.
          COMPARE   ONE,SEPHFD
          GOTO      NEWPG IF EQUAL
.
          COMPARE   "55",CLNO
          GOTO      PRTSUB1 IF LESS
          CALL      HEAD
          GOTO      PRTCONT
.
PRTSUB1   PRINT     *N;
          ADD       ONE,CLNO
          CALL      PRTLN
          GOTO      PRTCONT
.
NEWPG     CALL      HEAD
.
CHKTOT    MATCH     ACLAIM,SAVCLM
          GOTO      PRTCONT IF EQUAL
.
PRTTOT    PRINT      *1," **    TOTAL    **",*19,"| ",CLMPAT," Patients":
                    *62,"|",*123,"|",CLMBED,*132,"|",*N:
                    *19,"*------------------------------------------*":
                   *123,"*--------*"
.
          MOVE      ACLAIM,SAVCLM
          MOVE      ZERO,CLMPAT
          MOVE      ZERO,CLMBED
          CALL      HEAD
.
PRTCONT   COMPARE   "57",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      XPGNAME,DIM14
          PRINT     *1,"|",AADMNO,*10,"|",AURNO,*19,"| ",XPSNAME:
                    *39,DIM14,*54,"|",WARDBED,*62,"| ",AFUNDH,*71,"| ":
                    AFNDTB;
.
          UNPACK    ADATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *81,"|",CPCDATE;
.
          UNPACK    DDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *92,"|",CPCDATE,*103,"| ",DFMBS,"|",ATYPE,"|",ACARE:
                    *123,"| ",WRKDAY," |"
          ADD       ONE,CLNO
.
          ADD       ONE,TOTPAT
          ADD       ONE,HFDPAT
          ADD       ONE,CLMPAT
          ADD       WRKDAY,TOTBED
          ADD       WRKDAY,HFDBED
          ADD       WRKDAY,CLMBED
          GOTO      PLOOP
.
ENDP      CALL      PRTLN
          PRINT      *1," **  SUB TOTAL  **",*19,"| ",HFDPAT," Patients":
                    *62,"|",*123,"|",HFDBED,*132,"|",*N:
                    *19,"*------------------------------------------*":
                   *123,"*--------*"
          ADD       TWO,CLNO
.
          COMPARE   "54",CLNO
          CALL      HEAD IF NOT LESS
.
          PRINT      *1," **    TOTAL    **",*19,"| ",CLMPAT," Patients":
                    *62,"|",*123,"|",CLMBED,*132,"|",*N:
                    *19,"*------------------------------------------*":
                   *123,"*--------*"
.
           PRINT     *1," ** GRAND TOTAL **",*19,"| ",TOTPAT," Patients":
                    *62,"|",*123,"|",TOTBED,*132,"|",*N:
                    *19,"*------------------------------------------*":
                   *123,"*--------*"
          
          PRINT     *N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          CALL      KILL0000
          GOTO      START0
+
.
.         REPORT HEADINGS
.
HEAD      COMPARE   ZERO,CPAGENO
          GOTO      CLKTIM IF EQUAL
.
          COMPARE   ZERO,HFDPAT
          CALL      PRTLN IF NOT EQUAL
.
CLKTIM    CLOCK     TIME,CTIMEIS
          MOVE      NINE,CLNO
.
          PACK      KEY5 WITH CODECL,SAVCLM
          CALL      RDCODE1
.
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
          IF          IBCNMHOS =1
            PRINT     *30,"Hospital   : ",PTHSNAME
          ENDIF
          PRINT     *45,"Date        : From   ",XDAY1,"/",XMON1,"/",XCENT1:
                        XYEAR1:
                        " To   ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2,*N:
                    *45,"Claim Code  : From   ",PRTFCLM,*77,"To   ",PRTTCLM,*N:
                    *45,"Health Fund : From   ",PRTFHFD,*77,"To   ",PRTTHFD,*N:
                    *1,"Claim Code : ",TDESC
.
          CALL      PRTLN
          PRINT      *1,"|  Adm.",*10,"|   U/R",*19,"| Patient",*54,"| Ward":
                    *62,"| Health",*71,"| Fund",*81,"| Admission":
                        "| Discharge| Discharge|Adm|Care| No. of |":
                     *N,"| Number | Number | Surname",*39,"Given Name":
                     *54,"| /Bed",*62,"|  Fund",*71,"| Table",*81,"|   Date":
                    *92,"|   Date",*103,"| Diag Code|Typ|Clss|Bed Days|"
          CALL      PRTLN
          RETURN
+
.
PRTLN     PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
NOPAT     DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** No Patient Selected **",*W3;
          CALL      KILL0000
          GOTO      START0
+
.**************************************************************************
.*                              CREA0000               Called by: CNTCHK  *
.*                    create a new temporary file                         *
.**************************************************************************
CREA0000  CALL      KILL0000                     * remove existing file
.
          PACK      CMDLIN1,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLIN1,TASKID               * create temporary index file
.
          OPEN      PATWORK1,TEMPFILE            * open temp index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000                                    *
.*                 close and erase the temporary file                       *
.****************************************************************************
KILL0000  CLOSE     PATWORK1                     * close file
.
          PACK      CMDLIN2,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLIN2,TASKID               * erase temp file
.
KILL9999  RETURN
+
.****************************************************************************
.
.         I/O routines for temp indexed file
.
WRWORK1   RESET     KEY42
          WRITE     PATWORK1,KEY42;KEY42,XPGNAME,AFNDTB,WRKDAY,ADATE,DDATE:
                                   DFMBS,ATYPE,ACARE,WARDBED
          RETURN
.
RDWORK1   MOVE      ZERO,OVRCD
          RESET     KEY42
          READ      PATWORK1,KEY42;ACLAIM,AFUNDH,XPSNAME,XDAURNO,XDAADMNO:
                                   XPGNAME,AFNDTB,WRKDAY,ADATE,DDATE,DFMBS:
                                   ATYPE,ACARE,WARDBED
          GOTO      OVERCOND IF OVER
          MOVE      XDAURNO,AURNO
          MOVE      XDAADMNO,AADMNO
          RETURN
.
RDKWORK1  MOVE      ZERO,OVRCD
          READKS    PATWORK1;ACLAIM,AFUNDH,XPSNAME,XDAURNO,XDAADMNO,XPGNAME:
                             AFNDTB,WRKDAY,ADATE,DDATE,DFMBS,ATYPE,ACARE,WARDBED
          GOTO      OVERCOND IF OVER
          MOVE      XDAURNO,AURNO
          MOVE      XDAADMNO,AADMNO
          RETURN
.
RDSWORK1  RESET     KEY42
          READ      PATWORK1,KEY42;;
          RETURN
.
UPWORK1   MOVE      AADMNO,XDAADMNO
          MOVE      AURNO,XDAURNO
          UPDATE    PATWORK1;ACLAIM,AFUNDH,XPSNAME,XDAURNO,XDAADMNO,XPGNAME:
                             AFNDTB,WRKDAY,ADATE,DDATE,DFMBS,ATYPE,ACARE,WARDBED
          RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:21,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TWENTY1,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:21,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.
ENDIT     CALL      KILL0000
          CHAIN     PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS
          INC       NHOSPDAY
.
          INC       PATCODDS
          INC       PATFNDDS
.
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATFN1IO/INC
          INC       PATMA1IO/INC
          INC       PMSVX1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PATHSPIO/INC
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
+

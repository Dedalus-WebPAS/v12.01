.******************************************************************************
.* System    :   Patient                                                      *
.* Program   :   UPPATEOR                                                     *
.* Desc      :   External Organisation Table - Upload Schools data            *
.******************************************************************************
.* Date      :   14/01/2008                                                   *
.* Author    :   Mike Laming                                                  *
.* Mods      :                                                                *
.*                                                                            *
.*        V10.02.01 03/06/2011 J.Tan            CAR 239592                    *
.*                  Renamed PTCDUDF1 to PTCDFELC                              *
.*        V10.00.01 24/03/2010  Steve Armstrong   CAR 218670                  *
.*                  Added new fields for PATCODFD to CLPATCOD                 *
.******************************************************************************
.*        V9.11.00  14/01/2009 Mike Laming   CAR 150425                       *
.*                  New Program                                               *
.******************************************************************************
+
          INC       STD001FD
          INC       STDHLPDF
.
          INC       PATEORFD/INC            * External Organisation Table
          INC       PATKEOFD/INC            * External Organisation Keyword
          INC       PATCODFD/INC            * Codes Table
....      INC       WEBSECFD/INC            * Web Security / Web Users
.
. ----- Input Text File Definitions -----
.
INTEST01  FILE      ASCII, FIXED=2000       * used to test if file exists
INEORTXT  FILE      HL7, FIXED=2000         * input file for Health Fund Remitt.
.
FLD60001  DIM       60
FLD60002  DIM       60
FLD60003  DIM       60
FLD60004  DIM       60
FLD60005  DIM       60
FLD60006  DIM       60
FLD60007  DIM       60
FLD60008  DIM       60
FLD60009  DIM       60
FLD60010  DIM       60
FLD60011  DIM       60
FLD60012  DIM       60
FLD60013  DIM       60
FLD60014  DIM       60
FLD60015  DIM       60
FLD60016  DIM       60
FLD60017  DIM       60
.
. ----- Program Variables -----
.
CATEEDSC  DIM       30
CATEFDSC  DIM       30
CDATEIS   DIM       8
CMDLINE   DIM       60
UPDCODES  FORM      1
DIM22A    DIM       22
DIM30A    DIM       30
DONE      FORM      1
ERRDATA   DIM       40
ERRMESS   DIM       75
.
FLDNO     DIM       2   
FNAMESHT  DIM       20
FNAMEIN   DIM       60
FLD60IN   DIM       60
FLD60OUT  DIM       60
LENGTH    FORM      3 
LENIN     FORM      3 
LENOUT    FORM      3 
POSII     FORM      3
POSOO     FORM      3
POSXX     FORM      3
RECIN     FORM      5
.
EE        INIT      "EE"
EF        INIT      "EF"
.
. ----- Program Constants -----
.
PRGNAM    INIT      "Upload External Organisations"
PRGID     INIT      "UPPATEOR"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
          BRANCH    EXIT,ML9000
.
ML1000    CALL      OPTN0000                * Select Option          
          BRANCH    EXIT,ML9000
.
ML7000    CALL      PINP0000                * Process the input file
          BRANCH    EXIT,ML1000
          GOTO      ML1000
.
ML9000    MOVE      ZERO,EXIT
          MATCH     SP8,PGM
          GOTO      ML9999 IF EQUAL
.
          CHAIN     PGM                     * Chain back to appropriate menu
ML9999    SHUTDOWN  ""
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          PACK      FNAMEIN,SP30,SP30
.
....      DISPLAY   *P64:24,"controlf";
....      OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"pateoraf";
          OPEN      PATEORA1,"pateoraf"     
.
          DISPLAY   *P64:24,"patkeoaf";
          OPEN      PATKEOA1,"patkeoaf"     
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"     
.
          CALL      IBACLOCK
          PACK      CDATEIS,CCC,CYY,CMM,CDD
          REP       " 0",CDATEIS
.
INIT9000  MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*                         Select option to run upload                        *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Upload School file to PATEOR":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN2000
          GOTO      OPTN1000 IF NOT EQUAL
.
OPTN2000  DISPLAY   *P1:10,*EF:
                    *P4:10,"Enter Path and File Name for ":
                    *P4:11,"School File - ",*V2LON,UNDLN30,UNDLN30,*HOFF;
.
.
OPTN4000  MOVE      "11",CVERT
          DISPLAY   *P4:CVERT,"School File - ";
          CALL      KASCI000                * keyin file name
          BRANCH    EXIT,OPTN0000
.
OPTN5000  MOVE      SP1,KEY1
          MOVE      ZERO,UPDCODES
          DISPLAY   *P4:15,"Update Category/Codes file with Region & LGA ":
                     "Codes ? (Y or N) : ";
          KEYIN     *P68:15,*V2LON,*DV,UNDLN2,*P68:15,KEY1;
          REP       UPPLOW,KEY1
          MATCH     "Y",KEY1
          IF        @EQUAL
            MOVE      ONE,UPDCODES
            GOTO      OPTN6000
          ENDIF
          MATCH     "N",KEY1
          GOTO      OPTN5000 IF NOT EQUAL
.
OPTN6000  CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,OPTN9000,OPTN0000,OPTN9500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 KASCI000                                   *
.*                            Keyin ASCI file                                 *
.* Returns: EXIT  0 = no file name entered                                    *
.*                1 = valid filename entered                                  *
.******************************************************************************
KASCI000  KEYIN     *P18:CVERT,*V2LON,*EL,*RV,FNAMEIN;
.
          PACK      FNAMEIN,FNAMEIN,SP20,SP20,SP20
.
          MATCH     SP70,FNAMEIN            * anything entered ?
          GOTO      KASCI950 IF EQUAL       * no
          CMATCH    "\",FNAMEIN             * "\" entered ?
          GOTO      KASCI950 IF EQUAL       * no
.
          SCAN      "/",FNAMEIN
          IF        @EQUAL
            SCAN      ".txt",FNAMEIN
            IF        !@EQUAL
              SQUEEZE   FNAMEIN
              ENDSET    FNAMEIN
              APPEND    ".txt",FNAMEIN      * add ".txt" if there is a path
              RESET     FNAMEIN
              DISPLAY   *P18:CVERT,*V2LON,*EL,FNAMEIN;
            ENDIF
          ELSE   
            RESET     FNAMEIN
            SCAN      ".txt",FNAMEIN
            GOTO      KASCI100 IF NOT EQUAL
            BUMP      FNAMEIN,-1            * remove ".txt" if just a file name
            APPEND    SP20,FNAMEIN
            RESET     FNAMEIN
            DISPLAY   *P18:CVERT,*V2LON,*EL,FNAMEIN;
          ENDIF
.
.                                           * get the 8 char file name
KASCI100  RESET     FNAMEIN
          MOVE      ZERO,DONE
          MOVE      FNAMEIN,FNAMESHT        * in case there is no path
          REPEAT
            SCAN      "/",FNAMEIN
            IF        @EQUAL
              BUMP      FNAMEIN
              PACK      FNAMESHT,FNAMEIN,SP20
            ELSE
              MOVE      ONE,DONE
            ENDIF
          UNTIL       DONE = 1
.
          RESET     FNAMEIN
.
          SQUEEZE   FNAMESHT
          SCAN      ".txt",FNAMESHT
          GOTO      KASCI200 IF NOT EQUAL
          BUMP      FNAMESHT,-1             * remove ".txt" if just a file name
          APPEND    SP20,FNAMESHT
.
KASCI200  RESET     FNAMESHT
          SQUEEZE   FNAMESHT
          MOVELPTR  FNAMESHT,LENGTH
          COMPARE   "9",LENGTH
          GOTO      KASCI500 IF LESS
          DISPLAY   *P1:23,*EL,"File name must not be more than 8 characters":
                    *P1:24,*EL," ";
          CALL      HITENTER
          DISPLAY   *P1:23,*EF
          GOTO      KASCI0000
.
.                                           * see if file exists
KASCI500  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      INTEST01,FNAMEIN        * open file
          TRAPCLR   IO
.
          IF        OVRCD = 0
            MOVE      CVERT,F2
            DISPLAY   *P70:F2,*HOFF,"file found";
          ELSE
            DISPLAY   *P1:24,*EL,*B,"File not found.  ";
            CALL      HITENTER
            GOTO      KASCI0000
          ENDIF
.
KASCI900  MOVE      ZERO,EXIT
          GOTO      KASCI999
.
KASCI950  MOVE      ONE,EXIT
.
KASCI999  RETURN
+
.******************************************************************************
.*                                  PINP0000                                  *
.*                         Process The Input Text File                        *
.******************************************************************************
PINP0000  MOVE      ZERO,EXIT
          MOVE      ZERO,RECIN
          CALL      CLPATCOD
          OPEN      INEORTXT,FNAMEIN        * open file
.
. ----- Read the input data file into temporary variables -----
.
PINP1000  READ      INEORTXT,SEQ;FLD60001,FLD60002,FLD60003,FLD60004,FLD60005:
                                 FLD60006,FLD60007,FLD60008,FLD60009,FLD60010:
                                 FLD60011,FLD60012,FLD60013,FLD60014,FLD60015:
                                 FLD60016,FLD60017
          GOTO      PINP7000 IF OVER        * end of file
.
          ADD       ONE,RECIN
          IF        (RECIN%20)=0
            DISPLAY   *P16:20,RECIN,*P35:20,*V2LON,FLD60003;
          ENDIF
.
PINP2000  CALL      CLPATEOR
          MOVE      "C",PTERTYPE
.
          LJUSTIFY  FLD60002
          PACK      PTERCODE,FLD60002,SP10
          PACK      PTERNAME,FLD60003,SP70
.
          MOVE      "XXX",PTERUTYP
          REP       UPPLOW,FLD60004
          MATCH     "PRIM",FLD60004
          IF        @EQUAL
            MOVE      "PR ",PTERUTYP
          ENDIF
          MATCH     "PRI/S",FLD60004
          IF        @EQUAL
            MOVE      "PS ",PTERUTYP         * Primary/Secondary
          ENDIF
          MATCH     "SECO",FLD60004
          IF        @EQUAL
            MOVE      "SE ",PTERUTYP
          ENDIF
          MATCH     "SPEC",FLD60004
          IF        @EQUAL
            MOVE      "SP ",PTERUTYP
          ENDIF
          MATCH     "LANG",FLD60004
          IF        @EQUAL
            MOVE      "LA ",PTERUTYP
          ENDIF
.
          PACK      PTERADD1,FLD60006,SP70
..                  PTERADD2
          PACK      PTERADD3,FLD60007,SP70
..                  PTERADD4
          PACK      PTERPOST,FLD60008,SP10
          PACK      PTERTELP,FLD60012,SP30
          PACK      PTERCONT,FLD60005,SP30
          MOVE      "0",PTERSTAT
          LJUSTIFY  FLD60016
          PACK      PTERSECT,FLD60016,SP3
          PACK      CATEEDSC,FLD60017,SP30
          LJUSTIFY  FLD60014
          PACK      PTERREGN,FLD60014,SP3
          PACK      CATEFDSC,FLD60015,SP30
.
          PACK      KEY7,PTERTYPE,PTERCODE,SP10
          CALL      DEPTEOR1
          CALL      WRPTEOR1
.
.                                           * now clear & reload Keyword file
PINP3000  PACK      KEY22,PTERTYPE,PTERCODE,SP30
          CALL      RSPTKEO1
          IF        OVRCD = 0
            CALL      RPPTKEO1
          ENDIF
          CALL      RKPTKEO1
          BRANCH    OVRCD,PINP3500
          PACK      DIM22A,PTKOTYPE,PTKOCODE,SP30
          MATCH     DIM22A,KEY22
          GOTO      PINP3500 IF NOT EQUAL
.
          PACK      KEY22,PTKOTYPE,PTKOCODE,PTKOKWRD,SP30
          CALL      DEPTKEO1                * clear "patkeoaf" of this code
          GOTO      PINP3000
.                                           * create a "suburb" keyword entry
PINP3500  PROC      PATKEOUP
.
PINP4000  IF        UPDCODES <> 1
            GOTO      PINP1000 IF EQUAL
          ENDIF
.
.                                     * add Region & Sector data to Pat Codes
PINP5000  MATCH     SP3,PTERSECT
          GOTO      PINP5200 IF EQUAL
          PACK      KEY5,EE,PTERSECT,SP5
          CALL      RDCODE1
          IF        OVRCD <> 0
            MOVE      EE,TCODE
            MOVE      PTERSECT,ACODE
            MOVE      CATEEDSC,TDESC
            MOVE      "A",PTCOACTV
            MOVE      "0",PTCDDEFT
            CALL      WRCODE1
          ENDIF
.
PINP5200  MATCH     SP3,PTERREGN
          GOTO      PINP5900 IF EQUAL
          PACK      KEY5,EF,PTERREGN,SP5
          CALL      RDCODE1
          IF        OVRCD <> 0
            MOVE      EF,TCODE
            MOVE      PTERREGN,ACODE
            MOVE      CATEFDSC,TDESC
            MOVE      "A",PTCOACTV
            MOVE      "0",PTCDDEFT
            CALL      WRCODE1
          ENDIF
.
PINP5900  CALL      CLINP000                * clear the work fields
          GOTO      PINP1000 
.
. ----- Finished -----
.
PINP7000  MOVE      ZERO,EXIT
          GOTO      PINP9999
.
PINP9500  MOVE      ONE,EXIT
.
PINP9999  RETURN
+
.******************************************************************************
.*                                  RJUS0000                                  *
.*                         Right Justify a field                              *
.******************************************************************************
RJUS0000  MOVE      SP70,FLD60OUT
          SQUEEZE   FLD60IN
          MOVELPTR  FLD60IN,LENIN
          MOVE      LENGTH,LENOUT
          SUB       LENIN,LENOUT
          IF        LENOUT > 0
            SUB       ONE,LENOUT
            BUMP      FLD60OUT,LENOUT
            APPEND    FLD60IN,FLD60OUT
          ELSE
            MOVE      FLD60IN,FLD60OUT
          ENDIF
          RESET     FLD60OUT
.
RJUS9999  RETURN
+
.******************************************************************************
.*                                  CLHBR000                                  *
.*                           Clear Input variables                            *
.******************************************************************************
CLINP000  MOVE      SP70,FLD60001
          MOVE      SP70,FLD60002
          MOVE      SP70,FLD60003
          MOVE      SP70,FLD60004
          MOVE      SP70,FLD60005
          MOVE      SP70,FLD60006
          MOVE      SP70,FLD60007
          MOVE      SP70,FLD60008
          MOVE      SP70,FLD60009
          MOVE      SP70,FLD60010
          MOVE      SP70,FLD60011
          MOVE      SP70,FLD60012
          MOVE      SP70,FLD60013
          MOVE      SP70,FLD60014
          MOVE      SP70,FLD60015
          MOVE      SP70,FLD60016
          MOVE      SP70,FLD60017
.
CLINP999  RETURN
+
.                                           * Clear Invoice Details
CLPATEOR  MOVE      SP70,PTERTYPE  
          MOVE      SP70,PTERCODE  
          MOVE      SP70,PTERNAME  
          MOVE      SP70,PTERADD1  
          MOVE      SP70,PTERADD2  
          MOVE      SP70,PTERADD3  
          MOVE      SP70,PTERADD4  
          MOVE      SP70,PTERPOST  
          MOVE      SP70,PTERTELP  
          MOVE      SP70,PTERCONT  
          MOVE      SP70,PTERSTAT  
          MOVE      SP70,PTERSECT  
          MOVE      SP70,PTERREGN
          MOVE      SP70,PTERUTYP
          MOVE      SP70,PTERSPAR
CLPTEOR9  RETURN
.
CLPATCOD  UNPACK    SP70,TCODE,ACODE,TDESC,THCSCOD,TCINDC1,TCINDC2,TCINDC3
          UNPACK    SP70,TCINDC4,TCINDC5,PTCOACTV,TCINDC6,TCINDC7,TCINDC8
          UNPACK    SP70,TCINDC9,TCINDC10,TCINDC11,PTCDGRPV,PTCDDEFT,PTCDNHCP
          UNPACK    SP70,PTCDHOSS,PTCDLDES,PTCDDFIC,TCSPARE
          MOVE      SP70,TCINDC12
          MOVE      SP70,TCINDC13
          MOVE      SP70,TCINDC14
          MOVE      SP70,TCINDC15
          MOVE      SP70,TCINDC16
          MOVE      SP70,TCINDC17
          MOVE      SP70,TCINDC18
          MOVE      SP70,TCINDC19
          MOVE      SP70,TCINDC20
          MOVE      SP70,TCINDC21
          MOVE      SP70,PTCDFELC
          MOVE      SP70,PTCDUDF2
          MOVE      SP70,PTCDUDF3
          MOVE      SP70,PTCDUDF4
          MOVE      ZERO,TCFORM6
CLPATCO9  RETURN
.==============================================================================
.
          INC       STD001IO
          INC       STDHLPCD
          INC       PATKEOPR
.
          INC       PATCODIO/INC
          INC       PATEORIO/INC
          INC       PATKEOIO/INC
...       INC       WEBSECIO/INC            * 
+

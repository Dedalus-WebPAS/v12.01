.******************************************************************************
.* System   :  Patient                                                        *
.* Program  :  IBANHI10                                                       *
.* Desc     :  Auto Merge U/R 1                                               *
.******************************************************************************
.* Date     :  16/09/89                                                       *
.* Author   :  S O'Gorman                                                     *
.* Comment  :  DEON IS A KING......DEON IS A SICK DAY                         *
.* Mods     :                                                                 *
.*                                                                            *
.******************************************************************************
.* V10.11.03 27/11/2017  Thanh T.       TSK 0821710                           *
.            Recompiled as PATMASTD/MRGPMIML changed                          * 
.* V10.11.02 21/09/2017  Thanh T.       TSK 0821710                           *
.*           Recompiled for MRGPMIML                                          *
.* V10.11.01 12/09/2017  Thanh T.       TSK 0821710                           *
.*           Audit Amission/outpatient visit comments                         *
.******************************************************************************
.* V10.08.02 09/08/2016  Jill Parkinson TSK 0823914                           *
.*           Recompiled or MRGPMIML - Added set of patma1af to XXFILE so      *
.*           correct error message  displays                                  *
.* V10.08.01 09/05/2011  Davin             CAR 239539                         *
.*           Recompiled for MRGPMIML - Added chronic condition alert(savesn11)*
.* V10.06.02 29/07/2015  Don Nguyen        CAR 319786                         *
.*           Recompiled for changes to MRGPMIML - used KEY16 for RSPTALR1     *
.* V10.06.01 12/03/2015  Steve Armstrong   CAR 314108                         *
.*           Removed definition of PTCGDATE as PATCGPFD is no longer          *
.*           in use.                                                          *
.******************************************************************************
.* V10.03.02 30/06/2013  Davin             CAR 283202
.*           Recompiled for changes to hl7 messaging
.* V10.03.01 01/05/2013  Steve Armstrong   CAR 284376                         *
.*           Recompiled for changes to BRHLCOMN so that PMI messages          *
.*           can use H7CIHOSP for Facility Code.                              *
.*           Recompiled for changes to DGCLICMR.                              *
.******************************************************************************
.* V10.02.01 09/05/2011  Davin             CAR 239539                         *
.*           Recompiled for changes to MRGPMIML - added savesin7/8/9          *
.* V10.01.01 02/02/2011  Steve Armstrong   CAR 237520                         *
.*           Mods to remove Detente interface (PATDETNT).                     *
.*           Also recompiled for changes to MRGPMIML.                         *
.******************************************************************************
.* V10.00.01 09/04/2010 Steve Armstrong   CAR 219045                          *
.*           Recompiled for changes PATMRGFD & MRGPMIML                       *
.*           Mods to populate PTMRSTIM.                                       *
.******************************************************************************
.* V9.12.01  05/11/2009  Davin         CAR 209534                             *
.*           Recompiled for MRGPMIML - added call to PMIGTNID                 *
.* V9.10.01  01/07/2008  Jill Habasque CAR 162660                             *
.*           Recompiled for MRGPMIML                                          *
.* V9.08.02  03/04/2007  Jill Habasque CAR 137207                             *
.*           Removed NMPI0000 - deleting of old nhimasaf record               *
.* V9.08.01  03/10/2006  Jill Habasque CAR 107243                             *
.*           Recompiled for MRGPMIML - fixed update if CMERGEUR set to 1      *
.* V9.07.01  19/09/2006  Ebon Clements CAR 107243                             *
.*           Recompiled for MRGPMIML - Deleted alerts audit                   *
.* V9.04.01  21/03/2005  Ebon Clements CAR 58443                              *
.*           Recompiled for MRGPMIML - (S)ecurity patient alerts              *
.* V9.03.04  27/04/2004  Jill Habasque CAR 48411                              *
.*           Fixed clearing of "mergeurs.txt" and "mergelog.txt"              *
.* V9.03.03  25/03/2004  Mike Laming  CAR 48411                               *
.*           Clean up test files "mergeurs.txt" and "mergelog.txt" and        *
.*           Change LOOP0000 to process PTMRSTAT more clearly & set PTMRSTAT  *
.*           to 3 or 4 if UR No missing or already merged to stop re-process  *
.* V9.03.02  29/10/2003 Sandra Barcham   43783                                *
.*           Recompiled for NZHISIIO and NZHISIUP                             *
.* V9.03.01  15/07/2003  CAR 37193   Mike Laming                              *
.*           Port V5.10 changes for PMS05 -                                   *
.*           Recompiled for changes to MRGPMIML                               *
.******************************************************************************
.* V9.02.00  06/02/2003  Jill H       SRF 36025                               *
.*           Ported from 509                                                  *
.******************************************************************************
.*        V5.10.04  21/08/2002  Guomin Fu    SRF 17469                        *
.*                  Recompiled for PATDINVS                                   *
.*        V5.10.03  20/08/2002  Tonii                                         *
.*                  Recompiled for PATDETNT - I*C                             *
.*        V5.10.02  12/08/2002  Steve Downing  SRF 14635                      *
.*                  Added ETHDES1-3 for changes to PATDETNT                   *
.*        V5.10.01  28/06/2002  Mike Laming  SRF 16830                        *
.*                  Recompiled for PATDINVS                                   *
.******************************************************************************
.*        V5.09.01  22/09/2001  Tonii SRF 13075                               *
.*                  Recompiled for PATDINVS                                   *
.******************************************************************************
.*        V5.08.03  18/08/2000  Jill Habasque                                 *
.*                  Added variables for patdetnt                              *
.*        V5.08.02  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  18.07.2000  Sandra Barcham                                *
.*                  Recompiled for version change                             *
.******************************************************************************
.*        V5.07.03  14/09/1999  Jill Habasque SRF 645173                      *
.*                  Recompiled for MRGUREOC                                   *
.*        V5.07.02  02/08/1999  Steve Armstrong                               *
.*                  Recompiled for changes to PATDETNT                        *
.*        V5.07.01  28/04/1999  Steve Armstrong                               *
.*                  Recompiled for Y2K mods to DGCLIMRG                       *
.*       V5.07.B02  13/01/1999  Jim Stathopoulos                              *
.*                  Fixed Global Compile Errors                               *
.******************************************************************************
.*        V5.05.02  08/01/1998  Steve Armstrong   SRF 643927                  *
.*                  Recompiled for MRGPMIML                                   *
.*        V5.05.01  15/10/1997  Steve Armstrong                               *
.*                  Recompiled for DGCLIMRG                                   *
.******************************************************************************
.*        V5.04.04  16/06/1997  Steve Armstrong                               *
.*                  Fixed bugs in global recompile                            *
.*        V5.04.03  09/04/1997  Howellsy     HWK                              *
.*                  fixed merging of patlinkf                                 *
.*                  Trap for IO errors                                        *
.*        V5.04.02  21/10/1996  Howellsy       EOC & ACC                      *
.*                  Added EOC & ACC.                                          *
.*        V5.04.01  03/10/1996  Steve Armstrong   SRF 642082                  *
.*                  Added check to see if mergeurs.txt exists at start of     *
.*                  program.                                                  *
.******************************************************************************
.*        V5.01.129 29/07/1993  ROD                                           *
.*                  Added NZHIS Donor stuff                                   *
.*        V5.01.130 06/12/1993  SOG                                           *
.*                  Fixed NZHIS                                               *
.*        V5.01.131 24/03/1994    Matt Surridge                               *
.*                  Recompile for NZHISIIO and DONORDET.                      *
.*        V5.01.132 25/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               *
.*        V5.01.133 10/08/1994  ROD                                           *
.*                  recompile for NZHISIIO                                    *
.*        V5.01.134 22/08/1994  ROD                                           *
.*                  ignore check if inconsistent Nat/Local data, just print   *
.*        V5.02.02  17/01/1995  Graeme Williams   SRF 134547                  *
.*                  Only try to merge records that have a status of "2"       *
.*        V5.02.03  29/03/1995  Whirlpool and dude   srf 640026               *
.*                  fixed the merge so that it now works                      *
.*                    - doesn't write to temp file twice for each record      *
.*                      that had a status of one                              *
.*                    - Checks the CORRECT nmpi number on national to see     *
.*                      if merged !                                           *
.*                    - doesn't delete both old and new nhimasaf records and  *
.*                      cancel the ur number on local system                  *
.*        V5.03.01  21/06/1995  Whirlpool and Dude                            *
.*                  Fixed merge to ignore patients without nhi numbers        *
.*        V5.03.02  01/08/1995  Greg Horvat      SRF 610897                   *
.*                  Recompiled for PATDINVS                                   *
.*        V5.03.03  24/10/1995  Steve Armstrong  SRF 611544                   *
.*                  Mods to update patcomaf & pataxeaf                        *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCOMM/INC
          INC       OUTCLIFD/INC
          INC       OUTPREFD/INC
          INC       BOKRC1FD/INC
          INC       PATALRFD/INC
          INC       PATAU1FD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATCSLFD/INC
          INC       PATDHEAD/INC 
          INC       PATDINVS/INC
          INC       PATDO1FD/INC
          INC       PATDNRFD/INC
          INC       PATDSCFD/INC
          INC       PATGSRFD/INC
          INC       PATHOSFD/INC
          INC       PATKWCFD/INC
          INC       PATLINFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATMRGFD/INC
          INC       NHIMASFD/INC
          INC       PATNIDFD/INC
          INC       PATPA1FD/INC
          INC       PATPR1FD/INC
          INC       PATURAFD/INC
          INC       PMIVARS/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       NZHISIFD/INC
          INC       PATSDNFD/INC
          INC       PATSMWFD/INC
          INC       SCRPSTFD/INC
          INC       WEBSECFD/INC
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
CHAINPRG  INIT      "IBAPMS05"         * Second merge program to change
KWCFIL    INIT      "patkwc"
.
TEMP1     FILE      ASCII, FIXED=17
.Name     Type      Length    Start
.-------  ----      ------    -----
OLDURNO   DIM       8        1
NEWURNO   DIM       8        9
.
.     End of Record            17
.
TEMP3     FILE      ASCII, FIXED=107
.Name     Type      Length    Start
.-------  ----      ------    -----
.OLDURNO   DIM       8        1
.NEWURNO   DIM       8        9
.CHOSPNUM  FORM      2        17
XXFILE     DIM       8        19
XXDESC     DIM       80       27
.     End of Record           107
DIM107     DIM       107
SEQ1       FORM      "-3"
LOGERR     FORM      "1"
.
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CHG       FORM      1
CMDLINE   DIM       50
FNAMER    DIM       8
FNAMEA    DIM       8
OLDCASE   DIM       20
RECCOUNT  FORM      5
SAVEURNO  DIM       8
SAVESIN1  DIM       1
SAVESIN7  DIM       1
SAVESIN8  DIM       1
SAVESIN9  DIM       1
SAVESN11  DIM       1
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBANHI10"
PRGNAM    INIT      "Auto Merge U/R 1"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000    CALL      INIT0000           * initialization
          BRANCH    EXIT,ML9999        * cannot open national link
.
          CALL      LOOP0000           * Loop over the patmrgaf file and write
.                                      * all accepted mergers to the temp file
          COMPARE   ZERO,RECCOUNT
          GOTO      ML9000 IF EQUAL
.
          CALL      HOSP0000           * Loop over the hospital file and exec.
.                                      * their RUN macro on the temp file data
.
. ------- position at end of error log ---------------------
.
          OPEN      TEMP3,FNAMEA
          READ      TEMP3,SEQ1;DIM107
          CALL      COMM0000           * Execute the mergers on the
.                                      * files common to all hospitals
          CALL      DISCNNCT           * disconnect from National system
ML9000    CALL      LFIN0000           * log finished to error file
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.**********************************************************************
INIT0000  OPEN      CONTROLF,"controlf"
.         CALL      SETX0000                * Set up all Common Var in STD002
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*138,PTCNNHII,*174,PTCNDONR
          CALL      IBACLOCK
.
          OPEN      PATALRT1,"patalrtf"
          OPEN      PATMWSA1,"patmwsaf"
          OPEN      PATDNRA1,"patdnraf"
          OPEN      PATHOSP1,"pathospf"
          OPEN      PATLINK1,"patlinkf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PATMRGG1,"patmrgaf"
          OPEN      PATPA1A1,"patpa1af"
          OPEN      PATCODE1,"patcodes"
.
          OPEN      NHIMASA1,"nhimasaf"
          OPEN      NHIMASA2,"nhimasaf"
.
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      WEBSECA2,"websecaf"
.
INIT9000  OPEN      PATURAD1,"paturadf"
          OPEN      PATURAD2,"paturadf"
.
          READ      CONTROLF,TEN;*244,CHOSPNUM
          READ      CONTROLF,TWENTY;*186,PTCNUCSL,*196,PTCNUCRD,PTCNUFML
          READ      CONTROLF,FIFTY9;*84,CMERGEUR,*86,CDETENTE,*92,CDETPATH:
                                    *234,PTCNNMPI
          READ      CONTROLF,SEVENTY7;*240,PTCNAXEU
          READ      CONTROLF,TEN;*250,CAUDA1
.
          IF        CAUDA1<>1
            OPEN      PATAUDAL,"pataudal"
            OPEN      PATAUDA2,"pataudal"
          ENDIF
.
          COMPARE   ONE,PTCNUCSL
          GOTO      INIT9100 IF NOT EQUAL
          OPEN      PATCSLA1,"patcslaf"
.
INIT9100  CLEAR     FNAMER
          APPEND    "mergeurs",FNAMER
          RESET     FNAMER
          REPLACE   " 0",FNAMER
.
          CALL      CONNECT
          IF        OVRCD=1
            MOVE      ONE,EXIT                    * don't continue
            GOTO      INIT9999
          ENDIF
.
........  See if mergeurs.txt exists, and if not, don't proceed         D-48411
.         See if mergeurs.txt exists, and if not, create a new one     *I-48411
.
          TRAP      INIT9200 IF IO
          OPEN      TEMP1,FNAMER
          TRAPCLR   IO
          CLOSE     TEMP1                                               D-48411
          PREP      TEMP1,FNAMER            * create "mergeurs.txt"    *I-48411
.
          CLEAR     FNAMEA
          APPEND    "mergelog",FNAMEA
          RESET     FNAMEA
          REPLACE   " 0",FNAMEA
.
........  See if mergelog.txt exists, and if not, don't proceed         D-48411
.         See if mergelog.txt exists, and if not, create a new one     *I-48411
.
INIT9110  TRAP      INIT9300 IF IO                        * add Label  *C-48411
          OPEN      TEMP3,FNAMEA
          TRAPCLR   IO
          CLOSE     TEMP3                                               D-48411
          PREP      TEMP3,FNAMEA                                        D-48411
.
INIT9120  CLOCK     TIME,CTIMEIS                          * add Label  *C-48411
          PACK      DIM8,CCC,CYY,CMM,CDD,SP10
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE 
          CLEAR     DIM107
          APPEND    "Merge Started at ",DIM107
          APPEND    CPCDATE,DIM107
          APPEND    SP2,DIM107
          APPEND    CTIMEIS,DIM107          
          APPEND    SP70,DIM107
          APPEND    SP70,DIM107
          RESET     DIM107
          WRITE     TEMP3,SEQ;DIM107
.
          GOTO      INIT9500
.
INIT9200  NORETURN                          * "mergeurs.txt" does not exist
....      MOVE      ONE,EXIT                                            D-48411
....      GOTO      INIT9999                                            D-48411
          PREP      TEMP1,FNAMER            * create "mergeurs.txt"    *I-48411
          GOTO      INIT9110                                           *I-48411
.
INIT9300  NORETURN
          DISPLAY   *P1:24,*EL,*B,"mergelog.txt does not exist. "
....      MOVE      ONE,EXIT                                            D-48411
....      GOTO      INIT9999                                            D-48411
          PREP      TEMP3,FNAMEA            * create "mergelog.txt"    *I-48411
          GOTO      INIT9120                                           *I-48411
.
. since batch merging, dont give warning of inconsistent Local/Nat data
.
INIT9500  MOVE      ONE,NZIBIGCK
.
          MOVE      ZERO,EXIT
.
INIT9999  RETURN
+
.**********************************************************************
.* LFIN0000 - Log finished in the error file
.**********************************************************************
LFIN0000  CLOCK     TIME,CTIMEIS
          PACK      DIM8,CCC,CYY,CMM,CDD,SP10
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE 
          CLEAR     DIM107
          APPEND    "Merge Finished at ",DIM107
          APPEND    CPCDATE,DIM107
          APPEND    SP2,DIM107
          APPEND    CTIMEIS,DIM107          
          APPEND    SP70,DIM107
          APPEND    SP70,DIM107
          RESET     DIM107
          WRITE     TEMP3,SEQ;DIM107
.
LFIN9999  RETURN
.**********************************************************************
.*                            LOOP0000                                *
.*                   Loop over the patmrgaf file                      *
.**********************************************************************
LOOP0000  OPEN      TEMP1,FNAMER                           * was PREP  *C-48411
          MOVE      ZERO,RECCOUNT
          PACK      KEY17,SP30              * file is keyed on Status
          CALL      RSPTMRG1
LOOP0100  CALL      RKPTMRG1
          BRANCH    OVRCD,LOOP9999
.
.******************************************** Start of modified code   *C-48411
          BRANCH    PTMRSTAT,LOOP1000:      * "requested"
                             LOOP2000:      * "approved"
                             LOOP3000:      * "merge processed"
                             LOOP4000       * "merge rejected"
          GOTO      LOOP0100                * not a valid status
.
.                   PTMRSTAT = 1 - Merge Requested
.
LOOP1000  CALL      VALD0000                * Validate the two U/R's on File
          BRANCH    EXIT,LOOP0100:          * exit = 1
                         LOOP0100:          * exit = 2 (not used)
                         LOOP1500:          * exit = 3 UR already merged
                         LOOP1500           * exit = 4 UR not on file
.
.         Status is only requested (PTMRSTAT = 1) so update to approve if OK
.
          IF        PTCNNHII = 1
            CALL      CNAT0000              * Check national system
            IF        EXIT = 0
              PACK      KEY17,PTMRSTAT,PTMROLUR,PTMRNWUR   * old key
.
              PACK      PTMRSDTE,CCC,CYY,CMM,CDD  * Last Status Change Date
              REP       " 0",PTMRSDTE
              CLOCK     TIME,CTIMEIS
              MOVE      CTIMEIS,PTMRSTIM          * Last Status Change Time
              REP       " 0",PTMRSTIM
              MOVE      TWO,PTMRSTAT             * set local sys to approved
              CALL      UPPTMRG1
.
              CALL      RSPTMRG1                  * reposition on old key
            ENDIF
          ENDIF
.
          GOTO      LOOP0100
.
.                               * EXIT = 3 or 4 was returned from VALD0000
.                               * - Set PTMRSTAT accordingly
LOOP1500  MOVE      ZERO,OVRCD
          PACK      KEY17,PTMRSTAT,PTMROLUR,PTMRNWUR   * old key
          PACK      PTMRSDTE,CCC,CYY,CMM,CDD  * Last Status Change Date
          REP       " 0",PTMRSDTE
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,PTMRSTIM          * Last Status Change Time
          REP       " 0",PTMRSTIM
          MOVE      EXIT,PTMRSTAT             * set to VALD0000 exit value
          CALL      UPPTMRG1
          CALL      RSPTMRG1                  * reposition on old key
          GOTO      LOOP0100
.
.
.                   PTMRSTAT = 2 - Merge Approved
.
LOOP2000  CALL      VALD0000                * Validate the two U/R's on File
          BRANCH    EXIT,LOOP0100:          * exit = 1
                         LOOP0100:          * exit = 2 (not used)
                         LOOP1500:          * exit = 3 UR already merged
                         LOOP1500           * exit = 4 UR not on file
.
.         As this loop has processed the status 1's and changed any appropriate
.         records to a status 2, we can now process correctly the status 2's
.
.           ---------------------- Not required as processed when PTMRSTAT = 1
.
....      IF        PTCNNHII = 1
....        CALL      CNAT0000            Check if merged on national
....        BRANCH    EXIT,LOOP0100       just to be sure as IBANHI03 can
....                                      set ptmrstat to 2 as well !!.
....      ENDIF
.
          CALL      WRTEMP1
          ADD       ONE,RECCOUNT
          GOTO      LOOP0100
.
.
.                   PTMRSTAT = 3 & 4 - processing not required
.
LOOP3000  MOVE      ZERO,EXIT
.
LOOP4000  MOVE      ZERO,EXIT
.********************************************   End of modified code   *C-48411
.
LOOP9999  RETURN
+
.**********************************************************************
.*                            VALD0000                                *
.*                  Validate two UR's on Master File                  *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          PACK      KEY8,PTMROLUR
          CALL      RDMAST1
          COMPARE   ZERO,OVRCD
          GOTO      VALD1000 IF EQUAL
.
          PRINT     *N,"UR Number ",PTMROLUR," no Longer on File";
          MOVE      FOUR,EXIT               * set Status = 4 (was ONE) *C-48411
          GOTO      VALD9999
.
VALD1000  COMPARE   ZERO,PSTAT
          GOTO      VALD2000 IF EQUAL
.
          PRINT     *N,"UR Number ",PTMROLUR," Already Merged";
          MOVE      THREE,EXIT              * set Status = 3 (was ONE) *C-48411
          GOTO      VALD9999
.
VALD2000  PACK      KEY8,PTMRNWUR
          CALL      RDMAST1
          COMPARE   ZERO,OVRCD
          GOTO      VALD3000 IF EQUAL
.
          PRINT     *N,"UR Number ",PTMRNWUR," no Longer on File";
          MOVE      FOUR,EXIT               * set Status = 4 (was ONE) *C-48411
          GOTO      VALD9999
.
VALD3000  COMPARE   ZERO,PSTAT
          GOTO      VALD9999 IF EQUAL
.
          PRINT     *N,"UR Number ",PTMRNWUR,"Already Merged";
          MOVE      THREE,EXIT              * set Status = 3 (was ONE) *C-48411
.
VALD9999  RETURN
+
.****************************************************************************
.*                               CNAT0000                                   *
.*                        Check the National Details                        *
.****************************************************************************
.
. check that the merge has been performed on the national system before
. we do the merge on the local system
.
CNAT0000  MOVE      ZERO,EXIT
          MOVE      SP20,NZIBNMPI
          MOVE      SP20,NZIBALNO
.
. check the merge status of the Alernative HCU Id (Old)
.
          PACK      KEY8,PTMROLUR
          CALL      RDNHMAS2
          BRANCH    OVRCD,CNAT9500              * ignore patient
          MOVE      NHMANMPI,NZIBNMPI
.
. get merge status on National system
.
          CALL      GETHCU
          BRANCH    OVRCD,CNAT9000
.
. If National system has approved the merge, set the status to "Merge Approved"
.
.
          MATCH     "M",NZIBMERG
          GOTO      CNAT5000 IF NOT EQUAL          
          MOVE      ZERO,EXIT               * merged on  national
          GOTO      CNAT9999
.
. merge flag not set on National System so ignore this patient merge
.
CNAT5000  MOVE      ONE,EXIT                      
          GOTO      CNAT9999
.
CNAT9000  PRINT     *N,"Error on NHI/MWS System HCU ID : ",NZIBNMPI,SP1:
                    NZIBERRN,SP1,NZIBERRD
          MOVE      ONE,EXIT
          GOTO      CNAT9999
.
CNAT9500  PRINT     *N,"Error: UR ",PTMROLUR," has no NHI master record."
          MOVE      ONE,EXIT
          GOTO      CNAT9999
.
CNAT9999  RETURN
+
.**********************************************************************
.*                            HOSP0000                                *
.*                 Execute Hospital Dependent Mergers                 *
.**********************************************************************
.
HOSP0000  PACK      KEY4,SP30
          CALL      RDSHOSP1
HOSP0100  CALL      RDKHOSP1
          BRANCH    OVRCD,HOSP9999
.
          CMATCH    ANSI,PTHOACTV
          GOTO      HOSP0100 IF EQUAL       * ignore inactive hospitals
.
          MOVE      HOSDESC,CNAME
          CLEAR     CMDLINE                 * Get Hospital dependent RUN macro
          APPEND    HOSRUN,CMDLINE          * and append merge program 1 space
          ENDSET    CMDLINE                 * after end of RUN macro
.
HOSP1000  CMATCH    SP1,CMDLINE
          GOTO      HOSP2000 IF NOT EQUAL
.
          BUMP      CMDLINE,-1
          GOTO      HOSP0100 IF EOS         * No RUN macro on file
          GOTO      HOSP1000
.
HOSP2000  APPEND    SP1,CMDLINE
          APPEND    CHAINPRG,CMDLINE        * Run Second Merger program to
          RESET     CMDLINE                 * merge hos. dependent files
.
          EXECUTE   CMDLINE,TASKID
          GOTO      HOSP0100
.
HOSP9999  RETURN
.
+
.**********************************************************************
.*  NMPI0000  :  fix local NHI master file                            *
.**********************************************************************
..NMPI0000  MOVE      "nhimasaf",XXFILE
..          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
..          PACK      KEY8,OLDURNO                  * delete old U/R record
..          CALL      DENHMAS2
.   Here we assume that the new ur nhi details are correct so we just delete
.   the old details.
.   At some point we may want to reload the new nhi details and update nhimasaf
.   with them.  
..          GOTO      NMPI9999
.
..NMPI9999  RETURN
+
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       OUTCLIIO/INC
          INC       OUTPREIO/INC
          INC       BOKRC1IO/INC
          INC       CALCAGE 
          INC       PATALRIO/INC
          INC       PATHOSIO/INC
          INC       PATGSRIO/INC
          INC       PATAU1IO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATLINIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPA1IO/INC
          INC       PATURAIO/INC
          INC       PATCSLIO/INC
          INC       PATKWCIO/INC
          INC       PATMRGIO/INC
          INC       PATDNRIO/INC
          INC       PATSDNIO/INC
          INC       PATSMWIO/INC
          INC       PATNIDIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       NHIMASIO/INC
          INC       WEBSECIO/INC
          INC       NZHISIIO
          INC       NZIBSRCH
          INC       PATSNDX
          INC       PATSNX2
          INC       PATSRCH
          INC       AGECHK
          INC       CLPATMAS
          INC       CLPMSPX2
          INC       PMIGTNID
          INC       MRGPMIML
OPEN0000
CLOS0000
GETSVAR   RETURN

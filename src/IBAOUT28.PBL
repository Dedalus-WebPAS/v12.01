.******************************************************************************
.* System         : Outpatient                                                *
.* Program        : IBAOUT28.PBL                                              *
.* Name           : Patient Day Therapy Schedule                              *
.******************************************************************************
.* Date           : 05/02/2009                                                *
.* Author         : Mike Laming                                               *
.* Function       :                                                           *
.* Modifications  :                                                           *
.*----------------------------------------------------------------------------*
.* V12.00.01 03/07/2025  Thanh T          TSK 0961623                         *
.*           Fixed issue with dupliacte written out to TEMPFILE file          *
.*----------------------------------------------------------------------------*
.* V11.04.01 30/09/2024  Bella Turco      TSK 0951716                         *
.*           Fixed blank UR in report when no appointments to show            *
.* V11.02.01 09/02/2022  Thanh T          TSK 0905641                         *
.*           Recompiled as OUTHEDFD changes                                   *
.* V10.14.01 28/08/2019  Thanh T.          TSK 0877294                        *
.*           Changed TIME0000 to fix the issue when the Hour is 00:MM, it     *
.*           should show as 12:MM AM and not 0:MM AM.                         *
.*----------------------------------------------------------------------------*
.*        V10.11.04 27/11/2017  Thanh Tieu     TSK 0821710                    *
.*                  Recompiled as PATMASTD changed                            *
.*        V10.11.03 08/09/2017  Thanh T.       TSK 0821710                    *
.*                  Audit Amission/outpatient visit comments                  *
.*        V10.11.02 07/09/2017  Thanh T.        TSK 0821710                   *
.*                  Audit Amission/outpatient visit comments                  *
.*        V10.11.01 18/07/2017  Richa Phogat   TSK 0834684                    *
.*                  Replaced table VISCMTFD by VISUCMFD                       *
.*        V10.07.02 02/02/2016  Ebon Clements  TSK 0813050                    *
.*                  Changed LOOPCNTR to a FORM 6                              *
.*        V10.07.01 30/10/2015  Ebon Clements  CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.04.04 14/07/2014  Ebon Clements  CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.04.03 17/03/2014  Peter Vela     CAR 296927                     *
.*                  Added Print Clinic Description validation                 *
.*        V10.04.02 10/02/2014  Jill Parkinson CAR 294658                     *
.*                  Removed *F from head on first page                        *
.*        V10.04.01 10/12/2013  Ebon Clements CAR 292956                      *
.*                  Added print of clinic for non detailed report             *
.*        V10.03.01 02/12/2013  Ebon Clements CAR 294658                      *
.*                  Print 110 space on the first line of page 1 to            *
.*                  trigger landscape printing - HEAD0000                     *
.*        V9.12.06  02/10/2009  Ebon Clements CAR 205398                      *
.*                  Added printed date/time and page number                   *
.*        V9.12.05  10/08/2009  Ebon Clements CAR 202136                      *
.*                  Fixed print of appointment details if no pmsvx1af record  *
.*        V9.12.04  03/08/2009  Ebon Clements CAR 202136                      *
.*                  Fixed patient name format and current ward/bed length     *
.*        V9.12.03  27/07/2009  Ebon Clements CAR 202136                      *
.*                  Added cat code description for OTXSCO01 and OTXSCO02      *
.*        V9.12.02  15/07/2009  Ebon Clements CAR 202136                      *
.*                  Added detailed and by clinic Id/Type options              *
.*        V9.12.01  18/06/2009  Ebon Clements CAR 199193                      *
.*                  Added visit number to temp file key                       *
.*        V9.11.02  05/05/2009  Davin         CAR 189099                      *
.*                  Changed program name to IBAOUT28                          *
.*        V9.11.01  16/04/2009  Mike Laming   CAR 194034                      *
.*                  Correct sequence of Appointments - needs to check Slot time
.*        V9.11.00  05/02/2009  Mike Laming   CAR 189099                      *
.*                  New Program                                               *
.******************************************************************************
+
          INC       STD001FD
.
          INC       ALLPCTFD/INC            * Care team
          INC       IBACONFD/INC            * Control file
          INC       IBASEQFD/INC
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Control file
          INC       PMSUCHFD/INC            * U/R Comments
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       VISMDTFD/INC            * Visit Comments
          INC       VISMTXFD/INC
          INC       PATMISTD/INC
          INC       PATDO1FD/INC            * Doctors File
          INC       PATHSPFD/INC            * Hospital File
          INC       PATMA1FD/INC            * Master file
          INC       PATMI1FD/INC            * Admission File
          INC       PMSHCPFD/INC            * HCP file
          INC       PMSTSPFD/INC            * Transport file
          INC       PMSVX1FD/INC            * Visit Extn file
          INC       OUTBOAFD/INC            * Clinic Session Booking Table A
          INC       OUTBB1FD/INC            * Clinic Session Booking Table B
          INC       OUTCTYFD/INC            * Outpatient Clinic Type Table
          INC       OUTCLIFD/INC            * Outpatient Clinic file
          INC       OUTCMNFD/INC            * Outpatient system comments
          INC       OUTCONFD/INC            * Outpatient Control file
          INC       OUTHEDFD/INC            * Outpatient Slot Template Headers
          INC       OUTSESFD/INC            * Outpat Session
          INC       OUTSIPFD/INC            * Outpat Site Specific Categories
          INC       OUTSITFD/INC            * Site file
          INC       OUTXSCFD/INC            * Outpatient Extra Screens
          INC       TFILEVAR/INC
          INC       VISUCMFD/INC            * Visit Based Comments
          INC       WEBERRFD/INC   
.
. ----- Program Variables -----
.
TEMPFILE  IFILE     SQL, FIXED=171, KEY="U1-8,9-16,17-21,112-119"
TEMPFIL1  IFILE     SQL, FIXED=171, KEY="U120-122,123-125,126-145,146-171,1-8,9-16,17-21,112-119"
.
TEMPURNO  DIM       8        1              * U/R Number
TEMPDATE  DIM       8        9              * Booking date
TEMPTIME  DIM       5        17             * Slot time
TEMPLINE  DIM       90       22             * Print line  (Summary option only)
TEMPVISN  DIM       8        112            * Visit number 
TEMPAWRD  DIM       3        120            * Ward if current inpatient
TEMPABED  DIM       3        123            * Bed if current inpatient
TEMPSNAM  DIM       20       126            * Surname
TEMPGNAM  DIM       25       146            * Given Name
.
.         End-of-record      171  
.
APPTCNT   FORM      5
.
CAREDOCT  DIM       30
CDYSDAYS  FORM      6                       * CALCDAYS no of days
CDYSFDTE  DIM       8                       * CALCDAYS from date (ccyymmdd)
CDYSTDTE  DIM       8                       * CALCDAYS to date (ccyymmdd)
CDYSYEAR  FORM      2                       * CALCDAYS year
CLINICID  DIM       6
CLINICTY  DIM       6
CMDLINE   DIM       127
CURDATE   DIM       8                       * Current date (ccyymmdd)
CURRWBED  DIM       7
.
DAYNAM3   DIM       3
DETAILOP  DIM       1                       * Y = Detailed, N = Summary
DIM2A     DIM       2
DIM8      DIM       8
DIM2B     DIM       2
DIM10A    DIM       10
DIM14     DIM       14
DIM15A    DIM       15
DIM15B    DIM       15
DIM25     DIM       25
DIM25A    DIM       25
DIM40     DIM       40
DIM55     DIM       55
DSPEDDTE  DIM       10                      * Display end date (dd/mm/ccyy)
DSPERDTE  DIM       10                      * Display exp return date (dd/mm/yy)
DSPLEDTE  DIM       10                      * Display leave date (dd/mm/yy)
DSPREDTE  DIM       10                      * Display return date (dd/mm/yy)
DSPSTDTE  DIM       10                      * Display start date (dd/mm/ccyy)
.
ENDDATE   DIM       8                       * End date (ccyymmdd)
ENDDTIME  DIM       5
ENDPOS    FORM      3
FRMTTIME  DIM       7
FULNAME   DIM       40
HOSNAME   DIM       40
HOUR      FORM      2  
LINE      DIM       55
LOOPCNTR  FORM      6
MIN       DIM       2
MINUTES   FORM      3
MINTIME   FORM      2
MTHNAM3   DIM       3
.
PGNO      FORM      3
PRNTLINE  DIM       90
PRCLNDSC  DIM       1
.
SAVDATE   DIM       8
SAVEURNO  DIM       8
SIGNREQD  DIM       1
SITE      DIM       6 
STRTDATE  DIM       8                       * Start date (ccyymmdd)
.
TEMP55    DIM       55
TIME      FORM      3
TEMPNAME  DIM       8
.
URNUMBER  DIM       8
WORKTIME  DIM       5
.
. ----- Program Constants -----
.
AM        INIT      "am"
AST45     INIT      "*********************************************"
DSH45     INIT      "---------------------------------------------"
EQUAL45   INIT      "============================================="
LITCLINC  INIT      "Clinic"
LITCLINT  INIT      "Clinic Type"
LITPRTNM  INIT      "| PRINT NAME"
LITSIGN   INIT      "Signature"
PIPE      INIT      "|"
PM        INIT      "pm"
RESCHD    INIT      "Rescheduled from "
SP14      INIT      "              "
SP18      INIT      "                  "
SP19      INIT      "                   "
.
.
MON       INIT      "Mon"
TUE       INIT      "Tue"
WED       INIT      "Wed"
THU       INIT      "Thu"
FRI       INIT      "Fri"
SAT       INIT      "Sat"
SUN       INIT      "Sun"
JAN       INIT      "Jan"
FEB       INIT      "Feb"
MAR       INIT      "Mar"
APR       INIT      "Apr"
MAY       INIT      "May"
JUN       INIT      "Jun"
JUL       INIT      "Jul"
AUG       INIT      "Aug"
SEP       INIT      "Sep"
OCT       INIT      "Oct"
NOV       INIT      "Nov"
DEC       INIT      "Dec"
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBAOUT28"
PRGNAM    INIT      "Patient Day Therapy Schedule"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9999
.
          CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,ML1000
.
          COMPARE   TWO,OPTION              * By clinic id and type
          GOTO      ML3000 IF EQUAL

          CALL      KYUR0000                * Keyin UR No.
          BRANCH    EXIT,ML1000
.
          CALL      SIGN0000                * Keyin if Signature required
          BRANCH    EXIT,ML1000
.
          CALL      DETL0000                * Keyin if Summary or Detailed
          BRANCH    EXIT,ML1000
.
          CALL      CDSC0000                * Keyin if Printing Clinic Desc
          BRANCH    EXIT,ML1000
.
          GOTO      ML7000
.
ML3000    CALL      KCTY0000                * keyin clinic type
          BRANCH    EXIT,ML1000
.
          CALL      KCLI0000                * keyin clinic id 
          BRANCH    EXIT,ML1000
.
          CALL      KHOS0000                * Keyin hospital
          BRANCH    EXIT,ML1000
.
ML7000    CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,ML8000,ML1000,ML1000
          GOTO      ML1000
.
ML8000    CALL      CREA0000                * Create temp file
.
          IF        OPTION=1
            CALL      REPT0000              * Process the report by U/R
          ELSE
            CALL      SESN0000              * Process the report by Clinic ID
          ENDIF                             * and Type
.
          CALL      PRNT0000                * Print the report
          CALL      KILL0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000             Called by: ML0000   *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"allpctaf";
          OPEN      ALLPCTA1,"allpctaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A8,"patmi1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
.
          DISPLAY   *P64:24,"outsipaf";
          OPEN      OUTSIPA1,"outsipaf"
.
          MOVE      IDDD,KEY6
          CALL      RDOTSIP1
.
          PACK      CFNAME WITH UID,FILBOKA1        * OUTBOKFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA3        * OUTBOKFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA3,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA6        * OUTBOKFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA6,CFNAME
.
          PACK      CFNAME WITH UID,FILBB1A1        * OUTBB1FD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBB1A1,CFNAME
.
          PACK      CFNAME WITH UID,FILCLIA1        * OUTCLIFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCMNA1        * OUTCMNFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCMNA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA1        * OUTCTYFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME WITH UID,FILHEDA1        * OUTHEDFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTHEDA1,CFNAME
.
          PACK      CFNAME WITH UID,FILSESA1        * OUTSESFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTSESA1,CFNAME
.
          PACK      CFNAME WITH UID,FILSESA6        * OUTSESFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTSESA6,CFNAME
.
          PACK      CFNAME WITH UID,FILXSCA1        * OUTXSCFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTXSCA1,CFNAME
.
          DISPLAY   *P64:24,"pmsuchaf";
          OPEN      PMSUCHA1,"pmsuchaf"
          DISPLAY   *P64:24,"pmsucdaf";
          OPEN      PMSUCDA1,"pmsucdaf"
.
          DISPLAY   *P64:24,"vismdtaf";
          OPEN      VISMDTA1,"vismdtaf"
          DISPLAY   *P64:24,"vismtxaf";
          OPEN      VISMTXA1,"vismtxaf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"pmstspaf";
          OPEN      PMSTSPA1,"pmstspaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"visucmaf";
          OPEN      VISUCMA1,"visucmaf"
.
          READ      CONTROLF,TWO;*4,CONAME
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          PACK      HOSNAME,CONAME,SP70
.
          CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPNAME
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          MOVE      ZERO,CPAGENO            * Reset the page no
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By U/R":
                    *P1:6,*V2LON,"2",*HOFF," = By Clinic Id and Type":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KYUR0000              Called by: ML0000   *
.*                            Keyin the UR Number                             *
.******************************************************************************
KYUR0000  MOVE      ZERO,EXIT
          MOVE      SP8,URNUMBER
          DISPLAY   *P1:9,"UR Number     : ";
          KEYIN     *P18:9,*EL,*V2LON,*JR,URNUMBER;
          MATCH     SP8,URNUMBER
          IF        @EQUAL | @EOS
            GOTO      KYUR9900
          ENDIF
.
          PACK      KEY8,URNUMBER
          CALL      RDMAST1                 * Read a master file record
          BRANCH    OVRCD,KYUR9000
.
KYUR5000  STRIP     PTITL
          SCAN      SP1,PGNAME
          ENDSET    PGNAME
          APPEND    SP70,PGNAME             * clear extra given names
          RESET     PGNAME
          STRIP     PGNAME
          STRIP     PSNAME
          PACK      FULNAME,PTITL,SP1,PGNAME,SP1,PSNAME,SP70
          GOTO      KYUR9999
.
KYUR9000  UNPACK    SP70,PRNTLINE
          CLEAR     PRNTLINE
          APPEND    "Invalid UR Number - ",PRNTLINE
          APPEND    URNUMBER,PRNTLINE
          RESET     PRNTLINE
          CALL      PRNT0000
.
KYUR9900  MOVE      ONE,EXIT
KYUR9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      ZERO,EXIT
          MOVE      "18",CCOL
          DISPLAY   *P1:11,"Starting Date : ",*EF:
                    *P1:12,"Ending Date   : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "11",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9900          * Nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          CALL      PACDATE                 * Format the start date
          MOVE      CPCDATE,DSPSTDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPSTDTE;
.
. ----- Keyin end date -----
.
DATE2000  MOVE      "12",CVERT
          UNPACK    SP70,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          CALL      PACDATE                 * Format the end date
          MOVE      CPCDATE,DSPEDDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPEDDTE;
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must be after the start date. ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
          GOTO      DATE9999
.
DATE9900  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  SIGN0000              Called by: ML0000   *
.*                         Keyin Y/N for Signature Required                   *
.******************************************************************************
SIGN0000  MOVE      ZERO,EXIT
          MOVE      SP1,SIGNREQD
          DISPLAY   *P1:14,"Signature Required ? (Y/N) : ";
          KEYIN     *P30:14,*EL,*V2LON,SIGNREQD;
          REP       UPPLOW,SIGNREQD
          DISPLAY   *P30:14,*EL,*V2LON,SIGNREQD
.
          MATCH     "Y",SIGNREQD
          GOTO      SIGN9999 IF EQUAL
          MATCH     "N",SIGNREQD
          GOTO      SIGN9999 IF EQUAL
          GOTO      SIGN0000
.
SIGN9900  MOVE      ONE,EXIT
SIGN9999  RETURN
+
.******************************************************************************
.*                                  CDSC0000              Called by: ML0000   *
.*                         Keyin Y/N for Clinic Description                   *
.******************************************************************************
CDSC0000  MOVE      ZERO,EXIT
          MOVE      SP1,PRCLNDSC
          DISPLAY   *P1:16,"Print Clinic Description ? (Y/N) : ";
          KEYIN     *P36:16,*EL,*V2LON,PRCLNDSC;
          REP       UPPLOW,PRCLNDSC
          DISPLAY   *P36:16,*EL,*V2LON,PRCLNDSC
.
          MATCH     "Y",PRCLNDSC
          GOTO      CDSC9999 IF EQUAL
          MATCH     "N",PRCLNDSC
          GOTO      CDSC9999 IF EQUAL
          GOTO      CDSC0000
.
CDSC9900  MOVE      ONE,EXIT
CDSC9999  RETURN
+

.******************************************************************************
.*                                  DETL0000              Called by: ML0000   *
.*                         Keyin Y/N for Detailed Option                      *
.******************************************************************************
DETL0000  MOVE      ZERO,EXIT
.
          MOVE      ANSN,DETAILOP
          DISPLAY   *P1:15,"Detailed Report? (Y/N)    : ";
          KEYIN     *P30:15,*EL,*V2LON,DETAILOP;
          REP       UPPLOW,DETAILOP
          DISPLAY   *P30:15,*EL,*V2LON,DETAILOP
.
          MATCH     "Y",DETAILOP
          GOTO      DETL9999 IF EQUAL
.
          MATCH     "N",DETAILOP
          GOTO      DETL9999 IF EQUAL
.
          GOTO      DETL0000
.
DETL9900  MOVE      ONE,EXIT
DETL9999  RETURN
+
.*****************************************************************************
.*                                CREA0000                                   *
.*              Create the patient day file that doesn't exist               *
.*****************************************************************************
CREA0000  CALL      KILL0000
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TEMPNAME,CMDLINE
          APPEND    " 171 U1-8,9-16,17-21,112-119 ",CMDLINE
          APPEND    "U120-122,123-125,126-145,146-171,1-8,9-16,17-21",CMDLINE
          APPEND    ",112-119",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TFILNAME
          OPEN      TEMPFIL1,TFILNAME
.
CREA9999  RETURN
+
.******************************************************************************
.*                                  KILL0000                                  *
.*                 Kill the indexed files if they already exist               *
.******************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TEMPNAME,CMDLINE        * temp file for admission numbers
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.******************************************************************************
.*                                  REPT0000              Called by: ML0000   *
.*                              Process the Report                            *
.******************************************************************************
REPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,APPTCNT
.
          PACK      KEY36,URNUMBER,STRTDATE,SP70 * Clinic Session Booking Tab.A
          CALL      RDSBOKA3
REPT1000  CALL      RDKBOKA3
          BRANCH    OVRCD,REPT9000
.
          MATCH     OBAURNO,URNUMBER
          GOTO      REPT9000 IF NOT EQUAL
.
.                    Status  Booked   N/Used   Ex.Slot  Attended
          BRANCH    OBASTAT,REPT2000,REPT1000,REPT1000,REPT2000
          GOTO      REPT1000
.
REPT2000  MATCH     STRTDATE,OBADATE        * check against date range
          GOTO      REPT1000 IF LESS
          MATCH     OBADATE,ENDDATE
          GOTO      REPT9000 IF LESS
.
          IF        OBASTAT = 4
            MATCH     "Y",SIGNREQD
            GOTO      REPT1000 IF NOT EQUAL
            MATCH     OBADATE,STRTDATE      * select "Attended"
            GOTO      REPT1000 IF NOT LESS
          ENDIF
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1                 * Clinic Session Booking Table B
          BRANCH    OVRCD,REPT1000
.
          CALL      CLRT0000                * Clear temp file fields
.
          PACK      KEY8,OBAURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,REPT1000
.
          PACK      KEY17,OBAURNO,TWO,SP20
          CALL      RSPTMI18
          CALL      RKPTMI18
          BRANCH    OVRCD,REPT2500          * not on file
.
          MATCH     OBAURNO,AURNO           * same U/R Number?
          GOTO      REPT2500 IF NOT EQUAL   * No
.
          COMPARE   TWO,ASTAT
          GOTO      REPT2500 IF NOT EQUAL
.
          GOTO      REPT2600
.
REPT2500  CALL      CLPATMIS
.
REPT2600  MATCH     ANSY,DETAILOP           * Detailed Report
          GOTO      REPT5000 IF EQUAL
.                                           * get Clinic Type
          MOVE      SP70,OCTDESC
          PACK      KEY12,OBASITE,OBACTYP,SP70
          CALL      RDCTYA1                 * Clinic Type Table
.                                           * set up the print line
REPT3000  UNPACK    OBATIME,DIM2A,DIM1,DIM2B
          MOVE      DIM2A,F2
          MOVE      F2,DIM2A
          PACK      DIM10A,SP3,DIM2A,DIM1,DIM2B,AM
          IF        F2 = 12
            PACK      DIM10A,SP3,DIM2A,DIM1,DIM2B,PM
          ENDIF
          IF        F2 > 12
            SUB       "12",F2
            MOVE      F2,DIM2A
            PACK      DIM10A,SP3,DIM2A,DIM1,DIM2B,PM
          ENDIF
.
          MATCH     "Y",PRCLNDSC
          GOTO      REPT4500 IF NOT EQUAL
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE,Z70
          CALL      RDCLIA1
          IF        OVRCD=0
            GOTO      REPT4000
          ENDIF
.
          CALL      RDSCLIA1
          CALL      RDPCLIA1
          BRANCH    OVRCD,REPT3900
.
          MATCH     OCASITE,OBASITE
          GOTO      REPT3900 IF NOT EQUAL
.
          MATCH     OCACLIN,OBACLIN
          GOTO      REPT3900 IF NOT EQUAL
.
          GOTO      REPT4000
.
REPT3900  MOVE      SP70,OCADESC   
.
REPT4000  PACK      DIM25,OCADESC,SP70
          PACK      DIM25A,OCTDESC,SP70
          MATCH     "Y",SIGNREQD
          IF        @EQUAL
            PACK      TEMPLINE,SP1,DIM10A,SP4,DIM25A,SP1,DIM25,PIPE,SP70
          ELSE
            PACK      TEMPLINE,SP1,DIM10A,SP4,DIM25A,SP4,DIM25,SP70
          ENDIF
.
          GOTO      REPT5000
.
REPT4500  PACK      DIM25A,OCTDESC
          MATCH     "Y",SIGNREQD
          IF        @EQUAL
            PACK      TEMPLINE,SP1,DIM10A,SP4,DIM25A,PIPE,SP70
          ELSE
            PACK      TEMPLINE,SP1,DIM10A,SP4,DIM25A,SP70
          ENDIF
.
REPT5000  PACK      TEMPURNO,OBAURNO,SP10
          PACK      TEMPDATE,OBADATE,SP10
          PACK      TEMPTIME,OBATIME,SP10
          PACK      TEMPVISN,OBAOUTNO,SP10
          PACK      TEMPSNAM,PSNAME,SP10
          PACK      TEMPGNAM,PGNAME,SP10
          PACK      TEMPAWRD,AWARD,SP10
          PACK      TEMPABED,ABED,SP10
          PACK      KEY29,TEMPURNO,TEMPDATE,TEMPTIME,TEMPVISN
          CALL      WRTEMP1                 * write printline to Temp file
.
          ADD       ONE,APPTCNT
          GOTO      REPT1000
.
REPT9000  MOVE      ZERO,EXIT
          PACK      TEMPURNO,URNUMBER
.
REPT9999  RETURN
+
.******************************************************************************
.*                                  SESN0000              Called by: ML0000   *
.*                              Process the Report                            *
.******************************************************************************
SESN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,APPTCNT
          MOVE      ANSN,SIGNREQD
          MOVE      ANSY,DETAILOP
.
          PACK      KEY25,SITE,STRTDATE,SP70
          CALL      RDSSESA6
SESN0500  CALL      RDKSESA6
          BRANCH    OVRCD,SESN9999
.
          MATCH     SITE,OSESITE
          GOTO      SESN9999 IF NOT EQUAL
.
          MATCH     OSEDATE,ENDDATE
          GOTO      SESN9999 IF LESS
.
          MATCH     SP70,CLINICID
          IF        !@EQUAL
            MATCH     OSECLIN,CLINICID
            GOTO      SESN0500 IF NOT EQUAL
          ENDIF
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,SESN0500
.
          IF        IBCNMHOS=1
            MATCH     SP70,PTHSHOSP
            IF        !@EQUAL
              MATCH     PTHSHOSP,OTHEHOSP
              GOTO      SESN0500 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     SP70,CLINICTY
          IF        !@EQUAL
            MATCH     OTHECTYP,CLINICTY
            GOTO      SESN0500 IF NOT EQUAL
          ENDIF
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70 
          CALL      RDSBOKA1
SESN1000  CALL      RDKBOKA1
          BRANCH    OVRCD,SESN9000
.
          MATCH     OSESITE,OBASITE 
          GOTO      SESN0500 IF NOT EQUAL
.
          MATCH     OSECLIN,OBACLIN 
          GOTO      SESN0500 IF NOT EQUAL
.
          MATCH     OSEDATE,OBADATE 
          GOTO      SESN0500 IF NOT EQUAL
.
          MATCH     OSESTRT,OBASTRT 
          GOTO      SESN0500 IF NOT EQUAL
.
.                    Status  Booked   N/Used   Ex.Slot  Attended
          BRANCH    OBASTAT,SESN2000,SESN1000,SESN1000,SESN2000
          GOTO      SESN1000
.
SESN2000  MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1                 * Clinic Session Booking Table B
          BRANCH    OVRCD,SESN1000
.
          CALL      CLRT0000                * Clear temp file fields
.
          PACK      KEY8,OBAURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,SESN1000
.
          PACK      KEY17,OBAURNO,TWO,SP20
          CALL      RSPTMI18
          CALL      RKPTMI18
          BRANCH    OVRCD,SESN2500          * not on file
.
          MATCH     PURNO,AURNO           * same U/R Number?
          GOTO      SESN2500 IF NOT EQUAL   * No
.
          COMPARE   TWO,ASTAT
          GOTO      SESN2500 IF NOT EQUAL
.
          GOTO      SESN2600
.
SESN2500  CALL      CLPATMIS
.
SESN2600  PACK      KEY29,OBAURNO,OBADATE,OBATIME,OBAOUTNO,SP70
          CALL      RDTEMP1
          IF        OVRCD = 0
            GOTO      SESN1000 
          ENDIF
.             
          PACK      TEMPURNO,OBAURNO,SP10
          PACK      TEMPDATE,OBADATE,SP10
          PACK      TEMPTIME,OBATIME,SP10
          PACK      TEMPVISN,OBAOUTNO,SP10
          PACK      TEMPSNAM,PSNAME,SP10
          PACK      TEMPGNAM,PGNAME,SP10
          PACK      TEMPAWRD,AWARD,SP10
          PACK      TEMPABED,ABED,SP10
          PACK      KEY29,TEMPURNO,TEMPDATE,TEMPTIME,TEMPVISN
.
          CALL      WRTEMP1                 * write printline to Temp file
.
          ADD       ONE,APPTCNT
          GOTO      SESN1000
.
SESN9000  MOVE      ZERO,EXIT
.
SESN9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: REPT0000 *
.*                             Print Report Header                            *
.******************************************************************************
PRNT0000  MOVE      ZERO,EXIT               * Print report header
          MOVE      "99",CLNO
          MOVE      ZERO,PGNO
          MOVE      ZERO,APPTCNT
          MOVE      SP8,SAVDATE
          MOVE      SP8,SAVEURNO
          DISPLAY   *P1:24,*EL,"Printing : ";
.
          MATCH     ANSY,DETAILOP
          IF        @EQUAL
            PACK      KEY80,SP30
          ELSE
            PACK      KEY29,SP30
          ENDIF
.
          MATCH     ANSY,DETAILOP
          IF        @EQUAL
            CALL      RSTEMP2
          ELSE
            CALL      RSTEMP1
          ENDIF
.
PRNT1000  MATCH     ANSY,DETAILOP
          IF        @EQUAL
            CALL      RKTEMP2
          ELSE
            CALL      RKTEMP1
          ENDIF
          BRANCH    OVRCD,PRNT9000
.
          MATCH     ANSY,DETAILOP             * Detailed Report
          IF        @EQUAL
            MATCH     TEMPURNO,SAVEURNO
            CALL      HEAD0000 IF NOT EQUAL   * Print change of patient
            MOVE      TEMPURNO,SAVEURNO
          ELSE
            MATCH     TEMPDATE,SAVDATE
            CALL      DTPL0000 IF NOT EQUAL   * set up Date print line
          ENDIF
.
          MATCH     ANSY,DETAILOP             * Detailed Report
          IF        @EQUAL
            COMPARE   "50",CLNO
            CALL      HEAD0000 IF NOT LESS
          ELSE
            COMPARE   "56",CLNO
            CALL      HEAD0000 IF NOT LESS
          ENDIF
.
          MATCH     ANSY,DETAILOP           * Detailed Report
          IF        @EQUAL
            CALL      PRDT0000              * Print detailed report lines
          ELSE
            PRINT     *1,DSH45,DSH45
            PRINT     *1,TEMPLINE
            ADD       TWO,CLNO
          ENDIF
.
          ADD       ONE,APPTCNT
.
          GOTO      PRNT1000
.
PRNT9000  IF        APPTCNT = 0
            CALL      HEAD0000
            PRINT     *1,AST45,AST45
            PRINT     *1,"No Appointments to print"
          ENDIF
.
          MATCH     ANSY,DETAILOP
          IF        @EQUAL
            PRINT     *1,EQUAL45,EQUAL45
          ELSE
            PRINT     *1,AST45,AST45
          ENDIF
.
PRNT9999  RETURN
+
.******************************************************************************
.*                                  PRDT0000              Called by: PRNT0000 *
.*                             Print Detailed Report Line                     *
.******************************************************************************
PRDT0000  PACK      KEY36,TEMPVISN,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,PRDT9999
.
          MATCH     DOBAOUTN,TEMPVISN
          GOTO      PRDT9999 IF NOT EQUAL
.
          MATCH     OBAURNO,TEMPURNO
          GOTO      PRDT9999 IF NOT EQUAL
.
          PACK      KEY8,TEMPVISN,SP70
          CALL      RDBOKB1 
          BRANCH    OVRCD,PRDT9999
.
          PACK      KEY8,TEMPVISN,SP70
          CALL      RDPMVX11
          IF        OVRCD=1
            CALL      CLPMSVX1
          ENDIF
.
          PACK      KEY8,TEMPVISN,SP70
          CALL      RDOTXSC1
          IF        OVRCD=1
            CALL      CLOUTXSC
          ENDIF
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,PRDT9999
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT:
                          OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,PRDT9999
.
          PACK      KEY20,OSESITE,OSECLIN,OSEDATE,Z70
          CALL      RDCLIA1
          IF        OVRCD=0
            GOTO      PRDT1500
          ENDIF
.
          CALL      RDSCLIA1
          CALL      RDPCLIA1
          BRANCH    OVRCD,PRDT9999
.
          MATCH     OCASITE,OSESITE
          GOTO      PRDT9999 IF NOT EQUAL
.
          MATCH     OCACLIN,OSECLIN
          GOTO      PRDT9999 IF NOT EQUAL
.
PRDT1500  UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      DAYOFWEK
          LOAD      DAYNAM3,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
.
          CALL      STDUR000                * Calculate booking end time
.
          MATCH     OBADATE,SAVDATE
          IF        !@EQUAL
            PRINT     *1,EQUAL45,EQUAL45
            MOVE      OBADATE,SAVDATE
          ELSE
            PRINT     *1,DSH45,DSH45
          ENDIF
          ADD       ONE,CLNO
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      OBATIME,WORKTIME
          CALL      TIME0000
          PRINT     *V2LON,*1,CPCDATE,*12,FRMTTIME,SP1;
.
          MOVE      ENDDTIME,WORKTIME
          CALL      TIME0000
          PRINT     *V2LON,FRMTTIME,*28,DAYNAM3:
                    *32,OCADESC,*HOFF,*68,OBACTYP,*75,OTXSTIM1
          ADD       ONE,CLNO
.
          MOVE      SP70,VSMTCMNT
          CALL      VSCMNT00
.
          PRINT     *1,VSMTCMNT
          ADD       ONE,CLNO
.
          PACK      KEY19,TEMPURNO,TEMPVISN,Z70
          CALL      RSPMTSP1
          CALL      RPPMTSP1
          BRANCH    OVRCD,PRDT2000
.
          MATCH     TEMPURNO,PMTSURNO
          GOTO      PRDT2000 IF NOT EQUAL
.
          MATCH     TEMPVISN,PMTSVISN
          GOTO      PRDT2000 IF NOT EQUAL
.
          UNPACK    PMTSTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PACK      KEY17,TEMPVISN,ZERO,ZERO,SEVEN,PMTSUNIQ,SP2,ONE,SP70
          CALL      RDVSUCM1
          IF        OVRCD=1
            PACK      VSUCLNUM,SP70,SP70
          ENDIF
.
          MOVE      VSUCLINE,KEY62
          MOVE      PMTSTTIM,KEY5
.
          PRINT     *1,CPCDATE,*12,KEY5,SP1,KEY62
          ADD       ONE,CLNO
.
          GOTO      PRDT3000
.
PRDT2000  PRINT     *N
          ADD       ONE,CLNO
.
PRDT3000  MOVE      SP70,KEY28
          CLEAR     KEY28
.
          MATCH     SP70,OTSPXC01
          IF        !@EQUAL
            PACK      KEY5,OTSPXC01,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTXSCO01,TDESC
            ENDIF
            STRIP       TDESC
            APPEND      TDESC,KEY28
            APPEND      " : ",KEY28
          ENDIF
.
          MATCH     SP70,OTXSCO01
          IF        !@EQUAL
            PACK      KEY5,OTSPXC01,OTXSCO01,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTXSCO01,TDESC
            ENDIF
            APPEND    TDESC,KEY28
          ENDIF
.
          PRINT     *1,KEY28;
.
          MOVE      SP70,KEY28
          CLEAR     KEY28
.
          MATCH     SP70,OTSPXC02
          IF        !@EQUAL
            PACK      KEY5,OTSPXC02,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTXSCO02,TDESC
            ENDIF
            STRIP       TDESC
            APPEND      TDESC,KEY28
            APPEND      " : ",KEY28
          ENDIF
.
          MATCH     SP70,OTXSCO02
          IF        !@EQUAL
            PACK      KEY5,OTSPXC02,OTXSCO02,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTXSCO02,TDESC
            ENDIF
            APPEND    TDESC,KEY28
          ENDIF
.
          PRINT     *30,KEY28;
.
          MOVE      DNO,KEY3
          MATCH     "1",PMVXINTR
          IF        @EQUAL
            MOVE      DYES,KEY3
          ENDIF
.
          PRINT     *60,"Interpreter Req : ",KEY3
          ADD       ONE,CLNO
.
          STRIP     OTXSDSC1
          STRIP     OTXSDSC3
          STRIP     OTXSDSC5
          PRINT     *1,*+,OTXSDSC1,SP1,OTXSDSC2
          PRINT     *1,*+,OTXSDSC3,SP1,OTXSDSC4
          PRINT     *1,*+,OTXSDSC5,SP1,OTXSDSC6
          ADD       THREE,CLNO
.
PRDT9999  RETURN
.
.----------------------------------------------------------------------
. Retrieve the 1st comment text line of the latest active visit
. comment details
.----------------------------------------------------------------------
VSCMNT00  PACK      KEY17,TEMPVISN,VSCMTTYP,Z70      * Key for start of comment
          CALL      RSVSMDT1
VSCMNT20  CALL      RPVSMDT1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     TEMPVISN,VSMDVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSCMTTYP,VSMDTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT          * is comment deleted
          GOTO      VSCMNT20 IF EQUAL
.
. Retrieve the comment text line details
.
          MOVE      ZERO,F1
          PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
VSCMNT60  CALL      RKVSMTX1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      VSCMNT99 IF NOT EQUAL
.
VSCMNT99  RETURN
+
.------------------------------------------------------------
.                         STDUR000    called by: PRDT0000                
.              Calculate booking end time 
.------------------------------------------------------------
STDUR000  PACK      KEY5,ANSC,ANSV,OBAVISIT,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      ONE,TCFORM6
          ENDIF
          MOVE      ZERO,LOOPCNTR
.
          UNPACK    OBATIME,KEY2,KEY1,MIN   * HH:MM
STDUR100  MOVE      KEY2,HOUR               * HH to Form
          MOVE      MIN,MINUTES             * MM to Form
.         
          ADD       OTHEDURN,MINUTES   * Add Duration time to minutes
          ASSIGN    MINUTES-SIXTY,TIME    
          IF        TIME >= 0 
            IF        TIME >= 60
              MOVE      TIME,MINUTES
              ASSIGN    MINUTES-SIXTY,TIME
              MOVE      TIME,MINTIME
              MOVE      MINTIME,MIN
              ADD       TWO,HOUR                 * Add Two Hours
            ELSE
              MOVE      TIME,MINTIME
              MOVE      MINTIME,MIN
              ADD       ONE,HOUR                 * Add One Hour
            ENDIF
          ELSE
            MOVE      MINUTES,MINTIME            * No Hour Change
            MOVE      MINTIME,MIN
          ENDIF
.
          PACK      ENDDTIME,HOUR,COLON,MIN  * pack time save variable (HH:MM)
          REPLACE   " 0",ENDDTIME            * replace spaces with zero
.
          ADD       ONE,LOOPCNTR
.
          COMPARE   TCFORM6,LOOPCNTR      
          IF        @LESS
            UNPACK    ENDDTIME,KEY2,KEY1,MIN   * HH:MM
            GOTO      STDUR1000
          ENDIF
.
STDUR999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PRNT0000 *
.*                             Print Report Header                            *
.******************************************************************************
HEAD0000  MOVE      ZERO,EXIT
.
          IF        PGNO > 0
            PRINT     *1,DSH45,DSH45
          ELSE
            PRINT     *1,SP70,SP30,SP10     * Print 110 space to trigger hea
          ENDIF                             * landscape printing
.
          MOVE      SP8,KEY8
          ADD       "1",PGNO
          IF        PGNO > 1
            MOVE      "Page ",KEY5
            PACK      KEY8,KEY5,PGNO
          ENDIF
.
          MATCH     ANSY,DETAILOP           * Detailed Report
          GOTO      HEAD1000 IF EQUAL
.
          STRIP     FULNAME
          MOVE      TEMPURNO,DIM8
          SQUEEZE   DIM8
.
          IF        PGNO > 1
            PRINT     *F;
          ENDIF
.
          PRINT     *N,*N,*1,HOSNAME,*N:
                    *1,"DAY THERAPY SCHEDULE",*80,KEY8,*N,*N:
                    *1,"Appointment schedule ":
                       "for ",*+,FULNAME,SP1,LBRACK,"U/R ",*+,DIM8,RBRACK
.
          RESET     FULNAME
          RESET     DIM8
.
          MOVE      "6",CLNO
          GOTO      HEAD9000
.
HEAD1000  MATCH     SP70,TEMPABED
          IF        !@EQUAL
            PACK      CURRWBED,TEMPAWRD,SLASH,TEMPABED
          ELSE
            PACK      CURRWBED,TEMPAWRD,SP70
          ENDIF
.
          PACK        KEY8,TEMPURNO,SP70
          CALL        RDMAST1
          IF          OVRCD=1
            CALL        CLPATMAS
            MOVE        "Invalid U/R",PSNAME
          ENDIF
          SQUEEZE   PURNO
          STRIP     PSNAME
          STRIP     PGNAME
.
          PACK      KEY19,STRTDATE,SP70
          CALL      RSPCMNA1
HEAD1100  CALL      RKPCMNA1
          BRANCH    OVRCD,HEAD1140
.
          MATCH     OTCMFDAT,STRTDATE      * From date
          GOTO      HEAD1100 IF LESS
.
          MATCH     SP70,OTCMSITE         * Site Code
          IF        !@EQUAL
            MATCH     OTCMSITE,SITE
            GOTO      HEAD1100 IF NOT EQUAL
          ENDIF
.
          GOTO      HEAD1150
.
HEAD1140  MOVE      SP70,OTCMCOM1          * No valid site comment
.
HEAD1150  MOVE      SP70,PMUCCOMM
          MOVE      TEMPURNO,KEY8
          CALL      URCMNT00
.
          MOVE      SP70,CAREDOCT
          PACK      KEY26,TEMPURNO,SP1,ONE,SP70
          CALL      RSALPCT1
HEAD1200  CALL      RKALPCT1
          BRANCH    OVRCD,HEAD1300
.
          MATCH     TEMPURNO,ALPCURNO
          GOTO      HEAD1300 IF NOT EQUAL
.
          MATCH     " 1",ALPCTYPE
          GOTO      HEAD1300 IF NOT EQUAL
.
....      MATCH     "1",ALPCADFL
....      GOTO      HEAD1200 IF NOT EQUAL
.
          MOVE      SP70,PACFNAME
          PACK      KEY10,ALPCCODE,SP70
          CALL      RDPMHCP1
          IF        OVRCD<>1
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            PACK      CAREDOCT,PACFNAME,SP70
            RJUSTIFY  CAREDOCT
          ENDIF
.
HEAD1300  MOVE      CTIMEIS,WORKTIME
          CALL      TIME0000
.
          IF        PGNO > 1
            PRINT     *F;
          ENDIF
.
          PRINT     *N,*V2LON,*1,HOSNAME:
                    *49,"Printed: ",CDATE," @ ",FRMTTIME:
                    *N,*1,"Appointments for Dates: ",DSPSTDTE," - ",DSPEDDTE:
                    *60,CURRWBED,*71,"Page: ",PGNO:
                    *N,*50,CAREDOCT
.
          PRINT     *N,*V2LON,*1,"Patient Details : ",*+,PSNAME,SP1,PGNAME:
                    SP5,LBRACK,*+,PURNO,RBRACK:
                    *N,*19,PADD1:
                    *N,*19,PADD2:
                    *N,*19,PSUBRB:
                    *N,*19,PADD4,SP1,PPOST:
                    *N,*19,PTELEP:
                    *N,*19,PTMXCELL:
                    *N,*1,OTCMCOM1:
                    *N,*1,PMUCCOMM
.
          MOVE      "14",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
.
HEAD9999  RETURN
.
.-------------------------------------------------------------------------
. Retrieve the 1st U/R comment text line of the active U/R comment details
.-------------------------------------------------------------------------
URCMNT00  PACK      KEY17,KEY8,URCMTTYP,Z70
          CALL      RSPMUCH1
URCMNT10  CALL      RPPMUCH1
          BRANCH    OVRCD,URCMNT99
.
          MATCH     KEY8,PMUHURNO
          GOTO      URCMNT99 IF NOT EQUAL
.
          MATCH     URCMTTYP,PMUHCTYP
          GOTO      URCMNT99 IF NOT EQUAL
.
          MATCH     "1",PMUHDELT            * is comment deleted
          GOTO      URCMNT10 IF EQUAL
.
. Retrieve the comment text line details
.
          PACK      KEY20,PMUHURNO,PMUHCTYP,PMUHCNUM,SP70
          CALL      RSPMUCD1
          CALL      RKPMUCD1
          BRANCH    OVRCD,URCMNT99
.
          MATCH     PMUHURNO,PMUCURNO
          GOTO      URCMNT99 IF NOT EQUAL
.
          MATCH     PMUHCTYP,PMUCCTTY
          GOTO      URCMNT99 IF NOT EQUAL
.
          MATCH     PMUHCNUM,PMUCCNUM
          GOTO      URCMNT99 IF NOT EQUAL 
.
URCMNT99  RETURN
+
.******************************************************************************
.*                                  DTPL0000              Called by: REPT0000 *
.*                           Set up Date print line                           *
.******************************************************************************
DTPL0000  MOVE      ZERO,EXIT
          UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DAYOFWEK
          LOAD      DAYNAM3,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
.
          UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          PACK      DIM15A,DAYNAM3,SP1,CDAY,SP1,MTHNAM3,SP10
          MOVE      "Clinic Type",DIM15B
.
          MATCH     "Y",PRCLNDSC
          GOTO      DTPL5000 IF NOT EQUAL
.
          MATCH     "Y",SIGNREQD
          IF        @EQUAL
            PACK      PRNTLINE,DIM15A,LITCLINT,SP15,LITCLINC,SP19,LITPRTNM:
                               SP1,LITSIGN,SP70
          ELSE
            PACK      PRNTLINE,DIM15A,LITCLINT,SP18,LITCLINC,SP70
          ENDIF
.
          GOTO      DTPL7000
.
DTPL5000  MATCH     "Y",SIGNREQD
          IF        @EQUAL
            PACK      PRNTLINE,DIM15A,LITCLINT,SP14,LITPRTNM,SP15,LITSIGN,SP70
          ELSE
            PACK      PRNTLINE,DIM15A,LITCLINT,SP70
          ENDIF
.
DTPL7000  COMPARE   "50",CLNO               * check position on page
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,AST45,AST45          * print the Date information
          PRINT     *1,PRNTLINE
          ADD       TWO,CLNO
.
          MOVE      TEMPDATE,SAVDATE
.
DTPL9999  RETURN
+
.******************************************************************************
.*                                  CLRT0000              Called by: REPT0000 *
.*                           Clear temp file fields                           *
.******************************************************************************
CLRT0000  MOVE      SP70,TEMPURNO
          MOVE      SP70,TEMPDATE
          MOVE      SP70,TEMPTIME
          MOVE      SP70,TEMPLINE
          MOVE      SP70,TEMPVISN
          MOVE      SP70,TEMPAWRD
          MOVE      SP70,TEMPABED
          MOVE      SP70,TEMPSNAM
          MOVE      SP70,TEMPGNAM
.
CLRT9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000                                  *
.*                      Keyin hospital ID                                     *
.******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          MOVE       SP70,PTHSHOSP
          DISPLAY    *P1:15,*EL,"Hospital Id   : ";
          MOVE       TEN8,ECOL
          MOVE       TEN5,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ONE,CKYIMAND           * Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS9000,KHOS9000
.
KHOS3000  DISPLAY   *P22:15,*V2LON,PTHSNAME;
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
+
.**************************************************************************
.*  KCLI0000  :  Keyin Clinic Id                                          *
.**************************************************************************
KCLI0000  MOVE      ZERO,EXIT
          MOVE      "18",ECOL
          MOVE      "14",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          DISPLAY   *P1:14,"Clinic Id     : ":
                           *EL,*P18:14,*EL
.         
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLI2000,KCLI2000,KCLI2000      * nothing or EXITCHAR
.         
          MOVE      OCACLIN,CLINICID               * valid input
          DISPLAY   *P27:14,OCADESC
          MOVE      ZERO,EXIT
          GOTO      KCLI9999
.
KCLI2000  MOVE      SP6,CLINICID                   * nothing input
          DISPLAY   *P18:14,*V2LON,"All"
          MOVE      ZERO,EXIT
.
KCLI9999  RETURN
+
.***********************************************************************
.*                         KCTY0000                                    *
.*                Keyin clinic type                                    *
.***********************************************************************
KCTY0000  MOVE      TEN3,EVERT
          MOVE      "18",ECOL
          DISPLAY   *P1:13,"Clinic Type   : ":
                    *P59:EVERT,*EL
.          
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.         
          CALL      KYOUTCTY
          IF        EXIT = 1
           MOVE       ZERO,EXIT
           DISPLAY    *PECOL:EVERT,*V2LON,"All"
           MOVE       SP6,CLINICTY
           GOTO       KCTY9999
          ENDIF
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KCTY9999
          ENDIF
          MOVE      OCTCTYP,CLINICTY
          DISPLAY   *PECOL:EVERT,*V2LON,OCTCTYP,*HOFF,SP3,OCTDESC
.
          MOVE      ZERO,EXIT
.
KCTY9999  RETURN
.
+
.************************************************************************
.*                              TIME0000                                *
.*       Format the time in WORKTIME output in FRMTTIME                 *
.*                   called by :  VBOA VCAN                             *
.************************************************************************
TIME0000  UNPACK    WORKTIME,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2
.         
          IF        FORM2<12
            MATCH     "00",CHOUR                 * 00:45 becomes 12:45 am
            IF        @EQUAL
              MOVE      TEN2,FORM2 
            ENDIF
            PACK      LINE,CHOUR,COLON,CMIN,AM   * am
          ELSE
            MATCH     "12",CHOUR                 * 22:45 becomes 10:45 pm
            IF        !@EQUAL
              SUB       TEN2,FORM2
              MOVE      FORM2,CHOUR
            ENDIF
            PACK      LINE,CHOUR,COLON,CMIN,PM   * pm
          ENDIF
.
          CALL      COMP0000                * supress zeros
          MOVE      LINE,FRMTTIME
.
TIME9999  RETURN
.
+
.**********************************************************************
.*                              COMP0000                              *
.*       Routine to compress excessive blanks in LINE                 *
.**********************************************************************
COMP0000  MOVE      LINE,DIM55
          PACK      TEMP55,SP20,SP20,SP10,SP5
          MATCH     TEMP55,DIM55                 * test line for spaces
          GOTO      COMP9999 IF EQUAL
          CLEAR     LINE
.
COMP1000  CMATCH    SP1,DIM55
          GOTO      COMP5000 IF NOT EQUAL        * compress leading blanks
          BUMP      DIM55
          GOTO      COMP1000 IF NOT EOS
          GOTO      COMP8000
.
COMP5000  MOVE      DIM55,TEMP55
          MOVE      TEMP55,DIM55
          ENDSET    DIM55
.
COMP5100  CMATCH    SP1,DIM55
          GOTO      COMP7000 IF NOT EQUAL        * compress trailing blanks
          BUMP      DIM55,-1
          GOTO      COMP5100
.
COMP7000  LENSET    DIM55
          RESET     DIM55
.
COMP7100  MOVE      DIM55,ANS
          APPEND    ANS,LINE
          BUMP      DIM55                        * compress multiple blanks
          GOTO      COMP8000 IF EOS              *   from in-between words
          CMATCH    SP1,DIM55
          GOTO      COMP7100 IF NOT EQUAL
          MOVE      DIM55,ANS
          APPEND    ANS,LINE
.
COMP7200  BUMP      DIM55
          CMATCH    SP1,DIM55
          GOTO      COMP7100 IF NOT EQUAL
          GOTO      COMP7200
.
COMP8000  RESET     LINE
          GOTO      COMP9999
.
COMP9999  RETURN
+
.******************************************************************************
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY29
          READ      TEMPFILE,KEY29;TEMPURNO,TEMPDATE,TEMPTIME,TEMPLINE,TEMPVISN:
                             TEMPAWRD,TEMPABED,TEMPSNAM,TEMPGNAM
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY29
          READ      TEMPFILE,KEY29;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY29
          READKS    TEMPFILE;TEMPURNO,TEMPDATE,TEMPTIME,TEMPLINE,TEMPVISN:
                             TEMPAWRD,TEMPABED,TEMPSNAM,TEMPGNAM
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY29
          WRITE     TEMPFILE,KEY29;TEMPURNO,TEMPDATE,TEMPTIME,TEMPLINE,TEMPVISN:
                             TEMPAWRD,TEMPABED,TEMPSNAM,TEMPGNAM
          RETURN
.
RSTEMP2   MOVE      ZERO,OVRCD
          RESET     KEY80
          READ      TEMPFIL1,KEY80;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          RESET     KEY80
          READKS    TEMPFIL1;TEMPURNO,TEMPDATE,TEMPTIME,TEMPLINE,TEMPVISN:
                             TEMPAWRD,TEMPABED,TEMPSNAM,TEMPGNAM
          GOTO      OVERCOND IF OVER
          RETURN
.
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS                * Calculate no of days
          INC       CLPMSVX1
          INC       DAYOFWEK
          INC       CLPATMAS
          INC       CLPATMIS
          INC       CLOUTXSC
          INC       KYOUTCLI
          INC       KYOUTCTY
          INC       PATHSPKY
          INC       OUTDSCTY
          INC       OUTDSCLN
          INC       TFILENAM
.
          INC       ALLPCTIO/INC            * Care team
          INC       IBASEQIO/INC
          INC       PATCODIO/INC            * Codes file
          INC       PMSUCHIO/INC            * U/R Comments
          INC       PMSUCDIO/INC
          INC       PATDO1IO/INC            * Doctors File
          INC       PATHSPIO/INC            * Hospital File
          INC       VISMDTIO/INC            * Visit comments
          INC       VISMTXIO/INC
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC            * Admission File
          INC       PMSHCPIO/INC            * HCP file
          INC       PMSTSPIO/INC            * Transport file
          INC       PMSVX1IO/INC            * Visit Extn file
          INC       OUTBOAIO/INC            * Clinic Session Booking Table A
          INC       OUTBB1IO/INC            * Clinic Session Booking Table B
          INC       OUTCTYIO/INC            * Outpatient Clinic Type Table
          INC       OUTHEDIO/INC            * Outpatient Slot Template Headers
          INC       OUTSESIO/INC            * Outpat Session
          INC       OUTCLIIO/INC            * Outpatient Clinic file
          INC       OUTCMNIO/INC            * Outpatient system comments
          INC       OUTSIPIO/INC            * Outpat Site Specific Categories
          INC       OUTSITIO/INC            * Site file
          INC       OUTXSCIO/INC            * Outpatient Extra Screens
          INC       VISUCMIO/INC            * Visit Based Comments
          INC       WEBERRIO/INC
+

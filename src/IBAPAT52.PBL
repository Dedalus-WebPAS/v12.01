.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAPAT52                                             *
.*                                                                            *
.*     FUNCTION:         DOCTORS REGISTER LISTING                             *
.*                                                                            *
.*     DATE WRITTEN:     12/06/1985                                           *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                      V10.03.01 04/05/2012  Peter Vela      CAR 261579      *
.*                                Added Fax and Email                         *
.*                      V9.09.01  27/12/2007  Peter Vela      CAR 149167      *
.*                                Fixed packing of DPAGER and DTELEP into     *
.*                                DIM26                                       *
.*                      V9.08.01  02/04/2007  Neelkamal Pyla  CAR 120482      *
.*                                Commented DSADD3 field in PDATA3 to display * 
.*                                the 'State' only once                       *
.*                      V9.06.01  30/05/2006  Janet George    CAR 105829      *
.*                                Added Doctor's mobile number to Doctor's    *
.*                                Register Listing when printing              *
.*                      V5.09.B01 08/01/2001  Jill Habasque                   *
.*                                Fixed global recompiled errors              *
.*                      V5.07.01  19/10/1999  J.Tan                           *
.*                                Added Doctor credit code                    *
.*                      V5.01.02  03/12/91 i chung         SRF 110256         *
.*                                Added option to suppress printing inactive  *
.*                                doctor(s)                                   *
.*                      V5.01.03  03/12/91 i chung         SRF 111155         *
.*                                Fixed to use parameter "CUSEHCS" to control *
.*                                the printing of HDP details                 *
.*                      V5.01.04  09/07/92  Peter Eddey    SRF 114003         *
.*                                added supression of active outside  doctors *
.*                                if desired.                                 *
.*                      V5.01.05  21/09/92  Peter Eddey    SRF 114003         *
.*                                added supression of active outside  doctors *
.*                                if desired to the heading.                  *
.*                      V5.01.06  22/01/93  Steve O'Gorman                    *
.*                                Modified to look at new files               *
.*        V5.01.07  23.02.92 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Reformat print to look beautiful for new fields           *
.*        V5.01.08  17.12.93 Matt Surridge           MTA                      *
.*                  Changed report layout to include PTDOPAYN - Payee Name    *
.*        V5.03.01  17/08/95  ROD                    SRF 611263               *
.*                  Fixed report header & add "?" routine to Doctor Type keyin*
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATHDOFD/INC
.
.         WORK AREA
.
BIRDATE   DIM       8
CDESC     DIM       20
CLDESC    DIM       20
CODE      DIM       2
CONAME    DIM       35
CTHETR    FORM      1
CUSEHCS   FORM      1               * HCS control parameter
.
DALPHA    DIM       6
DDACCR    DIM       13
DDRTYPE   DIM       30
DDRTYPE1  DIM       13
DDRTYPE2  DIM       13
DDRTYPE3  DIM       13
DDRTYPE4  DIM       13
DDRTYPE5  DIM       13
DDSTAT    DIM       13
DHADD125  DIM       25
DHADD225  DIM       25
DIM10     DIM       10
DIM13     DIM       13
DIM26     DIM       26
DIM26A    DIM       26
DIM26B    DIM       26
DIM34     DIM       34
DIM50     DIM       50
DNAME     DIM       28
DSADD125  DIM       25
DSADD225  DIM       25
.
FIRDATE   DIM       8
.
ACTOTFLG  FORM      1
INACTFLG  FORM      1
.
.
LDRTYPE1  DIM       20
LDRTYPE2  DIM       20
LDRTYPE3  DIM       20
LDRTYPE4  DIM       20
LDRTYPE5  DIM       20
LDACCR    DIM       20
LNO       FORM      2
LSTDATE   DIM       8
.
OPTION1   FORM      1
OPTION2   FORM      1
.
PTCNUFMS  FORM      1
VMCNVMAP  FORM      1
SDRTYPE   DIM       3
SERVDES   DIM       20
STDOCT    DIM       2
SUBDESC   DIM       20
SUBTYPE   DIM       3
.
.         CONSTANTS
.
BRAKL     INIT      "("
BRAKR     INIT      ")"
CODEDT    INIT      "DT"
CODEDL    INIT      "DL"
.
DSTAT0    INIT      "Active in Hos"
DSTAT1    INIT      "Not Active"
DSTAT2    INIT      "Act. Outside"
.
SP12      INIT      "            "
SP14      INIT      "              "
.
PRGID     INIT      "IBAPAT52"
PRGNAM    INIT      "Doctor Register Listing"
VERSION   INIT      "V12.01.00"
+
.
.         START OF PROGRAM
.
.         Open Files
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"pathdoct";
          OPEN      PATHDOC1,"pathdoct"
.
          MOVE      ONE,CNOUNDLN
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN3;*187,CUSEHCS
          READ      CONTROLF,TEN5;*189,CTHETR
          READ      CONTROLF,FIFTY9;*60,PTCNUFMS
          READ      CONTROLF,SEVENTY5;*87,VMCNVMAP
.
.             Select Option
.
START     DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = All Doctors":
                    *P1:6,*V2LON,TWO,*HOFF," = By Doctor Type":
                    *P1:7,"Select Option : ":
                    *P1:12,*V2LON,ZERO,*HOFF," = Return":
                    *P1:13,*V2LON,ONE,*HOFF," = Code Sequence":
                    *P1:14,*V2LON,TWO,*HOFF," = Surname Sequence":
                    *P1:15,"Select Option : ":
                    *P1:17,*V2LON,ZERO,*HOFF," = Return":
                    *P1:18,*V2LON,ONE,*HOFF," = Full Listing":
                    *P1:19,*V2LON,TWO,*HOFF," = Surgery & Home Address only":
                    *P1:20,*V2LON,THREE,*HOFF," = Surgery Address only":
                    *P1:21,*V2LON,FOUR,*HOFF," = Doctor Contact List":
                    *P1:22,"Select Option :";
.
OPT1      KEYIN     *P17:7,*EL,"_",*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF CLR12,DOTYPE
          BEEP
          GOTO      OPT1
.
CLR12     MOVE      SP3,SDRTYPE
          MOVE      SP3,SUBTYPE
          GOTO      INACTDOC
+
.
+
.***************************************************************************
.*  DOTYPE    :  Keyin Doctor Type                                         *
.***************************************************************************
DOTYPE    MOVE      CODEDT,CODE
          MOVE      SP3,CKYIDEF3
          MOVE      "15",ECOL
          MOVE      "9",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      ONE,CNEWDS
          DISPLAY   *P1:9,*EL,"Doctor Type :";
.
          CALL      PATCODKY
          BRANCH    EXIT,START,START
.
          MOVE      ACODE,SDRTYPE
          MOVE      TDESC,DDRTYPE
          DISPLAY   *P19:9,*V2LON,DDRTYPE;
.
.
.
.             INPUT TYPE OF DOCTOR 
.
... DOTYPE    
...       MOVE      SP3,SDRTYPE
...       DISPLAY   *P1:9,*EL,"Doctor Type :";
...       KEYIN     *P15:9,*V2LON,SDRTYPE;
...       RESET     SDRTYPE
...       GOTO      DOTYPE1 IF NOT EOS
...       DISPLAY   *P1:9,*EL,*P1:10,*EL,*P1:11,*EL;
...       GOTO      OPT1
.
... YPE1   ENDSET    SDRTYPE
...       APPEND    SP3,SDRTYPE
...       RESET     SDRTYPE
.
...       PACK      KEY5 WITH CODEDT,SDRTYPE
...       CALL      RDCODE1
...       BRANCH    OVRCD OF KDERR
...       MOVE      TDESC,DDRTYPE
...       DISPLAY   *P19:9,*V2LON,DDRTYPE;
.
.
.
.
.
.         Keyin Sub-Type
.
DOSUBT    MOVE      SP3,SUBTYPE
          KEYIN     *P45:9,*EL,"Sub-Type : ",*V2LON,SUBTYPE;
.
          ENDSET    SUBTYPE
          APPEND    SP3,SUBTYPE
          RESET     SUBTYPE
.
          MATCH     SP3,SUBTYPE
.         GOTO      OPT2A IF EQUAL
          GOTO      INACTDOC IF EQUAL
.
          PACK      KEY5 WITH CODEDT,SUBTYPE
          CALL      RDCODE1
          BRANCH    OVRCD OF KDERR2
          MOVE      TDESC,SUBDESC
          DISPLAY   *P60:9,*EL,*V2LON,SUBDESC;
.         GOTO      OPT2A
          GOTO      INACTDOC
.
KDERR     DISPLAY   *P1:24,*EL,*B,"Invalid Doctor Type.  ";
          CALL      HITENTER
          GOTO      DOTYPE
.
KDERR2    DISPLAY   *P1:24,*EL,*B,"Invalid Sub-Type.  ";
          CALL      HITENTER
          GOTO      DOSUBT
.
.             Suppress inactive doctors ?
.
INACTDOC  DISPLAY   *P1:10,*EL,"Suppress the printing of inactive doctors (":
                           *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ";
.
INACTDO1  MOVE      ZERO,INACTFLG
          KEYIN     *P51:10,*EL,"_",*P51:10,*V2LON,ANS;
          RESET     ANS
          GOTO      INACTDO3 IF EOS
.
          MATCH     "Y",ANS
          GOTO      INACTDO3 IF EQUAL
          MATCH     "N",ANS
          GOTO      INACTDO2 IF EQUAL
          BEEP
          GOTO      INACTDO1
.
INACTDO2  DISPLAY   *P51:10,*V2LON,"No";
          MOVE      ONE,INACTFLG
          GOTO      ACTOTDOC
.
INACTDO3  DISPLAY   *P51:10,*V2LON,"Yes";
+
.             Suppress active outside doctors ?
.
ACTOTDOC  DISPLAY   *P1:11,*EL,"Suppress the printing of active outside doctors (":
                           *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ";
.
ACTOTDO1  MOVE      ZERO,ACTOTFLG
          KEYIN     *P57:11,*EL,"_",*P57:11,*V2LON,ANS;
          RESET     ANS
          GOTO      ACTOTDO3 IF EOS
.
          MATCH     "Y",ANS
          GOTO      ACTOTDO3 IF EQUAL
          MATCH     "N",ANS
          GOTO      ACTOTDO2 IF EQUAL
          BEEP
          GOTO      ACTOTDO1
.
ACTOTDO2  DISPLAY   *P57:11,*V2LON,"No";
          MOVE      ONE,ACTOTFLG
          GOTO      OPT2A
.
ACTOTDO3  DISPLAY   *P57:11,*V2LON,"Yes";
.
.             Select Sequence Option
.
OPT2A     KEYIN     *P17:15,*EL,"_",*P17:15,*V2LON,OPTION1;
.
          COMPARE   ZERO TO OPTION1
          GOTO      OPT2AA IF EQUAL
.
          BRANCH    OPTION1 OF OPT3A,OPT3A
          BEEP
          GOTO      OPT2A
.
OPT2AA    DISPLAY   *P17:15,*EL,*P1:10,*EL,*P1:11,*EL;
          BRANCH    OPTION,OPT1
          GOTO      DOTYPE
.
NODOCT    DISPLAY   *P1:24,*EL,*P29:24,*B,*V2LON:
                    "** No Doctors on File **",*W2;
          GOTO      START
.
.             Select Type of Listing Option
.
OPT3A     KEYIN     *P17:22,*EL,"_",*P17:22,*V2LON,OPTION2;
.
          COMPARE   ZERO TO OPTION2
          GOTO      OPT2A IF EQUAL
.
          BRANCH    OPTION2 OF OKCONT,OKCONT,OKCONT,OKCONT
          BEEP
          GOTO      OPT3A
.
.             OK to Continue ?
.
OKCONT    DISPLAY   *P1:24,*EL,"Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                           *V2LON,"N",*HOFF,"/",*V2LON,"C",*HOFF,") ? ";
.
OKCONT1   KEYIN     *P26:24,*EL,"_",*P26:24,*V2LON,ANS;
          REP       "yYnNcC",ANS
.
          MATCH     "Y",ANS
          GOTO      OKOPT3 IF EQUAL
          MATCH     "N",ANS
          GOTO      OKCONT2 IF EQUAL
          MATCH     "C",ANS
          GOTO      OKCONT3 IF EQUAL
          BEEP
          GOTO      OKCONT1
.
.         "No" option chosen
.
OKCONT2   DISPLAY   *P17:22,*EF,*P17:15,*EL:
                    *P1:10,*EL,*P1:11,*EL,*P15:9,SP20,SP6,*P56:9,*EL;
          GOTO      REPEAT
.
.         "Cancel" option chosen
.
OKCONT3   DISPLAY   *P17:22,*EF,*P17:15,*EL:
                    *P1:10,*EL,*P1:11,*EL,*P1:9,*EL;
          GOTO      OPT1
.
.         "Yes" option chosen
.
OKOPT3    MOVE      ZERO,CPAGENO
          BRANCH    OPTION1 OF DOCTNO,ALPHANO
+
.             PRINT VIA DOCTOR CODE SEQUENCE
.
DOCTNO    MOVE      SP6,KEY6
          CALL      RDSDOCT1
          MOVE      "60",LNO
.
          DISPLAY   *P1:24,*EL,*P20:24,"Doctor Code :",*P60:24,"Scanning :";
.
NEXTA     CALL      RDKDOCT1
          BRANCH    OVRCD OF ENDP
.
          DISPLAY   *P72:24,DCODE;
.
          BRANCH    OPTION OF DISPA
.
          MATCH     DRTYPE,SDRTYPE
          GOTO      NEXTA IF NOT EQUAL
.
DISPA     BRANCH    INACTFLG,ACTOUTCK       * do not suppress inactive doctor
          BRANCH    DRSTAT,NEXTA            * suppress - doctor inactive
ACTOUTCK  BRANCH    ACTOTFLG,DISPA1         * do not supress active outside docs
          IF        DRSTAT = 2
            GOTO      NEXTA
          ENDIF
.
DISPA1    MOVE      DCODE,KEY6
          CALL      RDDOCT1
.
          MATCH     SP3,SUBTYPE
          GOTO      DISPA2 IF EQUAL
.
          MATCH     DRTYPE2,SUBTYPE
          GOTO      DISPA2 IF EQUAL
.
          MATCH     DRTYPE3,SUBTYPE
          GOTO      DISPA2 IF EQUAL
.
          MATCH     DRTYPE4,SUBTYPE
          GOTO      DISPA2 IF EQUAL
.
          MATCH     DRTYPE5,SUBTYPE
          GOTO      DISPA2 IF EQUAL
          GOTO      NEXTA
.
DISPA2    CALL      DISPDA
          DISPLAY   *P35:24,*V2LON,DCODE;
          GOTO      NEXTA
+
.
.             PRINT VIA SURNAME SEQUENCE
.
ALPHANO   MOVE      SP12,KEY12
          CALL      RDSDOCT2
          MOVE      "60",LNO
.
          DISPLAY   *P1:24,*EL,"Surname : ",*P35:24,"Doctor Code : ":
                    *P60:24,"Scanning :";
.
NEXTALP   CALL      RDKDOCT2
          BRANCH    OVRCD OF ENDP
.
          DISPLAY   *P72:24,DCODE;
.
          BRANCH    OPTION OF CHKINACT
.
          MATCH     DRTYPE,SDRTYPE
          GOTO      NEXTALP IF NOT EQUAL
.
CHKINACT  BRANCH    INACTFLG,CHKACTOT       * do not suppress inactive doctor
          BRANCH    DRSTAT,NEXTALP          * suppress - doctor inactive
CHKACTOT  BRANCH    ACTOTFLG,RDEXTN         * do not supress active outside docs
          IF        DRSTAT = 2
            GOTO      NEXTALP
          ENDIF
.
RDEXTN    MOVE      DCODE,KEY6
          CALL      RDDOCT1
.
          MATCH     SP3,SUBTYPE
          GOTO      DISPA3 IF EQUAL
.
          MATCH     DRTYPE2,SUBTYPE
          GOTO      DISPA3 IF EQUAL
.
          MATCH     DRTYPE3,SUBTYPE
          GOTO      DISPA3 IF EQUAL
.
          MATCH     DRTYPE4,SUBTYPE
          GOTO      DISPA3 IF EQUAL
.
          MATCH     DRTYPE5,SUBTYPE
          GOTO      DISPA3 IF EQUAL
          GOTO      NEXTALP
.
DISPA3    DISPLAY   *P10:24,*V2LON,DSNAME,*P49:24,DCODE;
          CALL      DISPDA
          GOTO      NEXTALP
+
.
.             OVER Condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NODOCT IF EQUAL
.
          COMPARE   FOUR,OPTION2
          CALL      PEND4 IF EQUAL
.
          PRINT     *N,"  ***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*V2LON,*P24:24:
                    "** Listing Generation Completed **",*W2;
          GOTO      START
+
.
.             PRINT ACTUAL DATA 
.
DISPDA    MOVE      SP30,DNAME
          MOVE      DTITL,DNAME 
.
FINEND    CMATCH    SP1,DNAME
          GOTO      CONTR IF EQUAL
.
          BUMP      DNAME,1
          GOTO      FINEND IF NOT EOS
.
CONTR     APPEND    DSNAME,DNAME
          RESET     DNAME
.
          MOVE      SP20,LDRTYPE1
          MATCH     SP3,DRTYPE
          GOTO      NODTYP1 IF EQUAL
          PACK      KEY5 WITH CODEDT,DRTYPE

          CALL      RDCODE1
          BRANCH    OVRCD OF NODTYP1
          MOVE      TDESC,LDRTYPE1
.
NODTYP1   MOVE      SP20,LDRTYPE2
          MATCH     SP3,DRTYPE2
          GOTO      NODTYP2 IF EQUAL
          PACK      KEY5 WITH CODEDT,DRTYPE2
          CALL      RDCODE1
          BRANCH    OVRCD OF NODTYP2
          MOVE      TDESC,LDRTYPE2
.
NODTYP2   MOVE      SP20,LDRTYPE3
          MATCH     SP3,DRTYPE3
          GOTO      NODTYP3 IF EQUAL
          PACK      KEY5 WITH CODEDT,DRTYPE3
          CALL      RDCODE1
          BRANCH    OVRCD OF NODTYP3
          MOVE      TDESC,LDRTYPE3
.
NODTYP3   MOVE      SP20,LDRTYPE4
          MATCH     SP3,DRTYPE4
          GOTO      NODTYP4 IF EQUAL
          PACK      KEY5 WITH CODEDT,DRTYPE4
          CALL      RDCODE1
          BRANCH    OVRCD OF NODTYP4
          MOVE      TDESC,LDRTYPE4
.
NODTYP4   MOVE      SP20,LDRTYPE5
          MATCH     SP3,DRTYPE5
          GOTO      NODTYP5 IF EQUAL
          PACK      KEY5 WITH CODEDT,DRTYPE5
          CALL      RDCODE1
          BRANCH    OVRCD OF NODTYP5
          MOVE      TDESC,LDRTYPE5
.
NODTYP5   MOVE      SP30,DIM26
.
          MATCH     SP70,DPAGER
          GOTO      PCKPAG IF NOT EQUAL
.
          MATCH     SP12,DTELEP
          GOTO      NOPAGER IF EQUAL
.
PCKPAG    UNPACK    SP70,DIM10,DIM13
          MOVE      DPAGER,DIM10
          MOVE      DTELEP,DIM13
          PACK      DIM26 WITH DIM10,SP1,BRAKL,DIM13,BRAKR,SP12
.
NOPAGER   MOVE      SP20,SERVDES
          COMPARE   ONE,CUSEHCS
          GOTO      NOHCS IF NOT EQUAL
.
          MOVE      ZERO,HDCODE
          MOVE      SP14,HDNAME
          MOVE      SP3,HDSERV
.
          COMPARE   ZERO,DHCSCOD
          GOTO      NOHCS IF EQUAL
.
          MOVE      DHCSCOD,KEY3
          CALL      RDHDOC1
          BRANCH    OVRCD OF NOHCS
          PACK      KEY5 WITH CODEDT,HDSERV
          CALL      RDCODE1
          BRANCH    OVRCD OF NOHCS
          MOVE      TDESC,SERVDES
.
NOHCS     MOVE      PTDOFAXN,DIM26A
          MOVE      PTDOSEML,DIM26B
.
          BRANCH    OPTION2 OF PDATA1,PDATA2,PDATA3,PDATA4
.
PDATA1    MOVE      SP20,CLDESC
          COMPARE   ONE,CTHETR
          GOTO      PDATA1A IF NOT EQUAL
.
          MATCH     SP3,DALEVEL
          GOTO      PDATA1A IF EQUAL
.
          PACK      KEY5 WITH CODEDL,DALEVEL
          CALL      RDCODE1
          BRANCH    OVRCD OF PDATA1A
          MOVE      TDESC,CLDESC
.
PDATA1A   MOVE      DSTAT0,DDSTAT
          LOAD      DDSTAT USING DRSTAT OF DSTAT1,DSTAT2
.
          COMPARE   "50",LNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"|",*10,"|",*46,"| Surgery :":
                    *93,"|",*120,"|",*132,"|":
                    *N,"| ",DCODE:
                    *10,"| ",DNAME,*41,"(",DRTYPE,")":
                    *46,"| ",DSADD1:
                    *93,"| ",DASSOC1,*120,"| ",DPROV,*132,"|":
                    *N,"|",*10,"| ",DGNAME:
                    *46,"| ",DSADD2:
                    *93,"| ",DASSOC2,*120,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| ",DSADD3:
                    *93,"|",*120,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| ",DSADD4,SP1,DSPOST:
                    *93,"|",*120,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| Surgery Phone No. ",DTELES:
                    *93,"|--------------------------|-----------|":
                    *N,"| ",*10,"| RECP  - ",DRNAME,*46,"| Home :":
                    *93,"| ",LDRTYPE2,*120,"| ",PTDONHSN,*132,"|":
                    *N,"|",*10,"| ",*46,"| ",DHADD1:
                    *93,"| ",LDRTYPE3,*120,"|",*132,"|":
                    *N,"|",*10,"| PAGER - ",DIM26,*46,"| ",DHADD2:
                    *93,"| ",LDRTYPE4,*120,"|",*132,"|":
                    *N,"|",*10,"| FAX   - ",DIM26A,*46,"| ",DHADD3:
                    *93,"| ",LDRTYPE5,*120,"|",*132,"|":
                    *N,"|",*10,"| EMAIL - ",DIM26B,*46,"| ",DHADD4,SP1,DHPOST:
                    *93,"|",*120,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| Mobile Number. ",PTDOMOBL:
                    *93,"|",*120,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| Home Phone No. ",DTELEH;
          IF        PTCNUFMS=1 & VMCNVMAP=1
            PRINT     *93,"|Creditor Code: ",PTDOGLCC;
          ELSE
            PRINT     *93,"|";
          ENDIF
          PRINT     *120,"|",*132,"|";
          ADD       "5",LNO                 * added 5 lines for new files
.
          CALL      PNEW0000 
.
DISPEND   RETURN
+
.
PDATA2    COMPARE   "52",LNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"|",*10,"|",*46,"| Surgery :",*105,"|",*132,"|":
                    *N,"| ",DCODE:
                    *10,"| ",DSNAME,*46,"| ",DSADD1,*105,"| ",DTELES,*132,"|":
                    *N,"|",*10,"| ",DTITL," ",DGNAME:
                    *46,"| ",DSADD2,*105,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| ",DSADD3,*105,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| ",DSADD4,SP1,DSPOST:
                    *105,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| Home :",*105,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| ",DHADD1,*105,"| ",DTELEH,*132,"|":
                    *N,"|",*10,"|",*46,"| ",DHADD2,*105,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| ",DHADD3,*105,"| ",PTDOMOBL,*132,"|":
                    *N,"|",*10,"|",*46,"| ",DHADD4,SP1,DHPOST,*105,"|",*132,"|"
          ADD       "10",LNO
          CALL      PAGEND
          RETURN
+
.
PDATA3    COMPARE   "56",LNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"|",*10,"|",*46,"| Surgery :",*105,"|",*132,"|":
                    *N,"| ",DCODE:
                    *10,"| ",DSNAME,*46,"| ",DSADD1,*105,"| ",DTELES,*132,"|":
                    *N,"|",*10,"| ",DTITL," ",DGNAME:
                    *46,"| ",DSADD2,*105,"|",*132,"|":
.                    *N,"|",*10,"|",*46,"| ",DSADD3:
.                    *105,"|",*132,"|":
                    *N,"|",*10,"|",*46,"| ",DSADD3,SP1,DSPOST,*105,"| ":
                    PTDOMOBL,*105,"|",*132,"|"
          ADD       "5",LNO
          CALL      PAGEND
          RETURN
+
.
PDATA4    PACK      DIM34 WITH SP30,SP5
          RESET     DIM34
          APPEND    DTITL,DIM34
.
PDAT41    CMATCH    SP1,DIM34
          GOTO      PDAT42 IF NOT EQUAL
          BUMP      DIM34,-1
          GOTO      PDAT41 IF NOT EOS
.
PDAT42    APPEND    " ",DIM34
          APPEND    DGNAME,DIM34
          RESET     DIM34
.
          COMPARE   "57",LNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"| ",DCODE,*10,"| ",DSNAME," ",DIM34,*67,"| ",DTELES:
                    *89,"| ",LDRTYPE1,*111,"| ",LDRTYPE2,*132,"|"
          ADD       ONE,LNO
          RETURN
+
.
.             LAST LINE ON THE PAGE
.
PAGEND    BRANCH    OPTION2 OF PEND1,PEND2,PEND2,PEND4
.
PEND1     PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,LNO
          RETURN
.
PEND2     PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,LNO
          RETURN
.
PEND4     PRINT     "*-------------------------------------------------":
                    "--------------------------------------------------":
                    "-------------------------------*"
          ADD       ONE,LNO
          RETURN
+
.
.             HEADINGS FOR OUTPUT
.
HEAD      CLOCK     TIME,CTIMEIS
          COMPARE   ZERO,CPAGENO
          GOTO      MAINHEAD IF EQUAL
.
          BRANCH    OPTION2 OF MAINHEAD,MAINHEAD,MAINHEAD
          CALL      PAGEND
.
MAINHEAD  CALL      HEAD132
          BRANCH    OPTION1 OF PRNTC,PRNTA
.
PRNTA     PRINT     *40,"(Surname Sequence)";
          GOTO      HD30
.
PRNTC     PRINT     *40,"(Code Sequence)";
.
HD30      BRANCH    ACTOTFLG,HD30A
          PRINT     " - No active outside doctors"
          GOTO      PRTCONT1
HD30A
          PRINT     " - Includes active outside doctors"
.
PRTCONT1  BRANCH    OPTION OF PRNT1,PRNT2
.
PRNT1     PRINT     *40,"All Doctors"
          GOTO      PRTCONT2
.
PRNT2     MATCH     SP3,SUBTYPE
          GOTO      PRNT2A IF NOT EQUAL
.
          PRINT     *40,"Doctor Type : ",DDRTYPE
          GOTO      PRTCONT2
.
PRNT2A    PRINT     *40,"DOCTOR TYPE : ",DDRTYPE," SUB-TYPE : ",SUBDESC
.
PRTCONT2  CALL      PAGEND
.
          BRANCH    OPTION2 OF HEAD1,HEAD2,HEAD3,HEAD4
.
HEAD1     PRINT     *1,"| Doctor | Name",*46,"| Address/Telephone":
                    *93,"| Associated Doctors/",*120,"| Provider/",*132,"|":
                    *N,"|  Code/ |",*46,"|":
                    *93,"| Additional Doctor Types/ | Prescriber",*132,"|";
.
          COMPARE   ONE,CUSEHCS
          GOTO      HEAD1B IF NOT EQUAL
          PRINT     *N,"|HDP Code|",*46,"|",*93,"| HDP Service Code",*120,"|":
                    *132,"|"
          ADD       ONE,LNO
          GOTO      HEAD1C 
.
HEAD1B    PRINT     *N;
HEAD1C    COMPARE   ONE,CTHETR
          IF        !@EQUAL
            PRINT     "|",*10,"|",*46,"|",*93,"|";
            GOTO      HEAD1D 
          ENDIF
          PRINT     "|",*10,"|",*12," Classification";
.
HEAD1D    PRINT     *34," Doc.Status |"," Payee Name",*93,"|",*120,"|",*132,"|"
.
HEAD1E    MOVE      TEN,LNO
          GOTO      ENDHD10
.
HEAD2     PRINT     *1,"| Doctor | Name",*46,"| Address":
                    *105,"| Telephone",*132,"|":
                    *N,"|  Code  |",*46,"|",*105,"| Numbers",*132,"|"
          GOTO      ENDHEAD
.
HEAD3     PRINT     *1,"| Doctor | Name",*46,"| Address":
                    *105,"| Telephone",*132,"|":
                    *N,"|  Code  |",*46,"|",*105,"| Number",*132,"|"
          GOTO      ENDHEAD
.
.             Heading for Doctor Contact Listing
.
HEAD4     PRINT     *1,"| Doctor",*10,"|",*26," Name",*67,"|  Telephone":
                    *89,"|     Primary",*111,"|    Secondary",*132,"|":
                    *N,*1,"|  Code",*10,"|",*67,"|   Number":
                    *89,"|    Speciality",*111,"|    Speciality",*132,"|"
.
ENDHEAD   MOVE      "8",LNO
.
ENDHD10   CALL      PAGEND
          MOVE      " ",ANS
          RETURN
+
.******************************************************************************
.*        POLD0000 : Print old file format                                    *
.******************************************************************************
POLD0000
          COMPARE   ONE,CUSEHCS
          GOTO      POLD1000 IF NOT EQUAL
          PRINT     *N,"|--------|-----------------------------------";
          GOTO      POLD2000
.
POLD1000  PRINT     *N,"|",*10,"|";
.
POLD2000  COMPARE   ONE,CTHETR
          GOTO      POLD3000 IF NOT EQUAL
          PRINT     *46,"|--------------------------------|-------------";
          GOTO      POLD4000
.
POLD3000
          PRINT     *46,"|",*79,"|-------------";
.
POLD4000  COMPARE   ONE,CUSEHCS
          GOTO      POLD5000 IF NOT EQUAL
          PRINT     *93,"|--------------------------|",*132,"|";
          GOTO      POLD6000
.
POLD5000  PRINT     *93,"|",*120,"|",*132,"|";
.
POLD6000  COMPARE   ONE,CUSEHCS
          GOTO      POLD7000 IF NOT EQUAL
          PRINT     *N,"|  ",HDCODE,*10,"| ",HDNAME,*46,"|";
          GOTO      POLD8000
.
POLD7000  PRINT     *N,"|",*10,"|",*46,"|";
.
POLD8000  COMPARE   ONE,CTHETR
          GOTO      POLD9000 IF NOT EQUAL
          PRINT     *47,CLDESC;
.
POLD9000  PRINT     *79,"|",DDSTAT,*93,"| ",SERVDES,*120,"|",*132,"|"
          ADD       "10",LNO
          CALL      PAGEND
POLD9999  RETURN
.
+
.******************************************************************************
.*        PNEW0000 : Print new file format                                    *
.******************************************************************************
.
PNEW0000  COMPARE   ONE,CUSEHCS
          GOTO      PNEW1000 IF NOT EQUAL
          PRINT     *N,"|--------|-----------------------------------";
          PRINT     *93,"|--------------------------|",*132,"|";
          PRINT     *N,"|  ",HDCODE,*10,"| ",HDNAME,*46,"|";
          PRINT     *93,"| ",SERVDES,*120,"|",*132,"|";
          ADD       ONE,LNO
          GOTO      PNEW1000
.
PNEW1000  COMPARE   ONE,CTHETR
          GOTO      PNEW2000 IF NOT EQUAL
          PRINT     *N,"|",*10,"|-----------------------------------":
                    *46,"|----------------------------------------------":
                    *93,"|",*120,"|",*132,"|";
          GOTO      PNEW3000
.
PNEW2000   
          PRINT     *N,"|",*10,"|",*32,"--------------":
                    *46,"|----------------------------------------------":
                    *93,"|",*120,"|",*132,"|";
.
PNEW3000  PRINT     *N,"|",*10,"|",*46,"|";
.
PNEW4000  COMPARE   ONE,CTHETR
          GOTO      PNEW5000 IF NOT EQUAL
          PRINT     *12,CLDESC;
.
PNEW5000  PRINT     *32,"|",DDSTAT,*93,"| ",*120,"|",*132,"|"
          ADD       "10",LNO
          CALL      PAGEND
.
PNEW9999  RETURN
.
ENDIT     CHAIN     PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATHDOIO/INC
          INC       PATCODKY

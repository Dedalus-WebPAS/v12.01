.*****************************************************************************
.*    System       : PATIENT MANAGEMENT                                      *
.*    Program      : IBAOUT63                                                *
.*    Description  : Inpatient & Outpatient Actual Waiting Times             *
.*                   ( SRF 441169 / Quote 440049 )                           *
.*****************************************************************************
.*    Date         : 08/12/1994                                              *
.*    Author       : Max White                                               *
.*    Function     : This program reports the actual waiting times           *
.*                   (in weeks) for a specified date range of :              *
.*                                                                           *
.*                     Inpatients  - Elective admission cases.               *
.*                                                                           *
.*                     Outpatients - New visits only.                        *
.*                                                                           *
.*                    This report does not take suspensions, holidays, DNA's *
.*                    or patient cancellations into account, it only reports *
.*                    actual times waited.                                   *
.*                                                                           *
.*    Modifications:                                                         *
.*                                                                           *
.*****************************************************************************
.* V12.00.01 19/05/2025 Thanh T         TSK 0955096                          *
.*           Changed for alphanumeric visit number                           *
.*****************************************************************************
.*        V11.02.01 10/02/2022  Thanh T       TSK 0889899                    *
.*                  Recompiled as WATTR1FD changes                           *
.*        V10.07.01 30/10/2015 Ebon Clements  CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2        *
.*        V10.05.01 15/07/2014 Ebon Clements  CAR 261630                     *
.*                  Removed port number from temp file name                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                     *
.*                  Added pmsvx1af                                           *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                     *
.*                  Recompiled for KYOUTCLI - readk of outcliaf              *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                     *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf               *
.*        V9.02.01  20/12/2001  Dean Cramer                                  *
.*                  Change KEY28 to KEY30 for pattranf                       *
.*                  Make 6 character dates 8 for start and end on PATMI1A3   * 
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                 *
.*                  Recompiled for changes to OUTDSCLN                       *
.*        V5.08.01  28/08/2000  Caleb Sharman                                *
.*                  Changed Health Fund variables to be 8 chars              *
.*        V5.08.B01  14/03/2000  Steve Armstrong   5.08 Mods                 *
.*                   Mods for changes to OUTCLIFD (effective date)           *
.*****************************************************************************
.*        V5.07.B01  04/11/1998  Jim Stathopoulos                            *
.*                   507 Changes                                             *
.*****************************************************************************
.*         V5.03.01  03/10/1995  Max White       SRF 441399                  *
.*                   Correct page headings problem.                          *
.*****************************************************************************
.*         V5.01.02  05/06/1995  Max White       SRF 441300 / Quote 440066   *
.*                   Modify outpatient waiting times for DNA's and patient   *
.*                   cancellations.                                          *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       BOKRC1FD/INC            * Booking File
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC            * Clinic Session Booking File A
          INC       OUTBB1FD/INC            * Clinic Session Booking File B
          INC       OUTBOCFD/INC            * Clinic Session Booking File C
          INC       OUTCONFD/INC            * Outpatient Parameter File
          INC       OUTCLIFD/INC            * Outpatient Clinic File
          INC       OUTOSFFD/INC            * Open Session File
          INC       OUTRAPFD/INC            * Outpatients Reappointments
          INC       OUTRSHFD/INC            * Outpatients Reshedules
          INC       PATCODFD/INC            * Codes File
          INC       PATDO1FD/INC            * Doctor File
          INC       PMSHCPFD/INC            * National HCP file
          INC       PATDSCFD/INC            * Patient Discharge File
          INC       PATHSPFD/INC            * Hospital File
          INC       PATMI1FD/INC            * Patient Admissions File
          INC       PMSVX1FD/INC            * Patient Admissions File
          INC       PATTRNFD/INC            * Patient Transfer File
          INC       PATWR1FD/INC            * Patient Ward File
          INC       TFILEVAR/INC
          INC       WATTR1FD/INC            * Waiting List
          INC       WEBERRFD/INC
.
.------   Variables
.
CANDATE   DIM       8
CDYSDAYS  FORM      6                       * CALCDAYS reply variable
CDYSFDTE  DIM       8                       * CALCDAYS input variable
CDYSTDTE  DIM       8                       * CALCDAYS input variable
CDYSYEAR  FORM      2                       * CALCDAYS work variable
CODE      DIM       2
CMDLINE   DIM       50
DIM3      DIM       3
DIM8      DIM       8
DIM25     DIM       25
DIM40     DIM       40
DIM60     DIM       60
DIM75     DIM       75
DNADATE   DIM       8
DOCCODE   DIM       6
EHSP      DIM       3
EHSPDESC  DIM       55
ECLIN     DIM       6
ECLIDESC  DIM       25
ESPEC     DIM       3 
ESPCDESC  DIM       25
EDOL      DIM       10
FIRSTREC  DIM       1
FROMDATE  DIM       8
ENDDATE   DIM       8
ENDCENT   DIM       2
OUTHEAD   DIM       1
PTCNDCQS  FORM      1
SAVOUTNO  DIM       8
SAVRBKN   DIM       8
SHSP      DIM       3
SHSPDESC  DIM       55
SCLIN     DIM       6
SCLIDESC  DIM       25
SSPEC     DIM       3 
SSPCDESC  DIM       25
SDOL      DIM       10
STRTDATE  DIM       8
STRTCENT  DIM       2
TODAY     DIM       8
.
.------   Literals
.
START     INIT      "Start "
FINISH    INIT      "Finish"
.
.------   Saved Temporary File Variables & Calculation Fields
.
PRSPEC    DIM       3
PRHSP     DIM       3
PRIOIND   DIM       1
PRCLIN    DIM       6
PRCLDESC  DIM      30
PRHSDESC  DIM      50
PRSPDESC  DIM      30
PR1DAYS   FORM      8
PR1COUNT  FORM      8
PR1MAX    FORM      5
PR1MIN    FORM      5
PR2DAYS   FORM      8
PR2COUNT  FORM      8
PR2MAX    FORM      5
PR2MIN    FORM      5
.
AVDAYS    FORM      8.5
AVWORK    FORM      8.5
AVWEEKS   FORM      5.1
MAXWEEKS  FORM      5.1
MINWEEKS  FORM      5.1
.
.------   Temporary File Definition
.
TEMPFILE  IFILE  SQL, FIXED=129, KEY="d1-3,4-6,7-7,8-13,14-14"
.
XXSPEC    DIM       3     3    1            * specialty
XXHSP     DIM       3     3    4            * hospital
XXIOIND   DIM       1     1    7            * 1=inpatient, 2=outpatient
XXCLIN    DIM       6     6    8            * inpatient  = spaces
.                                           * outpatient = clinic id
XXATTIND  DIM       1     1    14           * inpatient  1 = inpatient
.                                           *            2 = daycase
.                                           * outpatient 1 = urgent
.                                           *            2 = routine
XXDAYS    FORM      5     4    15           * waiting time (days)
XXSPDESC  DIM      30    30    19           * specialty description
XXHSDESC  DIM      50    50    49           * hospital name
XXCLDESC  DIM      30    30    99           * clinic description
.
.                             129
.
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
.------   Program Definition
.
PRGID     INIT      "IBAOUT63"
PRGNAM    INIT      "Inpatient & New Outpatient Actual Waiting Times"         
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              ML0000                                       *
.*                             Mainline                                      *
.*****************************************************************************
.
ML0000    CALL      INIT0000                * initialistion
.
ML1000    CALL      MENU0000                * Program Menu
          BRANCH    EXIT,ML9999
.
ML2000    CALL      DSCR0000                * Display selection screen
.
          CALL      KDAT0000                * Keyin the date range
          BRANCH    EXIT,ML9999         
.
          CALL      KSPC0000                * Keyin the specialty
          BRANCH    EXIT,ML9999         
.
          CALL      KHSP0000                * Keyin the hospital range
          BRANCH    EXIT,ML9999         
.
          CALL      KCLI0000                * Keyin the clinic id range 
          BRANCH    EXIT,ML9999         
.
          CALL      CONTQST
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.
ML5000    CALL      CRET0000                * Create the tempfile

          CALL      EXTR0000                * Extract the data
          BRANCH    EXIT,ML1000
 
          CALL      PRNT0000                * Print the report
 
          GOTO      ML1000
.
ML9999    CHAIN     PGM
.
.*****************************************************************************
.*                              INIT0000                                     *
.*                        Initialisation section                             *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          DISPLAY   *P56:24,"Opening";
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P64:24,CFNAME;
          CALL      OPOTBB11
.
          PACK      CFNAME,UID,FILBOKC1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKC1,CFNAME
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          DISPLAY   *P64:24,"outosfaf";
          OPEN      OUTOSFA3,"outosfaf"
.
          PACK      CFNAME,UID,FILRAPA2
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTRAPA2,CFNAME  
.
          PACK      CFNAME,UID,FILRSHA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTRSHA1,CFNAME  
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          MOVE      ONE,CNEWDS
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,DIM8
.
INIT9999  RETURN
.
.*****************************************************************************
.*                              MENU0000                                     *
.*                        Program Menu Routine                               *
.*****************************************************************************
.
MENU0000  MOVE      ZERO,EXIT
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run Report":
                    *P1:7,"Select Option ? "

MENU1000  KEYIN     *P17:7,*V2LON,MOPTION:
                    *P17:7,*DV,MOPTION
.
          BRANCH    MOPTION,MENU9999
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BEEP
          GOTO      MENU1000
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
                   
.
.*****************************************************************************
.*                              DSCR0000                                     *
.*                    display the Keyin Screen                               *
.*****************************************************************************
.
DSCR0000  DISPLAY   *P1:9,*EF:
                    *P1:9,"Date      from : ":
                    *P1:10,"            to : ":
                    *P1:12,"Specialty from : ":
                    *P1:13,"            to : ":
                    *P1:15,"Hospital  from : ":
                    *P1:16,"            to : ":
                    *P1:18,"Clinic Id from : ":
                    *P1:19,"            to : "
.
DSCR9999  RETURN
.
.*****************************************************************************
.*                              KHSP0000                                     *
.*                        Keyin the hospital code                            *
.*****************************************************************************
.
KHSP0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          MOVE      TEN5,EVERT
          MOVE      "18",ECOL
.
          DISPLAY   *PECOL:EVERT,*EL
          CALL      PATHSPKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KHSP9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,START
            MOVE      SP6,SHSP
            PACK      SHSPDESC,START,SP30,SP30
            GOTO      KHSP5000
          ENDIF
.
          MOVE      KEY3,SHSP
          DISPLAY   *P25:EVERT,PTHSNAME
          PACK      SHSPDESC,SHSP,SP2,PTHSNAME
.
KHSP5000  MOVE      ZERO,EXIT
.
          MOVE      TEN6,EVERT
          DISPLAY   *PECOL:EVERT,*EL
          CALL      PATHSPKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KHSP9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,FINISH
            MOVE      "zzzzzzz",EHSP
            MOVE      ZERO,EXIT
            PACK      EHSPDESC,FINISH,SP30,SP30
            GOTO      KHSP9999
          ENDIF
.
          MOVE      KEY3,EHSP
          DISPLAY   *P25:EVERT,PTHSNAME
          PACK      EHSPDESC,EHSP,SP2,PTHSNAME
.
          MATCH     SHSP,EHSP
          IF        @LESS
            DISPLAY   *P1:24,*EL,"Hospital codes must be in sequence. ";
            CALL      HITENTER
            GOTO      KHSP0000
          ENDIF
.
KHSP9999  RETURN
.
.*****************************************************************************
.*                              KDAT0000                                     *
.*                          Keyin the date range                             *
.*****************************************************************************
.
KDAT0000  MOVE      ZERO,EXIT
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          MOVE      "18",CCOL
          MOVE      NINE,CVERT
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P18:9,*EL,*P18:10,*EL
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT0000
          BRANCH    OVRCD,KDAT9000                * anything input
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            BEEP 
            GOTO      KDAT0000
          ENDIF
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          MATCH     STRTDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date Cannot Be into the Future. ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
          PACK      SDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
KDAT5000  MOVE      TEN,CVERT
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT0000
          BRANCH    OVRCD,KDAT9000                * anything input
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            BEEP
            GOTO      KDAT5000
          ENDIF
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          MATCH     ENDDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date Cannot Be into the Future. ";
            CALL      HITENTER
            GOTO      KDAT5000
          ENDIF
          PACK      EDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,"Dates Must be in Sequence. ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
.
KDAT8000  MOVE        ZERO,EXIT
          GOTO        KDAT9999
.
KDAT9000  MOVE        ONE,EXIT
          GOTO        KDAT9999
.
KDAT9999  RETURN
.
.*****************************************************************************
.*                              KSPC0000                                     *
.*                        Keyin the Speciality code                          *
.*****************************************************************************
.
KSPC0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          MOVE      TEN2,EVERT
          MOVE      "18",ECOL
          MOVE      "A ",CODE
.
          DISPLAY   *PECOL:EVERT,*EL:
                    *P18:12,*EL
          CALL      PATCODKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KSPC9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,START
            MOVE      SP6,SSPEC
            PACK      SSPCDESC,START,SP30,SP10
            GOTO      KSPC5000
          ENDIF
.
          MOVE      ACODE,SSPEC
          DISPLAY   *P25:EVERT,TDESC
          PACK      SSPCDESC,SSPEC,SP2,TDESC
.
KSPC5000  MOVE      ZERO,EXIT
.
          MOVE      TEN3,EVERT
          DISPLAY   *PECOL:EVERT,*EL
          CALL      PATCODKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KSPC9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,FINISH
            MOVE      "zzzzzzz",ESPEC
            MOVE      ZERO,EXIT
            PACK      ESPCDESC,FINISH,SP30,SP10
            GOTO      KSPC9999
          ENDIF
.
          MOVE      ACODE,ESPEC
          DISPLAY   *P25:EVERT,TDESC
          PACK      ESPCDESC,ESPEC,SP2,TDESC
.
          MATCH     SSPEC,ESPEC
          IF        @LESS
            DISPLAY   *P1:24,*EL,"Specialty Codes Must be in Sequence. ";
            CALL      HITENTER
            GOTO      KSPC0000
          ENDIF
.
KSPC9999  RETURN
.
.*****************************************************************************
.*                              KCLI0000                                     *
.*                        Keyin the Clinic Id range                          *
.*****************************************************************************
.
KCLI0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          MOVE      TEN8,EVERT
          MOVE      "18",ECOL
.
          DISPLAY   *PECOL:EVERT,*EL:
                    *P18:18,*EL
          CALL      KYOUTCLI
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KCLI9999
          ENDIF
.
          IF        EXIT = 1 | EXIT = 3
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,START
            MOVE      SP6,SCLIN
            PACK      SCLIDESC,START,SP30,SP10
            GOTO      KCLI5000
          ENDIF
.
          MOVE      OCACLIN,SCLIN
          DISPLAY   *P25:EVERT,OCADESC
          PACK      SCLIDESC,SCLIN,SP2,OCADESC
.
KCLI5000  MOVE      ZERO,EXIT
.
          MOVE      TEN9,EVERT
          DISPLAY   *PECOL:EVERT,*EL
          CALL      KYOUTCLI
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KCLI9999
          ENDIF
.
          IF        EXIT = 1 | EXIT = 3
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,FINISH
            MOVE      "zzzzzzz",ECLIN
            MOVE      ZERO,EXIT
            PACK      ECLIDESC,FINISH,SP30,SP10
            GOTO      KCLI9999
          ENDIF
.
          MOVE      OCACLIN,ECLIN
          DISPLAY   *P25:EVERT,OCADESC
          PACK      ECLIDESC,ECLIN,SP2,OCADESC
.
          MATCH     SCLIN,ECLIN
          IF        @LESS
            DISPLAY   *P1:24,*EL,"Clinic Id Codes must be in Sequence. ";
            CALL      HITENTER
            GOTO      KCLI0000
          ENDIF
.
KCLI9999  RETURN
+
.*****************************************************************************
.*                           EXTR0000                                        *
.*                        Extract the relevant data                          *
.*****************************************************************************
.
EXTR0000  MOVE      ONE,FORM1
.
          CALL      EXIP0000                * extract inpatient information
          CALL      EXOP0000                * extract outpatient information
.
          MOVE      FORM1,EXIT
          IF        FORM1 = 1
            DISPLAY   *P1:24,*EL,"No data found. "; 
            CALL      HITENTER
          ENDIF
.
EXTR9999  RETURN
+
.*****************************************************************************
.*                           EXIP0000                                        *
.*                  Extract relevant inpatient data                          *
.*****************************************************************************
.
EXIP0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Processing.....",*HOFF:
                           "   Inpatients [          ]"
.
.------   Start reading the admission file from the start of the date range 
.
          PACK      KEY14,STRTDATE,SP10
          CALL      RDSMISS3
EXIP1000  CALL      RDKMISS3
          BRANCH    OVRCD,EXIP9999 
.
.------   Are we still within the selected date range ?
.
          MATCH     ADATE,ENDDATE
          GOTO      EXIP9999 IF LESS
.
          DISPLAY   *P32:24,ADATE;
.
.------   Ignore any Pre-admissions
.
          COMPARE   ZERO,ASTAT
          GOTO      EXIP1000 IF EQUAL
.
.------   Is admission within the specialty range ?
.
          MATCH     SSPEC,ATYPE
          GOTO      EXIP1000 IF LESS
.
          MATCH     ATYPE,ESPEC
          GOTO      EXIP1000 IF LESS
.
.------   Is admission within the hospital range ?
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,EXIP1000
.
          MATCH     TADMN,AADMNO
          GOTO      EXIP1000 IF NOT EQUAL
.
          PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
          BRANCH    OVRCD,EXIP1000
.
          MATCH     SHSP,PTWDHOSN
          GOTO      EXIP1000 IF LESS
.
          MATCH     PTWDHOSN,EHSP
          GOTO      EXIP1000 IF LESS
.
.------   Move details to the temporary file
.
          CALL      CLRF0000
.
          MOVE      ATYPE,XXSPEC
.
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,EXIP2000
.
          MOVE      TDESC,XXSPDESC
.
EXIP2000  MOVE      PTWDHOSN,XXHSP
.
          MOVE      PTWDHOSN,KEY3
          CALL      RDPTHSP1
          BRANCH    OVRCD,EXIP3000
.
          MOVE      PTHSNAME,XXHSDESC
.
EXIP3000  MOVE      ONE,XXIOIND
.
.------   Determine if inpatient or daycase
.
          PACK      KEY5,ANSP,SP1,ACLSS
          CALL      RDCODE1
          BRANCH    OVRCD,EXIP4000
.
          MATCH     "2",TCINDC2             * daycase service ?
          GOTO      EXIP4000 IF NOT EQUAL   * no, must be an inpatient
.
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1                 * discharged ?
          BRANCH    OVRCD,EXIP4000          * no, must be an inpatient
.
          MATCH     ADATE,DDATE             * a daycase ?
          GOTO      EXIP4000 IF NOT EQUAL   * no, must be an inpatient
.
          MOVE      TWO,XXATTIND            * daycase
          GOTO      EXIP5000
.
EXIP4000  MOVE      ONE,XXATTIND            * inpatient
.
.------   Determine number of days wait from either date on list  -----
.         or the booking date 
.
EXIP5000  PACK      KEY19,AURNO,SP20
          CALL      RDSTREA1
EXIP6000  CALL      RDKTREA1
          BRANCH    OVRCD,EXIP7000
.
          MATCH     AURNO,WMURNO            * same ur no. ?
          GOTO      EXIP7000 IF NOT EQUAL   * no, look for booking date
.
          MATCH     WMBOOK,AADMNO           * correct admission no. ?
          GOTO      EXIP6000 IF NOT EQUAL   * no, look for next one
.
          MOVE      WMDATE1,FROMDATE        * got date on list
          GOTO      EXIP8000
.
EXIP7000  PACK      KEY8,AADMNO
          CALL      RDBKREC1                * booking file record exists ?
          BRANCH    OVRCD,EXIP1000          * no, ignore
.
          MOVE      BKREADAT,FROMDATE       * got booking date
.
EXIP8000  MOVE      FROMDATE,CDYSFDTE
          MOVE      ADATE,CDYSTDTE
.
          MATCH     CDYSFDTE,CDYSTDTE       * to date less than from date ?
          GOTO      EXIP1000 IF LESS        * yes, ignore
.
          CALL      CALCDAYS
          SUBTRACT  ONE,CDYSDAYS            * get number of days waiting
.
          MOVE      CDYSDAYS,XXDAYS
.
.------   Write to the temp file  ------
.
          CALL      WRXXSLS1
          MOVE      ZERO,FORM1
          GOTO      EXIP1000                * get next admission
.
EXIP9999  RETURN
+
.*****************************************************************************
.*                           EXOP0000                                        *
.*                  Extract relevant outpatient data                         *
.*****************************************************************************
.
EXOP0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Processing.....",*HOFF:
                           "  Outpatients [          ]"
.
          PACK      KEY34,STRTDATE,SP30
          CALL      RSOTOSF3
EXOP1000  CALL      RKOTOSF3
          BRANCH    OVRCD,EXOP9999
.
          DISPLAY   *P32:24,OTOSDATE;
.
          MATCH     OTOSDATE,ENDDATE
          GOTO      EXOP9999 IF LESS
.
.------   Is session for current outpatients site ?
.
          MATCH     IDDD,OTOSSITE
          GOTO      EXOP1000 IF NOT EQUAL
.
.------   Is session within the hospital range ?
.
          MATCH     SHSP,OTOSHOSP
          GOTO      EXOP1000 IF LESS
.
          MATCH     OTOSHOSP,EHSP
          GOTO      EXOP1000 IF LESS
.
.------   Is session within the clinic id range ?
.
          MATCH     SCLIN,OTOSCLID
          GOTO      EXOP1000 IF LESS
.
          MATCH     OTOSCLID,ECLIN
          GOTO      EXOP1000 IF LESS
.
.------   Loop through outboka to get all attendances within date range
. 
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP30
          CALL      RDSBOKA1
EXOP2000  CALL      RDKBOKA1
          BRANCH    OVRCD,EXOP1000
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,KEY28
          GOTO      EXOP1000 IF NOT EQUAL
.
.------   Must be attended in period
.
          COMPARE   FOUR,OBASTAT
          GOTO      EXOP2000 IF NOT EQUAL 
.
.------   Must have been referred
.
          PACK      KEY8,OBAOUTNO
          CALL      RDOTBOC1
          BRANCH    OVRCD,EXOP2000
.
.------   Must have been an urgent or routine new attendance in period
.
          PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,EXOP2000
.
          MATCH     "1",TCINDC2
          GOTO      EXOP3000 IF EQUAL       * Urgent New, accept
.
          MATCH     "2",TCINDC2
          GOTO      EXOP2000 IF NOT EQUAL   * Not Routine New either, ignore
.
.------   get specialty from bokb record
.
EXOP3000  PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,EXOP2000
.
          PACK      KEY5,ANSA,SP1,OBACLASS
          CALL      RDCODE1
          BRANCH    OVRCD,EXOP2000
.
.------   Is appointment within specialty range ?
.
          MATCH     SSPEC,OBACLASS
          GOTO      EXOP2000 IF LESS
.
          MATCH     OBACLASS,ESPEC
          GOTO      EXOP2000 IF LESS
.
.------   Move detail to the temporary file
.
          CALL      CLRF0000
.
          MOVE      OBACLASS,XXSPEC         * specialty
.
          PACK      KEY5,ANSA,SP1,OBACLASS  * get specialty description
          CALL      RDCODE1
          BRANCH    OVRCD,EXOP4000
.
          MOVE      TDESC,XXSPDESC
.
EXOP4000  MOVE      OTOSHOSP,XXHSP          * hospital
.
          MOVE      OTOSHOSP,KEY3           * get hospital name
          CALL      RDPTHSP1
          BRANCH    OVRCD,EXOP5000
.
          MOVE      PTHSNAME,XXHSDESC
.
EXOP5000  MOVE      TWO,XXIOIND             * outpatient record
          MOVE      OTOSCLID,XXCLIN
.
          PACK      KEY20,OTOSSITE,OTOSCLID,OTOSDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTOSSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTOSCLID,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          BRANCH    OVRCD,EXOP6000
.
          MATCH     SP6,OCADOCT
          GOTO      EXOP6000 IF EQUAL
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,EXOP7000
.
          CALL      CONCATDR
          MOVE      DIM40,OCADESC
.
EXOP6000  MOVE      OCADESC,XXCLDESC
.
.------   Determine if urgent or routine  ------
.
EXOP7000  PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          MATCH     "1",TCINDC2
          IF        @EQUAL
            MOVE      ONE,XXATTIND
          ELSE
            MOVE      TWO,XXATTIND
          ENDIF
.
.------   Determine number of days wait -----
.
          CALL      GDNA0000                * Check if DNA  
          CALL      GCAN0000                * Check if Patient Cancellation
.
. The idea is to get the hospitals waiting times down, so use the latest date
.         
          MATCH     DNADATE,CANDATE
          IF        @LESS
            MATCH     DNADATE,OTBCRDAT
            IF        @LESS
              MOVE      DNADATE,CDYSFDTE    * DNA date = from date
            ELSE
              MOVE      OTBCRDAT,CDYSFDTE   * referral date = from date
            ENDIF
          ELSE
            MATCH     CANDATE,OTBCRDAT
            IF        @LESS
              MOVE      CANDATE,CDYSFDTE    * cancellation date = from date
            ELSE
              MOVE      OTBCRDAT,CDYSFDTE   * referral date = from date
            ENDIF
          ENDIF
.
          MOVE      OBADATE,CDYSTDTE        * appointment date = to date
.
          MATCH     CDYSFDTE,CDYSTDTE       * to date less than from date ?
          GOTO      EXOP2000 IF LESS        * yes, ignore
.
          CALL      CALCDAYS
          SUBTRACT  ONE,CDYSDAYS            * get number of days waiting
.
          MOVE      CDYSDAYS,XXDAYS
.
.------   Write to the temp file  ------
.
          CALL      WRXXSLS1
          MOVE      ZERO,FORM1
          GOTO      EXOP2000                * get next attendance
.
EXOP9999  RETURN
.
.****************************************************************************
.*                             CLRF0000                                     *
.*                   Initialise the temporary file record                   *
.****************************************************************************
.
CLRF0000  MOVE      SP3,XXSPEC    
          MOVE      SP3,XXHSP    
          MOVE      SP1,XXIOIND    
          MOVE      SP6,XXCLIN    
          MOVE      SP1,XXATTIND    
          MOVE      ZERO,XXDAYS    
          MOVE      SP30,XXSPDESC
          MOVE      SP30,XXHSDESC
          MOVE      SP30,XXCLDESC
.
CLRF9999  RETURN
.
.***********************************************************************
.*  GCAN0000  :  Check if Patient Cancelled the Booking                *
.***********************************************************************
GCAN0000  MOVE      SP8,CANDATE
          PACK      KEY11,OBAOUTNO,SP20
          CALL      RDSRSHA1
GCAN1000  CALL      RDKRSHA1
          BRANCH    OVRCD,GCAN9999
.
          MATCH     OBAOUTNO,OPRSOUTN
          GOTO      GCAN9999 IF NOT EQUAL
.
          MATCH     OBACLIN,OPRSNCLI
          GOTO      GCAN1000 IF NOT EQUAL
.
          MATCH     OBADATE,OPRSNDAT
          GOTO      GCAN1000 IF NOT EQUAL
.
          MATCH     OBASTRT,OPRSNSTR 
          GOTO      GCAN1000 IF NOT EQUAL
. 
          COMPARE   OBASLOT,OPRSSLOT 
          GOTO      GCAN1000 IF NOT EQUAL
.
          MATCH     OBATIME,OPRSNTIM
          GOTO      GCAN1000 IF NOT EQUAL
.
          PACK      KEY5,ANSR,ANSR,OPRSREAS * Reschedule Reason
          CALL      RDCODE1
          BRANCH    OVRCD,GCAN1000
.
          MATCH     "P",TCINDC1             * Patient Cancellation?
          GOTO      GCAN1000 IF NOT EQUAL
.
. We have a patient cancellation
.
          MATCH     OPRSDATE,STRTDATE       * before period
          GOTO      GCAN9999 IF LESS
.
          MOVE      OPRSDATE,CANDATE
.
.
GCAN9999  RETURN
.
.***********************************************************************
.*  GDNA0000  :  Get the End Date for DNA's                            *
.***********************************************************************
.
. Check if the attendance was originally DNA'd. 
.
GDNA0000
          MOVE      OBAOUTNO,SAVRBKN
          MOVE      OBAOUTNO,SAVOUTNO
          MOVE      SP8,DNADATE
          PACK      KEY16,OBAOUTNO,SP20
GDNA1000  CALL      RSOTRAP2
          CALL      RKOTRAP2
          BRANCH    OVRCD,GDNA9000
.
          MATCH     SAVRBKN,OTRPRBKN        * same reappointment
          GOTO      GDNA9000 IF NOT EQUAL
.
. patient attendance is a reappointment
.
. check to see if original booking was DNA'd
.
          PACK      KEY8,OTRPOBKN,SP10
          CALL      RDBOKB1
          BRANCH    OVRCD,GDNA5000
.
          PACK      KEY5,ANSO,ANSZ,OTBBOUTC
          CALL      RDCODE1
          BRANCH    OVRCD,GDNA5000
.
          MATCH     "1",TCINDC1
          GOTO      GDNA5000 IF NOT EQUAL
.
          MATCH     STRTDATE,OBADATE         * before period
          GOTO      GDNA5000 IF LESS
.
          MOVE      OBADATE,DNADATE
.
          MOVE      ZERO,EXIT
          GOTO      GDNA9000
.
GDNA5000  PACK      KEY16,OTRPOBKN,SP20
          MOVE      OTRPOBKN,SAVRBKN
          GOTO      GDNA1000
.
GDNA9000  PACK      KEY8,SAVOUTNO,SP20
          CALL      RDBOKB1                 * restore file
.
GDNA9999  RETURN
.
.*****************************************************************************
.*                           PRNT0000                                        *
.*                        Print the report                                   *
.*****************************************************************************
.
PRNT0000  MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO
.
.------   Initialise the waiting time calculation fields
.
          MOVE      ZERO,PR1DAYS
          MOVE      ZERO,PR1MAX
          MOVE      "99999",PR1MIN
          MOVE      ZERO,PR1COUNT
.
          MOVE      ZERO,PR2DAYS
          MOVE      ZERO,PR2MAX
          MOVE      "99999",PR2MIN
          MOVE      ZERO,PR2COUNT
.
          MOVE      ONE,FIRSTREC            * indicator for first time thru
.
          DISPLAY   *P1:24,*EL,"Printing....."
.
          PACK      KEY14,SP30
          CALL      RSXXSLS1
PRNT1000  CALL      RKXXSLS1
          BRANCH    OVRCD,PRNT8000
.
          MATCH     "1",FIRSTREC            * First Record ?
          IF        @EQUAL
            MOVE      ZERO,FIRSTREC
            CALL      SVPR0000              * save variables for printing
            CALL      HDSP0000
            CALL      HDHS0000
            GOTO      PRNT2000
          ENDIF
.
          MATCH     XXSPEC,PRSPEC           * change of specialty ?
          IF        !@EQUAL
            CALL      PRWT0000              * print waiting times
            CALL      SVPR0000              * save variables for printing
            CALL      UND20000
            CALL      HDSP0000              * print specialty headings
            CALL      HDHS0000              * print hospital headings
            GOTO      PRNT2000
          ENDIF
.
          MATCH     XXHSP,PRHSP             * change of hospital ?
          IF        !@EQUAL
            CALL      PRWT0000              * print waiting times
            CALL      SVPR0000              * save variables for printing
            CALL      UND20000
            CALL      HDHS0000              * print hospital headings
            GOTO      PRNT2000
          ENDIF
.
          MATCH     XXCLIN,PRCLIN           * change of clinic id ?
          IF        !@EQUAL
            CALL      PRWT0000              * print waiting times
            CALL      SVPR0000              * save variables for printing
          ENDIF
.
.------   Accumulate totals & deduce max./min. waiting times
.
PRNT2000  MATCH     "1",XXATTIND            * Inpatient or Urgent OP totals ?
          GOTO      PRNT4000 IF NOT EQUAL
.
.------   Accumulate Inpatient totals / Urgent Outpatient totals
.
          ADD       ONE,PR1COUNT            * increment number of visits
          ADD       XXDAYS,PR1DAYS          * accumulate waiting time
.
          COMPARE   PR1MIN,XXDAYS
          GOTO      PRNT3000 IF NOT LESS
.
          MOVE      XXDAYS,PR1MIN           * minimum waiting time
.
PRNT3000  COMPARE   XXDAYS,PR1MAX
          GOTO      PRNT1000 IF NOT LESS
.
          MOVE      XXDAYS,PR1MAX           * maximum waiting time
.
          GOTO      PRNT1000
.
.------   Accumulate daycase totals / Routine Outpatient totals
.
PRNT4000  ADD       ONE,PR2COUNT            * increment number of visits
          ADD       XXDAYS,PR2DAYS          * accumulate waiting time
.
          COMPARE   PR2MIN,XXDAYS
          GOTO      PRNT5000 IF NOT LESS
.
          MOVE      XXDAYS,PR2MIN           * minimum waiting time
.
PRNT5000  COMPARE   XXDAYS,PR2MAX
          GOTO      PRNT1000 IF NOT LESS
.
          MOVE      XXDAYS,PR2MAX           * maximum waiting time
.
          GOTO      PRNT1000
.
PRNT8000  COMPARE   ZERO,CPAGENO
          IF        @EQUAL
            CALL      HEAD0000
            GOTO      PRNT9000
          ENDIF
.
          CALL      PRWT0000                * print remaining waiting times
          CALL      UND20000
.
PRNT9000  PRINT     *N:
                    "***** End of Report *****"
.
          CALL      KILL0000                * erase temp file

PRNT9999  RETURN
.
.*****************************************************************************
.*                           SVPR0000                                        *
.*                 Save Temporary File Variables for printing                *
.*****************************************************************************
.
SVPR0000  MOVE      XXSPEC,PRSPEC            * Specialty Code
          MOVE      XXSPDESC,PRSPDESC        * Specialty Description
          MOVE      XXHSP,PRHSP              * Hospital Code
          MOVE      XXHSDESC,PRHSDESC        * Hospital Name
          MOVE      XXIOIND,PRIOIND          * Inpat/Outpat Indicator
          MOVE      XXCLIN,PRCLIN            * Clinic Id
          MOVE      XXCLDESC,PRCLDESC        * Clinic Description
.
SVPR9999  RETURN
.
.*****************************************************************************
.*                           PRWT0000                                        *
.*                      Print Waiting Times                                  *
.*****************************************************************************
.
PRWT0000  MATCH     "1",PRIOIND             * Inpatient details ?
          GOTO      PRWT5000 IF NOT EQUAL   * no, print outpatient details
.
.------   Print Inpatient Details
.
          CALL      UND10000
          PRINT     *50,"|",*63,"Inpatients",*86,"|",*99,"Daycases",*122,"|"
          CALL      UND10000
          PRINT     *50,"| Average":
                    *62,"|    Max.":
                    *74,"|    Min.":
                    *86,"| Average":
                    *98,"|    Max.":
                    *110,"|    Min.":
                    *122,"|"
          CALL      UND20000
.
          ADD       FIVE,CLNO
.
          PRINT     *9,"| INPATIENTS";
          CALL      PRTA0000
.
          GOTO      PRWT9000
.
.------   Print Outpatient Details
.
PRWT5000  IF        CLNO > 58
            CALL      UND20000
            CALL      HDSP0000
            CALL      HDHS0000
          ENDIF
.
          MATCH     "1",OUTHEAD             * have headings already been printed
                                            * for this hospital ?
          GOTO      PRWT6000 IF EQUAL
.
          CALL      UND20000
          PRINT     *9,"|",*50,"|",*63,"Urgent",*86,"|",*99,"Routine",*122,"|"
          CALL      UND20000
          PRINT     *9,"| Outpatient Clinic Id":
                    *50,"| Average":
                    *62,"|    Max.":
                    *74,"|    Min.":
                    *86,"| Average":
                    *98,"|    Max.":
                    *110,"|    Min.":
                    *122,"|"
          CALL      UND20000
.
          ADD       FIVE,CLNO
.
          MOVE      ONE,OUTHEAD
.
PRWT6000  PRINT     *9,"| ",PRCLIN,SP2,PRCLDESC;
          CALL      PRTA0000
.
PRWT9000  MOVE      ZERO,PR1DAYS            * reset calculation fields
          MOVE      ZERO,PR1MAX
          MOVE      "99999",PR1MIN
          MOVE      ZERO,PR1COUNT
.
          MOVE      ZERO,PR2DAYS
          MOVE      ZERO,PR2MAX
          MOVE      "99999",PR2MIN
          MOVE      ZERO,PR2COUNT
.
PRWT9999  RETURN
.
.*****************************************************************************
.*                           PRTA0000                                        *
.*               Calculate and print waiting time averages for               *
.*               Inpatients & daycases OR                                    *
.*               Urgent & Routine Outpatient attendances                     *
.*****************************************************************************
.
PRTA0000  COMPARE   PR1COUNT,ZERO           * Any inpatient/urgent totals ?
          GOTO      PRTA1000 IF EQUAL
.
          ASSIGN    (PR1DAYS/PR1COUNT),AVWORK
          MOVE      AVWORK,AVDAYS
          ASSIGN    (AVDAYS/7),AVWORK
          MOVE      AVWORK,AVWEEKS
          ASSIGN    (PR1MAX/7),AVWORK
          MOVE      AVWORK,MAXWEEKS
          ASSIGN    (PR1MIN/7),AVWORK
          MOVE      AVWORK,MINWEEKS
.
          PRINT     *50,"| ",AVWEEKS:
                    *62,"| ",MAXWEEKS:
                    *74,"| ",MINWEEKS;
          ADD       ONE,CLNO
.
          GOTO      PRTA2000
.
PRTA1000  PRINT     *50,"| ":
                    *62,"| ":
                    *74,"| ";
          ADD       ONE,CLNO
.
PRTA2000  COMPARE   PR2COUNT,ZERO           * Any daycase/routine totals ?
          GOTO      PRTA3000 IF EQUAL
.
          ASSIGN    (PR2DAYS/PR2COUNT),AVWORK
          MOVE      AVWORK,AVDAYS
          ASSIGN    (AVDAYS/7),AVWORK
          MOVE      AVWORK,AVWEEKS
          ASSIGN    (PR2MAX/7),AVWORK
          MOVE      AVWORK,MAXWEEKS
          ASSIGN    (PR2MIN/7),AVWORK
          MOVE      AVWORK,MINWEEKS
.
          PRINT     *86,"| ",AVWEEKS:
                    *98,"| ",MAXWEEKS:
                    *110,"| ",MINWEEKS:
                    *122,"|" 
          ADD       ONE,CLNO
.
          GOTO      PRTA9999
.
PRTA3000  PRINT     *86,"| ":
                    *98,"| ":
                    *110,"| ":
                    *122,"|"
          ADD       ONE,CLNO
.
PRTA9999  RETURN
.
.*****************************************************************************
.*                           HDSP0000                                        *
.*                      Print Speciality Headings                            *
.*****************************************************************************
.
HDSP0000  CALL      HEAD0000
          PRINT     *N,*1,"Specialty : ",PRSPEC,SP2,PRSPDESC
          ADD       TWO,CLNO
.
HDSP9999  RETURN
.
.*****************************************************************************
.*                           HDHS0000                                        *
.*                      Print Hospital Headings                              *
.*****************************************************************************
.
HDHS0000  IF        CLNO > 42
            CALL      HDSP0000
          ENDIF
.
          PRINT     *N,*9,"Hospital : ",PRHSP,SP2,PRHSDESC,*N
          ADD       THREE,CLNO
.
          MOVE      ZERO,OUTHEAD
.
HDHS9999  RETURN
.
.*****************************************************************************
.*                           HEAD0000                                        *
.*                        Print the report heading                           *
.*****************************************************************************
.
HEAD0000  CALL      HEAD132
.
          STRIP     SSPCDESC
          STRIP     ESPCDESC
          STRIP     SHSPDESC
          STRIP     EHSPDESC
          STRIP     SCLIDESC
          STRIP     ECLIDESC
.
          PRINT     *40,"Period from : ",SDOL," to ",EDOL:
                    "  (All waiting times are shown as number of weeks.)",*N
          ADD       TWO,CLNO
.
HEAD2000  PRINT     "Speciality       : ",*20,*+,SSPCDESC," to ":
                                             ESPCDESC
          PRINT     "Hospital         : ",*20,*+,SHSPDESC," to ":
                                             EHSPDESC
          PRINT     "Clinic Id        : ",*20,*+,SCLIDESC," to ":
                                             ECLIDESC
.
          ADD       THREE,CLNO  
          MOVE      ZERO,OUTHEAD
.
HEAD9999  RETURN
.
.*****************************************************************************
.*                           UND10000                                        *
.*                   Print half length underline                             *
.*****************************************************************************
.
UND10000 PRINT     *50,"*-----------------------------------":
                       "------------------------------------*"
.
UND19999  RETURN
.
.*****************************************************************************
.*                           UND20000                                        *
.*                   Print full length underline                             *
.*****************************************************************************
.
UND20000 PRINT     *9,"*------------------------------":
                      "-------------------------------":
                      "-------------------------------":
                      "--------------------*"
.
UND29999  RETURN
.
.*****************************************************************************
.*                           CRET0000                                        *
.*                        Create the Tempfile                                *
.*****************************************************************************
.
CRET0000  CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    DIM8,CMDLINE
          APPEND    " 129 d1-3,4-6,7-7,8-13,14-14",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TEMPFILE,DIM8

CRET9999  RETURN
.
.*****************************************************************************
.*                           KILL0000                                        *
.*                        Kill the Tempfile                                  *
.*****************************************************************************
.
KILL0000  CLOSE     TEMPFILE
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DIM8,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          
KILL9999  RETURN
.
.===========================
. Temporary File IO Routines
.===========================
.
RDXXSLS1  RESET     KEY14
          MOVE      ZERO,OVRCD
          READ      TEMPFILE,KEY14;XXSPEC,XXHSP,XXIOIND,XXCLIN,XXATTIND:
                    XXDAYS,XXSPDESC,XXHSDESC,XXCLDESC
          GOTO      OVERCOND IF OVER
          RETURN
.
RSXXSLS1  RESET     KEY14
          READ      TEMPFILE,KEY14;;         
          RETURN
.
RKXXSLS1  MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXSPEC,XXHSP,XXIOIND,XXCLIN,XXATTIND:
                    XXDAYS,XXSPDESC,XXHSDESC,XXCLDESC
          GOTO      OVERCOND IF OVER
          RETURN
.
RPXXSLS1  MOVE      ZERO,OVRCD
          READKP    TEMPFILE;XXSPEC,XXHSP,XXIOIND,XXCLIN,XXATTIND:
                    XXDAYS,XXSPDESC,XXHSDESC,XXCLDESC
          GOTO      OVERCOND IF OVER
          RETURN
.
WRXXSLS1  RESET     KEY14
          WRITE     TEMPFILE,KEY14;XXSPEC,XXHSP,XXIOIND,XXCLIN,XXATTIND:
                    XXDAYS,XXSPDESC,XXHSDESC,XXCLDESC
          RETURN
.
UPXXSLS1  UPDATE    TEMPFILE;XXSPEC,XXHSP,XXIOIND,XXCLIN,XXATTIND:
                    XXDAYS,XXSPDESC,XXHSDESC,XXCLDESC
          RETURN
.
.*****************************************************************************
.*                            I/O Section                                    *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       BOKRC1IO/INC
          INC       IBASEQIO/INC
          INC       OUTBOCIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTOSFIO/INC
          INC       OUTRAPIO/INC             * Outpatients Reappointments
          INC       OUTRSHIO/INC             * Outpatients Reshedules
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       WATTR1IO/INC
          INC       WEBERRIO/INC
.
          INC       CALCDAYS
          INC       KYOUTCLI
          INC       OUTDSCLN
          INC       PATCODKY
          INC       PATDOCKY
          INC       PATHSPKY
          INC       TFILENAM
................................................................................

. *****************************************************************************
. *                                                                           *
. *                       P R O G R A M   S U M M A R Y                       *
. *                       -------------   -------------                       *
. *                                                                           *
. *     PROGRAM NAME:    IBAPAT41                                             *
. *                                                                           *
. *     FUNCTION:        DISCHARGES REGISTER                                  *
. *                                                                           *
. *     DATE WRITTEN:    25/02/86                                             *
. *                                                                           *
. *     AUTHOR:          MALCOLM "SPASTIC" HAYSE                              *
. *                                                                           *
. *****************************************************************************
. *      MODIFICATIONS:                                                       *
. *       V12.00.01 23.05.2025  DA Sarkies     TSK 0955096                    *
. *                 Updated for Alpha-Numeric Visit Number                    *
. *****************************************************************************
. *       V10.04.01 18/06/2014 Steve Armstrong  CAR 301639                    *
. *                 Moved call to TFILENAM for TEMPFILE to start of program.  *
. *****************************************************************************
. *       V10.03.01 31/12/2012 Patrick Adair  CAR 261630                      *
. *                 Removed port number from temp file name                   *
. *****************************************************************************
. *       V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
. *                 Added pmsvx1af                                            *
. *****************************************************************************
. *       V9.09.01  02/04/2008  Ebon Clements CAR 165065                      *
. *                 Added ACARE and ATYPE to temp file for surname sequence   *
. *****************************************************************************
. *       V9.08.01  01/06/2007  Mike Laming  CAR 140164                       *
. *                 Correct DDEST/DSTAT Indicator selection at DISPB          *
. *****************************************************************************
. *       V9.04.01  20/08/2004  Lina Vo                                       *
. *                 Changed Report to allow All Hospitals for Mult-Hospital   *
. *****************************************************************************
. *       V9.02.03  26/11/2002  Jill Habasque                                 *
. *                 Mods to print discharge destination instead of status     *
. *       V9.02.02  02/10/2002  Jill Habasque SRF 18718                       *
. *                 Mods to print caretype and admission type on basic report *
. *       V9.02.01  01/02/2002  J.Tan                                         *
. *                 Mods to print claim code on basic report                  *
. *****************************************************************************
. *       V9.00.B01 06/06/2001  Dean Cramer                                   *
. *                 Changed so the program will not exit with a read error on *
. *                 the transfer file but print all records.                  *
. *                 Create error print routines.                              * 
. *****************************************************************************
. *       V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *****************************************************************************
. *       V5.07.03  29/10/1999  Steve Downing SRF 646205                      *
. *                 Fixed printing of footer following missing ward/bed error *
. *       V5.07.02  01/07/1999  Jill Habasque SRF 632836                      *
. *                 Fixed printing of report heading                          *
. *                     V5.07.01  19/02/1999  Jim Stathopoulos                *
. *                               Report Mods                                 *
. *                     V5.07.B02 25/01/1999  Nicole Harrington 507 rebound   *
. *                               Mods to line upi report                     *
. *                     V5.07.01  20/11/1998  Jill Habasque                   *
. *                               Mods to include centuries                   *
. *                 09/11/1998  Glenn Berry      5.07 changes                 *
. *****************************************************************************
. *                     V5.04.02  4/7/97  Nick   SRF 643175                   *
. *                               Fixed creation of temp index file           *
. *                     V5.04.01  18/06/1997 Nick    SRF 642840               *
. *                               Removed rollout file garbage and replaced   *
. *                               temp file code with standard temp file code *
. *****************************************************************************
. *                     V5.01.02  23/08/89 J.Tan           SRF 102085         *
. *                               Fixed printing same U/R No.                 *
. *                     V5.01.03  03.10.89 S.Barcham       SRF 102555, 102569 *
. *                               Fixed Temporary File                        *
. *                     V5.01.04  19/04/90 J.Tan           SRF 104721         *
. *                               Fixed the key of TMPFILE1                   *
. *                     V5.01.121 24/10/90 i chung         SRF 106789         *
. *                               Added total no. of patients at end of report*
. *                   V5.01.122 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *                   V5.01.123 28/02/93 J.Tan                                *
. *                            Mods for multiple hospitals                    *
. *       V5.01.124 08.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
. *                 Modify to use standard routine KEYDATE                    *
. *       V5.01.125 11/04/1994  Glenn Berry      SRF 126905                   *
. *                 Fixed problems with report layout when using new files    *
. *       V5.01.126 25/05/1994 Ian Rutt                                       *
. *                 Fixed Global Recompile Bugs                               * 
. *       V5.01.127 20/07/1994  Rob Leonard                                   *
. *                 Fixed hospital keyin                                      *
. *       V5.02.01  03/04/1995  Greg Horvat      SRF 135301                   *
. *                 Fixed the reporting of the 4th address line by adding it  *
. *                 to the TMPFILE1                                           *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
          INC       PATWR1FD/INC
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.
.         CONSTANTS AND WORK VARIABLES
.
A10DATE    DIM       10
B10DATE    DIM       10
D10DATE    DIM       10
.
CMDLINE   DIM       50
CODEC     INIT      "C "
CODED     INIT      "D "
CODEDD    INIT      "DD"
CODEM     INIT      "M "
CODER     INIT      "R "
.
DANS      DIM       3
DBAS      INIT      "(Basic Report)   "
DCONT     DIM       20
DDET      INIT      "(Detailed Report)"
DDSEQ     INIT      "Date Sequence"
DDSTAT    DIM       25
DIM10     DIM       10
DIM12     DIM       12
DIM18     DIM       18
DIM23     DIM       23
DMSTAT    DIM       13
DOCNAME   DIM       19
DOPTB     DIM       17
DOPTION   DIM       16
DREG      DIM       13
DSSEQ     INIT      "Surname Sequence"
.
ERRFLG    FORM       1
.
FROMDATE  DIM        8 
.
IBCNMHOS  FORM       1
.
ISBUILD   INIT       "isbuild "
ISERASE   INIT       "iserase "
.
OPTB      FORM       1
PTCNDRSM  FORM       1
PATCOUNT  FORM       7                    * added for V5.01.121 - patients count
PAGENO    FORM       3
.
STATUS    FORM       1
.
TEMPFILE  DIM        8
TODATE    DIM        8          
.
XDAY1     DIM        2
XDAY2     DIM        2
XMON1     DIM        2
XMON2     DIM        2
XYEAR1    DIM        2
XYEAR2    DIM        2
XCENT1    DIM        2
XCENT2    DIM        2
.
UKEY      INIT       " 538 U1-28"
.
Z30       INIT       "zzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
TMPFILE1  IFILE      SQL, FIXED=538, KEY="U1-28"
.
. ------- Temp File variables -------
.
.Name     Type    Length     Physical    Start
.----     ----    ------     --------    -----
.PSNAME   DIM       20          20        1
DUNIQUE   DIM       8           8         21
XDADMNO   DIM       8           5         29
.XDAY     DIM       2           2         34
.XMON     DIM       2           2         36
.XYEAR    DIM       2           2         38
.XCENT    DIM       2           2         40
.DTIME    DIM       8           8         42 
.DURNO    DIM       8           8         50 
.PGNAME   DIM       25          25        58 
.DSTAT    DIM       3           3         83
.DDEST    DIM       3           3         86
DAYCASE   DIM       3           3         89
.TWARD    DIM       3           3         92
.TBED     DIM       3           3         95
.PTITL    DIM       4           4         98
.PADD1    DIM       35          35        102
.PADD2    DIM       35          35        137
.PSUBRB   DIM       35          35        172
.PADD4    DIM       35          35        207
.PPOST    DIM       8           8         242
.PMSTAT   DIM       3           3         250
.PSEX     DIM       1           1         253
.PTELEP   DIM       20          20        254
.PTELEB   DIM       20          20        274
.PSAGE    FORM      3           3         294
.PBDATE   DIM       8           8         297
.PCONT    DIM       3           3         305
.PREG     DIM       3           3         308
.POCCUP   DIM       14          14        401
.ADATE    DIM       8           8         415
.ADOCTA   DIM       6           6         423
.DDIAG    DIM       50          50        429
.DDIAG2   DIM       50          50        479
.ACLAIM   DIM       3           3         529
.ACARE    DIM       3           3         532
.ATYPE    DIM       3           3         535
.
.End of Record                            538
.
.  redefine form fields from key
.  -----------------------------
.Name     Type    Length      Start
.----     ----    ------      -----
UNIQUE    FORM      8           21
+
.
PRGID     INIT      "IBAPAT41"
PRGNAM    INIT      "Discharges Register"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,*EL,"Opening ";
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
.
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM                     *I-140164
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          DISPLAY   *P1:3,*EF;
.
          IF        IBCNMHOS = 1
            DISPLAY    *P1:5,*EL,"Hospital Id : ";
            MOVE       TEN5,ECOL
            MOVE       FIVE,EVERT
            MOVE       SP3,CKYIDEF3
            MOVE       ZERO,CKYIMAND           * Not Mandatory
            OPEN       PATHSPA1,"pathspaf"
.
            CALL       PATHSPKY
            BRANCH     EXIT,KYHOSP20,ENDIT
            GOTO       KYHOSP30
.
KYHOSP20    MOVE       "All Hospitals",PTHSNAME
            MOVE       SP10,PTHSHOSP
.
KYHOSP30    CLOSE      PATHSPA1
.
            DISPLAY    *P20:3,*V2LON,PTHSNAME;
          ENDIF
+
.
.         MAIN MENU
.
START     MOVE      ZERO,CPAGENO
          MOVE      ZERO,PATCOUNT             * initialize patients count
.
          HLINE     *G37,2,50,80
          DISPLAY   *P1:4,*EF:
                    *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:6,*V2LON,ONE,*HOFF," = Date Sequence":
                    *P1:7,*V2LON,TWO,*HOFF," = Patient Surname Sequence"
.
START1    HLINE     *G37,2,50,80
          KEYIN     *P1:9,*EF,"Select Option : ",*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF KOPT,KOPT
          BEEP
          GOTO      START1
.
NODSCH    DISPLAY   *P20:24,*EL,*V2LON,"** No Discharges on File **",*W2;
          GOTO      START
.
KOPT      LOAD      DOPTION USING OPTION OF DDSEQ,DSSEQ
          STRIP     DOPTION
          RESET     DOPTION
          DISPLAY   *P50:2,*V2LON,"- ",*+,DOPTION,SP1
.
.         Check if a detail report is wanted
.
          DISPLAY   *P1:10,*EF:
                    *P1:11,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:12,*V2LON,ONE,*HOFF," = Basic Report":
                    *P1:13,*V2LON,TWO,*HOFF," = Detailed Report"
.
CHKDET    KEYIN     *P1:15,"Select Option : ",*EL,*V2LON,OPTB
.
          COMPARE   ZERO,OPTB
          GOTO      START1 IF EQUAL
.
          BRANCH    OPTB OF KOPTB,KOPTB
          BEEP
          GOTO      CHKDET
.
KOPTB     LOAD      DOPTB USING OPTB OF DBAS,DDET
          DISPLAY   *P65:2,*V2LON,SP1,DOPTB
+
.         KEYIN TO DATE
.
KDDATE    DISPLAY   *P1:5,*EF,"Keyin From Date : ":
                        *P1:7,"Keyin To   Date : "
.
KADAT1    
....
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      CCC,CCENT
.
          MOVE      " 5",CVERT         * set up row
          MOVE      "20",CCOL          * set up collumn
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
.
          COMPARE   ZERO TO IDAY
          GOTO      START IF EQUAL
....
.
ENDAD1    PACK      FROMDATE,ICENT,IYEAR,IMON,IDAY
          REP       " 0",FROMDATE
          UNPACK    FROMDATE,XCENT1,XYEAR1,XMON1,XDAY1
.
.         DISPLAY TO DATE 
.
KADAT2    
....
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      CCC,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      " 7",CVERT         * set up row
          MOVE      "20",CCOL          * set up collumn
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
.
....
.
ENDAD2    PACK      TODATE,ICENT,IYEAR,IMON,IDAY
          REP       " 0",TODATE
          UNPACK    TODATE,XCENT2,XYEAR2,XMON2,XDAY2
.
CNTCHK    MATCH     FROMDATE TO TODATE * 'FROM' DATE MUST BE 
          GOTO      REKEY IF NOT LESS           * >= 'TO' DATE
.
INVDATG   DISPLAY   *P20:24,*EL,*B,*V2LON:
                    "** From Date must be greater than To Date **",*W2:
                    *P20:24,*EL;
          GOTO      KADAT1
.
.         CHECK IF DATES ARE OK OR NOT
.
REKEY     KEYIN     *P1:24,*EF," Date ok (",*V2LON,"Y",*HOFF,*DV,SLASH:
              		    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     "N" TO ANS
          GOTO      KADAT1 IF EQUAL
.
          MATCH     "Y" TO ANS
          GOTO      BRNOPT IF EQUAL
.
          BEEP
          GOTO      REKEY
.
BRNOPT    BRANCH    OPTION OF DSCHNO,SURNAM
.
INVMASTD  DISPLAY   *P20:23,*B,*EL:
                    "** Discharge ",*V2LON,DADMNO,*HOFF," has an invalid U/R ":
                    *V2LON,DURNO,*HOFF," ***",*W2
.
          PRINT     "** Discharge ",DADMNO," has an invalid U/R ":
                    DURNO," ... Please Contact I.B.A **"
          GOTO      NEXTA
.
SURNAM    CALL      CREAIDX
.
          DISPLAY   *P56:24,"Opening ";
          DISPLAY   *P64:24,TEMPFILE;
          OPEN      TMPFILE1,TEMPFILE
          MOVE      ONE TO UNIQUE
.
.         PRINT VIA DSCHS DATE  
.
DSCHNO    MOVE      "60",CLNO
          CLOCK     TIME,CTIMEIS
.
          DISPLAY   *P1:24,*EL,*P20:24,"Admission Number : "
.
          PACK      KEY16,XCENT1,XYEAR1,XMON1,XDAY1,SP8
          CALL      RDSDSCH2
NEXTA     CALL      RDKDSCH2
          BRANCH    OVRCD OF SORTD
.
          DISPLAY   *P40:24,*EL,*V2LON,DADMNO
.
.         Get the last ward the patient was in
.
          MOVE      ZERO,ERRFLG
          PACK      KEY30,DADMNO,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD OF INVTRN
.
          MATCH     DADMNO,TADMN
          GOTO      INVTRN IF NOT EQUAL
.
          CMATCH    "D",TMOVE
          GOTO      INVTRN IF NOT EQUAL
.
GETMAS    MOVE      DURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMASTD
.
          IF        IBCNMHOS =1
            MATCH     SP10,PTHSHOSP
            IF        !@EQUAL
              PACK      KEY6,TWARD,SP3
              CALL      RDWARD1
              BRANCH    OVRCD,NEXTA
              MATCH     PTWDHOSN,PTHSHOSP
              GOTO      NEXTA IF NOT EQUAL
            ENDIF
          ENDIF
.
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
.
          MATCH     DDATE,TODATE
          GOTO      OK10 IF EQUAL
          GOTO      SORTD IF LESS
.
OK10      CALL      ISDAYCAS
.
          BRANCH    OPTION TO SRTDATE,SRTNAME
.
SRTDATE   CALL      DISPDA
          GOTO      NEXTA
.
SRTNAME   MOVE      DADMNO,XDADMNO
          MOVE      UNIQUE,DUNIQUE
          PACK      KEY28 WITH PSNAME,DUNIQUE
          WRITE     TMPFILE1,KEY28;PSNAME,DUNIQUE,XDADMNO,XDAY,XMON:
                                   XCENT,XYEAR,DTIME,DURNO,PGNAME,DSTAT:
                                   DDEST,DAYCASE,TWARD,TBED,PTITL:
                                   PADD1,PADD2,PSUBRB,PADD4,PPOST,PMSTAT:
                                   PSEX,PTELEP,PTELEB,PSAGE,PBDATE,PCONT:
                                   PREG,POCCUP,ADATE,ADOCTA,DDIAG,DDIAG2:
                                   ACLAIM,ACARE,ATYPE
          ADD       ONE TO UNIQUE
          GOTO      NEXTA
.
NOOPEN    DISPLAY   *P1:24,*B,*EL,"** TEMPORARY STATISTICS  FILE ":
                    TEMPFILE," NOT FOUND **",*W2;
          RETURN
+
          
INVTRN    DISPLAY   *P1:24,*EL,*V2LON,*B,*P5:24:
                    DADMNO," Missing Ward/Bed in Transfer file.  ":
                    "Contact I.B.A. immediately ",*W3;
.          GOTO      ENDP
.
          MOVE      ONE,ERRFLG
          MOVE      DURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,INVTRN1
          GOTO      INVTRN2
.
INVTRN1   MOVE      SP8,DURNO
.
INVTRN2   UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          IF  OPTB=1
             MOVE "Missing Ward/Bed in ",PSNAME
           ELSE
             MOVE "err",TWARD 
          ENDIF
.
          COMPARE   TWO,OPTION
          GOTO      SRTNAME IF EQUAL
.
          CALL      DISPDA
          GOTO      NEXTA
.
SORTD     BRANCH    OPTION OF ENDP   **** DATE SEQUENCE IS PRINTED ALREADY
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON,"** Processing Statistics **",*W;
.
          CLOSE     TMPFILE1
.
SORTFI    TRAP      NOOPEN IF IO
          OPEN      TMPFILE1,TEMPFILE
          TRAPCLR   IO
.
          MOVE      SP30 TO KEY28
          READ      TMPFILE1,KEY28;;
          DISPLAY   *P1:24,*EL,*P20:24,"Patient Name :"
.
READLOP   READKS    TMPFILE1;PSNAME,DUNIQUE,XDADMNO,XDAY,XMON:
                             XCENT,XYEAR,DTIME,DURNO,PGNAME,DSTAT:
                             DDEST,DAYCASE,TWARD,TBED,PTITL:
                             PADD1,PADD2,PSUBRB,PADD4,PPOST,PMSTAT:
                             PSEX,PTELEP,PTELEB,PSAGE,PBDATE,PCONT:
                             PREG,POCCUP,ADATE,ADOCTA,DDIAG,DDIAG2:
                             ACLAIM,ACARE,ATYPE
          GOTO      ENDP IF OVER
          MOVE      XDADMNO,DADMNO
          MOVE      ZERO,ERRFLG
.
          DISPLAY   *P40:24,*EL,*V2LON,PSNAME
.
          MATCH     "Missing Ward/Bed in ",PSNAME
          GOTO      PRTNOW IF NOT EQUAL
.
          MOVE      ONE,ERRFLG
.
PRTNOW    CALL      DISPDA
          GOTO      READLOP
.
.         OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NODSCH IF EQUAL
.
          CALL      PAGEND
          PRINT     *N,"  Total Number of Patients discharged : ",PATCOUNT:
                    *N,*N,*N,"  ***  End of Report  ***"
.
          DISPLAY   *P20:24,*EL,*V2LON,"** Report Generation Completed **",*W2;
.
END       BRANCH    OPTION TO START
.
          CALL      KILLIDX
          GOTO      START
.
.         PRINT ACTUAL DATA 
.
DISPDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
          ADD       ONE,PATCOUNT                * initialize patients count
.
          BRANCH    OPTB OF DISPB,DISPD
.
.         Basic Report
.
DISPB     MOVE      SP20,TDESC
.                                                                     *C-140164
          IF        PTCNDRSM=1
            PACK      KEY3,DSTAT
            PACK      KEY5,ANSD,SP1,DSTAT,SP5
          ELSE
            PACK      KEY3,DDEST
            PACK      KEY5,ANSD,ANSD,DDEST,SP5
          ENDIF
.
          MATCH     SP3,KEY3
          IF        !@EQUAL
            CALL      RDCODE1
            MOVE      TDESC,DDSTAT
          ENDIF
.
          IF  ERRFLG=1
            GOTO DISPBE   
          ENDIF
.
          MOVE      SP70,DIM10
          MOVE      SP20,TDESC
          MOVE      PGNAME,DIM12
.
          PRINT     *1,"|",DADMNO,*10,"|",XDAY,"/",XMON,"/",XYEAR:
                    *19,"|",DTIME:
                    *28,"|",DURNO,*37,"|",PSNAME,DIM12:
                    *70,"|",ACLAIM:
                    *74,"|",DDSTAT:
                    *105,"| ",ACARE,SP2,ATYPE,*115,"| ",DAYCASE,*122,"| ",TWARD:
                    "| ",TBED,*132,"|"
.
          ADD       ONE,CLNO
          RETURN
+
.
.         Basic Report - Error Line
.
DISPBE    MOVE      "Trans. File",DIM12
.
          PRINT     *1,"|",DADMNO,*10,"|",XDAY,"/",XMON,"/",XYEAR:
                    *19,"|",DTIME:
                    *28,"|",DURNO,*37,"|",PSNAME,DIM12:
                    *70,"|",ACLAIM:
                    *74,"|"," **error**error**",*94,"|"," **error**":
                    *105,"|",115,"| ",*122,"| ",SP3,"| ",*132,"|"
.
          ADD       ONE,CLNO
          RETURN
+
.
.
.         Detailed Report
.
DISPD     UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          PACK      A10DATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          UNPACK    PBDATE WITH CCENT,CYEAR,CMON,CDAY
          PACK      B10DATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          PACK      D10DATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          MOVE      SP30,DOCNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF GCODES
.
          MOVE      DGNAME,ANS
          PACK      DOCNAME WITH ANS,DOT,DSNAME
.
GCODES    MOVE      SP20,TDESC
          PACK      KEY5 WITH CODEDD,DDEST
          CALL      RDCODE1
          MOVE      TDESC,DDSTAT
.
          MOVE      SP20,TDESC
          PACK      KEY5 WITH CODEM,PMSTAT
          CALL      RDCODE1
          MOVE      TDESC,DMSTAT
.
          MOVE      SP20,TDESC
          PACK      KEY5 WITH CODER,PREG
          CALL      RDCODE1
          MOVE      TDESC,DREG
.
          MOVE      SP20,TDESC
          PACK      KEY5 WITH CODEC,PCONT
          CALL      RDCODE1
          MOVE      TDESC,DCONT
.
          CALL      PAGEND
.
          MATCH     "err",TWARD
          GOTO      DISPD1 IF NOT EQUAL
.
          MOVE      ONE,ERRFLG
.
DISPD1    COMPARE   ONE,ERRFLG
          GOTO      DISPD2 IF NOT EQUAL
.
          PRINT     *1,"**error** **error** **error**",SP3,"Missing Ward/Bed ":
                    "in Transfer File",SP3,"**error** **error** **error**"  
.
DISPD2    MOVE      PSNAME,DIM18
          MOVE      PGNAME,DIM23
          PRINT     *1,"|",DADMNO,*10,"|",A10DATE:
                    *21,"|",PTITL,SP1,DIM18,*45,"|",PADD1:
                    *86,"|",B10DATE,*97,"|",DMSTAT:
                    *112,"|",DDSTAT,*132,"|",*N:
                    *1,"|",*10,"|",*21,"|",DIM23,*45,"|",PADD2:
                    *86,"|",PSEX,SP6,PSAGE,*97,"|",DREG:
                    *112,"|",DOCNAME,*132,"|",*N:
                    *1,"|",DURNO,*10,"|",D10DATE,*21,"|",*45,"|",PSUBRB:
                    *86,"|",*97,"|",*112,"|",*132,"|",*N:
                    *1,"|",*10,"|",*21,"|",POCCUP,*45,"|",PADD4:
                    *86,"|",*97,"|",*112,"|",*132,"|",*N:
                    *1,"|",*10,"|",*21,"|",DCONT,*45,"|",PPOST:
                    *75,"*-----------------------------":
                              "---------------------------|",*N:
                    *1,"|",*10,"|",*21,"|",*45,"|",PTELEP:
                    *75,"| ",DDIAG,*132,"|",*N:
                    *1,"|",*10,"|",*21,"|",*45,"|",PTELEB:
                    *75,"| ",DDIAG2,*132,"|"
          IF  ERRFLG=1
            ADD EIGHT,CLNO
           ELSE      
            ADD SEVEN,CLNO
          ENDIF
          RETURN
+
.
.         LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,CLNO
          RETURN
+
.         HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,PAGENO
.
          COMPARE   ONE,PAGENO
          CALL      PAGEND IF NOT EQUAL
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *40,DOPTION,SP2,DOPTB;
.
          IF        IBCNMHOS =1
            PRINT     *N,*40,"Hospital : ",PTHSHOSP,"    ",PTHSNAME
          ENDIF
.
          PRINT     *N,*40,"From Date : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    *68,"To Date : ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2,*N
.
          CALL      PAGEND
.
          BRANCH    OPTB OF HEADB,HEADD
.
HEADB     PRINT     *1,"| Adm No",*10,"|Dis Date":
                    *19,"|Dis Time",*28,"| U/R No":
                    *37,"| Patient Name":
                    *70,"|CLM":
                    *74,"| Discharge Destination":
                    *105,"|Care type ",*115,"|D'Case":
                    *122,"|Ward| Bed",*132,"|"
.
          CALL      PAGEND
.
          IF        IBCNMHOS = 1
            MOVE      NINE,CLNO
          ELSE
            MOVE      EIGHT,CLNO
          ENDIF
.
          CLOCK     TIME,CTIMEIS
          RETURN
.
HEADD     PRINT     *1,"| Adm No",*10,"|Adm Date",*21,"| Patient Name":
                    *45,"| Patient Address":
                    *86,"|Birth Date",*97,"|Marital Status":
                    *112,"| Discharge Dest",*132,"|",*N:
                    *1,"|",*10,"|",*21,"| Patient Occupation":
                    *45,"| Phone (P)",*86,"|Sex    Age":
                    *97,"|Religion",*112,"| Attending Doctor",*132,"|",*N:
                    *1,"| U/R No",*10,"|Dis Date",*21,"| Country of Birth":
                    *45,"| Phone (B)",*75,"| Discharge Diagnosis",*132,"|"
          MOVE      TEN1,CLNO
          CLOCK     TIME,CTIMEIS
          RETURN
+
.
.         Subroutine to determine whether or not this is a Day Case
.
ISDAYCAS  MOVE      SP3,DAYCASE
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF ISDAYRET
.
          MATCH     DDATE,ADATE
          GOTO      ISDAYRET IF NOT EQUAL
.
          MOVE      DYES,DAYCASE
ISDAYRET  RETURN
+
.
.         Deleting AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLOSE     TMPFILE1
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,TEMPFILE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
CREAIDX   CALL      KILLIDX
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID
          RETURN
+
ENDIT    CALL       KILLIDX
         CHAIN      PGM
         STOP
.=============================================================================
.
          INC        PATHSPKY
          INC        STD001IO
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PATHSPIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

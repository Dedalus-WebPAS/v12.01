.-----------------------------------------------------------------------------
. Program       : PATWEB09
.
. Function      : Bulk Admission/Discharge Processing        
.
. Modifications  :
.-----------------------------------------------------------------------------
. V11.04.01 10/05/2024  Nikitha B     TSK 0945137
.           Added LOOPCNTR variable for PMSUPGCD.
. V11.02.01 10/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V10.11.02 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.01 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
.-----------------------------------------------------------------------------
. V10.08.01 15/06/2016  Ebon Clements  TSK 0318672
.           Added count OP appointments by associated number funcionality to
.           patient programs
. V10.06.01 24/03/2015  Don Nguyen      CAR 314369
.           Added clear for MRGEFLAG in CLRPAR00
.-----------------------------------------------------------------------------
. V10.04.02 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.-----------------------------------------------------------------------------
. V10.03.02 21/05/2012  Jill Parkinson  CAR 257615
.           Added NOPROGRM flag
. V10.03.01 09/11/2011  Jill Parkinson  CAR 251499
.           Added read of ptcnrcbn
. V10.02.07 11/10/2011  Mark Coupland    CAR 252429
.           Recompiled for PMSPX2TM and PATMASTM
. V10.02.06 13/09/2011 Jill Parkinson  CAR 248652
.           Added popupadd cgi
. V10.02.05 13/09/2011 Jill Parkinson  CAR 248652
.           Recompiled for PMSPX2TM - Added read of pmsvx1af in GETCURIP
. V10.02.04 03/08/2011  Jill Parkinson CAR 240712
.           Fixed ward selection list
. V10.02.03 26/07/2011  Sandra Barcham CAR 240712
.           Fix position of PTCNRCBM should be 126 not 1
. V10.02.02 12/07/2011  Jill Parkinson CAR 240725
.           Added save ward,bed,date and time fields
. V10.02.01 23/06/2011  Peter McMullen CAR 240725
.           Change ward selection list to sort by name, not code 
. V10.02.00 29/04/2011  Jill Parkinson CAR 240712
.           Created program
.
.-----------------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       WEBDATDF
          INC       ALLPCTFD/INC
          INC       APSMASFD/INC
          INC       BOKRC1FD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCONFD/INC
          INC       PATCONFD/INC
          INC       PATCALAG/INC
          INC       PATHSPFD/INC
          INC       PATPR1FD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       PATMI1FD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       OUTSITFD/INC
          INC       PATVISFD/INC
          INC       PATFN1FD/INC
          INC       PATIN1FD/INC
          INC       PATDO1FD/INC
          INC       PATALRFD/INC
          INC       PATMA1FD/INC
          INC       PATDSCFD/INC
          INC       PMSEVTFD/INC
          INC       PATICDFD/INC
          INC       PATICOFD/INC
          INC       PATDADFD/INC
          INC       PATUNAFD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSDTCFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCGFD/INC
          INC       PMSRCBFD/INC
          INC       PMSUPGFD/INC
          INC       PMSUPOFD/INC
          INC       PMSHPGFD/INC
          INC       PMSHPOFD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PMSPRBFD/INC
          INC       PMSAUPFD/INC
.
          INC       PMSHCPTD/INC
          INC       PMSHCGTD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
.
BAPRC001  DIM       8       * Admission Date
BAPRC002  DIM       8       * Admission Time
BAPRC003  DIM       8       * Discharge Time
BAPRC004  DIM       3       * Ward
BAPRC005  DIM       3       * Specialty
BAPRC006  DIM       6       * Printer
.
ADDNEWPG  FORM      1
AUDITKEY  DIM       36                   * Audit Key
AUDITTYP  DIM       1                    * Audit Type
ACTNCODE  DIM       3                    * Action Code 
ACTNCOMM  DIM       40                   * Comment          
ATYPDESC  DIM       20
.
ACTIDESC  DIM       8                    * Active or Inactive description
ADOCNAME  DIM       50
BATCHNUM  DIM       8
BRKODESC  DIM       3
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
CLAMDESC  DIM       20
DISCFLAG  FORM      1
DISPSUR   DIM       8  
DISPDATE  DIM       11
DISPMUR   DIM       8 
DOCTCODE  DIM       6
DSCHFLAG  FORM      1
EDITFLAG  FORM      1
EXPIRYDT  DIM       8
FORM8     FORM      8
FORM5     FORM      5
IPVCOUNT  FORM      3
IPVCMAXI  FORM      3
IPVEXTRA  FORM      3
IPVCWARN  FORM      3
LOOPCNTR  FORM      3
FOUNDFLG  FORM      1
MRGEFLAG  FORM      1
NEXTPAGE  DIM       1                    * Next page parameter
NEWVISIT  FORM      1
NOPROGRM  FORM      1
OPVCOUNT  FORM      3
OPVCMAXI  FORM      3
OPVEXTRA  FORM      3
OPVCWARN  FORM      3
POPUPADD  DIM       1
PROBLEMN  DIM       5                    * Problem number
PSAGETYP  DIM       1
PSAGECON  DIM       3
RADMDATE  DIM       8
RADMTIME  DIM       8
RADMWARD  DIM       3
RADMSPEC  DIM       3
RADMDTIM  DIM       8
SAVADOCT  DIM       10
SAVAUNIT  DIM       3
SAVKEY34  DIM       34
SAVSDOCT  DIM       10
SAVSUNIT  DIM       3
SAVEURNO  DIM       8
SERVER    DIM       13      * Server Name
REPTNO    DIM       3
TABTDESC  DIM       20
TRANDATE  DIM       8
TRANTIME  DIM       8
UPDTTYPE  DIM       1                    * Update Type
UPVISFLG  FORM      1
UNITDESC  DIM       25
VTYPDESC  DIM       20
.
VALDURNO  DIM       8
VALDCLAM  DIM       3
VALDATYP  DIM       3
VALDFUND  DIM       6
VALDTABT  DIM       3
VALDEDAT  DIM       8
VALDSTYP  DIM       3
VALDVTYP  DIM       1
WARDCODE  DIM       3
.
UNADMIT   INIT      "Unadmit"
UNDSCH    INIT      "Undischarge"
CHGADM    INIT      "Change Admission Date"
CHGDSC    INIT      "Change Discharge Date"
CANCPRE   INIT      "Cancel Preadmission"
.
.-----------------------------------------------------------------------------
PRGID     INIT      "PATWEB09"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Bulk Admission Processing"
.-----------------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      BLKADM00                * Bulk Admission Details
          BRANCH    EXIT,MAIN1000
          MOVE      "Enquiry Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Patient Enquiry Page
.------------------------------------------------------------
GETPAT00  MOVE      ZERO,MASTFLAG
          MOVE      ZERO,MISSFLAG
          MOVE      ZERO,RESPFLAG
          MOVE      ZERO,DSCHFLAG
          MOVE      SP70,DOCTCODE
          CALL      IBACLOCK
          CALL      CLPATMAS
          CALL      CLPATMIS
          CALL      CLPATDSC
          CALL      CLPMSPX2
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          MOVE      OVRCD,MASTFLAG
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          IF        PSTAT=1 | PSTAT=2
            MOVE      PURNO,SAVEURNO
            MOVE      PPSNAME,URNUMBER
            MOVE      ONE,MRGEFLAG
            GOTO      GETPAT00
          ENDIF
.
          IF        MASTFLAG=1
            MOVE      "Can't find patient master details",WEBTITLE
            CALL      WEBERR00
            GOTO      GETPAT99
          ENDIF

          MOVE      ADMISSNO,KEY8
          MATCH     SP70,KEY8
          IF        @EQUAL
            MOVE      ONE,MISSFLAG
            MOVE      ONE,RESPFLAG
            MOVE      ONE,DSCHFLAG
            GOTO      GETPAT05
          ELSE
            CALL      RDMISS1
            MOVE      OVRCD,MISSFLAG
            CALL      RDPTDAD1
            IF        OVRCD=1
              MOVE      SP70,PTDAADTS
              MOVE      SP70,PTDADCTS
            ENDIF
.
            CALL      RDPTVIS1
          ENDIF
.
          MOVE      ADMISSNO,KEY8
          CALL      RDPMVX11
.
          MOVE      ADMISSNO,KEY8
          CALL      RDDSCH1
          MOVE      OVRCD,DSCHFLAG
          IF        OVRCD=0
            MOVE      DDATE,ICDRDDTE   * Needed for RDPTICD1 & RDPTICO1 reads
          ENDIF
.
          MOVE      ADMISSNO,KEY8
          CALL      RDBKREC1
.
          MOVE      SP70,PMEVEVNT
          MOVE      ADMISSNO,KEY8
          CALL      RDPMEVT1
.
GETPAT05  CALL      PATNAA00                * format name without bold tags
.
.                                                 * read nutrition file
.
.    Webtplaf must be read here to get correct description for WEBTITLE
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          PACK      KEY13,PRGID,KEY2,TEMPLATE,SP70
          CALL      RDWBTP1
          CLEAR     WEBTITLE
          APPEND    WBTPDES,WEBTITLE
          APPEND    PATFNAME,WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,GETPAT99
.
          CALL      WEBBOD00   * Output Templated Body of HTML
          IF        MRGEFLAG=1
            CALL      MRGALT00
          ENDIF
.
GETPAT90  IF        PTCNNHII=1
            MOVE      URNUMBER,KEY8
            CALL      RDNHMAS2
            IF        OVRCD=1
              MOVE      "Missing nhimasaf record",WEBTITLE
              CALL      WEBERR00
              GOTO      GETPAT99
            ENDIF
          ENDIF
          CALL      WEBEND00   * Output End of Web Page
.
.
GETPAT99  RETURN
.------------------------------------------------------------
. Alert that this is a merged U/R
.------------------------------------------------------------
MRGALT00  MATCH     SP70,URNUMBER
          GOTO      MRGALT99 IF EQUAL
.
          MATCH     SP70,SAVEURNO
          GOTO      MRGALT99 IF EQUAL
.
          MOVE      SAVEURNO,DISPSUR
          SQUEEZE   DISPSUR
          MOVE      URNUMBER,DISPMUR
          SQUEEZE   DISPMUR
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " alert('U/R Number ",DISPSUR," is merged with ":
                             DISPMUR," \n Using ",DISPMUR,"');":
                             "}":
                             "</script>"
          GOTO      MRGALT99
.
MRGALT99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      PMSPRBA1,"pmsprbaf"
          OPEN      PMSPRBA2,"pmsprbaf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSAUPA1,"pmsaupaf" 
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATDADA1,"patdadaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSEVTA1,"pmsevtaf"
          OPEN      PATICDO1,"paticdof"
          OPEN      PATUNAA1,"patunaaf"
          OPEN      PATMI1A8,"patmi1af"
          OPEN      PATICDO1,"paticdof"
          OPEN      PATI10O1,"pati10of"
          OPEN      PMSDTCA1,"pmsdtcaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      ALLPCTA1,"allpctaf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A7,"patwr1af"
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSRCBA1,"pmsrcbaf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,THIRTY2;*143,OTCNCASS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          OPEN      OUTSITA1,"outsitaf"     * Open the site file
          OPEN      OUTSITA3,"outsitaf"     * Open the site file
          MOVE      "out",CFILEPRE          * Save Current File Prefix
          CALL      OPNOUT00                * Open Outpatient Files
.
          MOVE      ZERO,NOPROGRM
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PMSHPGA1,"pmshpgaf"
          TRAPCLR   IO
          IF        OVRCD<>1
            OPEN      PMSHPOA1,"pmshpoaf"
            OPEN      PMSUPGA1,"pmsupgaf"
            OPEN      PMSUPOA1,"pmsupoaf"
          ELSE
            MOVE      ONE,NOPROGRM
          ENDIF
.
          RETURN
.
.------------------------------------------------------------
. Open Outpatient Files
.------------------------------------------------------------
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBOKA6  * Get the name of the BOKA3 file
          CLOSE     OUTBOKA6
          OPEN      OUTBOKA6,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILBB1A1  * Get the name of the BOKA3 file
          CLOSE     OUTBB1A1
          OPEN      OUTBB1A1,CFNAME
.
OPNOUT99  RETURN
.
.*******************************************************************************
. COTFIL00 - Check that the correct outpatient site prefix files are open
.            according to the visit files site code. If the site is invalid
.            or blank just open the default site of out.

.*******************************************************************************
COTFIL00  MATCH     SP70,PVISITE             * Does the visit have a site
          GOTO      COTFIL50 IF NOT EQUAL
.
COTFIL10  MATCH     "out",CFILEPRE           * Is out currently open
          IF        !@EQUAL
            MOVE      "out",OSTFILE          * open default prefix of out
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open out Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
COTFIL50  MOVE      PVISITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,COTFIL10           * error invalid site code
.
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open prefixed Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
COTFIL99  RETURN
+
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      Z70,POPUPADD
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      ZERO,NEXTPAGE
          MOVE      ZERO,PROBLEMN
          MOVE      Z70,BATCHNUM
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,FRSTDATE
          MOVE      ZERO,VIEWTYPE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      Z70,EXPIRYDT
          MOVE      ZERO,DISCFLAG
.
          MOVE      SP70,ACTNCODE
          MOVE      SP70,ACTNCOMM
.
          MOVE      SP70,VALDURNO
          MOVE      SP70,VALDCLAM
          MOVE      SP70,VALDATYP
          MOVE      SP70,VALDFUND
          MOVE      SP70,VALDTABT
          MOVE      SP70,VALDEDAT
          MOVE      SP70,VALDSTYP
          MOVE      SP70,VALDVTYP
          MOVE      SP70,VALDSTYP
.
          MOVE      Z70,RADMDATE
          MOVE      Z70,RADMTIME
          MOVE      Z70,RADMWARD
          MOVE      Z70,RADMSPEC
          MOVE      Z70,RADMDTIM
.
          MOVE      ZERO,MRGEFLAG
.
CLRPAR99  RETURN
+
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "popupadd=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,POPUPADD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "batchnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,BATCHNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "discflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DISCFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "expirydt=",QRYNAME
          IF        @EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00              * Set CMON from MTHNAM3
            PACK      QRYDATA,CCENT,CYEAR,CMON,CDAY
            PACK      EXPIRYDT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdurno=",QRYNAME
          IF        @EQUAL
            PACK      VALDURNO,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdclam=",QRYNAME
          IF        @EQUAL
            PACK      VALDCLAM,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdatyp=",QRYNAME
          IF        @EQUAL
            PACK      VALDATYP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdfund=",QRYNAME
          IF        @EQUAL
            PACK      VALDFUND,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdtabt=",QRYNAME
          IF        @EQUAL
            PACK      VALDTABT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdedat=",QRYNAME
          IF        @EQUAL
            MATCH     SP70,QRYDATA
            GOTO      SETPAR99 IF EQUAL
            GOTO      SETPAR99 IF EOS
.
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00              * Set CMON from MTHNAM3
            PACK      VALDEDAT,CCENT,CYEAR,CMON,CDAY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdstyp=",QRYNAME
          IF        @EQUAL
            PACK      VALDSTYP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdvtyp=",QRYNAME
          IF        @EQUAL
            PACK      VALDVTYP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "radmdate=",QRYNAME
          IF        @EQUAL
            MATCH     SP70,QRYDATA
            GOTO      SETPAR99 IF EQUAL
            GOTO      SETPAR99 IF EOS
.
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00              * Set CMON from MTHNAM3
            PACK      RADMDATE,CCENT,CYEAR,CMON,CDAY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "radmtime=",QRYNAME
          IF        @EQUAL
            PACK      RADMTIME,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "radmward=",QRYNAME
          IF        @EQUAL
            PACK      RADMWARD,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "radmspec=",QRYNAME
          IF        @EQUAL
            PACK      RADMSPEC,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "radmdtim=",QRYNAME
          IF        @EQUAL
            PACK      RADMDTIM,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      RCURF000   
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------
. Recurring Care Fields
.------------------------------------------------------------
RCURF000  BRANCH    FLDITMNO,RCURF010,RCURF020,RCURF030,RCURF040,RCURF050:
                             RCURF060,RCURF070,RCURF080,RCURF090,RCURF100
          GOTO      RCURF999
.
RCURF010  CALL      WARDSL00
          GOTO      RCURF999
.
RCURF020  MATCH     Z70,BATCHNUM
          IF        !@EQUAL
            WRITE     HTMLFILE;BATCHNUM;
          ELSE
            READ      CONTROLF,HUND18;*126,PTCNRCBN
            WRITE     HTMLFILE;PTCNRCBN;
          ENDIF
          CALL      GENBATNO
          GOTO      RCURF999
.
RCURF030  CALL      OUTBT000         * Output batch details
          GOTO      RCURF999
.
RCURF040  WRITE     HTMLFILE;DISCFLAG;
          GOTO      RCURF999
.
RCURF050  MATCH     RADMDATE,SP70
          GOTO      RCURF999 IF EQUAL
          UNPACK    RADMDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      RCURF999
.
RCURF060  WRITE     HTMLFILE;RADMTIME;
          GOTO      RCURF999
.
RCURF070  WRITE     HTMLFILE;RADMDTIM;
          GOTO      RCURF999
.
RCURF080  WRITE     HTMLFILE;RADMWARD;
          GOTO      RCURF999
.
RCURF090  WRITE     HTMLFILE;RADMSPEC;
          GOTO      RCURF999
.
RCURF100  WRITE     HTMLFILE;POPUPADD;
          GOTO      RCURF999
.
RCURF999  RETURN
+
.------------------------------------------------------------
. Output Batch Details
.------------------------------------------------------------
OUTBT000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MATCH     Z70,BATCHNUM
          GOTO      OUTBT999 IF EQUAL
.
          PACK      KEY16,BATCHNUM,SP70
          CALL      RSPMRCB1
OUTBT100  CALL      RKPMRCB1
          BRANCH    OVRCD,OUTBT999
.
          MATCH     BATCHNUM,PMRCBATC
          GOTO      OUTBT999 IF NOT EQUAL
.
          PACK      KEY8,PMRCADMN,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,OUTBT100
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,OUTBT100
          CALL      RDPMPX21
          BRANCH    OVRCD,OUTBT100
          CALL      PATLN000
.
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,OUTBT100
.
          MATCH     DTADMN,DAADMNO
          GOTO      OUTBT100 IF NOT EQUAL
.
          MOVE      SP70,DDATE
          MOVE      SP70,DTIME
          IF        ASTAT=3
            PACK      KEY8,AADMNO,SP70
            CALL      RDDSCH1
            BRANCH    OVRCD,OUTBT100
          ENDIF
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,OUTBT100
          CALL      RDPMPX21
          BRANCH    OVRCD,OUTBT100
.
          CALL      PATLN000
.
          PACK      KEY6,TWARD,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            PACK      WWARD,SP70
          ENDIF
.
          PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            PACK      TDESC,SP70
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:removeAdmission('",HTMLLINK
          APPEND    AADMNO,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          WRITE     HTMLFILE;"t.addRow(#"",FORMATNM,"#",":
                             "#"",AURNO,"#",":
                             "#"",AWARD,"#",":
                             "#"",ADATE,"#",":
                             "#"",ATIME,"#",":
                             "#"",DTIME,"#",":
                             "#"",ACLSSFT,"#",":
                             "#"",TDESC,"#",":
                             "#"",WBDESC,"#",":
                             "#"",HTMLLINK,"#")"
          GOTO      OUTBT100
.
OUTBT999  RETURN
+
.------------------------------------------------------------
. Ward Slection List
.------------------------------------------------------------
WARDSL00  PACK      KEY29,SP70   
          IF        IBCNMHOS = 1 
            PACK      KEY29,SP3,WBSEHOSP,SP70 
          ENDIF                              
          CALL      RDSWARD7                
WARDSL10  CALL      RDKWARD7               
          BRANCH    OVRCD,WARDSL99        
.                                  
.                         if we have a bed number we have passed all the ward
.                         master records, so exit                          
          MATCH     SP70,WBED       
          GOTO      WARDSL99 IF NOT EQUAL 
.                                        
          COMPARE   WACTIVE,ZERO                 *List only active wards  
          GOTO      WARDSL10 IF NOT EQUAL                  
.                                                         
          IF        IBCNMHOS = 1                         
            MATCH      SP3,WBSEHOSP                     
            IF        !@EQUAL                          
              MATCH     WBSEHOSP,PTWDHOSN             
              GOTO      WARDSL99 IF NOT EQUAL        
            ENDIF                                   
          ENDIF                                    
.                                                 
.                       write the select option. 
          WRITE     HTMLFILE;"<option value=#"",WWARD,WCLASS,"#"";
          MATCH     WARDCODE,WWARD                        
          IF        @EQUAL                               
            WRITE     HTMLFILE;" selected";             
          ENDIF                                        
          WRITE     HTMLFILE;">",WBDESC;
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        @EQUAL
              WRITE     HTMLFILE;" - ",PTWDHOSN;
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</option>"
          GOTO      WARDSL10                         
.                                                     
WARDSL99  RETURN  
.
.------------------------------------------------------------
. Bulk Admission Details   
.------------------------------------------------------------
BLKADM00  
.
          MOVE      "Bulk Admission Processing",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,BLKADM99
          CALL      WEBBOD00
          CALL      WEBEND00
          MOVE      ONE,EXIT
          GOTO      BLKADM99
.
BLKADM99  RETURN
+
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
.                             " alert(#"",WEBTITLE,"#");":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
+
.------------------------------------------------------------
. Link to Next Day, Week, Month
.------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"patweb03.pbl":
                                 "?reportno=",KEY2:
                                 "&template=",KEY3:
                                 "&frstdate=",NXTFDATE:
                                 "&lastdate=",NXTLDATE:
                                 "&viewtype=",VIEWTYPE;
.
NEXT9999  RETURN
.-------------------------------------------------------------------------------
.  Previous Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"patweb03.pbl":
                                 "?reportno=",KEY2:
                                 "&template=",KEY3:
                                 "&frstdate=",PREFDATE:
                                 "&lastdate=",PRELDATE:
                                 "&viewtype=",VIEWTYPE;
.
PREV9999  RETURN
+
.*********************************************************************
.         generate the next batch number
.*********************************************************************
GENBATNO  OPEN      CONTROLF,"controlf"
          MOVE      ZERO,FORM8
          MOVE      "118",PRXCODE   * System Lock Sector 118
          CALL      GETSLK00        * Get System Lock of Sector 118
          READ      CONTROLF,HUND18;*126,PTCNRCBN
          MOVE      PTCNRCBN,FORM8
          ADD       ONE,FORM8
          MOVE      FORM8,PTCNRCBN
          WRITAB    CONTROLF,HUND18;*126,PTCNRCBN
.
          CALL      RELSLK00        * Release System Lock of Sector 118
          SUB       ONE,FORM8
          MOVE      FORM8,PTCNRCBN
.
          COMPARE   ZERO,FORM8
          GOTO      GENBATNO IF EQUAL       * invalid number
.
          RETURN
.------------------------------------------------------------
          INC       APSMASIO/INC
          INC       ALLPCTIO/INC
          INC       BOKRC1IO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       PATIN1IO/INC
          INC       PATPR1IO/INC
          INC       PATWR1IO/INC
          INC       MRTMASIO/INC
          INC       PATMI1IO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       OUTSITIO/INC
          INC       PATVISIO/INC
          INC       PATFN1IO/INC
          INC       PATDO1IO/INC
          INC       PATALRIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PMSEVTIO/INC
          INC       PATDSCIO/INC
          INC       PATICDIO/INC
          INC       PATICOIO/INC
          INC       PATDADIO/INC
          INC       PMSDTCIO/INC
          INC       PMSHCPIO/INC
          INC       PMSHCGIO/INC
          INC       PMSRCBIO/INC
          INC       PMSUPGIO/INC
          INC       PMSUPOIO/INC
          INC       PMSHPGIO/INC
          INC       PMSHPOIO/INC
          INC       PATTRNIO/INC
          INC       PATUNAIO/INC
.
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSVX1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSPRBIO/INC
          INC       PMSAUPIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
.
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPATMIS
          INC       CLPATMAS
          INC       CLPATDSC
          INC       CLPMSUPG
          INC       CLPMSUPO
          INC       IBAWEBCD
          INC       PATMASTM
          INC       CLPMSPX2
          INC       PMSPX2TM
          INC       PMSHCPTM
          INC       PMSHCGTM
          INC       PMSHPOTM
          INC       PMSHPGTM
          INC       PMSUPGCD
          INC       PMSUPGTM
          INC       PMSUPOTM
          INC       PATMSXTM
          INC       PATDOCTM
          INC       PATDOCCD
          INC       PATNAMCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       WEBDATCD
          INC       DAYOFWEK

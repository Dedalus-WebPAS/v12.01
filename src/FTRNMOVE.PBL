.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   FTRNMOVE                                                    *
.* Desc      :   Checking Transfer record movement                           *
.*****************************************************************************
.* Date      :   24/06/2009                                                  *
.* Author    :   J.Tan                                                       *
.* Function  :   This program will loop through the patmi1af file and        *
.*               check if the first record of Transfer file has Movement 'A' *
.*****************************************************************************
.* V12.00.01     14/05/2025  J.Tan          TSK 0955096                      *
.*               Added alpanumeric visit number generation                   *
.*****************************************************************************
.* V10.01.01     10/03/2011 Jill Parkinson CAR 233088                        *
.*               Added pmsvx1af                                              *
.* V9.12.01  22/12/2009  Sandra Barcham          CAR 212959                  *
.*                       Exclude outstanding debts                           *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      10
.
PRGID     INIT      "FTRNMOVE"
PRGNAM    INIT      "Checking the Transfer record Movement"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmi1af";
          OPEN      PATMI1A6,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATVISA1,"patvisaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Check Transfer Records"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*                  Loop through comdepaf and set invoice date               *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,COUNTER                 * initialise reset count
.
          MOVE      "2",KEY1                     * Current status
          PACK      KEY20,KEY1,SP70
          CALL      RDSMISS6                     * position at start of file
PROC0500  CALL      RDKMISS6                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          COMPARE   "5",ASTAT
          GOTO      PROC9000 IF EQUAL            * Cancelled record
.
          DISPLAY   *P13:24,*EL,*V2LON,AADMNO;
.
.         Exclude outstanding debts
.
          PACK       KEY8,AADMNO,SP70
          CALL       RDPTVIS1
          BRANCH     OVRCD,PROC0500
.
          MATCH      "00000000",PVIDATE
          GOTO       PROC0500 IF EQUAL
.
.         First record of transfer record must have Movement of 'A'
.
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,PROC1000
          MATCH     AADMNO,TADMN
          GOTO      PROC1000 IF NOT EQUAL
          MATCH     "A",TMOVE
          GOTO      PROC2000 IF EQUAL
.
          PRINT     *1,"Invalid First Movement for Admission No.:",AADMNO," - ":
                    TMOVE
          GOTO      PROC2000                     * get next record
.
PROC1000  PRINT     *1,"** Missing Transfer record for Admission No.:",AADMNO
          GOTO      PROC0500
.
.         Check discharge record for ASTAT=3
.
PROC2000  COMPARE   THREE,ASTAT
          GOTO      PROC0500 IF NOT EQUAL
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,PROC3000
          MATCH     AADMNO,TADMN
          GOTO      PROC3000 IF NOT EQUAL
          MATCH     "D",TMOVE
          GOTO      PROC0500 IF EQUAL
.
PROC3000  PRINT     *1,"Invalid Last Movement for Discharged Adm.No.:":
                    AADMNO," - ",TMOVE
          GOTO      PROC0500
.
PROC9000  DISPLAY   *P1:24,*EL,*V2LON,COUNTER,*HOFF,"Completed. ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC

. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBABIL64                                            *
. *                                                                           *
. *     FUNCTION:         DISCHARGED OUTSTANDING DEBTORS                      *
. *                                                                           *
. *     DATE WRITTEN:     21/08/87                                            *
. *                                                                           *
. *     AUTHOR:           GRAEME WILLIAMS (I.B.A)                             *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *****************************************************************************
. * V12.00.01 21/05/2025 Thanh T         TSK 0955096                          *
. *           Changed for alphanumeric visit number                           *
. *****************************************************************************
. *       V10.13.01  05/12/2018  Don Nguyen      TSK 0838558                  *
. *                  Deleted temp file (PSTROL) after processing              *
. *       V10.05.01  02/02/2015  Ania P       CAR 261630                      *
. *                  Removed use of PORT for temp file naming                 *
. *        V9.04.01  31/08/2004  Davin        CAR 53110                       *
. *                  Added Multi-Hospital processing                          *
. *        V9.02.01  17/03/2003  Dean Cramer  CAR 36515                       *
. *                  Fixed where overlap printing so can work on web          *
. *                  Fix out of bounds printing                               *
. *                  Fix where amount not printed in name                     *
. *                  sequence due to bad temp file def.                       *
. *        V5.08.01  29/08/2000  Caleb Sharman                                *
. *                  Changed Health Fund variables to be 8 chars              *
. *        V5.07.B01 12/11/1998 Jim Stathopoulos                              *
. *                  507 Changes                                              *
. *****************************************************************************
. *                       V5.00 05/05/89 Graeme Williams                      *
. *                             Added PINVCADM to inv file                    *
. *                             SRF 4219                                      *
. *                       V5.01 16/05/89 DIG                                  *
. *                             Changed to standard and                       *
. *                             modified print for 8 digit                    *
. *                             U/R's etc                                     *
. *                       V5.02 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                    V5.01.01 07/07/89 M.Ohleiter                           *
. *                             Changes for ur and admiss                     *
. *                             nos,standards - 5.01                          *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *       V5.01.122 04.03.93 Neeriem "I Hate Support" Dye                     *
. *                 Modify to use standard routine KEYDATE                    *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATINVFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.         Temporary index file for report
.
PSTADMP1 IFILE      SQL, FIXED=119, KEY="U1-28"
.
.Name      Type     Size     Physical    Start
.----      ----     ----     --------    -----
.PSNAME    DIM       20         20          1
.DDADMNO   DIM       8          8          21
.DURNO     DIM       8          8          29
.PGNAME    DIM       25         25         37
.PTELEP    DIM       20         20         63
.PTELEB    DIM       20         20         83
.DDATE     DIM       8          8          103
.OUTSTND   FORM     12.2        8          111
.                               EOR       119
.
+
.               WORK AREA
.
.
.
PSTSRT   FILE      ASCII, FIXED=256
PSTROL   FILE      ASCII, FIXED=256
.
FNAMER   DIM     8
FNAMED   DIM     8
.
DIM8     DIM     8
STATUS   FORM    1
OPCND    FORM    1
CMDLINE  DIM     50
.
DHEAD    DIM    20
DHEAD1   INIT   "(ADMISSION SEQUENCE)"
DHEAD2   INIT   "(SURNAME SEQUENCE)  "
.
D5TIME   DIM    5         ** DIM 5 OF TIME
DIM15B   DIM    15
DIM15P   DIM    15
DDSTAT   DIM    18
DDDEST   DIM    18
.
DANS      DIM    7
.
DALERG    DIM    7
DILLN     DIM    7
.
DTABL     DIM    20 
DCLAIM    DIM    23
.
DHOSP     DIM       7
.
DHHMM     DIM       5
.
IBCNMHOS  FORM      1           43      Multi Hospital Indicator
.
MIN1      FORM      1
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
XCENT2    DIM       2
.
DSEX      DIM       6
.
ACTDATE   DIM       8        * ACTUAL DATE IN (CCYYMMDD) FORMAT
FROMDATE  DIM       8        * FROM DATE IN (CCYYMMDD) FORMAT
TODATE    DIM       8        * TO DATE IN (CCYYMMDD) FORMAT
.
OUTSTND   FORM      12.2
.
PRGID     INIT      "IBABIL64"
PRGNAM    INIT      "Discharged Outstanding Debtors"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
OPFIL     DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
+
......... START OF PROGRAM
.
......... SELECT OPTION
.
START     MOVE      ZERO,CPAGENO
          DISPLAY   *P1:4,*EF:
                    *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:6,*V2LON,ONE,*HOFF," = Admission Number Sequence":
                    *P1:7,*V2LON,TWO,*HOFF," = Patient Surname Sequence"
.
OPT1      KEYIN     *P1:9,"Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF KDATE,KDATE
          BEEP
          GOTO      OPT1
.
KDATE     DISPLAY   *P1:11,*EF,"Keyin Date : "
.
          LOAD      DHEAD USING OPTION OF DHEAD1,DHEAD2
+
.
.             KEYIN TO DATE
.
KADAT1    MOVE      SP10,CDAY          * no default day/mon
          MOVE      SP10,CMON
          MOVE      SP10,CYEAR
          MOVE      SP10,CCENT
          MOVE      "14",CCOL          * set up collumn
          MOVE      "11",CVERT         * set up row
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
          CALL      KEYDATE            * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
          COMPARE   ZERO TO IDAY
          GOTO      START IF EQUAL
.
.
ENDAD1    PACK      TODATE WITH ICENT,IYEAR,IMON,IDAY
          REP       " 0",TODATE
          UNPACK    TODATE INTO XCENT1,XYEAR1,XMON1,XDAY1
.
          CALL      KHOS0000
          BRANCH    EXIT,KDATE
.
......... CHECK IF DATES ARE OK OR NOT
.
REKEY     KEYIN     *P1:24,*EF," Date ok (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          CMATCH    "N" TO ANS
          GOTO      KDATE IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      SURNAM IF EQUAL
.
          BEEP
          GOTO      REKEY
+
.
......... ACCESS VIA PATIENT SURNAME
.
SURNAM    BRANCH    OPTION OF KADMNO
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialising Report File **",*P1:12;
.
          CALL      INDZD
          BRANCH    OPCND OF ENDIT
          GOTO      KADMNO
.
INVMASTA  DISPLAY   *P20:23,*B,*EL:
                    "** Admission ",*V2LON,DADMNO,*HOFF:
                    " has an invalid U/R ",*V2LON,DURNO,*HOFF," ***"
.
          PRINT     "** Admission ",DADMNO," has an invalid U/R ":
                    DURNO," ... Please Contact I.B.A **"
          GOTO      NEXTA
.
+
........  USE DATE ON ADMISSION KEY 4  
.
KADMNO    PACK      KEY16 WITH TODATE,SP8
          CALL      RDSDSCH2
          MOVE      "60",CLNO
.
          DISPLAY   *P1:24,*EL,*P20:24,"Admission :";
.
NEXTA     CALL      RDKDSCH2
          BRANCH    OVRCD OF ENDP1
.
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      ACTDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",ACTDATE
.
          MATCH     ACTDATE,TODATE
          GOTO      ENDP1 IF NOT EQUAL
.
          MOVE      DURNO,KEY8
.
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMASTA
.
          COMPARE   ONE,IBCNMHOS
          GOTO      NEXTA1 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      NEXTA1 IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,NEXTA
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      NEXTA IF NOT EQUAL
.
NEXTA1    MOVE      ZERO,OUTSTND
          PACK      KEY16 WITH DADMNO,SP8
          CALL      RDSINV3
INVLP     CALL      RDKINV3
          BRANCH    OVRCD OF ENDINV
.
          MATCH     PINVADM,DADMNO
          GOTO      ENDINV IF NOT EQUAL
.
          ADD       PINVAMT,OUTSTND
          ADD       PINVPATA,OUTSTND
          ADD       PINVHFDA,OUTSTND
          ADD       PINVOTHA,OUTSTND
          ADD       PINVJOUR,OUTSTND
.
          GOTO      INVLP
.
ENDINV    COMPARE   ZERO,OUTSTND
          GOTO      NEXTA IF EQUAL
.
          DISPLAY   *P32:24,*V2LON,DADMNO;
.
          BRANCH    OPTION OF NORMPA
. 
          PACK      KEY28 WITH PSNAME,DADMNO
.
          READ      PSTADMP1,KEY28;ANS
          GOTO      NEXTA IF NOT OVER
.
          WRITE     PSTADMP1,KEY28;KEY28,DURNO,PGNAME:
                                   PTELEP,PTELEB,DDATE,OUTSTND
          GOTO      NEXTA
.
NORMPA    CALL      DISPDA
          GOTO      NEXTA
.
NOADMN    DISPLAY   *P20:24,*B,*EL,*V2LON:
                    "** No Discharged Outstanding Debtors during this Date **":
                    *W3;
          GOTO      START
+
.
......... OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP1     BRANCH    OPTION OF CNTEP1,ENDW
.
CNTEP1    COMPARE   ZERO,CPAGENO
          GOTO      NOADMN IF EQUAL
.
          MOVE      ZERO,CPAGENO
          CALL      PAGEND
          PRINT     *N,"  ***  END OF REPORT  ***"
.
ENDREP    DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Report Generation Completed **",*W3;
          GOTO      START
+
.
......... OVER CONDITION FOUND..
.
ENDW      MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      SP30,KEY28
          DISPLAY   *P10:24,*EL,"Generating Admission :";
          READ      PSTADMP1,KEY28;;
.
NEXTR     READKS    PSTADMP1;PSNAME,DIM8,DURNO,PGNAME:
                                    PTELEP,PTELEB,DDATE,OUTSTND
          GOTO      CNTNR IF OVER
          MOVE      DIM8,DADMNO
.
          DISPLAY   *P35:24,*V2LON,PSNAME;
          CALL      DISPDA
          GOTO      NEXTR
.
CNTNR  
          COMPARE   ZERO,CPAGENO
          GOTO      NONXR IF EQUAL
.
          CALL      PAGEND
          PRINT     *N,"  ***  END OF REPORT  ***"
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Report Generation Completed **",*W3;
          CALL      KILLIDZ
          GOTO      START
.
NONXR    DISPLAY    *P20:24,*B,*EL,*V2LON:
                    "** No Discharged Outstanding Debtors during this Date **":
                    *W3;
         CALL       KILLIDZ
         GOTO       START
+
.
.        Create an indexed file based on port
.        for accumulating Admission Type totals
.
INDZD    MOVE       ZERO,STATUS
.
         CALL       TFILENAM
         MOVE       TFILNAME,FNAMED
.
         CALL       TFILENAM
         MOVE       TFILNAME,FNAMER
.
........ CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
.
         CALL       KILLIDZ
.
         PREP       PSTROL,FNAMER
         WRITE      PSTROL,SEQ;"isbuild ",FNAMED," 119 u1-28"
         WEOF       PSTROL,SEQ
.
         MOVE       ONE,STATUS
.
         CLEAR      CMDLINE
         APPEND     "sh ",CMDLINE
         APPEND     FNAMER,CMDLINE
         APPEND     ".txt",CMDLINE
         RESET      CMDLINE
.
         CLOSE      PSTROL,DELETE
         DISPLAY    *P1:12,*EF
         EXECUTE    CMDLINE,TASKID
.
         MOVE       ZERO,OPCND
         TRAP       NOINDZ IF IO
         OPEN	    PSTADMP1,FNAMED
         TRAPCLR    IO
         DISPLAY    *P1:12,*EF
         RETURN
.
NOINDZ   MOVE       ONE,OPCND
         DISPLAY    *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMED," Not Found **",*W4;
         TRAPCLR    IO
         RETURN
+
.
.       Deleting an indexed file based on port
.
KILLIDZ   CLOSE     PSTADMP1
.
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Deleting Indexed File **";
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMED,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:3,*EF
          RETURN
+
.
.     PRINT ACTUAL ADMISSION DATA 
.
DISPDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          UNPACK    DDATE WITH XCENT,XYEAR,XMON,XDAY
          MOVE      PTELEP,DIM15P 
          MOVE      PTELEB,DIM15B 
.
          PRINT     *1,"! ",DURNO,*11,"! ",DADMNO:
                    *21,"! ",PSNAME,PGNAME:
                    *69,"! ",DIM15P,*86,"! ",DIM15B:
                    *103,"! ",OUTSTND,*120,"!"
.
          ADD       ONE,CLNO
          RETURN
+
.
......... LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*----------------------------------------------":
                    "-----------------------------------------------":
                    "-------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
......... HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
	  COMPARE   ONE,CPAGENO
	  CALL      PAGEND IF NOT EQUAL
          CLOCK     TIME,CTIMEIS
.
          PRINT     *F,*1,"IBABIL64",*40,CNAME,*118,"DATE - ":
                    CDATE:
                    *N,*1,VERSION:
                    *40,"*** DISCHARGED OUTSTANDING DEBTORS ***":
                    *118,"TIME - ",CTIMEIS:
                    *N,*118,"PAGE - ",*130,CPAGENO,*N:
                    *40,"Discharges For : ",XDAY1,"/",XMON1,"/",XYEAR1:
                    "     ",DHEAD,*N
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME
          ELSE
            PRINT     *N;
          ENDIF
.
          CALL      PAGEND
.
          PRINT     *1,"! U/R no",*11,"! Adm no":
                    *21,"! Patients Name":
                    *69,"! Telephone-P":
                    *86,"! Telephone-B":
                    *103,"! Outstanding",*120,"!"
.
          CALL      PAGEND
          MOVE      NINE,CLNO
          RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
          INC       STD001IO
          INC       PATHSPKY
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATDSCIO/INC
          INC       PATINVIO/INC
          INC       PMSVX1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
.
ENDIT    CHAIN      PGM
         STOP

.
.*******************************************************************************
.* System   : PATIENT BILLING                                                  *
.* Program  : IBAPMS18                                                         *
.* Desc     : Doctors Clinic Statistic 1                                       * 
.*******************************************************************************
.* Date     :  08/05/90                                                        *
.* Author   :  K.Peak (PASTA)                                                  * 
.* N.B.     :  This program should balance with IBAHOS86 when run for          * 
.*             individual periods. If this program is run over a               *
.*             range of periods, the bed day figures, the average              *
.*             stay figures and the occupancy figures will not be              *
.*             accurate and IBAHOS86 should be used instead. This is           *
.*             because the statistics file this program reads is               *
.*             generated for individual periods and not ranges of              *
.*             periods.                                                        *
.*******************************************************************************
.* Mods:                                                                       *
.*       V10.05.01  01/01/2015  Ania P          CAR 261630                     *
.*                  Removed use of PORT for temp file naming                   *
.*        V5.07.03  13/08/1999  Steve Downing                                  *
.*                  Removed redundant code                                     *  
.*        V5.07.02  29/07/1999  Steve Downing                                  *
.*                  Replaced CALC routine with DAYSEP                          *
.*        V5.07.01  22/02/1999  Jim Stathopoulos                               *
.*                  Display Modifications                                      *
.*                  09/11/1998  Glenn Berry      5.07 changes                  *
.*******************************************************************************
.*          :  V5.01.02 02/11/90 J.Tan   SRF 106993                            *
.*             Fixed discharged bed days                                       *
.*             V5.01.03 05/01/94 I.Rutt   Mt Alvernia                          *
.*             Recompiled for Mt Alvernia                                      *  
.*******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
          INC       PATC1SFD/INC     * clinical statistics 1 file
          INC       PATCODFD/INC     * codes file
          INC       PATCONFD/INC     * control file
          INC       PATDO1FD/INC     * doctors file
          INC       PATDRGFD/INC     * period date range file
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.**********************************************************************
.*                         VARIABLES                                  *
.**********************************************************************
.
STATMPXX  IFILE SQL, FIXED=131, KEY="u1-3,4-9"
.
CSTPER    DIM       6       * ccyymm
CURPER    DIM       6       * current period
CMDLINE   DIM       50
COMPBD    FORM      5       * compensable bed days in period
COMDIS    FORM      5       * compensable discharged
COMBED    FORM      5       * compensable discharged bed days
COMSTY    FORM      3.2     * compensable average stay
COMOCC    FORM      3.2     * compensable average daily occupancy
CODEDT    INIT      "DT"
.
DOCDESC   DIM       14      * doctors name
DNAME     DIM       25      * doctors name working variable
DAYNUM    FORM      5       * number of days in report period range
.
ENDDATE   DIM       10      * enddate
ENDDTE    DIM       10      * enddate
.
FROMPER   DIM       8       * from period for printing (mm/ccyy)
FADMDTE   DIM       6       * ccyymm
FDMDATE   DIM       8       * yymmdd
FORM52    FORM      5.2     * working variable for totals
.
GSTPBD    FORM      5       * grand total public bed days in period
GPRPBD    FORM      5       * grand total private bed days in period
GCOPBD    FORM      5       * grand total compensable bed days in period
GBEDSTD   FORM      5       * discharged public sub-total
GBEDPRV   FORM      5       * discharged private sub-total
GBEDCOM   FORM      5       * discharged compensable sub-total
GBEDTOT   FORM      5       * discharged total sub-total
GDISSTD   FORM      5       * discharged bed days public sub-total
GDISPRV   FORM      5       * discharged bed days private sub-total
GDISCOM   FORM      5       * discharged bed days compensable sub-total
GDISTOT   FORM      5       * discharged bed days total sub-total
GOCCSTD   FORM      3.2     * average daily occupancy public sub-total
GOCCPRV   FORM      3.2     * average daily occupancy private sub-total
GOCCCOM   FORM      3.2     * average daily occupancy compensable sub-total
GOCCTOT   FORM      3.2     * average daily occupancy total sub-total
GSTYSTD   FORM      3.2     * average stay public sub-total
GSTYPRV   FORM      3.2     * average stay private sub-total
GSTYCOM   FORM      3.2     * average stay compensable sub-total
GSTYTOT   FORM      3.2     * average stay total sub-total
.
JDAYS     FORM      3       * used in calc0000
JYEAR     FORM      2       * used in calc0000
.
.
LYR       FORM      1       * used in calc0000
.
OYEAR     FORM      "365"   * used in calc0000
.
PRTFLG    FORM      1       * printing flag
PRVPBD    FORM      5       * private bed days in period
PRVDIS    FORM      5       * private discharged 
PRVBED    FORM      5       * private discharged bed days 
PRVSTY    FORM      3.2     * private average stay
PRVOCC    FORM      3.2     * private average daily occupancy
.
STDPBD    FORM      5       * public bed days in period
STDDIS    FORM      5       * public discharged
STDBED    FORM      5       * public discharged bed days
STDSTY    FORM      3.2     * public average stay
STDOCC    FORM      3.2     * public average daily occupancy
STRDATE   DIM       10      * start date
STRDTE    DIM       10      * start date
SAVDTYP   DIM       3       * saved doctors type
SBEDSTD   FORM      5       * discharged public sub-total
SBEDPRV   FORM      5       * discharged private sub-total
SBEDCOM   FORM      5       * discharged compensable sub-total
SBEDTOT   FORM      5       * discharged total sub-total
SDISSTD   FORM      5       * discharged bed days public sub-total
SDISPRV   FORM      5       * discharged bed days private sub-total
SDISCOM   FORM      5       * discharged bed days compensable sub-total
SDISTOT   FORM      5       * discharged bed days total sub-total
SOCCSTD   FORM      3.2     * average daily occupancy public sub-total
SOCCPRV   FORM      3.2     * average daily occupancy private sub-total
SOCCCOM   FORM      3.2     * average daily occupancy compensable sub-total
SOCCTOT   FORM      3.2     * average daily occupancy total sub-total
SSTYSTD   FORM      3.2     * average stay public sub-total
SSTYPRV   FORM      3.2     * average stay private sub-total
SSTYCOM   FORM      3.2     * average stay compensable sub-total
SSTYTOT   FORM      3.2     * average stay total sub-total
.
TSTPBD    FORM      5       * sub total public bed days in period
TPRPBD    FORM      5       * sub total private bed days in period
TCOPBD    FORM      5       * sub total compensable bed days in period
TOPER     DIM       8       * to period for printing (mm/ccyy)
TOTPBD    FORM      5       * total bed days in period range
TOTDAY    FORM      5       * total period bed days
TADMDTE   DIM       6       * ccyymm
TDMDATE   DIM       8       * yymmdd
TODATE    DIM       8       * todate 
TTODATE   DIM       8       * todate of period range plus one 
TOTBED    FORM      5.2     * total discharged bed days
TOTDIS    FORM      5.2     * total discharged 
TOTOCC    FORM      3.2     * total average daily occupancy
TOTSTY    FORM      3.2     * total average stay 
.
UNTDESC   DIM       10      * unit description
.
WDATE1    DIM       8       * used in calc0000
WDATE2    DIM       8       * used in calc0000
.
.-----------------------------------------------
.Name     Type      Length     Physical    Start
.----     ----      ------     --------    -----
.PTC1TYPE DIM            3            3        1
.PTC1DOCT DIM            6            6        4
.DSNAME   DIM           20           20       10
.DGNAME   DIM           25           25       30
DISSTD    FORM           5            4       55
DISPRV    FORM           5            4       59
DISCOM    FORM           5            4       63
DISTOT    FORM           5            4       67
BEDSTD    FORM           5            4       71
BEDPRV    FORM           5            4       75
BEDCOM    FORM           5            4       79
BEDTOT    FORM           5            4       83
STYSTD    FORM           3.2          4       87
STYPRV    FORM           3.2          4       91
STYCOM    FORM           3.2          4       95
STYTOT    FORM           3.2          4       99
OCCSTD    FORM           3.2          4      103
OCCPRV    FORM           3.2          4      107
OCCCOM    FORM           3.2          4      111
OCCTOT    FORM           3.2          4      115
TSTDPBD   FORM           5            4      119
TPRVPBD   FORM           5            4      123
TCOMPBD   FORM           5            4      127
.
.End of Record                               131
.-----------------------------------------------
.
PRGID     INIT      "IBAPMS18"
PRGNAM    INIT      "Doctors Clinic Statistic 1"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
.
ML0000    CALL      INIT0000        * initialisation
.
ML1000    CALL      SCR0000         * select option
          BRANCH    EXIT,ML9000
.
ML1500    CALL      CTMP0000        * clear temporary file variables
          CALL      CSUB0000        * clear sub total variables
          CALL      CGRD0000        * clear grand total variables
.
          CALL      DAT0000         * keyin period range
          BRANCH    EXIT,ML1000
          CALL      CONTQST         * ok to continue y/n ?
          BRANCH    CEXIT,ML2000,ML1500,ML1000
.
ML2000    CALL      DSPS0000        * display screen       
          CALL      CREA0000        * create temporary file
.
          CALL      UC1S0000        * process patc1sad data
          CALL      PRNT0000        * print patc1sad data
          CALL      END0000         * end of report
          GOTO      ML1000
.
.----------------------------------------------------------------------
.                          .. The End                                  
.----------------------------------------------------------------------
ML9000    CALL      KILL0000        * remove temporary file
          CHAIN     PGM
+
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialisation                                *
.**********************************************************************

INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patc1sad";
          OPEN      PATC1SA1,"patc1sad"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
          CALL      TFILENAM
.
          RETURN
+
.**********************************************************************
.*                         CTMP000                                    *
.*                     Clear Temporary file variables                 *
.**********************************************************************
.
CTMP0000  MOVE      ZERO,STDBED          * discharged public 
          MOVE      ZERO,PRVBED          * discharged private 
          MOVE      ZERO,COMBED          * discharged compensable 
          MOVE      ZERO,TOTBED          * discharged total 
          MOVE      ZERO,STDDIS          * discharged bed days public 
          MOVE      ZERO,PRVDIS          * discharged bed days private 
          MOVE      ZERO,COMDIS          * discharged bed days compensable 
          MOVE      ZERO,TOTDIS          * discharged bed days total 
          MOVE      ZERO,STDOCC          * average daily occupancy public 
          MOVE      ZERO,PRVOCC          * average daily occupancy private 
          MOVE      ZERO,COMOCC          * average daily occupancy compensable 
          MOVE      ZERO,TOTOCC          * average daily occupancy total 
          MOVE      ZERO,STDSTY          * average stay public 
          MOVE      ZERO,PRVSTY          * average stay private 
          MOVE      ZERO,COMSTY          * average stay compensable 
          MOVE      ZERO,TOTSTY          * average stay total 
.
          MOVE      ZERO,STDPBD          * public bed days in period
          MOVE      ZERO,PRVPBD          * private bed days in period
          MOVE      ZERO,COMPBD          * compensable bed days in period
.
          RETURN
+
.**********************************************************************
.*                         CSUB0000                                   *
.*                     Clear Sub Total Variables                      *
.**********************************************************************
.
CSUB0000  MOVE      ZERO,SBEDSTD         * initialise sub totals
          MOVE      ZERO,SBEDPRV
          MOVE      ZERO,SBEDCOM
          MOVE      ZERO,SBEDTOT
          MOVE      ZERO,SDISSTD
          MOVE      ZERO,SDISPRV
          MOVE      ZERO,SDISCOM
          MOVE      ZERO,SDISTOT
          MOVE      ZERO,SOCCSTD
          MOVE      ZERO,SOCCPRV
          MOVE      ZERO,SOCCCOM
          MOVE      ZERO,SOCCTOT
          MOVE      ZERO,SSTYSTD
          MOVE      ZERO,SSTYPRV
          MOVE      ZERO,SSTYCOM
          MOVE      ZERO,SSTYTOT
.
          RETURN
+
.**********************************************************************
.*                         CGRD0000                                   *
.*                     Clear Grand Total Variables                    *
.**********************************************************************
.
CGRD0000  MOVE      ZERO,GBEDSTD         * initialise grand totals
          MOVE      ZERO,GBEDPRV
          MOVE      ZERO,GBEDCOM
          MOVE      ZERO,GBEDTOT
          MOVE      ZERO,GDISSTD
          MOVE      ZERO,GDISPRV
          MOVE      ZERO,GDISCOM
          MOVE      ZERO,GDISTOT
          MOVE      ZERO,GOCCSTD
          MOVE      ZERO,GOCCPRV
          MOVE      ZERO,GOCCCOM
          MOVE      ZERO,GOCCTOT
          MOVE      ZERO,GSTYSTD
          MOVE      ZERO,GSTYPRV
          MOVE      ZERO,GSTYCOM
          MOVE      ZERO,GSTYTOT
.
          RETURN
+
.**********************************************************************
.*                         SCR0000                                    *
.*                      Main Option Screen                            *
.**********************************************************************
.
SCR0000   HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Clinical Statistics 1 Report" 
.
SCR1000   KEYIN     *P1:7,"Option : ",*EF,*DV,UNDLN1:
                    *P10:7,*V2LON,OPTION
.
          BRANCH    OPTION,SCR8000
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
          BEEP
          GOTO      SCR1000
.
SCR8000   MOVE      FALSE,EXIT
          GOTO      SCR9999
.
SCR9000   MOVE      TRUE,EXIT
SCR9999   RETURN
+
.**********************************************************************
.*                         DAT0000                                    *
.*                       Keyin Period                                 *
.**********************************************************************
.
DAT0000   DISPLAY   *P1:8,*EF:
                    *P1:9,"From ",CPERMTH," : ":
                    *P1:11,"To   ",CPERMTH," : "
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,DAT2000
.
          PACK      CURPER,DRGYR,DRGNUM
.
.         Find previous period
.
          PACK      KEY6,CURPER
          CALL      RDDRGA1
          CALL      RDPDRGA1
          PACK      CSTPER,DRGCYR,DRGCNUM
.
.         Keyin the from period
.
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      "15",CCOL            * column for keyin
          MOVE      NINE,CVERT           * row for keyin
          CALL      KEYPER
          BRANCH    OVRCD,DAT0000        * no date keyed in
.
.         Validate the from period entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DAT3000
.
.         Pack appropriate start dates for from period
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          PACK      FDMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,STRDATE
          PACK      FADMDTE,DRGYR,DRGNUM
.
          UNPACK    KEY6,CCENT,CYEAR,CMON        * from period for printing
          PACK      FROMPER,CMON,SLASH,CCENT,CYEAR
.
.         Pack appropriate end dates for to period
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENDDATE
.
          DISPLAY   *P33:9,*EL,STRDATE," to ",ENDDATE
.
.         Keyin the to period
.
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      "15",CCOL            * column for keyin
          MOVE      TEN1,CVERT           * row for keyin
          CALL      KEYPER
          BRANCH    OVRCD,DAT0000        * no date keyed in
.
.         Validate the to period entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DAT3000
.
.         Pack appropriate start dates for from period
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,STRDTE
.
          UNPACK    KEY6,CCENT,CYEAR,CMON        * to period for printing
          PACK      TOPER,CMON,SLASH,CCENT,CYEAR
.
.         Pack appropriate end dates for to period
.
          MOVE      DRGTDTE,TODATE
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          PACK      TDMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENDDTE
          PACK      TADMDTE,DRGYR,DRGNUM
.
          DISPLAY   *P33:11,*EL,STRDTE," to ",ENDDTE
.
.         Check if the from period is less than or equal to the to period
.
          MATCH     FADMDTE,TADMDTE
          GOTO      DAT4000 IF LESS
          GOTO      DAT8000
.
.         No current period on file
.
DAT2000   KEYIN     *P1:24,*EL,*B,*V2LON:
                    "** Error: Current date not found in Period file. ":
                    "Hit <ENTER> to exit ",*EOFF,ANS,*P1:24;
          GOTO      DAT9000
.
.         Invalid date entered
.
DAT3000   KEYIN     *P1:24,*EL,*B,*V2LON:
                    "** Invalid Date **",*EOFF,ANS,*P1:24;
          GOTO      DAT0000
.
.         From period must be less than or equal to the to period
.
DAT4000   KEYIN     *P1:24,*EL,*B,*V2LON:
                    "** From Period must be <= To Period **",*EOFF,ANS,*P1:24;
          GOTO      DAT0000
.
DAT8000   MOVE      FALSE,EXIT
          GOTO      DAT9999
.
DAT9000   MOVE      TRUE,EXIT
DAT9999   RETURN
+
.**********************************************************************
.*                         DSPS0000                                   *
.*                       Display Screen                               *
.**********************************************************************
.
DSPS0000  MOVE      ZERO,CPAGENO   * page counter
          MOVE      "60",CLNO      * line counter
          CLOCK     TIME,CTIMEIS   * find time report started
.
          DISPLAY   *P1:24,*EL,*P20:24:
                    "Doctor : ",*P48:24,"Scanning : "
          RETURN
+
.**********************************************************************
.*                         UC1S0000                                   *
.*                   Process PATC1SFD Data                            *
.**********************************************************************
.
UC1S0000  
.         Find the number of days in the report period
.
          MOVE      FDMDATE,WDATE2      * start date of from period
.
.         Add one to the end date of the period
.
          UNPACK    TDMDATE,XCENT,XYEAR,XMON,XDAY
          MOVE      XCENT,CC
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          PACK      TTODATE,CC,YY,MM,DD    * end date of period plus one day extra
          REP       " 0",TTODATE
.
          MOVE      TTODATE,WDATE1         * end date of to period
          DAYSEP    WDATE2,WDATE1,TOTDAY   * calculate dates difference
          MOVE      TOTDAY,DAYNUM
.
.         Loop through the clinical statistics 1 file 
.
          PACK      KEY16,FADMDTE,SP20
          CALL      RSPTC1S1
UC1S1000  CALL      RKPTC1S1
          BRANCH    OVRCD,UC1S9999
.
          DISPLAY   *P59:24,*EL,PTC1DOCT
.
          MATCH     PTC1DATE,TADMDTE    * date still within report period range
          GOTO      UC1S9999 IF LESS
.
          CALL      CTMP0000            * clear temporary file variables
.
.         Branch to appropriate type of patient
.         1 = public, 2 = private, 3 = compensable
.
UC1S1500  MOVE      ZERO,FORM1
          MOVE      PTC1TYPP,FORM1
          BRANCH    FORM1,UC1S2000,UC1S3000,UC1S4000
          GOTO      UC1S5200
.
.         Public type of patient
.
UC1S2000  ADD       PTC1DSCH,STDDIS      * number of discharged
          MOVE      PTC1TBDY,STDBED      * discharged bed days
.
          MOVE      ZERO,STDSTY          
          COMPARE   ZERO,STDDIS
          GOTO      UC1S2500 IF EQUAL
.
          MOVE      STDBED,FORM52        * calculate average stay
          DIV       STDDIS,FORM52
          MOVE      FORM52,STDSTY
.
UC1S2500  MOVE      PTC1PBDY,STDPBD      * calculate average daily occupancy
          MOVE      STDPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,STDOCC
          GOTO      UC1S5000
.
.         Private type of patient
.
UC1S3000  ADD       PTC1DSCH,PRVDIS      * number of discharged
          MOVE      PTC1TBDY,PRVBED       * discharged bed days
.
          MOVE      ZERO,PRVSTY          
          COMPARE   ZERO,PRVDIS
          GOTO      UC1S3500 IF EQUAL
.
          MOVE      PRVBED,FORM52         * calculate average stay
          DIV       PRVDIS,FORM52
          MOVE      FORM52,PRVSTY
.
UC1S3500  MOVE      PTC1PBDY,PRVPBD      * calculate average daily occupancy
          MOVE      PRVPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,PRVOCC
          GOTO      UC1S5000
.
.         Compensable type of patient
.
UC1S4000  ADD       PTC1DSCH,COMDIS      * number of discharged
          MOVE      PTC1TBDY,COMBED      * discharged bed days
.
          MOVE      ZERO,COMSTY          
          COMPARE   ZERO,COMDIS
          GOTO      UC1S4500 IF EQUAL
.
          MOVE      COMBED,FORM52        * calculate average stay
          DIV       COMDIS,FORM52
          MOVE      FORM52,COMSTY
.
UC1S4500  MOVE      PTC1PBDY,COMPBD      * calculate average daily occupancy
          MOVE      COMPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,COMOCC
.
.         Calculate totals for each doctor
.
UC1S5000  MOVE      STDDIS,TOTDIS        * discharged total
          ADD       PRVDIS,TOTDIS
          ADD       COMDIS,TOTDIS
.
          MOVE      STDBED,TOTBED        * discharged bed days total
          ADD       PRVBED,TOTBED
          ADD       COMBED,TOTBED
.
          MOVE      PTC1DOCT,KEY6        * read doctors file for doctors name
          CALL      RDDOCT1
.
          DISPLAY   *P30:24,*V2LON,PTC1DOCT
.
.         Write or update the temporary file
.
UC1S5200  PACK      KEY9,PTC1TYPE,PTC1DOCT
          CALL      RDTMP1
          BRANCH    OVRCD,UC1S6000
.
          ADD       STDDIS,DISSTD
          ADD       PRVDIS,DISPRV
          ADD       COMDIS,DISCOM
          ADD       TOTDIS,DISTOT
          ADD       STDBED,BEDSTD
          ADD       PRVBED,BEDPRV
          ADD       COMBED,BEDCOM
          ADD       TOTBED,BEDTOT
          ADD       STDSTY,STYSTD
          ADD       PRVSTY,STYPRV
          ADD       COMSTY,STYCOM
          ADD       STDOCC,OCCSTD
          ADD       PRVOCC,OCCPRV
          ADD       COMOCC,OCCCOM
          ADD       STDPBD,TSTDPBD
          ADD       PRVPBD,TPRVPBD
          ADD       COMPBD,TCOMPBD
.
          MOVE      ZERO,TOTSTY          * average stay total
          COMPARE   ZERO,DISTOT
          GOTO      UC1S5500 IF EQUAL
.
          MOVE      BEDTOT,FORM52
          DIV       DISTOT,FORM52
          MOVE      FORM52,TOTSTY
UC1S5500  MOVE      TOTSTY,STYTOT
.
          MOVE      TSTDPBD,TOTPBD       * average daily occupancy total
          ADD       TPRVPBD,TOTPBD
          ADD       TCOMPBD,TOTPBD
.
          MOVE      TOTPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,TOTOCC
          MOVE      TOTOCC,OCCTOT
.
          CALL      UPTMP1
          GOTO      UC1S1000
.
.         Write to the temporary file
. 
UC1S6000  MOVE      STDDIS,DISSTD
          MOVE      PRVDIS,DISPRV
          MOVE      COMDIS,DISCOM
          MOVE      TOTDIS,DISTOT
          MOVE      STDBED,BEDSTD
          MOVE      PRVBED,BEDPRV
          MOVE      COMBED,BEDCOM
          MOVE      TOTBED,BEDTOT
          MOVE      STDSTY,STYSTD
          MOVE      PRVSTY,STYPRV
          MOVE      COMSTY,STYCOM
          MOVE      STDOCC,OCCSTD
          MOVE      PRVOCC,OCCPRV
          MOVE      COMOCC,OCCCOM
          MOVE      STDPBD,TSTDPBD
          MOVE      PRVPBD,TPRVPBD
          MOVE      COMPBD,TCOMPBD
.
          MOVE      ZERO,TOTSTY          * average stay total
          COMPARE   ZERO,DISTOT
          GOTO      UC1S6500 IF EQUAL
.
          MOVE      BEDTOT,FORM52
          DIV       DISTOT,FORM52
          MOVE      FORM52,TOTSTY
UC1S6500  MOVE      TOTSTY,STYTOT
.
          MOVE      TSTDPBD,TOTPBD       * average daily occupancy total
          ADD       TPRVPBD,TOTPBD
          ADD       TCOMPBD,TOTPBD
.
          MOVE      TOTPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,TOTOCC
          MOVE      TOTOCC,OCCTOT
.
          CALL      WRTMP1
          GOTO      UC1S1000
.
UC1S9999  RETURN
+
.**********************************************************************
.*                         END0000                                    *
.*                      End of Report                                 *
.**********************************************************************
.
END0000   COMPARE   ZERO,CPAGENO
          GOTO      END1000 IF NOT EQUAL
.
.         No data selected
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** No Record Selected ** ";
          CALL      HITENTER
          CALL      HEAD0000
          PRINT     *N,*N,"***  END OF REPORT  ***"
          GOTO      END9999
.
END1000   COMPARE   ZERO,PRTFLG
          GOTO      END2000 IF EQUAL
.
          CALL      TOTL0000
.
END2000   COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.         Print Grand Totals
.
          PRINT     *1,"!Grand Total",*17,"!":
                    *19,GDISSTD,*25,GDISPRV,*32,GDISCOM,*39,GDISTOT,*46,"!":
                    *47,GBEDSTD,*54,GBEDPRV,*61,GBEDCOM,*68,GBEDTOT,*75,"!":
                    *76,GSTYSTD,*83,GSTYPRV,*90,GSTYCOM,*97,GSTYTOT,*104,"!":
                    *105,GOCCSTD,*112,GOCCPRV,*119,GOCCCOM,*125,GOCCTOT,*132,"!"
          ADD       ONE,CLNO
.
          CALL      PGE0000
.
          PRINT     *N,"NOTE : This report only incorporates discharged ":
                    "patients.":
                    *N,*N,"Discharged = No. of Patients Discharged ":
                    "in the Period (Including Deaths) Assigned to ":
                    "Discharge (Current) Honorary.":
                    *N,"Average Stay = Discharged Bed Days / No. of ":
                    "Patients Discharged.":
                    *N,"Discharged Bed Days = Total Days (Dish - Admit) ":
                    "For Patients Discharged in the Period ":
                    "(Inc. 1 Day Stayers) Assigned to Current HMO.":
                    *N,"Average Daily Occupancy = Total Period Bed Days ":
                    "(Census Bed Days + 1 Day Stayers) / No. of Days in ":
                    "Report Period.":
                    *N,*N,"***  END OF REPORT  ***"
.
          DISPLAY   *P1:23,*EL,*P1:24,*EL,*P20:24,*V2LON:
                    "** Report Generation Completed ** ";
          CALL      HITENTER
.
END9999   DISPLAY   *P1:14,*EF
          CALL      KILL0000        * remove temporary file
          RETURN
+
.**********************************************************************
.*                         PRNT0000                                   *
.*                        Print Data                                  *
.**********************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Printing Report ** ";
          CALL      HITENTER
.
          MOVE      ZERO,PRTFLG         * printing flag
          MOVE      SP3,SAVDTYP         * saved doctor type
.
          DISPLAY   *P1:24,*EL,*P20:24,"Doctor : "
.
.         Read through the temporary file
.
          PACK      KEY9,SP10
          CALL      RDSTMP1
PRNT1000  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT9999
.
          DISPLAY   *P30:24,*V2LON,PTC1DOCT
.
          COMPARE   ZERO,PRTFLG
          GOTO      PRNT2000 IF EQUAL
.
          MATCH     PTC1TYPE,SAVDTYP     * same doctor type ?
          GOTO      PRNT2000 IF EQUAL
.
          CALL      TOTL0000             * print doctor type totals
.
PRNT2000  MOVE      ONE,PRTFLG
.
.         Pack the doctors name
.
          CLEAR     DOCDESC
          MOVE      DSNAME,DOCDESC
          CALL      FIND0000             * routine to find the end of drs name
          APPEND    SP1,DOCDESC
.
          MOVE      DGNAME,ANS
          APPEND    ANS,DOCDESC
          APPEND    ".",DOCDESC
          CALL      GIVN0000             * routine for doctors given name
          RESET     DOCDESC
.
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"!",DOCDESC,*17,"!":
                    *19,DISSTD,*25,DISPRV,*32,DISCOM,*39,DISTOT,*46,"!":
                    *47,BEDSTD,*54,BEDPRV,*61,BEDCOM,*68,BEDTOT,*75,"!":
                    *76,STYSTD,*83,STYPRV,*90,STYCOM,*97,STYTOT,*104,"!":
                    *105,OCCSTD,*112,OCCPRV,*119,OCCCOM,*125,OCCTOT,*132,"!"
          ADD       ONE,CLNO
.
.         Add totals to get the sub totals for the different doctor types
.
          ADD       DISSTD,SDISSTD       * discharged
          ADD       DISPRV,SDISPRV
          ADD       DISCOM,SDISCOM
          ADD       DISTOT,SDISTOT
.
          ADD       BEDSTD,SBEDSTD       * discharged bed days
          ADD       BEDPRV,SBEDPRV
          ADD       BEDCOM,SBEDCOM
          ADD       BEDTOT,SBEDTOT
.
          COMPARE   ZERO,SDISSTD         * average stay
          GOTO      PRNT3200 IF EQUAL
.
          MOVE      SBEDSTD,FORM52
          DIV       SDISSTD,FORM52
          MOVE      FORM52,SSTYSTD
.
PRNT3200  COMPARE   ZERO,SDISPRV
          GOTO      PRNT3500 IF EQUAL
.
          MOVE      SBEDPRV,FORM52
          DIV       SDISPRV,FORM52
          MOVE      FORM52,SSTYPRV
.
PRNT3500  COMPARE   ZERO,SDISCOM
          GOTO      PRNT3700 IF EQUAL
.
          MOVE      SBEDCOM,FORM52
          DIV       SDISCOM,FORM52
          MOVE      FORM52,SSTYCOM
.
PRNT3700  COMPARE   ZERO,SDISTOT
          GOTO      PRNT4000 IF EQUAL
.
          MOVE      SBEDTOT,FORM52
          DIV       SDISTOT,FORM52
          MOVE      FORM52,SSTYTOT
.
PRNT4000  ADD       TSTDPBD,TSTPBD        * add bed days in period for average
          ADD       TPRVPBD,TPRPBD        * daily occupancy sub totals
          ADD       TCOMPBD,TCOPBD
.
          ADD       TSTDPBD,GSTPBD        * add bed days in period for average
          ADD       TPRVPBD,GPRPBD        * daily occupancy grand totals
          ADD       TCOMPBD,GCOPBD
.
          MOVE      TSTPBD,FORM52         * average daily occupancy
          DIV       DAYNUM,FORM52
          MOVE      FORM52,SOCCSTD
.
          MOVE      TPRPBD,FORM52 
          DIV       DAYNUM,FORM52
          MOVE      FORM52,SOCCPRV
.           
          MOVE      TCOPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,SOCCCOM
.           
          MOVE      TSTPBD,TOTPBD
          ADD       TPRPBD,TOTPBD
          ADD       TCOPBD,TOTPBD
.
          MOVE      TOTPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,SOCCTOT
.           
          MOVE      PTC1TYPE,SAVDTYP        * saved doctors type
.
          GOTO      PRNT1000
.
PRNT9999  RETURN
+
.**********************************************************************
.*                         TOTL0000                                   *
.*                        Print Totals                                *
.**********************************************************************
.
TOTL0000  
.         Add the totals of each doctor type to get the grand totals
.            
          ADD       SDISSTD,GDISSTD        * discharged
          ADD       SDISPRV,GDISPRV
          ADD       SDISCOM,GDISCOM
          ADD       SDISTOT,GDISTOT
.
          ADD       SBEDSTD,GBEDSTD        * discharged bed days
          ADD       SBEDPRV,GBEDPRV
          ADD       SBEDCOM,GBEDCOM
          ADD       SBEDTOT,GBEDTOT
.
          MOVE      ZERO,GSTYSTD           * average stay
          COMPARE   ZERO,GDISSTD
          GOTO      TOTL0200 IF EQUAL
.
          MOVE      GBEDSTD,FORM52
          DIV       GDISSTD,FORM52
          MOVE      FORM52,GSTYSTD
.
TOTL0200  MOVE      ZERO,GSTYPRV
          COMPARE   ZERO,GDISPRV
          GOTO      TOTL0400 IF EQUAL
.
          MOVE      GBEDPRV,FORM52
          DIV       GDISPRV,FORM52
          MOVE      FORM52,GSTYPRV
.
TOTL0400  MOVE      ZERO,GSTYCOM
          COMPARE   ZERO,GDISCOM
          GOTO      TOTL0600 IF EQUAL
.
          MOVE      GBEDCOM,FORM52
          DIV       GDISCOM,FORM52
          MOVE      FORM52,GSTYCOM
.
TOTL0600  MOVE      ZERO,GSTYTOT
          COMPARE   ZERO,GDISTOT
          GOTO      TOTL0800 IF EQUAL
.
          MOVE      GBEDTOT,FORM52
          DIV       GDISTOT,FORM52
          MOVE      FORM52,GSTYTOT
.
TOTL0800  MOVE      GSTPBD,FORM52         * average daily occupancy
          DIV       DAYNUM,FORM52
          MOVE      FORM52,GOCCSTD
.
          MOVE      GPRPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,GOCCPRV
.           
          MOVE      GCOPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,GOCCCOM
.           
          MOVE      GSTPBD,TOTPBD
          ADD       GPRPBD,TOTPBD
          ADD       GCOPBD,TOTPBD
.
          MOVE      TOTPBD,FORM52
          DIV       DAYNUM,FORM52
          MOVE      FORM52,GOCCTOT
.           
          MOVE      "Unknown   ",UNTDESC
.
          MATCH     SP3,SAVDTYP
          GOTO      TOTL1000 IF EQUAL
.
.         Find the doctors type description
.
          PACK      KEY5,CODEDT,SAVDTYP
          CALL      RDCODE1
          BRANCH    OVRCD,TOTL1000
          MOVE      TDESC,UNTDESC
.
TOTL1000  CALL      PGE0000
.
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"!Tot:",UNTDESC,*17,"!":
                    *19,SDISSTD,*25,SDISPRV,*32,SDISCOM,*39,SDISTOT,*46,"!":
                    *47,SBEDSTD,*54,SBEDPRV,*61,SBEDCOM,*68,SBEDTOT,*75,"!":
                    *76,SSTYSTD,*83,SSTYPRV,*90,SSTYCOM,*97,SSTYTOT,*104,"!":
                    *105,SOCCSTD,*112,SOCCPRV,*119,SOCCCOM,*125,SOCCTOT,*132,"!"
          ADD       ONE,CLNO
          MOVE      ZERO,PRTFLG
.
          CALL      DPGE0000
.
          CALL      CSUB0000            * initialise sub totals
          CALL      CTMP0000
.
          MOVE      ZERO,TSTPBD
          MOVE      ZERO,TPRPBD
          MOVE      ZERO,TCOPBD
          MOVE      ZERO,TOTPBD
.
          RETURN
+
.**********************************************************************
.*                         FIND0000                                   *
.*                   Find the end of the doctors name                 *
.**********************************************************************
.
FIND0000  CMATCH    SP1,DOCDESC
          GOTO      FIND1000 IF EQUAL
          BUMP      DOCDESC,1
          GOTO      FIND0000 IF NOT EOS
          GOTO      FIND9999 
.
FIND1000  BUMP      DOCDESC,1
          GOTO      FIND9999 IF EOS
          CMATCH    SP1,DOCDESC
          GOTO      FIND9999 IF EQUAL
          GOTO      FIND1000
.
FIND9999  RETURN
+
.**********************************************************************
.*                         GIVN0000                                   *
.*                   Find initial of doctors given name               *
.**********************************************************************
.
GIVN0000  MOVE      SP30,DNAME
          MOVE      DGNAME,DNAME
          CALL      BUMP0000
.
          MATCH     SP30,DNAME
          GOTO      GIVN9999 IF EQUAL
.
          BUMP      DNAME,1
          MOVE      DNAME,ANS
          APPEND    ANS,DOCDESC
          APPEND    ".",DOCDESC
.
GIVN9999  RETURN
+
.**********************************************************************
.*                         BUMP0000                                   *
.*                   Bump the doctors given name                      *
.**********************************************************************
.
BUMP0000  CMATCH    SP1,DNAME
          GOTO      BUMP9999 IF EQUAL
.
          CMATCH    ".",DNAME
          GOTO      BUMP9999 IF EQUAL
.
          BUMP      DNAME,1
          GOTO      BUMP1000 IF EOS
          GOTO      BUMP0000
.
BUMP1000  MOVE      SP30,DNAME
BUMP9999  RETURN
+
.**********************************************************************
.*                         HEAD0000                                   *
.*                      Print Headings                                *
.**********************************************************************
.
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      PGE0000 IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          PRINT     *45,"From Period : ",FROMPER:
                    " To Period : ",TOPER
.
          CALL      PGE0000
.
          PRINT     *1,"!",*17,"!",*27,"Discharged",*46,"!":
                    *53,"Discharged Bed Days",*75,"!",*85,"Average Stay":
                    *104,"!",*110,"Av. Daily Occupancy",*132,"!":
                    *N,*1,"!",*4,"Unit",*17,"!",*20,"STD",*27,"PRV":
                    *34,"3RD",*40,"Total",*46,"!",*50,"STD",*56,"PRV":
                    *64,"3RD",*69,"Total",*75,"!",*79,"STD",*86,"PRV":
                    *93,"3RD",*98,"Total",*104,"!",*108,"STD",*115,"PRV":
                    *122,"3RD",*126,"Total",*132,"!"
.
          CALL      PGE0000
          MOVE      TWO,CLNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                         PGE0000                                    *
.*                        Print Line                                  *
.**********************************************************************
.
PGE0000   PRINT     *1,"*--------------------------------------------------":
                    "---------------------------------------------------":
                    "-----------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                         DPGE0000                                   *
.*                    Print Double Line                               *
.**********************************************************************
.
DPGE0000  PRINT     *1,"*==================================================":
                    "===================================================":
                    "=============================*"
          ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                         CREA0000                                   *
.*                    Create Temporary File                           *
.**********************************************************************
.
CREA0000  CALL      KILL0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 131 u1-3,4-9",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      STATMPXX,TFILNAME
          RETURN
+
.**********************************************************************
.*                         KILL0000                                   *
.*                    Remove Temporary File                           *
.**********************************************************************
.
KILL0000  CLOSE     STATMPXX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**********************************************************************
.*                    Temporary file layout                           *
.**********************************************************************
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      STATMPXX,KEY9;PTC1TYPE,PTC1DOCT,DSNAME,DGNAME:
                                 DISSTD,DISPRV,DISCOM,DISTOT,BEDSTD,BEDPRV:
                                 BEDCOM,BEDTOT,STYSTD,STYPRV,STYCOM,STYTOT:
                                 OCCSTD,OCCPRV,OCCCOM,OCCTOT,TSTDPBD,TPRVPBD:
                                 TCOMPBD
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTMP1   RESET     KEY9
          READ      STATMPXX,KEY9;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READKS    STATMPXX;PTC1TYPE,PTC1DOCT,DSNAME,DGNAME:
                                 DISSTD,DISPRV,DISCOM,DISTOT,BEDSTD,BEDPRV:
                                 BEDCOM,BEDTOT,STYSTD,STYPRV,STYCOM,STYTOT:
                                 OCCSTD,OCCPRV,OCCCOM,OCCTOT,TSTDPBD,TPRVPBD:
                                 TCOMPBD
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    UPDATE    STATMPXX;PTC1TYPE,PTC1DOCT,DSNAME,DGNAME:
                                 DISSTD,DISPRV,DISCOM,DISTOT,BEDSTD,BEDPRV:
                                 BEDCOM,BEDTOT,STYSTD,STYPRV,STYCOM,STYTOT:
                                 OCCSTD,OCCPRV,OCCCOM,OCCTOT,TSTDPBD,TPRVPBD:
                                 TCOMPBD
          RETURN
.
WRTMP1    RESET     KEY9
          WRITE     STATMPXX,KEY9;PTC1TYPE,PTC1DOCT,DSNAME,DGNAME:
                                 DISSTD,DISPRV,DISCOM,DISTOT,BEDSTD,BEDPRV:
                                 BEDCOM,BEDTOT,STYSTD,STYPRV,STYCOM,STYTOT:
                                 OCCSTD,OCCPRV,OCCCOM,OCCTOT,TSTDPBD,TPRVPBD:
                                 TCOMPBD
          GOTO      OVERCOND IF OVER
          RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           * 
.**********************************************************************
.
          INCLUDE   PATCOMN3
.
          INCLUDE   PATC1SIO/INC       * clinical statistics 1 file
          INC       PATCODIO/INC       * codes file
          INC       PATDO1IO/INC       * doctors file
          INCLUDE   PATDRGIO/INC       * period date range file
          INCLUDE   STD001IO
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.

.******************************************************************************
.* System         : Medical Records                                           *
.* Program        : IBAMRT77.PBL                                              *
.* Name           : Pulling Lists                                             *
.******************************************************************************
.* Date           : 09/09/1988                                                *
.* Author         : Whirlpool                                                 *
.* Function       : Taranaki Pulling lists with mrt                           *
.* Modifications  :                                                           *
.******************************************************************************
.*       V12.00.01  30/05/2025  Don Nguyen     TSK 0955096                    *
.*                  Alphanumeric visit number changes                         *
.*       V11.02.01  09/02/2022  Thanh T          TSK 0905641                  *
.*                  Recompiled as OUTHEDFD/OUTMA1FD changes                   *
.*       V10.12.02  10/07/2018 Ebon Clements   TSK 0859796                    *
.*                  Open OUTMA1A1 regardless of multi hospital                *
.*       V10.12.01  01/03/2018 Ebon Clements   TSK 0851049                    *
.*                  Match OP hospital from slot template header - POPS0000    *
.*       V10.11.01  07/09/2017 Thanh T.        TSK 0821710                    *
.*                  Audit Amission/outpatient visit comments                  *
.******************************************************************************
.*       V10.07.01  30/10/2015  Ebon Clements  CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*       V10.05.01  23/02/2015 Sandra Barcham  CAR 313255                     *
.*                  Fixed I * C on mrtmasa3                                   *
.*       V10.04.01  14/07/2014  Ebon Clements  CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*       V10.03.05  30/08/2013  Ania P         CAR 290305                     *
.*                  Added "LOCATION" to report heading                        *
.*       V10.03.04  13/07/2012  Peter Vela     CAR 259672                     *
.*                  Fixed I * C                                               *
.*       V10.03.03  22/05/2012  Peter Vela     CAR 259672                     *
.*                  Fixed validations for medical bookings in INPT0000        *
.*       V10.03.02  02/05/2012 Peter Vela      CAR 259672                     *
.*                  Added Medical Bookings to INPT0000                        *
.*       V10.03.01  20/02/2012 Sandra Barcham  CAR 260436                     *
.*                  Fixed I * c on pmshcpaf                                   *
.*       V10.02.04  22/07/2011  Peter McMullen CAR 245597                     *
.*                  Check for clinics excluded from pulling list              *
.*       V10.02.03  12/07/2011  Mike Laming   CAR 240716                      *
.*                  Mods to use MR Digit Sort Sequence - see routine MRTRKSEQ *
.*       V10.02.02  04/07/2011  Ebon Clements  CAR 240724                     *
.*                  Mods for MRT hospital/location - Added hospital to        *
.*                  location key                                              *
.*       V10.02.01  10/02/2011  Jill Parkinson CAR 233088                     *
.*                  Use pmsvx1af to check multihosp inpatiets                 *
.*       V10.01.01  10/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added pmsvx1af                                            *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  23/10/2007  Jill Habasque CAR 151361                      *
.*                  Added check for spaces in MRHIREAS before reading mrtmovaf*
.*        V9.08.01  25/01/2007  Peter Vela CAR 127930                         *
.*                  Recompiled for MRTMASFD                                   *
.*        V9.04.04  18/06/2005 Jill Habasque                                  *
.*                  Fixed titles                                              *
.*        V9.04.03  20/05/2005 Ebon Clements      CAR 61449                   *
.*                  MR info should always be displayed regardless of history. *
.*                  New records do not have history so just show record       *
.*                  identifier. PDAT2200                                      *
.*        V9.04.02  11/03/2005 Guomin Fu          CAR 55589                   *
.*                  Fixed a bug that MR info is diplayed only when            *
.*                  lastest volumn of MR has movement history                 *
.*        V9.04.01  01/07/2004 Peter Vela         CAR 50964                   *
.*                  Added check for PTCNRQCF - Requested by free format field *
.*        V5.09.B02 09/01/2001 J.Tan                                          *
.*                  Mods for WEB Report Scheduler                             *
.*        V5.09.00  20.12.2000 Bronko Sosic                                   *
.*                  Recompiled for MRTHISFD                                   *
.*        V5.08.03  25.10.2000 J.Tan                                          *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 12/03/2000  Steve Armstrong       5.08 Mods               *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.*        V5.07.02  26/11/1999  Jill Habasque  SRF 646266                     *
.*                  Mods to remove printing of header & footer if no data     *
.*        V5.07.01  11/10/1999  Steve Downing            SRF 642509           *
.*                  Mods to print header and footer when there is no data     *
.*                  Returns straight to option screen when finished           *
.*                  22/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*                  V5.03.B2  23/06/95  Paul Howells     SRF 610046           *
.*                  Display date borrowed not date of clinic                  *
.******************************************************************************
.
          INC       STD001FD
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       IBASEQFD/INC
          INC       MRTHISFD/INC                 * tracking history file
          INC       MRTLOCFD/INC                 * tracking location file
          INC       MRTMASFD/INC                 * tracking master file
          INC       MRTMOVFD/INC
          INC       MRTSTFFD/INC
          INC       OUTBOAFD/INC                 * Outpatients Booking file
          INC       OUTBB1FD/INC                 * Outpatients Booking file
          INC       OUTCLIFD/INC                 * Outpatients Clinics file
          INC       OUTSESFD/INC                 * Outpatients Clinic Sessions
          INC       OUTHEDFD/INC                 * Outpatients Session Headers 
          INC       OUTCONFD/INC
          INC       OUTMA1FD/INC                 * Clinic Master file
          INC       OUTSITFD/INC                 * Outpatients Sites file
          INC       PATBAYFD/INC                 * Bay coding file
          INC       PATCONFD/INC                                       *I-50964
          INC       PATCODFD/INC
          INC       PATDO1FD/INC                 * Doctors file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC                 * Patients Masterfile details
          INC       PATMI1FD/INC                 * Inpatients details file
          INC       PMSVX1FD/INC                 * Inpatients details file
          INC       PATTRNFD/INC                 * Inpatients Bed Transfers file
          INC       PATVISFD/INC                 * Visit details file
          INC       PATWR1FD/INC
          INC       TFILEVAR/INC
          INC       MRTCONFD/INC
          INC       PMSHCPFD/INC
          INC       WEBERRFD/INC
.
.        File layout for temporary index file
.
PSTIDX    IFILE     SQL, FIXED=91, KEY="U31-36,1-22,72-79"             *C-240716
PSTROL    FILE      ASCII, FIXED=256
.
.VAR     TYPE       SIZE  PHYS. START  DESCRIPTION
.----    ----       ----  ----- -----  -----------
XCLIN     DIM       6       6       1  Clinic Id (Blank for Inpatients)
XSTIM     DIM       5       5       7  Clinic Start time (Blank for Inp)
XBAYC     DIM       3       3      12  Bay Code
XSRTS     DIM       8       8      15  MR Digit sort sequence         *I-240716
XURNO     DIM       8       8      23  U/R Number
XCTYP     DIM       6       6      31  Clinic Type (Blank for Inpatients)
XSLOT     FORM      3       3      37  Clinic Slot Number (Zero for Inp)
XDOFW     FORM      1       2      40  Clinic Day of Week (Zero for Inp)
XCOMM     DIM       30      30     42  Comments **** no longer used ****
XADMN     DIM       8       8      72  Admission Number
XDOCA     DIM       6       6      80  Attending Doctor (Blank for Outp)
XATIM     DIM       5       5      86  Appointment Time
.         End of record            91
.
.         program variables
.
AUDIFLAG  FORM      1
.CCNOTES   DIM       1                 * Available in PATCONFD         *D-50964
CLINIC    DIM       6
CMDLINE   DIM       50
COMENT1   DIM       30
COMENT2   DIM       30
CURCLIN   DIM       6
CURCTYP   DIM       6
CURDOFW   FORM      1
CURSTIM   DIM       5
D8DATE    DIM       8
DADDOPT   FORM      1
DADDS     DIM       11
DATE      DIM       8
DATETIME  DIM       16
DAYOFW    DIM       9
DIM20     DIM       20
DIM40     DIM       40
DIM20A    DIM       20
DIM23     DIM       23
DIM6      DIM       6
DOPTION   DIM       10
DYOFW     DIM       2
FIRSTFLG  FORM      1
FNAMEI    DIM       8
FNAMER    DIM       8
FORM8     DIM       8
HOSPITAL  DIM       3
IBCNMHOS  FORM      1
LOCATION  DIM       20
LOCA14    DIM       14
LST1CLIN  DIM       18
LST2CLIN  DIM       18
LST2UR    DIM       2
LSTADMN   DIM       8
LSTWRD    DIM       7
LASTDATE  DIM       10        * last visit date
LASTLOCN  DIM       7         * last visit location
MEDILOCN  DIM       4       * MR Location
NUMURS    FORM      8
NAME      DIM       30
.PTCNH77U  FORM      1        * Available in PATCONFD                  *D-50964
REQUESTR  DIM       12
REASON    DIM       11
SITE      DIM       6
RECID     DIM       14
.                                                                     *I-240716
SORTORDN  DIM       1         * Sort Sequence No.
SORTORDR  DIM       8         * Sort Sequence code (from UR Number)
WBSEHOSP  DIM       3
T1A       DIM       1
T2A       DIM       2
T2B       DIM       2
T3A       DIM       3
T4A       DIM       4
T5A       DIM       5
.
.         program constants
.
CODETY    INIT      "TY"
CODEAP    INIT      "ap"
DMO       INIT      "MO"
DTU       INIT      "TU"
DWE       INIT      "WE"
DTH       INIT      "TH"
DFR       INIT      "FR"
DSA       INIT      "SA"
DSU       INIT      "SU"
DOPT1     INIT      "INPATIENTS"
DOPT2     INIT      "OUTPATIENT"
DADD1     INIT      "(Additions)"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBAMRT77"
PRGNAM    INIT      "Pulling Lists"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000
ML1000    CALL      OPTN0000                * run program ??
          BRANCH    EXIT,ML9999             * exit
          BRANCH    OPTION,ML2000,ML3000,ML4000
.
ML2000    CALL      INPT0000                * inpatient list
          GOTO      ML1000
.
ML3000    CALL      OUTS0000                * outpatient single clinic
          GOTO      ML1000
.
ML4000    CALL      OUTA0000                * outpatient all clinics
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY1;*51,HOAUDA
          READ      CONTROLF,NINTY7;*172,MRCNNOMR
.
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          BRANCH    HOAUDA,INIT1000
          OPEN      OUTAUDBA,"outaudba"
.
INIT1000  DISPLAY   *P64:24,"patbaysf";
          OPEN      PATBAYS1,"patbaysf"
.
          DISPLAY   *P64:24,"patbaysf";
          OPEN      PATBAYS2,"patbaysf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.  
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"mrtmasaf"
          OPEN      MRTMASA1,"mrtmasaf"
.
          DISPLAY   *P64:24,"mrtmasaf"
          OPEN      MRTMASA3,"mrtmasaf"
.
          DISPLAY   *P64:24,"mrthisaf";
          OPEN      MRTHISA1,"mrthisaf"
.
          DISPLAY   *P64:24,"mrtlocaf";
          OPEN      MRTLOCA1,"mrtlocaf"
.
          DISPLAY   *P64:24,"mrtmovaf";
          OPEN      MRTMOVA1,"mrtmovaf"
.
          DISPLAY   *P64:24,"mrtstfaf";
          OPEN      MRTSTFA1,"mrtstfaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
.
          DISPLAY   *P64:24,"bokrx1af";
          OPEN      BOKRX1A1,"bokrx1af"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,THIRTY1;*134,HWORDPRO
          READ      CONTROLF,FIFTY9;*193,PTCNRQCF                      *I-50964
          READ      CONTROLF,FIFTY9;*34,CTYPEUR                       *I-240716

          IF        IBCNMHOS = 1
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
       
          CLOSE     CONTROLF
.
.         Generate the names of the temporary files
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEI
.
          MOVE      ONE,CNEWDS
.
          MOVE      SP20,XBAYC                                        *I-240716
          MOVE      SP20,SORTORDN                                     *I-240716
.
INIT9999  RETURN
+
.*********************************************************************
.         run the report ??
.*********************************************************************
OPTN0000  MOVE      ZERO,NUMURS
          MOVE      SP20,DADDS
          MOVE      ZERO,DADDOPT
.
          HLINE     *G37,2,47,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Inpatient Pulling List":
                    *P1:6,*V2LON,TWO,*HOFF:
                    " = Outpatient Pulling List - Single Clinic":
                    *P1:7,*V2LON,THREE,*HOFF:
                    " = Outpatient Pulling List - All Clinics"
.
OPTN1000  KEYIN     *P1:9,"Select option : ",*V2LON,OPTION
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL       * exit
          MOVE      ZERO,EXIT
          BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.*********************************************************************
.         Inpatient Pulling List
.*********************************************************************
INPT0000  DISPLAY   *P47:2,*V2LON,"- Inpatients "
.
INPT1000  CALL      KDTA0000                * Keyin Date for list
          BRANCH    OVRCD,INPT9999          * nothing input
.
          IF        IBCNMHOS = 1
            MOVE      SIX,CVERT
            CALL      KHOS0000
            BRANCH    EXIT,INPT1000 
            MOVE      KEY3,HOSPITAL
            MOVE      KEY3,WBSEHOSP                                   *I-240716
            CALL      GTSRTIND              * get MR Digit Sort Seq. indicator
          ENDIF
.                                  
          CALL      CONTQST
          BRANCH    CEXIT,INPT2000,INPT1000,INPT9999
.
INPT2000  CLOCK     TIME,CTIMEIS
          CALL      CTPI0000
          MOVE      SP6,XCLIN
          MOVE      SP6,XCTYP
          MOVE      SP5,XSTIM
          MOVE      ZERO,XSLOT
          MOVE      ZERO,XDOFW
.
.         Loop over the admissions for the date selected
.

          DISPLAY   *P1:24,*EL,*P10:24,"U/R No:",*P60:24,"Scanning:";
          UNPACK    DATE,TDATE
          PACK      KEY16,TDATE,SP30
          CALL      RDSMISS3
INPT3000  CALL      RDKMISS3
          BRANCH    OVRCD,INPT5000
.
          MATCH     TDATE,ADATE
          GOTO      INPT5000 IF NOT EQUAL
.
          DISPLAY   *P70:24,AADMNO;
.
.         Check for a valid pre-admission
.
          COMPARE   ONE,ASTAT
          GOTO      INPT3000 IF NOT EQUAL
.
          MATCH     SP8,AURNO
          GOTO      INPT3000 IF EQUAL
.
.-----   If we are using multi-hospital system ------
.
          IF        IBCNMHOS = 1
            MATCH     HOSPITAL,PMVXMHOS
            GOTO      INPT3000 IF NOT EQUAL
          ENDIF
.
.         Write a record to the temporary file
.
          MOVE      AURNO,FORM8
          MOVE      FORM8,XURNO
          MOVE      DAADMNO,XADMN
          MOVE      ADOCTA,XDOCA
.                                                           * start   *I-240716
          MOVE      XURNO,SORTORDR
          CALL      MRSRTORD                * generate MR Sort Order code
          MOVE      SORTORDR,XSRTS
.
          MATCH     XURNO,SORTORDN          * is Sort Seq the same as UR No.?
          IF        @EQUAL
            CALL      GBAY0000
          ENDIF                                             * end     *I-240716
.
          PACK      KEY36,XCTYP,XCLIN,XSTIM,XBAYC,XSRTS,XADMN,SP10     *C-240716
          CALL      WRTEMP1
          BRANCH    OVRCD,INPT3000
.
          DISPLAY   *P18:24,*V2LON,XURNO;
          ADD       ONE,NUMURS
          GOTO      INPT3000
.
INPT5000  PACK      KEY16,TDATE,SP70
          CALL      RSBKREC4
INPT5100  CALL      RKBKREC4
          BRANCH    OVRCD,INPT7000
.
          MATCH     BKREEDAT,TDATE
          GOTO      INPT7000 IF NOT EQUAL
.
          COMPARE   ZERO,BKRESTAT
          GOTO      INPT7000 IF NOT EQUAL
.
          MATCH     "Y",BKREINDC
          GOTO      INPT5100 IF EQUAL
.
          PACK      KEY8,DBKREBOO
          CALL      RDBKRX11
          BRANCH    OVRCD,INPT5100
.
          MOVE      BKREURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,INPT5100
.
          PACK      KEY17,BKREURNO,Z70
          CALL      RSMRMAS3
INPT5200  CALL      RPMRMAS3                    * Reads MRT master file
          BRANCH    OVRCD,INPT5250
.
          MATCH     MRMAURNO,BKREURNO
          GOTO      INPT5250 IF NOT EQUAL
.
.         PACK      KEY5,CODETY,MRMADOTY,SP70
.         CALL      RDCODE1
.         BRANCH    OVRCD,INPT5200
.
.         MATCH     "1",TCINDC1             * Look for OUT or EMR record
.         GOTO      INPT5200 IF NOT EQUAL
          GOTO      INPT5300
.
INPT5250  MATCH     "1",MRCNNOMR                 * Include patients with no MR
          GOTO      INPT5100 IF NOT EQUAL        * on pulling lists
.
          CALL      CLMRTMAS                     * Clear MR master file fields
          MOVE      BKREURNO,MRMAURNO            * Set U/R from booking
.
INPT5300  MATCH     SP70,BKRXADWD           * Booking Ward Set
          GOTO      INPT5400 IF EQUAL
.
          PACK      KEY5,CODEAP,BKRXADWD,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,INPT5100
.
          MOVE      PTCDNHCP,MEDILOCN     * Set MR Location
.
          MATCH     SP70,PTCDNHCP         * Adm Point Location Set?
          IF        @EQUAL
            PACK      KEY6,THCSCOD,SP70     * Read Ward to get mr location
            CALL      RDWARD1
            IF        OVRCD=0
              MOVE      PTWDLOCN,MEDILOCN   * MR Location
            ELSE
              GOTO      INPT5400
            ENDIF
          ENDIF
.
          MATCH     SP70,MRMACLOC
          IF        !@EQUAL
            MATCH     MRMACLOC,MEDILOCN   * Check Current Loc / Adm Point Loc
            GOTO      INPT5100 IF EQUAL
          ENDIF
.
INPT5400  MOVE      BKREURNO,FORM8
          MOVE      FORM8,XURNO
          MOVE      DBKREBOO,XADMN
          MOVE      BKREADOC,XDOCA
.
          MOVE      XURNO,SORTORDR
          CALL      MRSRTORD
          MOVE      SORTORDR,XSRTS
.
          MATCH     XURNO,SORTORDN
          IF        @EQUAL
            CALL      GBAY0000
          ENDIF          
.
          PACK      KEY36,XCTYP,XCLIN,XSTIM,XBAYC,XSRTS,XADMN,SP70
          CALL      WRTEMP1
          BRANCH    OVRCD,INPT5100
.
          DISPLAY   *P18:24,*V2LON,XURNO
          ADD       ONE,NUMURS
          GOTO      INPT5100
.
INPT7000  CALL      REPT0000                * print the report
          MATCH     "IBARSH99",PGM
          GOTO      INPT9999 IF EQUAL
          GOTO      INPT0000
.
INPT9999  RETURN
+
.*********************************************************************
.         Outpatient Pulling List - Single Clinic
.*********************************************************************
OUTS0000  DISPLAY   *P47:2,*V2LON,"- Outpatients (Single Clinic) "
          CALL      KCLN0000                * Keyin Clinic details
          BRANCH    EXIT,OUTS9999           * exit
.
          CLOCK     TIME,CTIMEIS
          CALL      CTPI0000
          MOVE      SP6,XDOCA
          DISPLAY   *P1:24,*EL,*P10:24,"U/R No:",*P60:24,"Scanning:";
          CALL      POPS0000                * process the clinic
          CALL      REPT0000                * print the report
    GOTO  OUTS9999
.
.         MATCH     "IBARSH99",PGM
.         GOTO      OUTS9999 IF EQUAL
.         GOTO      OUTS0000
.
OUTS9999  RETURN
+
.*********************************************************************
.         Outpatients Pulling List - All Clinics
.*********************************************************************
OUTA0000  DISPLAY   *P47:2,*V2LON,"- Outpatients (All Clinics) "
          CALL      KSIT0000                * Keyin Site/Date details
          BRANCH    EXIT,OUTA9999
.
          CLOCK     TIME,CTIMEIS
          CALL      CTPI0000
          MOVE      SP6,XDOCA
          DISPLAY   *P1:24,*EL,*P10:24,"U/R No:",*P60:24,"Scanning:";
.
.         Loop over all clinics 
.
          PACK      KEY20,SITE,SP20
OUTA1000  CALL      RDSCLIA1
          CALL      RDKCLIA1
          BRANCH    OVRCD,OUTA5000
.
          MATCH     SITE,OCASITE
          GOTO      OUTA5000 IF NOT EQUAL   * different site
.
          DISPLAY   *P1:24,*V2LON,OCACLIN;
.
.         Process the data for this clinic
.
          MOVE      OCACLIN,CLINIC
          CALL      POPS0000
          PACK      KEY20,SITE,CLINIC,Z20
          GOTO      OUTA1000
.
OUTA5000  CALL      REPT0000                * print the report
          MATCH     "IBARSH99",PGM
          GOTO      OUTA9999 IF EQUAL
          GOTO      OUTA0000
.
OUTA9999  RETURN
+
.*********************************************************************
.         do the report
.*********************************************************************
REPT0000  COMPARE   ZERO,NUMURS
          GOTO      REPT9000 IF EQUAL       * nothing found
.
.         Generate the report from the temporary file
.
          CALL      GRPT0000
          DISPLAY   *P1:24,*EL,*V2LON,*B:
                    "** Report Generation Completed **",*HOFF," - ";
          CALL      HITENTER
          GOTO      REPT9100
.
.         No data for the pulling list found
.
REPT9000  MATCH     "IBARSH99",PGM
          IF        @EQUAL
            PRINT     *1,"No Data found for Pulling List."
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,"No Data found for Pulling List.  ";
          CALL      HITENTER
.
REPT9100  CALL      DTPI0000
.
REPT9999  RETURN
+
.*********************************************************************
.         Subroutine to ask if Additions only are wanted
.*********************************************************************
ADDN0000  KEYIN     *P1:CVERT,*EL,"Additions Only (Y/N) : ",*V2LON,ANS
          REP       UPPLOW,ANS
          CMATCH    ANSY,ANS
          GOTO      ADDN1000 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      ADDN2000 IF EQUAL
          BEEP
          GOTO      ADDN0000
.
ADDN1000  MOVE      ONE,DADDOPT
          MOVE      DADD1,DADDS
          DISPLAY   *P24:CVERT,*V2LON,"Yes";
          GOTO      ADDN9999
.
ADDN2000  MOVE      ZERO,DADDOPT
          MOVE      SP20,DADDS
          DISPLAY   *P24:CVERT,*V2LON,"No ";
.
ADDN9999  RETURN
+
.*********************************************************************
.         Subroutine to get and validate a single date
.*********************************************************************
KDTA0000  DISPLAY   *P1:4,*EF
          MOVE      FOUR,CVERT
          CALL      GDOL0000
KDTA9999  RETURN
+
.*********************************************************************
.         Subroutine to get the date of the pulling list
.*********************************************************************
GDOL0000  DISPLAY   *P1:CVERT,*EL,"Enter Report Date    : "
          CALL      TMRW0000
          MOVE      TWENTY4,CCOL
          MOVE      ONE,CDEFDTE
          MOVE      ONE,CCANLDTE
          CALL      KEYDATE
          BRANCH    CQUEST,GDOL0000
          BRANCH    OVRCD,GDOL9999
.
          PACK      DATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATE
.
GDOL9999  RETURN
.
.*********************************************************************
.         Subroutine to get tomorrow's date
.*********************************************************************
TMRW0000  MOVE     CDD,DD
          MOVE     CMM,MM
          MOVE     CYY,YY
          MOVE     CCC,CC
.
          CALL     DMYCON                  * Convert to Julian date
.
          ADD      ONE,JULDAY
          MOVE     JULDAY,JWKDAY
          MOVE     JULYR,JWKYER
          MOVE     JULCC,JWKCC
.
          CALL     JULCON    
.
          MOVE     DD,CDAY
          MOVE     MM,CMON
          MOVE     YY,CYEAR
          MOVE     CC,CCENT
          REP      " 0",CYEAR              * incase the year was 2000-2009
.
TMRW9999  RETURN
.
.*********************************************************************
.         Inpatient pulling list header
.*********************************************************************
HDRI0000  PRINT     *40,"PRE-ADMISSIONS FOR ":
                    CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,*N
.
          ADD       TWO,CLNO
          IF        IBCNMHOS = 1
            PRINT     *40,"HOSPITAL : ",HOSPITAL,SP3,PTHSNAME,*N
            ADD       TWO,CLNO
          ENDIF
.
          CALL      UNLN0000
          PRINT     *1,"| Bay",*7,"|   U/R No",*19,"| Patients Name":
                    *52,"|",*54,"Current Location",*76,"|":
                    *78,"Record Identifier",*96,"|":
                    *98,"Date and Time borrowed",*132,"|"
          CALL      UNLN0000
          ADD       ONE,CLNO
.
HDRI9999  RETURN
+
.*********************************************************************
.         print an underline
.*********************************************************************
UNLN0000  PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
UNLN9999  RETURN
+
.*********************************************************************
.         Subroutine to print the tail for outpatients
.*********************************************************************
TAIL0000  BRANCH    OPTION,TAIL9999
.
.         Outpatients only - print the clinic details on the end of the page
.
TAIL1000  COMPARE   "60",CLNO
          GOTO      TAIL2000 IF NOT LESS
.
          PRINT     *N;
          ADD       ONE,CLNO           * Loop to the bottom of the page
          GOTO      TAIL1000
.
TAIL2000  PACK      OCADESC,OCADESC,SP30
          PACK      KEY20,SITE,CURCLIN,DATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     CURCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      GCLD0000
          ENDIF
          UNPACK    DATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          LOAD      DAYOFW,CURDOFW,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
.
          PRINT     *1,DAYOFW,SP1,CPCDATE," AT ",CURSTIM:
                    *40,"CLINIC : ",CURCTYP,SP1,CURCLIN,SP4,OCADESC
.
TAIL9999  RETURN
+
.*********************************************************************
.         Subroutine to open site dependant files
.*********************************************************************
OSDF0000  CLOSE     OUTCLIA1
          PACK      CFNAME,OSTFILE,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
          CLOSE     OUTBOKA1
          PACK      CFNAME,OSTFILE,FILBOKA1
          OPEN      OUTBOKA1,CFNAME
.
          CLOSE     OUTBOKA3
          PACK      CFNAME,OSTFILE,FILBOKA3
          OPEN      OUTBOKA3,CFNAME
.
          CLOSE     OUTBOKA4
          PACK      CFNAME,OSTFILE,FILBOKA4
          OPEN      OUTBOKA4,CFNAME
.
          CLOSE     OUTSESA1
          PACK      CFNAME,OSTFILE,FILSESA1
          OPEN      OUTSESA1,CFNAME
.
          CLOSE     OUTHEDA1
          PACK      CFNAME,OSTFILE,FILHEDA1
          OPEN      OUTHEDA1,CFNAME
.
          CLOSE     OUTBB1A1
          PACK      CFNAME,OSTFILE,FILBB1A1
          CALL      OPOTBB11
.
          CLOSE     OUTMA1A1
          PACK      CFNAME,OSTFILE,FILMA1A1
          CALL      OPOTMA11
.
OSDF9999  RETURN
.
.****************************************************************************
.         AUDIT ROUTINES for BOKA 
.         AUDIFLAG : 1   Write a A audit to file
.                    2   Write a B audit to file
.                    3   Write a C audit to file
.                    4   Write a D audit to file
.                    5   Delete  B audit to file
.****************************************************************************
.                                 AUDOTBOA
.    Audits for patient outpatients bookings file (outaudba)
.****************************************************************************
AUDOTBOA  BRANCH    HOAUDA,OTBOA999
.
          COMPARE   THREE,AUDIFLAG      * is it a after
          GOTO      OTBOA200 IF EQUAL
.
          COMPARE   FIVE,AUDIFLAG      * is it a delete
          GOTO      OTBOA600 IF EQUAL
.
          CALL      IBACLOCK           * get current date
          PACK      OTBAAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OTBAAUDD
          CLOCK     TIME,OTBAAUDT      * clock current time
          MOVE      ONE,OTBAAUDS       * set print status
          MOVE      PASSCODE,OTBAAUDO  * get user ID
          MOVE      PORT,OTBAAUDP      * get port number
.
          BRANCH    AUDIFLAG,OTBOA200,OTBOA200,OTBOA200,OTBOA200,OTBOA600
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      OTBOA999
.
OTBOA200  MOVE      AUDIFLAG,OTBAAUDR
          REPLACE   "1A2B3C4D",OTBAAUDR
          PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,OTBAAUDR  
          CALL      AWOTBOA           * write to audit file
          GOTO      OTBOA999
.
OTBOA600  PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,ANSB
          CALL      ADOTBOA           * delete audit file
.
OTBOA999  RETURN
.
.*********************************************************************
.         Keyin the hospital code
.*********************************************************************
KHOS0000  DISPLAY   *P1:CVERT,"Enter Hospital ID    : "
          MOVE      CVERT,EVERT
          MOVE      "24",ECOL
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS9999,KHOS0000
.
          DISPLAY   *P40:EVERT,PTHSNAME
.
KHOS9999  RETURN
.
.*********************************************************************
.         Subroutine to get and validate a single clinic
.*********************************************************************
KCLN0000  KEYIN     *P1:4,*EF:
                    *P1:4,"Enter Site Code      : ",*V2LON,SITE
          PACK      SITE,SITE,SP6
          MATCH     SP6,SITE
          GOTO      KCLN9100 IF EQUAL
.
.         Validate the site code
.
          MOVE      SITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,KCLN7100
.
          DISPLAY   *P40:4,OSTDESC
          CALL      OSDF0000                * open site dependant files
.
KCLN1000  DISPLAY   *P1:6,*EF,"Enter Clinic Code    : "
          MOVE      "24",ECOL
          MOVE      SIX,EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
          MOVE      SITE,IDDD
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLN0000,KCLN9100,KCLN0000
.
          MOVE      OCACLIN,CLINIC
          CALL      GCLD0000
          DISPLAY   *P1:8,"Clinic Description   : ",*V2LON,OCADESC
          MOVE      TEN,CVERT
          CALL      GDOL0000
          BRANCH    OVRCD,KCLN1000                * nothing input
.
.         see if want additions only
.
          MOVE      TEN2,CVERT
          CALL      ADDN0000
.
          IF        IBCNMHOS = 1
            LOAD      CVERT,OPTION,TEN4,TEN4,TEN2
            CALL      KHOS0000
            BRANCH    EXIT,KCLN0000
            MOVE      KEY3,HOSPITAL
            MOVE      KEY3,WBSEHOSP                                   *I-240716
            CALL      GTSRTIND              * get MR Digit Sort Seq. indicator
          ENDIF
.
          CALL      CONTQST
          BRANCH    CEXIT,KCLN5000,KCLN0000,KCLN9100
.
KCLN5000  MOVE      ZERO,EXIT
          GOTO      KCLN9999
.
KCLN7100  DISPLAY   *P1:24,*B,"Invalid Site Code.  ",*EL;
          CALL      HITENTER
          GOTO      KCLN0000
.
KCLN7200  DISPLAY   *P1:24,*B,"Invalid Clinic Code.  ",*EL;
          CALL      HITENTER
          GOTO      KCLN1000
.
KCLN9100  MOVE      ONE,EXIT
.
KCLN9999  RETURN
+
.*********************************************************************
.         Subroutine to get and validate a single site
.*********************************************************************
KSIT0000  KEYIN     *P1:4,*EF:
                    *P1:4,"Enter Site Code      : ",*V2LON,SITE
.
          PACK      SITE,SITE,SP6
          MATCH     SP6,SITE
          GOTO      KSIT9100 IF EQUAL
.
.         Validate the clinic code
.
          MOVE      SITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,KSIT7100
.
          CALL      OSDF0000                * open site dep. files
          DISPLAY   *P1:6,"Site Description     : ",*V2LON,OSTDESC
.
          MOVE      EIGHT,CVERT
          CALL      GDOL0000                * nothing input
          BRANCH    OVRCD,KSIT0000
.
.         Check if we want additions only
.
          MOVE      TEN,CVERT
          CALL      ADDN0000 
.
          IF        IBCNMHOS = 1
            LOAD      CVERT,OPTION,TEN4,TEN4,TEN2
            CALL      KHOS0000
            BRANCH    EXIT,KSIT0000
            MOVE      KEY3,HOSPITAL
            MOVE      KEY3,WBSEHOSP                                   *I-240716
            CALL      GTSRTIND              * get MR Digit Sort Seq. indicator
          ENDIF
.
          CALL      CONTQST
          BRANCH    CEXIT,KSIT9000,KSIT0000,KSIT9100
.
KSIT7100  DISPLAY   *P1:24,*B,"Invalid Site Code.  ",*EL;
          CALL      HITENTER
          GOTO      KSIT0000
.
KSIT9000  MOVE      ZERO,EXIT 
          GOTO      KSIT9999
.
KSIT9100  MOVE      ONE,EXIT 
KSIT9999  RETURN
+
.*********************************************************************
.         Subroutine to get the description for a clinic
.*********************************************************************
GCLD0000  MATCH     SP6,OCADOCT
          GOTO      GCLD9999 IF EQUAL
.
.         Use the doctor's code from the clinic file to get the doctor's name
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GCLD2000
.
.        Pack up the doctors name
.
          CLEAR     OCADESC
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OCADESC
          GOTO      GCLD9999
.
.         Doctor code not found
.
GCLD2000  CLEAR     OCADESC
          APPEND    "UNKNOWN DOCT. ",OCADESC
          APPEND    OCADOCT,OCADESC
          APPEND    SP30,OCADESC
          RESET     OCADESC
.
GCLD9999  RETURN
+
.*********************************************************************
.         Subroutine to get the bay code for a given U/R number
.*********************************************************************
GBAY0000  MOVE      SP3,BAYCODE
          MOVE      SP3,XBAYC
.
.         Get the last two digits of the U/R number
.
          UNPACK    XURNO,DIM6,LST2UR
          REP       " 0",LST2UR
.
.         Check for a record in the bay code file
.
          PACK      KEY5,LST2UR,SP3
          CALL      RDSBAYS2
          CALL      RDKBAYS2
          BRANCH    OVRCD,GBAY9999
          MATCH     LST2UR,BAYUR
          GOTO      GBAY9999 IF NOT EQUAL
.
          MOVE      BAYCODE,XBAYC
.
GBAY9999  RETURN
.
.*********************************************************************
.         Subroutine to process the temporary file
.*********************************************************************
GRPT0000  DISPLAY   *P1:24,*EL,*P40:24,"** Generating Report **":
                    *P1:24,*EL,*P10:24,"U/R No : ";
.
.         Initialize some variables
.
          MOVE      "60",CLNO
          MOVE      ZERO,CPAGENO
          MOVE      SP6,CURCLIN
          MOVE      SP5,CURSTIM
          MOVE      SP6,CURCTYP
          MOVE      ZERO,CURDOFW
.
.         Loop over the temporary file
.
          MOVE      SP30,KEY36
          CALL      RDSTEMP1
GRPT1000  CALL      RDKTEMP1
          BRANCH    OVRCD,GRPT9000
.
          DISPLAY   *P19:24,*V2LON,XCLIN,SP1,XURNO;
.
.         Get the master file details
.
          MOVE      XURNO,FORM8
          MOVE      FORM8,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GRPT1000
.
.         Check if we have changed clinics
.
          MATCH     XCLIN,CURCLIN
          GOTO      GRPT2000 IF NOT EQUAL
.
          MATCH     XSTIM,CURSTIM
          GOTO      GRPT3000 IF EQUAL
.
.         Changed clinic's - force a new page
.
GRPT2000  CALL      HEAD0000
.
.         Print the data for this record
.
GRPT3000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.         Inpatient Report
.
          MOVE      SP1,ANS
          LOAD      ANS,PCASE,CCNOTES
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME 
          MOVE      PACFNAME,NAME  
.
          CALL      PDAT0000
          GOTO      GRPT1000
.
.         End of the report - print a line and finish
.
GRPT9000  CALL      TAIL0000
.
GRPT9999  RETURN
.
.*********************************************************************
.         Subroutine to print the inpatient pulling list details
.*********************************************************************
PDAT0000  MOVE      SP30,OCADESC
.         Set the case note's indicator and print the data
.
          MOVE      SP1,ANS
          LOAD      ANS,PCASE,CCNOTES
.
          CALL      CLIN0000                * print the common  bit of line
          ADD       ONE,CLNO
.
PDAT1000  MOVE      SP30,MRLODESC
          MOVE      ONE,FIRSTFLG
          PACK      KEY20,XURNO,Z30
          CALL      RSMRMAS1
PDAT2000  CALL      RPMRMAS1
          BRANCH    OVRCD,PDAT9000
.
          MATCH     XURNO,MRMAURNO
          GOTO      PDAT9000 IF NOT EQUAL
.
. get latest movement for this volume
.
          PACK      KEY26,MRMAKEY,Z30
          CALL      RSMRHIS1
          CALL      RPMRHIS1
          BRANCH    OVRCD,PDAT2200
.
          MATCH     MRMAKEY,MRHIKEY
          GOTO      PDAT2200 IF NOT EQUAL
.
          GOTO      PDAT2400
.
. No history so only display record identifier and clear other fields
.
PDAT2200  MOVE      SP70,LOCATION
          MOVE      SP70,DATETIME
          MOVE      SP70,MRHIREQB
          MOVE      SP70,MRHIRECV
          MOVE      SP70,MRHIREAS
          PACK      RECID,MRMAHHOS,MRMAHLOC,SLASH,MRMADOTY,SLASH,MRMAVOLN,SP30  
          GOTO      PDAT2500
.
. format data for temp file write
.
PDAT2400  MOVE      SP30,MRLODESC
          PACK      KEY7,MRHIDHOS,MRHILOC,SP70
          CALL      RDMRLOC1
.
          UNPACK    MRHIDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DATETIME,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,SP1,MRHITIME
          MOVE      MRLODESC,LOCATION
          PACK      RECID,MRMAHHOS,MRMAHLOC,SLASH,MRMADOTY,SLASH,MRMAVOLN,SP30  
.
PDAT2500  ADD       ONE,CLNO
.
          IF        CLNO > 55
            CALL      HEAD0000
            CALL      CLIN0000
          ELSE 
            IF        FIRSTFLG = 1
              MOVE      ZERO,FIRSTFLG
            ELSE
              PRINT     *1,"|",*7,"|",*19,"|";
            ENDIF
          ENDIF
.
          BRANCH    OPTION,PDAT3000,PDAT5000,PDAT5000
          GOTO      PDAT9999
.
PDAT3000  PRINT     *52,"|",*54,LOCATION,*76,"|",*78,RECID,*96,"|":
                    *98,DATETIME,*132,"|"
          GOTO      PDAT2000
.
PDAT5000  MOVE      LOCATION,LOCA14
          MOVE      SP30,REQUESTR
.
.     Get the name of the requester
.
          IF        PTCNRQCF=ONE
            MOVE      MRHIREQB,REQUESTR                                *I-50964
          ELSE
            PACK      KEY6,MRHIRECV
            CALL      RDMRSTF1
            BRANCH    OVRCD,PDAT5500
            MOVE      MRSTGNAM,PACGNAME
            MOVE      MRSTSNAM,PACSNAME
            MOVE      MRSTTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME 
            MOVE      PACFNAME,REQUESTR
          ENDIF
PDAT5500
          MOVE      SP30,REASON
          MATCH     SP70,MRHIREAS
          IF        !@EQUAL
            PACK      KEY4,MRHIREAS
            CALL      RDMRMOV1                * get movement reason
            MOVE      MRMODESC,REASON
          ENDIF
          PRINT     *52,"|",*54,LOCA14,*70,"|",*72,DATETIME,*89,"|":
                    *90,RECID,*105,"|",*106,REQUESTR,*119,"|",*121,REASON:
                    *132,"|"
          GOTO      PDAT2000
.
.     Finish off the line if necessary
.
PDAT9000  IF        FIRSTFLG = 1
            IF        OPTION = 1
              PRINT     *52,"|",*76,"|",*96,"|",*132,"|"
            ELSE
              PRINT     *52,"|",*70,"|",*89,"|",*105,"|",*119,"|",*132,"|"
            ENDIF
          ENDIF
.
          CALL      UNLN0000
.
PDAT9999  RETURN
+
.*********************************************************************
.         Subroutine to print the common part of the line            
.*********************************************************************
CLIN0000  PRINT     *1,"|",*3,XBAYC,*7,"|",*9,XURNO,ANS:
                    *19,"|",*21,NAME;
CLIN9999  RETURN
.*********************************************************************
.         Subroutine to process the booking file for a single clinic
.*********************************************************************
POPS0000  PACK      KEY28,SITE,CLINIC,DATE,SP30
          CALL      RDSBOKA1
POPS1000  CALL      RDKBOKA1
          BRANCH    OVRCD,POPS9999
.
.         Check to see if we have finished this clinic
.
          MATCH     SITE,OBASITE
          GOTO      POPS9999 IF NOT EQUAL
.
          MATCH     CLINIC,OBACLIN
          GOTO      POPS9999 IF NOT EQUAL
.
          MATCH     DATE,OBADATE
          GOTO      POPS9999 IF NOT EQUAL
.
.  * Check for Exclude form pulling list
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,POPS1000
          PACK      KEY28,OBASITE,OBACLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,POPS1000
          MATCH     "1",OTHEXPUL
          GOTO      POPS1000 IF EQUAL
.
          DISPLAY   *P70:24,OBAURNO;
.
.         Check for a valid outpatient number
.
          MATCH     SP8,OBAURNO
          GOTO      POPS1000 IF EQUAL
.
          COMPARE   ONE,OBASTAT
          GOTO      POPS1000 IF NOT EQUAL
.
          MOVE      "*Missing details record* ",OBACOMM
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
.
.         Check if we only want additions ( Reject if DADDOPT=1 & OBAPULL=1 )
.
          MOVE      ZERO,FORM1
          LOAD      FORM1,DADDOPT,OBAPULL
.
          BRANCH    FORM1,POPS1000
.
.-------   If we are using multi hospitals we need to check the hospital ----
.
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,POPS1000
.
          IF        IBCNMHOS = 1
            MATCH     HOSPITAL,OTHEHOSP
            GOTO      POPS1000 IF NOT EQUAL
          ENDIF
.
.         Write a record to the temporary file
.
          MOVE      OBAURNO,XURNO
          MOVE      OBACLIN,XCLIN
          MOVE      OBACTYP,XCTYP
          MOVE      OBASTRT,XSTIM
          MOVE      OBASLOT,XSLOT
          MOVE      OBADAY,XDOFW
          MOVE      DOBAOUTN,XADMN
          MOVE      OBACOMM,XCOMM
          MOVE      OBATIME,XATIM
.                                                           * start   *I-240716
          MOVE      XURNO,SORTORDR
          CALL      MRSRTORD                * generate MR Sort Order code
          MOVE      SORTORDR,XSRTS
.
          MATCH     XURNO,SORTORDN          * is Sort Seq the same as UR No.?
          IF        @EQUAL
            CALL      GBAY0000
          ENDIF                                             * end     *I-240716
.
          PACK      KEY36,XCTYP,XCLIN,XSTIM,XBAYC,XSRTS,XADMN,SP10
          CALL      WRTEMP1
          BRANCH    OVRCD,POPS1000
.
          DISPLAY   *P18:24,*V2LON,XURNO;
          ADD       ONE,NUMURS
.
.         Flag this outpatient record as having appeared on a pulling list
.
          MOVE      TWO,AUDIFLAG                  * set flag to write a b audit
          CALL      AUDOTBOA                      * write audit
          MOVE      ONE,OBAPULL    
          CALL      UPBOKA1
          MOVE      THREE,AUDIFLAG                * set flag to write a c audit
          CALL      AUDOTBOA                      * write audit
          GOTO      POPS1000
.
POPS9999  RETURN
+
.*********************************************************************
.         Subroutine to print the headings for the reports
.*********************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          GOTO      HEAD1000 IF EQUAL
.
          CALL      UNLN0000
          CALL      TAIL0000
.
.         Print the top section of the heading
.
HEAD1000 
          LOAD      DOPTION,OPTION,DOPT1,DOPT2,DOPT2
          UNPACK    DATE,CCENT,CYEAR,CMON,CDAY
.
.         Print the basic heading that is common to both forms of the report
.
          CALL      HEAD132
.         Branch to the appropriate heading details, depending on option
.
          BRANCH    OPTION,HEAD2100,HEAD2200,HEAD2200
.
HEAD2100  CALL      HDRI0000
          GOTO      HEAD9999
.
.         Outpatient pulling list
.            - Get the clinic description
.
HEAD2200  PACK      OCADESC,OCADESC,SP30
          PACK      KEY20,SITE,XCLIN,DATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     XCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      GCLD0000
          ENDIF
          LOAD      DAYOFW,XDOFW,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          PRINT     *40,"CLINIC :",*49,XCTYP,SP1,XCLIN,SP4,OCADESC,*N:
                    *49,DAYOFW,SP1,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    " AT ",XSTIM,*N
.
          PACK      KEY25,SITE,XCLIN,CCENT,CYEAR,CMON,CDAY,XSTIM
          CALL      RDSESA1
          BRANCH    OVRCD,HEAD2210         
.
          PACK      KEY28,OSESITE,OSECLIN,DOSEDAY,OSESTRT,OSESSHNO,OSESSDAT
          CALL      RDOTHED1
          BRANCH    OVRCD,HEAD2210
.
          MATCH     SP70,OTHELTYP
          GOTO      HEAD2210 IF EQUAL
.
          PACK      KEY5,ANSC,ANST,OTHELTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD2210
.
          PRINT     *40,"LOCATION : ",OTHELTYP,SP3,TDESC
          GOTO      HEAD2220
.
HEAD2210  PRINT     *40,"LOCATION : ","Unknown Location ",KEY25,SP1,KEY28
.
HEAD2220  IF        IBCNMHOS = 1
            PRINT     *40,"HOSPITAL : ",HOSPITAL,SP3,PTHSNAME,*N
            ADD       TWO,CLNO
          ENDIF
.
          ADD       THREE,CLNO
          CALL      UNLN0000
.
.         Print the headings for each of the fields to be printed
.
          PRINT     *1,"| Bay",*7,"|   U/R No",*19,"| Patients Name":
                    *52,"|",*54,"Location",*70,"|",*72,"Date and Time",*89,"|":
                    *91,"Record",*105,"|",*106,"Requested",*119,"|":
                    *121,"Reason",*132,"|"
          PRINT     *1,"|",*7,"|",*19,"|":
                    *52,"|",*70,"|",*72,"Borrowed",*89,"|":
                    *91,"Identifier",*105,"|",*106,"By",*119,"|":
                    *132,"|"
.
          ADD       TWO,CLNO
          CALL      UNLN0000
          MOVE      XCLIN,CURCLIN
          MOVE      XSTIM,CURSTIM
          MOVE      XDOFW,CURDOFW
          MOVE      XCTYP,CURCTYP
HEAD9999  RETURN
+
.****************************************************************************
.         Delete the temporary index file
.****************************************************************************
DTPI0000  CLOSE     PSTIDX
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
DTPI9999  RETURN
.
.*********************************************************************
.         get the last visit details for patient
.*********************************************************************
GLVD0000  UNPACK    SP30,LASTDATE,LASTLOCN
          PACK      KEY24,XURNO,DATE,Z30
          CALL      RDSVISA2
.
GLVD1000  CALL      RDPVISA2
          BRANCH    OVRCD,GLVD9999
          MATCH     XURNO,PVIURNO
          GOTO      GLVD9999 IF NOT EQUAL
.
          BRANCH    PVITYPE,GLVD1000,GLVD1500,GLVD1500
          GOTO      GLVD1000
.
.         if an outpatient, get last date and clinic
.
GLVD1500  IF        PVITYPE = TWO
            UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,LASTDATE
            MOVE      PVIDOCT,LASTLOCN
          ENDIF
.
.         if an inpatient, get last visit discharge date and ward
.
          IF        PVITYPE = THREE
            MOVE      PVIBILL,AADMNO
            MOVE      AADMNO,KEY8
            CALL      RDMISS1
            BRANCH    OVRCD,GLVD1000
            COMPARE   THREE,ASTAT
            GOTO      GLVD1000 IF NOT EQUAL      * not discharged
.
.           get the last discharge date and ward
.
            PACK      KEY30,AADMNO,Z30
            CALL      RDSTRAN2
            CALL      RDPTRAN2
            BRANCH    OVRCD,GLVD1000
            MATCH     TADMN,AADMNO
            GOTO      GLVD1000 IF NOT EQUAL
            CMATCH    ANSD,TMOVE
            GOTO      GLVD1000 IF NOT EQUAL
.
            UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,LASTDATE
            PACK      LASTLOCN,TWARD,SLASH,TBED
          ENDIF
.
GLVD9999  RETURN
+
.*********************************************************************
.         Create the temporary index file
.*********************************************************************
CTPI0000  CALL      DTPI0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 91 u31-36,1-22,72-79",CMDLINE                    *C-240716
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      PSTIDX,FNAMEI
CTPI9999  RETURN
+
.
.         I/O for the temporary index file
.
RDSTEMP1  RESET     KEY36
          READ      PSTIDX,KEY36;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    PSTIDX;XCLIN,XSTIM,XBAYC,XSRTS,XURNO,XCTYP:       *C-240716
                           XSLOT,XDOFW,XCOMM,XADMN,XDOCA,XATIM
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY36
.
          READ      PSTIDX,KEY36;ANS
          GOTO      OVERCOND IF NOT OVER
.
....      UNPACK    KEY36,XCTYP,KEY22
          WRITE     PSTIDX,KEY36;XCLIN,XSTIM,XBAYC,XSRTS,XURNO:       *C-240716
                           XCTYP,XSLOT,XDOFW,XCOMM,XADMN,XDOCA,XATIM
          RETURN
+
.............................................................................
          INC       STD001IO
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       KYOUTCLI
          INC       PATHSPKY
          INC       CLMRTMAS
          INC       IBASEQIO/INC
          INC       MRTRKSEQ                *Medical Records Tracking Sort Order
          INC       MRTHISIO/INC            * tracking history file
          INC       MRTLOCIO/INC            * tracking location file
          INC       MRTMOVIO/INC
          INC       MRTSTFIO/INC
          INC       MRTMASIO/INC            * tracking master file
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTSESIO/INC
          INC       OUTHEDIO/INC
          INC       OUTDSCLN
          INC       OUTMA1IO/INC            * Clinic Master file
          INC       OUTSITIO/INC
          INC       PATHSPIO/INC
          INC       PATBAYIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PMSHCPIO/INC
          INC       TFILENAM
          INC       WEBERRIO/INC
.............................................................................

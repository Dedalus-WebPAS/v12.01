.*****************************************************************************
.* System    :   Medical Records Tracking                                    *
.* Program   :   FX266738                                                    *
.* Desc      :   Fixit for MRT Location code of F069 for WA Health           *
.*****************************************************************************
.* Date      :   06/06/2012                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through the mrtmasaf table and for   *
.*               each record it will update the following where applicable:  *
.*                - where home location code is F069, F071 or F073 then      *
.*                  load home hospital and location into current hospital    *
.*                  and location and change home location to F067            *
.*                - if current hospital is blank then populate it with home  *
.*                  hospital if blank                                        *
.* Mods      :                                                               *
.*        V10.03.01 14/06/2012  Steve Armstrong  CAR 266738                  *
.*                  Mods to load the home location into the current location *
.*                  when updating an invalid home location.                  *
.*        V10.03.00 06/06/2012  Steve Armstrong  CAR 266738                  *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       MRTMASFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
RECCOUNT  FORM      8
SAVKEY20  DIM       20
.
.
PRGID     INIT      "FX266738"
PRGNAM    INIT      "Fixit for MRT Master Locations"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with clean up
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"mrtmasaf";
          OPEN      MRTMASA1,"mrtmasaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Covert any home locations of F069 to F067 and default              *
.*        the population of the current hospital id if it's blank.           *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER                 * initialise counters
          MOVE      ZERO,RECCOUNT
          DISPLAY   *P1:24,*EL,"Processing record:";
.
          MOVE      SP20,KEY20
PROC0100  CALL      RSMRMAS1                     * position at start of file
PROC0500  CALL      RKMRMAS1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          ADD       ONE,RECCOUNT                 * increment total record count
.
          IF        (RECCOUNT%100) = 0 | RECCOUNT = 1
            DISPLAY   *P20:24,*EL,*V2LON,RECCOUNT;
          ENDIF
.
.         First, check to see if a key field needs changing
.
          MATCH     "F069",MRMAHLOC              * home location of "F069"?
          GOTO      PROC7000 IF EQUAL            * yes
.
          MATCH     "F071",MRMAHLOC              * home location of "F071"?
          GOTO      PROC7000 IF EQUAL            * yes
.
          MATCH     "F073",MRMAHLOC              * home location of "F073"?
          GOTO      PROC7000 IF EQUAL            * yes
.
.         No key fields need changing, so check the current locations
.
          MATCH     SP3,MRMACHOS                 * current hospital id blank ?
          GOTO      PROC0500 IF NOT EQUAL        * no
.
          MOVE      MRMAHHOS,MRMACHOS            * update hospital id
          GOTO      PROC8000
.
.         A key field needs to be changed, so see if the record exists already
.
PROC7000  PACK      SAVKEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN
.
          MOVE      "F067",MRMAHLOC
          PACK      KEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN
          CALL      RAMRMAS1                     * does record already exist ?
          IF        OVRCD = 0
            MOVE      SAVKEY20,KEY20             * reposition on key
            GOTO      PROC0100
          ENDIF
.
          MOVE      SAVKEY20,KEY20
          CALL      RDMRMAS1
          BRANCH    OVRCD,PROC0500
.
.         Save the home location details into the current location
.
          MOVE      MRMAHHOS,MRMACHOS
          MOVE      MRMAHLOC,MRMACLOC
.
          MOVE      "F067",MRMAHLOC              * update home location
.
PROC8000  ADD       ONE,COUNTER                  * increment updated rec count
          CALL      UPMRMAS1                     * update record
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:23,*EF,"Fixit completed.  ",*V2LON,COUNTER,*HOFF:
                    " records updated.":
                    *P1:24;
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       MRTMASIO/INC

.*******************************************************************************
.* System   : Patient Management                                               *
.* Program  : IBAPMS77                                                         *
.* Desc.    : Disaster Report                                                  *
.*******************************************************************************
.* Date     : 03/06/2011                                                       *
.* Author   : Peter Vela                                                       *
.* Function : This program provides a printed report of disaster patients      *
.*            within a specified disaster range, hospital range and visit type *
.*            range.                                                           *
.*                                                                             *
.* Mods     :                                                                  *
.*******************************************************************************
.* V12.00.01 30/05/2025  Don Nguyen     TSK 0955096                            *
.*           Alphanumeric visit number changes                                 *
.* V11.04.01 18/12/2023  Thanh T        TSK 0906342                            *
.*           Added Clear OTMAPCOD field                                        *
.* V11.02.01 09/02/2022 Thanh T         TSK 0905641                            *
.*           Added Additional HCP Code (1-4) for outma1af                      *
.*******************************************************************************
.*             V10.05.01   06/10/2014  Ebon Clements    CAR 268970             *
.*                         Change to use OUTCLIFD index 1 instead of index 2   *
.*             V10.03.01   26/11/2013 J.Tan             CAR 212716             *
.*                         Mods to define CATACTIV for PATCODTM                *
.*             V10.02.03   09/08/2011  Peter Vela       CAR 239559             *
.*                         Added Hospital Description, Unit Description,       *
.*                         U/R Number, Location parameter functionality         *
.*             V10.02.02   01/08/2011  Peter Vela       CAR 239583             *
.*                         Fixed title                                         *
.*             V10.02.01   03/06/2011  Peter Vela       CAR 239583             *
.*                         Created report program                              *
.*******************************************************************************
.
.         FILE DESCRIPTION INCLUDES
.
          INC       STDGENDF
          INC       IBACONFD/INC
          INC       BOKDIAFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       DISMASFD/INC
          INC       DISPATFD/INC
          INC       DISPTLFD/INC
          INC       PATCODFD/INC            * Patient Codes File
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC            * hospital file
          INC       PATMA1FD/INC            * Patient Master File
          INC       PATMI1FD/INC            * Patient Admission File
          INC       PATWR1FD/INC            * Patient Ward/Bed File
          INC       PATVISFD/INC
          INC       PMSCCDFD/INC            * Concession Card Details File
          INC       PMSEVTFD/INC
          INC       PMSHCPFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC            * Visit Extension file
          INC       OUTCLIFD/INC
          INC       OUTHEDFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCANFD/INC
          INC       OUTCTYFD/INC
          INC       OUTDIAFD/INC
          INC       OUTMA1FD/INC
          INC       OUTRSHFD/INC
          INC       OUTSESFD/INC
          INC       WATTR1FD/INC
          INC       WEBCONFD/INC
          INC       WEBSECFD/INC
          INC       MLTCODFD/INC
.
.         LOCAL VARIABLE DEFINITION
.

PRGID     INIT      "IBAPMS77"
PRGNAM    INIT      "Disaster Report"
VERSION   INIT      "V12.01.00"
.
INPSTAT1  INIT      "Pre-adm"
INPSTAT2  INIT      "Current IP"
INPSTAT3  INIT      "Discharged"
INPSTA3A  INIT      "Disc-"
INPSTA3B  INIT      "Cancelled"
INPSTAT4  INIT      "Leave"
INPSTAT5  INIT      "Cancel"
.
VISTCOMH  INIT      "COM"
VISTEMER  INIT      "EMG"
VISTOUTP  INIT      "OP"
VISTINPT  INIT      "IP"
VISTALLH  INIT      "RF"
VISTEVNT  INIT      "EV"
.
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
JAN       INIT      "January"
FEB       INIT      "February"
MAR       INIT      "March"
APR       INIT      "April"
MAY       INIT      "May"
JUN       INIT      "June"
JUL       INIT      "July"
AUG       INIT      "August"
SEP       INIT      "September"
OCT       INIT      "October"
NOV       INIT      "November"
DEC       INIT      "December"
.
QUOTE     INIT      "#""
.
ADIADESC  DIM      25
CATACTIV  FORM     "0"
CURRDATE  DIM      8                         * Current Date
CMDLINE   DIM      60                        * Command Line
CFILEPRE  DIM       3
CATEGORY  DIM       2
CODE      DIM       3
UNITDESC  DIM       18
DIM127    DIM       127
DISPNAME  DIM      60
DIM20HOS  DIM      20
DIM20DIS  DIM      20 
DIM3TYP   DIM      3
DISACODE  DIM      9                         * Disaster Work Variable
DOCTCODE  DIM       6
NAME35    DIM       35
FLDITMNO  FORM      3
FLDITEM   DIM       3
FMTSNAME  DIM       35
FMTGNAME  DIM       35
FMTTITLE  DIM       10
FILTHOSP  DIM      3
FILTTYPE  DIM      25
HTMLFILE  FIFO      ASCII, FIXED=250
LINKTEM   DIM       3
LINKREP   DIM       2
LINKPRG   DIM       8
MTHNAM3   DIM       3
VISTDESC  DIM      25                        * Visit Type work Variable
VISTLOC   DIM      50
PATNAME   DIM      20                        * Patient Name
DOCFNAME  DIM      20                        * Doctor Name
UNITCODE  DIM       6
.
CATct     INIT      "ct"
SP11      INIT     "           "
.
.*******************************************************************************
.*                              ML0000                    CALLED BY :     0000 *
.*                   Controlling Logic (Mainline Code)                         *
.*******************************************************************************
ML0000    CALL      INIT0000                 * Initialization and open files
.
ML0100    CALL      OPTN0000                 * Select options
          BRANCH    EXIT,ML9999              * Exit = 1 if 0 chosen in menu
          BRANCH    OPTION,ML1000            
.
ML1000    CALL      KDIS0000                 * Keyin disaster id
          CALL      KHOS0000                 * Keyin hospital id
          CALL      KVIS0000                 * Keyin visit type
.
          CALL      CONTQST                  * Ok to Continue ?
          BRANCH    CEXIT,ML1100,ML1000,ML0100  *Yes, No, Cancel
ML1100    CALL      PROC0000                 * Yes. Process each record
          GOTO      ML0100                   * Return for next option
.
ML9999    CHAIN     PGM
+
.*******************************************************************************
.*                             INIT0000                   CALLED BY :   ML0000 *
.*            Initialize variables, display heading, and open files            *
.*******************************************************************************
INIT0000  CALL      DISPHEAD                 * Display Heading
.
          DISPLAY   *P56:24,"Opening ";
.
          DISPLAY   *P64:24,"controlf"       * Open CONTROLF
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND12;*104,PTCNWBDS
.
          DISPLAY   *P64:24,"patcodes"       * Open PATCODE1
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patmi1af"       * Open PATMI1A2
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af"       * Open PATMA1A1
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patwr1af"       * Open PATWR1A4
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
.
          DISPLAY   *P64:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsccdaf"       * Open PMSCCDA1
          OPEN      PMSCCDA1,"pmsccdaf"
.
          DISPLAY   *P64:24,"pmsevtaf";
          OPEN      PMSEVTA1,"pmsevtaf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
. 
          DISPLAY   *P64:24,"dismasaf";
          OPEN      DISMASA1,"dismasaf"
.
          DISPLAY   *P64:24,"dispataf";
          OPEN      DISPATA1,"dispataf"
.
          DISPLAY   *P64:24,"dispataf";
          OPEN      DISPATA2,"dispataf"
.
          DISPLAY   *P64:24,"disptlaf";
          OPEN      DISPTLA1,"disptlaf"
          OPEN      DISPTLA2,"disptlaf"
.
          OPEN      PATDO1A1,"patdo1af"
          OPEN      BOKDIAA1,"bokdiaaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      WATTR1A1,"wattr1af"
.
          MOVE      "out",CFILEPRE          * Save Current File Prefix
          CALL      OPNOUT00                * Open Outpatient Files
.         
INIT9999  RETURN
+
.*******************************************************************************
.*                             OPTN0000                   CALLED BY :   ML0100 *
.*                         Obtain the options                                  *
.*******************************************************************************
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Produce Report" 
          KEYIN     *P1:7,"Please Select : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL        * Exit
.
          BRANCH    OPTION,OPTN8000
.
          BEEP                               * Error : Invalid Option
          GOTO      OPTN0000
.
OPTN8000  MOVE      FALSE,EXIT               * Set Exit = 0
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT                * Set Exit = 1
.
OPTN9999  RETURN
+
.*******************************************************************************
.*                             PROC0000                   CALLED BY :   ML1000 *
.*******************************************************************************
PROC0000  DISPLAY   *P1:24,"Printing Admission No. : ";
          CALL      IBACLOCK 
          CALL      PHED0000
          PACK      KEY25,DISACODE,SP70
          CALL      RSDSPTL2                 * Positional read
PROC0010  CALL      RKDSPTL2                 * Read next record
          BRANCH    OVRCD,PROC9000           * End of file
.
          DISPLAY   *P26:24,*EL,DSPLURNO,SP1,DSPLVISN,SP1,DSPLCODE
.
.         ********  INSERT PRINT DETAIL LINE HERE **********
.
          PACK      KEY17,DSPLURNO,DSPLCODE,SP70
          CALL      RDDSPAT1
          BRANCH    OVRCD,PROC0010
.
          MATCH     SP70,DISACODE                             * Disaster Filter
          IF        !@EQUAL & !@EOS
            PACK      DISACODE,DISACODE,SP70
            PACK      DSPLCODE,DSPLCODE,SP70
            MATCH     DISACODE,DSPLCODE
            GOTO      PROC0010 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,DSPLURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PROC0010
.
          MOVE      SP70,KEY3                                   * Folder Colour
          PACK      KEY8,DSPLURNO,SP70
          CALL      RDPMPX21
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * Patients name
.
          MOVE      SP70,DIM20DIS 
          PACK      KEY9,DSPLCODE,SP70
          CALL      RDDSMAS1
          IF        OVRCD=1
            CALL      CLRDIS00
          ELSE
            MOVE      DSMADESC,DIM20DIS
          ENDIF
.
          MOVE      SP70,DIM3TYP                                         * Type
          MOVE      SP70,DIM20HOS                                    * Hospital
          MOVE      SP70,VISTLOC                                     * Location
          MOVE      SP70,UNITCODE                                        * Unit
          MOVE      SP70,UNITDESC
          MOVE      SP70,DOCFNAME                                      * Doctor
          MOVE      SP70,ADIADESC                                   * Diagnosis
          MOVE      SP70,VISTDESC                                  * Visit Type
.
.-------------------------------------------------------------------------------
.
          PACK      KEY8,DSPLVISN,SP70                       * Check Inpatients
          CALL      RDPTVIS1
          IF        OVRCD=0
            PACK      KEY8,DSPLVISN,SP70
            CALL      RDPMVX11
            IF        OVRCD=0
.                                                                  * Visit Type
              IF      PVITYPE=ONE
                MOVE    "EMG",DIM3TYP
              ENDIF
              IF      PVITYPE=TWO
                MOVE    "OP ",DIM3TYP
              ENDIF
              IF      PVITYPE=THREE
                MOVE    "IP ",DIM3TYP
              ENDIF
              IF      PVITYPE=FOUR
                MOVE    "FIN",DIM3TYP
              ENDIF
              IF      PVITYPE=FIVE
                MOVE    "DAY",DIM3TYP
              ENDIF
              IF      PVITYPE=SIX
                MOVE    "COM",DIM3TYP
              ENDIF
              IF      PVITYPE=SEVEN
                MOVE    "WRD",DIM3TYP
              ENDIF
              IF      PVITYPE=EIGHT
                MOVE    "ADH",DIM3TYP
              ENDIF
              IF      PVITYPE=NINE
                MOVE    "OTH",DIM3TYP
              ENDIF
.
              LOAD    VISTDESC,PVITYPE,VISTEMER,VISTOUTP,VISTINPT
              COMPARE SIX,PVITYPE
              IF      @EQUAL
                MOVE    VISTCOMH,VISTDESC
              ENDIF
.
              MATCH   SP70,PVIDOCT
              IF      !@EQUAL & !@EOS
                MOVE      PVIDOCT,KEY6
                CALL      RDDOCT1
                IF        OVRCD=0
.                 CALL      DOCNAM00             * format doctors name
                  MOVE      DTITL,FMTTITLE            * I CAR 13038
                  MOVE      DGNAME,FMTGNAME
                  MOVE      DSNAME,FMTSNAME
                  CALL      DOCNM000
                ENDIF
              ENDIF
.
              IF      IBCNMHOS=1
                MATCH   SP70,PMVXMHOS
                IF      !@EQUAL & !@EOS
                  PACK    KEY3,PMVXMHOS,SP70
                  CALL    RDPTHSP1
                  IF      OVRCD=0
                    MOVE    PTHSNAME,DIM20HOS                        * Hospital
                  ENDIF
                ENDIF
.
                MATCH   SP70,FILTHOSP                         * Hospital Filter
                IF      !@EQUAL & !@EOS
                  PACK    FILTHOSP,FILTHOSP,SP70
                  MATCH   PMVXMHOS,FILTHOSP
                  GOTO    PROC0010 IF NOT EQUAL
                ENDIF
              ENDIF
.
              PACK    KEY8,DPVIBILL,SP70
              CALL    RDMISS1
              IF      OVRCD=0
                PACK      VISTLOC,AWARD,SP70                         * Location
.
                MATCH     "1",PTCNWBDS
                IF        @EQUAL
                  PACK      KEY6,AWARD,SP70
                  CALL      RDWARD1
                  IF        OVRCD=0
                    MOVE      WBDESC,VISTLOC
                  ENDIF
                ENDIF
.
                MATCH     "2",PTCNWBDS
                IF        @EQUAL
                  PACK      KEY6,AWARD,SP70
                  CALL      RDWARD1
                  IF        OVRCD=0
                    MOVE      PTWDSDES,VISTLOC
                  ENDIF
                ENDIF
.
                PACK      UNITCODE,ACLSSFT,SP70                          * Unit
.
                MATCH     SP70,ACLSSFT
                IF        !@EQUAL & !@EOS
                  PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
                  CALL      RDCODE1
                  IF        OVRCD=0
                    MOVE      TDESC,UNITDESC 
                  ENDIF
                ENDIF
.
                MOVE      ADIAG1,ADIADESC                           * Diagnosis
.
                MATCH     SP70,ADOCTA                                  * Doctor
                IF        !@EQUAL & !@EOS
                  PACK      KEY10,ADOCTA,SP70
                  CALL      RDPMHCP1
                   IF        OVRCD=0
                    MOVE      PMHCTITL,FMTTITLE
                    MOVE      PMHCGNAM,FMTGNAME
                    MOVE      PMHCSNAM,FMTSNAME
                    CALL      DOCNM000
                  ENDIF
                ENDIF
.
                IF      PVITYPE=3
                  LOAD    VISTDESC,ASTAT,INPSTAT1,INPSTAT2,INPSTAT3,INPSTAT4:
                                         INPSTAT5
                ENDIF
.
              ENDIF
.
              PACK    KEY8,DPVIBILL,SP70
              CALL    RDPMEVT1
              IF      OVRCD=0
                PACK      UNITCODE,PMEVUNIT,SP70                         * Unit
.
                MATCH     SP70,PMEVUNIT
                IF        !@EQUAL & !@EOS
                  PACK      KEY5,ANSA,ANSC,PMEVUNIT,SP70
                  CALL      RDCODE1
                  IF        OVRCD=0
                    MOVE      TDESC,UNITDESC
                  ENDIF
                ENDIF
.
                MOVE      "EV",CATEGORY
                MOVE      PMEVEVNT,CODE
                PACK      KEY5,CATEGORY,CODE,SP70
                CALL      RDCODE1
                IF        OVRCD=1
                  MOVE      SP70,TCINDC1
                ENDIF 
.
                MATCH     "I",TCINDC1                         * Inpatient Event
                IF        @EQUAL
                  PACK      VISTLOC,PMEVPWRD,SLASH,PMEVPBED,SP70     * Location
.
                  MATCH     SP70,PMEVPWRD
                  IF        !@EQUAL & !@EOS
                    MATCH     "1",PTCNWBDS
                    IF        @EQUAL
                      PACK      KEY6,PMEVPWRD,PMEVPBED,SP70
                      CALL      RDWARD1
                      IF        OVRCD=0
.                       MOVE      WBDESC,VISTLOC
                        PACK      VISTLOC,PMEVPWRD,SLASH,WBDESC,SP70
                      ENDIF
                    ENDIF
.
                    MATCH     "2",PTCNWBDS
                    IF        @EQUAL
                      PACK      KEY6,PMEVPWRD,PMEVPBED,SP70
                      CALL      RDWARD1
                      IF        OVRCD=0
.                       MOVE      WBDESC,VISTLOC
                        PACK      VISTLOC,PMEVPWRD,SLASH,PTWDSDES,SP70
                      ENDIF
                    ENDIF
                  ENDIF
.
                ENDIF
.
                MATCH     "O",TCINDC1                        * Outpatient Event
                IF        @EQUAL
                  PACK      VISTLOC,PMEVPLOC,SP70                    * Location
                  PACK      UNITCODE,PMEVCTYP,SP70                       * Unit
                  MOVE      SP70,UNITDESC
                ENDIF
.
                MATCH     SP70,PMEVACON                                * Doctor
                IF        !@EQUAL & !@EOS
                  PACK      KEY10,PMEVACON,SP70
                  CALL      RDPMHCP1
                  IF        OVRCD=0
                    MOVE      PMHCTITL,FMTTITLE
                    MOVE      PMHCGNAM,FMTGNAME
                    MOVE      PMHCSNAM,FMTSNAME
                    CALL      DOCNM000
                  ENDIF
                ENDIF
.
              ENDIF
.
            ENDIF
            GOTO    PROC0020
          ENDIF
.
.-------------------------------------------------------------------------------
.
          PACK      KEY8,DSPLVISN,SP70                         * Check Bookings
          CALL      RDBKREC1
          IF        OVRCD=0
            MOVE      "BOK",DIM3TYP
            MOVE      "Booking",VISTDESC
            PACK      UNITCODE,BKREDEPT,SP70                             * Unit
.
            MATCH     SP70,BKREDEPT
            IF        !@EQUAL & !@EOS
              PACK      KEY5,ANSA,ANSC,BKREDEPT,SP70
              CALL      RDCODE1
              IF        OVRCD=0
                MOVE      TDESC,UNITDESC
              ENDIF
            ENDIF
.
            PACK      KEY8,BKREBOOK,SP70
            CALL      RDBKRX11
            IF        OVRCD=0
              IF        IBCNMHOS=1
                MATCH     SP70,BKRXPOWD
                IF        !@EQUAL & !@EOS
.
                  PACK      KEY6,BKRXPOWD,SP70
                  CALL      RDWARD1
                  IF        OVRCD<>1
                    PACK      DIM20HOS,PTWDHOSN,SP70
.
                    PACK      KEY3,PTWDHOSN,SP70
                    CALL      RDPTHSP1
                    IF        OVRCD=0
                     MOVE      PTHSNAME,DIM20HOS                     * Hospital
                    ENDIF
                  ENDIF
                ENDIF
.
                MATCH   SP70,FILTHOSP                         * Hospital Filter
                IF      !@EQUAL & !@EOS
                  PACK    FILTHOSP,FILTHOSP,SP70
                  MATCH   PTWDHOSN,FILTHOSP
                  GOTO    PROC0010 IF NOT EQUAL
                ENDIF
              ENDIF
.
              PACK      VISTLOC,BKRXPOWD,SP70                        * Location
.
              MATCH     SP70,BKRXPOWD
              IF        !@EQUAL & !@EOS
                MATCH     "1",PTCNWBDS
                IF        @EQUAL
                  PACK      KEY6,BKRXPOWD,SP70
                  CALL      RDWARD1
                  IF        OVRCD=0
                    MOVE      WBDESC,VISTLOC
                  ENDIF
                ENDIF
.
                MATCH     "2",PTCNWBDS
                IF        @EQUAL
                  PACK      KEY6,BKRXPOWD,SP70
                  CALL      RDWARD1
                  IF        OVRCD=0
                    MOVE      PTWDSDES,VISTLOC
                  ENDIF
                ENDIF
              ENDIF
.
              MATCH     SP70,BKREADOC                                  * Doctor
              IF        !@EQUAl & !@EOS
                PACK      KEY6,BKREADOC,SP70
                CALL      RDDOCT1
                IF        OVRCD=0
                  MOVE      DTITL,FMTTITLE
                  MOVE      DGNAME,FMTGNAME
                  MOVE      DSNAME,FMTSNAME
                  CALL      DOCNM000
                ENDIF
              ENDIF
.
              PACK      KEY18,BKREBOOK,SP70
              CALL      RSBKDIA1
              CALL      RKBKDIA1
              IF        OVRCD=0
                MATCH     BKREBOOK,BKDIBOOK
                IF        @EQUAL
                  MOVE      BKDIDES1,ADIADESC                       * Diagnosis
                ENDIF
              ENDIF
.
            ENDIF
            GOTO    PROC0020
          ENDIF
.
.-------------------------------------------------------------------------------
.
          PACK      KEY19,DSPLURNO,SP20                    * Check Waiting List
          CALL      RDSTREA1
PROC0015  CALL      RDKTREA1
          IF        OVRCD=0
            MATCH     DSPLURNO,WMURNO
            GOTO      PROC0018 IF NOT EQUAL
.
            MOVE      SP70,DIM8
            MOVE      WMBOOK,DIM8
            MATCH     DSPLVISN,DIM8
            GOTO      PROC0015 IF NOT EQUAL
.
            MOVE      "WAT",DIM3TYP
            PACK      UNITCODE,WMUNIT,SP70                               * Unit
.
            MATCH     SP70,WMUNIT
            IF        !@EQUAL & !@EOS
              PACK      KEY5,ANSA,ANSC,WMUNIT,SP70
              CALL      RDCODE1
              IF        OVRCD=0
                MOVE      TDESC,UNITDESC
              ENDIF
            ENDIF
.
            MOVE      WMREASON,ADIADESC                             * Diagnosis
.
            IF        IBCNMHOS=1
.
              MATCH   SP70,FILTHOSP
              IF      !@EQUAL & !@EOS
                PACK    FILTHOSP,FILTHOSP,SP70
                MATCH   WTWMIHSP,FILTHOSP
                GOTO    PROC0010 IF NOT EQUAL
              ENDIF
.
              PACK      DIM20HOS,WTWMIHSP,SP70
.
              PACK      KEY3,WTWMIHSP,SP70
              CALL      RDPTHSP1
              IF        OVRCD=0
                MOVE      PTHSNAME,DIM20HOS                          * Hospital
              ENDIF
            ENDIF
.
            MATCH     SP70,WMDOCTOR                                    * Doctor
            IF        !@EQUAL & !@EOS
              PACK      KEY6,WMDOCTOR,SP70
              CALL      RDDOCT1
              IF        OVRCD=0
                MOVE      DTITL,FMTTITLE
                MOVE      DGNAME,FMTGNAME
                MOVE      DSNAME,FMTSNAME
                CALL      DOCNM000
              ENDIF
            ENDIF
.
            GOTO      PROC0020
          ENDIF
.
.-------------------------------------------------------------------------------
.
PROC0018  PACK      KEY36,DSPLVISN,SP70                     * Check Outpatients
          CALL      RDSBOKA6
PROC0019  CALL      RDKBOKA6
          IF        OVRCD=0
            MATCH     DOBAOUTN,DSPLVISN
            GOTO      PROC0010 IF NOT EQUAL
.
            MOVE      "OP ",DIM3TYP
.
            MOVE      OBAOUTNO,KEY8
            CALL      RDDIAG1
            IF        OVRCD=0
              MOVE      OPDIDESC,ADIADESC                           * Diagnosis
            ENDIF
.
            MOVE      OBACTYP,UNITCODE                                   * Unit
            MOVE      SP70,UNITDESC
.
            MOVE      OBAOUTNO,KEY8
            CALL      RDBOKB1
            IF        OVRCD=0
              MOVE      OTBBDEPT,UNITCODE                                * Unit
.
              MATCH     SP70,OTBBDEPT
              IF        !@EQUAL & !@EOS
                PACK      KEY5,ANSA,ANSC,OTBBDEPT,SP70
                CALL      RDCODE1
                IF        OVRCD=0
                  MOVE      TDESC,UNITDESC
                ENDIF
              ENDIF
.
            ENDIF
.
            PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT,SP70
            CALL      RDMASA1
            IF        OVRCD=1
              CALL      CLRMAS00
            ENDIF
            PACK      VISTLOC,OTMALTYP,SP70                          * Location
.
            PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
            CALL      RDSESA1
            IF        OVRCD=1
              CALL      CLRSES00
            ENDIF
.
           PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
            CALL      RDOTHED1
            IF        OVRCD=1
              CALL     CLOUTHED
            ENDIF
            PACK      VISTLOC,OTHELTYP,SP70                          * Location
.
            PACK      DIM20HOS,OTHELTYP,SP70
            COMPARE   ONE,IBCNMHOS
            IF        @EQUAL
.
              MATCH   SP70,FILTHOSP
              IF      !@EQUAL & !@EOS
                PACK    FILTHOSP,FILTHOSP,SP70
                MATCH   OTHEHOSP,FILTHOSP
                GOTO    PROC0010 IF NOT EQUAL
              ENDIF
.
              PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT,SP70
              CALL      RDMASA1
              IF        OVRCD<>1
                PACK      DIM20HOS,OTHEHOSP
.
                PACK      KEY3,OTHEHOSP,SP70
                CALL      RDPTHSP1
                IF        OVRCD=0
                  MOVE      PTHSNAME,DIM20HOS                        * Hospital
                ENDIF
.
              ENDIF
            ENDIF
.
            MATCH     SP70,OBACLIN
            IF        !@EQUAL & !@EOS
              PACK      KEY20,OBASITE,OBACLIN,OBADATE,SP70
              CALL      RDCLIA1
              IF        OVRCD=1
                CALL      RDSCLIA1
                CALL      RDPCLIA1
                BRANCH    OVRCD,PROC0020
.
                MATCH     OBASITE,OCASITE
                GOTO      PROC0020 IF NOT EQUAL
.
                MATCH     OBACLIN,OCACLIN
                GOTO      PROC0020 IF NOT EQUAL
              ENDIF
.
              MATCH     SP70,OCADOCT
              IF        !@EQUAL & !@EOS
                PACK      KEY6,OCADOCT,SP70
                CALL      RDDOCT1
                IF        OVRCD=0
                  MOVE      DTITL,FMTTITLE
                  MOVE      DGNAME,FMTGNAME
                  MOVE      DSNAME,FMTSNAME
                  CALL      DOCNM000
                ENDIF
              ENDIF
.
            ENDIF
.
            GOTO      PROC0020
.
          ENDIF
.
.-------------------------------------------------------------------------------
.
          GOTO      PROC0010
.
PROC0020  MATCH   SP70,FILTTYPE                             * Visit Type Filter
          IF      !@EQUAL & !@EOS
            PACK    FILTTYPE,FILTTYPE,SP70
            PACK    VISTDESC,VISTDESC,SP70
            MATCH   VISTDESC,FILTTYPE
            GOTO    PROC0010 IF NOT EQUAL
          ENDIF
.
          CALL      PREC0000                                * Print Detail Line
.
          ADD       ONE,CLNO                    * Add 1 to total number of adm.
          IF        CLNO >= 55
            CALL      PHED0000
          ENDIF
          GOTO      PROC0010                               * Goto print Heading
.
PROC9000  CALL      PEND0000                              * Print End of report
.
PROC9999  RETURN
+
.*******************************************************************************
.*                             PHED0000                                        *
.*                 Print the head of the report                                *
.*******************************************************************************
PHED0000  CALL      HEAD132
.
          MATCH     SP70,DISACODE
          IF        @EQUAL | @EOS
            PRINT     *40,"Disaster : All"
          ELSE
            PRINT     *40,"Disaster : ",DISACODE
          ENDIF
          ADD       ONE,CLNO
.
          IF        IBCNMHOS=1
.
            MATCH     SP70,FILTHOSP
            IF        @EQUAL | @EOS
              PRINT     *40,"Hospital : All"
            ELSE
              OPEN      PATHSPA1,"pathspaf"
.
              PACK      KEY3,FILTHOSP,SP70
              CALL      RDPTHSP1
              IF        OVRCD=0
                PRINT     *40,"Hospital : ",FILTHOSP," ",PTHSNAME
              ELSE
                PRINT     *40,"Hospital : ",FILTHOSP
              ENDIF
            ENDIF
            ADD       ONE,CLNO
.
          ENDIF
.
          MATCH     SP70,FILTTYPE
          IF        @EQUAL | @EOS
            PRINT     *40,"Visit Type : All"
          ELSE
            PRINT     *40,"Visit Type : ",FILTTYPE
          ENDIF
          ADD       ONE,CLNO
.                                                        * Line 1  
          CALL      UND132
          PRINT     *1,"|",*3,"Hospital":                * Column 1
                    *25,"|",*28,"Name":                  *        2
                    *50,"|",*53,"Disaster":              *        3
                    *75,"|",*78,"Disaster ID":           *        4
                    *100,"|",*103,"Type":                *        5
                    *110,"|",*113,"Location":            *        6
                    *132,"|" 
.                                                        * Line 2
          PRINT     *1,"|",*3,"Unit":                    * Column 1
                    *25,"|",*28,"Doctor":                *        2
                    *50,"|":
                    *75,"|":
                    *100,"|":
                    *110,"|":
                    *132,"|"
.                                                        * Line 3
          PRINT     *1,"|":                              * Column 1
                    *25,"|",*28,"U/R Number":            *        2
                    *50,"|":
                    *75,"|":
                    *100,"|":
                    *110,"|":
                    *132,"|"
          ADD       THREE,CLNO
          CALL      UND132
PHED9999  RETURN
+
.*******************************************************************************
.*                             PREC0000                                        *
.*                 Print record                                                *
.*******************************************************************************
PREC0000  PRINT     *1,"|",*3,DIM20HOS:                  * Column 1
                    *25,"|",*28,PATNAME:                 *        2
                    *50,"|",*53,DIM20DIS:                *        3
                    *75,"|",*78,DSPTDIID:                *        4
                    *100,"|",*103,DIM3TYP:               *        5
                    *110,"|",*113,VISTLOC:               *        6
                    *132,"|" 
.                                                        * Line 2
          PRINT     *1,"|",*3,UNITCODE," ",UNITDESC:     * Column 1
                    *25,"|",*28,DOCFNAME:                *        2
                    *50,"|":
                    *75,"|":
                    *100,"|":
                    *110,"|":
                    *132,"|" 
.                                                        * Line 3
          PRINT     *1,"|":                              * Column 1
                    *25,"|",*28,DSPLURNO:                *        2
                    *50,"|":
                    *75,"|":
                    *100,"|":
                    *110,"|":
                    *132,"|"
          ADD       THREE,CLNO
          CALL      UND132
PREC9999  RETURN
+
.*******************************************************************************
.*                             PEND0000                                        *
.*                 Print the end of the report                                 *
.*******************************************************************************
PEND0000  PRINT     *N,"**** End of Report ****" 
.
PEND9999  RETURN
+
.********************************************************************
.         Enter selected Hospital code
.********************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9000 IF NOT EQUAL
.
          DISPLAY   *P1:11,*EL,"Hospital: ";
.
          MOVE      "11",EVERT
          MOVE      "11",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS3000,KHOS3000
.
KHOS2000  DISPLAY   *P20:11,*V2LON,PTHSNAME;
          MOVE      KEY3,FILTHOSP
          GOTO      KHOS9999
.
KHOS3000  MOVE      "All Hospitals",PTHSNAME
          DISPLAY   *P20:11,*V2LON,PTHSNAME;
          MOVE      KEY3,FILTHOSP
.
KHOS9000  MOVE      SP10,PTHSHOSP
.
KHOS9999  RETURN
+
.*******************************************************************************
.*                             KDIS0000                   CALLED BY :   ML0100 *
.*                         Keyin the Disaster                                  *
.*******************************************************************************
KDIS0000  MOVE      SP70,DISACODE
          DISPLAY   *P1:10,*V2LON,ONE,*HOFF,"Disaster: "
          KEYIN     *P20:10,*V2LON,DISACODE
.
KDIS9999  RETURN
+
.*******************************************************************************
.*                             KVIS0000                   CALLED BY :   ML0100 *
.*                         Keyin the Visit Type                                *
.*******************************************************************************
KVIS0000  MOVE      SP70,FILTTYPE
          DISPLAY   *P1:10,*V2LON,ONE,*HOFF,"Visit Type: "
          KEYIN     *P20:10,*V2LON,FILTTYPE
.
KVIS9999  RETURN
+
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRDIS00  MOVE      SP70,DSMACODE     * Disaster Code
          MOVE      SP70,DSMADESC     * Disaster Description
          MOVE      SP70,DSMASDAT     * Disaster Start Date
          MOVE      SP70,DSMAEDAT     * Disaster End Date
          MOVE      SP70,DSMAACTV     * Disaster Status
          MOVE      SP70,DSMASPAR     * Spare
.
CLRDIS99  RETURN
.
.-----------------------------------------------------------
. Initialize's file data : Clinic Master File A
.-----------------------------------------------------------
CLRMAS00  MOVE      SP70,OMASITE
          MOVE      SP70,OMACLIN
          MOVE      SP70,OMATYP
          MOVE      SP70,DOMADAY
          MOVE      SP70,OMASTART
          MOVE      SP70,OMAFIN
          MOVE      SP70,OMAEND
          MOVE      SP70,OMACIND
          MOVE      SP70,OMAREG
          MOVE      SP70,OMAKDOC
          MOVE      SP70,OMACOM1
          MOVE      SP70,OMACOM2
          MOVE      ZERO,OMADURN
          MOVE      ZERO,OMANEWSL
          MOVE      ZERO,OMASPCSL
          MOVE      ZERO,OMASTAT
          MOVE      SP70,OMAFREQM
          MOVE      SP70,DOMASEQ
          MOVE      SP70,OMALOCK
          MOVE      ZERO,OMAFREQF
          MOVE      SP70,OMAFREQY
          MOVE      SP70,OMADATE
          MOVE      SP70,OTMAAVIS
          MOVE      SP70,OTMASTVT
          MOVE      SP70,OTMASUPX
          MOVE      SP70,OTMARUSE
          MOVE      SP70,OTMADCMB
          MOVE      SP70,OTMAIXTA
          MOVE      SP70,OTMADEFD
          MOVE      SP70,OTMAUATR
          MOVE      ZERO,OTMADDNA
          MOVE      ZERO,OTMANDRP
          MOVE      ZERO,OTMALPTR
          MOVE      ZERO,OTMALGPD
          MOVE      ZERO,OTMALPTD
          MOVE      ZERO,OTMABKLT
          MOVE      ZERO,OTMARALT
          MOVE      ZERO,OTMARSLT
          MOVE      SP70,OTMACLSE
          MOVE      SP70,OTMAHOSP
          MOVE      SP70,OTMACLOC
          MOVE      SP70,OTMAWDCL
          MOVE      SP70,OTMAWARD
          MOVE      SP70,OTMALTYP
          MOVE      SP70,OTMADFSL
          MOVE      SP70,OTMADFGN
          MOVE      SP70,OTMAGRPS
          MOVE      SP70,DOTMASOV
          MOVE      SP70,DOTMANOO
          MOVE      SP70,DOTMASLF
          MOVE      SP70,DOTMASLR
          MOVE      SP70,OTMASBKL
          MOVE      SP70,OTMASAPL
          MOVE      SP70,OTMASPMM
          MOVE      SP70,OTMASGPM
          MOVE      SP70,DOTMALPP
          MOVE      SP70,DOTMASRL
          MOVE      SP70,DOTMANCO
          MOVE      SP70,OTMALOCN
          MOVE      SP70,OTMADTCO
          MOVE      SP70,OTMADTCC
          MOVE      SP70,OTMAINS1
          MOVE      SP70,OTMAINS2
          MOVE      SP70,OTMAINS3
          MOVE      SP70,OTMALSER
          MOVE      SP70,OTMAUMCI
          MOVE      SP70,OTMAPROV
          MOVE      SP70,OTMAMBST
          MOVE      SP70,OTMAROOM
          MOVE      SP70,OTMALEDG
          MOVE      SP70,OTMACCTR
          MOVE      SP70,OTMAMPRC
          MOVE      SP70,OTMAMBSA
          MOVE      SP70,OTMALDNA
          MOVE      SP70,OTMAADEP
          MOVE      SP70,OTMACENC
          MOVE      SP70,OTMACCAR
          MOVE      SP70,OTMACTRN
          MOVE      SP70,OTMADPSL
          MOVE      SP70,OTMACPMI
          MOVE      SP70,OTMACPAG
          MOVE      SP70,OTMACTEL
          MOVE      SP70,OTMANMDS
          MOVE      SP70,OTMACART
          MOVE      SP70,OTMASRVD
          MOVE      SP70,OTMACLHR
          MOVE      SP70,OTMAMLTD
          MOVE      SP70,OTMAXPUL
          MOVE      SP70,OTMAMREF
          MOVE      SP70,OTMATCOD
          MOVE      SP70,OTMAHCLI
          MOVE      SP70,OTMAHCAI
          MOVE      SP70,OTMAHCAI
          MOVE      SP70,OTMAAOPN
          MOVE      SP70,OTMAAOFR
          MOVE      SP70,OTMAAOWM
          MOVE      SP70,OTMAAOWA
          MOVE      SP70,OTMAHCP1
          MOVE      SP70,OTMAHCP2
          MOVE      SP70,OTMAHCP3
          MOVE      SP70,OTMAHCP4
          MOVE      SP70,OTMAPCOD
          MOVE      SP70,OTMASPAR
.
CLRMAS99  RETURN
+
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRSES00  MOVE      SP70,OSESITE       * SITE CODE
          MOVE      SP70,OSECLIN       * CLINIC ID
          MOVE      SP70,OSEDATE       * USAGE DATE (CCYYMMDD)
          MOVE      SP70,OSESTRT       * SESSION START TIME (HH:MM)
          MOVE      SP70,DOSEDAY       * DAY INDICATOR e.g. 1 = MON
          MOVE      ZERO,OSESLOTS      * TOTAL SLOTS SCHEDULED
          MOVE      ZERO,OSESLOTB      * TOTAL SLOTS BOOKED
          MOVE      SP70,OSETIMES      * TOTAL TIME SCHEDULED (HH:MM)
          MOVE      SP70,OSETIMEU      * TOTAL TIME USED      (HH:MM)
          MOVE      ZERO,OSEBNEW       * NUMBER OF NEW PATIENTS
          MOVE      ZERO,OSEBRV        * NUMBER OF RE-VISITING PATIENTS
          MOVE      ZERO,OSEBSPC       * NUMBER OF SPECIAL PATIENTS
          MOVE      ZERO,OSEANEW       * NUMBER OF NEW PATIENTS
          MOVE      ZERO,OSEARV        * NUMBER OF RE-VISITING PATIENTS
          MOVE      ZERO,OSEASPC       * NUMBER OF SPECIAL PATIENTS
          MOVE      ZERO,OSESSLTR      * REVIEW SLOTS SCHEDULED
          MOVE      ZERO,OSESSLTN      * NEW SLOTS SCHEDULED
          MOVE      ZERO,OSESSLTS      * SPECIAL SLOTS SCHEDULED
          MOVE      SP70,OSESCSTT      * CLINIC STATUS
          MOVE      SP70,OSESCOMM
          MOVE      SP70,OSESCTYP      * CLINIC TYPE
          MOVE      SP70,OSESSHNO      * SCHEDULE NUMBER
          MOVE      SP70,OSESSDAT      * SCHEDULE DATE (CCYYMMDD)
          MOVE      SP70,OSESPARE      * SPARE
.
CLRSES99  RETURN
.
.------------------------------------------------------------
. Open Outpatient Files
.------------------------------------------------------------
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBOKA3  * Get the name of the BOKA3 file
          CLOSE     OUTBOKA3
          OPEN      OUTBOKA3,CFNAME           * Open the file
          CLOSE     OUTBOKA2
          OPEN      OUTBOKA2,CFNAME           * Open the file
          CLOSE     OUTBOKA6
          OPEN      OUTBOKA6,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILRSHA1
          CLOSE     OUTRSHA1
          OPEN      OUTRSHA1,CFNAME
.
          PACK      CFNAME,CFILEPRE,FILCANA2
          CLOSE     OUTCANA2
          OPEN      OUTCANA2,CFNAME
.
          PACK      CFNAME,CFILEPRE,FILBB1A1  * Get the name of the BOKA3 file
          CLOSE     OUTBB1A1
          OPEN      OUTBB1A1,CFNAME
.
          PACK      CFNAME,CFILEPRE,FILSESA1  * Get the name of the SESA1 file
          CLOSE     OUTSESA1
          OPEN      OUTSESA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCLIA1  * Get the name of the CLIA1 file
          CLOSE     OUTCLIA1
          OPEN      OUTCLIA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCTYA1  * Get the name of the CTYA1 file
          CLOSE     OUTCTYA1
          OPEN      OUTCTYA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILMA1A1
          CLOSE     OUTMA1A1
          OPEN      OUTMA1A1,CFNAME
.
          PACK      CFNAME,CFILEPRE,FILHEDA1
          CLOSE     OUTHEDA1
          OPEN      OUTHEDA1,CFNAME
.
          PACK      CFNAME,CFILEPRE,FILDIAG1
          CLOSE     OUTDIAG1
          OPEN      OUTDIAG1,CFNAME
.
OPNOUT99  RETURN
.
.         I/O INCLUDES
.
          INC       STDGENCD
          INC       PATHSPKY
          INC       BOKDIAIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       DISMASIO/INC
          INC       DISPATIO/INC
          INC       DISPTLIO/INC
          INC       PATCODIO/INC            * Patient Codes File
          INC       PATDO1IO/INC
          INC       PATHSPIO/INC            * hospital file
          INC       PATMA1IO/INC            * Patient Master File
          INC       PATMI1IO/INC            * Patient Admission File
          INC       PATWR1IO/INC            * Patient Ward/Bed File
          INC       PATVISIO/INC
          INC       PMSCCDIO/INC            * Concession Card Details File
          INC       PMSEVTIO/INC
          INC       PMSHCPIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC            * Visit Extension file
          INC       OUTBOAIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTHEDIO/INC
          INC       OUTCANIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDIAIO/INC
          INC       OUTMA1IO/INC
          INC       OUTRSHIO/INC
          INC       OUTSESIO/INC
          INC       WATTR1IO/INC
          INC       MLTCODIO/INC
          INC       PATDOCCD
          INC       PATDOCTM
          INC       PATCODTM
          INC       CLOUTHED
          INC       NAMSTRCD
+

.***************************************************************************
.* System    :   Health Fund Table Mainteneance                            *
.* Program   :   FXPATKHF.PBL                                              *
.* Desc      :   Program to Create the New Key Word File                   *
.***************************************************************************
.* Date      :   29/06/1999                                                *
.* Author    :   Pat Dirito                                                *
.* Function  :   This program will loop thru the Health Fund Tables and    *
.*               calls the procedure to update the Key Word Index for      * 
.*               every Table                                               *
.*=========================================================================*
.*                                                                         *    
.*               V9.03.01  Tracey Nguyen CAR 40588                         *
.*                         Allowed the inclusion of the health fund header *
.*                         record in the keyword search file.              *
.*=========================================================================*
.*               V9.02.01  Jill Habasque                                   *
.*                         Changed from key10 to key14                     *
.*               V5.07.01  Nicole Harrington                               *
.*                         Recompiled for patkhfaf                         *
.*               V5.07.00  Pat Dirito                                      *
.*                   Ported from 6.06                                      * 
.*                                                                         * 
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCODFD/INC   * Codes Table File
          INC       PATFN1FD/INC   * Patient Health Fund File 
          INC       PATKHFFD/INC   * Health Fund Tables Key Words
.
RECNUM    FORM      6
ZERO8     INIT      "0000    "
CODE      DIM       3
CATEGORY  DIM       2    
SAVKEY14  DIM       14
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
.----------------------------------------------------------------------
PRGID     INIT      "FXPATKHF"
PRGNAM    INIT      "Create Health Fund Table Key Word Index"
VERSION   INIT      "V12.01.00"
.----------------------------------------------------------------------
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD         * display heading
          OPEN      PATFN1A1,"patfn1af"   * Open Health Fund File          
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Create Key Word Index" 
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Creating Key Word Index ..."
          MOVE      ZERO,RECNUM
          PACK      KEY14,SP70
          CALL      RDSFUND1
PROC1000  CALL      RDKFUND1
          BRANCH    OVRCD,PROC9999
...>>          MATCH     ZERO8,HFTABL
...>>          GOTO      PROC1000 IF EQUAL
          PACK      SAVKEY14,HCODE,HFTABL,SP70  * Validate Record on File
.
          MOVE      SP70,TDESC
          MOVE      "HT",CATEGORY 
          MOVE      PTHFBCAT,CODE
          MATCH     SP70,CODE
          GOTO      PROC1100 IF EQUAL
          PACK      KEY5,CATEGORY,CODE
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
.
PROC1100  PROC      PATKHFUP
.
          MOVE      SAVKEY14,KEY14
          CALL      RDFUND1
.
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY      *P40:24,*V2LON,RECNUM
          ENDIF
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       PATCODIO/INC   * IO Codes Table
          INC       PATFN1IO/INC   * Patient Health Fund File
          INC       PATKHFIO/INC   * Health Fund Tables Key Words 
          INC       PATKHFPR       * Key Word search Proc: Health Fund Tables 

          INC       STD001IO
.

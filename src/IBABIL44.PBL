.***************************************************************************
.* System    :   Patient Management                                        *
.* Program   :   IBABIL44                                                  *
.* Desc      :   Clear Extraction Files                                    *
.***************************************************************************
.* Date      :   03/11/1986                                                *
.* Author    :   Malcolm Hayes                                             *
.* Function  :   This program will clear the selected extract files        *
.* Mods      :                                                             *
.*        V10.04.01  26/02/2014  Ania P         CAR 294994                 *
.*                   Changed KEY11 to KEY12 before calls to xxPTEXC1       *
.*        V10.03.01  01/08/2013  Ebon Clements  CAR 289242                 *
.*                   Fixed delete of PATEXCFD - Unique counter length issue*
.*        V10.00.01  Mike Laming   CAR 157391                              *
.*                   Recompiled for changes to PATEX1FD.                   *
.***************************************************************************
.*        V9.08.01   07/03/2006  Steve Armstrong  CAR 134788               *
.*                   Modify program to clear extract files                 *
.*                   instead of iserasing them.                            *
.***************************************************************************
.*        V9.04.01   10/10/2005  Mike Laming  CAR 78671                    *
.*                   Modify program to run as scheduled from Web           *
.***************************************************************************
.*        V5.07.02   15/10/1999  Steve Armstrong                           *
.*                   Removed reference to CSTRTYR (not used)               *
.*        V5.07.01   06/05/1998   J.Tan                                    *
.*                   Changed record length of patextbf file                *
.*        V5.07.B01  12/11/1998   Jim Stathopoulos                         *
.*                   507 Changes                                           *
.***************************************************************************
.*        V5.05.01   06/01/1998   Nick  SRF 643441                         *
.*                   added CLRT0000 to clear the extract                   *
.*                   file prior to erasing it, just in                     *
.*                   case ORABLE can't manage it                           *
.***************************************************************************
.*        V5.01.06   28/09/1994  bez              SRF 131952               *
.*                   Fixed  F * I                                          *
.*        V5.01.05   15/07/93  WHIRLPOOL        SRF 123030                 *
.*                   Dudley postcode changes                               *
.*        V5.01.04   15/01/93 J.Tan  SRF 119867                            *
.*                   Recompiled for PATEXCIO                               *
.*        V5.01.03   14/05/91 DIG                                          *
.*                   Changed to use iserase for                            *
.*                   the new extraction files                              *
.*        V5.01.01   06/07/89  S. O'Gorman                                 *
.*                   Converted for N.Z.                                    *
.*        V5.01      10/06/89 S.Barcham                                    *
.*                   Make remove                                           *
.*                   SRF 100910 & 101010                                   *
.***************************************************************************
.*        W2.02      TOTAL AMOUNTS CLEARED NOW                             *
.*        W2.01      NEW EXTRACTION ..ETRANS                               *
.*        W2.00      M.HAYSE (IBA) NEW DISPLAYS                            *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCONFD/INC
          INC       PATEX1FD/INC
          INC       PATEXBFD/INC
          INC       PATEXCFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CNEXTINV  FORM      8
.
EXCDATE   DIM       10
EXFDATE   DIM       10
EXTDATE   DIM       10
.
FILEDIM8  DIM       8
.
SEC       FORM      2
.
VERT      FORM      2
.
.
. PROGRAM CONSTANTS
. -----------------
PATEXB    INIT      "patexb"
PATEXC    INIT      "patexc"
.
.
PRGID     INIT      "IBABIL44"
PRGNAM    INIT      "Clear Extraction Files"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAIN0000                                  *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,"Opening":
                    *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                               GFIL0000              Called by: MAIN0000   *
.*             Get extraction file to be cleared                             *
.*****************************************************************************
.
PROC0000  CALL      GEXTN                        * get extraction file
.         
          COMPARE   ZERO,OPTION                  * finished ?
          GOTO      PROC9999 IF EQUAL            * yes
.
PROC0500  KEYIN     *P1:24,*EL," Ok to remove Extraction File (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,"/":
                    *V2LON,"E",*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     ANSE,ANS
          GOTO      PROC9999 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      PROC0000 IF EQUAL
.
          MATCH     ANSY,ANS
          GOTO      PROC5000 IF EQUAL
.
          BEEP
          GOTO      PROC0500
.
.         CHECK IF TO DATE > FROM DATE
.
PROC5000  MOVE      ZERO,EDOPERS
          CALL      ETABPOS
          WRITAB    CONTROLF,SEC;*ETABPS,SP8,SP8,SP8,EDOPERS
.
          CALL      ETOTPOS
          MOVE      ZERO,EADMNS
          MOVE      ZERO,EBDAYS
          MOVE      ZERO,EDCASES
          MOVE      ZERO,ENETAMT
          MOVE      ZERO,ETPRIMOP
          MOVE      ZERO,ETPRIMDS
          WRITAB    CONTROLF,SEC;*ETABPS,EADMNS,ETPRIMOP,ETPRIMDS:
                                  EBDAYS,EDCASES,ENETAMT
.
          PRINT     *1,"IBABIL44 - Remove Management Extract files",*N
.
          CALL      CLRT0000                * Clear the extraction file A
          GOTO      PROC0000                * get next extraction file
.
PROC9999  RETURN
+
.******************************************************************
.*                            CLRT0000                            *
.*                   Clear extraction files                       *
.* Requires: OPTION - Management generation option selected       *
.* Returns:  EXIT   0 = Ok to continue                            *
.*                  1 = error opening extraction files            *
.******************************************************************
.
CLRT0000  MOVE      OPTION,DIM2
          REP       " 0",DIM2
.
.         Open extraction file A
.
          PACK      FILEDIM8,FILEX1A1,DIM2
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATEX1A1,FILEDIM8            * file opened ?
          TRAPCLR   IO
          BRANCH    OVRCD,CLRT9100               * no - error
.
.         Clear extraction file A records
.
CLRT2500  MOVE      SP10,KEY8
          CALL      RSPTEXA1                     * position at start of file
          CALL      RKPTEXA1                     * read next record
          BRANCH    OVRCD,CLRT4000               * eof - clear next file
.
          MOVE      PTXAADMN,KEY8
          CALL      DEPTEXA1                     * delete record
          GOTO      CLRT2500                     * get next record
.
.         Open extraction file B
.
CLRT4000  PRINT     *5,FILEDIM8," *** CLEARED ***"
          PACK      FILEDIM8,PATEXB,DIM2
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATEXTB1,FILEDIM8            * file opened ?
          TRAPCLR   IO
          BRANCH    OVRCD,CLRT9100               * no - error
.
.         Clear extraction file B records
.         
CLRT4500  MOVE      SP20,KEY17
          CALL      RSPTEXB1                     * position at start of file
          CALL      RKPTEXB1                     * read next record
          BRANCH    OVRCD,CLRT6000               * eof - clear next file
.         
          PACK      KEY17,DPTXBADM,PTXBTDAT,PTXBTYPE
          CALL      DEPTEXB1                     * delete record
          GOTO      CLRT4500                     * get next record
.         
.         Open extraction file C
.         
CLRT6000  PRINT     *5,FILEDIM8," *** CLEARED ***"
          PACK      FILEDIM8,PATEXC,DIM2
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATEXTC1,FILEDIM8            * file opened ?
          TRAPCLR   IO
          BRANCH    OVRCD,CLRT9100               * no - error
.         
.         Clear extraction file C records
.         
CLRT6500  MOVE      SP20,KEY12
          CALL      RSPTEXC1                     * position at start of file
          CALL      RKPTEXC1                     * read next record
          BRANCH    OVRCD,CLRT9000               * eof - finished
.
          PACK      KEY12,PTXCADMN,PTXCTYPE,DPTXCUNQ
          CALL      DEPTEXC1                     * delete record
          GOTO      CLRT6500                     * get next record
.
CLRT9000  PRINT     *5,FILEDIM8," *** CLEARED ***"
          MOVE      ZERO,EXIT
          GOTO      CLRT9999
.
CLRT9100  PRINT     *1,"Extraction file ",FILEDIM8," not found."
          MOVE      ONE,EXIT
.
CLRT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.         
          INC       PATGEXT     
          INC       PATEX1IO/INC
          INC       PATEXBIO/INC
          INC       PATEXCIO/INC

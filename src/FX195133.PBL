.*****************************************************************************
.* System    :   Allied Health System                                        *
.* Program   :   FX195133                                                    *
.* Desc      :   Reset all allhdtaf records                                  *
.*****************************************************************************
.* Date      :   15/07/1997                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through all the allhdtaf records     *
.* Mods      :                                                               *
.*       V10.14.01 17/04/2019  Steve Armstrong  TSK 0868837                  *
.*                 Recompiled for changes to ALLHDTFD.                       *
.*****************************************************************************
.*       V10.04.01 15/05/2014  Steve Armstrong   CAR 285465                  *
.*                 Recompiled for changes to ALLHDTFD.                       *
.*****************************************************************************
.*       V10.03.01 05/12/2012  Steve Armstrong   CAR 272756                  *
.*                 Recompiled for changes to ALLHDTFD.                       *
.*                 Also changed SACS to VINAH.                               *
.*                 Also changed to delete records created by the extract     *
.*                 program.                                                  *
.*****************************************************************************
.*       V10.00.02 28/05/2010  Steve Armstrong   CAR 220289                  *
.*                 Recompiled for changes to ALLHDTFD                        *
.*       V10.00.01 22/04/2010  Steve Armstrong   CAR 220289                  *
.*                 Recompiled for changes to ALLHDTFD                        *
.*****************************************************************************
.*        V9.10.01 30/04/2009  Steve Armstrong  CAR                          *
.*                 Cleared batch number (ALHDBNUM).                          *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLHDTFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
DELCOUNT  FORM      10
UPDCOUNT  FORM      10
.
PRGID     INIT      "FX195133"
PRGNAM    INIT      "Reset All VINAH Extraction Records"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allhdtaf";
          OPEN      ALLHDTA1,"allhdtaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run Fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        For all records in allhdtaf where the status of the record         *
.*        is six or greater (minimum of extracted), reset the status         *
.*        back to waiting (zero).  Also, delete any PMI related records      *
.*        (A04, A08 or A40).                                                 *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,UPDCOUNT                * initialise record count
          DISPLAY   *P1:24,*EL,"Processing:";
.
PROC0500  PACK      KEY42,SP1,SIX,SP30,SP20
          CALL      RSALHDT1                     * position on extracted status
          CALL      RKALHDT1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,ALHDSTAT,ALHDURNO,ALHDVISN:
                                       ALHDCONT,ALHDDTTM,ALHDRTYP,ALHDACTN
.
.         A record has been found with a status of 6 or greater, so
.         determine whether to reset the status or delete the record.
.
.         If this is a message where the source was VINAHEXT, delete it.
.
          COMPARE   ONE,ALHDSRCE                 * created by extract program ?
          GOTO      PROC5000 IF EQUAL            * yes - delete
.
.         Reset the status back to waiting (zero)
.
          ADD       ONE,UPDCOUNT                 * increment update count
.
          PACK      ALHDSTAT,SP1,ZERO            * reset status to waiting
          MOVE      SP20,ALHDBNUM                * clear batch number
          CALL      UPALHDT1                     * update record
.
          GOTO      PROC0500                     * get next record
.
.         Delete a PMI record
.
PROC5000  ADD       ONE,DELCOUNT                 * increment delete count
.
          PACK      KEY42,ALHDSTAT,ALHDURNO,ALHDVISN,ALHDCONT,ALHDDTTM:
                          ALHDRTYP,ALHDACTN
          CALL      DEALHDT1
.
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:21,*EF,*V2LON,DELCOUNT,*HOFF:
                    " PMI records deleted.":
                    *P1:22,*V2LON,UPDCOUNT,*HOFF:
                    " records reset.":
                    *P1:24,"Fixit completed.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLHDTIO/INC

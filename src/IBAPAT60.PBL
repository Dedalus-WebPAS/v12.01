. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                  *
. *              -------------  -------------                                  *
. *                                                                            *
. *     PROGRAM NAME:     IBAPAT60                                             *
. *                                                                            *
. *     FUNCTION:         CANCELLATION REPORT                                  *
. *                                                                            *
. *     DATE WRITTEN:     24/12/87                                             *
. *                                                                            *
. *     AUTHOR:           J.TAN                                                *
. *                                                                            *
. *     MODIFICATIONS:                                                         *
. *                                                                            *
. *        V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
. *                  Deleted temp file (PSTROL) after processing               *
. *        V10.10.01 09/05/2017 Ania P         CAR 261630                      *
. *                  Removed use of PORT                                       *
. *        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
. *                  Added pmsvx1af                                            *
. *        V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                  Changed Health Fund variables to be 8 chars               *
. *        V5.07.B02 29/01/1999 Jim Stathopoulos                               *
. *                  507 Mods                                                  *
. ******************************************************************************
. *        V5.01.02  23.02.93 Neeriem "I Hate Support" Dye                     *
. *                  Modify to use standard routine KEYDATE                    *
. *        V5.01.03  10.03.93 Neeriem "I Hate Support" Dye                     *
. *                  Fixed to print heading if nothing to                      *
. *                  print                                                     *
. *                                                                            *
. ******************************************************************************
.
          INC       STD001FD
.
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATCODFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         Temporary file used by report
.
TEMPFIL      IFILE     SQL, FIXED=9, KEY="u1-3"
.
.Name       Type    Size   Physical   Start
.----       ----    ----   --------   -----
XCODE       DIM      3        3         1
XTOTAL      FORM     8        5         4
.                             EOR       9
.
.               VARIABLES DEFINITIONS
.
PSTROL   FILE      ASCII, FIXED=256
FNAMER   DIM     8
FNAMEI   DIM     8
.
STATUS   FORM    1
OPCND    FORM    1
CMDLINE  DIM     50
.
.
.
XADATE          DIM     8
TOTPAT          FORM    8
.
PC              INIT    "PC"
.
XDAY1           DIM     2
XMON1           DIM     2
XYEAR1          DIM     2
XCENT1          DIM     2
.
FROMDAT         DIM     8        * FROM DATE IN (CCYYMMDD) FORMAT
TODAT           DIM     8        * TO DATE IN (CCYYMMDD) FORMAT
VERT            FORM    2
.
PRGID     INIT      "IBAPAT60"
PRGNAM    INIT      "Cancellation Report"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
.
.         START OF PROGRAM
.
START   DISPLAY     *P1:4,*EF,"Keyin From Date :":
                        *P1:6,"Keyin To   Date :"
.
.             KEYIN FROM DATE
.
KADAT1 
        MOVE        FOUR,VERT
        MOVE        ZERO,IDAY
        CALL        KEYINDTE
.
        COMPARE     ZERO,IDAY
        GOTO        ENDIT IF EQUAL
.
        PACK        FROMDAT WITH ICENT,IYEAR,IMON,IDAY
        REP         " 0",FROMDAT
.
.             Key in to date
.
KADAT2  MOVE        SIX,VERT
        CALL        KEYINDTE
.
        PACK        TODAT WITH ICENT,IYEAR,IMON,IDAY
        REP         " 0",TODAT
.
        MATCH       FROMDAT,TODAT
        GOTO        INVDATG IF LESS
        GOTO        REKEY
.
INVDATG DISPLAY    *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                   "To Date **",*W3,*P1:24,*EL:
                   *P20:6,*EL;
        GOTO       KADAT1
.
.        Check if dates are OK or not
.
REKEY   KEYIN       *P1:24,*EF," Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
        MATCH       "N" TO ANS
        GOTO        START IF EQUAL
.
        MATCH       "Y" TO ANS
        GOTO        CNTCHK IF EQUAL
.
        BEEP
        GOTO       REKEY
.
.            PROCESS START
.
CNTCHK  DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                   "** Initialising Cancellation File **",*W2,*P1:10;
        CALL       INDZD
        BRANCH     OPCND OF ENDIT
.
        MOVE        ZERO,CPAGENO
        PACK        KEY9 WITH FIVE,SP8
        DISPLAY     *P30:24,"Admission No: ",*P57:24,"Scanning : ";
.
        CALL        RDSMISS2
NXTADM  CALL        RDKMISS2
        BRANCH      OVRCD OF REPORT
.
        MOVE        SP8,XADATE
        UNPACK      ADATE,XCENT,XYEAR,XMON,XDAY
        PACK        XADATE,XCENT,XYEAR,XMON,XDAY
        REP         " 0",XADATE
.  
        DISPLAY    *P68:24,AADMNO;
.
        COMPARE     FIVE,ASTAT
        GOTO        REPORT IF NOT EQUAL
.
        MATCH       FROMDAT,XADATE
        GOTO        NXTADM IF LESS
.
        MATCH       XADATE,TODAT
        GOTO        NXTADM IF LESS
.
        DISPLAY     *P45:24,*V2LON,AADMNO;
.
        MATCH       SP3,ACANCEL
        GOTO        NXT10 IF NOT EQUAL
.
        MOVE        "UNK",ACANCEL
.
NXT10   MOVE        ACANCEL,KEY3
        CALL        RDTEMP
        BRANCH      OVRCD OF NEWREC
.
        ADD         ONE,XTOTAL
        CALL        UPTEMP
        GOTO        NXTADM
.
NEWREC  MOVE        SP3,XCODE
        MOVE        ZERO,XTOTAL
.
        MOVE        ACANCEL,XCODE
        MOVE        ONE,XTOTAL
        CALL        WRTEMP
        GOTO        NXTADM
.
.      Create an indexed file based on port
.
INDZD
        MOVE        ZERO,STATUS
.
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
.
        CALL        KILLIDZ
        PREP        PSTROL,FNAMER
        WRITE       PSTROL,SEQ;"isbuild ",FNAMEI," 9 u1-3"
        WEOF        PSTROL,SEQ
.
        MOVE        ONE,STATUS
.
        DISPLAY    *P1:3,*EF,*P1:10
        CLEAR       CMDLINE
        APPEND      "sh ",CMDLINE
        APPEND      FNAMER,CMDLINE
        APPEND      ".txt",CMDLINE
        RESET       CMDLINE
.
        EXECUTE     CMDLINE,TASKID
.
        CLOSE       PSTROL,DELETE
.
        MOVE        ZERO,OPCND
        TRAP        NOINDZ IF IO
        OPEN	    TEMPFIL,FNAMEI
        TRAPCLR     IO
ENDC    RETURN
.
NOINDZ  MOVE        ONE,OPCND
        DISPLAY     *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMEI," Not Found **",*W4;
        TRAPCLR     IO
        RETURN
+
.
.       Deleting an indexed file based on port
.
KILLIDZ CLOSE      TEMPFIL
.
        MOVE       ZERO,STATUS
.
        DISPLAY    *P1:24,*EL,*P20:24,*V2LON:
                   "** Deleting Indexed File **",*W;
.
        CLEAR      CMDLINE
        APPEND     "iserase ",CMDLINE
        APPEND     FNAMEI,CMDLINE
        RESET      CMDLINE
        EXECUTE    CMDLINE,TASKID
        DISPLAY    *P1:3,*EF,*P1:10
        RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
          PRINT     *F,*1,PRGID,*27,CNAME,*78,"DATE : ":
                    CDATE:
                    *N,*1,VERSION:
                    *27,"*** ",PRGNAM," ***":
                    *78,"TIME :   ",CTIMEIS,*N:
                    *78,"PAGE : ",*92,CPAGENO,*N:
                    *27,"From Date : ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *50,"To Date : ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1,*N
.
          CALL      PRLINE
          PRINT     *25,"!",*27,"Reason for Cancellation ",*56,"!":
                    *61,"Total",*69,"!"
.
          CALL      PRLINE
          MOVE      NINE,CLNO
          RETURN
.
PRLINE    PRINT     *25,"*------------------------------*------------*"
          RETURN
.
.              PRINT REPORT
.
REPORT    DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                     "** Generating Report **",*W2:
                     *P1:24,*EL,*P20:24,"Printing Code :"
.
.            For the heading
.
          UNPACK      FROMDAT WITH XCENT,XYEAR,XMON,XDAY
          UNPACK      TODAT WITH XCENT1,XYEAR1,XMON1,XDAY1
.
          MOVE      ZERO,TOTPAT
          MOVE      "60",CLNO
          MOVE       SP3,KEY3
          CALL       RDSTEMP
NXTTEMP   CALL       RDKTEMP
          BRANCH     OVRCD OF ENDLN
.
          MOVE      "UNKNOWN",TDESC
          PACK      KEY5 WITH PC,XCODE
          CALL      RDCODE1
.
          DISPLAY   *P35:24,*V2LON,TDESC;
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MATCH     "UNK",XCODE
          GOTO      KNOWN1 IF NOT EQUAL
.
          PRINT     *25,"!",*31,TDESC,*56,"!",*58,XTOTAL,*69,"!"
          GOTO      KNOWN2
.
KNOWN1    PRINT     *25,"!",*27,XCODE," ",TDESC,*56,"!",*58,XTOTAL,*69,"!"
.
KNOWN2    ADD       XTOTAL,TOTPAT
          ADD       ONE,CLNO
          GOTO      NXTTEMP
.
ENDLN    COMPARE   ZERO,CPAGENO
         GOTO      NOPAT IF EQUAL
         CALL      PRLINE
.
         PRINT     *25,"!     TOTAL",*56,"!",*58,TOTPAT,*69,"!"
         CALL      PRLINE
.
END10    PRINT     *N,*N,"  ***  END OF REPORT  ***"
.
         DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                   "** Report Generation Completed **",*W2;
         CALL       KILLIDZ
         GOTO      START
.
NOPAT    DISPLAY   *P1:24,*EL,*P10:24,*B,*V2LON,"No Patient Selected",*W2;
         CALL      HEAD
         GOTO      END10
+
.
.         Subroutine to key in a valid date
.
KEYINDTE  
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      CCC,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      VERT,CVERT         * set up position
          MOVE      "20",CCOL
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
.
          RETURN
.
+
.           I/O Routine for tempfile
.
RDTEMP
         MOVE      ZERO,OVRCD
         RESET     KEY3
         READ      TEMPFIL,KEY3;XCODE,XTOTAL
         GOTO      OVERCOND IF OVER
         RETURN
.
RDKTEMP
         MOVE      ZERO,OVRCD
         READKS    TEMPFIL;XCODE,XTOTAL
         GOTO      OVERCOND IF OVER
         RETURN
.
WRTEMP
         RESET     KEY3
         WRITE     TEMPFIL,KEY3;XCODE,XTOTAL
         RETURN
.
RDSTEMP
         RESET     KEY3
         READ      TEMPFIL,KEY3;;
         RETURN
UPTEMP
         UPDATE    TEMPFIL;XCODE,XTOTAL
         RETURN
.
         INC        STD001IO
.
         INC       PATMI1IO/INC
         INC       PMSVX1IO/INC
         INC       PATCODIO/INC
         INC       TFILENAM 
         INC       IBASEQIO/INC 
         INC       WEBERRIO/INC
.
ENDIT    CHAIN      PGM
         STOP

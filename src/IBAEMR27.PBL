.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:    IBAEMR27                                              *
.*                                                                            *
.*     FUNCTION:        Attendance Length of Stay report                      *
.*                                                                            *
.*     DATE WRITTEN:    30/09/2010                                            *
.*                                                                            *
.*     AUTHOR:          Mike Laming                                           *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V10.04.01 28/04/2014  Steve Armstrong  CAR 261630                   *
.*                  Mods for non-port based tempfile use.                     *
.******************************************************************************
.*        V10.01.01 03/02/2011  Mike Laming      CAR 233554                   *
.*                  Add Total line to final totals array                      *
.*        V10.01.00 30/09/2010  Mike Laming      CAR 230584                   *
.*                  New program                                               *
.******************************************************************************
.
          INC       STD001FD
.
          INC       EMRVISFD/INC
          INC       EMRUNKFD/INC
          INC       IBASEQFD/INC
          INC       PMSHCPFD/INC          * HCP file
          INC       PATMA1FD/INC          * patient master file 
          INC       PATFN1FD/INC          * Health fund file 
          INC       PATCODFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
EMRTEMP1  IFILE SQL, FIXED=225, KEY="U1-50"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
TEMPKEY   DIM       50     50       1     Sequence key
TEMPURNO  DIM       8      8        51    U/R Number 
TEMPVISN  DIM       8      8        59    EMR Visit No.
TEMPPNAM  DIM       50     50       67    Patient Name
TEMPDATE  DIM       8      8        117   Visit Date (CCYYMMDD)
TEMPATIM  DIM       5      5        125   Arrival Time
TEMPSTIM  DIM       5      5        130   Time Seen
TEMPLOS   DIM       5      5        135   Length of Stay
TEMPCLMT  DIM       3      3        140   Claim Type (Cat."CL")
TEMPHFND  DIM       6      6        143   Health Fund
TEMPTRIG  DIM       1      1        149   Triage Cat.
TEMPWARD  DIM       3      3        150   Expected Ward
TEMPDOCN  DIM       50     50       153   Doctor Name
TEMPSPAR  DIM       22     22       203 
.End of Record                      225
.
. LOCAL VARIABLE DEFINITION
. --------------------------.
.
CDYSDAYS  FORM      6                       * For CALCDAYS
CDYSFDTE  DIM       8                       * For CALCDAYS
CDYSTDTE  DIM       8                       * For CALCDAYS
CDYSYEAR  FORM      2                       * For CALCDAYS
CFEETYP   FORM      1                       * used for PATFNDKY
CMDLINE   DIM       100
CURRDATE  DIM       8
DIM2A     DIM       2
DIM2B     DIM       2
DIM4      DIM       4
DIM32A    DIM       32
DIM21A    DIM       21
DIM50A    DIM       50
FORM2A    FORM      2
FORM2B    FORM      2
FORM6     FORM      6
FROMDATE  DIM       8                       * FROMdate ccyymmdd
HFUNDCOD  DIM       6
HFUNDNAM  DIM       30
SORTSEQN  DIM       1
LT4HRS    FORM      4[9]
LT6HRS    FORM      4[9]
LT8HRS    FORM      4[9]
LTGHRS    FORM      4[9]
LTTHRS    FORM      4[9]
LOSHRS    DIM       2
LOSMIN    DIM       2
DISCMINS  FORM      6
MAXMINS   FORM      6
STRTMINS  FORM      6
TEMPFILE  DIM       8
TODATE    DIM       8                       * TOdate ccyymmdd
TOTHRS    FORM      4[5]
TOTPATS   FORM      5
.
. CONSTANT DEFINITION
. -------------------
ASTER     INIT      "*"
CATAA     INIT      "AA"
UKEY      INIT      " 225 U1-50"            * set up for record length & key
SEQ1      INIT      " - U/R Sequence"
SEQ2      INIT      " - Visit No. Sequence"
SEQ3      INIT      " - Patient Sequence"
SEQ4      INIT      " - Visit Date Sequence"
SEQ5      INIT      " - Doctor Sequence"
SEQ6      INIT      " - LoS Sequence"
SEQ7      INIT      " - Claim Type Sequence"
SEQ8      INIT      " - Category Sequence"
.
.
PRGID     INIT      "IBAEMR27"
PRGNAM    INIT      "EMR Attendances Length of Stay"
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                                MAIN0000                                   *
.*                       Controlling Logic (Mainline code)                   *
.*****************************************************************************
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      OPTN0000               * get user options
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      OKCON000               * ok to continue?
          BRANCH    EXIT,MAIN1000
.
          CALL      CREA0000               * create temp file
          CALL      EXTR0000               * extract data
.
MAIN5000  CALL      PRNT0000               * print grand total
.
MAIN9999  CALL      KILL0000               * delete temp file
          CHAIN     PGM
+
.
.*****************************************************************************
.*                                INIT0000                                   *
.*                             Initialization                                *
.*****************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA4,"emrvisaf"
.
          DISPLAY   *P64:24,"emrunkaf";
          OPEN      EMRUNKA1,"emrunkaf"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
...       DISPLAY   *P64:24,"patdo1af";
...       OPEN      PATDO1A1,"patdo1af"
...       OPEN      PATDO1A2,"patdo1af"
.
...       DISPLAY   *P64:24,"controlf";
...       OPEN      CONTROLF,"controlf"
.
.
INIT9999  RETURN
.
.*****************************************************************************
.*                                OPTN0000                                   *
.*                              User Options                                 *
.*****************************************************************************
OPTN0000  MOVE      ZERO,EXIT 
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Create Length of Stay report":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    OPTION,OPTN2000
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      KDAT0000                * Key in date range
          BRANCH    EXIT,OPTN0000
.
OPTN3000  CALL      KHFND000                * Key in Health Fund
          BRANCH    EXIT,OPTN0000
.
OPTN4000  CALL      SEQN0000                * Key in Report sequence
          BRANCH    EXIT,OPTN0000
.
          GOTO      OPTN9999
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.******************************************************************************
.*                                KDAT0000                                    *
.*                          Keyin range of dates                              *
.******************************************************************************
KDAT0000  MOVE      ZERO,EXIT
          MOVE      "19",CCOL
          MOVE      "10",CVERT
          UNPACK    SP70,CCENT,CYEAR,CMON,CDAY
.
          DISPLAY   *P1:10,*EL,"From Date       : ":
                    *P1:11,*EL,"  To Date       : ";
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9000
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          MOVE      "19",CCOL
          MOVE      "11",CVERT
          UNPACK    SP70,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT0000
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     CURRDATE,TODATE
          IF        !@LESS
            MOVE      CURRDATE,TODATE
          ENDIF
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT8000 IF LESS
.
          MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT8000  DISPLAY   *P43:24,*B,*V2LON:
                    "** FROM Date Greater Than TO Date **",*W2,*P1:24,*EL;
          GOTO      KDAT0000
.
KDAT9000  MOVE      ONE,EXIT
.
KDAT9999  RETURN
.
.******************************************************************************
.*                                  KHFND000                                  *
.*                          Enter selected Health Fund                        *
.******************************************************************************
KHFND000  MOVE      ZERO,EXIT
          UNPACK    SP30,HFUNDCOD,HFUNDNAM
.
          MOVE      "19",ECOL
          MOVE      "12",EVERT
          MOVE      SP6,CKYIDEF6
          DISPLAY   *P1:EVERT,*EL,"Health Fund     : ";
          CALL      PATFNDKY
          BRANCH    EXIT,KHFND100,KHFND800,KHFND100
.
          MOVE      HCODE,HFUNDCOD
          MOVE      HFNAME,HFUNDNAM
          GOTO      KHFND900
.
KHFND100  MOVE      "All Funds",HFUNDNAM
          GOTO      KHFND900
.
.
KHFND800  PRINT     *1,"Exitchar used for Health Fund ";
          MOVE      ONE,EXIT
          GOTO      KHFND999
.
KHFND900  DISPLAY   *PECOL:EVERT,*EL,*V2LON,HFUNDCOD,SP2,HFUNDNAM;
          MOVE      ZERO,EXIT
.
KHFND999  RETURN
.*****************************************************************************
.*                                 SEQN0000                                  *
.*                       Get required Report Sequence                        *
.*****************************************************************************
SEQN0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:13,*EL,"Report Sequence : ";
.
          MOVE      "50",HLEF
          MOVE      "13",HTOP
          MOVE      "78",HRIG
          MOVE      "23",HBOT

....      CALL      GETSCR00                * Save the current screen
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,*V2LON,HLEF,HTOP,HRIG,HBOT
.
          DISPLAY   *P51:14,*V2LON,"1 ",*HOFF,"U/R Number       ":
                    *P51:15,*V2LON,"2 ",*HOFF,"EMR Visit Number ":
                    *P51:16,*V2LON,"3 ",*HOFF,"Patient Name     ":
                    *P51:17,*V2LON,"4 ",*HOFF,"Visit Date       ":
                    *P51:18,*V2LON,"5 ",*HOFF,"Doctor Name      ":
                    *P51:19,*V2LON,"6 ",*HOFF,"Length of Stay   ":
                    *P51:20,*V2LON,"7 ",*HOFF,"Claim Type       ":
                    *P51:21,*V2LON,"8 ",*HOFF,"Triage Category  ";
.
SEQN2000  KEYIN     *P19:13,*DV,UNDLN1,*P19:13,*V2LON,SORTSEQN;
          MOVE      ONE,F1                  * default
          MOVE      SORTSEQN,F1
          IF        F1 < 1 | F1 > 8
            GOTO      SEQN2000
          ENDIF
.
SEQN9999  RETURN
.
.*****************************************************************************
.*                                OKCON000                                   *
.*                          Get User Confirmation                            *
.*****************************************************************************
OKCON000  KEYIN     *P1:24,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSN,ANS
          GOTO      OKCON200 IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      OKCON100 IF EQUAL
.
          BEEP
          GOTO      OKCON000
.
OKCON100  MOVE      ZERO,EXIT
          GOTO      OKCON999
.
OKCON200  MOVE      ONE,EXIT
.
OKCON999  RETURN
.
.*****************************************************************************
.*                                EXTR0000                                   *
.*                         Extract EMR Visit data                            *
.*****************************************************************************
EXTR0000  MOVE      ZERO,EXIT
          MOVE      ZERO,F1
          REPEAT                            * clear count fields
            ADD       ONE,F1
            MOVE      ZERO,LT4HRS[F1]
            MOVE      ZERO,LT6HRS[F1]
            MOVE      ZERO,LT8HRS[F1]
            MOVE      ZERO,LTGHRS[F1]
            MOVE      ZERO,LTTHRS[F1]
            IF        F1 < 6
              MOVE      ZERO,TOTHRS[F1]
            ENDIF
          UNTIL     F1 = 9
.
          PACK      KEY24,FROMDATE,SP70     * set start date for Visits
          CALL      RSEMVIS4
EXTR1000  CALL      RKEMVIS4                * read through EMR Visit table
          BRANCH    OVRCD,EXTR9999
.
          MATCH     "4",DEMVISTA            * bypass "Cancelled"         
          GOTO      EXTR1000 IF EQUAL
.
          MATCH     EMVIDATE,TODATE         * check that Date is in range
          GOTO      EXTR9999 IF LESS
.
          MATCH     SP6,EMVIDRDT            * check is seen by Doctor
          IF        @EQUAL
            MATCH     SP6,EMVINSDT          * seen by Nurse ?
            IF        @EQUAL
              MATCH     "1",DEMVISTA        * is patient still current?
              GOTO      EXTR1000 IF NOT EQUAL     
            ELSE
              MOVE      EMVINSDT,EMVIDRDT   * move Nurse Date/Time to Doctor
              MOVE      EMVINSTM,EMVIDRTM
            ENDIF
          ENDIF
.
          MATCH     SP6,HFUNDCOD            * check for Health Fund
          IF        !@EQUAL
            MATCH     HFUNDCOD,EMVIFUND
            GOTO      EXTR1000 IF NOT EQUAL
          ENDIF
.
EXTR2000  CALL      FNAM0000                * format Patient name
          CALL      FDOC0000                * format Doctor name
          CALL      FLOS0000                * calc & format Length of Stay
.
          MOVE      EMVIURNO,TEMPURNO
          MOVE      DEMVIADM,TEMPVISN
          PACK      DIM50A,TEMPPNAM,SP70
          MOVE      DIM50A,TEMPPNAM
          MOVE      EMVIDATE,TEMPDATE
          MOVE      EMVITIME,TEMPATIM
          MOVE      EMVIDRTM,TEMPSTIM
          MOVE      EMVICOMP,TEMPCLMT
          MOVE      EMVIFUND,TEMPHFND
.
EXTR2500  MOVE      "0",TEMPTRIG
          PACK      KEY5,CATAA,EMVITRIG,SP10
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      TEMPTRIG,TCINDC1,SP2
          ENDIF
.
          MOVE      EMVIEWRD,TEMPWARD
          PACK      DIM50A,TEMPDOCN,SP70
          MOVE      DIM50A,TEMPDOCN
          PACK      TEMPSPAR,SP70
.
.                                           * set up selected sort sequence
EXTR3000  MOVE      SORTSEQN,F1
          BRANCH    F1,EXTR3100,EXTR3200,EXTR3300,EXTR3400,EXTR3500:
                       EXTR3600,EXTR3700,EXTR3800
.
EXTR3100  PACK      TEMPKEY,TEMPURNO,TEMPDATE,TEMPVISN,TEMPATIM,SP70
          GOTO      EXTR4000
.
EXTR3200  PACK      TEMPKEY,TEMPVISN,TEMPURNO,TEMPDATE,SP70
          GOTO      EXTR4000
.
EXTR3300  STRIP     TEMPPNAM
          PACK      TEMPKEY,TEMPPNAM,TEMPVISN,TEMPDATE,SP70
          GOTO      EXTR4000
.
EXTR3400  PACK      TEMPKEY,TEMPDATE,TEMPATIM,TEMPVISN,SP70
          GOTO      EXTR4000
.
EXTR3500  STRIP     TEMPDOCN
          PACK      TEMPKEY,TEMPDOCN,TEMPDATE,TEMPATIM,TEMPVISN,SP70
          GOTO      EXTR4000
.
EXTR3600  PACK      TEMPKEY,TEMPLOS,TEMPDATE,TEMPATIM,TEMPVISN,SP70
          GOTO      EXTR4000
.
EXTR3700  PACK      TEMPKEY,TEMPCLMT,TEMPDATE,TEMPATIM,TEMPVISN,SP70
          GOTO      EXTR4000
.
EXTR3800  PACK      TEMPKEY,TEMPTRIG,TEMPDATE,TEMPATIM,TEMPVISN,SP70
          GOTO      EXTR4000
.
.
EXTR4000  PACK      KEY50,TEMPKEY,SP70
          CALL      WRTEMP1
. 
.                                           * create Time/Triage totals
EXTR5000  UNPACK    TEMPLOS,KEY2,KEY1,DIM2
          MOVE      ZERO,F2
          MOVE      KEY2,F2
.
          MOVE      TEMPTRIG,F1
          MOVE      ZERO,F1
          MOVE      TEMPTRIG,F1
          IF        F1 = 0
            MOVE      "8",F1                * fake Triage = 0
          ENDIF
.
          IF        F2 < 4
            ADD       ONE,LT4HRS[F1]
            ADD       ONE,TOTHRS[1]
          ELSE
            IF        F2 < 6
              ADD       ONE,LT6HRS[F1]
              ADD       ONE,TOTHRS[2]
            ELSE
              IF        F2 < 8
                ADD       ONE,LT8HRS[F1]
                ADD       ONE,TOTHRS[3]
              ELSE
                ADD       ONE,LTGHRS[F1]
                ADD       ONE,TOTHRS[4]
              ENDIF
            ENDIF
          ENDIF
          ADD       ONE,LTTHRS[F1]
          ADD       ONE,TOTHRS[5]
          ADD       ONE,TOTPATS
.
          GOTO      EXTR1000
.
EXTR9999  RETURN
.
.*****************************************************************************
.*                                FNAM0000                                   *
.*                    Read Master & format Patient Name                      *
.*****************************************************************************
FNAM0000  MOVE      ZERO,EXIT
          PACK      TEMPPNAM,SP70
.
          PACK      KEY8,EMVIURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            PACK      KEY8,EMVIADMN
            CALL      RDEMUNK1
            IF        OVRCD = 0
              UNPACK    SP70,PTITL,PSNAME,PGNAME
              MOVE      EMUNDET2,PGNAME
              MOVE      EMUNDET1,PSNAME
            ELSE
              MOVE      "Unknown Patient",TEMPPNAM
              GOTO      FNAM9999
            ENDIF
          ENDIF
.
          STRIP     PSNAME
          STRIP     PTITL
          STRIP     PGNAME
          CLEAR     TEMPPNAM
          MATCH     "3",SORTSEQN
          IF        @EQUAL
            APPEND    PSNAME,TEMPPNAM
            MOVE      ", ",DIM2
            APPEND    DIM2,TEMPPNAM
            APPEND    PTITL,TEMPPNAM
            APPEND    SP1,TEMPPNAM
            APPEND    PGNAME,TEMPPNAM
          ELSE
            APPEND    PTITL,TEMPPNAM
            APPEND    SP1,TEMPPNAM
            APPEND    PGNAME,TEMPPNAM
            APPEND    SP1,TEMPPNAM
            APPEND    PSNAME,TEMPPNAM
          ENDIF
          RESET     TEMPPNAM
.
FNAM9999  RETURN
.
.*****************************************************************************
.*                                FDOC0000                                   *
.*                    Read Doctor file & format Doc.Name                     *
.*****************************************************************************
FDOC0000  MOVE      ZERO,EXIT
          PACK      TEMPDOCN,SP70
.
          PACK      KEY10,EMVIDOCT,SP70      * Attending Doctor
          CALL      RDPMHCP1
          IF        OVRCD=1
            MOVE      "Unknown Doctor ",KEY15    
            PACK      TEMPDOCN,KEY15,EMVIDOCT,SP70
            GOTO      FDOC9999
          ENDIF
.
          MATCH     SP5,PMHCINIT
          IF        @EQUAL
            MOVE      PMHCGNAM,KEY1
            MOVE      KEY1,PMHCINIT
          ENDIF
          STRIP     PMHCSNAM
          STRIP     PMHCTITL
          STRIP     PMHCINIT
          CLEAR     TEMPDOCN
          MATCH     "5",SORTSEQN
          IF        @EQUAL
            APPEND    PMHCSNAM,TEMPDOCN
            MOVE      ", ",DIM2
            APPEND    DIM2,TEMPDOCN
            APPEND    PMHCTITL,TEMPDOCN
            APPEND    SP1,TEMPDOCN
            APPEND    PMHCINIT,TEMPDOCN
          ELSE
            APPEND    PMHCTITL,TEMPDOCN
            APPEND    SP1,TEMPDOCN
            APPEND    PMHCINIT,TEMPDOCN
            APPEND    SP1,TEMPDOCN
            APPEND    PMHCSNAM,TEMPDOCN
          ENDIF
          RESET     TEMPDOCN
.
FDOC9999  RETURN
.
.*****************************************************************************
.*                                FLOS0000                                   *
.*                       Calculate Length of Stay                            *
.*****************************************************************************
FLOS0000  MOVE      ZERO,EXIT
          MOVE      SP5,TEMPLOS
          MATCH     SP8,EMVIDDAT            * is Discharge Date blank?
          IF        @EQUAL
            MOVE      CURRDATE,EMVIDDAT
            MOVE      CTIMEIS,EMVIDTIM
          ENDIF
.
          MATCH     "1",DEMVISTA            * is this a current patient?
          IF        @EQUAL
            MOVE      CURRDATE,EMVIDDAT
            MOVE      CTIMEIS,EMVIDTIM
          ENDIF
.
          MOVE      EMVIDATE,CDYSFDTE       * arrival date
          MOVE      EMVIDDAT,CDYSTDTE       * discharge date
.
          CALL      CALCDAYS                * Calculate diff between dates
          SUB       ONE,CDYSDAYS            * remove the inclusive date
.
          MOVE      ZERO,FORM2A
          MOVE      ZERO,FORM2B
          UNPACK    EMVITIME,DIM2A,KEY1,DIM2B
          MOVE      DIM2A,FORM2A            * start time hours
          MOVE      DIM2B,FORM2B            * start time minutes
          ASSIGN    (FORM2A*60)+FORM2B,STRTMINS
.
          MOVE      ZERO,FORM2A
          MOVE      ZERO,FORM2B
          UNPACK    EMVIDTIM,DIM2A,KEY1,DIM2B
          MOVE      DIM2A,FORM2A            * discharge time hours
          MOVE      DIM2B,FORM2B            * discharge time minutes
          ASSIGN    (FORM2A*60)+FORM2B,DISCMINS
.
.                                           * calculate length of stay in mins
          ASSIGN    ((CDYSDAYS*1440)-STRTMINS+DISCMINS),MAXMINS
          MOVE      MAXMINS,FORM6
          DIVIDE    "60",FORM6  
          MOVE      FORM6,KEY6              * <= hours for Length of Stay
.
          UNPACK    KEY6,DIM4,LOSHRS        * reformat to Hours & Minutes
          MULT      "60",FORM6
          SUB       FORM6,MAXMINS
          MOVE      MAXMINS,KEY6
          UNPACK    KEY6,DIM4,LOSMIN
          REP       " 0",LOSMIN
          MOVE      ":",KEY1
          PACK      TEMPLOS,LOSHRS,KEY1,LOSMIN  
.
FLOS9999  RETURN
.
.*****************************************************************************
.*                                PRNT0000                                   *
.*                            Print the Report                               *
.*****************************************************************************
PRNT0000  MOVE      ZERO,EXIT
          MOVE      "99",CLNO
.
          PACK      KEY50,SP70
          CALL      RSTEMP1
PRNT1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRNT3000
.
          COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          REP       "0 ",TEMPTRIG
          PACK      KEY3,SP1,TEMPTRIG,SP1
          MOVE      TEMPPNAM,DIM32A
          MOVE      TEMPDOCN,DIM21A
          UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          UNPACK    TEMPLOS,KEY2,KEY1,DIM2
          MOVE      ZERO,F2
          MOVE      KEY2,F2
          IF        F2 > 23
            PACK      KEY6,ASTER,TEMPLOS
          ELSE
            PACK      KEY6,SP1,TEMPLOS
          ENDIF
.
          PRINT     *1,"|",TEMPURNO,"|",TEMPVISN,"| ",DIM32A,"| ",CPCDATE:
                    "| ",TEMPATIM,"| ",TEMPSTIM,"|",KEY6," | ",TEMPCLMT:
                    " |",TEMPHFND,"|",KEY3,"|",TEMPWARD," | ",DIM21A,"|"
.
          ADD       ONE,CLNO
          GOTO      PRNT1000
.
PRNT3000  CALL      PDOT0000
          ADD       "15",CLNO               * size of Totals array
          COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS
          PRINT     *N,*1,"TRIAGE| <4hrs|4<6hrs|6<8hrs|=>8hrs|Total|"
.
          MOVE      LT4HRS[8],FORM4
          ADD       LT6HRS[8],FORM4
          ADD       LT8HRS[8],FORM4
          ADD       LTGHRS[8],FORM4
          ADD       LTTHRS[8],FORM4
          IF        FORM4 > 0
            MOVE      "8",F1                * print "Triage 0" counts
            CALL      PTOT0000
          ENDIF
.
          MOVE      ZERO,F1
          REPEAT
            ADD       "1",F1
            IF        F1 < 7 | F1 > 8
              CALL      PTOT0000
            ENDIF
          UNTIL     F1 = 9
.
          PRINT     *1,"Total",*7,"|",TOTHRS[1],*14,"|",TOTHRS[2]:
                    *21,"|",TOTHRS[3],*28,"|",TOTHRS[4]:
                    *35,"|",TOTHRS[5],*41,"|",*N
.
          PRINT     *1,"Total Number of Patients:",TOTPATS
.
PRNT9999  RETURN
.
.*****************************************************************************
.*                             Print Totals                                  *
.*                                PTOT0000                                   *
.*****************************************************************************
PTOT0000  MOVE      ONE,CNOUNDLN
...       COMPARE   "56",CLNO
...       CALL      HEAD0000 IF NOT LESS
.
          MOVE      F1,KEY1
          REP       "8 ",KEY1               * handle "non triaged" patients
.
          PRINT     *3,KEY1,*7,"|",LT4HRS[F1],*14,"|",LT6HRS[F1]:
                    *21,"|",LT8HRS[F1],*28,"|",LTGHRS[F1]:
                    *35,"|",LTTHRS[F1],*41,"|" 
...       ADD       ONE,CLNO
.
PTOT9999  RETURN
.
.*****************************************************************************
.*                             Print Heading                                 *
.*                                HEAD0000                                   *
.*****************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          MOVE      ONE,F1                  * default
          MOVE      SORTSEQN,F1
          LOAD      CPHDROPT,F1,SEQ1,SEQ2,SEQ3,SEQ4,SEQ5,SEQ6,SEQ7,SEQ8
          CALL      HEAD132                 * Print the report header
.
          MOVE      "8",CLNO
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"From : ",CPCDATE;
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *60,"To : ",CPCDATE
.
          MATCH     SP6,HFUNDCOD
          IF        !@EQUAL
            SQUEEZE   HFUNDCOD
            MOVE      " Patients only",KEY15
            PACK      KEY25,HFUNDCOD,KEY15
            PRINT     *48,KEY25
            ADD       ONE,CLNO
          ENDIF
.
          CALL      PDOT0000
          PRINT     *1,"| U/R    | EMR    | Patient Name ":
                    *53,"| Visit Date| Time | Time |  LoS  |Claim|H/Fund|Tri":
                    *104,"|Exp.| EMR Doctor Name ",*132,"|"
          PRINT     *1,"| Number | Number | ":
                    *53,"|           | Arr. | Seen | HH:MM |Type |      |Cat":
                    *104,"|Ward| ",*132,"|"
          CALL      PDOT0000
.
HEAD9999  RETURN
.
.*****************************************************************************
.*                                PDOT0000                                   *
.*                          Print dotted lines                               *
.*****************************************************************************
.
PDOT0000  PRINT     *1,"*--------------------------------------------":
                    "--------------------------------------------":
                    "------------------------------------------*"
          ADD       ONE,CLNO
.
PDOT9999  RETURN
.
.
.******************************************************************************
.*                                CREA0000                                    *
.*                          Create a tempfile                                 *
.******************************************************************************
CREA0000  CALL      TFILENAM                     * get new tempfile name
          MOVE      TFILNAME,TEMPFILE
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EMRTEMP1,TEMPFILE
          BRANCH    OVRCD,CREA1000
          TRAPCLR   IO
          CALL      CLER0000                * clear existing temp file
          GOTO      CREA9999
.
CREA1000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TEMPFILE,CMDLINE
          APPEND    UKEY,CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      EMRTEMP1,TEMPFILE
.
CREA9999  RETURN
+
.******************************************************************************
.*                                CLER0000                                    *
.*                       Delete records from tempfile                         *
.******************************************************************************
CLER0000  TRAP      OVERCOND IF IO
          OPEN      EMRTEMP1,TEMPFILE
          BRANCH    OVRCD,CREA9999
          TRAPCLR   IO
.
CLER2000  MOVE      SP70,KEY50
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY50,TEMPKEY,SP70
          CALL      DETEMP1
          GOTO      CLER2000
.
CLER9999  RETURN
+
.******************************************************************************
.*                                KILL0000                                    *
.*                          Clear up temp Tables                              *
.******************************************************************************
KILL0000  CLOSE     EMRTEMP1
          CALL      CLER0000
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TEMPFILE,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*****************************************************************************
.*                              TEMP File I/Os                               *
.*                                                                           *
.*****************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          READ      EMRTEMP1,KEY50;ANS
          GOTO      OVERCOND IF OVER
          RETURN                   
.                                  
RSTEMP1   MOVE      ZERO,OVRCD     
          RESET     KEY50          
          READ      EMRTEMP1,KEY50;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          READ      EMRTEMP1,KEY50;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSTIM,TEMPLOS:
                                   TEMPCLMT,TEMPHFND,TEMPTRIG,TEMPWARD:
                                   TEMPDOCN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN

.
RKTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          READKS    EMRTEMP1;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSTIM,TEMPLOS:
                                   TEMPCLMT,TEMPHFND,TEMPTRIG,TEMPWARD:
                                   TEMPDOCN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          READKP    EMRTEMP1;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSTIM,TEMPLOS:
                                   TEMPCLMT,TEMPHFND,TEMPTRIG,TEMPWARD:
                                   TEMPDOCN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          WRITE     EMRTEMP1,KEY50;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSTIM,TEMPLOS:
                                   TEMPCLMT,TEMPHFND,TEMPTRIG,TEMPWARD:
                                   TEMPDOCN,TEMPSPAR
          RETURN
.
UPTEMP1   UPDATE    EMRTEMP1;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSTIM,TEMPLOS:
                                   TEMPCLMT,TEMPHFND,TEMPTRIG,TEMPWARD:
                                   TEMPDOCN,TEMPSPAR
          RETURN
.
DETEMP1   RESET     KEY50
          DELETE    EMRTEMP1,KEY50
          RETURN
.
+
.*****************************************************************************
.*                             I/O's Includes                                *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001IO
          INC       PATFNDKY                * Keyin a health fund
          INC       CALCDAYS                * Calculate diff between dates
          INC       TFILENAM
.
          INC       EMRVISIO/INC
          INC       EMRUNKIO/INC
          INC       IBASEQIO/INC
...       INC       PATDO1IO/INC            * doctor table
          INC       PATMA1IO/INC
          INC       PATFN1IO/INC
          INC       PATCODIO/INC
          INC       PMSHCPIO/INC            * HCP table
          INC       WEBERRIO/INC
.
.

.******************************************************************************
.* System         : Patient                                                   *
.* Program        : IBAADT82.PBL                                              *
.* Name           : ICD10 Disease Code Master Listing                         *
.******************************************************************************
.* Date           : 25/05/1998                                                *
.* Author         : Nick Read                                                 *
.* System         : Print Master Listing of ICD10 Disease Code File           *
.* Modifications  :                                                           *
.*        V10.02.01 12/10/2011  Ebon Clements  CAR 244004                     *
.*                  Changed to use standard ICD10 reads                       *
.*        V9.11.01  10/02/2009  Mike Laming    CAR 189076                     *
.*                  Added standard ICD10 Open statement in INIT0000           *
.*        V9.04.00  09/08/2005  Davin          CAR 66409                      *
.*                  Ported from 5.13                                          *
.******************************************************************************
.*        V5.09.02  07/06/2002  Davin  2002/2003 changes                      *
.*                  Recompiled for ICD10 v3 file changes                      *
.*        V5.09.01  21/05/2002  Davin                                         *
.*                  Added ICD10 v3 validation flag                            *
.*        V5.08.02  02/08/2000  Greg Horvat      SRF 4535                     *
.*                  Changed to display the ICD10 version 1 map code           *
.*        V5.08.01  27/06/2000  Greg Horvat                                   *
.*                  Changed to display the ICD10 version 1 & 2 validation code*
.*        V5.08.B01 06/03/2000  Jill Habasque                                 *
.*                  Recompiled for STDKWSDF & STDKWSCD                        *
.*        V5.07.02  16/11/1999  Greg Horvat                                   *
.*                  Fixed code displays & report header                       *
.*        V5.07.01  06/11/1999  Greg Horvat                                   *
.*                  Recompiled for PATICDKY                                   *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCONFD/INC            * Control file
          INC       PATICDFD/INC            * Procedure Code File
          INC       PATDICVR/INC            * ICD9 keyword index
.
. ----- Program Variables -----
.
DSPEDCOD  DIM       9
DSPEDDSC  DIM       40
DSPSTCOD  DIM       9
DSPSTDSC  DIM       40
DSPV1CD   DIM       3
DSPV2CD   DIM       3
DSPV3CD   DIM       3
ENDCODE   DIM       9
ENDDESC   DIM       70
RECCOUNT  FORM      6
SAVKEY9   DIM       9
STRTCODE  DIM       9
STRTDESC  DIM       70
.
. ----- Program Constants -----
.
OPTION1   INIT      "- Code Sequence "
OPTION2   INIT      "- Description Sequence "
OPTION2P  INIT      "- Desc. Sequence "
START     INIT      "Start"
FINISH    INIT      "Finish"
.
PRGID     INIT      "IBAADT82"
PRGNAM    INIT      "ICD10 Disease Code Master Listing"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  MAINLINE                                  *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialize variables & open files
ML1000    CALL      OPTN0000                * Select main option
          BRANCH    OPTION,ML2000,ML5000
          GOTO      ML9999
.
. ---   Print Report By Code Sequence  ---
.
ML2000    CALL      KCOD0000                * Keyin Procedure Codes 
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ready to Print?
          BRANCH    CEXIT,ML3000:           * Yes
                          ML2000:           * No
                          ML1000            * Cancel
.
ML3000    CALL      PCOD0000                * Print Report
          GOTO      ML1000
.
.
. ---   Print Report By Description Sequence  ---
.
ML5000    CALL      KDES0000                 * Keyin Procedure Descriptions
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ready to Print?
          BRANCH    CEXIT,ML6000:           * Yes
                          ML5000:           * No
                          ML1000            * Cancel
.
ML6000    CALL      PDES0000                 * Print Report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialize Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"pati10df";
          OPEN      PATI10D1,"pati10df"
          OPEN      PATI10D2,"pati10df"
          OPEN      PATI13D1,"pat10d3f"
          OPEN      PATI14D1,"pat10d4f"
.
          CALL      OPICD1                  * Open current ICD10 files *-189076
          CALL      OPICD2                  * Open current ICD10 files *-189076
.
          MOVE      ZERO,CKYIMODE
          MOVE      SP8,DSICDDTE
          MOVE      SP8,ICDRDDTE
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                             Select Main Option                             *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          MOVE      ZERO,CPAGENO
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Code Sequence":
                    *P1:6,*V2LON,"2",*HOFF," = Description Sequence":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*EL,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN2000,OPTN3000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  DISPLAY   *P57:2,*V2LON,OPTION1;
          GOTO      OPTN9000
.
OPTN3000  DISPLAY   *P57:2,*V2LON,OPTION2;
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KCOD0000              Called by: ML0000   *
.*                            Enter The Procedure Codes                       *
.******************************************************************************
KCOD0000  MOVE      "23",ECOL
          MOVE      ZERO,CKYIMAND
          MOVE      ONE,CNEWDS
          MOVE      "00000000",PTCNI10D
.
          MOVE      "10",EVERT
          MOVE      SP9,CKYIDEF9
          DISPLAY   *P1:EVERT,*EF,"Starting ICD10 Code : ";
.
          CALL      PATICDKY
          BRANCH    EXIT,KCOD1000,KCOD9500
.
          PACK      STRTCODE,DPCODE,SP10
          PACK      DSPSTCOD,STRTCODE
          STRIP     DSPSTCOD
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DSPSTCOD,SP1,DDESC;
          GOTO      KCOD5000
.
KCOD1000  PACK      STRTCODE,SP20
          PACK      DSPSTCOD,START
          STRIP     DSPSTCOD
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DSPSTCOD;
.
KCOD5000  MOVE      "12",EVERT
          MOVE      SP9,CKYIDEF9
          DISPLAY   *P1:EVERT,*EL,"Ending ICD10 Code   : ";
.
          CALL      PATICDKY
          BRANCH    EXIT,KCOD6000,KCOD9500
.
          PACK      ENDCODE,DPCODE,SP10
          PACK      DSPEDCOD,ENDCODE
          STRIP     DSPEDCOD
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DSPEDCOD,SP1,DDESC;
          GOTO      KCOD7000
.
KCOD6000  PACK      ENDCODE,Z70
          PACK      DSPEDCOD,FINISH
          STRIP     DSPEDCOD
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DSPEDCOD;
.
KCOD7000  MATCH     STRTCODE,ENDCODE
          GOTO      KCOD9000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Ending code must be after the starting ":
                    "code.  ";
          CALL      HITENTER
          GOTO      KCOD0000
.
KCOD9000  MOVE      ZERO,EXIT
          GOTO      KCOD9999
.
KCOD9500  MOVE      ONE,EXIT
KCOD9999  RETURN
+
.******************************************************************************
.*                                  KDES0000              Called by: ML0000   *
.*                       Enter The Procedure Descriptions                     *
.******************************************************************************
KDES0000  PACK      STRTDESC,SP70               
          PACK      ENDDESC,SP70
          PACK      DSPSTDSC,SP70                
          PACK      DSPEDDSC,SP70                
          DISPLAY   *P1:10,*EF,"Starting Description : ";      
.
          KEYIN     *P24:10,*V2LON,STRTDESC:
                    *P24:10,*DV,STRTDESC
.
          PACK      STRTDESC,STRTDESC,SP30,SP30,SP10
          MOVE      STRTDESC,KEY70
          CALL      CHKD0000
          BRANCH    EXIT,KDES1000:          * ? Entered
                         KDES4000:          * valid description
                         KDES2000:          * nothing entered
                         KDES9500           * exitchar
.
KDES1000  MOVE      ZERO,HLEF
          DISPLAY   *P1:24,*EL,*B,"Question mark facility not available.  ";
          CALL      HITENTER
          GOTO      KDES0000
.
KDES2000  PACK      STRTDESC,SP70 
          PACK      DSPSTDSC,START
          STRIP     DSPSTDSC
          DISPLAY   *P24:10,*EL,*V2LON,START 
          GOTO      KDES5000
.
KDES4000  DISPLAY   *P24:10,*EL,*V2LON,STRTDESC
          PACK      DSPSTDSC,STRTDESC
          STRIP     DSPSTDSC
.
KDES5000  PACK      ENDDESC,SP70
          DISPLAY   *P1:12,*EL,"Ending Description   : ";
.
          KEYIN     *P24:12,*V2LON,ENDDESC:
                    *P24:12,*DV,ENDDESC
.
          PACK      ENDDESC,ENDDESC,SP30,SP30,SP10
          MOVE      ENDDESC,KEY70
          CALL      CHKD0000
          BRANCH    EXIT,KDES7000:          * ? Entered
                         KDES8000:          * valid description
                         KDES6000:          * nothing entered 
                         KDES9500           * exitchar
.
KDES6000  PACK      ENDDESC,Z70
          PACK      DSPEDDSC,FINISH
          STRIP     DSPEDDSC
          DISPLAY   *P24:12,*EL,*V2LON,FINISH
          GOTO      KDES9000
.
KDES7000  MOVE      ZERO,HLEF
          DISPLAY   *P1:24,*EL,*B,"Question mark facility not available.  ";
          CALL      HITENTER
          GOTO      KDES5000
.
KDES8000  PACK      DSPEDDSC,ENDDESC
          STRIP     DSPEDDSC
          MATCH     STRTDESC,ENDDESC
          GOTO      KDES9000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Ending description must be after starting ":
                    "description.  ";
          CALL      HITENTER
          GOTO      KDES5000
.
KDES9000  MOVE      ZERO,EXIT
          GOTO      KDES9999
.
KDES9500  MOVE      ONE,EXIT
KDES9999  RETURN
+
.***************************************************************************
.*                                  CHKD0000           Called by: KDES0000 *
.*                      See what was input                                 *
.*      Returns: EXIT  0 = invalid                                         *
.*                     1 = "?"                                             *
.*                     2 = valid                                           *
.*                     3 = nothing                                         *
.*                     4 = exitchar                                        *
.***************************************************************************
.
CHKD0000  PACK      KEY70,KEY70,SP70
          MATCH     SP70,KEY70
          GOTO      CHKD9000 IF EQUAL       * Spaces
.
          MATCH     EXITCHAR,KEY70
          GOTO      CHKD7000 IF EQUAL       * Exitchar
.
          MATCH     QUEST,KEY70
          GOTO      CHKD8000 IF EQUAL       * ? entred
.
CHKD5000  MOVE      TWO,EXIT                     * valid entry
          GOTO      CHKD9999
.
CHKD7000  MOVE      FOUR,EXIT
          GOTO      CHKD9999
.
CHKD8000  MOVE      ONE,EXIT
          GOTO      CHKD9999
.
CHKD9000  MOVE      THREE,EXIT
CHKD9999  RETURN
+
.******************************************************************************
.*                                  PCOD0000              Called by: ML0000   *
.*                        Print Report by Code Sequence                       *
.******************************************************************************
PCOD0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Generating Report ... ";
          MOVE      ZERO,RECCOUNT
          CALL      IBACLOCK
          CALL      HEAD0000
.
          PACK      KEY9,STRTCODE,SP70
          CALL      RDPTICD1
          IF        OVRCD<>1
            GOTO      PCOD1100
          ENDIF
.
PCOD1000  CALL      RKPTICD1
          BRANCH    OVRCD,PCOD8000
.
PCOD1100  MATCH     STRTCODE,DPCODE
          GOTO      PCOD1000 IF LESS
.
          STRIP     DPCODE
          MATCH     DPCODE,ENDCODE
          GOTO      PCOD8000 IF LESS 
.
          CALL      PRNT0000                * Print line
          GOTO      PCOD1000
.
PCOD8000  CALL      PEND0000                * Print End of Report
.
PCOD9000  MOVE      ZERO,EXIT
PCOD9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: Lots     *
.*                            Print Report Heading                            *
.******************************************************************************
HEAD0000  MOVE      SP30,CPHDROPT
          LOAD      CPHDROPT,OPTION,OPTION1,OPTION2P
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          BRANCH    OPTION,HEAD1000:        * Printing by Code Sequence
                           HEAD2000         * Printing by Description Sequence
.
HEAD1000  PRINT     *40,*+,"ICD10 Code Range : ",DSPSTCOD," to ",DSPEDCOD,*N
          GOTO      HEAD8000
.
HEAD2000  PRINT     *20,"ICD10 Description Range : ":
                        *+,DSPSTDSC," to ",DSPEDDSC,*N
          GOTO      HEAD8000
.
HEAD8000  ADD       "4",CLNO
          CALL      UND132                  * Print an underline
.ICD10 Code|Description                                                 |Valid|Age Lim 1|Age Lim 2| Mapped  |CMap|Ver|Ver|  ICD10  |
.          |                                                            |     |Low  High|Low  High|ICD9 Code|Frmt| 1 | 2 | 3 |Ver 1 Map|
          PRINT     *1,"|ICD10 Code",*12,"|Description":
                    *69,"|Valid",*75,"|Age Lim 1",*85,"|Age Lim 2":
                    *95,"| Mapped",*105,"|CMap",*110,"|Ver",*114,"|Ver",*118:
                    "|Ver",*122,"|  ICD10",*132,"|"
          PRINT     *1,"|",*12,"|",*69,"|",*75,"|Low  High",*85,"|Low  High":
                    *95,"|ICD9 Code",*105,"|Frmt",*110,"| 1",*114,"| 2":
                    *118,"| 3",*122,"|Ver 1 Map",*132,"|"
          ADD       "2",CLNO
          CALL      UND132                  * Print an underline
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PCOD0000 *
.*                                 Print Line                      & PDES0000 *
.******************************************************************************
PRNT0000  IF        CLNO>54
              CALL     UND132
              MOVE     ZERO,CLNO
              PRINT    *F
              CALL     HEAD0000
          ENDIF
.
          MOVE      DDESC,KEY56
          MOVE      DNO,DSPV1CD
          LOAD      DSPV1CD,PT0DV1CD,DYES
          MOVE      DNO,DSPV2CD
          LOAD      DSPV2CD,PT0DV2CD,DYES
          MOVE      DNO,DSPV3CD
          LOAD      DSPV3CD,PT0DV3CD,DYES
.
.123456789 |12345678901234567890123456789012345678901234567890123456|  1  | 12
.2  | 12  12  |123456789|  1 |123|123|123|123456789|
          PRINT     *1,"|",DPCODE,*12,"|",KEY56,*69,"|  ",DFLAG:
                    *75,"| ",DLOW,SP2,DHIGH,*85,"| ",PT0D2ALL,SP2,PT0D2AHL:
                    *95,"|",PT0DMI9C,*105,"|  ",PT0DCMFT,*110,"|",DSPV1CD:
                    *114,"|",DSPV2CD,*118,"|",DSPV3CD,*122,"|",PT0DV1MP,*132,"|"
          ADD       ONE,CLNO
          ADD       ONE,RECCOUNT
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+ 
.******************************************************************************
.*                                  PEND0000              Called by: PCOD0000 *
.*                         Print End of Report Summary             & PDES0000 *
.******************************************************************************
PEND0000  IF        CLNO>54
            CALL     UND132
            PRINT    *F
            CALL     HEAD0000
          ELSE
            CALL      UND132
          ENDIF
          PRINT     *N," *** End of Report *** ":
                    *N,*N,"No. of Records : ",RECCOUNT
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete.  ";
          CALL      HITENTER
.
PEND9000  MOVE      ZERO,EXIT
PEND9999  RETURN
+
.******************************************************************************
.*                                  PDES0000              Called by: ML0000   *
.*                     Print Report by Description Sequence                   *
.******************************************************************************
PDES0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Generating Report ... ";
          MOVE      ZERO,RECCOUNT
          CALL      IBACLOCK
          CALL      HEAD0000
.
          PACK      ENDDESC,ENDDESC,Z70
          PACK      KEY9,STRTDESC,SP70
          CALL      RDPTICD2
          IF        OVRCD<>1
            GOTO      PDES1100
          ENDIF
.
PDES1000  CALL      RKPTICD2
          BRANCH    OVRCD,PDES8000
.
PDES1100  MATCH     STRTDESC,DDESC
          GOTO      PDES1000 IF LESS
.
          STRIP     DDESC
          MATCH     DDESC,ENDDESC
          GOTO      PDES8000 IF LESS 
.
          CALL      PRNT0000                * Print line
          GOTO      PDES1000
.
PDES8000  CALL      PEND0000                * Print End of Report
.          
PDES9000  MOVE      ZERO,EXIT
PDES9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       STDKWSCD
.
          INC       PATICDKY
          INC       PATDICIO/INC            * ICD9 keyword index
          INC       PATICDIO/INC
+
.


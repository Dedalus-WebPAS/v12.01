.******************************************************************************
.*     PROGRAM NAME:    NUTLAB01                                              *
.*     FUNCTION:        St Vincent's Nutrition Labels                         *
.*     AUTHOR:          Tonii Tang                                            *
.*     DATE WRITTEN:    12/03/2002                                            *
.******************************************************************************
.*     MODIFICATIONS:                                                         *
.*     V12.00.01 28/05/2025  J.Tan          TSK 0955096                       *
.*                  Added Alphanumeric visit number changes                   *
.*     V10.02.01    07/04/2011 Jill Parkinson CAR 239574                      *
.*                  Removed open of ibaprntf                                  *
.******************************************************************************
          INC       STD001FD
          INC       STDKWSDF
          INC       STDHLPDF
          INC       IBAPRNFD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATVISFD/INC
          INC       PMSPX2FD/INC    ***
          INC       PMSVX1FD/INC    ***
          INC       PATWR1FD/INC
          INC       PATCONFD/INC
          INC       PATNOBFD/INC
          INC       PATTRNFD/INC
          INC       IBADLABS/INC
          INC       PATDO1FD/INC
...
DIM8      DIM       8
DIM12     DIM       12
DIM3      DIM       3
DIM3A     DIM       3
FORM3     FORM      3
FORM21    FORM      21
FROMWARD  DIM       3
LABELTYP  DIM       10
PRINTVAR  DIM       80   
SCRID     DIM       2
TOWARD    DIM       3
WARD      DIM       3
ZEBRAFLG  FORM      1
.
START     INIT      "START"
FINISH    INIT      "FINISH"
SP80      INIT      "                                   ":
                    "                                             "
.
PRGID     INIT      "NUTRILAB"
PRGNAM    INIT      "Print Nutrition Label By Ward"
VERSION   INIT      "V12.01.00"
.
.******************************************************************************
.*                                 ML0000                                     *
.*                              Mainline Code                                 *
.******************************************************************************
ML0000    CALL      INIT0000
          BRANCH    EXIT,ML9999
.
ML0100    CALL      SCRN0000
          BRANCH    OPTION,ML1000
          GOTO      ML9999
.
ML1000    CALL      KYWD0000
          BRANCH    EXIT,ML0100
.
ML2000    GOTO      ML0100
.
ML9999    CHAIN     PGM

.**********************************************************************
.*                            INIT0000                                *
.*                       Initialization Routine                       *
.**********************************************************************
INIT0000  MOVE      FALSE,EXIT
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A2,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          OPEN      IBASPOL1,"ibaspolf"
          OPEN      IBAPRTA1,"ibaprtaf"        
.
          CALL      IBACLOCK
.
INIT9999  RETURN
.**********************************************************************
.*                             SCRN0000                               *
.*                        Main Option Screen                          *
.*                        Called : ML0000                             *
.**********************************************************************
.
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print by Ward " 
.
SCRN1000  KEYIN     *P1:7,"Select Option : ",*EF,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION                  * exit selected ?
          GOTO      SCRN9999 IF EQUAL            * yes - finished
.
          BRANCH    OPTION,SCRN9999              * print range of wards
.
          BEEP                                   * invalid option
          GOTO      SCRN1000
.
SCRN9999  RETURN
+
.**********************************************************************
.*                            KYWD0000                                *
.*                       Keyin ward range for print                   *
.**********************************************************************
KYWD0000  DISPLAY   *P1:10,*EL,"From Ward : ":
                    *P1:12,*EL,"To Ward   : "
.
KYWD0500  MOVE      TEN3,ECOL
          MOVE      TEN,EVERT
          MOVE      "\",EXITCHAR
          CALL      PATWRDKY
          IF        EXIT = 1
            MOVE      SP3,FROMWARD 
            PACK      WBDESC,START,SP80
          ELSE
            IF         EXIT = 2
              GOTO       KYWD9999
            ELSE
              MOVE      WWARD,FROMWARD 
            ENDIF
          ENDIF
.
          IF        WACTIVE = 1      
            KEYIN     *P1:24,*EL,*B,*V2LON,"** Ward is not active **   ";
            CALL      HITENTER
            GOTO      KYWD0500
          ENDIF
.
          DISPLAY   *P21:10,*EL,*V2LON,WBDESC
.
KYWD1000  MOVE      TEN3,ECOL
          MOVE      TEN2,EVERT
          MOVE      "\",EXITCHAR
          CALL      PATWRDKY
          IF        EXIT = 1
            MOVE      "zzz",TOWARD 
            PACK      WBDESC,FINISH,SP80
          ELSE
            IF         EXIT = 2
              GOTO       KYWD9999
            ELSE
              MOVE      WWARD,TOWARD 
            ENDIF
          ENDIF
.
          IF        WACTIVE = 1      
            KEYIN     *P1:24,*EL,*B,*V2LON,"** Ward is not active **   ";
            CALL      HITENTER
            GOTO      KYWD1000
          ENDIF
.
          DISPLAY   *P21:12,*EL,*V2LON,WBDESC
.
          CALL      CONTQST
          BRANCH    CEXIT,KYWD2000,KYWD0000,KYWD9999
.
.
KYWD2000  PACK      KEY6,FROMWARD,SP20
          CALL      RDSWARD1
KYWD2500  CALL      RDKWARD1
          BRANCH    OVRCD,KYWD9999
.
          MATCH     TOWARD,WWARD
          GOTO      KYWD3000 IF LESS
.
          MATCH     WWARD,TOWARD
          GOTO      KYWD9999 IF NOT EQUAL
.
KYWD3000  PACK      KEY6,WWARD,SP20
          CALL      RDWARD1
          BRANCH    OVRCD,KYWD9999
.
          MOVE      WWARD,DIM3
.
          IF        WINPUT = 0
            CALL      WARDA000        * Ward/Bed
          ELSE
            IF        WINPUT = 1
              CALL      WARDB000      * Ward Only, read thru patnobef
            ELSE
              IF        WINPUT = 2  
                CALL      WARDA000    * Ward/Bed 
                CALL      WARDB000    * Ward Only 
              ENDIF
            ENDIF
          ENDIF
.
          GOTO      KYWD2500
.                          
KYWD9999  RETURN
+
.******************************************************************************
.*                            WARDA000                                        *
.*      Ward Type = 0, Ward/Bed, loop thru patwr1af file                      *
.******************************************************************************
WARDA000  PACK      KEY6,DIM3,SP20
          CALL      RDSWARD1
WARDA100  CALL      RDKWARD1
          BRANCH    OVRCD,WARDA999
.
          MATCH     WWARD,DIM3
          GOTO      WARDA999 IF NOT EQUAL
.
          MATCH     ZEROVISN,WADMNO
          IF        @EQUAL
            MATCH     ZEROVISN,WSTBY
            IF        @EQUAL
              GOTO      WARDA100 
            ENDIF
          ENDIF
.
          PACK      KEY8,WADMNO
          CALL      RDMISS1
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
.
          MOVE      TWO,ZEBRAFLG
          MOVE      WWARD,WARD  
          CALL      SETLAB00
          CALL      PRTLAB00
.
          GOTO      WARDA100
.
WARDA999  RETURN
+
.******************************************************************************
.*                            WARDB000                                        *
.*      Ward Type = 1, Ward Only, loop thru patnobef file                     *
.******************************************************************************
WARDB000  PACK      KEY13,DIM3,SP20
          CALL      RDSNOBE1
WARDB100  CALL      RDKNOBE1
          BRANCH    OVRCD,WARDB999
.
          MATCH     DIM3,NBWARD
          GOTO      WARDB999 IF NOT EQUAL
.
          PACK      KEY8,NBADMNO
          CALL      RDMISS1
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
.
          MOVE      NBWARD,WARD  
          CALL      SETLAB00
          CALL      PRTLAB00
.
          GOTO      WARDB100
. 
WARDB999  RETURN
+
.
SETLAB00  PACK      WLINEZ1,WARD,SLASH,WBED
          RESET     WLINEZ1,8
.         PACK      DIM8,WARD,SLASH,WBED.
          PACK      WLINEZ2,WBDESC
          RESET     WLINEZ2,20
.
          MOVE      PSNAME,WLINEZ3
          RESET     WLINEZ3,16
.
          MOVE      PGNAME,WLINEZ4
          RESET     WLINEZ4,20
.
          MOVE      AURNO,WLINEZ5
          RESET     WLINEZ5,8
.
.          UNPACK    NOLABEL,DIM1,ZCOPY
.          REP       " 0",ZCOPY
          MOVE      "01",ZCOPY
.
SETLAB99  RETURN
.
PRTLAB00  PRINT     *1,ZXA:
                    *N,"^FO001,037^A0,20,20",ZFD,WLINEZ1,ZFS:
                    *N,"^FO001,062^A0,20,20",ZFD,WLINEZ2,ZFS:
                    *N,"^FO001,087^A0,20,20",ZFD,WLINEZ3,ZFS:
                    *N,"^FO001,112^A0,20,20",ZFD,WLINEZ4,ZFS:
                    *N,"^FO001,137^A0,20,20",ZFD,WLINEZ5,ZFS:
                    *N,"^PQ",ZCOPY:
                    *N,ZXZ
PRTLAB99 RETURN
.
...........................................................................
          INC       STD001IO
          INC       STDKWSCD
          INC       STDHLPCD
          INC       IBAPRINT
          INC       IBAPRNIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC    ***
          INC       PMSVX1IO/INC    ***
          INC       PATWR1IO/INC
          INC       PATNOBIO/INC
          INC       PATTRNIO/INC
          INC       PATDO1IO/INC
          INC       PATWRDKY

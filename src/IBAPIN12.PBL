. **********************************************************************
. * System    :   Private Practice/Accounting System Interface         *
. * Program   :   IBAPIN12                                             *
. * Function  :   Interface Listing                                    *
. **********************************************************************
. * Date      :   13/08/92                                             *
. * Author    :   DIG                                                  *
. * Comments  :   This program was directly copied from IBAPIN02 and   *
. *               modified for Private Practice                        *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.01  18/10/2001  J.Tan                                        *
. *           Mods to keyin free format accounts if not using fms      *
. * V9.02.00  09/10/2001  J.Tan                                        *
. *           Ported from 5.09                                         *
. * V5.09.B02 15/01/2001  Charles Handaya                              *
. *           BAS Statement Modification                               *
. * V5.09.B01 15/12/2000  Sandra Barcham                               *
. *           Changed file from fmspataf to patfmsaf                   *
. **********************************************************************
. * V5.07.B01 28/03/2000  Jill Habasque                                *
. *           GST Mods                                                 *
. * V5.05.01  07/10/1997  Steve Armstrong       WWM mods for G/L       *
. *           Mods for changes to FMSPRIFD                             *
. **********************************************************************
. Standard Accounting Include
.----------------------------
          INC       STDGENDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       IBABASFD/INC    * BAS Code
.
          INC       FMSAMAFD/INC    * Accounts
          INC       FMSDISFD/INC    * Dissection
          INC       FMSLMAFD/INC    * Ledger
          INC       FMSRESFD/INC    * Responsibility
.
          INC       PRIFMSFD/INC    * Interface File
.
          INC       PRICONFD/INC
          INC       PRIITMFD/INC
          INC       PATCODFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
FILEOPEN  DIM       30
FMSAMA    INIT      "fmsamaaf"
FMSDIS    INIT      "fmsdisaf"
FMSLMA    INIT      "fmslmaaf"
FMSRES    INIT      "fmsresaf"
ALAB      INIT      "            "                * labels for screen A
ALAB1     INIT      "Print Report"
KEY3A     DIM       3
.
OPTIONA   FORM      1        * option on screen A
.
IBCNUBAS  FORM      1
IBCNUGST  FORM      1
DBASDESC  DIM       30       * default BAS description
IBASDESC  DIM       30
IDIDESC   DIM       35
IREDESC   DIM       35
ILDDESC   DIM       35
ICADESC   DIM       35
IDADESC   DIM       35
IGSDESC   DIM       35
TDIDESC   DIM       35
TREDESC   DIM       35
TLDDESC   DIM       35
TCADESC   DIM       35
TDADESC   DIM       35
PTCNUFMS  FORM      1
.
CODEA     INIT      "A "
CODEJ     INIT      "J "
CODECL    INIT      "CL"
CODEFI    INIT      "FI"
CODEPG    INIT      "PG"
AMA       INIT      "AMA"
MBS       INIT      "MBS"
SP11      INIT      "           "
SECTION   FORM      1
ITEMTYPE  DIM       3
LASTSEC   FORM      1
SAVSECT   FORM      1
WARD      DIM       3
CLAIM     DIM       3
TODAY     DIM       8
NINE8     INIT      "99999999"
.
PRGID     INIT      "IBAPIN12"
PRGNAM    INIT      "Private Practice Interface Master Listing"
VERSION   INIT      "V12.01.00"
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SELA0000      * display screen A and get "report by" option
          BRANCH    EXIT,ML9999   * user wants to exit screen
.
          CALL      PRNT0000      * print report
          GOTO      ML1000        * get another option
.
ML9999    CHAIN     PGM
.******************************************************************************
.* INIT - Open Files                                                          *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P30:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*85,IBCNUGST,*107,IBCNUBAS
          READ      CONTROLF,THIRTY4;*166,PRCNGLIN,PRCNGLPA
          READ      CONTROLF,FIFTY9;*60,PTCNUFMS
.
          DISPLAY   *P38:24,"ibabasaf";
          OPEN      IBABASA1,"ibabasaf"
.
          COMPARE   ONE,PRCNGLIN
          GOTO      INIT1000 IF EQUAL
          DISPLAY   *P1:24,*B,*EL,"General Ledger Interface is Turned Off - ";
          CALL      HITENTER
          GOTO      ML9999       * Abort Program
.
INIT1000  MOVE      TWENTY,LLCNT
INIT1100  RESET     PRCNGLPA,LLCNT
          CMATCH    SP1,PRCNGLPA
          GOTO      INIT1200 IF NOT EQUAL
          SUB       ONE,LLCNT
          COMPARE   ZERO,LLCNT
          GOTO      INIT1100 IF NOT EQUAL
          CLEAR     PRCNGLPA
          GOTO      INIT1300
.
INIT1200  LENSET    PRCNGLPA
          RESET     PRCNGLPA
.
INIT1300  DISPLAY   *P38:24,"prifmsaf";
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIFMSA1,"prifmsaf"
          TRAPCLR   IO
          BRANCH    OVRCD,INIT8000
.
          PACK      FILEOPEN,PRCNGLPA,FMSAMA
          DISPLAY   *P38:24,FILEOPEN;
          OPEN      FMSAMAA1,FILEOPEN
          OPEN      FMSAMAA2,FILEOPEN
          OPEN      FMSAMAA3,FILEOPEN
          OPEN      FMSAMAA4,FILEOPEN
.
          PACK      FILEOPEN,PRCNGLPA,FMSDIS
          DISPLAY   *P38:24,FILEOPEN;
          OPEN      FMSDISA1,FILEOPEN
.
          PACK      FILEOPEN,PRCNGLPA,FMSLMA
          DISPLAY   *P38:24,FILEOPEN;
          OPEN      FMSLMAA1,FILEOPEN
.
          PACK      FILEOPEN,PRCNGLPA,FMSRES
          DISPLAY   *P38:24,FILEOPEN;
          OPEN      FMSRESA1,FILEOPEN
.
          DISPLAY   *P38:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P38:24,"priitemf";
          OPEN      PRIITEM1,"priitemf"
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          GOTO      INIT9999
.
INIT8000  DISPLAY   *P1:24,*EL,*B,"File Not Found, prifmsaf - ";
          CALL      HITENTER
          GOTO      ML9999       * Abort Program
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.**********************************************************************
. SELA -  display screen A and get "print by" option   Called By ML
.         Returns : EXIT       (0=continue, 1=quit)
.**********************************************************************
SELA0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = ",ALAB1:
                    *P1:7,"Select Option :";
.
SELA1000  KEYIN     *P17:7,"_",*EL,*P17:7,*V2LON,OPTIONA; * read option
          COMPARE   OPTIONA,ZERO
          GOTO      SELA9500 IF EQUAL            * user wants to exit ?
.
          PACK      ALAB,SP70                    * clear screen label
          LOAD      ALAB,OPTIONA,ALAB1           * get screen label
.
          MATCH     SP70,ALAB
          GOTO      SELA9000 IF NOT EQUAL        * legal option ?
.
          BEEP                                   * illegal option
          GOTO      SELA1000                     * get another option
.
SELA9000  MOVE      ZERO,EXIT                    * continue
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * quit
.
SELA9999  RETURN
.********************************************************************
. PRNT - print report                               Called by ML
.********************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :   ";
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      "999",CLNO                   * init last line number
.
          CALL      FILE0000                     * go to correct posy in file
.
PRNT1000  CALL      NREC0000                     * get next record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          DISPLAY   *P61:24,*V2LON,PRFMSUB,SP1,PRFMCOD,*EL;
          COMPARE   "45",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS
.
          CALL      WRIT0000                     * print details
          GOTO      PRNT1000                     * get next record
.
PRNT9000  COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL          * print header if nothing printed
          CALL      UND80                       * print line
          PRINT     "****   End of Report   ****"
.
PRNT9999  RETURN
.**********************************************************************
.  FILE - Set file pointer                             Called By PRNT
.**********************************************************************
FILE0000  
.
.---- print by type 1 ----
.
FILE0100  PACK      KEY18,SP70
          CALL      RSPRFM1
          GOTO      FILE9999
.
FILE9999  RETURN
.**********************************************************************
.  NREC - Get next record                              Called By PRNT
.**********************************************************************
NREC0000  
.
.---- print by type 1 ----
.
NREC0100  CALL      RKPRFM1
          BRANCH    OVRCD,NREC9500
          COMPARE   "1",PRFMSUB
          GOTO      NREC1000 IF EQUAL
          COMPARE   "3",PRFMSUB
          GOTO      NREC2000 IF EQUAL
          GOTO      NREC0100
.
NREC1000  MOVE      ONE,SECTION
          MATCH     SP70,PRFMCOD
          GOTO      NREC1100 IF NOT EQUAL
          MOVE      ONE,SAVSECT    * Default
.
          MOVE      SP70,DBASDESC
          PACK      KEY3,PRFMBASC,SP70
          CALL      RDIBBA1
          MOVE      IBBADESC,DBASDESC    * save default BAS Description
          MOVE      IBBADESC,IBASDESC
.
          GOTO      NREC5000
.
NREC1100  PACK      KEY3,PRFMBASC,SP70
          CALL      RDIBBA1
          IF        OVRCD=0
            MOVE      IBBADESC,IBASDESC
          ELSE
            MOVE      DBASDESC,IBASDESC       * display default BAS Description
          ENDIF
.
          UNPACK    PRFMCOD,KEY3,KEY14
          MATCH     SP70,KEY14
          GOTO      NREC1200 IF NOT EQUAL
          MOVE      TWO,SAVSECT
          PACK      KEY5,CODEPG,KEY3,SP70
          CALL      RDCODE1
          GOTO      NREC5000
.
NREC1200  UNPACK    PRFMCOD,KEY3A,KEY2,KEY9,KEY3
          MOVE      THREE,SAVSECT
          PACK      KEY22,KEY2,KEY9,KEY3,NINE8
          CALL      RSPRIT1
          CALL      RPPRIT1
          BRANCH    OVRCD,NREC1250
.
          MOVE      KEY2,FORM2
          COMPARE   FORM2,PRITFLAG
          GOTO      NREC1250 IF NOT EQUAL
.
          MATCH     KEY9,PRITITMN
          GOTO      NREC1250 IF NOT EQUAL
.
          MATCH     KEY3,PRITSUBN
          GOTO      NREC1250 IF NOT EQUAL
.
          GOTO      NREC5000
.
NREC1250  MOVE      "Invalid Code",PRITDESC
          GOTO      NREC5000
.         
NREC2000  MOVE      TWO,SECTION
          MATCH     SP70,PRFMCOD
          GOTO      NREC2100 IF NOT EQUAL
          MOVE      ONE,SAVSECT    * Default
.
          MOVE      SP70,DBASDESC
          PACK      KEY3,PRFMBASC,SP70
          CALL      RDIBBA1
          IF        OVRCD=0
            MOVE      IBBADESC,DBASDESC       * save default BAS Description
            MOVE      IBBADESC,IBASDESC
          ELSE
            MOVE      DBASDESC,IBASDESC       * display default BAS Description
          ENDIF
.
          GOTO      NREC5000
.
NREC2100  PACK      KEY3,PRFMBASC,SP70
          CALL      RDIBBA1
          IF        OVRCD=0
            MOVE      IBBADESC,IBASDESC
          ELSE
            MOVE      DBASDESC,IBASDESC       * display default BAS Description
          ENDIF
.
          MOVE      TWO,SAVSECT    * Default
          MOVE      PRFMCOD,KEY2
          PACK      KEY5,CODEJ,KEY2,SP70
          CALL      RDCODE1
          GOTO      NREC5000
.
NREC5000  MOVE      SP70,FMDSDESC
          MOVE      SP70,FMLADESC
          MOVE      SP70,FMRSDESC
          MOVE      SP70,FMAMDESC
          MOVE      PRFMIDI,KEY5
          CALL      RDFMDS1
          MOVE      FMDSDESC,IDIDESC
.
          MOVE      PRFMTDI,KEY5
          CALL      RDFMDS1
          MOVE      FMDSDESC,TDIDESC
.
          MOVE      PRFMIRE,KEY4
          CALL      RDFMRS1
          MOVE      FMRSDESC,IREDESC
.
          MOVE      PRFMTRE,KEY4
          CALL      RDFMRS1
          MOVE      FMRSDESC,TREDESC
.
          MOVE      PRFMILD,KEY2
          CALL      RDFMLA1
          MOVE      FMLADESC,ILDDESC
.
          MOVE      PRFMTLD,KEY2
          CALL      RDFMLA1
          MOVE      FMLADESC,TLDDESC
.
          PACK      KEY14,PRFMILD,PRFMIDA
          CALL      RDFMAM1
          MOVE      FMAMDESC,IDADESC
.
          PACK      KEY14,PRFMILD,PRFMICA
          CALL      RDFMAM1
          MOVE      FMAMDESC,ICADESC
.
          PACK      KEY14,PRFMILD,PRFMIGS
          CALL      RDFMAM1
          MOVE      FMAMDESC,IGSDESC
.
          PACK      KEY14,PRFMTLD,PRFMTDA
          CALL      RDFMAM1
          MOVE      FMAMDESC,TDADESC
.
          PACK      KEY14,PRFMTLD,PRFMTCA
          CALL      RDFMAM1
          MOVE      FMAMDESC,TCADESC
.
          COMPARE   SECTION,LASTSEC
          GOTO      NREC9000 IF EQUAL
          MOVE      SECTION,LASTSEC
          MOVE      "99",CLNO
          GOTO      NREC9000
.
NREC9000  MOVE      ZERO,OVRCD         * continue
          GOTO      NREC9999
.
NREC9500  MOVE      ONE,OVRCD          * quit
.
NREC9999  RETURN
.********************************************************************
. HEAD - print report                               Called by PRNT
.********************************************************************
HEAD0000  CALL      HEAD80                      * print heading
.
          CALL      UND80                       * print line
          BRANCH    SECTION,HEAD1000,HEAD2000
HEAD1000  PRINT     "| Items ",*80,"|"
          GOTO      HEAD4000
HEAD2000  PRINT     "| Journals",*80,"|"
HEAD4000
          ADD       ONE,CLNO
.
HEAD9999  RETURN
.********************************************************************
. WRIT - print data line                            Called by PRNT
.********************************************************************
WRIT0000  
          CALL      UND80                       * print line
          BRANCH    SECTION,WRIT1000,WRIT2000
WRIT1000  BRANCH    SAVSECT,WRIT1100,WRIT1200,WRIT1300
.
WRIT1100  PRINT     "| Default Item Interface Details",*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT1200  PRINT     "| Set Code                   : ",KEY3,SP10,TDESC,*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT1300  MOVE      MBS,ITEMTYPE
          LOAD      ITEMTYPE,PRITFLAG,AMA
          PRINT     "| Item Type                  : ",PRITFLAG,SP11,ITEMTYPE:
                       *80,"|":
                    *N,"| Item Code                  : ",PRITITMN,SP4,PRITDESC:
                       *80,"|":
                    *N,"| Sub-Item Code              : ",PRITSUBN,*80,"|"
          ADD       THREE,CLNO
          GOTO      WRIT5000
.
WRIT2000  BRANCH    SAVSECT,WRIT2100,WRIT2200
WRIT2100 
          PRINT     "| Default Journal Interface Details",*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
WRIT2200  
          PRINT     "| Journal Code               : ",ACODE,SP1,TDESC,*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT5000  PRINT     "|",*80,"|":
                    *N,"| Invoiced       Dissection  : ":
                    *32,PRFMIDI,*45,IDIDESC,*80,"|":
                    *N,"|                Resp.       : ":
                    *32,PRFMIRE,*45,IREDESC,*80,"|":
                    *N,"|                Ledger      : ":
                    *32,PRFMILD,*45,ILDDESC,*80,"|";
.
          IF        IBCNUGST=2 & SECTION<>2
            PRINT     *N,"|                Income Acct : ":
                      *32,PRFMICA,*45,ICADESC,*80,"|";
.
            IF        PTCNUFMS=1
              PRINT    *N,"|      Debtors Control Acct  : ":
                        *32,PRFMIDA,*45,IDADESC,*80,"|":
                        *N,"|          GST Control Acct  : ":
                        *32,PRFMIGS,*45,IGSDESC,*80,"|";
            ENDIF
          ELSE
            PRINT     *N,"|                Credit Acct : ":
                      *32,PRFMICA,*45,ICADESC,*80,"|":
                      *N,"|                Debit Acct  : ":
                      *32,PRFMIDA,*45,IDADESC,*80,"|";
          ENDIF
          IF        IBCNUBAS=1 & SECTION <>2
            PRINT     *N,"|                   BAS Code : ":
                      *32,PRFMBASC,*45,IBASDESC,*80,"|"
            ADD       ONE,CLNO
          ENDIF
.
          COMPARE   TWO,SECTION
          GOTO      WRIT9000 IF EQUAL      * Journal has no To be invoiced
.
          PRINT     *N,"| To be Invoiced Dissection  : ":
                    *32,PRFMTDI,*45,TDIDESC,*80,"|":
                    *N,"|                Resp.       : ":
                    *32,PRFMTRE,*45,TREDESC,*80,"|":
                    *N,"|                Ledger      : ":
                    *32,PRFMTLD,*45,TLDDESC,*80,"|":
                    *N,"|                Credit Acct : ":
                    *32,PRFMTCA,*45,TCADESC,*80,"|":
                    *N,"|                Debit Acct  : ":
                    *32,PRFMTDA,*45,TDADESC,*80,"|"
WRIT9000  ADD       TEN,CLNO
WRIT9999  RETURN
.

.******************************************************************************
.  INCLUDES FOR I/O'S
.******************************************************************************
.---------------------------------
. Standard Accounting Code include
.---------------------------------
          INC       STDGENCD
.
          INC       IBABASIO/INC    * BAS Code
.
          INC       FMSAMAIO/INC    * Accounts
          INC       FMSDISIO/INC    * Dissection
          INC       FMSLMAIO/INC    * Ledger
          INC       FMSRESIO/INC    * Responsibility
.
          INC       PRIFMSIO/INC    * Interface File
.
          INC       PATCODIO/INC
.
          INC       PRIITMIO/INC

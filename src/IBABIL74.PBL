.******************************************************************************
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*      PROGRAM NAME:     IBABIL74                                            *
.*                                                                            *
.*      FUNCTION:         MIDNIGHT CENSUS                                     *
.*                                                                            *
.*      DATE WRITTEN:     30/07/87                                            *
.*                                                                            *
.*      AUTHOR:           J.TAN    (I.B.A.)                                   *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.*        V12.00.01 15.05.2025  DA Sarkies     TSK 0955096                    *
.                   Updated for Alpha-Numeric Visit Number                    *
.*        V10.05.01 02/02/2015  Ania P         CAR 261630                     *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.03.05 07/08/2013 Ania P          CAR 289561                     *
.*                  Changed column widths                                     *
.*        V10.03.04 25/07/2013 Peter Vela      CAR 289011                     *
.*                  Changed Ward column to display code rather than desc      *
.*        V10.03.03 16/06/2013 Peter Vela      CAR 287136                     *
.*                  Fixed Ward Only loop in WARD0000                          *
.*        V10.03.02 27/06/2013 Ebon Clements   CAR 287511                     *
.*                  Changes for WEXTN and NSEXTN length change from 4 to 20   *
.*        V10.03.01 22/05/2013 J.Tan           CAR 284677                     *
.*                  Mods to recalculate patient age                           *
.*        V10.02.04 08/07/2011 Peter Vela      CAR 240705                     *
.*                  Fixed column layouts                                      * 
.*        V10.02.03 25/06/2011 Steve Armstrong CAR 240722                     *
.*                  Removed reference to PATLOCFD.                            *
.*        V10.02.02 22/06/2011 Peter Vela      CAR 240705                     *
.*                  Added Total Boarders                                      *
.*        V10.02.01 21/06/2011 Peter Vela      CAR 240705                     *
.*                  Added Hospital Boarders                                   *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*        V9.04.01  02/09/2004  Ebon Clements    CAR 53144                    *
.*                  Added Multi-Hospital processing                           *
.******************************************************************************
.*        V5.10.B01 29/03/2001  Dean Cramer                                   *
.*                  Mods to fix where single ward print sometimes does not    *
.*                  work but says "No available data on File".                *
.*        V5.08.03  06/12/2000  Caleb Sharman                                 *
.*                  Mods to use keyin routine for nursing station entries     *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*                    V5.07.B02 27/01/1999 N Harrington                       *
.*                              507 rebounds                                  *
.******************************************************************************
. *                       W2.01 06/01/88 G.Williams (I.B.A)                   *
. *                             Fix bug with Total Patients                   *
. *                       W2.02 15/03/88 J.Clark (I.B.A.)                     *
. *                             Include MAT files                             *
. *                       W2.03 14/07/88 G.Williams (I.B.A)                   *
. *                             Added new file - PATNOBFD                     *
. *                       W2.04 19/09/88 J.Tan      (I.B.A)                   *
. *                             Mods to print total patient                   *
. *                             and fix bugs in nursing st                    *
. *                       V4.01 20/10/88 Graeme Williams                      *
. *                             Fix up maternity interface                    *
. *                       V4.02 01/12/88 M.HAYSE (I.B.A)                      *
. *                             CHECK STANBY  S:005530                        *
. *                       V4.03 21/12/88 J.Tan   (I.B.A.)                     *
. *                             Print Tot patient & Standby                   *
. *                             SRF 005589                                    *
. *                       V5.00 16/05/89 DIG                                  *
. *                             Changed to standard and                       *
. *                             modified print for 8 digit                    *
. *                             admission numbers                             *
. *                       V5.01 27/05/89 J.Tan                                *
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMI1FD).     SRF 100750                    *
. *                       V5.02 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                       V5.03 09.06.89 S.Barcham                            *
. *                             Fixed MATADM includes                         *
. *                             SRF 100916                                    *
. *                       V5.04 17/06/89 J.Tan                                *
. *                             Removed Standby file                          *
. *                             SRF 101101                                    *
. *                    V5.01.01 06/07/89 K.Peak                               *
. *                             Changed Adm & U/R to 8 chrs                   *
. *                    V5.01.02 30/08/89 K.Peak                               *
. *                             Added grand totals                            *
. *                             SRF 102162                                    *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *                   V5.01.122 11/06/93 J.Tan  SRF 121660                    *
. *                             Mods to check for active                      *
. *                             ward/bed                                      *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATBRDFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATNOBFD/INC
          INC       PATNURFD/INC
          INC       PATONLFD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       PATHSPFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
+
.  Tempfile Midnight Census Report Hospital Boarders
.
SECTION1  IFILE     SQL,FIXED=22, KEY="U1-8"
.
.Name     Type      Length    Physical  Position  Description
.----     ----      ------    --------  --------  -----------
XADMN     DIM       8            8         1      Admission Number
XWRDESC   DIM       10           10        9      Ward Desc
XBED      DIM       3            3         19     Bed 
.
.End of Record                             22
+
SECTIO1   INIT      "secti1"
.**********************************************************************
.*                            VARIABLES                               *
.**********************************************************************
ADIAG     DIM       50
AJDAY     FORM      3                  * JULIAN ADMISSION DAY
.
BJDAY     FORM      3
.
CDESC     DIM       20
CJDAY     FORM      3                  * JULIAN CURRENT DAY
COUNT     FORM      2
CMDLINE   DIM       50
.
DATYPE    DIM       3
DHHMM     DIM       5
DNAME15   DIM       15                 * DOCTOR NAME SHORTENED
DPSKIP    DIM       3
DRLGTYP   DIM       30
DSEX      DIM       6
DIM3      DIM       3
DIM8      DIM       8
DIM10     DIM       10
DIM12A    DIM       12
DIM12B    DIM       12
DIM18     DIM       18
.
EOFNUR    DIM       1
EOFWR1    DIM       1
..
GRDPAT    FORM      4                  * grand total of patients
GRDSTBY   FORM      4                  * grand total of standby patients
GRDLVE    FORM      4                  * grand total of patients on leave
.
IBCNMHOS  FORM      1                  * Multi hospital indicator from controlf
.
LOCDESC   DIM       25
MIN1      FORM      1
.
PAGENO    FORM      3
PHDAYS    FORM      3
PNAME     DIM       33                 * Formatted Patient's Name
PSKIP     FORM      1
.
SAVBED    DIM       3
SAVNDESC  DIM       30
SAVNEXTN  DIM       20
SAVNSIST  DIM       30
SAVNSTAT  DIM       3
SAVWEXTN  DIM       20
SAWARD    DIM       3
SBED      DIM       3
SRLGTYP   DIM       3

SWARD     DIM       3
.
TOTPAT    FORM      3                  * total patients
TOTSTBY   FORM      3                  * total standby patients
TOTLVE    FORM      3                  * total patients on leave
TOTBRD    FORM      3                  * total boarders
.
WRDESC    DIM       32
WRSDESC   DIM       10
.
XNAME     DIM       25
+
.**********************************************************************
.*                            CONSTANTS                               *
.**********************************************************************
CODEA     INIT      "A "
CODECO    INIT      "CO"                    * patient condition category
CODEM     INIT      "M "
INVUR     INIT      "Invalid UR -"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
Z22       INIT      "zzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBABIL74"
PRGNAM    INIT      "Midnight Census Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                               ML0000                               *
.*                              Mainline                              *
.**********************************************************************
ML0000    CALL      INIT0000           * initialization
.
ML1000    CALL      CLR0000            * clear variables
          CALL      OPTN0000           * select option
          BRANCH    EXIT,ML9000
          BRANCH    OPTION,ML2000,ML3000,ML4000,ML5000
.
.         Particular Ward
.
ML2000    CALL      PWRD0000
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
.         Particular Nursing Station
.
ML3000    CALL      PNUR0000
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
.         All Nursing Station
.
ML4000    CALL      ANUR0000
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
.         All Ward
.
ML5000    CALL      AWRD0000
          BRANCH    EXIT,ML1000
.
ML6000    CALL      END0000            * end of report
.
          CALL      KILL0000               * delete the tempfiles
          GOTO      ML1000
.
.         ....The End
.
ML9000    CHAIN     PGM
+
.**********************************************************************
.*                       INIT0000                                     *
.*                    Initialisation                                  *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patbrdaf";
          OPEN      PATBRDA1,"patbrdaf"
          OPEN      PATBRDA2,"patbrdaf"
          OPEN      PATBRDA3,"patbrdaf"
          OPEN      PATBRDA4,"patbrdaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patnursm";
          OPEN      PATNURS1,"patnursm"
.
          DISPLAY   *P64:24,"patonlvf";
          OPEN      PATONLV1,"patonlvf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN;*13,PTCNTLVE
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TWENTY;*213,PTCNDPCN
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          MOVE      "99",CLNO               * force new page
.
          CALL      TFILENAM
          CALL      CREA0000                  * create/open temp files
.
          RETURN
+
.**********************************************************************
.*                       CLR0000                                      *
.*                    Clear Variables                                 *
.**********************************************************************
CLR0000   MOVE       ZERO,PAGENO           * page number
          PACK       WRDESC,SP30,SP30       * Ward Name
          MOVE       SP20,WRSDESC
          MOVE       ZERO,TOTPAT            * Total Patients
          MOVE       ZERO,TOTSTBY           * Total Standby Patients
          MOVE       ZERO,TOTLVE
          MOVE       ZERO,GRDPAT            * Grand Patients Total
          MOVE       ZERO,GRDSTBY           * Grand Standby Patients Total
          MOVE       ZERO,GRDLVE
          RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                         Select OPtioN                              *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          CALL       SCR0000
.
OPTN1000  KEYIN      *P17:10,*EL,*DV,UNDLN1,*P17:10,*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9000 IF EQUAL
.
          BRANCH     OPTION,OPTN9999,OPTN9999,OPTN9999,OPTN9999
          BEEP
          GOTO       OPTN1000
.
OPTN9000  MOVE       TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                            SCR0000                                 *
.*                         SCReeN options                             *
.**********************************************************************
SCR0000   DISPLAY    *P1:3,*EF:
                     *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF," = Particular Ward":
                     *P1:6,*V2LON,TWO,*HOFF," = Particular Nursing Station":
                     *P1:7,*V2LON,THREE,*HOFF," = All Nursing Stations":
                     *P1:8,*V2LON,FOUR,*HOFF," = All Wards":
                     *P1:10,"Select Option : ";
          RETURN
+
.**********************************************************************
.*                       PWRD0000                                     *
.*                    Keyin Paricular Ward                            *
.**********************************************************************
PWRD0000 MOVE       "Particular Ward",CPHDROPT
         MOVE      FALSE,EXIT
         KEYIN      *P1:13,*EF,"Enter Ward Number : ",*P40:13,"( ",*V2LON,"?":
                    *HOFF," for Scan )",*P21:13,*V2LON,SAWARD,*EL;
         RESET      SAWARD
         GOTO       PWRD9000 IF EOS
.
         ENDSET     SAWARD
         APPEND     SP3,SAWARD
         RESET      SAWARD
.
         MATCH      SP3,SAWARD
         GOTO       PWRD9000 IF EQUAL
.
         CMATCH     QUEST,SAWARD
         GOTO       PWRD1000 IF NOT EQUAL
         CALL       PATWRDDS
         CALL       SCR0000
         DISPLAY    *P17:10,*V2LON,OPTION;
         GOTO       PWRD0000
.
PWRD1000 PACK       KEY6 WITH SAWARD,SP6
         CALL       RDWARD1
         BRANCH     OVRCD,PWRD8000
.
         BRANCH     WACTIVE,PWRD8500       * Ignore non-active wards
.
         MOVE       ONE,PSKIP
         MOVE       WBDESC,WRDESC
         MOVE       WWARD,WRSDESC
         MOVE       WEXTN,SAVWEXTN
         DISPLAY    *P30:13,*EL,WRDESC;
.
PWRD2000 MOVE       "60",CLNO
         DISPLAY    *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :":
                    *P50:24,"Adm : ";
         CALL       WARD0000
         GOTO       PWRD9000
.
PWRD8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Ward does not exist **    ";
          CALL      HITENTER
          GOTO      PWRD0000
.
PWRD8500  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Ward is not active **    ";
          CALL      HITENTER
          GOTO      PWRD0000
.
PWRD9000  MOVE      TRUE,EXIT
.
PWRD9999  RETURN
+
.**********************************************************************
.*                             PNUR0000                               *
.*                  Keyin Paricular Nursing Station                   *
.**********************************************************************

PNUR0000  MOVE      "Particular Nurs. St.",CPHDROPT
          MOVE      FALSE,EXIT
          KEYIN     *P1:13,*EF,"Enter Station Number : ",*P40:13,"( ",*V2LON:
                    "?",*HOFF," for Scan )",*P24:13,*V2LON,SAVNSTAT,*EL;
          RESET     SAVNSTAT
          GOTO      PNUR9000 IF EOS
.
          ENDSET    SAVNSTAT
          APPEND    SP3,SAVNSTAT
          RESET     SAVNSTAT
.
          MATCH     SP3,SAVNSTAT
          GOTO      PNUR9000 IF EQUAL
.
          CMATCH    QUEST,SAVNSTAT
          GOTO      PNUR1000 IF NOT EQUAL
          CALL      PATNURDS
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          CALL      SCR0000
          DISPLAY   *P17:10,*V2LON,OPTION;
          GOTO      PNUR0000
.
PNUR1000  MOVE      SP3,NSTAT
          MOVE      SP3,NWARD
          MOVE      SP3,SAWARD
.
          PACK      KEY6 WITH SAVNSTAT,SP3
          CALL      RDNURS1
          BRANCH    OVRCD,PNUR8000
          DISPLAY   *P32:13,*EL,NSDESC;
          MOVE      NSDESC,SAVNDESC         * N/Station description
          MOVE      NSIST,SAVNSIST          * Sister-in-Charge
          MOVE      NSEXTN,SAVNEXTN         * Ward extension
.
          CALL      SEP0000                 * separate page ?
.
PNUR2000  MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
          CALL     NURS0000
.
          GOTO     PNUR9000
.

.
PNUR8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Station does not exist **    ";
          CALL      HITENTER
          GOTO      PNUR0000
.
PNUR9000  MOVE      TRUE,EXIT
.
PNUR9999  RETURN
+
.**********************************************************************
.*                            AWRD0000                                *
.*                           All Wards                                *
.**********************************************************************
AWRD0000  MOVE      "All Wards",CPHDROPT
          CALL      KHOS0000                * Check which hospital
          BRANCH    EXIT,AWRD9999
          MOVE      FALSE,EXIT
          CALL      SEP0000                 * Separate page ?
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
          MOVE      SP3,SAWARD
          MOVE      SP6,KEY6
          CALL      RDSWARD1
          CALL      RDKWARD1                * read first ward record
          BRANCH    OVRCD,AWRD9999
.
          MOVE      SP1,EOFWR1
AWRD2000  CALL      WARD0000
          MATCH     SP1,EOFWR1
          GOTO      AWRD2000 IF EQUAL
.         BRANCH    EXIT,AWRD2000
.
          CALL      GTOT0000                * grand totals
.
AWRD9000  MOVE      ONE,EXIT
.
AWRD9999  RETURN
+
.**********************************************************************
.*                       ANUR0000                                     *
.*                   All Nursing Stations                             *
.**********************************************************************
ANUR0000  MOVE      "All Nursing Stations",CPHDROPT
          MOVE      FALSE,EXIT
          CALL      SEP0000
.
ANUR0500  DISPLAY   *P1:24,*EL,"Station : ",*P20:24,"Ward :",*P40:24,"Bed :";
.
          MOVE      SP6,KEY6
          CALL      RDSNURS1
.
ANUR1000  CALL      RDKNURS1
          BRANCH    OVRCD,ANUR9000
.
ANUR1001  CALL      NURS0000
          BRANCH    EXIT,ANUR1001
.
ANUR9000  MOVE      ONE,EXIT
.
ANUR9999  RETURN
+
.**********************************************************************
.*                             HEAD0000                               *
.*                        Heading for report                          *
.**********************************************************************
HEAD0000  CALL      HEADP000   * std page header
.
          CALL      HEADW000
.
HEAD9999  RETURN
+
.**********************************************************************
.*                             HEADA000                               *
.*                        Heading for report                          *
.**********************************************************************
HEADA000  CALL      HEADP000   * std page header
.
          CALL      HEADB000
.
HEADA999  RETURN
+
.**********************************************************************
.*                             HEADP000                               *
.*                        Just the top heading                        *
.**********************************************************************
HEADP000  ADD       ONE,PAGENO
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      HEAD132
          MOVE      FOUR,CLNO
.
          BRANCH    OPTION,HEADP999,HEADP100,HEADP100,HEADP999
          GOTO      HEAD9999
.
HEADP100  PRINT     *1,"SISTER : ",SAVNSIST,*40,"STATION : ",SAVNSTAT:
                    *55,SAVNDESC,*95,"Extension : ",SAVNEXTN,*N
          ADD       TWO,CLNO
.
HEADP999  RETURN
+
.**********************************************************************
.*                       HEADW0000                                    *
.*                    Print Ward Heading                              *
.**********************************************************************
HEADW000  PRINT     *40,"WARD    : ",WWARD,*55,WRDESC:
                    *95,"Extension : ",SAVWEXTN,*N
          ADD       TWO,CLNO
          CALL      PGE0000
.
          PRINT        *1,"| Ward",*9,"|Admission",*19,"|Patient's Name":
                      *53,"|Rel|Sex|Age|Admission Diagnosis":
                     *116,"|Attending",*132,"|":
                    *N,*1,"| /Bed",*9,"| Number",*19,"| ":
                      *53,"|   |M/F|   |":
                     *116,"|Doctor",*132,"|"
          CALL      PGE0000
.
HEADW900  ADD       TWO,CLNO
          RETURN
+
.**********************************************************************
.*                             PGE0000                                *
.*                       Print line separator                         *
.**********************************************************************
PGE0000   PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                       PRTB0000                                     *
.*                     Print Bed/Patient                              *
.**********************************************************************
PRTB0000  COMPARE   "58",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      KEY8,WADMNO
          PACK      DIM3,PSAGE
          SQUEEZE   PREG
          MATCH     ZEROVISN,WADMNO
          IF        @EQUAL
            PACK      KEY8,SP30
            PACK      DIM3,SP30
          ENDIF
.
          PRINT     *1,"|",WWARD,SP1,WBED,*9,"|",KEY8,*19,"|",PNAME:
                    *53,"|",PREG,*57,"|",PSEX,*61,"|",DIM3:
                    *65,"|",ADIAG:
                    *116,"|",DNAME15,*132,"|"
.
          MOVE      WWARD,SWARD
          ADD       ONE,CLNO
          CALL      PGE0000
          DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,SAVBED;
.
          PACK      KEY8,WADMNO,SP70
          CALL      RDASECT1
          IF        OVRCD=1
            MOVE      WADMNO,XADMN
            MOVE      WRSDESC,XWRDESC
            MOVE      SAVBED,XBED 
            CALL      WRSECT1
          ENDIF
.
PRTB999   RETURN
+
.**********************************************************************
.*                             TOT0000                                *
.*                           Print Total                              *
.**********************************************************************
TOT0000   COMPARE   "58",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *N,*44,"Total Boarders : ",TOTBRD:
                    *67,"Total In Ward : ",TOTPAT:
                    *90,"Total on Leave : ",TOTLVE:
                    *113,"Total Standby : ",TOTSTBY
          ADD       TWO,CLNO
.
TOT9999  RETURN
+
.**********************************************************************
.*                             GTOT0000                               *
.*                        Print Grand Total                           *
.**********************************************************************
GTOT0000  COMPARE   "57",CLNO
          CALL      HEADP000 IF NOT LESS
.
          PRINT     *N,*50,"GRAND TOTALS : ",*66,"Total in Ward : ",GRDPAT:
                                             *88,"Total on Leave : ",GRDLVE:
                                            *112,"Total Standby : ",GRDSTBY:
                    *N,*50,"------------"
          RETURN
+
.**********************************************************************
.*                             END0000                                *
.*                          End of Report                             *
.**********************************************************************
END0000   COMPARE   ZERO,PAGENO
          GOTO      NDAT0000 IF NOT EQUAL
          COMPARE   "57",CLNO
          CALL      HEADP000 IF NOT LESS
.
          PRINT     *N," ***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
END9999   RETURN
+
.**********************************************************************
.*                             NDAT0000                               *
.*                       No Data to be Printed                        *
.**********************************************************************
NDAT0000  DISPLAY   *P1:24,*EL,*P25:24,*B,*V2LON:
                    "** No available data on file **",*W2;
          RETURN
+
.*********************************************************************
.*                  VOLP0000                    Called by : lots     *
.*        Check that the patient is on Short-term leave.             *
.*        Para's  : OADMNO        Admission Number                   *
.*        Returns : EXIT = 1      not a valid patient                *
.*********************************************************************
VOLP0000  COMPARE   ONE,PTCNTLVE
          GOTO      VOLP9000 IF NOT EQUAL   * not using Type of Leave
.
          PACK      KEY30,OADMNO,Z22
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,VOLP9100          * no tran record
.
          MATCH     OADMNO,TADMN
          GOTO      VOLP9100 IF NOT EQUAL   * no tran record
.
          MATCH     "L",TMOVE
          GOTO      VOLP9100 IF NOT EQUAL   * not a leave record
.
          MATCH     SP3,PTTRLTYP
          GOTO      VOLP9100 IF EQUAL       * no on-leave type
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          BRANCH    OVRCD,VOLP9100          * invalid Type of Leave
.
          MATCH     SP1,TCINDC1
          GOTO      VOLP9100 IF NOT EQUAL   * not short term leave
.
VOLP9000  MOVE      FALSE,EXIT
          GOTO      VOLP9999
.
VOLP9100  MOVE      TRUE,EXIT
.
VOLP9999  RETURN
++
.**********************************************************************
.*                       NURS0000                                     *
.*                    Process One  Ward                               *
.* This routine assumes we are positioned on the ward master record   *
.* (NWARD = "   ") on entry.                                          *
.**********************************************************************
NURS0000 MOVE      FALSE,EXIT
         MOVE      NSTAT,SAVNSTAT
         MOVE      NSDESC,SAVNDESC         * N/Station description
         MOVE      NSIST,SAVNSIST          * Sister-in-Charge
         MOVE      NSEXTN,SAVNEXTN         * Ward extension
         MOVE      ZERO,GRDPAT             * Grand Patients Total
         MOVE      ZERO,GRDSTBY            * Grand Standby Patients Total
         MOVE      ZERO,GRDLVE
.
NURS1000 CALL      RDKNURS1
         IF        OVRCD  = 1
           MOVE      "Y",EOFNUR
         ENDIF
         BRANCH    OVRCD,NURS9000
         MATCH     NSTAT,SAVNSTAT
         GOTO      NURS9000 IF NOT EQUAL
.
         PACK      KEY6,NWARD,SP30
         CALL      RDWARD1
         BRANCH    OVRCD,NURS1000          * (should not happen)
         CALL      WARD0000
         GOTO      NURS1000
.
NURS9000 CALL      GTOT0000
         MOVE      TRUE,EXIT
         MATCH     "Y",EOFNUR
         IF        @EQUAL
           MOVE      FALSE,EXIT
         ENDIF
NURS9999 RETURN
+
.**********************************************************************
.*                       WARD0000                                     *
.*                    Process One  Ward                               *
.* This routine assumes we are positioned on the ward master record   *
.* (WBED = "   ") on entry.                                           *
.**********************************************************************
WARD0000 MOVE      FALSE,EXIT
.
         COMPARE   ZERO,WACTIVE
         GOTO      WARD0500 IF NOT EQUAL
.
         COMPARE   ONE,IBCNMHOS         * Using multi hospital
         GOTO      WARD1000 IF NOT EQUAL
.
         PACK      PTHSHOSP,PTHSHOSP,SP70
         MATCH     SP10,PTHSHOSP        * All Hospitals
         GOTO      WARD1000 IF EQUAL
.
         MATCH     PTWDHOSN,PTHSHOSP    * Correct hospital
         GOTO      WARD1000 IF EQUAL
.
. If we are processing a single ward we cannot select an inactive one, but
. process all wards will find them, and we just skip over the entire ward set.
.
WARD0500 PACK      KEY6,WWARD,Z70         * set key for end of this ward
         CALL      RDSWARD1
         CALL      RDKWARD1               * skip to next if any
         IF        OVRCD = 1
           MOVE      "Y",EOFWR1
         ENDIF
         BRANCH    OVRCD,WARD9000
         GOTO      WARD0000               * start again
.
WARD1000 MOVE      WBDESC,WRDESC
         MOVE      WWARD,WRSDESC
         MOVE      WEXTN,SAVWEXTN
         MOVE      WWARD,SAWARD
         MOVE      SP3,SAVBED
         DISPLAY   *P30:13,*EL,WRDESC;
.
         COMPARE   "53",CLNO              * all 4 for head, 2 for bed
         GOTO      WARD1100 IF NOT LESS
         CALL      HEADW0000
         GOTO      WARD1200
.
WARD1100 CALL      HEAD0000
.
WARD1200 STRIP     WRDESC
         ENDSET    WRDESC
         APPEND    " (continued)",WRDESC
         RESET     WRDESC
.
.  Find patients assigned to the ward, but without a bed
.
WARD2000 PACK      KEY13 WITH WWARD,SP10   * report the no-beds
         CALL      RDSNOBE1
WARD2100 CALL      RDKNOBE1
         BRANCH    OVRCD,WARD3000
.
         MATCH     WWARD,NBWARD
         GOTO      WARD3000 IF NOT EQUAL
         MOVE      NBADMNO,WADMNO
         CALL      GETP0000
         BRANCH    EXIT,WARD2100
         ADD       ONE,TOTPAT
         CALL      PRTB0000
         GOTO      WARD2100
.
WARD3000 PACK      KEY14,WWARD,SP20        * report patients on leave
..          PRINT     *2,"War31000 - read onleave PTCNTLVE=",PTCNTLVE
         CALL      RDSONLV1
WARD3100 CALL      RDKONLV1
         BRANCH    OVRCD,WARD4000
.
         MATCH     WWARD,OWARD
         GOTO      WARD4000 IF NOT EQUAL
         CALL      VOLP0000                * is patient on short term leave >
         BRANCH    EXIT,WARD3100           * not on short term leave
.
         MOVE      OADMNO,WADMNO
         MOVE      "LVE",SAVBED
         MOVE      SP20,WBDESC
         CALL      GETP0000
         BRANCH    EXIT,WARD3100
         ADD       ONE,TOTLVE
         CALL      PRTB0000
         GOTO      WARD3100

WARD4000 BRANCH    WINPUT,WARD8000         * if ward only, straight to totals
.

WARD5000 CALL      RDKWARD1
         IF        OVRCD = 1
           MOVE      "Y",EOFWR1
         ENDIF
         BRANCH    OVRCD,WARD8000
.
         MATCH     SP3,WBED
.        GOTO      WARD8000 IF EQUAL
         IF        @EQUAL
           COMPARE   ONE,WINPUT
           IF        @EQUAL
.
             CALL      PBRD0000               * Print Hospital Boarders
             ADD       TOTPAT,GRDPAT          * add for grand patient total
             ADD       TOTSTBY,GRDSTBY        * add for grand standby total
             ADD       TOTLVE,GRDLVE
             CALL      TOT0000
             MOVE      ZERO,TOTPAT            * Total Patients
             MOVE      ZERO,TOTSTBY           * Total Standby Patients
             MOVE      ZERO,TOTLVE
.                                  * if new page per ward, force headings
             IF        PSKIP = 0
               MOVE      "99",CLNO
             ENDIF
             GOTO      WARD9000
.
           ELSE
             GOTO      WARD8000
           ENDIF
         ENDIF
.
         MATCH     WWARD,SAWARD
         GOTO      WARD8000 IF NOT EQUAL
.
         BRANCH    WACTIVE,WARD5000        * Non-active bed, ignore
.
         MOVE      WBED,SAVBED
.
WARD6000 CALL      GETP0000
         MATCH     WADMNO,ZEROVISN
         IF        !@EQUAL
           ADD       ONE,TOTPAT
         ENDIF
         CALL      PRTB0000
.
         MATCH     ZEROVISN,WSTBY
         GOTO      WARD5000 IF EQUAL
         MOVE      WSTBY,WADMNO
         MOVE      "STB",SAVBED

         CALL      GETP0000
         BRANCH    EXIT,WARD5000
         ADD       ONE,TOTSTBY
         CALL      PRTB0000
.
         GOTO      WARD5000
.
WARD8000 CALL      PBRD0000               * Print Hospital Boarders
         ADD       TOTPAT,GRDPAT          * add for grand patient total
         ADD       TOTSTBY,GRDSTBY        * add for grand standby total
         ADD       TOTLVE,GRDLVE
         CALL      TOT0000
         MOVE      ZERO,TOTPAT            * Total Patients
         MOVE      ZERO,TOTSTBY           * Total Standby Patients
         MOVE      ZERO,TOTLVE
.
.                                  * if new page per ward, force headings
         IF        PSKIP = 0
           MOVE      "99",CLNO
         ENDIF
.
         COMPARE   ONE,WINPUT              * is this a no-bed ward
         GOTO      WARD9000 IF NOT EQUAL
.
                                          * skip to next ward
         PACK      KEY6,WWARD,Z70         * set key for end of this ward
         CALL      RDSWARD1
         CALL      RDKWARD1               * skip to next if any
         IF        OVRCD = 1
           MOVE      "Y",EOFWR1
         ENDIF
         GOTO      WARD9000
.
WARD9000 MOVE      FALSE,EXIT
         MATCH     "Y",EOFWR1
         IF         !@EQUAL
           MOVE       TRUE,EXIT
         ENDIF
WARD9999 RETURN
+
.**********************************************************************
.*                             GETP0000                               *
.*                       Fetch Patient data                           *
.**********************************************************************
GETP0000  PACK      PNAME,SP30,SP30
          MOVE      SP30,DNAME15
          MOVE      SP30,DATYPE
          MOVE      SP30,PREG
          MOVE      SP30,PSEX
          MOVE      SP30,PSAGE
          MOVE      SP30,ADIAG
          MOVE      FALSE,EXIT
          PACK      LOCDESC,SP30,SP30

          PACK      KEY8,WADMNO
          MOVE      ZEROVISN,WADMNO
          CALL      RDMISS1
.
.         Check if patient is currently admitted
          BRANCH    OVRCD,GETP9999
          IF        ASTAT = 2
            GOTO      GETP1000
          ENDIF
.
.         Check if patient is on leave
          IF        ASTAT = 4
            GOTO      GETP1000
          ENDIF
.
          MOVE      TRUE,EXIT
          GOTO      GETP9999

.
GETP1000  MOVE      AADMNO,WADMNO
.
          MOVE      ADIAG1,ADIAG
.
          MOVE      AURNO,KEY8
          PACK      PSNAME,INVUR,AURNO
          UNPACK    SP30,PTITL,PGNAME
          CALL      RDMAST1
.
          PACK       AGEDATE,CCC,CYY,CMM,CDD
          CALL       CALCAGE                 * Calculate age as of admin date
          MOVE       PSAGEYRS,PSAGE
.                                           * get doctor name
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GETP5000
          MOVE      DGNAME,ANS
          PACK      DNAME15 WITH ANS,DOT,DSNAME
.
GETP5000  STRIP     PSNAME
          STRIP     PTITL
          STRIP     PGNAME
          PACK      PNAME,PSNAME,SP1,PTITL,SP1,PGNAME
.
GETP9000  MOVE      FALSE,EXIT
.
GETP9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.**********************************************************************
.*                            SEP0000                                 *
.*                        Separate page ?                             *
.**********************************************************************
SEP0000   KEYIN     *P1:16,*EL,"Would you like each Ward on a seperate PAGE (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          RESET     ANS
          GOTO      SEP1000 IF EOS
          REP       UPPLOW,ANS
.
          MATCH     "Y",ANS
          GOTO      SEP2000 IF EQUAL
          MATCH     "N",ANS
          GOTO      SEP1000 IF EQUAL
          BEEP
          GOTO      SEP0000
.
SEP1000   MOVE      ONE,PSKIP               * No page separation
          DISPLAY   *P53:16,*EL,*V2LON,"No";
          GOTO      SEP9999
.
SEP2000   MOVE      ZERO,PSKIP              * Page separation
          DISPLAY   *P53:16,*EL,*V2LON,"Yes";
.
SEP9999   RETURN
+
.**********************************************************************
.*                         CREA0000                                   *
.*                  Create Temporary Files                            *
.**********************************************************************
.
CREA0000  CALL      KILL0000
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 22 UC1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      SECTION1,TFILNAME
.
          RETURN
.
.**********************************************************************
.*                         KILL0000                                   *
.*                  Remove Temporary Files                            *
.**********************************************************************
.
KILL0000  CLOSE     SECTION1
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
+
.**********************************************************************
.*                       HEADB000                                     *
.*                    Print Boarders Heading                          *
.**********************************************************************
HEADB000  PRINT     *1,"|",*40,"HOSPITAL BOARDERS",*132,"|"
          ADD       ONE,CLNO
          CALL      PGE0000
.
          PRINT     *1,"| Ward",*9,"| U/R No",*19,"|Patient's Name":
                    *60,"|Sex|Age| Mobile Number":
                    *85,"| Phone Number",*104,"| Email",*132,"|"
          CALL      PGE0000
.
HEADB900  ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                         PBRD0000                                   *
.**********************************************************************
.
PBRD0000  CALL      HEADB000
          MOVE      ZERO,TOTBRD
.
          PACK      KEY8,SP70
          CALL      RDSSECT1
PBRD1000  CALL      RDKSECT1
          BRANCH    OVRCD,PBRD9000
.
          PACK      KEY26,XADMN,SP70
          CALL      RSPTBRD3
PBRD2000  CALL      RKPTBRD3
          BRANCH    OVRCD,PBRD1000
.
          MATCH     XADMN,PTBRVISN
          GOTO      PBRD1000 IF NOT EQUAL
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          MATCH     SP70,PTBRSDAT
          IF        !@EQUAL & !@EOS
            MATCH     PTBRSDAT,DIM8
            GOTO      PBRD2000 IF LESS
          ENDIF
.
          MATCH     SP70,PTBREDAT
          IF        !@EQUAL & !@EOS
            MATCH     DIM8,PTBREDAT
            GOTO      PBRD2000 IF LESS
          ENDIF
.
          PACK      KEY8,XADMN,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,PBRD2000
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PBRD2000
.
          MOVE      PTBRSNAM,PACSNAME            * set up patients name
          MOVE      PTBRGNAM,PACGNAME
          MOVE      SP70,PACTITLE
          MOVE      TWO, PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,XNAME 
.
          MOVE      SP70,PSAGEYRS
          MATCH     SP70,PTBRBDAT
          IF        !@EQUAL & !@EOS
            CALL      IBACLOCK
            PACK      AGEDATE,CCC,CYY,CMM,CDD
            MOVE      PTBRBDAT,PBDATE
            CALL      CALCAGE
          ENDIF
.
          MOVE      PTBRMPHN,DIM12A
          MOVE      PTBRHPHN,DIM12B
          MOVE      PTBREMAL,DIM18
          MOVE      XWRDESC,DIM3
.
          PRINT     *1,"|",DIM3,SP1,XBED,*9,"| ",XADMN,*19,"|",XNAME:
                    *60,"|",*62,PTBRGEND,*64,"|",PSAGEYRS,*68,"|",DIM12A:
                    *85,"|",DIM12B:
                    *104,"|",DIM18,*132,"|"
.
          ADD       ONE,TOTBRD
.
          CALL      PGE0000
          ADD       TWO,CLNO
.
          COMPARE   "58",CLNO
          CALL      HEADA000 IF NOT LESS
.
          GOTO      PBRD2000
.
PBRD9000  CALL      CREA0000
PBRD9999  RETURN
+
.**********************************************************************
. TEMP FILE I/O
.
RDSSECT1  RESET     KEY8
          READ      SECTION1,KEY8;;
          RETURN
.
RDASECT1 RESET      KEY8
         MOVE       ZERO,OVRCD
         READ       SECTION1,KEY8;ANS
         GOTO       OVERCOND IF OVER
         RETURN
.
RDKSECT1  MOVE      ZERO,OVRCD
          READKS    SECTION1;XADMN,XWRDESC,XBED
          GOTO      OVERCOND IF OVER
          RETURN
.
.
WRSECT1   WRITE     SECTION1,KEY8;XADMN,XWRDESC,XBED
          RETURN
.==============================================================================
.
          INC       STD001IO
.
          INC       PATNURDS
          INC       PATWRDDS
          INC       PATHSPKY
          INC       CALCAGE
.
          INC       PATBRDIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATNOBIO/INC
          INC       PATNURIO/INC
          INC       PATONLIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PATHSPIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
+

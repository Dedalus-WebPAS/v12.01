.******************************************************************************
.* System         : Theatre System                                            *
.* Program        : IBAOPR93                                                  *
.* Name           : Booking Register                                          *
.******************************************************************************
.* Date           : 20/12/1995                                                *
.* Author         : Junior                                                    *
.* Function       : Print all patients for a range of operation types who have*
.*                  been booked.  Print the report in scheduled admission date*
.*                  range.                                                    *
.* Modifications  :                                                           *
.******************************************************************************
.*        V12.00.01 30/05/2025 Jacob Jackson     TSK 0955096                  *
.*                  Alphanumeric changes                                      *
.*        V11.03.02 19/04/2023  Thanh T          TSK 0909393                  *
.*                  Changed LOAD1000 to e RDOPSES1 instead of RDOPSES7        *
.*        V11.03.01 20/03/2023  PJ Le Febour  TSK 0909393a                    *
.*                  Amended KEY19 to KEY22 for theatre index changes          *
.*        V10.04.01 07/07/2014  J.Tan     CAR 261630                          *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.09.01  26/10/2007  Steve Armstrong   CAR 152934                  *
.*                  Added multi-hospital functionality                        *
.******************************************************************************
.*        V5.08.B01 06/02/2001  Tonii  SRF 5305                               *
.*                  Fixed wrong display for attending doctor                  *
.******************************************************************************
.*        V5.08.01  23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund table variables to 8 chars            *
.*        V5.07.05  21/12/1999  J.Tan  SRF 634119                             *
.*                  Mods to use attending doc from patmis for Pre-admission   *
.*        V5.07.04  27/04/1999  Jill Habasque                                 *
.*                  Added Claim Type                                          *
.*        V5.07.03  01/03/1999  Jill Habasque                                 *
.*                  Recompiled for PATDSCOD                                   *
.*        V5.07.02  22/02/1999  Jill Habasque                                 *
.*                  Added clear screen when returning to options menu         *
.*        V5.07.01  15/02/1999  Jill Habasque                                 *
.*                  Fixed read of BOKREC                                      *
.*        V5.07.00  08/02/1999  Jill Habasque                                 *
.*                  Copied from 6.06                                          *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       PATCALAG/INC
.
          INC       BOKDIAFD/INC            * Booking diagnosis file
          INC       BOKRC1FD/INC            * Booking master file
          INC       OPRDEAFD/INC            * Operation detail file
          INC       OPRSESFD/INC
          INC       PATCODFD/INC            * Patient codes file
          INC       PATCONFD/INC            * Control File
          INC       PATDHEAD/INC            
          INC       PATDO1FD/INC            * Patient doctors file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC            * Patient master file
          INC       PATMI1FD/INC            * Patient admission file
          INC       PMSVX1FD/INC            * Patient admission file
          INC       PATPR1FD/INC            * Patient pre admission file
          INC       PATWR1FD/INC            * Patient ward file
.
. ----- Tempfile Definition -----
.
TEMP1     IFILE     SQL, FIXED=281, KEY="U1-8,9-12"
TEMP2     IFILE     SQL, FIXED=281, KEY="U53-60,61-65,13-15,1-8,9-12"
.
FILELINE  INIT      " 281 U1-8,9-12 U53-60,61-65,13-15,1-8,9-12"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
BOOKNUMB  DIM       8        8          1       Booking number
DTMPCONT  DIM       4        4          9       Tempfile counter
OPRTYPE   DIM       3        3          13      Operation Type (Cat OY)
PATNAME   DIM       22       22         16      Patient name
PATHTELE  DIM       12       12         38      Patient home telephone number
PATAGE    DIM       3        3          50      Patient age
SCHADDTE  DIM       8        8          53      Scheduled adm date (ccyymmdd)
SCHADTME  DIM       5        5          61      Scheduled admission time
BOOKSTAT  DIM       14       14         66      Booking status
ATTENDOC  DIM       20       20         80      Attending doctor
CLAIMTYP  DIM       7        7          100     Claim type (cat CL)
URNO      DIM       8        8          107     U/R number
WARD      DIM       3        3          115     Expected ward
BED       DIM       3        3          118     Expected bed
OPRCMNTS  DIM       33       33         121     Operation comments
BOOKDIA1  DIM       54       54         154     Booking diagnosis code 1
BOOKDIA2  DIM       54       54         208     Booking diagnosis code 2
BEDTYPE   DIM       19       19         262     Bed type (Cat BP)
.
.End of Record                          281
.
. ----- Program Variables -----
.
BJDAY     FORM      3
.
CJDAY     FORM      3
CMDLINE   DIM       80
CODE      DIM       2
CLAIMDES  DIM       7                       * Cat CL description
.
DIM3      DIM       3
DSPEDDTE  DIM       10                      * Display ending sch'd adm date
DSPEDOPT  DIM       20                      * Display ending operation type
DSPSTDTE  DIM       10                      * Display starting sch'd adm date
DSPSTOPT  DIM       20                      * Display starting operation type
.
ENDOPRT   DIM       3                       * Ending operation type
ENDDATE   DIM       8                       * Ending sch'd adm date (ccyymmdd)
.
FORM8     FORM      8
FNAME     DIM       8                       * Filename
.
IBCNMHOS  FORM      1
.
PATCOUNT  FORM      4                       * Patient counter
.
STRTOPRT  DIM       3                       * Starting operation type
STRTDATE  DIM       8                       * Starting sch'd adm date (ccyymmdd)
.
TMPCOUNT  FORM      4                       * Tempfile counter
.
. ----- Program Constants -----
.
CODEBP    INIT      "BP"
CODEOY    INIT      "OY"
.
SP100     INIT      "                                                  ":
                    "                                                  "
ADM       INIT      "ADM"
BOK       INIT      "BOK"
CAN       INIT      "CAN"
CODECL    INIT      "CL"
DSC       INIT      "DSC"
PRE       INIT      "PRE"
UNKNOWN   INIT      "Unknown"
.
PRGID     INIT      "IBAOPR93"
PRGNAM    INIT      "Booking Register"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9000
.
ML2000    CALL      OPRT0000                * Keyin the operation type range
          BRANCH    EXIT,ML1000
.
ML3000    CALL      DATE0000                * Keyin the sch'd admin date range
          BRANCH    EXIT,ML2000
.
          CALL      KHOS0000                * keyin hospital (multi-campus only)
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
.                         Yes    No     Cancel
.
ML4000    CALL      LOAD0000                * Load the tempfile
          CALL      PRNT0000                * Print the report
          GOTO      ML1000
.
ML9000    CALL      KILL0000
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called By: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P56:24,"Opening bokdiaaf"
          OPEN      BOKDIAA1,"bokdiaaf"
.
          DISPLAY   *P64:24,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
.
          DISPLAY   *P64:24,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
.
            DISPLAY   *P60:24,"oprsesaf";
            OPEN      OPRSESA1,"oprsesaf"
          ENDIF
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME
.
          CALL      IBACLOCK                * Get the date & time
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called By: ML0000   *
.*                             Choose Main Option                             *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:4,*EF,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Booking Register":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN1,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000       Called by:                 *
.*                For multi-hospital, get the hospital code                   *
.******************************************************************************
.
KHOS0000  COMPARE   ONE,IBCNMHOS                 * using multi-hospital ?
          GOTO      KHOS9999 IF NOT EQUAL        * no - finished
.
          DISPLAY   *P1:15,*EL,"Hospital Id               : ";
          MOVE      TWENTY9,ECOL
          MOVE      "15",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND                * Not Mandatory
.
          CALL      PATHSPKY                     * get code
          BRANCH    EXIT,KHOS2000:               * nothing input
                         KHOS9500                * exitchar input
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          DISPLAY   *P29:15,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS3000  DISPLAY   *P37:15,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
.
KHOS9999  RETURN
+
.******************************************************************************
.*                                  OPRT0000              Called By: ML0000   *
.*                       Keyin The Operation Type Range                       *
.******************************************************************************
OPRT0000  MOVE      "29",ECOL
          MOVE      SP3,CKYIDEF3            * No default
          MOVE      ZERO,CKYIMAND           * Keyin not mandatory
          MOVE      ONE,CNEWDS              * Don't return for keyin
          MOVE      CODEOY,CODE
.
. ----- Starting Operation Type -----
.
OPRT1000  MOVE      "9",EVERT
          DISPLAY   *P1:EVERT,*EF,"Starting Operation Type   : ";
          ADD       ONE,EVERT
          DISPLAY   *P1:EVERT,*EL,"Ending Operation Type     : ";
          SUB       ONE,EVERT
          MOVE      SP10,STRTOPRT
          MOVE      SP20,DSPSTOPT
          CALL      KEYINCOD                * Keyin the operation type
          BRANCH    EXIT,OPRT2000,OPRT9500
.                        Space    Exit
.
          MOVE      ACODE,STRTOPRT          * Starting operation type
          MOVE      TDESC,DSPSTOPT
          DISPLAY   *P35:EVERT,DSPSTOPT;
          GOTO      OPRT3000
.
OPRT2000  MOVE      "Start ",DSPSTOPT       * Starting operation type
          DISPLAY   *PECOL:EVERT,*V2LON,DSPSTOPT;
.
. ----- Ending Operation Type -----
.
OPRT3000  MOVE      "10",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          MOVE      SP10,ENDOPRT
          MOVE      SP20,DSPEDOPT
          CALL      KEYINCOD                * Keyin the operation type
          BRANCH    EXIT,OPRT5000,OPRT9500
.                        Space    Exit
.
          MOVE      ACODE,ENDOPRT           * Ending operation type
          MATCH     STRTOPRT,ENDOPRT
          GOTO      OPRT4000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid operation type range. ";
          CALL      HITENTER
          GOTO      OPRT1000
.
OPRT4000  MOVE      TDESC,DSPEDOPT
          DISPLAY   *P35:EVERT,DSPEDOPT;
          GOTO      OPRT9000
.
OPRT5000  MOVE      "zzzzzzzz",ENDOPRT      * Ending operation type
          MOVE      "Finish",DSPEDOPT
          DISPLAY   *PECOL:EVERT,*V2LON,DSPEDOPT;
.
OPRT9000  MOVE      ZERO,EXIT
          GOTO      OPRT9999
.
OPRT9500  MOVE      ONE,EXIT
OPRT9999  RETURN
+
.******************************************************************************
.*                                 DATE0000               Called by: ML0000   *
.*                             Keyin Date Range                               *
.******************************************************************************
DATE0000  MOVE      "29",CCOL
          MOVE      ONE,CCANLDTE            * Cancel default date
.
. ----- Enter Starting Date -----
.
DATE1000  MOVE      "12",CVERT
          DISPLAY   *P1:CVERT,*EF,"Starting Sch'd Admin Date : ";
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,*EL,"Ending Sch'd Admin Date   : ";
          SUB       ONE,CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,DATE9500
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY    * Starting date range
          REP       " 0",STRTDATE
          CALL      PACDATE                 * Format the starting date
          MOVE      CPCDATE,DSPSTDTE
          REP       " 0",DSPSTDTE
.
. ----- Enter Ending Date -----
.
DATE2000  MOVE      "13",CVERT
          DISPLAY   *PCCOL:CVERT,*EL;
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,DATE1000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY     * Ending date range
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Format the ending date
          MOVE      CPCDATE,DSPEDDTE
          REP       " 0",DSPEDDTE
.
          MATCH     STRTDATE,ENDDATE
          GOTO      DATE9000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid date range. ";
          CALL      HITENTER
          GOTO      DATE1000
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  LOAD0000              Called by: ML0000   *
.*                              Load The Tempfile                             *
.******************************************************************************
LOAD0000  MOVE      ZERO,TMPCOUNT           * Initialise the tempfile counter
          CALL      OPEN0000                * Open the tempfile
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
          PACK      KEY16,STRTDATE,SP20
          CALL      RSBKREC4                * Position on & read the booking
LOAD1000  CALL      RKBKREC4                  master file
          BRANCH    OVRCD,LOAD9999
.
          MATCH     BKREEDAT,ENDDATE
          GOTO      LOAD9999 IF LESS        * Out of date range
.
          IF        BKRESTAT=2 | BKRESTAT=4
            GOTO      LOAD1000         * Ignore cancelled or discharged patients
          ENDIF
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the scheduled admin date
          DISPLAY   *P12:24,*V2LON,BKREBOOK,SP2,CPCDATE
.
          PACK      KEY31,BKREBOOK,SP30
          CALL      RSOPDEA2                * Position on & read the operation
          CALL      RKOPDEA2                  detail file
          BRANCH    OVRCD,LOAD1000
.
          MOVE      OPDAADMN,DIM8VISN
          MATCH     BKREBOOK,DIM8VISN
          GOTO      LOAD1000 IF NOT EQUAL   * Different booking number
.
          MATCH     STRTOPRT,OPDATYPE
          GOTO      LOAD1000 IF LESS        * Out of operation type range
.
          MATCH     OPDATYPE,ENDOPRT
          GOTO      LOAD1000 IF LESS        * Out of operation type range
.
.         Check for multi hospital
.
          COMPARE   ONE,IBCNMHOS                 * using multi-hospital ?
          GOTO      LOAD1500 IF NOT EQUAL        * no - ignore check
.
          MATCH     SP10,PTHSHOSP                * all hospitals ?
          GOTO      LOAD1500 IF EQUAL            * yes - ignore check
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1                     * session record found ?
          BRANCH    OVRCD,LOAD1000               * no - ignore record    
.
          MATCH     OPSEHOSP,PTHSHOSP            * same hospital ?
          GOTO      LOAD1000 IF NOT EQUAL        * no - ignore record
.
LOAD1500  CALL      CVAR0000                * Clear the tempfile variables
.
. ----- Set Up The Variables For The Tempfile -----
.
          MOVE      BKRECLMT,CLAIMTYP
          MOVE      BKREURNO,URNO           * U/R number
          MOVE      BKREURNO,PURNO
          PACK      KEY8,PURNO
          CALL      RDMAST1                 * Read the master file
          IF        OVRCD=1
            MOVE      BKREBOOK,PURNO
            PACK      KEY8,PURNO
            CALL      RDPRAM1               * Read the preadmission file
            BRANCH    OVRCD,LOAD1000
.
            MOVE      "       0",URNO       * Clear the U/R number
          ENDIF
.
          MOVE      BKREBOOK,BOOKNUMB       * Booking number
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patient name
          MOVE      PACFNAME,PATNAME        * Patient name
.
          MOVE      PTELEP,PATHTELE         * Patient home telephone number
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          CALL      CALCAGE                 * Calculate age as of today
          MOVE      PSAGEYRS,PATAGE         * Patient age
.
          MOVE      BKREEDAT,SCHADDTE       * Scheduled admission date
          MOVE      BKREATIM,SCHADTME       * Scheduled admission time
          MOVE      BOK,DIM3
          LOAD      DIM3,BKRESTAT,PRE,CAN,ADM,DSC
          MATCH     ADM,DIM3
          IF        @EQUAL
            PACK      KEY8,BOOKNUMB
            CALL      RDMISS1 
            BRANCH    OVRCD,LOAD2000
												MOVE      ADOCTA,BKREADOC
            MOVE      ACLAIM,CLAIMTYP
          ENDIF
.
          MATCH     PRE,DIM3
          IF        @EQUAL
            PACK      KEY8,BOOKNUMB
            CALL      RDMISS1 
            BRANCH    OVRCD,LOAD2000
            MOVE      ADOCTA,BKREADOC
          ENDIF
.
LOAD2000  UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the session date
          PACK      BOOKSTAT,DIM3,SP1,CPCDATE     * Booking status
          MOVE      OPDATYPE,OPRTYPE        * Operation type
.
          PACK      KEY6,BKREADOC
          CALL      RDDOCT1                 * Read the doctors file
          IF        OVRCD<>1
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME               * Format doctors name
            MOVE      PACFNAME,ATTENDOC     * Doctors name
          ENDIF
.
          MOVE      BKREWARD,WARD           * Expected ward
          MOVE      BKREEBED,BED            * Expected bed
          MOVE      OPDACOMM,OPRCMNTS       * Operation comments
.
          CLEAR     BOOKDIA1
          MATCH     SP9,OPDAPROV
          IF        !@EQUAL
            STRIP     OPDAPROV
            APPEND    OPDAPROV,BOOKDIA1   * Booking diagnosis code
            APPEND    SP1,BOOKDIA1
          ENDIF
          APPEND    OPDADES1,BOOKDIA1     * Booking diagnosis description 1
          RESET     BOOKDIA1
.
          CLEAR     BOOKDIA2
          MATCH     SP9,OPDAPRV2
          IF        !@EQUAL
            STRIP     OPDAPRV2
            APPEND    OPDAPRV2,BOOKDIA2   * Booking diagnosis 2
            APPEND    SP1,BOOKDIA2
          ENDIF
          APPEND    OPDADES2,BOOKDIA2     * Booking diagnosis description 2
          RESET     BOOKDIA2
.
.         PACK      KEY8,BKREBOOK
.         CALL      RDBKDIA1                * Read the booking diagnosis file
.         IF        OVRCD<>1
.           CLEAR     BOOKDIA1
.           MATCH     SP9,BKDICODE
.           IF        !@EQUAL
.             STRIP     BKDICODE
.             APPEND    BKDICODE,BOOKDIA1   * Booking diagnosis code
.             APPEND    SP1,BOOKDIA1
.           ENDIF
.           APPEND    BKDIDES1,BOOKDIA1     * Booking diagnosis description 1
.           RESET     BOOKDIA1
.
.           CLEAR     BOOKDIA2
.           APPEND    BKDIDES2,BOOKDIA2     * Booking diagnosis description 2
.           RESET     BOOKDIA2
.         ENDIF
.
          MATCH     SP3,BKREACCM
          IF        !@EQUAL
            PACK      KEY5,CODEBP,BKREACCM
            CALL      RDCODE1             * Read the codes file
            IF        OVRCD<>1
              MOVE      TDESC,BEDTYPE     * Bed type
            ENDIF
          ENDIF
.
          ADD       ONE,TMPCOUNT            * Increment the tempfile counter
          PACK      KEY12,BOOKNUMB,TMPCOUNT
          CALL      RATEMP1                 * Position on the tempfile
          IF        OVRCD=1
            CALL      WRTEMP1               * Write to the tempfile
            DISPLAY   *P45:24,*V2LON,BOOKNUMB;
          ENDIF
          GOTO      LOAD1000
.
LOAD9999  RETURN
+
.******************************************************************************
.*                                  CVAR0000              Called by: LOAD0000 *
.*                        Clear The Tempfile Variables                        *
.******************************************************************************
CVAR0000  UNPACK    SP100,BOOKNUMB,DTMPCONT,OPRTYPE,PATNAME,PATHTELE
          UNPACK    SP100,PATAGE,SCHADDTE,SCHADTME,BOOKSTAT,ATTENDOC
          UNPACK    SP100,CLAIMTYP,URNO,WARD,BED,OPRCMNTS
          UNPACK    SP100,BOOKDIA1
          UNPACK    SP100,BOOKDIA2,BEDTYPE
CVAR9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: ML0000   *
.*                              Print The Report                              *
.******************************************************************************
PRNT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,PATCOUNT
          DISPLAY   *P1:24,*EL,"Printing : ";
          MOVE      ZERO,CPAGENO            * Initialise page number
          MOVE      ONE,CNOUNDLN            * Just print heading on new page
          CALL      HEAD0000                * Print the report header
          PACK      KEY25,SP30
          CALL      RSTEMP2                 * Position on & read the 2nd
PRNT1000  CALL      RKTEMP2                   tempfile
          BRANCH    OVRCD,PRNT9000
.
          MATCH     SP3,CLAIMTYP
          IF        @EQUAL
             MOVE     SP8,CLAIMDES
             GOTO      PRNT2000
          ENDIF
.
          PACK      KEY5,CODECL,CLAIMTYP
          CALL      RDCODE1
          MOVE      TDESC,CLAIMDES
.
PRNT2000  COMPARE   FIFTY8,CLNO
          CALL      HEAD0000 IF NOT LESS    * Print the report heading 
.
          UNPACK    SCHADDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the scheduled admin date
.
          PRINT     *1,"|",BOOKNUMB,*11,PATNAME,*34,PATHTELE:
                    *48,PATAGE,*53,CPCDATE,*65,SCHADTME:
                    *72,BOOKSTAT,*87,ATTENDOC:
                    *108,CLAIMTYP,*116,URNO,*125,WARD,"/",BED:
                    *132,"|"
          PRINT     *1,"|Comments:",OPRCMNTS,*45,"Diagnosis:",BOOKDIA1:
                    *108,CLAIMDES,*132,"|"
          PRINT     *1,"|",*55,BOOKDIA2,*132,"|"
          ADD       "3",CLNO
          CALL      UND132                  * Print an underline
          ADD       ONE,PATCOUNT
          DISPLAY   *P12:24,*V2LON,BOOKNUMB;
          GOTO      PRNT1000
.
PRNT9000  PRINT     *1,"Total Patients Printed : ",PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,"Report Generation Complete. ";
          CALL      HITENTER
PRNT9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PRNT0000 *
.*                          Print The Report Heading                          *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print main report heading
          IF        IBCNMHOS =1             
            PRINT     *40,"Hospital: ",PTHSNAME,*N;
          ENDIF
.
          STRIP     DSPSTOPT
          PRINT     *40,"Operation Type Range   : ",*+,DSPSTOPT," to ",DSPEDOPT
          PRINT     *40,"Sch'd Admin Date Range : ",DSPSTDTE," to ",DSPEDDTE
.
          CALL      UND132                  * Print an underline
.
          PRINT     *1,"|Booking",*11,"Patient",*34,"Patient",*48,"Age":
                    *53,"Scheduled",*65,"Sch'd",*72,"Booking",*87,"Attending":
                    *108,"Claim",*116,"U/R",*125,"Ward/",*132,"|"
          PRINT     *1,"|Number",*11,"Name",*34,"Home Phone":
                    *53,"Adm Date",*65,"Time",*72,"Status",*87,"Doctor":
                    *108,"Type",*116,"Number",*125,"Bed",*132,"|"
.
          CALL      UND132                  * Print an underline
.
          IF        IBCNMHOS = 1
            MOVE      TEN,CLNO
          ELSE
            MOVE      NINE,CLNO
          ENDIF
.
HEAD9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000              Called by: LOAD0000 *
.*                              Open The Tempfile                             *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    FILELINE,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID          * Create the tempfile
          OPEN      TEMP1,FNAME
          OPEN      TEMP2,FNAME
OPEN9999  RETURN
+
.******************************************************************************
.*                                  KILL0000              Called by: ML0000   *
.*                             Delete The Tempfile                 & LOAD0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
          CLOSE     TEMP2
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.******************************************************************************
.*                         I/O Routines For The Tempfile                      *
.******************************************************************************
RATEMP1   RESET     KEY12
          MOVE      ZERO,OVRCD
          READ      TEMP1,KEY12;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY12
          MOVE      TMPCOUNT,DTMPCONT
          WRITE     TEMP1,KEY12;BOOKNUMB,DTMPCONT,OPRTYPE,PATNAME,PATHTELE:
                                PATAGE,SCHADDTE,SCHADTME,BOOKSTAT,ATTENDOC:
                                CLAIMTYP,URNO,WARD,BED,OPRCMNTS:
                                BOOKDIA1,BOOKDIA2,BEDTYPE
          RETURN
.
RSTEMP2   RESET     KEY25
          READ      TEMP2,KEY25;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMP2;BOOKNUMB,DTMPCONT,OPRTYPE,PATNAME,PATHTELE:
                          PATAGE,SCHADDTE,SCHADTME,BOOKSTAT,ATTENDOC:
                          CLAIMTYP,URNO,WARD,BED,OPRCMNTS:
                          BOOKDIA1,BOOKDIA2,BEDTYPE
          GOTO      OVERCOND IF OVER
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       CALCAGE                 * Calculate patients age
          INC       KEYINCOD                * Input code
          INC       PATDSCOD                * "?" on codes
          INC       PATHSPKY
.
          INC       BOKDIAIO/INC            * Booking diagnosis file
          INC       BOKRC1IO/INC            * Booking master file
          INC       OPRDEAIO/INC            * Operation detail file
          INC       OPRSESIO/INC
          INC       PATCODIO/INC            * Patient codes file
          INC       PATDO1IO/INC            * Patient doctors file
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC            * Patient master file
          INC       PATMI1IO/INC            * Patient admission file
          INC       PMSVX1IO/INC            * Patient admission file
          INC       PATPR1IO/INC            * Patient pre admission file
          INC       PATWR1IO/INC            * Patient ward file
+
.

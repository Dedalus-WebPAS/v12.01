.******************************************************************************
.* System    :   Outpatients                                                  *
.* Program   :   FX861257                                                     *
.* Desc      :   Loop through the Outpatient Direct Actions Table (OUTDANFD)  *
.*               and update new 'Requestor' field added when a record has a   *
.*               blank requestor and an 'Actiion Type' of 'Appointment        *
.*               message' = 1                                                 *
.******************************************************************************
.* Date      :   29/10/2019                                                   *
.* Author    :   Richa Phogat                                                 *
.* Function  :   This program will update outpatient Direct Action table      *
.* Modifications  :                                                           *
.*    V10.15.00  29/10/2019  Richa Phogat TSK 0861257                         *
.*               Created program                                              *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OUTDANFD/INC
          INC       PATCODFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CATRS     INIT      "rs"
CODE      DIM       2
COUNTER   FORM      8
SAVEKEY   DIM       34
.
PRGID     INIT      "FX861257"
PRGNAM    INIT      "Outpatient Direct Actions Tbl"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAIN0000                                  *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    EXIT,MAIN1000          * proceed with clean up
.
MAIN1000  CALL      KCOD0000               * Get requestor type code 
          BRANCH    EXIT,MAIN0100          * nothing/exitchar entered
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"outdanaf";
          OPEN      OUTDANA1,"outdanaf"       
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit to update blank ":
                    "Requestor field in OUTDANFD "
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit    
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.* Keyin the event program  - Cat rs                                        *
.****************************************************************************
KCOD0000  DISPLAY   *P1:10,*EF,"Select Code for CAT 'rs' :"
.
          MOVE      SP10,ACODE
          MOVE      TWENTY8,ECOL                 * set keyin column
          MOVE      TEN,EVERT                    * set keyin row
          MOVE      CATRS,CODE                   * set category "rs"
          MOVE      SP3,CKYIDEF3                 * set blank default
          MOVE      ZERO,CKYIMAND                * set non-mandatory
.
          CALL      PATCODKY                     * get code for safety net no
          BRANCH    EXIT,KCOD9100:               * nothing entered
                         KCOD9100                * exitchar input
.
          DISPLAY   *P40:10,TDESC                * display description
          MOVE      ZERO,EXIT
          GOTO      KCOD9999
.
KCOD9100  MOVE      ONE,EXIT
.
KCOD9999  RETURN
+
.****************************************************************************
.                         PROC0000                    Called by: MAIN0000   *
.   Loop through outpatient Direct Actions (outdanaf) file for every record *
.   update OTDNREQC where it is blank and OTDNATYP is ' 1'                  *
.****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,COUNTER                 * initialise counter
.
          PACK      KEY34,SP70
          CALL      RSOTDAN1                     * position at start of file
PROC0500  CALL      RKOTDAN1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          PACK      SAVEKEY,OTDNURNO,OTDNADAT,OTDNATIM,OTDNATYP,OTDNVISN,SP70
.
          MATCH     " 1",OTDNATYP                * Appointment Message
          GOTO      PROC0500 IF NOT EQUAL        * no - read next record
.   
          MATCH     SP10,OTDNREQC                * blank requestor?
          GOTO      PROC0500 IF NOT EQUAL        * no - read next record
.
          PACK      KEY34,SAVEKEY
          CALL      RDOTDAN1
          PACK      OTDNREQC,ACODE
          CALL      UPOTDAN1
          ADD       ONE,COUNTER
          PRINT     *1,"|",OTDNURNO
          GOTO      PROC0500
.
.         Processing completed
.
PROC9000  PRINT     *N,*1,"Total number of records updated: ",COUNTER
          DISPLAY   *P1:22,"Records Updated  : ",*EL,*V2LON,COUNTER,*HOFF:
                    *P1:24,*EL,"Fixit Completed. ";
          CALL      HITENTER        
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODKY
.
          INC       OUTDANIO/INC
          INC       PATCODIO/INC

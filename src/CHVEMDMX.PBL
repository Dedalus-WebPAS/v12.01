.***************************************************************************
.* System    :   Emergency                                                 *
.* Program   :   CHVEMDMX                                                  *
.* Desc      :   ******** Required for Victorian sites only ************   *
.*           :   This program will check if the emrmtxaf.emmxpdg field     *
.***************************************************************************
.* Date      :   27/05/2016                                                *
.* Author    :   Jill Parkinson                                            *
.* Function  :   This program will loop emrmtxaf and check if the emmxpdg  *
.*               and check if the value in emmxpdg can be moved from using *
.*               emicvemd to use emic9cod                                  *
.* Modifications  :                                                        *
.*        V10.08.00 27/05/2016 Jill Parkinson TSK 0814124                  *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       EMRMTXFD/INC
          INC       EMRICDFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
DIM6      DIM       6
DIM3      DIM       3
.
.
PRGID     INIT      "CHVEMDMX"
PRGNAM    INIT      "Check emrmtxaf from using VEMD code"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      EMRMTXA1,"emrmtxaf"
          OPEN      EMRICDA1,"emricdaf"
          OPEN      EMRICDA4,"emricdaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL       HEAD80
          PACK       KEY10,SP70
          CALL       RSEMMX1
PROC1000  CALL       RKEMMX1
          BRANCH     OVRCD,PROC9000
.
          MATCH      "N/A",EMMXPDG
          GOTO       PROC1000 IF EQUAL
.
          PACK       KEY18,EMMXPDG,SP70
          CALL       RSEMICD4
PROC2000  CALL       RKEMICD4
          BRANCH     OVRCD,PROC3000
.
          MATCH      EMMXPDG,EMICVEMD
          GOTO       PROC3000 IF NOT EQUAL
.
          COMPARE    ONE,EMICACTV
          GOTO       PROC2000 IF EQUAL
.
          MATCH      EMMXPDG,EMIC9COD
          GOTO       PROC4000 IF NOT EQUAL
.
          UNPACK     EMIC9COD,DIM6,DIM3
          PACK       DIM3,DIM3,SP70
          MATCH      SP70,DIM3
          GOTO       PROC5000 IF NOT EQUAL
.
          GOTO       PROC1000
.
PROC3000  PRINT      *1,"Invalid VEMD Code ",EMMXNAT,SP1,EMMXBRG,SP1,EMMXPDG
          GOTO       PROC1000
.
PROC4000  PRINT      *1,"VEMD Code ",EMIC9COD," differs from ICD Code for Matrix record ":
                     EMMXNAT,SP1,EMMXBRG,SP1,EMMXPDG
          GOTO       PROC1000
.
PROC5000  PRINT      *1,"EMR ICD code over 6 characters ",EMIC9COD
          GOTO       PROC1000
.
PROC9000  PRINT      *N,*N,*1,"Finished"
          GOTO       PROC9999
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       EMRMTXIO/INC
          INC       EMRICDIO/INC

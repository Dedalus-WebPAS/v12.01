.*****************************************************************************
.* System    :   Outpatient System                                           *
.* Program   :   CONVACTN                                                    *
.* Desc      :   Data Migration Program to upload O/P Action List data from  *
.*               a third party legacy system into the O/P webPAS module.     *
.*****************************************************************************
.* Date      :   06/05/2011                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through the convactn.txt file and    *
.*               for each valid record found, it will write a new outartaf   *
.*               record for the patient.                                     *
.* Mods      :                                                               *
.*****************************************************************************
.* V11.02.01  09/02/2022  Thanh T       TSK 0905641                          *
.*            Recompiled as OUTHEDFD changes                                 *
.*****************************************************************************
.*        V10.12.03  29/05/2018  Tracey Nguyen     TSK 0857468               *
.*                   Replaced double quotes (") with single quote (') for    *
.*                   Presenting Complaint field OTARPCOM to fix javascript   *
.*                   error in OUTWEB08.PBL                                   *
.*        V10.12.02  02/02/2018  Richa Phogat      TSK 0829351               *
.*                   Updated ACTNCOMM to accept data upto 60 character length*
.*        V10.12.01  15/01/2018  Steve Armstrong   TSK 0829351               *
.*                   Added ACTNCOMM and associated processing code for       *
.*                   comments.                                               *
.*****************************************************************************
.*        V10.11.01  21/07/2017  Ebon Clements     TSK 0268970               *
.*                   Change to use OUTCLIFD index 1 instead of index 2       *
.*****************************************************************************
.*        V10.09.01  25/01/2017  Steve Armstrong   TSK 0830055               *
.*                   Mods to ignore eReferral type ibavaraf records in       *
.*                   CVIS0000.                                               *
.*****************************************************************************
.*        V10.05.01  16/12/2014  Steve Armstrong   CAR 305263                *
.*                   Added mapping file functionality                        *
.*****************************************************************************
.*        V10.04.01  17/03/2014  Steve Armstrong   CAR 296128                *
.*                   Added display of warning message if error records found.*
.*****************************************************************************
.*        V10.03.03  29/01/2013  Ebon Clements     CAR 262292                *
.*                   Added interpreter required to upload file               *
.*        V10.03.02  23/04/2012  Steve Armstrong   CAR 261237                *
.*                   Moved call to GTYP0000 from within VCOD3000 to after    *
.*                   CVAL0000.                                               *
.*        V10.03.01  16/04/2012  Steve Armstrong   CAR 261237                *
.*                   Mods to determine default clinic type.                  *
.*****************************************************************************
.*        V10.02.03  29/08/2011  Steve Armstrong   CAR 249864                *
.*                   Changed ACTNRDEP and ACTNADEP to use Cat CG instead of  *
.*                   Cat AC.                                                 *
.*                   Also made changes when validating clinic id to do so    *
.*                   disregarding date.                                      *
.*                   Also made changes when validating preferred date to     *
.*                   accept dates in the past as well as in the future.      *
.*        V10.02.02  10/08/2011  Steve Armstrong   CAR 248244                *
.*                   Mods to make Reason for Request non-mandatory.          *
.*        V10.02.01  06/06/2011  Steve Armstrong   CAR 240687                *
.*                   Mods to use ostcatg for source of referral              *
.*        V10.02.00  06/05/2011  Steve Armstrong   CAR 240687                *
.*                   Created program                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC
          INC       IBAALVFD/INC
          INC       OUTARTFD/INC
          INC       OUTCLIFD/INC
          INC       OUTMA1FD/INC
          INC       OUTRCMFD/INC
          INC       OUTSITFD/INC
          INC       PATCODFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
.
.         Temporary Mapping Files
.
.         Hospital File - hospital.dat (pathspaf)
.
HOSPLFLG  FORM      1                            * Hospital mapping flag
TEMP1     IFILE     SQL, FIXED=7, KEY="U1-3"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
OLDHOSPL  DIM       3        3          1       Old hospital code
NEWHOSPL  DIM       3        3          4       New hospital code
.End of Record                          7
.
.
.         Category-Codes File - catcodes.dat
.
CATCDFLG  FORM      1                            * Cat-Code mapping flag
TEMP2     IFILE     SQL, FIXED=11, KEY="U1-2,3-5"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
OLDCTCAT  DIM       2        2          1       Old category
OLDCTCOD  DIM       3        3          3       Old code
NEWCTCAT  DIM       2        2          6       New category
NEWCTCOD  DIM       3        3          8       New code
.End of Record                         11
.
.
.         Clinic Id File - clinicid.dat (outcliaf)
.
CLIIDFLG  FORM      1                            * O/P Clinic Id mapping flag
TEMP3     IFILE     SQL, FIXED=25, KEY="U1-6,7-12"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
OLDSITCD  DIM       6        6          1       Old site code
OLDCLIID  DIM       6        6          7       Old clinic id code
NEWSITCD  DIM       6        6         13       New site code
NEWCLIID  DIM       6        6         19       New clinic id code
.End of Record                         25
.
.
.         O/P Action List upload file layout - convactn.txt
.
OPACTUPL  FILE      HL7, FIXED=4000         * Pipe delimited upload file
.                                             4096 is maximum
.
.         Fields must be pipe delimited and in the following sequence.
.         Field lengths are irrelevant other than the fact that where
.         a field is longer than allowed, the extra data will be ignored.
.
ACTNURNO  DIM       8       1     * UR Number                         (otarurno)
ACTNAVIS  DIM       20      9     * Alternate Referral No.   (ibaalvaf.ibavavis)
                                      (Linked A/H Referral Number)    (otarrefn)
ACTNRQDT  DIM       8       29    * Request Date (ccyymmdd)           (otarrqdt)
ACTNRQTM  DIM       8       37    * Request Time (hh:mm:ss)           (otarrqtm)
ACTNRDEP  DIM       3       45    * Requesting Department (Cat CG)    (otarrdep)
ACTNADEP  DIM       3       48    * Appt. with Department (Cat CG)    (otaradep)
ACTNPRDT  DIM       8       51    * Preferred Date (ccyymmdd)         (otarprdt)
ACTNPRHS  DIM       3       59    * Preferred Hospital                (otarprhs)
ACTNPRSI  DIM       6       62    * Preferred Site                    (otarprsi)
ACTNCLID  DIM       6       68    * Clinic Id                         (otarclid)
ACTNVIST  DIM       3       74    * Visit Type (Cat CV)               (otarvist)
ACTNTRRQ  DIM       3       77    * Transport Required (Cat OB)       (otartrrq)
ACTNPCOM  DIM       50      80    * Presenting Complaint              (otarpcom)
ACTNFINC  DIM       3       130   * Financial Class (Cat CL)          (otarfinc)
ACTNPRIO  DIM       3       133   * Priority (Cat PR)                 (otarprio)
ACTNSORF  DIM       3       136   * Source of Referral (ostcatg)      (otarsorf)
ACTNSARR  DIM       3       139   * Special Arrangements (Cat SA)     (otarsarr)
ACTNSTAT  DIM       1       142   * Status  0 - Requested             (otarstat)
.                                           1 - Pending Reschedule
ACTNRREQ  DIM       3       143   * Reason for Request (Cat VU)       (otarrreq)
ACTNCDAT  DIM       8       146   * Create Request Date (ccyymmdd)    (otarcdat)
ACTNCTIM  DIM       8       154   * Create Request Time (hh:mm:ss)    (otarctim)
ACTNCUID  DIM       10      162   * User who created Request          (otarcuid)
ACTNINTR  DIM       1       172   * Interpreter Req'd (Y/N)           (otarintr)
ACTNCOMM  DIM       800     173   * Outpatient Request Comments       (outrcmaf)
.
. End of Record             973
.
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ACTNREFN  DIM       8             * Linked O/P A/H Referral
ADDCOUNT  FORM      8             * added record count
.
CODCOUNT  FORM      8             * code error record count
COMCOUNT  FORM      8             * comment error record count
COMMTLEN  FORM      4
CURRCENT  FORM      2             * current century
CURRDATE  DIM       8             * current date
.
DATEFLG1  FORM      1             * Date Type flag
.                                     0 = full date (ccyymmdd)
.                                     1 = partial date (ccyymm)
DATEFLG2  FORM      1             * Dates Allowed flag
.                                     0 = any date allowed
.                                     1 = future dates not allowed
.                                     2 = future dates only allowed
DIM1A     DIM       1
DIM1B     DIM       1
DIM2C     DIM       2
DIM2D     DIM       2
DIM2H     DIM       2
DIM2M     DIM       2
DIM2S     DIM       2
DIM2Y     DIM       2
DIM3      DIM       3
DIM4      DIM       4
DIM6      DIM       6
DIM8      DIM       8
DIM50     DIM       50
DIM60     DIM       60
DTECOUNT  FORM      8             * date error record count
DUPCOUNT  FORM      8             * duplicate record count
.
ERORDESC  DIM       70
ERORFLAG  FORM      1             * Field Validation Error Flag
.                                     0 = No Errors on Field Validations
.                                     1 = Errors on Field Validations
.
FILEPRFX  DIM       3             * O/P file prefix
FORM10    FORM      10
.
LINENUMB  FORM      3
.
MANCOUNT  FORM      8             * mandatory field error record count
.
NUMCOUNT  FORM      8             * number error record count
.
RECCOUNT  FORM      8             * record read counter
SAVPRFIX  DIM       3             * saved O/P file prefix (outcliaf)
.
TEMPCATG  DIM       6             * Category (or HF for HF Table validation)
TEMPCODE  DIM       10            * Coded field (length = max code field length)
TEMPDATE  DIM       8
TEMPTIME  DIM       8
TEMPTYPE  FORM      1             * Type of coded field
TIMCOUNT  FORM      8             * time error record count
TMPFIELD  DIM       8             * Field name
TMPSTRNG  DIM       40            * Temporary string (max field length)
.
UNKCOUNT  FORM      8             * unknown U/R counter
.
VCHKFLAG  FORM      1             * Validation flag
.                                     0 = writing data to database
.                                     1 = only validating import data file
.
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
QUOTE     INIT      "#"'"                                            *T0857468
TILDA20   INIT      "~~~~~~~~~~~~~~~~~~~~"
.
.
.
PRGID     INIT      "CONVACTN"
PRGNAM    INIT      "Upload O/P Action List"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with upload
.
MAIN1000  CALL      OPEN0000               * open upload file
          BRANCH    EXIT,MAIN0100          * file not found
.
          CALL      DMAP0000               * display mapping files
          CALL      ASKQ0000               * validation run only ?
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      UPLD0000               * process upload
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"ibaalvaf";
          OPEN      IBAALVA1,"ibaalvaf"
          OPEN      IBAALVA2,"ibaalvaf"
.
          DISPLAY   *P64:24,"outartaf";
          OPEN      OUTARTA1,"outartaf"
.
          DISPLAY   *P64:24,"outrcmaf";
          OPEN      OUTRCMA1,"outrcmaf"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          MOVE      ONE,CNOUNDLN
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data upload                         *
.*                        TRUE  (1)  Exit selected                           *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Data Upload"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run data upload
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               OPEN0000              Called by: MAIN0000   *
.*                  Open upload file (convactn.txt)                          *
.*****************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD
.
          TRAP      OVERCOND IF IO
          OPEN      OPACTUPL,"convactn"
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN9100
.
.         Open all Mapping files.
.
.         Hospital mapping file (pathspaf)
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP1,"hospital"             * Hospital file found ?
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      ZERO,HOSPLFLG              * no
          ELSE
            MOVE      ONE,HOSPLFLG               * yes
          ENDIF
.
.         Category-Codes mapping file (patcodes)
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP2,"catcodes"             * Cat-Code file found ?
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      ZERO,CATCDFLG              * no
          ELSE
            MOVE      ONE,CATCDFLG               * yes
          ENDIF
.
.         O/P Clinic Id mapping file (outcliaf)
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP3,"clinicid"             * O/P Clinic Id file found ?
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      ZERO,CLIIDFLG              * no
          ELSE
            MOVE      ONE,CLIIDFLG               * yes
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9100  DISPLAY   *P1:24,*EL,*B,"Upload file - convactn.txt - not found.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
OPEN9999  RETURN
+
.******************************************************************************
.*                                  DMAP0000              Called by: MAIN0000 *
.*                       Display File Name & Mapping files                    *
.* Requires: Mapping Flags                                                    *
.******************************************************************************
.
DMAP0000  MOVE      ZERO,HLEF
          CALL      GETSCR00                     * save current screen
.
          DISPLAY   *P1:4,*EF,*V2LON,SP9,*ULON,"Mapping Tables"
.
          MOVE      DNO,DIM3
          LOAD      DIM3,HOSPLFLG,DYES
.
          DISPLAY   *P1:6,*EL,"Mapping Hospital Code (pathspaf)       ":
                    *V2LON,DIM3
.
          MOVE      DNO,DIM3
          LOAD      DIM3,CATCDFLG,DYES
.
          DISPLAY   *P1:7,*EL,"Mapping Category/Codes (patcodes)      ":
                    *V2LON,DIM3
.
          MOVE      DNO,DIM3
          LOAD      DIM3,CLIIDFLG,DYES
.
          DISPLAY   *P1:8,*EL,"Mapping Site/Clinic Id (outcliaf)      ":
                    *V2LON,DIM3
.
          DISPLAY   *P1:24;
          CALL      HITENTER
          CALL      PUTSCR00                     * restore previous screen
.
DMAP9999  RETURN
+
.*****************************************************************************
.*                               ASKQ0000              Called by: MAIN0000   *
.*                  Ask if running validation only                           *
.* Returns: VCHKFLAG  - validation flag                                      *
.*                       0 = normal mode                                     *
.*                       1 = validation only mode                            *
.*****************************************************************************
.
ASKQ0000  MOVE      ANSY,ANS
          KEYIN     *P1:10,*EL,"Validation only (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P25:10,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P25:10,*V2LON,DYES,*HOFF:
                      *P35:10,"(No data will be uploaded)"
            MOVE      ONE,VCHKFLAG               * yes
            GOTO      ASKQ9999
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P25:10,*V2LON,DNO,*HOFF:
                      *P35:10,"(Data will be uploaded)"
            MOVE      ZERO,VCHKFLAG              * yes
            GOTO      ASKQ9999
          ENDIF
.
          GOTO      ASKQ0000                     * invalid input
.
ASKQ9999  RETURN
+
.******************************************************************************
.*                                  UPLD0000              Called by: MAIN0000 *
.*                  Process the PMI upload file records                       *
.******************************************************************************
.
UPLD0000  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      CCC,CURRCENT
.
          DISPLAY   *P1:13,"Processing ",*V2LON,"convactn.txt",*HOFF:
                    *P1:15,"Started    : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:17,"U/R Number : ":
                    *P1:18,"Referral   : ":
                    *P1:19,"Records    : "
.
          MOVE      ZERO,RECCOUNT                * initialise records read
          MOVE      ZERO,ADDCOUNT                * initialise added record count
          MOVE      ZERO,CODCOUNT                * init. code error record count
          MOVE      ZERO,COMCOUNT                * init. comm error record count
          MOVE      ZERO,DUPCOUNT                * initialise dup. record count
          MOVE      ZERO,DTECOUNT                * init. date error record count
          MOVE      ZERO,TIMCOUNT                * init. time error record count
          MOVE      ZERO,MANCOUNT                * init. mand error record count
          MOVE      ZERO,NUMCOUNT                * init. numb error record count
          MOVE      ZERO,UNKCOUNT                * init. unknown pt record count
.
          CALL      HEAD0000                     * Print the report header
.
.         Loop through upload file records
.
UPLD1000  READ      OPACTUPL,SEQ;ACTNURNO,ACTNAVIS,ACTNRQDT,ACTNRQTM,ACTNRDEP:
                                 ACTNADEP,ACTNPRDT,ACTNPRHS,ACTNPRSI,ACTNCLID:
                                 ACTNVIST,ACTNTRRQ,ACTNPCOM,ACTNFINC,ACTNPRIO:
                                 ACTNSORF,ACTNSARR,ACTNSTAT,ACTNRREQ,ACTNCDAT:
                                 ACTNCTIM,ACTNCUID,ACTNINTR,ACTNCOMM
          GOTO      UPLD9000 IF OVER
.
          ADD       ONE,RECCOUNT                 * increment records read
          IF        (RECCOUNT%1000) = 0 | RECCOUNT = 1
            DISPLAY   *P14:17,*V2LON,ACTNURNO:
                      *P14:18,ACTNAVIS:
                      *P14:19,RECCOUNT;
          ENDIF
.
          CALL      POUT0000                     * pad out fields with spaces
.
          CALL      CLER0000                     * clear database variables
          CALL      CLPATMAS                     * clear patient master vars.
.
          CALL      VURN0000                     * validate U/R
          IF        EXIT = 1 & VCHKFLAG = 0
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      MAND0000                     * check for Mandatory fields
          IF        EXIT = 1 & VCHKFLAG = 0
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      DVAL0000                     * date validation
          IF        EXIT = 1 & VCHKFLAG = 0
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      TVAL0000                     * time validation
          IF        EXIT = 1 & VCHKFLAG = 0
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      VNUM0000                     * number validation
          IF        EXIT = 1 & VCHKFLAG = 0
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      SETV0000                     * set up file variables
.
          CALL      CVAL0000                     * validate coded fields
          IF        EXIT = 1 & VCHKFLAG = 0
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      GTYP0000                     * get clinic type
.
          PACK      KEY32,ACTNURNO,ACTNREFN,ACTNRQDT,ACTNRQTM
          CALL      RAOTART1
          IF        OVRCD = 1
            BRANCH    VCHKFLAG,UPLD1000          * validation only
.
.           If there are comments, then we need to get the next unique
.           comment id before we write the outartaf record
.
            IF        COMMTLEN > 0
              CALL      GUNQ0000                 * load comment unique no.
            ENDIF
.
            CALL      WROTART1                   * write new record
            ADD       ONE,ADDCOUNT               * increment added record count
.
            CALL      PCOM0000                   * write comments
          ELSE
            MOVE      "Action list record already exists ",ERORDESC
            CALL      PERR0000                   * print error line
            ADD       ONE,DUPCOUNT               * increment dupl. record count
          ENDIF
.
          GOTO      UPLD1000                     * get next record
.
.         Upload completed
.
UPLD9000  ASSIGN    (CODCOUNT+COMCOUNT+DUPCOUNT+DTECOUNT+TIMCOUNT+MANCOUNT+NUMCOUNT+UNKCOUNT),FORM10
          IF        FORM10 > 0
            CALL      LINE0000                   * print bottom line if errors
          ENDIF
.
          COMPARE   CLNO,FIFTY2                  * room for stats ?
          CALL      HEAD0000 IF LESS             * no - new page req'd.
.
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P14:19,*V2LON,RECCOUNT,*HOFF:
                    *P1:21,"Finished   : ",*V2LON,CTIMEIS
.
.         Warn the user if there were any records with errors
.
          IF        FORM10 > 0
            DISPLAY   *P1:22,*V2LON,*BLINKON,"Warning:  Records found with ":
                      "errors.  Refer to error report."
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,"Upload complete.  ";
          CALL      HITENTER
.
          PRINT     *N:
                    *1,"Total records read                    - ",RECCOUNT,*N:
                    *1,"Records added                         - ",ADDCOUNT,*N:
                    *1,"Duplicate Records detected            - ",DUPCOUNT,*N:
                    *1,"Records with invalid codes detected   - ",CODCOUNT,*N:
                    *1,"Records where comments didn't load    - ",COMCOUNT,*N:
                    *1,"Records with invalid dates detected   - ",DTECOUNT,*N:
                    *1,"Records with invalid times detected   - ",TIMCOUNT,*N:
                    *1,"Records with unknown U/R numbers      - ",UNKCOUNT,*N:
                    *1,"Records with blank mandatory fields   - ",MANCOUNT,*N:
                    *1,"Records with invalid numbers          - ",NUMCOUNT:
                    *N,*N,*1,"*** End of Report ***"
.
UPLD9999  RETURN
+
.****************************************************************************
.*                             MAND0000                Called by: UPLD0000  *
.*                 Check for standard Mandatory fields                      *
.* Returns: EXIT   0 = all mandatory fields are populated                   *
.*                 1 = one or more mandatory fields are blank               *
.*          MANCOUNT - updated count of records with mandatory fields       *
.*                     missing                                              *
.****************************************************************************
.
MAND0000  MOVE      ZERO,ERORFLAG                * init. validation error flag
.
          MOVE      ACTNAVIS,TMPSTRNG
          MOVE      "ACTNAVIS",TMPFIELD          * Alternate Referral Number
          CALL      CHKM0000
          BRANCH    ERORFLAG,MAND1000            * field not populated
          CALL      CVIS0000                     * Get webPAS A/H Ref. Number
.
MAND1000  MOVE      ACTNRQDT,TMPSTRNG
          MOVE      "ACTNRQDT",TMPFIELD          * Request Date
          CALL      CHKM0000
.
          MOVE      ACTNRQTM,TMPSTRNG
          MOVE      "ACTNRQTM",TMPFIELD          * Request Time
          CALL      CHKM0000
.
          MOVE      ACTNPRHS,TMPSTRNG
          MOVE      "ACTNPRHS",TMPFIELD          * Preferred Hospital
          CALL      CHKM0000
.
          MOVE      ACTNPRSI,TMPSTRNG
          MOVE      "ACTNPRSI",TMPFIELD          * Preferred Site
          CALL      CHKM0000
.
          MOVE      ACTNSTAT,TMPSTRNG
          MOVE      "ACTNSTAT",TMPFIELD          * Status
          CALL      CHKM0000
.
          BRANCH    ERORFLAG,MAND9100            * errors on mand. fields
.
          MOVE      ZERO,EXIT                    * no errors on mand. fields
          GOTO      MAND9999
.
MAND9100  ADD       ONE,MANCOUNT                 * incr. mand error record count
          MOVE      ONE,EXIT
.
MAND9999  RETURN
+
.*****************************************************************************
.*                            CHKM0000             Called by: MAND0000       *
.*                    Check if a mandatory field is blank                    *
.* Requires: TMPSTRNG - field to be checked                                  *
.*           TMPFIELD - 8 character field name                               *
.* Returns:  ERORFLAG = 1   field is blank (error)                           *
.*****************************************************************************
.
CHKM0000  MATCH     TMPSTRNG,SP20                * blank field ?
          GOTO      CHKM9999 IF NOT EQUAL        * no - finished
.
          MOVE      "Mandatory Field blank ",ERORDESC
          ENDSET    ERORDESC
          APPEND    LBRACK,ERORDESC
          APPEND    TMPFIELD,ERORDESC
          APPEND    RBRACK,ERORDESC
          APPEND    SP70,ERORDESC
          RESET     ERORDESC
          CALL      PERR0000                     * print error line
.
          MOVE      ONE,ERORFLAG                 * set error flag
.
CHKM9999  RETURN
+
.******************************************************************************
.*                                  CLER0000              Called by: UPLD0000 *
.*                    Clear all the database fields                           *
.******************************************************************************
.
CLER0000  MOVE      SP8,OTARREFN
          MOVE      SP8,OTARRQDT
          MOVE      SP8,OTARRQTM
          MOVE      SP3,OTARRDEP
          MOVE      SP3,OTARADEP
          MOVE      SP8,OTARPRDT
          MOVE      SP3,OTARPRHS
          MOVE      SP6,OTARPRSI
          MOVE      SP6,OTARCLTY
          MOVE      SP6,OTARCLID
          MOVE      SP3,OTARVIST
          MOVE      SP3,OTARTRRQ
          PACK      OTARPCOM,SP30,SP20
          MOVE      SP3,OTARFINC
          MOVE      SP3,OTARPRIO
          MOVE      SP3,OTARSORF
          MOVE      SP3,OTARSARR
          MOVE      SP8,OTAROOBN
          MOVE      SP1,OTARSTAT
          MOVE      SP3,OTARRREQ
          MOVE      SP3,OTARRCAN
          MOVE      SP8,OTARCADT
          MOVE      SP8,OTARCATM
          MOVE      SP10,OTARCAUS
          MOVE      SP8,OTARBKNO
          MOVE      SP6,OTARBKSI
          MOVE      SP6,OTARBKCL
          MOVE      SP8,OTARBKDT
          MOVE      SP5,OTARCLST
          MOVE      SP3,OTARCLSN
          MOVE      SP5,OTARCLTM
          MOVE      SP10,OTARBKID
          MOVE      SP8,OTARCDAT
          MOVE      SP8,OTARCTIM
          MOVE      SP10,OTARCUID
          MOVE      SP8,OTARCPDT
          MOVE      SP8,OTARCPTM
          MOVE      SP10,OTARCPID
          MOVE      SP10,OTARINTR
          MOVE      SP10,OTARLNG1
          MOVE      SP3,OTARFSRC
          MOVE      SP10,OTARCMID
.
CLER9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: UPLD0000 *
.*                           Print The Report Header                 PERR0000 *
.******************************************************************************
.
HEAD0000  MOVE      "- Error Report",CPHDROPT
          CALL      HEAD132                 * Print the report header
.
          PRINT     *1,"< convactn.txt >"
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"U/R No.",*12,PIPE,*14,"Patient Name":
                    *65,PIPE,*67,"Alt. Referral No.":
                    *88,PIPE,*90,"Error Description",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SEVEN,CLNO                   * initialise line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      Draw line across page                               *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
.******************************************************************************
.*                                  SETV0000              Called by: UPLD0000 *
.*                       Load the record variables                            *
.******************************************************************************
.
SETV0000  MOVE      ACTNREFN,OTARREFN
          MOVE      ACTNRQDT,OTARRQDT
          MOVE      ACTNRQTM,OTARRQTM
.
          MOVE      ACTNRDEP,OTARRDEP
          IF        CATCDFLG = 1
            PACK      KEY5,ANSC,ANSG,ACTNRDEP
            CALL      RDTEMP2                    * cat-code on map file ?
            IF        OVRCD = 0
              MOVE      NEWCTCOD,OTARRDEP        * yes - load mapped code
            ENDIF
          ENDIF
.
          MOVE      ACTNADEP,OTARADEP
          IF        CATCDFLG = 1
            PACK      KEY5,ANSC,ANSG,ACTNADEP
            CALL      RDTEMP2                    * cat-code on map file ?
            IF        OVRCD = 0
              MOVE      NEWCTCOD,OTARADEP        * yes - load mapped code
            ENDIF
          ENDIF
.
          MOVE      ACTNPRDT,OTARPRDT
.
          MOVE      ACTNPRHS,OTARPRHS
          IF        HOSPLFLG = 1
            MOVE      ACTNPRHS,KEY3
            CALL      RDTEMP1                    * hospital code on map file ?
            IF        OVRCD = 0
              MOVE      NEWHOSPL,OTARPRHS        * yes - load mapped code
            ENDIF
          ENDIF
.
          MOVE      ACTNPRSI,OTARPRSI
          MOVE      ACTNCLID,OTARCLID
          IF        CLIIDFLG = 1
            PACK      KEY12,ACTNPRSI,ACTNCLID
            CALL      RDTEMP3                    * site/clinid id on map file ?
            IF        OVRCD = 0
              MOVE      NEWSITCD,OTARPRSI        * yes - load mapped site code
              MOVE      NEWCLIID,OTARCLID        * load mapped clinic id code
            ENDIF
          ENDIF
.
          MOVE      ACTNVIST,OTARVIST
          IF        CATCDFLG = 1
            PACK      KEY5,ANSC,ANSV,ACTNVIST
            CALL      RDTEMP2                    * cat-code on map file ?
            IF        OVRCD = 0
              MOVE      NEWCTCOD,OTARVIST        * yes - load mapped code
            ENDIF
          ENDIF
.
          MOVE      ACTNTRRQ,OTARTRRQ
          IF        CATCDFLG = 1
            PACK      KEY5,ANSO,ANSB,ACTNTRRQ
            CALL      RDTEMP2                    * cat-code on map file ?
            IF        OVRCD = 0
              MOVE      NEWCTCOD,OTARTRRQ        * yes - load mapped code
            ENDIF
          ENDIF
.
          MOVE      ACTNPCOM,OTARPCOM
          REP       QUOTE,OTARPCOM                                   *T0857468
.
          MOVE      ACTNFINC,OTARFINC
          IF        CATCDFLG = 1
            PACK      KEY5,ANSC,ANSL,ACTNFINC
            CALL      RDTEMP2                    * cat-code on map file ?
            IF        OVRCD = 0
              MOVE      NEWCTCOD,OTARFINC        * yes - load mapped code
            ENDIF
          ENDIF
.
          MOVE      ACTNPRIO,OTARPRIO
          IF        CATCDFLG = 1
            PACK      KEY5,ANSP,ANSR,ACTNPRIO
            CALL      RDTEMP2                    * cat-code on map file ?
            IF        OVRCD = 0
              MOVE      NEWCTCOD,OTARPRIO        * yes - load mapped code
            ENDIF
          ENDIF
.
          MOVE      ACTNPRSI,KEY6
          CALL      RDSITA1                      * valid code ?
          IF        OVRCD = 0
            MOVE      ACTNSORF,OTARSORF
            IF        CATCDFLG = 1
              PACK      KEY5,OSTCATG,ACTNSORF
              CALL      RDTEMP2                  * cat-code on map file ?
              IF        OVRCD = 0
                MOVE      NEWCTCOD,OTARSORF      * yes - load mapped code
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ACTNSARR,OTARSARR
          IF        CATCDFLG = 1
            PACK      KEY5,ANSS,ANSA,ACTNSARR
            CALL      RDTEMP2                    * cat-code on map file ?
            IF        OVRCD = 0
              MOVE      NEWCTCOD,OTARSARR        * yes - load mapped code
            ENDIF
          ENDIF
.
          MOVE      ACTNSTAT,OTARSTAT
.
          MOVE      ACTNRREQ,OTARRREQ
          IF        CATCDFLG = 1
            PACK      KEY5,ANSV,ANSU,ACTNRREQ
            CALL      RDTEMP2                    * cat-code on map file ?
            IF        OVRCD = 0
              MOVE      NEWCTCOD,OTARRREQ        * yes - load mapped code
            ENDIF
          ENDIF
.
          MOVE      ACTNCDAT,OTARCDAT
          MOVE      ACTNCTIM,OTARCTIM
          MOVE      ACTNCUID,OTARCUID
.
          MATCH     ANSY,ACTNINTR
          IF        @EQUAL
            MOVE      ONE,OTARINTR
          ELSE
            MOVE      ZERO,OTARINTR
          ENDIF
.
          MATCH     "1",OTARINTR
          IF        @EQUAL
            MOVE      PMPXLNG1,OTARLNG1
          ELSE
            MOVE      SP70,OTARLNG1
          ENDIF
.
          MOVE      SP70,OTARSPAR
.
.         If there is no user id for creation of the record, then
.         default to program name and date/time of upload.
.
          MATCH     SP10,OTARCUID
          IF        @EQUAL
            MOVE      "CONVACTN  ",OTARCUID
            MOVE      CURRDATE,OTARCDAT
            MOVE      CTIMEIS,OTARCTIM
          ENDIF
.
SETV9999  RETURN
+
.*****************************************************************************
.*                            POUT0000             Called by: UPLD0000       *
.*            Pad out all the record fields with spaces given that we        *
.*            are dealing with pipe delimited records                        *
.*****************************************************************************
.
POUT0000  PACK      ACTNURNO,ACTNURNO,SP8 
          RJUSTIFY  ACTNURNO
          PACK      ACTNAVIS,ACTNAVIS,SP20 
          PACK      ACTNRQDT,ACTNRQDT,SP8 
          PACK      ACTNRQTM,ACTNRQTM,SP8 
          PACK      ACTNRDEP,ACTNRDEP,SP3 
          PACK      ACTNADEP,ACTNADEP,SP3 
          PACK      ACTNPRDT,ACTNPRDT,SP8 
          PACK      ACTNPRHS,ACTNPRHS,SP3 
          PACK      ACTNPRSI,ACTNPRSI,SP6 
          PACK      ACTNCLID,ACTNCLID,SP6 
          PACK      ACTNVIST,ACTNVIST,SP3 
          PACK      ACTNTRRQ,ACTNTRRQ,SP3 
          PACK      ACTNPCOM,ACTNPCOM,SP30,SP20
          PACK      ACTNFINC,ACTNFINC,SP3 
          PACK      ACTNPRIO,ACTNPRIO,SP3 
          PACK      ACTNSORF,ACTNSORF,SP3 
          PACK      ACTNSARR,ACTNSARR,SP3 
          PACK      ACTNSTAT,ACTNSTAT,SP1 
          PACK      ACTNRREQ,ACTNRREQ,SP3 
          PACK      ACTNCDAT,ACTNCDAT,SP8 
          PACK      ACTNCTIM,ACTNCTIM,SP8 
          PACK      ACTNCUID,ACTNCUID,SP10
          PACK      ACTNINTR,ACTNINTR,SP10
          REP       UPPLOW,ACTNINTR
.
.         Determine if we have any comments
.
          STRIP     ACTNCOMM                     * remove trailing spaces
          MOVELPTR  ACTNCOMM,COMMTLEN            * get comment field length
.
POUT9999  RETURN
+
.*****************************************************************************
.*                              DVAL0000           Called by: UPLD0000       *
.*                  Validate all date fields                                 *
.* Returns:  EXIT - 0 = No date validation errors                            *
.*                  1 = Date validation errors                               *
.*          DTECOUNT - updated count of records with date field errors       *
.*****************************************************************************
.
DVAL0000  MOVE      ZERO,ERORFLAG                * init. validation error flag
.
          MOVE      ACTNRQDT,TEMPDATE
          MOVE      ZERO,DATEFLG1                * ccyymmdd
          MOVE      ONE,DATEFLG2                 * future date not allowed
          MOVE      "ACTNRQDT",TMPFIELD
          CALL      VDAT0000
.
          MOVE      ACTNPRDT,TEMPDATE
          MOVE      ZERO,DATEFLG1                * ccyymmdd
          MOVE      ZERO,DATEFLG2                * only future date allowed
          MOVE      "ACTNPRDT",TMPFIELD
          CALL      VDAT0000
.
          MOVE      ACTNCDAT,TEMPDATE
          MOVE      ZERO,DATEFLG1                * ccyymmdd
          MOVE      ONE,DATEFLG2                 * future date not allowed
          MOVE      "ACTNCDAT",TMPFIELD
          CALL      VDAT0000
.
          BRANCH    ERORFLAG,DVAL9100            * errors on date validation
.
          MOVE      ZERO,EXIT                    * no errors on date validation
          GOTO      DVAL9999
.
DVAL9100  ADD       ONE,DTECOUNT                 * incr. date error record count
          MOVE      ONE,EXIT
.
DVAL9999  RETURN
+
.*****************************************************************************
.*                              VDAT0000           Called by: DVAL0000       *
.*                  Validate a date field                                    *
.* Requires:  TEMPDATE - Date in format ccyymmdd                             *
.*            DATEFLG1 - Date Type flag                                      *
.*                          0 = full date (ccyymmdd)                         *
.*                          1 = partial date (ccyymm)                        *
.*            DATEFLG2 - Dates Allowed flag                                  *
.*                          0 = any date allowed                             *
.*                          1 = no future dates allowed                      *
.*                          2 = only future dates allowed                    *
.*            TMPFIELD - Date field name                                     *
.* Returns:   ERORFLAG = 1 if an error in date validation occurs.            *
.*****************************************************************************
.
VDAT0000  MATCH     SP8,TEMPDATE                 * blank date ?
          GOTO      VDAT9999 IF EQUAL            * yes
.
.         Validate that the date has been fully populated
.
          SQUEEZE   TEMPDATE
          MOVELPTR  TEMPDATE,FORM1
          IF        DATEFLG1 = 0 & FORM1 <> 8 | DATEFLG1 = 1 & FORM1 <> 6
            MOVE      "Insufficient digits ",ERORDESC
            CALL      DERR0000
            GOTO      VDAT9999
          ENDIF
.
.         Validate that the date is numeric
.
VDAT0100  TYPE      TEMPDATE
          IF        !@EQUAL
            MOVE      "Not numeric ",ERORDESC
            CALL      DERR0000
            GOTO      VDAT9999
          ENDIF
.
          UNPACK    TEMPDATE,DIM2C,DIM2Y,DIM2M,DIM2D
.
.         Validate the century is >= 18 and <= current century
.
          MOVE      DIM2C,FORM2
          IF        FORM2 < 18 | FORM2 > CURRCENT
            MOVE      "Century not valid ",ERORDESC
            CALL      DERR0000
            GOTO      VDAT9999
          ENDIF
.
.         Validate the month
.
          MOVE      DIM2M,FORM2
          IF        FORM2 < 1 | FORM2 > 12
            MOVE      "Month not valid ",ERORDESC
            CALL      DERR0000
            GOTO      VDAT9999
          ENDIF
.
          COMPARE   ONE,DATEFLG1                 * short date (ccyymm) ?
          GOTO      VDAT9999 IF EQUAL            * yes - finished
.
.         Validate the day is:
.              < 32 for January, March, May, July, August, October & December
.              < 31 for April, June, September & November
.              < 29 for February, except in a leap year where it is < 30
.
          BRANCH    FORM2,VDAT1000:              * Jan
                          VDAT3000:              * Feb
                          VDAT1000:              * Mar
                          VDAT2000:              * Apr
                          VDAT1000:              * May
                          VDAT2000:              * Jun
                          VDAT1000:              * Jul
                          VDAT1000:              * Aug
                          VDAT2000:              * Sep
                          VDAT1000:              * Oct
                          VDAT2000:              * Nov
                          VDAT1000               * Dec
.
.         Validate days for month of 31 days
.
VDAT1000  MOVE      DIM2D,FORM2
          IF        FORM2 < 1 | FORM2 > 31
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT4000
.
.         Validate days for month of 30 days
.
VDAT2000  MOVE      DIM2D,FORM2
          IF        FORM2 < 1 | FORM2 > 30
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT4000
.
.         Validate days for February
.         A leap year is one where:
.           1. Every year divisible by 4 is a leap year.
.           2. But every year divisible by 100 is NOT a leap year
.           3. Unless the year is also divisible by 400, then it is still a
.              leap year.
.
VDAT3000  MOVE      DIM2D,FORM2
          PACK      DIM4,DIM2C,DIM2Y
          MOVE      DIM4,FORM4
          IF        (FORM4%4) = 0
            IF        (FORM4%100) = 0
              IF        (FORM4%400) = 0
                GOTO      VDAT3200
              ENDIF
            ELSE
              GOTO      VDAT3200
            ENDIF
          ENDIF
.
.         Check for normal Feb days
.
VDAT3100  IF        FORM2 < 1 | FORM2 > 28
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT4000
.
.         Check for leap year Feb days
.
VDAT3200  IF        FORM2 < 1 | FORM2 > 29
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT4000
.
VDAT3900  MOVE      "Day not valid ",ERORDESC
          CALL      DERR0000
          GOTO      VDAT9999
.
VDAT4000  COMPARE   ZERO,DATEFLG2                * any date allowed ?
          GOTO      VDAT9999 IF EQUAL            * yes
.
          BRANCH    DATEFLG2,VDAT5000:           * date cannot be in the future
                             VDAT6000            * date must be in the future
.
.         Make sure the date is not in the future
.
VDAT5000  MATCH     TEMPDATE,CURRDATE
          IF        @LESS
            MOVE      "Cannot be in the future ",ERORDESC
            CALL      DERR0000
          ENDIF
          GOTO      VDAT9999
.
.         Make sure the date is in the future
.
VDAT6000  MATCH     TEMPDATE,CURRDATE
          IF        !@LESS
            MOVE      "Must be in the future ",ERORDESC
            CALL      DERR0000
          ENDIF
.
VDAT9999  RETURN
+
.*****************************************************************************
.*                            DERR0000             Called by: VDAT0000       *
.*                    Process date error                                     *
.* Requires: TMPFIELD - field name                                           *
.*           ERORDESC - error description                                    *
.* Returns:  ERORFLAG = 1 if an error in date validation occurs.             *
.*****************************************************************************
.
DERR0000  ENDSET    ERORDESC
          APPEND    LBRACK,ERORDESC
          APPEND    TMPFIELD,ERORDESC
          APPEND    RBRACK,ERORDESC
          APPEND    SP70,ERORDESC
          RESET     ERORDESC
          CALL      PERR0000                     * print error line
.
          MOVE      ONE,ERORFLAG                 * set date error flag
.
DERR9999  RETURN
+
.*****************************************************************************
.*                              TVAL0000           Called by: UPLD0000       *
.*                  Validate all time fields                                 *
.* Returns:  EXIT - 0 = No time validation errors                            *
.*                  1 = Time validation errors                               *
.*          TIMCOUNT - updated count of records with time field errors       *
.*****************************************************************************
.
TVAL0000  MOVE      ZERO,ERORFLAG                * init. validation error flag
.
          MOVE      ACTNRQDT,TEMPDATE
          MOVE      ACTNRQTM,TEMPTIME            * request time
          MOVE      "ACTNRQTM",TMPFIELD
          CALL      VTIM0000
.
          MOVE      ACTNCDAT,TEMPDATE
          MOVE      ACTNCTIM,TEMPTIME            * create request time
          MOVE      "ACTNCTIM",TMPFIELD
          CALL      VTIM0000
.
          BRANCH    ERORFLAG,TVAL9100            * errors on time validation
.
          MOVE      ZERO,EXIT                    * no errors on time validation
          GOTO      TVAL9999
.
TVAL9100  ADD       ONE,TIMCOUNT                 * incr. time error record count
          MOVE      ONE,EXIT
.
TVAL9999  RETURN
+
.*****************************************************************************
.*                            VTIM0000             Called by: TVAL0000       *
.*                    Validate time fields                                   *
.* Requires:  TEMPTIME - Time in format hh:mm:ss                             *
.*            TEMPDATE - Date in format ccyymmdd                             *
.*            TMPFIELD - Time field name                                     *
.* Returns:   ERORFLAG = 1 if an error in time validation occurs.            *
.*****************************************************************************
.
VTIM0000  MATCH     SP8,TEMPTIME                 * blank time ?
          GOTO      VTIM9999 IF EQUAL            * yes
.
          MATCH     SP8,TEMPDATE                 * blank date ?
          IF        @EQUAL
            MOVE      "Time without corresponding date ",ERORDESC
            CALL      TERR0000
            GOTO      VTIM9999
          ENDIF
.
          UNPACK    TEMPTIME,DIM2H,DIM1A,DIM2M,DIM1B,DIM2S    * (hh:mm:ss)
.
.         Make sure colon's separate the hours, minutes and seconds
.
          MATCH     DIM1A,DIM1B
          GOTO      VTIM1000 IF NOT EQUAL
.
          MATCH     COLON,DIM1A
          GOTO      VTIM1000 IF NOT EQUAL
.
          GOTO      VTIM2000
.
VTIM1000  MOVE      "Format not valid ",ERORDESC
          CALL      TERR0000
          GOTO      VTIM9999
.
.         Validate the hour
.
VTIM2000  MOVE      DIM2H,FORM2
          IF        FORM2 < 0 | FORM2 > 23
            MOVE      "Hour not valid ",ERORDESC
            CALL      TERR0000
            GOTO      VTIM9999
          ENDIF
.
.         Validate the minute
.
          MOVE      DIM2M,FORM2
          IF        FORM2 < 0 | FORM2 > 59
            MOVE      "Minutes not valid ",ERORDESC
            CALL      TERR0000
            GOTO      VTIM9999
          ENDIF
.
.         Validate the second
.
          MOVE      DIM2S,FORM2
          IF        FORM2 < 0 | FORM2 > 59
            MOVE      "Seconds not valid ",ERORDESC
            CALL      TERR0000
            GOTO      VTIM9999
          ENDIF
.
          MATCH     SP8,TEMPDATE                 * date blank ?
          GOTO      VTIM9999 IF EQUAL            * yes
.
.         Make sure that the transaction time is not in the future
.
          MATCH     TEMPDATE,CURRDATE
          IF        @EQUAL
            MATCH     TEMPTIME,CTIMEIS
            IF        @LESS
              MOVE      "Time in the future ",ERORDESC
              CALL      TERR0000
            ENDIF
          ENDIF
.
VTIM9999  RETURN
+
.*****************************************************************************
.*                            TERR0000             Called by: VTIM0000       *
.*                    Process time error                                     *
.* Requires: TMPFIELD - field name                                           *
.*           ERORDESC - error description                                    *
.* Returns:  ERORFLAG = 1 if an error in time validation occurs.             *
.*****************************************************************************
.
TERR0000  ENDSET    ERORDESC
          APPEND    LBRACK,ERORDESC
          APPEND    TMPFIELD,ERORDESC
          APPEND    RBRACK,ERORDESC
          APPEND    SP70,ERORDESC
          RESET     ERORDESC
          CALL      PERR0000                     * print error line
.
          MOVE      ONE,ERORFLAG                 * set time error flag
.
TERR9999  RETURN
+
.*****************************************************************************
.*                            VNUM0000             Called by: UPLD0000       *
.*                    Validate numeric fields                                *
.* Returns:  EXIT - 0 = No numeric validation errors                         *
.*                  1 = Numeric validation errors                            *
.*          NUMCOUNT - updated count of records with numeric field errors    *
.*****************************************************************************
.
VNUM0000  MOVE      ZERO,ERORFLAG                * init. validation error flag
.
.         Status numeric field (valid values 0 and 1)
.
          REP       " 0",ACTNSTAT
.
          TYPE      ACTNSTAT                     * status numeric ?
          GOTO      VNUM0050 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ACTNSTAT,FORM1
          COMPARE   FORM1,ONE                    * status 0 or 1 ?
          GOTO      VNUM0050 IF LESS             * no - error
          GOTO      VNUM9000                     * yes - valid value
.
VNUM0050  MOVE      "ACTNSTAT",TMPFIELD
          CALL      NERR0000
.
VNUM9000  BRANCH    ERORFLAG,VNUM9100            * errors on number validation
.
          MOVE      ZERO,EXIT                    * no errors on no. validation
          GOTO      VNUM9999
.
VNUM9100  ADD       ONE,NUMCOUNT                 * incr. num. error record count
          MOVE      ONE,EXIT
.
VNUM9999  RETURN
+
.*****************************************************************************
.*                            NERR0000             Called by: VNUM0000       *
.*                    Process number error                                   *
.* Requires: TMPFIELD - field name                                           *
.*****************************************************************************
.
NERR0000  MOVE      "Invalid numeric field ",ERORDESC
          ENDSET    ERORDESC
          APPEND    LBRACK,ERORDESC
          APPEND    TMPFIELD,ERORDESC
          APPEND    RBRACK,ERORDESC
          APPEND    SP70,ERORDESC
          RESET     ERORDESC
          CALL      PERR0000                     * print error line
.
          MOVE      ONE,ERORFLAG                 * set number error flag
.
NERR9999  RETURN
+
.*****************************************************************************
.*                           CVAL0000              Called by: UPLD0000       *
.*                 Validate all the relevant coded fields.                   *
.*        This validation is performed on the database fields just prior     *
.*        to writing the data and after any mappings have been performed.    *
.* Returns:  EXIT - 0 = No code validation errors                            *
.*                  1 = Code validation errors                               *
.*          CODCOUNT - updated count of records with coded field errors      *
.*****************************************************************************
.
CVAL0000  MOVE      ZERO,ERORFLAG                * init. validation error flag
.
          MATCH     SP3,OTARRDEP                 * blank code ?
          GOTO      CVAL0050 IF EQUAL            * yes - no validation req'd
.
          PACK      TEMPCATG,ANSC,ANSG
          PACK      TEMPCODE,OTARRDEP,SP10
          MOVE      "ACTNRDEP",TMPFIELD
          MOVE      ONE,TEMPTYPE                 * patcodes (requesting dept.)
          CALL      VCOD0000
.
CVAL0050  MATCH     SP3,OTARADEP                 * blank code ?
          GOTO      CVAL0100 IF EQUAL            * yes - no validation req'd
.
          PACK      TEMPCATG,ANSC,ANSG
          PACK      TEMPCODE,OTARADEP,SP10
          MOVE      "ACTNADEP",TMPFIELD
          MOVE      ONE,TEMPTYPE                 * patcodes (appt. with dept.)
          CALL      VCOD0000
.
CVAL0100  MATCH     SP3,OTARVIST                 * blank code ?
          GOTO      CVAL0200 IF EQUAL            * yes - no validation req'd
.
          PACK      TEMPCATG,ANSC,ANSV
          PACK      TEMPCODE,OTARVIST,SP10
          MOVE      "ACTNVIST",TMPFIELD
          MOVE      ONE,TEMPTYPE                 * patcodes (visit type)
          CALL      VCOD0000
.
CVAL0200  MATCH     SP3,OTARTRRQ                 * blank code ?
          GOTO      CVAL0300 IF EQUAL            * yes - no validation req'd
.
          PACK      TEMPCATG,ANSO,ANSB
          PACK      TEMPCODE,OTARTRRQ,SP10
          MOVE      "ACTNTRRQ",TMPFIELD
          MOVE      ONE,TEMPTYPE                 * patcodes (transport required)
          CALL      VCOD0000
.
CVAL0300  MATCH     SP3,OTARFINC                 * blank code ?
          GOTO      CVAL0400 IF EQUAL            * yes - no validation req'd
.
          PACK      TEMPCATG,ANSC,ANSL
          PACK      TEMPCODE,OTARFINC,SP10
          MOVE      "ACTNFINC",TMPFIELD
          MOVE      ONE,TEMPTYPE                 * patcodes (financial class)
          CALL      VCOD0000
.
CVAL0400  MATCH     SP3,OTARPRIO                 * blank code ?
          GOTO      CVAL0500 IF EQUAL            * yes - no validation req'd
.
          PACK      TEMPCATG,ANSP,ANSR
          PACK      TEMPCODE,OTARPRIO,SP10
          MOVE      "ACTNPRIO",TMPFIELD
          MOVE      ONE,TEMPTYPE                 * patcodes (priority)
          CALL      VCOD0000
.
CVAL0500  MATCH     SP3,OTARSARR                 * blank code ?
          GOTO      CVAL0600 IF EQUAL            * yes - no validation req'd
.
          PACK      TEMPCATG,ANSS,ANSA
          PACK      TEMPCODE,OTARSARR,SP10
          MOVE      "ACTNSARR",TMPFIELD
          MOVE      ONE,TEMPTYPE                 * patcodes (spec. arrangements)
          CALL      VCOD0000
.
CVAL0600  PACK      TEMPCATG,ANSV,ANSU
          PACK      TEMPCODE,OTARRREQ,SP10
          MOVE      "ACTNRREQ",TMPFIELD
          MOVE      ONE,TEMPTYPE                 * patcodes (reason for request)
          CALL      VCOD0000
.
          PACK      TEMPCODE,OTARPRHS,SP10
          MOVE      "ACTNPRHS",TMPFIELD
          MOVE      TWO,TEMPTYPE                 * pathspaf (preferred hospital)
          CALL      VCOD0000
.
          PACK      TEMPCODE,OTARPRSI,SP10
          MOVE      "ACTNPRSI",TMPFIELD
          MOVE      THREE,TEMPTYPE               * outsitaf (preferred site)
          CALL      VCOD0000
.         
          MATCH     SP3,FILEPRFX                 * O/P file prefix blank ?
          GOTO      CVAL9000 IF EQUAL            * yes - finished
.
.         The site file has been read, so now we can check the source
.         of referral using the referral category (outsitaf.ostcatg).
.
          MATCH     SP2,OSTCATG                  * blank SoR category ?
          IF        @EQUAL
            MOVE      "Referral category is blank ",ERORDESC 
            CALL      PERR0000                   * print error line
            MOVE      ONE,ERORFLAG
            GOTO      CVAL0700
          ENDIF
.
          MATCH     SP3,OTARSORF                 * blank code ?
          GOTO      CVAL0700 IF EQUAL            * yes - no validation req'd
.
          MOVE      OSTCATG,TEMPCATG
          PACK      TEMPCODE,OTARSORF,SP10
          MOVE      "ACTNSORF",TMPFIELD
          MOVE      ONE,TEMPTYPE                 * patcodes (source of referral)
          CALL      VCOD0000
.         
CVAL0700  MATCH     SAVPRFIX,TILDA20             * O/P file open ?
          GOTO      CVAL0800 IF EQUAL            * no - finished
.
          MATCH     SP6,OTARCLID                 * blank clinic id ?
          GOTO      CVAL0800 IF EQUAL            * yes - finished
.
          PACK      TEMPCODE,OTARCLID,SP10
          MOVE      "ACTNCLID",TMPFIELD
          MOVE      FOUR,TEMPTYPE                * outcliaf (clinic id)
          CALL      VCOD0000
.
CVAL0800  MATCH     SP1,ACTNINTR                 * blank code ?
          GOTO      CVAL9000 IF EQUAL            * yes - no validation req'd
.
          PACK      TEMPCODE,ACTNINTR,SP10
          MOVE      "ACTNINTR",TMPFIELD
          MOVE      FIVE,TEMPTYPE                * Y/N (Interpreter required)
          CALL      VCOD0000
.
          MATCH     ANSY,ACTNINTR                * yes interpreter required
          GOTO      CVAL9000 IF NOT EQUAL
.
          MATCH     "1",PMPXINTR
          IF        !@EQUAL
            CLEAR     ERORDESC
            APPEND    "PMI interpreter required mandatory ",ERORDESC
            APPEND    "for referral interpreter required ",ERORDESC
            RESET     ERORDESC
            CALL      PERR0000                     * print error line
            MOVE      ONE,ERORFLAG                 * set number error flag
          ENDIF
.
          MATCH     SP70,PMPXLNG1
          IF        @EQUAL
            CLEAR     ERORDESC
            APPEND    "PMI preferred language mandatory ",ERORDESC
            APPEND    "for referral interpreter required ",ERORDESC
            RESET     ERORDESC
            CALL      PERR0000                     * print error line
            MOVE      ONE,ERORFLAG                 * set number error flag
          ENDIF
.
CVAL9000  BRANCH    ERORFLAG,CVAL9100            * errors on code validation
.
          MOVE      ZERO,EXIT                    * no errors on code validation
          GOTO      CVAL9999
.
CVAL9100  ADD       ONE,CODCOUNT                 * incr. code error record count
          MOVE      ONE,EXIT
.
CVAL9999  RETURN
+
.*****************************************************************************
.*                           VCOD0000              Called by: CVAL0000       *
.*                   Validate a coded field                                  *
.* Requires: TEMPCATG - temporary category variable                          *
.*           TEMPCODE - temporary code variable                              *
.*           TEMPTYPE - temporary code type                                  *
.*           TMPFIELD - temporary field name                                 *
.* Returns:  ERORFLAG = 1 if an error in code validation occurs.             *
.*****************************************************************************
.
VCOD0000  BRANCH    TEMPTYPE,VCOD1000:           * patcodes (category-codes)
                             VCOD2000:           * hospital (pathspaf)
                             VCOD3000:           * site (outsitaf)
                             VCOD4000:           * clinic id (outcliaf)
                             VCOD5000            * Y/N
.
.         Validate a category-coded field (patcodes)
.
VCOD1000  PACK      KEY5,TEMPCATG,TEMPCODE
          CALL      RDCODE1                      * valid code ?
          BRANCH    OVRCD,VCOD9100               * no - error
          GOTO      VCOD9999                     * yes
.
.         Validate a hospital field (pathspaf)
.
VCOD2000  MOVE      TEMPCODE,KEY3
          CALL      RDPTHSP1                     * valid code ?
          BRANCH    OVRCD,VCOD9100               * no - error
          GOTO      VCOD9999                     * yes
.
.         Validate a site field (outsitaf)
.
VCOD3000  MOVE      SP3,FILEPRFX                 * default site prefix to blank
          MOVE      TEMPCODE,KEY6
          CALL      RDSITA1                      * valid code ?
          BRANCH    OVRCD,VCOD9100               * no - error
.
          MOVE      OSTFILE,FILEPRFX             * load site file prefix
.         
          MATCH     SP3,OSTFILE
          IF        @EQUAL
            MOVE      "Site prefix is blank ",ERORDESC 
            CALL      PERR0000                   * print error line
            GOTO      VCOD9200
          ENDIF
.
          MATCH     FILEPRFX,SAVPRFIX            * same O/P file prefix ?
          GOTO      VCOD9999 IF EQUAL            * yes - don't reopen file
.         
.         We have a new site prefix, so open the clinic id file.
.
          CLOSE     OUTCLIA1                     * close current file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,FILEPRFX,FILCLIA1
          OPEN      OUTCLIA1,CFNAME              * open new file
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Clinic Id file not found (",ERORDESC
            ENDSET    ERORDESC
            APPEND    CFNAME,ERORDESC
            APPEND    RBRACK,ERORDESC
            RESET     ERORDESC
            CALL      PERR0000                   * print error line
            MOVE      TILDA20,SAVPRFIX
            GOTO      VCOD9200
          ENDIF
          MOVE      FILEPRFX,SAVPRFIX            * save O/P file prefix
.
.         Open the clinic master as well and get the default clinic type
.
          CLOSE     OUTMA1A1                     * close current file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,FILEPRFX,FILMA1A1
          OPEN      OUTMA1A1,CFNAME              * open new file
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Clinic Master file not found (",ERORDESC
            ENDSET    ERORDESC
            APPEND    CFNAME,ERORDESC
            APPEND    RBRACK,ERORDESC
            RESET     ERORDESC
            CALL      PERR0000                   * print error line
            MOVE      TILDA20,SAVPRFIX
            GOTO      VCOD9200
          ENDIF
          GOTO      VCOD9999
.
.         Validate a clinic id field (outcliaf)
.
VCOD4000  MOVE      TEMPCODE,DIM6
          PACK      KEY20,OTARPRSI,DIM6,TILDA20
          CALL      RDCLIA1                      * valid code ?
          IF        OVRCD = 1
            CALL      RDPCLIA1                   * no - read previous record
            BRANCH    OVRCD,VCOD9100             * error
.
            MATCH     OCASITE,OTARPRSI           * same site still ?
            GOTO      VCOD9100 IF NOT EQUAL      * no - error
.
            MATCH     OCACLIN,TEMPCODE           * same clinic id still ?
            GOTO      VCOD9100 IF NOT EQUAL      * no - error
          ENDIF
          GOTO      VCOD9999
.
.         Validate a Y/N field
.
VCOD5000  MATCH     ANSY,TEMPCODE                * "Y" ?
          GOTO      VCOD9999 IF EQUAL            * yes - valid code
.
          MATCH     ANSN,TEMPCODE                * "N" ?
          GOTO      VCOD9999 IF EQUAL            * yes - valid code
          GOTO      VCOD9100                     * no - invalid code
.
VCOD9100  MOVE      "Invalid coded field ",ERORDESC
          ENDSET    ERORDESC
          APPEND    LBRACK,ERORDESC
          APPEND    TMPFIELD,ERORDESC
          APPEND    RBRACK,ERORDESC
          APPEND    SP70,ERORDESC
          RESET     ERORDESC
          CALL      PERR0000                     * print error
.
VCOD9200  MOVE      ONE,ERORFLAG                 * set code error flag
.
VCOD9999  RETURN
+
.*****************************************************************************
.*                              PERR0000           Called by: UPLD0000       *
.*                   Print an error detail line               CHKM0000       *
.* Requires: Valid read on PMI records (for name fields)      DERR0000       *
.*           ERORDESC - error description                     TERR0000       *
.*           CLNO - current page line count                   VURN0000       *
.* Returns:  CLNO - updated page line count                   NERR0000       *
.*                                                            VCOD0000       *
.*****************************************************************************
.
PERR0000  COMPARE   CLNO,SIXTY3                  * page full ?
          IF        @LESS
            CALL      LINE0000                   * yes - print bottom line
            CALL      HEAD0000                   * print header
          ENDIF
.
          MOVE      PSNAME,PACSNAME              * format patient name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM50
.
          PRINT     *1,PIPE,*3,ACTNURNO,*12,PIPE,*14,DIM50:
                    *65,PIPE,*67,ACTNAVIS,*88,PIPE,*90,ERORDESC,*132,PIPE
          ADD       ONE,CLNO                     * increment page line count
.
PERR9999  RETURN
+
.*****************************************************************************
.*                              VURN0000           Called by: UPLD0000       *
.*                        Validate U/R Number                                *
.* Requires: ACTNURNO - U/R number (right justified)                         *
.* Returns:  Valid read on PMI data                                          *
.*           EXIT  0 = PMI record found - ok to continue                     *
.*                 1 = PMI record not found - ignore record                  *
.*          UNKCOUNT - updated count of unknown U/R numbers                  *
.*          OTARURNO - U/R number right justified                            *
.*****************************************************************************
.
VURN0000  MATCH     SP8,ACTNURNO                 * blank U/R ?
          IF        @EQUAL
            MOVE      "U/R missing ",ERORDESC    * yes
            GOTO      VURN9150
          ENDIF
.
          MOVE      ACTNURNO,KEY8
          CALL      RDMAST1                      * U/R on file ?
          BRANCH    OVRCD,VURN9100               * no - error
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21                     * U/R on file ?
          BRANCH    OVRCD,VURN9100               * no - error
.
          COMPARE   ONE,PSTAT                    * merged U/R ?
          GOTO      VURN1000 IF EQUAL            * yes
.         
          COMPARE   ONE,PCEASE
          IF        @EQUAL
            MOVE      "Patient deceased ",ERORDESC
            GOTO      VURN9150
          ENDIF
          GOTO      VURN9000
.
.         We have a merged U/R record, so get the new U/R
.
VURN1000  MATCH     PPSNAME,SP20                 * blank previous name field ?
          GOTO      VURN9100 IF EQUAL            * yes - error
.
          MOVE      PPSNAME,ACTNURNO             * no - load new U/R
          GOTO      VURN0000                     * get new pmi record
.
VURN9000  MOVE      ACTNURNO,OTARURNO            * load U/R number
          MOVE      ZERO,EXIT
          GOTO      VURN9999
.
VURN9100  MOVE      "PMI Record not found ",ERORDESC
VURN9150  CALL      PERR0000                     * print error line
          ADD       ONE,UNKCOUNT                 * increment unknown U/R count
          MOVE      ONE,EXIT
.
VURN9999  RETURN
+
.*****************************************************************************
.*                              CVIS0000           Called by: UPLD0000       *
.*        Check if an alternate visit number record exists on ibaalvaf.      *
.* Requires: ACTNAVIS - Alternate Visit Number (left justified)              *
.* Returns:  ERORFLAG  1 = Alternate visit number not found                  *
.*           ACTNREFN - webPAS Linked A/H Referral Number                    *
.*****************************************************************************
.
CVIS0000  PACK      KEY28,ACTNAVIS,SP30
          CALL      RSIBALV2                     * position on alt. visit no.
CVIS0500  CALL      RKIBALV2                     * read next record
          BRANCH    OVRCD,CVIS9100               * eof
.
          MATCH     ACTNAVIS,IBAVAVIS            * same alt. visit no. ?
          GOTO      CVIS9100 IF NOT EQUAL        * no
.
          MATCH     " 1",IBAVTYPE                * eReferral record type ?
          GOTO      CVIS0500 IF EQUAL            * yes - ignore record
.
.         We have found a matching "legacy" type record
.
          MOVE      IBAVVISN,ACTNREFN            * save A/H referral number
          GOTO      CVIS9999
.
CVIS9100  MOVE      "Alternate visit id not found in ibaalvaf",ERORDESC
          CALL      PERR0000                     * print error line
          MOVE      ONE,ERORFLAG                 * set error flag
          GOTO      CVIS9999
.
CVIS9999  RETURN
+
.*****************************************************************************
.*                               GTYP0000          Called by: VCOD0000       *
.* Returns: OTALCLTY - clinic type                                           *
.*****************************************************************************
.
GTYP0000  MATCH     SP6,ACTNCLID                 * clinic id 
          GOTO      GTYP9999 IF EQUAL            * yes - no default available
.
          MATCH     SP8,ACTNPRDT                 * preferred date blank ?
          GOTO      GTYP5000 IF EQUAL            * yes
.
          UNPACK    ACTNPRDT,CCENT,CYEAR,CMON,CDAY  * no
          CALL      DAYOFWEK                     * get day of week
.
          PACK      KEY18,ACTNPRSI,ACTNCLID,WEEKDAY,SP20
          CALL      RDSMASA1                     * position in file
GTYP0500  CALL      RDKMASA1                     * read next record
          BRANCH    OVRCD,GTYP5000               * eof - no matching master rec.
.
          MATCH     ACTNPRSI,OMASITE             * same site still ?
          GOTO      GTYP5000 IF NOT EQUAL        * no
.
          MATCH     ACTNCLID,OMACLIN             * same clinic id still ?
          GOTO      GTYP5000 IF NOT EQUAL        * no
.
          COMPARE   WEEKDAY,OMADAY               * same day ?
          GOTO      GTYP5000 IF NOT EQUAL        * no
.
          MATCH     ACTNPRHS,OTMAHOSP            * same hospital ?
          GOTO      GTYP0500 IF NOT EQUAL        * no
.
          GOTO      GTYP9000                     * yes
.
.         There is no preferred date, so try for any day
.
GTYP5000  PACK      KEY18,ACTNPRSI,ACTNCLID,SP20
          CALL      RDSMASA1                     * position in file
GTYP5500  CALL      RDKMASA1                     * read next record
          BRANCH    OVRCD,GTYP9999               * eof - no matching master rec.
.
          MATCH     ACTNPRSI,OMASITE             * same site still ?
          GOTO      GTYP9999 IF NOT EQUAL        * no
.
          MATCH     ACTNCLID,OMACLIN             * same clinic id still ?
          GOTO      GTYP9999 IF NOT EQUAL        * no
.
          MATCH     ACTNPRHS,OTMAHOSP            * same hospital ?
          GOTO      GTYP5500 IF NOT EQUAL        * no
.
GTYP9000  MOVE      OMATYP,OTARCLTY              * yes - load clinic type
.
GTYP9999  RETURN
+
.*****************************************************************************
.*                              PCOM0000           Called by: UPLD0000       *
.*                Process any action related comments                        *
.* Requires: ACTNCOMM - action related comments (stripped of trailing blanks)*
.*           COMMTLEN - length of comment field                              *
.*****************************************************************************
.
PCOM0000  COMPARE   ZERO,COMMTLEN                * any comments ?
          GOTO      PCOM9999 IF EQUAL            * no - finished
.
          MOVE      ZERO,LINENUMB                * yes - initialise line number
.
PCOM0500  MOVE      ACTNCOMM,DIM60
          PACK      DIM60,DIM60,SP70             * load comment line
.
          MOVE      OTARCMID,OTRCUNIQ
          ADD       ONE,LINENUMB                 * increment line number
          MOVE      LINENUMB,OTRCLINE
          MOVE      DIM60,OTRCCOMM
          MOVE      CTIMEIS,DIM8
          REP       " 0",DIM8
          PACK      OTRCCUID,PRGID,SP10
          MOVE      CURRDATE,OTRCCDAT
          MOVE      DIM8,OTRCCTIM
          MOVE      SP10,OTRCUUID
          MOVE      SP8,OTRCUDAT
          MOVE      SP8,OTRCUTIM
          MOVE      SP70,OTRCSPAR
.
          PACK      KEY13,OTRCUNIQ,OTRCLINE
          CALL      RAOTRCM1                     * record on file already ?
          IF        OVRCD = 1 & VCHKFLAG = 0
            CALL      WROTRCM1                   * no - write new record
          ELSE
            IF        OVRCD = 0
              MOVE      "Error writing comment records ",ERORDESC
              CALL      PERR0000                 * print error line
              ADD       ONE,COMCOUNT             * increment comment error count
              GOTO      PCOM9999
            ENDIF
          ENDIF
.
          COMPARE   COMMTLEN,SIXTY               * any comments remaining ?
          GOTO      PCOM9999 IF NOT LESS         * no - finished
.
.         There is one more line of comments to load
.
          BUMP      ACTNCOMM,60                  * move forward 60 characters
          SUB       SIXTY,COMMTLEN               * reduce string length by 60
          GOTO      PCOM0500                     * process next line
.
PCOM9999  RETURN
+
.*****************************************************************************
.*                             GUNQ0000            Called by: UPLD0000       *
.*               Get the next unique comment number                          *
.* Returns: OTARCMID - Unique comment id                                     *
.*****************************************************************************
.
GUNQ0000  MOVE      "121",PRXCODE           * System Lock Sector 121
          CALL      GETSLK00                * Get System Lock of Sector 121
.
          READ      CONTROLF,HUND21;*112,ALCNNUCI
          MOVE      ALCNNUCI,FORM10
          ADD       ONE,FORM10
          MOVE      FORM10,ALCNNUCI
          WRITAB    CONTROLF,HUND21;*112,ALCNNUCI
.
          CALL      RELSLK00                * Release System Lock of Sector 121
          SUB       ONE,FORM10
          MOVE      FORM10,ALCNNUCI         * Next unique comment id
.
          MOVE      ALCNNUCI,OTARCMID
.
GUNQ9999  RETURN
+
.*****************************************************************************
.*        Temp File I/O Routines                                             *
.*****************************************************************************
.
.         Hospital (pathspaf)
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      TEMP1,KEY3;OLDHOSPL,NEWHOSPL
          GOTO      OVERCOND IF OVER
          RETURN
.
.         Category-Codes (patcodes)
.
RDTEMP2   MOVE      ZERO,OVRCD
          RESET     KEY5
          READ      TEMP2,KEY5;OLDCTCAT,OLDCTCOD,NEWCTCAT,NEWCTCOD
          GOTO      OVERCOND IF OVER
          RETURN
.
.         O/P Clinic Id (outcliaf)
.
RDTEMP3   MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      TEMP3,KEY12;OLDSITCD,OLDCLIID,NEWSITCD,NEWCLIID
          GOTO      OVERCOND IF OVER
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       CLPATMAS
          INC       DAYOFWEK
.
          INC       IBAALVIO/INC
          INC       OUTARTIO/INC
          INC       OUTCLIIO/INC
          INC       OUTMA1IO/INC
          INC       OUTRCMIO/INC
          INC       OUTSITIO/INC
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC

. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBABIL85                         *
. *                                                        *
. *     FUNCTION:         DOCTORS REACCREDITATION REPORT   *
. *                                                        *
. *     DATE WRITTEN:     07/01/87                         *
. *                                                        *
. *     AUTHOR:           MALCOLM HAYSE (I.B.A)            *
. *                                                        *
. *     MODIFICATIONS:                                     *
. *                 V5.09.B01 04/01/2001 Ebon Clements     *
. *                           509 mods for inactive doctors*
. *                 V5.07.B02 27/01/1999  N Harrington     *
. *                           507 rebounds                 *
. *                 V5.07.B01 13/11/1998 Jim Stathopoulos  *
. *                             507 Changes                *
. **********************************************************
. *                       W2.01 22/02/86 M.HAYSE I.B.A     *
. *                       ACCRED TYPE IS NOW PRINTED       *
. *                                                        *
. *                       W2.02 10/07/87 J.TAN  (I.B.A.)   *
. *                       MODS TO HAVE A QUESTION MARK     *
. *                       FACILITY FOR DOCTOR TYPE         *
. *                                                        *
. *                       V5.01 26/05/89 J.Tan             *
. *                       Fixed the heading of report      *
. *                       SRF 100585                       *
. *                                                        *
. *                       V5.02 01/07/89 J.Tan             *
. *                       Mods to print INACTIVE. SRF101321*
. *                    V5.01.01 06/07/89 M.Ohleiter        *
. *                       Changes for 8 char ur and admiss *
. *                       no, standards - release 5.01     *
. *                                                        *
. *       V5.01.02  04.03.93 Neeriem "I Hate Support" Dye  *
. *                 Modify to use standard routine KEYDATE *
. *       V5.01.03  08.03.92 Neeriem "I Hate Support" Dye  *
. *                 Modify to print address nicely         *
. *       V5.01.04  16.03.92 Neeriem "I Hate Support" Dye  *
. *                 Modify to print address even nicer     *
. *       V5.01.05  03/06/1994  Matt Surridge              *
. *                 Called DISPHEAD before opening Files.  *
. **********************************************************
.
          INC      STD001FD
.
          INC       PATDO1FD/INC
          INC       PATCODFD/INC
.
.                 WORK AREA
.
DIM3       DIM     3
.
LNO        FORM    2
PAGENO     FORM    3
.
OPTION1    FORM     1
OPTION2    DIM      1           * Include inactive doctors in report
OPTDESC    DIM      3 
.
.
SP12       INIT    "            "
.
CODE       DIM     2
CODEAT     INIT    "AT"
CODEDT     INIT    "DT"
.
QUES       INIT    "?"
.
.      EXTRA VARIABLES
.
BIRDATE    DIM     10
FIRDATE    DIM     10
LSTDATE    DIM     10
.
SDRTYPE    DIM     3
.
AYEARS     FORM     2
TODATE     DIM      10
.
XDAY1            DIM     2
XMON1            DIM     2
XYEAR1           DIM     2
XCENT1           DIM     2
.
XDAY2            DIM     2
XMON2            DIM     2
XYEAR2           DIM     2
XCENT2           DIM     2
.
DALPHA     DIM     6
DNAME      DIM     22
.
DDRTYPE    DIM     20
DDRTYPE1   DIM     20
DDRTYPE2   DIM     20
DDRTYPE3   DIM     20
DDRTYPE4   DIM     20
DDRTYPE5   DIM     20
DOCSTAT    DIM     8
INACTIVE   INIT    "INACTIVE"
INACT      FORM    1
.
DDATYPE1   DIM     20
.
STDOCT     DIM     2
CDESC      DIM     20
.
VERSION   INIT      "V12.01.00"
PRGID     INIT      "IBABIL85"
PRGNAM    INIT      "Doctors Reaccreditation Report"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A2,"patdo1af"
.
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
OPFIL     DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
+
.        START OF PROGRAM
.             SELECT OPTION
.
START   MOVE       ZERO,PAGENO
.
        DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = All Doctors":
                  *P1:6,*V2LON,TWO,*HOFF," = By Doctor Type":
                  *P1:12,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:13,*V2LON,ONE,*HOFF," = Code Sequence":
                  *P1:14,*V2LON,TWO,*HOFF," = Surname Sequence"
.
OPT1
        KEYIN     *P1:8,"Option : ",*EL,*V2LON,OPTION1
.
        COMPARE   ZERO TO OPTION1
        GOTO      ENDIT IF EQUAL
        BRANCH    OPTION1 OF CLR12,DOTYPE
        BEEP
        GOTO      OPT1
CLR12   DISPLAY   *P1:10,*EL
        GOTO      OPT2A
+
.             INPUT TYPE OF DOCTOR 
.
DOTYPE  MOVE            SP3,SDRTYPE
        DISPLAY         *P1:10,*EL,"Doctor Type :"
        KEYIN           *P15:10,*EL,*V2LON,SDRTYPE
        RESET           SDRTYPE
        GOTO            OPT1 IF EOS
.
        MATCH           QUES,SDRTYPE
        GOTO            QUESOPT IF EQUAL
.
        PACK            KEY5 WITH CODEDT,SDRTYPE
        CALL            RDCODE1
        BRANCH          OVRCD OF KDERR
        MOVE            TDESC,DDRTYPE
        DISPLAY         *P20:10,*EL,*V2LON,DDRTYPE
        GOTO            OPT2A
KDERR   BEEP
        DISPLAY         *P20:10,*EL,"***  Invalid Doctor Type ***",*W2;
        GOTO            DOTYPE
.
QUESOPT MOVE            CODEDT,CODE
        CALL            PATCODDS
        BRANCH          OVRCD OF KDERR
        MOVE            SP3,DRTYPE
        CALL            REDISP
        GOTO            DOTYPE
.
+
.
OPT2A   KEYIN     *P1:16,"Option : ",*EF,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      OPT1 IF EQUAL
.
        BRANCH    OPTION OF KYIN0000,KYIN0000
        BEEP
        GOTO      OPT2A
.
NODOCT  DISPLAY   *P1:24,*EL,*P20:24,*B,*V2LON:
                  "** No Doctors on File **",*W2;
        GOTO      START
.
INVYR1  DISPLAY   *P40:20,*B,*V2LON:
                  "** Invalid number of Years **",*W2;
.
KYIN0000  KEYIN     *P1:18,*EL,"Include Inactive Doctors (":
                         *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") : ":
                         *V2LON,OPTION2
.  
          MATCH     ANSY,OPTION2
          GOTO      KYIN9000 IF EQUAL
.
          MATCH     ANSN,OPTION2
          GOTO      KYIN8000 IF EQUAL
.
          BEEP
          GOTO      KYIN0000
.
KYIN8000  MOVE      ZERO,INACT
          PACK      OPTDESC,DNO,SP1
          GOTO      KYEARS   
.
KYIN9000  MOVE      ONE,INACT
          PACK      OPTDESC,DYES 
.
KYEARS  DISPLAY   *P1:20,*EF,"Years Accreditation  :":
                      *P1:21,"Prior to Last Accd   :"
.
        KEYIN     *P24:20,*EL,*V2LON,AYEARS
.
        COMPARE   ZERO TO AYEARS
        GOTO      OPT2A IF EQUAL
        GOTO      INVYR1 IF LESS
+
.
.             KEYIN TO DATE
.
KADAT1  
....
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
.
          MOVE      "24",CCOL          * set up collumn
          MOVE      "21",CVERT         * set up row
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
        COMPARE   ZERO TO IDAY
        GOTO      KYEARS IF EQUAL
.
....
.
ENDAD1  PACK      TODATE WITH ICENT,IYEAR,IMON,IDAY
        REP       " 0",TODATE
        UNPACK    TODATE INTO XCENT1,XYEAR1,XMON1,XDAY1
.
.        CHECK IF DATES ARE OK OR NOT
.
REKEY   KEYIN     *P1:24,*EL," Date ok (":
                  *V2LON,"Y",*HOFF,"/":
                  *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
        MATCH     "N" TO ANS
        GOTO      KADAT1 IF EQUAL
.
        MATCH     "Y" TO ANS
        GOTO      CNTREP IF EQUAL
.
        BEEP
        GOTO     REKEY
+
.
CNTREP  BRANCH    OPTION OF KDOCT,ALPHANO
.
+
.               PRINT VIA DOCTS NUMBER
.
KDOCT   MOVE       SP6,KEY6
        CALL       RDSDOCT1
        MOVE       "60",LNO
.
        DISPLAY    *P1:24,*EL,*P20:24,"Doctor Code :":
                   *P60:24,"Scanning :";
.
NEXTA   CALL       RDKDOCT1
        BRANCH     OVRCD OF ENDP
.
        MOVE        DCODE,KEY6
        CALL        RDDOCT1
.
        DISPLAY     *P72:24,DCODE;
.
        COMPARE     AYEARS,DAYEARS
        GOTO        NEXTA IF NOT EQUAL
.
        MOVE        DALAST,XDATE
        REP         " 0",XDATE
.
        MATCH       XDATE,TODATE
        GOTO        NEXTA IF LESS
.
        IF          INACT=0 & DRSTAT = 1
          GOTO      NEXTA
        ENDIF
.
        BRANCH      OPTION1 OF DISPA
.
        MATCH       DRTYPE,SDRTYPE
        GOTO        NEXTA IF NOT EQUAL
.
DISPA
        CALL        DISPDA
.
        DISPLAY     *P35:24,*V2LON,DCODE;
        GOTO        NEXTA
+
.
.               PRINT VIA SURNAME       
.
ALPHANO MOVE        SP12,KEY12
        CALL        RDSDOCT2
        MOVE        "60",LNO
.
        DISPLAY    *P1:24,*EL,"Surname : ":
                    *P35:24,"Doctor Code : ",*P60:24,"Scanning :";
.
NEXTALP CALL        RDKDOCT2
        BRANCH      OVRCD OF ENDP
.
        MOVE        DCODE,KEY6
        CALL        RDDOCT1
.
        DISPLAY     *P72:24,DCODE;
.
        COMPARE     AYEARS,DAYEARS
        GOTO        NEXTALP IF NOT EQUAL
.
        MOVE        DALAST,XDATE 
        REP         " 0",XDATE
.
        MATCH       XDATE,TODATE
        GOTO        NEXTALP IF LESS
.
        IF          INACT = 0 & DRSTAT = 1
          GOTO        NEXTALP
        ENDIF
.
        BRANCH      OPTION1 OF DISPALP
.
        MATCH       DRTYPE,SDRTYPE
        GOTO        NEXTALP IF NOT EQUAL
.
DISPALP DISPLAY     *P10:24,*V2LON,DSNAME,*P50:24,DCODE;
        CALL        DISPDA
        GOTO        NEXTALP
+
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     COMPARE      ZERO,PAGENO
         GOTO         NODOCT IF EQUAL
         PRINT        *N,"  ***  END OF REPORT  ***"
         DISPLAY      *P1:24,*EL,*V2LON,*P20:24:
                      "** Report Generation Completed **",*W2;
         GOTO         START
+
.
.     PRINT ACTUAL DATA 
.
DISPDA  COMPARE   "53",LNO
        CALL      HEAD IF NOT LESS
.
        MOVE        DGNAME,ANS
        MOVE        DTITL,DIM3
        PACK        DNAME WITH DIM3,SP1,ANS,DOT,DSNAME
.
        MOVE         SP8,BIRDATE
        MOVE         SP8,FIRDATE
        MOVE         SP8,LSTDATE
.
        MATCH        SP8,DBIRTH
        GOTO         NOBDAT IF EQUAL
        UNPACK       DBIRTH INTO XCENT1,XYEAR1,XMON1,XDAY1
        PACK         BIRDATE WITH XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1
.
NOBDAT  MATCH        SP8,DAFIRST
        GOTO         NOFDAT IF EQUAL
        UNPACK       DAFIRST INTO XCENT1,XYEAR1,XMON1,XDAY1
        PACK         FIRDATE WITH XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1
.
NOFDAT  MATCH        SP8,DALAST
        GOTO         NOLDAT IF EQUAL
        UNPACK       DALAST INTO XCENT1,XYEAR1,XMON1,XDAY1
        PACK         LSTDATE WITH XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1
.
NOLDAT  MOVE      SP20,DDRTYPE1
        MATCH     SP3,DRTYPE
        GOTO      NODTYP1 IF EQUAL
        PACK      KEY5 WITH CODEDT,DRTYPE
        CALL      RDCODE1
        BRANCH    OVRCD OF NODTYP1
        MOVE      TDESC,DDRTYPE1
.
NODTYP1 MOVE      SP20,DDRTYPE2
        MATCH     SP3,DRTYPE2
        GOTO      NODTYP2 IF EQUAL
        PACK      KEY5 WITH CODEDT,DRTYPE2
        CALL      RDCODE1
        BRANCH    OVRCD OF NODTYP2
        MOVE      TDESC,DDRTYPE2
.
NODTYP2 MOVE      SP20,DDRTYPE3
        MATCH     SP3,DRTYPE3
        GOTO      NODTYP3 IF EQUAL
        PACK      KEY5 WITH CODEDT,DRTYPE3
        CALL      RDCODE1
        BRANCH    OVRCD OF NODTYP3
        MOVE      TDESC,DDRTYPE3
.
NODTYP3 MOVE      SP20,DDRTYPE4
        MATCH     SP3,DRTYPE4
        GOTO      NODTYP4 IF EQUAL
        PACK      KEY5 WITH CODEDT,DRTYPE4
        CALL      RDCODE1
        BRANCH    OVRCD OF NODTYP4
        MOVE      TDESC,DDRTYPE4
.
NODTYP4 MOVE      SP20,DDRTYPE5
        MATCH     SP3,DRTYPE5
        GOTO      NODTYP5 IF EQUAL
        PACK      KEY5 WITH CODEDT,DRTYPE5
        CALL      RDCODE1
        BRANCH    OVRCD OF NODTYP5
        MOVE      TDESC,DDRTYPE5
.
NODTYP5 MOVE      SP20,DDATYPE1
        MATCH     SP3,DACCRED
        GOTO      NODTYP6 IF EQUAL
        PACK      KEY5 WITH CODEAT,DACCRED
        CALL      RDCODE1
        BRANCH    OVRCD OF NODTYP6
        MOVE      TDESC,DDATYPE1
.
NODTYP6 MOVE      SP10,DOCSTAT
        LOAD      DOCSTAT USING DRSTAT OF INACTIVE
.
        PRINT       *1,"! ",DCODE:
                    *10,"! ",DNAME,*35,"!",BIRDATE:
                    *46,"! ",DSADD1:
                    *93,"! ",DDRTYPE2,*116,"!",LSTDATE,*132,"!":
                    *N,"!",*10,"! (",DDRTYPE1,")",*35,"!",*46,"! ",DSADD2:
                    *93,"! ",DDRTYPE3,*116,"!",*132,"!":
                    *N,"!",*10,"! (",DDATYPE1,")":
                    *35,"!",*46,"! ",DSADD3:
                    *93,"! ",DDRTYPE4,*116,"! ",DOCSTAT,*132,"!":
                    *N,"!",*10,"!",*35,"!",*46,"! ",DSADD4,SP1,DSPOST:
                    *93,"! ",DDRTYPE5,*116,"!",*132,"!":
                    *N,"!",*10,"!",*35,"!",*46,"! Phone : ",DTELES:
                    *93,"! ",*116,"!",*132,"!"
        ADD         "5",LNO
        CALL      PAGEND
.
DISPEND   RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,LNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE TO PAGENO
          CALL      IBACLOCK
          CALL      HEAD132
          PRINT     *N,*N,*44;
.
          BRANCH    OPTION OF PRNTC,PRNTA
.
PRNTA     PRINT     "SURNAME-SEQUENCE ";
          GOTO      PRTCONT1
.
PRNTC     PRINT     "CODE-SEQUENCE ";
.
PRTCONT1  BRANCH    OPTION1 OF PRNT1,PRNT2
.
PRNT1     PRINT     "  ALL DOCTORS"
          GOTO      PRTCONT2
.
PRNT2     PRINT     "  DOCTOR TYPE : ",DDRTYPE
.
PRTCONT2  UNPACK    TODATE INTO XCENT1,XYEAR1,XMON1,XDAY1
          PRINT     *N,*44,"Number of Years Accreditation : ",AYEARS:
                    *N,*44,"Prior to a last Accreditation : ":
                    XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:   
                    *N,*44,"Include Inactive Doctors : ",OPTDESC,*N
.
          CALL     PAGEND
.
          PRINT    *1,"! Doctor",*10,"! Name / Doctor Type /":
                   *35,"!  D.O.B",*46,"! Surgery Address":
                   *93,"! Additional":
                   *116,"! Date of Last",*132,"!":
                   *N,"!  Code",*10,"! Accreditation Type",*35,"!",*46,"!":
                   *93,"! Doctor Types",*116,"! Accreditation",*132,"!"
.
          CALL     PAGEND
.
          MOVE      "8",LNO
          MOVE      " ",ANS
          RETURN
+
.
REDISP
        DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = All Doctors":
                  *P1:6,*V2LON,TWO,*HOFF," = By Doctor Type":
                  *P1:12,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:13,*V2LON,ONE,*HOFF," = Code Sequence":
                  *P1:14,*V2LON,TWO,*HOFF," = Surname Sequence"
.
        DISPLAY   *P1:8,"Option : ",*EL,*V2LON,OPTION1
.
        RETURN
.
          INC       PATCODKY
          INC       PATDO1IO/INC
          INC       PATCODIO/INC
          INCLUDE   STD001IO        
.
ENDIT    CHAIN      PGM
         STOP

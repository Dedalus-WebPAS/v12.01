.***************************************************************************
.* System    :   Check long patient surname                                *
.* Program   :   FXPTMASN                                                  *
.* Desc      :   Check and fix the long patient surname                    *
.***************************************************************************
.* Date      :   15/06/2017                                                *
.* Author    :   Jill Parkinson                                            *
.* Function  :   Find all patma1af records with blank ptmasnam and copy    *
.*           :   the existing psname into it                               *
.* Modifications  :                                                        *
.*        V10.10.00 15/06/2017  Jill Parkinson TSK 0840516                 *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECCOUNT  FORM      8
UND80     INIT      "------------------------------------------------------------------------------"
.
PRGID     INIT      "FXPTMASN"
PRGNAM    INIT      "Fix blank patma1af.ptmasnam "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000             * process master table
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run and print records":
                    *P1:6,*V2LON,TWO,*HOFF,". Run and Update records";
.
OPTN0500  KEYIN     *P1:10,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:10,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  MOVE      ZERO,RECCOUNT          * Number of records updated
.
          CALL      HEAD80
          PRINT     UND80
          PRINT     *1,"|UR Number | Surname ",*60,"|Date changed",*80,"|"
          PACK      KEY8,SP70
          CALL      RDSMAST1
PROC1000  CALL      RDKMAST1               * Loop master  file
          BRANCH    OVRCD,PROC9000
.
          MATCH     SP70,PTMASNAM
          GOTO      PROC1000 IF NOT EQUAL
.
          PRINT     *1,"| ",PURNO,"| ",PSNAME,*60,"| ",PCHGDTE,*80,"|"
          PACK      PTMASNAM,PSNAME
          ADD       ONE,RECCOUNT           * Number of records updated
.
          IF        COPTION=2              
            CALL      UPMAST1              * Option 2 - Report and Update
          ENDIF
          GOTO      PROC1000
.
PROC9000  IF        COPTION=2
            DISPLAY   *P1:24,*EL,"Total Records Updated : ":
                      *V2LON,RECCOUNT,*HOFF,SP5;
          ELSE
            DISPLAY   *P1:24,*EL,"Total Records with blank long surname : ":
                      *V2LON,RECCOUNT,*HOFF,SP5;
          ENDIF
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATMA1IO/INC

.******************************************************************************
.*  System        :  PRS2                                                     *
.*  Program       :  IBAINP49                                                 *
.*  Description   :  AIMS Form Generation                                     *
.******************************************************************************
.*  Date          :  13/07/1993                                               *
.*  Author        :  Gabrielle Cameron                                        *
.*  Function      :  To count the number of seperations and bed days (include *
.*                   same day patients) depending on care class, admission    *
.*                   type and claim type.                                     *
.*  Modifications :                                                           *
.******************************************************************************
.* V12.00.01 16/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved TFILENAM to INIT0000                                      *
.* V10.03.01  28/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.09.01  22/11/2007  Peter Vela   CAR 132264                       *
.*                  Recompiled for PATCHCFD                                   *
.*        V9.03.03  15/07/2004  Mike Laming  CAR 51565                        *
.*                  Correct Care Type at HCSE0000                             *
.*        V9.03.02  18/06/2004  Mike Laming  CAR 49016  2004 PRS/2 changes    *
.*                  Change CARETYPE to 2 chars for Mental Health sub-codes    *
.*        V9.03.01  16/05/2003  Henry Son   CAR39223                          *
.*                  Column B Adjusted Previous Month is Zero.                 *
.******************************************************************************
.*        V9.02.01  04/09/2002 Jill Habasque SRF 17573                        *
.*                  Changed to use RTIODAYS                                   *
.*        V5.09.01  16/05/2002 Tonii  July 2002/2003 HDP mods                 *
.*                  - Modified to generate report on a monthly basis after    *
.*                    01/07/2002                                              *
.*        V5.08.03  10.10.2000 Sandra Barcham                                 *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.02  23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  13/07/2000  J Habasque SRF 3411                           *
.*                  Added check of TCINDC3 - to not include boarders          *
.*        V5.08.B02 09/02/2000  J Habasque SRF 647385                         *
.*                  Changed from only picking up acute (caretype 4) to pick   *
.*                  up all care types except 8,9 or 5                         *
.*        V5.08.B01 07/01/2000  J Habasque                                    *
.*                  Fixed caretype                                            *
.*        V5.07.07  08/12/1999  J Habasque                                    *
.*                  Added check for caretype of 4 in PRMO0000                 *
.*        V5.07.06  28/10/1999  J.Tan                                         *
.*                  Changed to public acute separation to check on last episod*
.*        V5.07.05  05/10/1999  Greg Horvat      SRF 634664 (Rebound)         *
.*                  Changed to count unqual patients in the unqual section    *
.*        V5.07.04  28/09/1999  Greg Horvat      SRF 634664                   *
.*                  Changed to only count acute patients                      *
.*        V5.07.03  24/09/1999  Jill Habasque                                 *
.*                  Removed unqualified separations                           *
.*        V5.07.02  27/08/1999  Jill Habasque                                 *
.*                  Recompiled for PRS2CLAS                                   *
.*        V5.07.01  21/07/1999  Jill Habasque                                 *
.*                  Changed to include Unqualified Newborns                   *
.*        V5.07.B01 18/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.******************************************************************************
.*        V5.03.09  07/01/1997  Greg Horvat      SRF 617782                   *
.*                  Changed to include a bed day for a daycase unqualified    *
.*                  newborn                                                   *
.*        V5.03.08  19/08/1996  Greg Horvat      SRF 616592                   *
.*                  Changed the public hospital format to a new format        *
.*        V5.03.07  06/03/96 J.Tan                                            *
.*                  Fixed number of statistic separations (balance with ADT06)*
.*        V5.03.06  12/10/1995  Paul Howells     SRF 611581                   *
.*                  Fixed more bugs, so it now balances with PMS16.           *
.*        V5.03.05  20/09/1995  Paul Howells     SRF 611581                   *
.*                  Removed YTD columns for 95.                               *
.*        V5.03.04  05/09/1995  Paul Howells     SRF 611581                   *
.*                  Fixed calculation of bed days when discharged             *
.*                  on last day of period.                                    *
.*                  Count the last separation properly.                       *
.*                  Count episode only once when adm type changes.            *
.*        V5.03.03  29/08/1995  Paul Howells     SRF 611264                   *
.*                  Fixed calculation of bed days when change of              *
.*                  Care class on last day of period.                         *
.*        V5.03.02  22/08/1995  Paul Howells     PRS/2 95                     *
.*                  Change 'Bed Days' to Patient Days                         *
.*        V5.03.01  15/08/1995  Paul Howells     PRS/2 95                     *
.*                  Don't Include Unqualified babies                          *
.*                                                                            *
.*                 V5.01.01  14/01/94  Matt Surridge                          *
.*                           Fixed Bugs From Global Recompile                 *
.*                 V5.01.02  07/02/94  Whirlpool                              *
.*                           Changed name of PRS2CLAS.INC to PRS2CLDF.INC     *
.*                 V5.01.03  25/05/1994 Ian Rutt                              *
.*                           Global Rcompile                                  *
.*                 V5.01.04  27/05/1994 Ian Rutt                              *
.*                           Fixed Global Recompile Bugs                      *
.*                                                                            *
.******************************************************************************
.
.         FILE DESCRIPTION INCLUDES
.
          INC       STDGENDF
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATACCFD/INC             * Account class file
          INC       PATASFFD/INC
          INC       PATCODFD/INC             * Patient Codes file
          INC       PATCONFD/INC             * Patient Control file
          INC       PATCHCFD/INC             * Codes changes file
          INC       PATDSCFD/INC             * Discharge file
          INC       PATMI1FD/INC             * Admissions file
          INC       PMSVX1FD/INC             * Admissions file
          INC       PATMA1FD/INC             * Admissions file
          INC       PATTRNFD/INC             * Transfer file
          INC       PRS2CLDF/INC
          INC       PRSADFFD/INC
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
PRGID     INIT      "IBAINP49"
PRGNAM    INIT      "Aims Form Generation"
VERSION   INIT      "V12.01.00"
.
.         VARIABLES
.
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSDAYS  FORM      6
CDYSYEAR  FORM      2
.
ADMTYPE   DIM       3
CARETYPE  DIM       2                                         * was 1  *C-49016
CARECLSS  DIM       3
ENDDAY    DIM       3
EPSSDATE  DIM       8
EPSEDATE  DIM       8
HCSFLAG   FORM      1
PREVTYPE  DIM       2                                         * was 1  *C-49016
PREVCLSS  DIM       3
SAVKEY24  DIM       24
SKEY30    DIM       30
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
SAVKEY30  DIM       30
S1NUM     DIM       11
S1PRE     INIT      "(306.S1."
RIGHTB    INIT      ")"
BJDAY     FORM      5
CJDAY     FORM      5
ACMSAME   FORM      6                        * C. Mon. Same Day patient, XREF= 1
ALMSAME   FORM      6                        * L. Mon. Same Day patient, XREF= 1
ARAFSAME  FORM      6                        * Adivsed Fig. Same Day Pat,XREF= 1
.
CMDLINE   DIM       60                       * Command line
CMSEP     FORM      6                        * Current Month Separtions
COUNT     FORM      2                        * Counter
CURNTMON  DIM       2                        * Current Month
.
DATEERR   FORM      1                        * Error flag
DIM1A     DIM       1
.
EDATCM    DIM       8                        * End of current month/Qtr.
EDATCMS   DIM       8                        * End of current month (start)
EDATLM    DIM       8                        * End of last month/Qtr.
EDATLMS   DIM       8                        * End of last month (start)
ENDRNG    DIM       8                        * End date of current month/Qtr.
EPIADATE  DIM       8                        * Episode start date
.
FCCENT    FORM      2                        * Form 2 of CCENT
FCMON     FORM      2                        * Form 2 of CMON
FCYEAR    FORM      2                        * Form 2 of CYEAR
FORM7     FORM      7
FROMDATE  DIM       8                        * Start date of period
FXREF     FORM      2                        * XREF FORM
.
JULDATE   DIM       7                        * Julian Date
.
LASTEPS   FORM      1                        * Flag. last Episode
LMSEP     FORM      6                        * YTD Last Month Separtions
.
ELEVEN    FORM      "11"
MONTH     DIM       9                        * Printing the month 
MONTH1    INIT      "January"
MONTH2    INIT      "Feburary"
MONTH3    INIT      "March"
MONTH4    INIT      "April"
MONTH5    INIT      "May"
MONTH6    INIT      "June"
MONTH7    INIT      "July"
MONTH8    INIT      "August"
MONTH9    INIT      "September"
MONTH10   INIT      "October"
MONTH11   INIT      "November"
MONTH12   INIT      "December"
.
NEWFILE   FORM      1
NADM      FORM      1                        * Flag to read Next Admis. record
NBRDAYS   FORM      6                        * Number of Bed days (PRS2DAYS)
NCHC      FORM      1                        * Flag to read Next CHC record
NEG1      FORM      "-1"
NEWBORN   FORM      1
NINETY9   FORM      "99"
NINES     FORM      "99999999"
NOTHING   DIM       25                       * Used to allow load/store commands
NUMBER    FORM      2                        * Count used when printing
CODEA     INIT      "A "
.
PEREND    DIM       8                      * period end date for stay (CCYYMMDD)
PRIREF01  INIT      "Private-Acute"
PRIREF02  INIT      "Private-Nursing Home Type"
PRIREF03  INIT      "Compensable"
PRIREF04  INIT      "Ineligible"
PRIREF05  INIT      "Public-Under Contract"
PRIREF06  INIT      "Private-Under Contract"
PRIREF07  INIT      "Private-Same Day"
PRIREF08  INIT      "Compensable-Same Day"
PRIREF09  INIT      "Ineligible-Same Day"
PRIREF10  INIT      "Pub.-Under Contract-Same Day"
PRIREF11  INIT      "Priv.-Under Contract-Same Day"
PRNTDESC  DIM       29                       * description field for printing 
PRS2END   DIM       8                        * End of range to be calculated
PRS2IACC  DIM       3                        * Account class input (PRS2CLAS)
PRS2RACC  FORM      2                        * Account class return (PRS2CLAS)
PRS2STRT  DIM       8                        * Start of range to be calculated
PSECTION  FORM      1                        * Section currently being printed
PUBREF01  INIT      "Public-Acute"
PUBREF02  INIT      "Private-Acute"
PUBREF03  INIT      "Compensable-Acute"
PUBREF04  INIT      "Ineligible-Acute"
PUBREF05  INIT      "Public NHT-NH5"
PUBREF06  INIT      "Public NHT-Non NH5"
PUBREF07  INIT      "Private NHT-NH5"
PUBREF08  INIT      "Private NHT-Non NH5"
PUBREF09  INIT      "Compensable-Non Acute"
PUBREF10  INIT      "Ineligible-Non Acute"
PUBREF11  INIT      "Public-Same Day"
PUBREF12  INIT      "Private-Same Day"
PUBREF13  INIT      "Compensable-Same Day"
PUBREF14  INIT      "Ineligible-Same Day"
.
RTDAYS    FORM      4
SAMEDAY   FORM      1                        * Flag to indicate same day patient
SAVADATE  DIM       8                        * Saving Admission Date
SATYPE1   DIM       3                        * Saving Admission Type 1
SATYPE2   DIM       3                        * Saving Admission Type 2
SPCHCODE  FORM      2                        * Seperations for dif. THCSCOD    
ST1P1CA   FORM      6                        * SubTotal Section 1 Part 1 Col. A
ST1P1CB   FORM      6                        * SubTotal Section 1 Part 1 Col. B
ST1P1CC   FORM      6                        * SubTotal Section 1 Part 1 Col. C
ST1P1CD   FORM      6                        * SubTotal Section 1 Part 1 Col. D
ST1P2CA   FORM      6                        * SubTotal Section 1 Part 2 Col. A
ST1P2CB   FORM      6                        * SubTotal Section 1 Part 2 Col. B
ST1P2CC   FORM      6                        * SubTotal Section 1 Part 2 Col. C
ST1P2CD   FORM      6                        * SubTotal Section 1 Part 2 Col. D
ST2P1CA   FORM      6                        * SubTotal Section 2 Part 1 Col. A
ST2P1CB   FORM      6                        * SubTotal Section 2 Part 1 Col. B
ST2P1CC   FORM      6                        * SubTotal Section 2 Part 1 Col. C
ST2P1CD   FORM      6                        * SubTotal Section 2 Part 1 Col. D
ST2P2CA   FORM      6                        * SubTotal Section 2 Part 2 Col. A
ST2P2CB   FORM      6                        * SubTotal Section 2 Part 2 Col. B
ST2P2CC   FORM      6                        * SubTotal Section 2 Part 2 Col. C
ST2P2CD   FORM      6                        * SubTotal Section 2 Part 2 Col. D
ST3P1CA   FORM      6                        * SubTotal Section 3 Part 1 Col. A
ST3P1CB   FORM      6                        * SubTotal Section 3 Part 1 Col. B
ST3P1CC   FORM      6                        * SubTotal Section 3 Part 1 Col. C
ST3P1CD   FORM      6                        * SubTotal Section 3 Part 1 Col. D
ST3P2CA   FORM      6                        * SubTotal Section 3 Part 2 Col. A
ST3P2CB   FORM      6                        * SubTotal Section 3 Part 2 Col. B
ST3P2CC   FORM      6                        * SubTotal Section 3 Part 2 Col. C
ST3P2CD   FORM      6                        * SubTotal Section 3 Part 2 Col. D
STHCSCOD  DIM       4                        * Save HCS equivalent code
SDATCM    DIM       8                        * Start of current Month/Qtr.
SFINYEAR  DIM       8                        * Start of Financial year
.
T1CA      FORM      6                        * Total Section 1 Column A
T1CB      FORM      6                        * Total Section 1 Column B
T1CC      FORM      6                        * Total Section 1 Column C
T1CD      FORM      6                        * Total Section 1 Column D
T2CA      FORM      6                        * Total Section 2 Column A
T2CB      FORM      6                        * Total Section 2 Column B
T2CC      FORM      6                        * Total Section 2 Column C
T2CD      FORM      6                        * Total Section 2 Column D
T3CA      FORM      6                        * Total Section 3 Column A
T3CB      FORM      6                        * Total Section 3 Column B
T3CC      FORM      6                        * Total Section 3 Column C
T3CD      FORM      6                        * Total Section 3 Column D
TMPFNAM1  DIM       8                        * Temporary filename1
TMPFNAM2  DIM       8                        * Temporary filename2
TODATE    DIM       8                        * End date of period
TODAY     DIM       8                        * Current date
TOTSEPS   FORM      6                        * Column D/Total of Seperations
TOTBDAY   FORM      6                        * Column D/Total of Bed days 
TOTSAME   FORM      6                        * Column D/Total of Same Days
.
UNQLNBDY  FORM      6                       * Unqualified newborn days
UNQLNBEP  FORM      6                       * Unqualified newborn episodes
.
YRORMON   FORM      1                        * Flag to process YTD/current month
.
Z10       INIT      "zzzzzzzzzz"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
.         TEMPORARY FILES
.
TMPFILE1  IFILE     SQL,FIXED=9, KEY="U1-8"
.
. Name    Type   Length  Physical Start loc.  Description
. ----    ----   ------  -------- ---------   ------------------------
DTEMPADM  DIM       8        8       1        * Admission Number
.                                 ---------
.End of record                       9 
.
. redefined from files from key
. ----------------------------
TEMPADMN  DIM       8        8       1        * Admission Number
.
TMPFILE2  IFILE     SQL,FIXED=27, KEY="U1-2"
.
. Name    Type   Length  Physical Start loc.  Description
. ----    ----   ------  -------- ---------   ------------------------
XREF      DIM       2       2        1        Reference field
.                                             Public Hospital
.                                                1 = Public - Acute
.                                                2 = Private - Acute Private
.                                                3 = Compensable - Acute
.                                                4 = Ineligible - Acute
.                                                5 = Public NHT with NH5
.                                                6 = Public NHT without NH5
.                                                7 = Private NHT with NH5
.                                                8 = Private NHT without NH5
.                                                9 = Compensable - Non Acute
.                                               10 = Ineligible - Non Acute
.                                             Private Hospital
.                                                1 = Private - Aucte
.                                                2 = Private - Nursing Home Type
.                                                3 = Compensable
.                                                4 = Ineligible
.                                                5 = Public - Under Contract
.                                                6 = Private - Under Contract
XLMYTDS   FORM      6       4        3        Public Hospital
.                                                Last Month YTD Separations
.                                             Private Hospital
.                                                Last Quarter YTD Separations
XLMYTDB   FORM      6       4        7        Public Hospital 
.                                                Last Month Bed Days
.                                             Private Hospital
.                                                Last Quarter Bed Days
XLMYTDD   FORM      6       4        11       Public Hospital 
.                                                Last Month Same Day Patients
.                                             Private Hospital
.                                                Last Quarter Same Day Patients
XCMSEPS   FORM      6       4        15       Public Hospital 
.                                                Current Month Separations
.                                             Private Hospital
.                                                Current Quarter Separations
XCMBDAY   FORM      6       4        19       Public Hospital 
.                                                Current Month Bed Days
.                                             Private Hospital
.                                                Current Quarter Bed Days
XCMSAME   FORM      6       4        23       Public Hospital
.                                                Current Month Same Days
.                                             Private Hospital
.                                                Current Quarter Same Days
.                                ----------
.End of record                      27
+
.*******************************************************************************
.*                                ML0000                                       *
.*                    Controlling Logic (Mainline Code)                        *
.*******************************************************************************
ML0000    CALL      INIT0000                 * Initiaize & open files
          CALL      OPTN0000                * user selects option
          BRANCH    OPTION,ML0100
          GOTO      ML9999
.
ML0100    CALL      CLRB0000                 * Clear Variables
.
          BRANCH    PTCNHOSP,ML0150          * 0 = Private, 1 = Public
          CALL      KDAT0000                 * Key in to date
          BRANCH    EXIT,ML9999
          GOTO      ML0170
.
ML0150    CALL      KDTE0000                 * Key in to date
          BRANCH    EXIT,ML9999
.
ML0170    CALL      CONTQST
          BRANCH    CEXIT,ML0200,ML0100,ML9999

ML0200    DISPLAY   *P1:23,*EL               * Clear line 23. 
          CALL      CTMP0000                 * Create Temporary files
          BRANCH    PTCNHOSP,ML2000          * 0 = Private, 1 = Public
.
.         Private   Hospital
.
ML1000    BRANCH    NEWFILE,ML1500 
          CALL      CSCQ0000                 * Calculate Start of Current Qtr.
          CALL      CSFY0000                 * Calculate Start of Financial Yr
          CALL      CELM0000                 * Calculate End of Last Quarter
          CALL      CECQ0000                 * Calculate End of Current Quarter
          CALL      LMIS0000                 * Look thru admissions file
          CALL      LDSC0000                 * Look thru discharge file
          CALL      PROC0000                 * Process Data
          CALL      HPRI0000                 * Print private hospital header 
          CALL      PPRI0000                 * Print private hospital statistics
          CALL      EREP0000                 * End Report
          GOTO      ML0100                   * Return for next date
.
ML1500    CALL      CSCM0000
          CALL      CELM0000                 * Calculate End of Last Month  
          CALL      LMIS0000                 * Look thru admissions file
          CALL      LDSC0000                 * Look thru discharge file
          CALL      PROC0000                 * Process Data
          CALL      HPRI0000                 * Print private hospital header 
          CALL      PPRI0000                 * Print private hospital statistics
          CALL      EREP0000                 * End Report
          GOTO      ML0100                   * Return for next date
.
.         Public Hospital
.
ML2000    CALL      LMIS0000                 * Look thru admissions file
          CALL      LDSC0000                 * Look thru discharge file
          CALL      PROC0000                 * Process Data
          CALL      HPUB0000                 * Print public hospital header
          CALL      PPUB0000                 * Print public hospital statistics
          CALL      EREP0000                 * End Report
          GOTO      ML0100
.
ML9999    CALL      RTMP0000                 * Remove Temporary files 
          CHAIN     PGM
+
.*******************************************************************************
.*                              INIT0000                    CALLED BY : ML0000 *
.*                        Initialize and open files                            *
.*******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,*EL,"Opening";
.
          DISPLAY   *P64:24,*EL,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,*EL,"pataccaf";
          OPEN      PATACCA1,"pataccaf"
.
          DISPLAY   *P64:24,*EL,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,*EL,"patchcof";
          OPEN      PATCHCO1,"patchcof"
.
          DISPLAY   *P64:24,*EL,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,*EL,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,*EL,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,*EL,"prsadfaf";
          OPEN      PRSADFA1,"prsadfaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af";
.
          READ      CONTROLF,TEN;*54,CFILENO
          READ      CONTROLF,TWENTY1;*210,PTCNHOSP
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TMPFNAM1
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TMPFNAM2
.
INIT9999  RETURN
.******************************************************************************
.*                                  OPTN0000                                  *
.*        get user's option selection                    called by : MAINLINE *
.*                                                                            *
.*        valid options     --->  (0) Exit                                    *
.*                                (1) Run                                     *
.******************************************************************************
OPTN0000  HLINE     *G37,2,57,80
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*P1:5,ONE,*HOFF:
                    *P2:4," = Exit":
                    *P2:5," = Generate AIMS Form":
                    *P1:7,"Select Option :";
.
OPTN1000  KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
          COMPARE   ZERO,OPTION               * exit ?
          GOTO      OPTN9999 IF EQUAL         * yes
.
          BRANCH    OPTION,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.*******************************************************************************
.*                              CLRA0000                  CALLED BY : ITMP0000 *
.*                                                                    CLRB0000 *
.*                        Clear tempfile variables                             *
.*******************************************************************************
CLRA0000  MOVE      SP2,XREF                 Reference field
          MOVE      ZERO,XLMYTDS             Last Month/Quarter YTD Seperations
          MOVE      ZERO,XLMYTDB             Last Month/Quarter Bed Days
          MOVE      ZERO,XLMYTDD             Last Month/Quarter YTD Same Day Pat
          MOVE      ZERO,XCMSEPS             Current Month/Quarter Separations
          MOVE      ZERO,XCMBDAY             Current Month/Quarter Bed Days
          MOVE      ZERO,XCMSAME             Current Month/Quarter Same Days
CLRA9999  RETURN
+
.*******************************************************************************
.*                              CLRB0000                  CALLED BY : ML0100   *
.*                        Clear global variables                               *
.*******************************************************************************
CLRB0000  CALL      CLRA0000                 * Clear Tempfile variables
.
          MOVE      ZERO,CMSEP               * Current Month Sepeartions
          MOVE      ZERO,LMSEP               * YTD Last Month Sepeartions
          MOVE      ZERO,ST1P1CA             * SubTotal Section 1 Part 1 Col. A
          MOVE      ZERO,ST1P1CB             * SubTotal Section 1 Part 1 Col. B
          MOVE      ZERO,ST1P1CC             * SubTotal Section 1 Part 1 Col. C
          MOVE      ZERO,ST1P1CD             * SubTotal Section 1 Part 1 Col. D
          MOVE      ZERO,ST1P2CA             * SubTotal Section 1 Part 2 Col. A
          MOVE      ZERO,ST1P2CB             * SubTotal Section 1 Part 2 Col. B
          MOVE      ZERO,ST1P2CC             * SubTotal Section 1 Part 2 Col. C
          MOVE      ZERO,ST1P2CD             * SubTotal Section 1 Part 2 Col. D
          MOVE      ZERO,ST2P1CA             * SubTotal Section 2 Part 1 Col. A
          MOVE      ZERO,ST2P1CB             * SubTotal Section 2 Part 1 Col. B
          MOVE      ZERO,ST2P1CC             * SubTotal Section 2 Part 1 Col. C
          MOVE      ZERO,ST2P1CD             * SubTotal Section 2 Part 1 Col. D
          MOVE      ZERO,ST2P2CA             * SubTotal Section 2 Part 2 Col. A
          MOVE      ZERO,ST2P2CB             * SubTotal Section 2 Part 2 Col. B
          MOVE      ZERO,ST2P2CC             * SubTotal Section 2 Part 2 Col. C
          MOVE      ZERO,ST2P2CD             * SubTotal Section 2 Part 2 Col. D
          MOVE      ZERO,ST3P1CA             * SubTotal Section 3 Part 1 Col. A
          MOVE      ZERO,ST3P1CB             * SubTotal Section 3 Part 1 Col. B
          MOVE      ZERO,ST3P1CC             * SubTotal Section 3 Part 1 Col. C
          MOVE      ZERO,ST3P1CD             * SubTotal Section 3 Part 1 Col. D
          MOVE      ZERO,ST3P2CA             * SubTotal Section 3 Part 2 Col. A
          MOVE      ZERO,ST3P2CB             * SubTotal Section 3 Part 2 Col. B
          MOVE      ZERO,ST3P2CC             * SubTotal Section 3 Part 2 Col. C
          MOVE      ZERO,ST3P2CD             * SubTotal Section 3 Part 2 Col. D
.
          MOVE      ZERO,T1CA                * Total Section 1 Column A
          MOVE      ZERO,T1CB                * Total Section 1 Column B
          MOVE      ZERO,T1CC                * Total Section 1 Column C
          MOVE      ZERO,T1CD                * Total Section 1 Column D
          MOVE      ZERO,T2CA                * Total Section 2 Column A
          MOVE      ZERO,T2CB                * Total Section 2 Column B
          MOVE      ZERO,T2CC                * Total Section 2 Column C
          MOVE      ZERO,T2CD                * Total Section 2 Column D
          MOVE      ZERO,T3CA                * Total Section 3 Column A
          MOVE      ZERO,T3CB                * Total Section 3 Column B
          MOVE      ZERO,T3CC                * Total Section 3 Column C
          MOVE      ZERO,T3CD                * Total Section 3 Column D
.
          MOVE      ZERO,UNQLNBDY            * Unqualified newborn days
          MOVE      ZERO,UNQLNBEP            * Unqualified newborn episodes
CLRB9999  RETURN 
+
.*******************************************************************************
.*                              CTMP0000                  CALLED BY : INIT0000 *
.*                       Create Temporary files                                *
.*******************************************************************************
CTMP0000  CALL      RTMP0000                 * Ensure old tempfiles are removed
.
          CLEAR     CMDLINE                  * build admiss. temporary file
          APPEND    "isbuild ",CMDLINE 
          APPEND    TMPFNAM1,CMDLINE
          APPEND    " 9 U1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID           * execute isbuild
          OPEN      TMPFILE1,TMPFNAM1        * Open admiss. temporary file
.
          CLEAR     CMDLINE                  * build public/private temp. file
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFNAM2,CMDLINE
          APPEND    " 27 U1-2",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID           * execute isbuild
          OPEN      TMPFILE2,TMPFNAM2        * Open public temporary file
.
          CALL      ITMP0000                 * Initialize Temporary files
.
CTMP9999  RETURN
+
.*******************************************************************************
.*                              RTMP0000                  CALLED BY : ML9999   *
.*                                                                    CTMP0000 *
.*                       Remove Temporary files                                *
.*******************************************************************************
RTMP0000  CLOSE     TMPFILE1                 * close discharge tempfile
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE       * remove discharge tempfile
          APPEND    TMPFNAM1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID            * execute iserase
.
          CLOSE     TMPFILE2                  * Close public tempfile
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFNAM2,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID            * execute iserase
.
RTMP9999  RETURN
+
.*******************************************************************************
.*                              ITMP0000                  CALLED BY : CTMP0000 *
.*                       Initialize Temporary file                             *
.*******************************************************************************
ITMP0000  MOVE      ZERO,COUNT
          BRANCH    PTCNHOSP,ITMP2000        * PTCNHOSP = 1 = Public Hospital 
.
.         Private Hospital
.
ITMP1000  CALL      CLRA0000                 * Initialize to zero
          ADD       ONE,COUNT
.
          PACK      XREF,COUNT
          CALL      WRTEMPA2                 * Write to tempfile2
.
          COMPARE   ELEVEN,COUNT             * Count < 11
          GOTO      ITMP1000 IF LESS         * Yes. Write next XREF
          GOTO      ITMP9999                 * No. Exit Routine
.
.         Public Hospital
.
ITMP2000  CALL      CLRA0000                 * Initialize to zero
          ADD       ONE,COUNT
.
          PACK      XREF,COUNT
          CALL      WRTEMPA2                 * Write to tempfile2
.
          COMPARE   ELEVEN,COUNT             * Count < 11
          GOTO      ITMP2000 IF LESS         * Yes. Write next XREF
.
ITMP9999  RETURN
+
.*******************************************************************************
.*                              IDAT0000                  CALLED BY : KDAT0000 *
.*                        Initialize date variables                            *
.*******************************************************************************
IDAT0000
          MOVE      ZERO,CCANLDTE
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
.
IDAT9999  RETURN
+
.*******************************************************************************
.*                              KDAT0000                  CALLED BY : ML0100   *
.*                Keyin the date that will end the period                      *
.*******************************************************************************
KDAT0000  CALL      IDAT0000                 * Clear date variables 
          MOVE      ZERO,NEWFILE
          DISPLAY   *P1:9,*EF,"Period End Date : "
          MOVE      "19",CCOL
          MOVE      "9",CVERT
          CALL      KEYDATE                  * Keyin date
          BRANCH    OVRCD,KDAT9000           * Nothing input, exit
.
          PACK      ENDRNG,CCENT,CYEAR,CMON,CDAY * End of period/current month
          MOVE      CMON,CURNTMON           * Current Month
.
          CALL      VDAT0000                 * Validate date
          BRANCH    DATEERR,KDAT0000         * Error. Re-enter a new date
.
          MATCH     "20020701",ENDRNG
          IF        !@LESS
            MOVE      ONE,NEWFILE
          ENDIF
.
.         Date entered do not exit 
.
          MOVE      FALSE,EXIT               * Do not exit
          GOTO      KDAT9999                 * Exit Routine
.
.         No date entered exit.
.
KDAT9000  MOVE      TRUE,EXIT                * Exit
.
KDAT9999  RETURN
+
.*******************************************************************************
.*                              VDAT0000                  CALLED BY : KDAT0000 *
.*                Validate the date so it is not in the future                 *
.*                or before the date the report was last ran                   *
.*******************************************************************************
VDAT0000  
.         Ensure date is not in the future.
.
          CALL      IBACLOCK                 * obtain today's date
          PACK      TODAY,CCC,CYY,CMM,CDD    * pack today' date
          REP       " 0",TODAY               * replaces spaces with zeros
.
          MATCH     ENDRNG,TODAY             * Date in future ?
          GOTO      VDAT1000 IF NOT LESS     * No. Date is valid
.
          DISPLAY   *P1:24,*EL,*B,"Date must not be in the future. ";
          CALL      HITENTER
          GOTO      VDAT9000                 * Error. Re-Key date.
.
.         Ensure date is not before the date the report was last ran
.
VDAT1000  PACK      KEY10,Z10
          CALL      RSPRADF1                 * Postional read
          CALL      RPPRADF1                 * Read next record
          BRANCH    OVRCD,VDAT8000           * End of file
          MATCH     PRAFDATE,ENDRNG          * Date before last ran report date?
          GOTO      VDAT8000 IF NOT LESS     * No. Date is valid
.
          UNPACK    PRAFDATE,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P1:24,*EL,*B,"Date must be after ":
                    CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,". ";
          CALL      HITENTER
          GOTO      VDAT9000                 * Error. Re-Key date.
          
VDAT8000  MOVE      FALSE,DATEERR            * No Error.
          GOTO      VDAT9999
.
VDAT9000  MOVE      TRUE,DATEERR             * Error. Re-Key date.
.
VDAT9999  RETURN
+
.*******************************************************************************
.*                              KDTE0000                  CALLED BY : ML0100   *
.*                Keyin the date that will end the period                      *
.*******************************************************************************
KDTE0000  CALL      IDAT0000                 * Clear date variables 
          DISPLAY   *P1:9,*EF,"Period End Date : "
          MOVE      "19",CCOL
          MOVE      NINE,CVERT
          CALL      KEYDATE                  * Keyin date
          BRANCH    OVRCD,KDTE9000
.
          PACK      ENDRNG,CCENT,CYEAR,CMON,CDAY * End of period/current month
          MOVE      CMON,CURNTMON           * Current Month
.
          CALL      VDTE0000                 * Validate date
          BRANCH    DATEERR,KDTE0000         * Error. Re-enter a new date
.
          CALL      CSCM0000
          UNPACK    SDATCM,CCENT,CYEAR,CMON,CDAY
          UNPACK    ENDRNG,XCENT,XYEAR,XMON,XDAY
.
          DISPLAY   *P1:15,*EL,"Date range for this run will be : ",*V2LON:
                           CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,*HOFF," to ":
                           *V2LON,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
.         Date entered do not exit 
.
          MOVE      FALSE,EXIT               * Do not exit
          GOTO      KDTE9999                 * Exit Routine
.
.         No date entered exit.
.
KDTE9000  MOVE      TRUE,EXIT                * Exit
.
KDTE9999  RETURN
+
.*******************************************************************************
.*                              VDTE0000                  CALLED BY : KDTE0000 *
.*                Validate the date so it is not in the future                 *
.*                or before the date the report was last ran                   *
.*******************************************************************************
VDTE0000  
.         Ensure date is not in the future.
.
          CALL      IBACLOCK                 * obtain today's date
          PACK      TODAY,CCC,CYY,CMM,CDD    * pack today' date
          REP       " 0",TODAY               * replaces spaces with zeros
.
          MATCH     ENDRNG,TODAY             * Date in future ?
          GOTO      VDTE8000 IF NOT LESS     * No. Date is valid
.
          DISPLAY   *P1:24,*EL,*B,"Date must not be in the future. ";
          CALL      HITENTER
          GOTO      VDTE9000                 * Error. Re-Key date.
.
.         Ensure date is not before the date the report was last ran
.
. ---------------------------------------------------------------------------
. ******* this file is not used anymore as ytd figures are not required *****
. ---------------------------------------------------------------------------
VDTE1000  PACK      KEY10,Z10
          CALL      RSPRADF1                 * Postional read
          CALL      RPPRADF1                 * Read next record
          BRANCH    OVRCD,VDTE8000           * End of file
          MATCH     PRAFDATE,ENDRNG          * Date before last ran report date?
          GOTO      VDTE8000 IF NOT LESS     * No. Date is valid
.
          UNPACK    PRAFDATE,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P1:24,*EL,*B,"Date must be after ":
                    CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,". ";
          CALL      HITENTER
          GOTO      VDTE9000                 * Error. Re-Key date.
          
VDTE8000  MOVE      FALSE,DATEERR            * No Error.
          GOTO      VDTE9999
.
VDTE9000  MOVE      TRUE,DATEERR             * Error. Re-Key date.
.
VDTE9999  RETURN
+
.*******************************************************************************
.*                              CSCM0000                  CALLED BY : ML2000   *
.*                Calculate the Start of the Current Month                     *
.*******************************************************************************
CSCM0000  UNPACK    ENDRNG,CCENT,CYEAR,CMON,CDAY   * End of period/current month
.
          PACK      SDATCM,CCENT,CYEAR,CMON,ZERO,ONE * With Century
.
          IF        NEWFILE = 1
            MOVE      ENDRNG,EDATCM
          ENDIF
.
CSCM9999  RETURN
+
.*******************************************************************************
.*                              CSCQ0000                  CALLED BY : ML1000   *
.*                Calculate the Start of the Current Quarter                   *
.*******************************************************************************
CSCQ0000  UNPACK    ENDRNG,CCENT,CYEAR,CMON,CDAY   * End of period/current month
.
          MOVE      CMON,FCMON
          LOAD      FCMON,FCMON,ONE,ONE,ONE:        * Start Quarter = January
                                FOUR,FOUR,FOUR:     * Start Quarter = April
                                SEVEN,SEVEN,SEVEN:  * Start Quarter = July
                                TEN,TEN,TEN         * Start Quarter = October
          MOVE      FCMON,CMON
.
          PACK      SDATCM,CCENT,CYEAR,CMON,ZERO,ONE * With Century
          REP       " 0",SDATCM                  * Replace spaces with zeros
.
CSCQ9999  RETURN
+
.*******************************************************************************
.*                              CSFY0000                  CALLED BY : ML1000   *
.*                                                                    ML2000   *
.* Calculate the Start of financial year from the end of current month/quarter *
.*******************************************************************************
CSFY0000  UNPACK    ENDRNG,CCENT,CYEAR,CMON,CDAY   * End of period/current month
.
          MOVE      CMON,FCMON
          COMPARE   FCMON,SIX                * Month > June 
          GOTO      CSFY1000 IF LESS         * Yes
.
          MOVE      CYEAR,FCYEAR             * Mth 1-6:CYEAR is the previous yr
          SUB       ONE,FCYEAR
          MOVE      FCYEAR,CYEAR 
.
CSFY1000  PACK      SFINYEAR,CCENT,CYEAR,ZERO,SEVEN,ZERO,ONE * Start of fin. Yr
.
CSFY9999  RETURN
+
.*******************************************************************************
.*                              CELM0000                  CALLED BY : ML1000   *
.*                                                                    ML2000   *
.*Calculate the End of Last Month/Qtr. from the start of the current month/Qtr.*
.*******************************************************************************
CELM0000  UNPACK    SDATCM,CCENT,CYEAR,CMON,CDAY * Start of current month
.
.         Check if current month/quarter is July
.
          MOVE      CMON,FCMON
          COMPARE   FCMON,SEVEN              * Month/Quarter = July 
          GOTO      CELM1000 IF NOT EQUAL    * No. Con. for previous month/Qtr
.
          BRANCH    NEWFILE,CELM1000
.
          MOVE      SFINYEAR,EDATLM        * Yes. End last month/Qtr = SFINYEAR
          GOTO      CELM9999                 *      Exit Routine
.
.         Calcaluate the end date of the previous month
.
CELM1000  MOVE      CCENT,CC                 * Move CCENT to CC for DMYCON
          MOVE      CYEAR,YY                 * Move CYEAR to YY for DMYCON
          MOVE      CMON,MM                  * Move CMON  to MM for DMYCON
          MOVE      CDAY,DD                  * Move CDAY  to DD for DMYCON
          CALL      DMYCON                   * Convert to Julian format
          PACK      JULDATE,JULCC,JULYR,JULDAY * Pack Julian format
          REP       " 0",JULDATE             * Replace spaces with zeros
.
          MOVE      JULDATE,FORM7            * Move to FORM7 for subtraction
          SUB       ONE,FORM7                * Subtract for previous day
          MOVE      FORM7,JULDATE            * Move to JULDATE for pack
.
          UNPACK    JULDATE,JWKCC,JWKYER,JWKDAY
          CALL      JULCON                   * Convert to gregorian format
          PACK      EDATLM,CC,YY,MM,DD       * End date last month with century 
          REP       " 0",EDATLM              * Replace spaces with zeros
.
          PACK      EDATLMS,CC,YY,MM,ZERO,ONE   * start date of last month
          REP       " 0",EDATLMS
.
CELM9999  RETURN
+
.*******************************************************************************
.*                              CECM0000                  CALLED BY : ML2000   *
.*   Calculate the End of Current Month from the start of the current month    *
.*******************************************************************************
CECM0000  UNPACK    SDATCM,CCENT,CYEAR,CMON,CDAY * Start of current month
          MOVE      CMON,FCMON
.
          COMPARE   TEN2,FCMON               * Current Month = December
          GOTO      CECM1000 IF EQUAL        * Yes.
.
          ADD       ONE,FCMON                * No. Add one to next month
          MOVE      FCMON,CMON   
          GOTO      CECM2000                 * Calculate first day of next month
.
CECM1000  MOVE      ONE,CMON                 * Current Mon = DEC, Next Mon = JAN
.
          MOVE      CYEAR,FCYEAR             * Yes. Increase Year by 1
          COMPARE   NINETY9,FCYEAR           * Current Year = 99 ?
          GOTO      CECM1100 IF EQUAL        * Yes.
.
          ADD       ONE,FCYEAR               * No. Add one to next year
          MOVE      FCYEAR,CYEAR
          GOTO      CECM2000                 * Calculate first day of next Qtr.
.
CECM1100  MOVE      CCENT,FCCENT             * Increase Century by 1
          ADD       ONE,FCCENT
          MOVE      FCCENT,CCENT
          PACK      CYEAR,ZERO,ZERO
.
CECM2000  PACK      CDAY,ZERO,ONE            * First Day of Next Month
.
.         Calcuate  last day of current month
.
          MOVE      CCENT,CC                 * Move CCENT to CC for DMYCON
          MOVE      CYEAR,YY                 * Move CYEAR to YY for DMYCON
          MOVE      CMON,MM                  * Move CMON  to MM for DMYCON
          MOVE      CDAY,DD                  * Move CDAY  to DD for DMYCON
          CALL      DMYCON                   * Convert to Julian format
          PACK      JULDATE,JULCC,JULYR,JULDAY * Pack Julian format
          REP       " 0",JULDATE             * Replace spaces with zeros
.
          MOVE      JULDATE,FORM7            * Move to FORM7 for subtraction
          SUB       ONE,FORM7                * Subtract for previous day
          MOVE      FORM7,JULDATE            * Move to JULDATE for pack
.
          UNPACK    JULDATE,JWKCC,JWKYER,JWKDAY
          CALL      JULCON                   * Convert to gregorian format
          PACK      EDATCM,CC,YY,MM,DD       * End date curnt month with century
          REP       " 0",EDATCM              * Replace spaces with zeros
.
CECM9999  RETURN
+
.*******************************************************************************
.*                              CECQ0000                  CALLED BY : ML1000   *
.*     Calculate the End of Current Qtr.from the start of the current Qtr      *
.*******************************************************************************
CECQ0000  UNPACK    SDATCM,CCENT,CYEAR,CMON,CDAY * Start of current month
.
.         Load Next Quarter
.
          MOVE      CMON,FCMON 
          LOAD      CMON,FCMON,FOUR,FOUR,FOUR,SEVEN,SEVEN,SEVEN:
                               TEN,TEN,TEN,ONE,ONE,ONE
          MOVE      CMON,FCMON 
.
          COMPARE   ONE,FCMON                * Next Quarter = One ?
          GOTO      CECQ2000 IF NOT EQUAL    * No.
.
          MOVE      CYEAR,FCYEAR             * Yes. Increase Year by 1
          COMPARE   NINETY9,FCYEAR           * Current Year = 99 ?
          GOTO      CECQ1100 IF EQUAL        * Yes.
.
          ADD       ONE,FCYEAR               * No. Add one to next year
          MOVE      FCYEAR,CYEAR
          GOTO      CECQ2000                 * Calculate first day of next Qtr.
.
CECQ1100  MOVE      CCENT,FCCENT             * Increase Century by 1
          ADD       ONE,FCCENT
          MOVE      FCCENT,CCENT
          PACK      CYEAR,ZERO,ZERO
.
CECQ2000  PACK      CDAY,ZERO,ONE            * First Day of Next Quarter
.
.         Calcuate  last day of current month
.
          MOVE      CCENT,CC                 * Move CCENT to CC for DMYCON
          MOVE      CYEAR,YY                 * Move CYEAR to YY for DMYCON
          MOVE      CMON,MM                  * Move CMON  to MM for DMYCON
          MOVE      CDAY,DD                  * Move CDAY  to DD for DMYCON
          CALL      DMYCON                   * Convert to Julian format
          PACK      JULDATE,JULCC,JULYR,JULDAY * Pack Julian format
          REP       " 0",JULDATE             * Replace spaces with zeros
.
          MOVE      JULDATE,FORM7            * Move to FORM7 for subtraction
          SUB       ONE,FORM7                * Subtract for previous day
          MOVE      FORM7,JULDATE            * Move to JULDATE for pack
.
          UNPACK    JULDATE,JWKCC,JWKYER,JWKDAY
          CALL      JULCON                   * Convert to gregorian format
          PACK      EDATCM,CC,YY,MM,DD       * End date current Qtr. with centur
          REP       " 0",EDATCM              * Replace spaces with zeros
.
CECQ9999  RETURN
+
.*******************************************************************************
.*                              LMIS0000                  CALLED BY : ML1000   *
.*                                                                    ML2000   *
.*   Loop through the admission file for current patients and onleave patients *
.*******************************************************************************
.         Obtain Current inpatients
.
LMIS0000  DISPLAY   *P1:24,*EL,"Collecting Admission No. :";
          PACK      KEY9,TWO,SP8             * Read for current admissions
          CALL      RDSMISS2                 * Positional Read
LMIS0100  CALL      RDKMISS2                 * Read next record
          BRANCH    OVRCD,LMIS1000           * EOF. Read for on leave patients
.
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSB,TCINDC3
            GOTO      LMIS0100 IF EQUAL
          ENDIF
.
          COMPARE   TWO,ASTAT                * Current Admission ?
          GOTO      LMIS1000 IF NOT EQUAL    * No. Read for on leave patients
.
          MATCH     ADATE,ENDRNG             * Admitted after End of current mon
          GOTO      LMIS0100 IF LESS         * Yes. Read next record
          MOVE      AADMNO,KEY8
          MOVE      AADMNO,TEMPADMN          * Admission Number
          CALL      WRTA0000                 * No. Write to tempfile1
          GOTO      LMIS0100                 * Read next current admn. record
.
.         Obtain on leave patients
.
LMIS1000  PACK      KEY9,FOUR,SP8            * Read for on leave patients
          CALL      RDSMISS2                 * Positional Read
LMIS1100  CALL      RDKMISS2                 * Read next record
          BRANCH    OVRCD,LMIS9999     
.
          COMPARE   FOUR,ASTAT               * Onleave Patient ?
          GOTO      LMIS9999 IF NOT EQUAL    * No. Exit  
.
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSB,TCINDC3
            GOTO      LMIS1100 IF EQUAL
          ENDIF
.
          MATCH     ADATE,ENDRNG             * Admitted after End of current mon
          GOTO      LMIS1100 IF LESS         * Yes. Read next record
          MOVE      AADMNO,KEY8
          MOVE      AADMNO,TEMPADMN          * Admission Number
          CALL      WRTA0000                 * No. Write to tempfile1
          GOTO      LMIS1100                 * Read next on leave record
.
LMIS9999  RETURN
+
.*******************************************************************************
.*                              LDSC0000                  CALLED BY : ML0100   *
.*                Loop through the discharge file                              *
.*******************************************************************************
LDSC0000  DISPLAY   *P1:24,*EL,"Collecting Admission No. :";
.
. ------- Only calculating the current month for public hospitals ---------
.
. As of the July 2002, the report was changed from reporting per quater*C-90301
. to reporting per month. This means if you run before 20020701 then   *C-90301
. the figures look huge as it includes all discharges from the         *C-90301
. Beginning of the last financial year.                                *C-90301
. If you run after 20020701 then it only takes from start of           *C-90301
. Last month.                                                          *C-90301
.
          IF        PTCNHOSP=1                
            MOVE      SDATCM,SFINYEAR                                  *C-90301
          ENDIF                                                        *C-90301
.
          IF        NEWFILE=1
            MOVE      EDATLMS,SFINYEAR                                 *C-90301
          ENDIF                                                        *C-90301
.
          PACK      KEY16,SFINYEAR,SP20      * Pack Key
          CALL      RDSDSCH2                 * Positional read
.
LDSC1000  CALL      RDKDSCH2                 * Read next record 
          BRANCH    OVRCD,LDSC9999           * End of file - Exit
.
          MATCH     SFINYEAR,DDATE           * Disch. date before "SFinYear" ?
          GOTO      LDSC9999 IF LESS         * Yes. Exit 
.
          MOVE      DADMNO,KEY8              * No.  
          CALL      RDMISS1
          IF        OVRCD<>1 
            PACK      KEY5,ANSA,SP1,ATYPE
            CALL      RDCODE1
            IF        OVRCD<>1
              MATCH     ANSB,TCINDC3
              GOTO      LDSC1000 IF EQUAL
            ENDIF
          ENDIF 
.
          MATCH     DDATE,ENDRNG             * Discharge date after "Enddate" ?
          GOTO      LDSC2000 IF LESS         * Yes. Check admission date 
.
          MOVE      DADMNO,TEMPADMN          * Admission Number
          CALL      WRTA0000                 * No. Write to tempfile1
          GOTO      LDSC1000                 * Read next record
.
.         Discharged after end date check if admission date 
.         is within the range
.
LDSC2000  MOVE      DADMNO,KEY8              
          CALL      RDMISS1                  * Read admission file
          BRANCH    OVRCD,LDSC1000           * Admission number not on file
.
          MATCH     ADATE,ENDRNG             * Admitted after End of period ?
          GOTO      LDSC1000 IF LESS         * Yes. Read next record
. 
          MOVE      DADMNO,KEY8
          MOVE      DADMNO,TEMPADMN          * Admission Number
          CALL      WRTA0000                 * No. Write to tempfile1
          GOTO      LDSC1000                 * Read next record
.
LDSC9999  RETURN
+
.******************************************************************************
.*                                  ACTE0000                                  *
.*        Check the care class                                                *
.******************************************************************************
ACTE0000  MOVE      ZERO,EXIT
          COMPARE   ONE,PTCNHOSP
          GOTO      ACTE9999 IF NOT EQUAL   * Private hospital, exit
.
          MOVE      AADMNO,CHADMN
          MOVE      SDATCM,CHDATE
          PACK      KEY24,CHADMN,SDATCM,SP30   * Position on adm and start date
          CALL      RDSCHCO1                * Position on the code changes file
ACTE1000  CALL      RDKCHCO1                * Read the next code changes record
          BRANCH    OVRCD,ACTE5000
.
          MATCH     AADMNO,CHADMN
          GOTO      ACTE5000 IF NOT EQUAL   * Different admission number, exit
.
          MATCH     "CC",CHCATG
          GOTO      ACTE5000 IF NOT EQUAL
.
          MATCH     CHDATE,ENDRNG           * change after end period
          GOTO      ACTE5000 IF LESS
.
          PACK      KEY24,CHADMN,CHDATE,Z30 * Position on last rec. of the date
          CALL      RDSCHCO1                * Position on the code changes file
          CALL      RDPCHCO1                * Read the next code changes record
.
          PACK      KEY5,ANSC,ANSC,CHCODE   * pack key for care class
          CALL      CCOD0000
          BRANCH    EXIT,ACTE1000
          GOTO      ACTE9999
.
ACTE5000  PACK      KEY5,ANSC,ANSC,ACARE    * check the admission care class
          CALL      CCOD0000
ACTE9999  RETURN
+
.*******************************************************************************
.*       Check the change code record                                          *
.*******************************************************************************
CCOD0000  CALL      RDCODE1                 * read code file
          BRANCH    OVRCD,CCOD8000
.
          MATCH     "8",THCSCOD             * Palliative?
          GOTO      CCOD8000 IF EQUAL
          MATCH     "9",THCSCOD             * Geriatric?
          GOTO      CCOD8000 IF EQUAL
          MATCH     "5",THCSCOD             * Psych?
          GOTO      CCOD8000 IF EQUAL
.
          GOTO      CCOD9000
.
CCOD8000  MOVE      ONE,EXIT                * non acute
          GOTO      CCOD9999
.
CCOD9000  MOVE      ZERO,EXIT               * acute
CCOD9999  RETURN
+
.*******************************************************************************
.*                              WRTA0000                  CALLED BY : LMIS0000 *
.*                                                                    LDSC0000 *
.*       Write the temporary file1, - if admission number already exists       *
.*       do no write to the file again                                         *
.*                                                                             *
.* Input parameters : KEY8                                                     *
.*******************************************************************************
WRTA0000  CALL      RDTEMPA1                 * Admission Number exist ?
          BRANCH    OVRCD,WRTA1000           * No. Write  
          GOTO      WRTA9999                 * Yes. Do not write 
.
WRTA1000  CALL      WRTEMPA1                 * Record does not exist; write  
          DISPLAY   *P28:24,*EL,TEMPADMN     * Display current admission no.
.
WRTA9999  RETURN
+
.******************************************************************************
.*                                  PROC0000             CALLED BY :   ML1000 *
.*                                                                     ML2000 *
.*       generate separtions and count the according XREF variables           *
.******************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Processing Admission No. :"
          PACK      KEY8,SP8
          CALL      RSTEMPA1                 * postional read
PROC1000  CALL      RKTEMPA1                 * read Adm Nos temp file
          BRANCH    OVRCD,PROC9000           * End of File. Exit
.
          MOVE      SP3,STHCSCOD             * Clear save HCS code
          DISPLAY   *P27:24,TEMPADMN
          MOVE      FALSE,LASTEPS            * Not last episode for Admis.
.
          MOVE      TEMPADMN,KEY8 
          CALL      RDMISS1                  * Admission Number on file ?
          BRANCH    OVRCD,PROC1000           * No. Read next record
.
          MOVE      SP10,DDATE
          IF        ASTAT=3
            CALL      RDDSCH1
          ENDIF
.
          MOVE      ADATE,SAVADATE
          MOVE      ADATE,EPIADATE           * Admis. date = Start Epiosde Date
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000               * missing master record
.
. ------- check whether a newborn --------------------------------------
.
          PACK      AGEDATE,ADATE
          REP       " 0",AGEDATE
.
          CALL      CALCAGE
.
. if age at admission date <= 9, then patient is a newborn
.
          MOVE      ZERO,NEWBORN
          COMPARE   NINE,PSAGEDAY
          IF        @LESS | @EQUAL
            MOVE      ONE,NEWBORN
          ENDIF 
.
          CALL      GUNE0000                 * Get unqualified newborn episodes
.
          MOVE      "999",ENDDAY
          PACK      KEY24,TEMPADMN,SP20
          CALL      RDSCHCO1                 * Positional read on PATCHC file
PROC2000  CALL      RDKCHCO1                 * Read next PATCHC record
          BRANCH    OVRCD,PROC2700           * End-of-file
.
PROC2600  MATCH     TEMPADMN,CHADMN          * Same Admission Number ?
          GOTO      PROC3000 IF EQUAL        * Yes.
.
PROC2700  MOVE      TRUE,LASTEPS             * Episode = Last
          CALL      HCSE0000                 * Check the HCS equv. are not same
          BRANCH    EXIT,PROC2000
.
          MOVE      NINES,CHDATE             * Needed for PRS2DAYS
          BRANCH    PTCNHOSP,PROC2800
.
          CALL      LMTD0000                 * Calc. Last Eps. for YTD L. Month
.
PROC2800  CALL      CMTD0000                 * Calc. Last Eps. for Current Month
          GOTO      PROC1000 
.
.         Only read changed in care class/type categories (CC)
.
PROC3000  MATCH     "CC",CHCATG                   
          GOTO      PROC2000 IF NOT EQUAL    * Read next PATCHC record
.
          PACK      KEY24,TEMPADMN,CHDATE,Z20
          CALL      RDSCHCO1                 * Position at last record for date
          CALL      RDPCHCO1                 * Read last care type for date
.
          MATCH     CHDATE,ADATE             * Change date on day of admission?
          GOTO      PROC2000 IF EQUAL        * Yes.
.
          CALL      HCSE0000                 * Check the HCS equv. are not same
          BRANCH    EXIT,PROC9000
.
          BRANCH    PTCNHOSP,PROC4500
.
          CALL      LMTD0000                 * No. Calc. Last Month YTD figures
          BRANCH    NCHC,PROC2000            * Read Next CHC Record 
.
PROC4500  CALL      CMTD0000                 * Calculate Current Month figures
          MOVE      CHDATE,EPIADATE
          BRANCH    NCHC,PROC2000            * Read Next CHC Record 
          BRANCH    NADM,PROC1000            * Read Next Admission Record
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
+
.******************************************************************************
.*                                 GUNE0000               Called by: PROC0000 *
.*                    Get The Unqualified Newborn Episodes                    *
.* Note: I had to use the episode routine out of IBAINP50 because I cannot    *
.* make the current PROC0000 routine extract unqualified newborn episodes. If *
.* you can make PROC0000 extract unqualified newborn episodes, you can remove *
.* GUNE0000, CNEX0000, GCTY0000 & CVUE0000.                                   *
.******************************************************************************
GUNE0000  COMPARE   ONE,PTCNHOSP
          GOTO      GUNE9999 IF NOT EQUAL   * Private hospital, exit
.
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * Read the discharge file
          BRANCH    OVRCD,GUNE3000
.
          PACK      EPSSDATE,ADATE          * New episode start date
          MOVE      AADMNO,CHADMN
          PACK      KEY24,CHADMN,SP30
          CALL      RDSCHCO1                * Position on the code changes file
GUNE1000  CALL      RDKCHCO1                * Read the next code changes record
          BRANCH    OVRCD,GUNE3000
.
          MATCH     AADMNO,CHADMN
          GOTO      GUNE3000 IF NOT EQUAL   * Different admission number, exit
.
          MATCH     "CC",CHCATG
          GOTO      GUNE3000 IF NOT EQUAL   * Different care class, exit
.
          UNPACK    CHDATE,EPSEDATE
          MATCH     EPSEDATE,ADATE
          GOTO      GUNE1000 IF EQUAL       * Ignore changes on admission date
.
          MATCH     EPSEDATE,EPSSDATE
          GOTO      GUNE1000 IF EQUAL       * Ignore changes on the same day  
.
          CALL      CNEX0000                * Check for change in care type
          BRANCH    EXIT,GUNE3000           * No more changes in care type 
.
          CALL      CVUE0000                * Check for a valid unqual episode
.
GUNE2000  MOVE      EPSEDATE,EPSSDATE       * Update start of next episode with
          GOTO      GUNE1000
.
. ----- Reached last episode for this patient -----
.
GUNE3000  MOVE      ACARE,CARECLSS          * Last care class
          MOVE      DDATE,CHDATE            * Last change date
          CALL      GCTY0000                * Get the care type
          CALL      CVUE0000                * Check for a valid unqual episode
GUNE9999  RETURN
+
.******************************************************************************
.*                                  CNEX0000              Called by: GUNE0000 *
.*                        Check For Change In Care Type                       *
.******************************************************************************
CNEX0000  PACK      SAVKEY24,CHADMN,CHDATE,CHTIME,SP20
          MOVE      CHCODE,CARECLSS
          CALL      GCTY0000                * Get the care type
          MOVE      CARECLSS,PREVCLSS
          MOVE      CARETYPE,PREVTYPE
.
. ----- Check whether care class has changed -----
.
          PACK      KEY24,CHADMN,CHDATE,Z30
          CALL      RDSCHCO1                * Position on & read the next codes
CNEX1000  CALL      RDKCHCO1                  changes file
          BRANCH    OVRCD,CNEX2000
.
          MATCH     AADMNO,CHADMN
          GOTO      CNEX2000 IF NOT EQUAL   * Different admission number, exit
.
          MATCH     "CC",CHCATG                   
          GOTO      CNEX2000 IF NOT EQUAL   * Not a care class record, exit
.
          MOVE      CHCODE,CARECLSS
          CALL      GCTY0000                * Get the care type
.
          MATCH     PREVTYPE,CARETYPE
          GOTO      CNEX1000 IF EQUAL       * HDP eq hasnt changed, get next
          GOTO      CNEX9000
.
. ----- Reached last episode for this patient -----
.
CNEX2000  MOVE      ACARE,CARECLSS          * Last care class
          MOVE      DDATE,CHDATE            * Last change date
          CALL      GCTY0000                * Get the care type
.
          MATCH     PREVTYPE,CARETYPE
          GOTO      CNEX9500 IF EQUAL       * HDP eq has changed, exit
.
CNEX9000  MOVE      ZERO,EXIT
          GOTO      CNEX9900
.
CNEX9500  MOVE      ONE,EXIT
.
. ----- Reposition back to the original record -----
.
CNEX9900  MOVE      SAVKEY24,KEY24
          CALL      RDCHCO1                 * Reposition on codes changes file
          MOVE      PREVTYPE,CARETYPE
          MOVE      PREVCLSS,CARECLSS
CNEX9999  RETURN
+
.******************************************************************************
.*                                  GCTY0000              Called by: GUNE0000 *
.*                              Get The Care Type                  & CNEX0000 *
.******************************************************************************
GCTY0000  MOVE      SP2,CARETYPE                                       *C-49016
          MATCH     SP3,CARECLSS
          GOTO      GCTY9000 IF EQUAL       * Blank care class, exit
.
          PACK      KEY5,ANSC,ANSC,CARECLSS
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,GCTY9000
.
          UNPACK    THCSCOD,CARETYPE,DIM2                              *C-49016
.
GCTY9000  MOVE      ZERO,EXIT
GCTY9999  RETURN
+
.******************************************************************************
.*                                 CVUE0000               Called by: GUNE0000 *
.*                   Check For A Valid Unqualified Episode                    *
.******************************************************************************
CVUE0000  MATCH     SDATCM,CHDATE                                           
          GOTO      CVUE9999 IF LESS        * Episode date < period start date
.
          MATCH     CHDATE,ENDRNG
          GOTO      CVUE9999 IF LESS        * Episode date > period end date
.
          CMATCH    ANSU,CARETYPE                                      *C-49016
          GOTO      CVUE9999 IF NOT EQUAL   * Not an unqualified episode
.
          ADD       ONE,UNQLNBEP            * Unqualified newborn episode
CVUE9999  RETURN
+
.******************************************************************************
.*                                  HCSE0000             CALLED BY : PROC0000 *
.*   Check HCS equivalent are not the same between code changes               *
.******************************************************************************
HCSE0000  MOVE      ZERO,SPCHCODE            * Init. Next CHC record to False 
.
          COMPARE   TRUE,LASTEPS             * Last Episode ?
          GOTO      HCSE2000 IF EQUAL        * Yes.
.
          PACK      KEY5,ANSC,ANSC,CHCODE    * No. Obtain original THCSCOD
          CALL      RDCODE1                  * 'CC' Code  on file ?
          BRANCH    OVRCD,HCSE3000           * No. Read next CHC record
.
          UNPACK    THCSCOD,KEY1,DIM1,DIM2                             *C-51565
.
          MATCH     "U",KEY1
          GOTO      HCSE0500 IF EQUAL       * Unqualified patient
.
          MATCH     "8",KEY1
          GOTO      HCSE3000 IF EQUAL       * Palliative patient
          MATCH     "9",KEY1
          GOTO      HCSE3000 IF EQUAL       * Geriatric patient
          MATCH     "5",KEY1
          IF        @EQUAL
            MOVE      DIM1,DIM1A                                       *I-51565
            REP       "E-T-K-G-S-A- -",DIM1A   * "5x" sub codes        *I-51565
            MATCH     "-",DIM1A                * acute sub-codes       *I-51565
            GOTO      HCSE3000 IF EQUAL       * Psych patient
          ENDIF                                                        *I-51565
.
HCSE0500  PACK      STHCSCOD,KEY1,DIM1                                 *C-51565
.
          CALL      RDPCHCO1                 * No. Read previous code
          BRANCH    OVRCD,HCSE3500           * End of file. Exit routine
.
HCSE1000  MATCH     TEMPADMN,CHADMN          * Same Admission Number ?
          GOTO      HCSE3500 IF NOT EQUAL    * No.
.
          PACK      KEY5,ANSC,ANSC,CHCODE    
          CALL      RDCODE1                  * 'CC' Code  on file ?
          BRANCH    OVRCD,HCSE3500           * No. Read next CHC record
.
          UNPACK    THCSCOD,KEY1,DIM1,DIM2                             *C-51565
.
          MATCH     "U",KEY1
          GOTO      HCSE1500 IF EQUAL       * Unqualified patient
.
          MATCH     "8",KEY1
          GOTO      HCSE3500 IF EQUAL       * Palliative patient
          MATCH     "9",KEY1
          GOTO      HCSE3500 IF EQUAL       * Geriatric patient
          MATCH     "5",KEY1
          IF        @EQUAL
            MOVE      DIM1,DIM1A                                       *I-51565
            REP       "E-T-K-G-S-A- -",DIM1A   * "5x" sub codes        *I-51565
            MATCH     "-",DIM1A                * acute sub-codes       *I-51565
            GOTO      HCSE3500 IF EQUAL       * Psych patient
          ENDIF                                                        *I-51565
.
HCSE1500  PACK      KEY2,KEY1,DIM1           * rebuild care type       *I-51565
          MATCH     KEY2,STHCSCOD            * Yes. Same codes ?       *C-51565
          GOTO      HCSE3500 IF NOT EQUAL    * No.
.
HCSE2000  MOVE      TEMPADMN,KEY8
          CALL      RDMISS1                  * Admn file has current care class
          BRANCH    OVRCD,HCSE3000           * Admn No, not on file
.
          PACK      KEY5,ANSC,ANSC,ACARE    
          CALL      RDCODE1                  * 'CC' Code  on file ?
          BRANCH    OVRCD,HCSE3000           * No. Read next CHC record
.
          MATCH     SP4,STHCSCOD
          GOTO      HCSE3000 IF EQUAL
.
          UNPACK    THCSCOD,KEY1,DIM1,DIM2                             *C-51565
.
          MATCH     "U",KEY1
          GOTO      HCSE2500 IF EQUAL       * Unqualified patient
.
          MATCH     "8",KEY1
          GOTO      HCSE3500 IF EQUAL       * Palliative patient
          MATCH     "9",KEY1
          GOTO      HCSE3500 IF EQUAL       * Geriatric patient
          MATCH     "5",KEY1
          IF        @EQUAL
            MOVE      DIM1,DIM1A                                       *I-51565
            REP       "E-T-K-G-S-A- -",DIM1A   * "5x" sub codes        *I-51565
            MATCH     "-",DIM1A                * acute sub-codes       *I-51565
            GOTO      HCSE3500 IF EQUAL       * Psych patient
          ENDIF                                                        *I-51565
.
HCSE2500  PACK      KEY2,KEY1,DIM1           * rebuild care type       *I-51565
          MATCH     KEY2,STHCSCOD            * Yes. Same codes ?       *C-51565
          GOTO      HCSE3500 IF NOT EQUAL    * No.
.
HCSE3000  MOVE      NEG1,SPCHCODE            * No seperation.
.
HCSE3500  CALL      RDKCHCO1                 * Repostion CHC Record.
          GOTO      HCSE9000
.
HCSE9000  MOVE      ZERO,EXIT
          GOTO      HCSE9999
.
HCSE9500  MOVE      ONE,EXIT
HCSE9999  RETURN
+
.******************************************************************************
.*                                  LMTD0000             CALLED BY : PROC0000 *
.*                Calculate Previous Month/quarter YTD figures                *
.******************************************************************************
LMTD0000  MOVE      FALSE,NADM               * Init. Next Admis. record to False
          MOVE      FALSE,NCHC               * Init. Next CHC record to False 
          MOVE      ZERO,LMSEP               * Init. YTD Last Month Separation
.
          COMPARE   TRUE,LASTEPS             * Last Episode ?
          GOTO      LMTD1000 IF EQUAL        * Yes.
.
.         Calculate the Actual Start of Episode Date
.
          MATCH     CHDATE,SFINYEAR          * Chng Date Before Start of Year?
          GOTO      LMTD1000 IF LESS         * No.
.
          MOVE      CHDATE,EPIADATE          * Yes. Update New Admission Date
          GOTO      LMTD9800                 *      Get next CHC record
.
.         Actual Episode date in the peroid required
.
LMTD1000  BRANCH    NEWFILE,LMTD6000
.
          MATCH     EPIADATE,EDATLM          * Is Act. Eps. Date > End of Period
          GOTO      LMTD9999 IF LESS         * Yes. Process current month
.
.         Calculate Period Start Date for this episode
.
          MATCH     SFINYEAR,EPIADATE        * Eps. before Start of Fin Yr.?
          GOTO      LMTD2000 IF LESS         * Yes.
          MOVE      EPIADATE,PRS2STRT        * No. Start Episode = Start
          GOTO      LMTD3000                 *     Calc peroid end date
.
LMTD2000  MOVE      SFINYEAR,PRS2STRT        * Start of Finanical Year = Start
.
.         Calculate Period End Date for this episode
.
LMTD3000  COMPARE   TRUE,LASTEPS             * Last Episode ?
          GOTO      LMTD4000 IF EQUAL        * Yes. 
          MATCH     CHDATE,EDATLM            * End Eps. after End last Mon date?
          GOTO      LMTD4000 IF LESS         * Yes.
          MOVE      CHDATE,PRS2END           * No. End Eps. = End 
          ADD       ONE,LMSEP                *     Yes. Add to sepeartion
          ADD       SPCHCODE,LMSEP           
          GOTO      LMTD5000                 * Process last month YTD figures
.
LMTD4000  MOVE      EDATLM,PRS2END           * End Last Month = End
          MATCH     PRS2END,SFINYEAR         * End range = Start financial year?
          GOTO      LMTD9999 IF EQUAL        * Yes. Do not process.
          CALL      LMDC0000                 * Check if discharged
          BRANCH    EXIT,LMTD9999
.
LMTD5000  MOVE      ONE,YRORMON              * Process last Month YTD figures
          CALL      PREP0000                 * Process this Episode
          GOTO      LMTD9999                 * Exit Routine
.
. July 2002/2003 HDP mods   
.
LMTD6000  MATCH     EPIADATE,EDATLM          * Is Act. Eps.Date>End of last mth
          GOTO      LMTD9999 IF LESS         * Yes. Process current month
.
          MOVE      EDATLMS,PRS2STRT
          COMPARE   TRUE,LASTEPS             * Last Episode ?
          GOTO      LMTD6500 IF EQUAL        * Yes. 
          MATCH     CHDATE,EDATLM            * End Eps. after End last Mon date?
          GOTO      LMTD6500 IF LESS         * Yes.
          MOVE      CHDATE,PRS2END           * No. End Eps. = End 
          ADD       ONE,LMSEP                *     Yes. Add to sepeartion
          ADD       SPCHCODE,LMSEP           
          GOTO      LMTD7000                 * Process last month YTD figures
.
LMTD6500  MOVE      EDATLM,PRS2END           * End Last Month = End
          MATCH     PRS2END,SFINYEAR         * End range = Start financial year?
          GOTO      LMTD9999 IF EQUAL        * Yes. Do not process.
          CALL      LMDC0000                 * Check if discharged
          BRANCH    EXIT,LMTD9999
.
LMTD7000  MOVE      ONE,YRORMON              * Process last Month YTD figures
          CALL      PREP0000                 * Process this Episode
          GOTO      LMTD9999                 * Exit Routine
.
LMTD9800  MOVE      TRUE,NCHC                * Read Next Change of Codes record
.
LMTD9999  RETURN
+
.******************************************************************************
.*                                  LMDC0000             CALLED BY : LMTD0000 *
.*                     Check if dishcarged within the month                   *
.******************************************************************************
LMDC0000  PACK      KEY8,TEMPADMN
          CALL      RDDSCH1                  * Discharged ?
          BRANCH    OVRCD,LMDC9000           * No. Exit = False
.
          MATCH     SFINYEAR,DDATE           * Yes. Dsch. before Start of C.Mon?
          GOTO      LMDC8000 IF LESS         *      Yes.
.
          MATCH     DDATE,EDATLM             * Disch. Before End of last Mon.?
          GOTO      LMDC9000 IF LESS         * No. Exit = True
          MOVE      DDATE,PRS2END            * Yes, DDate = End Date
.
          MATCH     CHDATE,DDATE             * Change Date & Disch Date Same ?
          GOTO      LMDC9000 IF EQUAL        * Yes.
          ADD       ONE,LMSEP                * No. Add 1 to Last Month Sep.
          GOTO      LMDC9000
.
LMDC8000  MOVE      TRUE,EXIT                * Discharge = True
          GOTO      LMDC9999                 * Exit Routine
.
LMDC9000  MOVE      FALSE,EXIT               * Discharge = False
          COMPARE   TRUE,LASTEPS             * Last episode ?
          GOTO      LMDC9999 IF EQUAL        * yes.
          ADD       ONE,LMSEP                * No. Add 1 to Last Month Sep.
          ADD       SPCHCODE,LMSEP           
.
LMDC9999  RETURN
+
.******************************************************************************
.*                                  CMTD0000             CALLED BY : PROC0000 *
.*                Calculate Current Month To date figures                     *
.******************************************************************************
CMTD0000  MOVE      FALSE,NADM               * Init. Next Admis. record to False
          MOVE      FALSE,NCHC               * Init. Next CHC record to False 
          MOVE      ZERO,CMSEP               * Init. Current Month Separation
.
          COMPARE   TRUE,LASTEPS             * Last Episode ?
          GOTO      CMTD1000 IF EQUAL        * Yes.
.
.         Calculate the Actual Start of Episode Date
.
CMTD0100  MATCH     SDATCM,CHDATE            * Chng Date Before Start of Month
          GOTO      CMTD1000 IF NOT LESS     * No.
.
          MOVE      CHDATE,EPIADATE          * Yes. Update Start of Episode Date
          GOTO      CMTD9800                 *      Get next CHC record
.
.         Actual Episode date in the peroid required
.
CMTD1000  MATCH     EPIADATE,ENDRNG          * Is Act. Eps. Date > End of Period
          GOTO      CMTD9900 IF LESS         * Yes. Process next Admission
.
          BRANCH    NEWFILE,CMTD6000
.
.         Calculate Period Start Date for this episode
.
          MATCH     SDATCM,EPIADATE          * Eps. Before Start of Current Mon?
          GOTO      CMTD2000 IF LESS         * Yes.
          MOVE      EPIADATE,PRS2STRT        * No. Start Episode = Start
          GOTO      CMTD3000                       Calc. period end date 
.
CMTD2000  MOVE      SDATCM,PRS2STRT          *          No. Start C. Mon = Start
.
.         Calculate Period End Date for this episode
.
CMTD3000  COMPARE   TRUE,LASTEPS             * Last Episode ?
          GOTO      CMTD4000 IF EQUAL        * Yes.
.
          MATCH     CHDATE,ENDRNG            * End Eps.after End Range date?
          GOTO      CMTD3500 IF LESS         * Yes.
          MOVE      CHDATE,PRS2END           * No. End Eps. = End 
          ADD       ONE,CMSEP                *     Yes. Add to Seperation
          ADD       SPCHCODE,CMSEP           
          GOTO      CMTD5000                 * Process Current month YTD figures
.
.         Change record after end date
.
CMTD3500  MOVE      ENDRNG,PRS2END           * End Last Month = End
          GOTO      CMTD5000
.
CMTD4000  MOVE      ENDRNG,PRS2END           * End Last Month = End
          CALL      CMDC0000                 * Check if discharged
          BRANCH    EXIT,CMTD9800 
.
CMTD5000  MOVE      TWO,YRORMON
          CALL      PREP0000                 * Process this Episode
          GOTO      CMTD9800
.
. July 2002/2003 mods
.
CMTD6000  MATCH     SDATCM,EPIADATE          * Eps. Before Start of Current Mon?
          GOTO      CMTD6050 IF LESS         * Yes.
          MOVE      EPIADATE,PRS2STRT        * No. Start Episode = Start
          GOTO      CMTD6100                       Calc. period end date 
.
CMTD6050  MOVE      SDATCM,PRS2STRT          *          No. Start C. Mon = Start
.
.         Calculate Period End Date for this episode
.
CMTD6100  COMPARE   TRUE,LASTEPS             * Last Episode ?
          GOTO      CMTD6500 IF EQUAL        * Yes.
.
          MATCH     CHDATE,ENDRNG            * End Eps.after End Range date?
          GOTO      CMTD6400 IF LESS         * Yes.
          MOVE      CHDATE,PRS2END           * No. End Eps. = End 
          ADD       ONE,CMSEP                *     Yes. Add to Seperation
          ADD       SPCHCODE,CMSEP           
          GOTO      CMTD7000                 * Process Current month YTD figures
.
.         Change record after end date
.
CMTD6400  MOVE      ENDRNG,PRS2END           * End Last Month = End
          GOTO      CMTD7000
.
CMTD6500  MOVE      ENDRNG,PRS2END           * End Last Month = End
          CALL      CMDC0000                 * Check if discharged
          BRANCH    EXIT,CMTD9800 
.
CMTD7000  MOVE      TWO,YRORMON
          CALL      PREP0000                 * Process this Month  
.
CMTD9800  MOVE      TRUE,NCHC                * Read Next Change of Codes record
          GOTO      CMTD9999                 * Exit routine.
.
CMTD9900  MOVE      TRUE,NADM                * Read Next Admission Record
.
CMTD9999  RETURN
+
.******************************************************************************
.*                                  CMDC0000             CALLED BY : CMTD0000 *
.*                           Calculate Last Episodes                          *
.******************************************************************************
CMDC0000  PACK      KEY8,TEMPADMN
          CALL      RDDSCH1                  * Discharged ?
          BRANCH    OVRCD,CMDC9000           * No. Exit = False
.
          MATCH     SDATCM,DDATE             * Yes. Dsch. before Start of C.Mon?
          GOTO      CMDC8000 IF LESS         * Yes.
.
          MATCH     DDATE,ENDRNG             * No. Disch. before End of Range.?
          GOTO      CMDC9000 IF LESS         * No. Exit = True
          MOVE      DDATE,PRS2END            * Yes, DDate = End Date
          MOVE      ZERO,SPCHCODE
.
          MATCH     CHDATE,DDATE             * Change Date & Disch Date Same ?
          GOTO      CMDC9000 IF EQUAL        * Yes.
          ADD       ONE,CMSEP                * No. Add 1 to Last Month Sep.
          GOTO      CMDC9000 
.
CMDC8000  MOVE      TRUE,EXIT                * Discharge = True
          GOTO      CMDC9999                 * Exit Routine
.
CMDC9000  MOVE      FALSE,EXIT               * Discharge = False
          COMPARE   TRUE,LASTEPS             * Last episode ?
          GOTO      CMDC9999 IF EQUAL        * yes.
          ADD       ONE,CMSEP                * No. Add 1 to Last Month Sep.
          ADD       SPCHCODE,CMSEP           
.
CMDC9999  RETURN
+
.*******************************************************************************
.*                             PREP0000                   CALLED BY : LMTD0000 *
.*                                                                    CMTD0000 *
.*       Get Bed Days & Seps for this episode into correct vars for just       *
.*       the period between start and end of current calcs.                    *
.*                                                                             *
.* Input parameter : PRS2STRT - Start of Current Calculation                   *
.*                   PRS2END  - End of Current Calculation                     *
.* Output parameter: FROMDATE - Start of calculated range                      *
.*                   TODATE   - End of calculated range                        *
.*******************************************************************************
PREP0000  MOVE      FALSE,EXIT
          MOVE      SP8,TADMN
          PACK      KEY30,TEMPADMN,PRS2STRT,Z30 
          CALL      RDSTRAN2                 * Obtain the current Admn Type
.
.         Check if Admn Type carried over from previous Episode
.
          CALL      RDPTRAN2                 * Read previous TRAN record 
          BRANCH    OVRCD,PREP1000           * end of file
.
          MATCH     TEMPADMN,TADMN           * Same Admission ?
          GOTO      PREP2000 IF EQUAL        * Yes. - Setup Admn Type
.
PREP1000  CALL      RDKTRAN2                 * Read next TRAN record
          BRANCH    OVRCD,PREP9000           * end of file
.
          MATCH     TEMPADMN,TADMN           * Same Admission ?
          GOTO      PREP8500 IF NOT EQUAL    * No.
.
PREP2000  MOVE      TATYPE,SATYPE1           * Yes, save Admn Type
          MOVE      PRS2STRT,FROMDATE        * Start of segment for episode
.
PREP3000  CALL      RDKTRAN2                 * Read next Tran record
          BRANCH    OVRCD,PREP8000           * end of file 
.
          MATCH     TEMPADMN,TADMN           * Same Admission ?
          GOTO      PREP8000 IF NOT EQUAL    * No. 
.
          MATCH     PRS2END,TDATE            * Yes. In range ?
          GOTO      PREP4000 IF LESS         *      Yes.
.
          MOVE      PRS2END,TODATE           *      No. End date = TODATE
.
PREP3100  PERFORM   YRORMON,PRYR0000,PRMO0000
          GOTO      PREP9000                 * Finished this Episode
.
PREP4000  MOVE      TATYPE,SATYPE2
          MATCH     SATYPE1,SATYPE2          * Same Admn. Type
          GOTO      PREP3000 IF EQUAL        * Yes. read next TRAN record
.
          MATCH     TDATE,FROMDATE           * New from date = Tran Date 
          GOTO      PREP3000 IF EQUAL        * Yes. Processed dates already
.
          MOVE      TDATE,TODATE             * No. 
.
          PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED,SP30
          PERFORM   YRORMON,PRYR0000,PRMO0000
          CALL      GUND0000                 * Get unqualified newborn days
          MOVE      TODATE,FROMDATE          * New from date = old todate
          MOVE      SATYPE2,SATYPE1          * New Account Class Current 
          MOVE      SAVKEY30,KEY30
          CALL      RDSTRAN2                 * Obtain the current Admn Type
          GOTO      PREP3000                 * Read Next Tran Record
.
PREP8000  MOVE      PRS2END,TODATE
          PERFORM   YRORMON,PRYR0000,PRMO0000
          GOTO      PREP9000
.
PREP8500  MOVE      TRUE,EXIT
PREP9000  CALL      GUND0000                 * Get unqualified newborn days
PREP9999  RETURN
+
.*******************************************************************************
.*                           PRYR0000                     CALLED BY : PREP0000 *
.*        Process Bed Days & Separation Updates to the appropriate PRS2        *
.*        variables for last Month year to date                                *
.*******************************************************************************
PRYR0000  MOVE      FALSE,EXIT
          MOVE      ZERO,NBRDAYS
          MOVE      ZERO,SAMEDAY
.
. ----- check for unqualified and acute patient -----
.
          PACK      KEY5,CODEA,SATYPE1
          CALL      RDCODE1
          IF        OVRCD=0
            CMATCH    ANSU,THCSCOD
            IF        @EQUAL
              CMATCH    ANSU,CARETYPE                                  *C-49016
              GOTO      PRYR1200 IF NOT EQUAL
              GOTO      PRYR9999 
            ENDIF
          ENDIF
.
.  Ignore Palliative, Geriatric, Psychiatric caretypes                         
.                                                                              
          CMATCH    "8",CARETYPE                                               
          GOTO      PRYR9999 IF EQUAL                                          
          CMATCH    "9",CARETYPE                                               
          GOTO      PRYR9999 IF EQUAL                                          
          CMATCH    "5",CARETYPE                                               
          GOTO      PRYR9999 IF EQUAL                                          
          CMATCH    "0",CARETYPE                                               
          GOTO      PRYR9999 IF EQUAL                                          
.
          MATCH     EDATLM,TODATE            * End Date of Last Month = Todate
          GOTO      PRYR0100 IF NOT EQUAL    * No.
.
          MOVE      EDATLM,PEREND
          CALL      STAY0000                 * Yes. Get next day if stayover Pat
PRYR0100  CALL      PRS2DAYS                 * Calculate Bed Days (NBRDAYS)
          MOVE      ZERO,SAMEDAY
          MATCH     EPIADATE,SAVADATE        * Changed on admission date
          GOTO      PRYR1000 IF NOT EQUAL    * No.
.
          PACK      KEY16,SAVADATE,TEMPADMN  * Yes. Pack Key Admn Date & Admn No
          CALL      RDDSCH2                  * Admit & Discharg on same day?
          BRANCH    OVRCD,PRYR1000           * No.   Continue 
.
          MOVE      ONE,SAMEDAY              * Yes.  Add to Same Day
.
PRYR1000  CALL      GACC0000                 * Get account status
          BRANCH    EXIT,PRYR9999
.
PRYR1200  BRANCH    PTCNHOSP,PRYR2000        * If Public Hospital Different
          CALL      WPRY0000                 * Write Private Hospital LMYTD Data
          GOTO      PRYR9999                 * Exit Routine
.
PRYR2000  CALL      WPUY0000                 * Write Public Hospital LMYTD Data
.
PRYR9999  RETURN
+
.*******************************************************************************
.*                           WPRY0000                     CALLED BY : PRYR0000 *
.*    Write to tempfile2 the Private Hosiptal Last month/quarter year to date  *
.*    data.                                                                    *
.*    NB : in PRS2CLAS, the private PRS2RACC variable, is broken down further  *
.*    than is required for this report. Therefore :                            *
.*    PRS2RACC = 1                === XREF = 1                                 *
.*    PRS2RACC = 2 = PRS2RACC = 3 === XREF = 2                                 *
.*    PRS2RACC = 4 = PRS2RACC = 5 === XREF = 3                                 *
.*    PRS2RACC = 6 = PRS2RACC = 7 === XREF = 4                                 *
.*    PRS2RACC = 8                === XREF = 5                                 *
.*    PRS2RACC = 9                === XREF = 6                                 *
.*******************************************************************************
WPRY0000  
          MOVE      PRS2RACC,FORM2
          MOVE      FORM2,XREF            * Move to XREF
.
          MOVE      XREF,FXREF
          LOAD      XREF,FXREF,ONE,TWO,TWO,THREE,THREE,FOUR,FOUR,FIVE,SIX
.
          MOVE      XREF,FORM2
          MOVE      FORM2,XREF
.
          PACK      KEY2,XREF                * Pack Key
          CALL      RDTEMPA2                 * Record for account class exist ?
          BRANCH    OVRCD,WPRY9000           * No. Write new record
.
          ADD       LMSEP,XLMYTDS            * Yes. Add to Separations
.
. ------- only count separation once (ie. may have many adm types within eps)
.
          ADD       NBRDAYS,XLMYTDB          *      Add to Bed Days
.
          COMPARE   ONE,SAMEDAY              *      Same Day Patient ?
          GOTO      WPRY1000 IF NOT EQUAL    *      No. Exit routine
          ADD       ONE,XLMYTDD              *      Yes. Add to Same Day Patient
          ADD       ONE,XLMYTDB              *           Add to Bed Days
.
WPRY1000  CALL      UPTEMPA2                 *      Update Record.
          MOVE      ZERO,LMSEP               * initialise no. of seps
          GOTO      WPRY9999                 *      Exit Routine
.
.         Previously a record does not exist. Write new record.
.
WPRY9000  MOVE      LMSEP,XLMYTDS            * Init. Seperations
.
. ------- only count separation once (ie. may have many adm types within eps)
.
          MOVE      NBRDAYS,XLMYTDB          * Init  Number of bed days
          COMPARE   ONE,SAMEDAY              * Same Day Patient ?
          GOTO      WPRY9100 IF NOT EQUAL    * No.  Write record 
          MOVE      ONE,XLMYTDD              * Yes. Add to Same Day Patient
          ADD       ONE,XLMYTDB              *      Add to Number of bed days
.  
WPRY9100  CALL      WRTEMPA2                 * Write Record.
          MOVE      ZERO,LMSEP               * initialise no. of seps
.
WPRY9999  RETURN
+
.*******************************************************************************
.*                           WPUY0000                     CALLED BY : PRYR0000 *
.* Write to tempfile2 the Public Hosiptal last month/quarter Year-To-Date data *
.*******************************************************************************
WPUY0000  BRANCH    LASTEPS,WPUY0050         * last episode
          MOVE      ZERO,LMSEP               * initialise the separation
.
WPUY0050  MOVE      PRS2RACC,FORM2
.
          BRANCH    FORM2,WPUY0100,WPUY0100,WPUY0200,WPUY0200,WPUY0200:
                          WPUY0200,WPUY0200,WPUY0200,WPUY0200,WPUY0200
.
WPUY0100  LOAD      XREF,FORM2,TWO,ONE       * Swap 1 to 2, 2 to 1 
          MOVE      XREF,FORM2
.
WPUY0200  MOVE      FORM2,XREF               * Move to XREF
.
          PACK      KEY2,XREF                * Pack Key
          CALL      RDTEMPA2                 * Record for account class exist ?
          BRANCH    OVRCD,WPUY9000           * No. Write new record
.
          ADD       LMSEP,XLMYTDS            * Yes. Add to Seperations
.
. ------- only count separation once (ie. may have many adm types within eps)
.
          ADD       NBRDAYS,XLMYTDB          *      Add to Bed Days
.
          COMPARE   ONE,SAMEDAY              *      Same Day Patient ?
          GOTO      WPUY1000 IF NOT EQUAL    *      No. Exit routine
          ADD       ONE,XLMYTDD              *      Yes. Add to Same Day Patient
          ADD       ONE,XLMYTDB              *      Add to Bed Days
.
WPUY1000  CALL      UPTEMPA2                 *      Update Record.
          MOVE      ZERO,LMSEP               * initialise no. of seps
          GOTO      WPUY9999                 *      Exit Routine
.
.         Previously a record does not exist. Write new record.
.
WPUY9000  MOVE      LMSEP,XLMYTDS            * Init. Number of Sepeartions 
.
. ------- only count separation once (ie. may have many adm types within eps)
.
          MOVE      NBRDAYS,XLMYTDB          * Init. Number of bed days
          COMPARE   ONE,SAMEDAY              * Same Day Patient ?
          GOTO      WPUY9100 IF NOT EQUAL    * No.
          MOVE      ONE,XLMYTDD              * Yes.  Init. Same day Patients
          MOVE      ONE,XLMYTDB              *       Add 1 to Number of bed days
.
WPUY9100  CALL      WRTEMPA2                 * Write Record.
          MOVE      ZERO,LMSEP               * initialise no. of seps
.
WPUY9999  RETURN
+
.******************************************************************************
.*                                  GACC0000              Called by: PRYR0000 *
.*                           Get The Account Status                & PRMO0000 *
.******************************************************************************
GACC0000  MOVE      ZERO,HCSFLAG            * HCS flag - no
          MOVE      SATYPE1,ADMTYPE
          MATCH     SP8,DDATE
          GOTO      GACC0500 IF EQUAL       * Patient discharged
.
          MATCH     FROMDATE,TODATE
          GOTO      GACC1000 IF NOT EQUAL   * Not a daycase
.
          MOVE      "  0",ENDDAY            * Daycase rate
          GOTO      GACC1500
.
GACC0500  MATCH     TODATE,ADATE
          GOTO      GACC1500 IF EQUAL       * Admin date = transfer date
.
GACC1000  MATCH     "  0",ENDDAY
          GOTO      GACC1500 IF NOT EQUAL   * Not daycase rate, continue
.
          MOVE      "  1",ENDDAY            * Non daycase rate
.
GACC1500  PACK      KEY9,ACLAIM,ADMTYPE,ENDDAY
          CALL      RDPTACC1                * Read an account status file record
          COMPARE   ONE,OVRCD
          GOTO      GACC2000 IF NOT EQUAL   * Record exists
.
          CALL      RKPTACC1                * Read an account status file record
          BRANCH    OVRCD,GACC3000
.
          MATCH     ACLAIM,PTASCLAM
          GOTO      GACC3000 IF NOT EQUAL   * Different claim type
.
          MATCH     ADMTYPE,PTASATYP
          GOTO      GACC3000 IF NOT EQUAL   * Different admission type
.
GACC2000  MOVE      PTASACCS,PRS2IACC       * Default current year acc status
          MATCH     SP3,PTASPRAC
          GOTO      GACC2500 IF EQUAL       * No previous year account status
.
.         UNPACK    TODATE,KEY4,KEY4
.         MATCH     "0701",KEY4
.         GOTO      GACC2500 IF NOT LESS    * Change date >= 01/07
.
          MATCH     "19990701",TODATE
          GOTO      GACC2500 IF NOT LESS
.
          MOVE      PTASPRAC,PRS2IACC       * Using previous year account status
.
GACC2500  MATCH     SP3,PRS2IACC
          GOTO      GACC6000 IF NOT EQUAL   * Account status not blank
.
GACC3000  BRANCH    HCSFLAG,GACC3500        * Already validated with HCS code
.
          PACK      KEY5,ANSA,SP1,ADMTYPE
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,GACC3500
.
          MATCH     SP4,THCSCOD
          GOTO      GACC3500 IF EQUAL       * Blank HCS eq
.
          UNPACK    THCSCOD,ADMTYPE         * Try with HCS code
          MOVE      ONE,HCSFLAG             * HCS flag - yes
          GOTO      GACC2000
.
GACC3500  PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,GACC5000
.
          MOVE      ZERO,FORM2
GACC4000  ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      GACC5000 IF NOT LESS    * Counter >= 6
.
          LOAD      ANS,FORM2,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
          CMATCH    "W",ANS
          GOTO      GACC4500 IF EQUAL       * WC patient
.
          CMATCH    "M",ANS
          GOTO      GACC4000 IF NOT EQUAL   * Not a TAC patient
.
          MOVE      "ST ",PRS2IACC
          MATCH     "19990701",EPIADATE
          IF        !@LESS
            MOVE      "TA ",PRS2IACC
          ENDIF
          GOTO      GACC6000
.
GACC4500  MOVE      "SA ",PRS2IACC
          MATCH     "19990701",EPIADATE
          IF        !@LESS
            MOVE      "WC ",PRS2IACC
          ENDIF
          GOTO      GACC6000
.
GACC5000  BRANCH    PTCNHOSP,GACC5500
.
          MOVE      "XX ",PRS2IACC
          GOTO      GACC6000
.
GACC5500  MOVE      "HE ",PRS2IACC
          MATCH     "19990701",EPIADATE
          IF        !@LESS
            MOVE      "ME ",PRS2IACC
          ENDIF
.
GACC6000  MOVE      ZERO,PRS2RACC
          CALL      PRS2CLAS                 * Calculate account class
          BRANCH    EXIT,GACC9500
.
GACC9000  MOVE      ZERO,EXIT
          GOTO      GACC9999
.
GACC9500  MOVE      ONE,EXIT
GACC9999  RETURN
.
.*******************************************************************************
.*                            PRMO0000                    CALLED BY : PREP0000 *
.*      Process Bed Days & Separation Updates to the appropriate PRS2          *
.*      variables for Current Month/Qtr.                                       *
.*******************************************************************************
PRMO0000  MOVE      FALSE,EXIT
          MOVE      ZERO,NBRDAYS
          MOVE      ZERO,SAMEDAY
.
. ----- check for unqualified and acute patient -----
.
          PACK      KEY5,CODEA,SATYPE1
          CALL      RDCODE1
          IF        OVRCD=0
            CMATCH    ANSU,THCSCOD
            IF        @EQUAL
              CMATCH    ANSU,CARETYPE                                  *C-49016
              GOTO      PRMO1200 IF NOT EQUAL
              GOTO      PRMO9999 
            ENDIF
          ENDIF
.
          CMATCH    "8",CARETYPE
          GOTO      PRMO9999 IF EQUAL
          CMATCH    "9",CARETYPE
          GOTO      PRMO9999 IF EQUAL
          CMATCH    "5",CARETYPE
          GOTO      PRMO9999 IF EQUAL
          CMATCH    "0",CARETYPE
          GOTO      PRMO9999 IF EQUAL
.
          MATCH     ENDRNG,TODATE            * End Date of Cur. Month = Todate
          GOTO      PRMO0100 IF NOT EQUAL    * No.
.
          MOVE      ENDRNG,PEREND
          CALL      STAY0000                 * Yes. Get next day if stayover Pat
PRMO0100  CALL      PRS2DAYS                 * Calculate Bed Days (NBRDAYS)
          MOVE      ZERO,SAMEDAY
          MATCH     EPIADATE,SAVADATE        * Change on Admision date ?
          GOTO      PRMO1000 IF NOT EQUAL    * No.
.
          PACK      KEY16,SAVADATE,TEMPADMN  * Yes. Pack Key Admn Date & Admn No
          CALL      RDDSCH2                  * Admit & Discharg on same day?
          BRANCH    OVRCD,PRMO1000           * No.   Continue 
.
          MOVE      ONE,SAMEDAY              * Yes.  Add to Same Day
.
PRMO1000  CALL      GACC0000                 * Get account status
          BRANCH    EXIT,PRMO9999
.
PRMO1200  BRANCH    PTCNHOSP,PRMO2000        * If Public Hospital Different
          CALL      WPRM0000                 * Write Priv. current Mon/Qtr data
          GOTO      PRMO9999                 * Exit Routine
.
PRMO2000  CALL      WPUM0000                 * Write Pub. current Mon/Qtr data
.
PRMO9999  RETURN
+
.******************************************************************************
.*                                  GUND0000              Called by: PREP0000 *
.*                        Get Unqualified Newborn Days                        *
.******************************************************************************
GUND0000  COMPARE   ONE,PTCNHOSP
          GOTO      GUND9999 IF NOT EQUAL   * Private hospital, exit
.
          PACK      KEY5,ANSA,SP1,SATYPE1
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,GUND9999
.
          CMATCH    ANSU,THCSCOD
          GOTO      GUND9999 IF NOT EQUAL   * Not unqualified newborn, exit
.
          MATCH     ENDRNG,TODATE           * End date of current month = todate
          IF        @EQUAL
            MOVE      ENDRNG,PEREND
            CALL      STAY0000              * Get next day if stayover patient
          ENDIF
.
          MATCH     DDATE,ADATE
          IF        @EQUAL
            MOVE      ONE,NBRDAYS           * Daycase, add 1 day
          ELSE
            CALL      PRS2DAYS              * Calculate bed days
          ENDIF
.
          ADD       NBRDAYS,UNQLNBDY        * Unqualified newborn days
.
GUND9999  RETURN
+
.*******************************************************************************
.*                           WPRM0000                     CALLED BY : PRYM0000 *
.*    Write to tempfile2 the Private Hosiptal Current month/quarter data       *
.*    NB : in PRS2CLAS, the private PRS2RACC variable, is broken down further  *
.*    than is required for this report. Therefore :                            *
.*    PRS2RACC = 1                === XREF = 1                                 *
.*    PRS2RACC = 2 = PRS2RACC = 3 === XREF = 2                                 *
.*    PRS2RACC = 4 = PRS2RACC = 5 === XREF = 3                                 *
.*    PRS2RACC = 6 = PRS2RACC = 7 === XREF = 4                                 *
.*    PRS2RACC = 8                === XREF = 5                                 *
.*    PRS2RACC = 9                === XREF = 6                                 *
.*******************************************************************************
WPRM0000  MOVE      PRS2RACC,FORM2
          MOVE      FORM2,XREF            * Move to XREF
.
          STRIP     XREF
          MOVE      XREF,FXREF
          LOAD      XREF,FXREF,ONE,TWO,TWO,THREE,THREE,FOUR,FOUR,FIVE,SIX
.
          MOVE      XREF,FORM2
          MOVE      FORM2,XREF
.
          PACK      KEY2,XREF                * Pack Key
          CALL      RDTEMPA2                 * Record for account class exist ?
          BRANCH    OVRCD,WPRM9000           * No. Write new record
.
          ADD       CMSEP,XCMSEPS            * Yes. Add to Separations
.
. ------- only count separation once (ie. may have many adm types within eps)
.
          ADD       NBRDAYS,XCMBDAY          *      Add to Bed Days
.
          COMPARE   ONE,SAMEDAY              *      Same Day Patient ?
          GOTO      WPRM1000 IF NOT EQUAL    *      No. Exit routine
          ADD       ONE,XCMSAME              *      Yes. Add to Same Day Patient
          ADD       ONE,XCMBDAY              *      Add to Bed Days
.
WPRM1000  CALL      UPTEMPA2                 *      Update Record.
          MOVE      ZERO,CMSEP               * initialise no. of seps
          GOTO      WPRM9999                 *      Exit Routine
.
.         Previously a record does not exist. Write new record.
.
WPRM9000  MOVE      CMSEP,XCMSEPS            * Init  Number of Seperations 
.
. ------- only count separation once (ie. may have many adm types within eps)
.
          MOVE      NBRDAYS,XCMBDAY          * Init  Number of bed days
          COMPARE   ONE,SAMEDAY              * Same Day Patient ?
          GOTO      WPRM9100 IF NOT EQUAL    * No.  Write record 
          MOVE      ONE,XCMSAME              * Yes. Init Same Day Patient
          MOVE      ONE,XCMBDAY              *      Add 1 Number of bed days
.
WPRM9100  CALL      WRTEMPA2                 * Write Record.
          MOVE      ZERO,CMSEP               * initialise no. of seps
.
WPRM9999  RETURN
+
.*******************************************************************************
.*                           WPUM0000                     CALLED BY : PRYM0000 *
.* Write to tempfile2 the Public Hosiptal Current month/quarter data           *
.*******************************************************************************
WPUM0000  BRANCH    LASTEPS,WPUM0050         * last episode
          MOVE      ZERO,CMSEP               * initialise the separation
.
WPUM0050  MOVE      SP10,XREF
          MOVE      PRS2RACC,FORM2
.
          BRANCH    FORM2,WPUM0100,WPUM0100,WPUM0200,WPUM0200,WPUM0200:
                          WPUM0200,WPUM0200,WPUM0200,WPUM0200,WPUM0200
.
WPUM0100  LOAD      XREF,FORM2,TWO,ONE       * Swap 1 to 2, 2 to 1 
          MOVE      XREF,FORM2
.
WPUM0200  MOVE      FORM2,XREF               * Move to XREF
          PACK      KEY2,XREF                * Pack Key
          CALL      RDTEMPA2                 * Record for account class exist ?
          BRANCH    OVRCD,WPUM9000           * No. Write new record
.
          ADD       CMSEP,XCMSEPS           * Yes. Add to Separations
.
. ------- only count separation once (ie. may have many adm types within eps)
.
          ADD       NBRDAYS,XCMBDAY          *      Add to Bed Days
.
          COMPARE   ONE,SAMEDAY              *      Same Day Patient ?
          GOTO      WPUM1000 IF NOT EQUAL    *      No. Exit routine
          ADD       ONE,XCMSAME              *      Yes. Add to Same Day Patient
          ADD       ONE,XCMBDAY              *      Add to Bed Days
.
WPUM1000  CALL      UPTEMPA2                 *      Update Record.
          MOVE      ZERO,CMSEP               * initialise no. of seps
          GOTO      WPUM9999                 *      Exit Routine
.
.         Previously a record does not exist. Write new record.
.
WPUM9000  MOVE      CMSEP,XCMSEPS            * Init. Number of seperations
.
. ------- only count separation once (ie. may have many adm types within eps)
.
          MOVE      NBRDAYS,XCMBDAY          * Init. Number of bed days
          COMPARE   ONE,SAMEDAY              * Same Day Patient ?
          GOTO      WPUM9100 IF NOT EQUAL    * No.
          MOVE      ONE,XCMSAME              * Yes. 
          MOVE      ONE,XCMBDAY              *      Add 1 to Number of bed days
.
WPUM9100  CALL      WRTEMPA2                 * Write Record.
          MOVE      ZERO,CMSEP               * initialise no. of seps
.
WPUM9999  RETURN
+
.*******************************************************************************
.*                           STAY0000                     CALLED BY : PRYR0000 *
.*        Add one day to TODATE   to consider a patient still admitted after   *
.*        the end of period date                                               *
.*        Only add a day if they didn't change care type on the last day of    *
.*        period as this is a new episode of 1 day.                            *
.*        Parameters : PEREND - period end date (CCYYMMDD).                    *
.*******************************************************************************
STAY0000  
.
. ------- don't add a day if discharged on last day of period --------------
.
          MOVE      SP8,DDATE
          PACK      KEY8,TEMPADMN,SP10  
          CALL      RDDSCH1                  * Admit & Discharg on same day?
          BRANCH    OVRCD,STAY0500           * No.   Continue 
.
          MATCH     DDATE,PEREND
          GOTO      STAY9999 IF EQUAL
.
. ------- don't add a day if changed care type (episode) on last day of period
.
STAY0500  COMPARE   TRUE,LASTEPS             * Last Episode ?
          GOTO      STAY1000 IF EQUAL        * Yes.
.
          MATCH     PEREND,CHDATE
          GOTO      STAY9999 IF EQUAL
.
STAY1000  UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY 
.
          MOVE      CCENT,CC                 * Initialise DMYCON var
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                   * convert to julien format
.
          ADD       ONE,JULDAY               * Add one to the Julien day
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON                  * convert back to CC,YY,MM,DD format
.
          PACK      TODATE,CC,YY,MM,DD       * Reformat TODATE
          REP       " 0",TODATE
.
STAY9999  RETURN
+
.*******************************************************************************
.*                              HPUB0000                  CALLED BY : ML2000  *
.*                Print header for the Public Hospital                        *
.------------------------------------------------------------------------------*
.               ACUTE HEALTH SERVICES PROGRAM - ADMITTED PATIENTS              |
.            PUBLIC HOSPITAL MONTHLY RETURN - Funding Program 306 S1           |
.                                                                              |
. Hospital: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                                |
.                                                                              |
. Campus:   xxxxxxxxxx   Campus Code: xxxxxx   Month: xxxxxxxxx   Year: xxxx   |
.                                                                              |
.------------------------------------------------------------------------------*
.                                                            |  Current Month  |
.------------------------------------------------------------------------------*
.*                                                                            *
.******************************************************************************
HPUB0000  DISPLAY   *P1:24,*EL,"Generating Report"
          UNPACK    ENDRNG,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,FCMON
          LOAD      MONTH,FCMON,MONTH1,MONTH2,MONTH3,MONTH4,MONTH5,MONTH6:
                                MONTH7,MONTH8,MONTH9,MONTH10,MONTH11,MONTH12
          PRINT     *F 
          CALL      UND80
          PRINT     *1,"|",*17,"ACUTE HEALTH SERVICES PROGRAM - ADMITTED ":
                    "PATIENTS",*80,"|",*N:
                    *1,"|",*14,"PUBLIC HOSPITAL MONTHLY RETURN - Funding ":
                    "Program 306 S1",*80,"|",*N:
                    *1,"|",*80,"|",*N:
                    *1,"|",*3,"Hospital: ",CNAME,*80,"|",*N:
                    *1,"|",*80,"|",*N:
                    *1,"|",*3,"Campus:",*26,"Campus Code: ",CFILENO:
                    *48,"Month: ",MONTH,*67,"Year: ",CCENT,CYEAR,*80,"|",*N:
                    *1,"|",*80,"|",*N;
          CALL      UND80
          PRINT     *1,"|",*62,"|  Current Month":
                    *80,"|"
          CALL      UND80
HPUB9999  RETURN
+
.*******************************************************************************
.*                              PPUB0000                  CALLED BY : ML2000   *
.*                Print the details for the public hospital                    *
.*-----------------------------------------------------------------------------*
.* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxxxxxxx     |     xxxxxx     |*
.* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxxxxxxx     |     xxxxxx     |*
.* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx    xxxxxxxxxx     |     xxxxxx     |*
.*-----------------------------------------44-------53-------62-------71-------*
.*                                                                             *
.*******************************************************************************
PPUB0000  MOVE      ZERO,NUMBER 
.
          MOVE      "1",PSECTION             * Printing Section One
          CALL      PUSP0000                 * Print Public Seperations 
.
          MOVE      "2",PSECTION             * Printing Section Two
          CALL      PUBD0000                 * Print Public Bed days  
.
          MOVE      "3",PSECTION             * Printing Section Three
          CALL      PUSD0000                 * Print Public Same Day Seperations
.
          MOVE      "4",PSECTION             * Printing Section Four
          CALL      PUUN0000                 * Print Public Unqualified Newborn
.
          CALL      PUTL0000                 * Print Tail section
.
PPUB9999  RETURN
+
.*******************************************************************************
.*                              PUSP0000                  CALLED BY : PPUB0000 *
.*      Public Hospital : Print section 1 - Seperations (Include Same Day)     *
.*******************************************************************************
PUSP0000  PACK      KEY2,SP2
          CALL      RSTEMPA2                 * Positional Read TempFile 2
. 
          PRINT     *1,"| SEPARATIONS (Includes Same Day)":
                    *62,"|",*80,"|"
.
PUSP1000  CALL      RKTEMPA2                 * Read next Tempfile 2 Record
          BRANCH    OVRCD,PUSP9999           * End of file exit routine
.
          STRIP     XREF
          MOVE      XREF,FXREF               * XREF to a form XREF
          LOAD      PRNTDESC,FXREF,PUBREF01,PUBREF02,PUBREF03,PUBREF04
.
          MOVE      XCMSEPS,TOTSEPS          * Add Total Seperations YTD 
          ADD       XLMYTDS,TOTSEPS          
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          COMPARE   ONE,NUMBER               * 1st Number ?
          GOTO      PUSP1100 IF NOT EQUAL    * No. So do not print "Acute"
.
          PRINT     *1,"| ACUTE",*14,NUMBER,*17,PRNTDESC,*50,S1NUM:
                    *62,"| ",*67,XCMSEPS,*80,"|"
          GOTO      PUSP1200                 * Add subtotals
.
PUSP1100  PRINT     *1,"|",*14,NUMBER,*17,PRNTDESC,*50,S1NUM:
                    *62,"| ",*67,XCMSEPS,*80,"|"
.
PUSP1200  ADD       XCMSEPS,ST1P1CC           * Subtotal Section 1 Part 1 Col. C
.
.         Write to file the YTD Advised for Previous Month
.
PUSP1300  COMPARE   FOUR,FXREF
          GOTO      PUSP1000 IF LESS
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          PRINT     *1,"|",*14,NUMBER,*17,"Sub Total (=1 to 4)":
                    *50,S1NUM,*62,"| ",*67,ST1P1CC,*80,"|"
.
PUSP2000  CALL      RKTEMPA2
          BRANCH    OVRCD,PUSP9999
.
          STRIP     XREF
          MOVE      XREF,FXREF               * XREF to a form XREF
          LOAD      PRNTDESC,FXREF,NOTHING,NOTHING,NOTHING,NOTHING:
                                   PUBREF05,PUBREF06,PUBREF07,PUBREF08:
                                   PUBREF09,PUBREF10
.
          MOVE      XCMSEPS,TOTSEPS          * Add Total Seperations YTD 
          ADD       XLMYTDS,TOTSEPS          
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          COMPARE   SIX,NUMBER               * 6th Number ?
          GOTO      PUSP2100 IF NOT EQUAL    * No. So do not print "Non-Acute"
.
          PRINT     *1,"| NON-ACUTE",*14,NUMBER,*17,PRNTDESC,*50,S1NUM:
                    *62,"| ",*67,XCMSEPS,*80,"|"
          GOTO      PUSP2200                 * Add subtotals
.
PUSP2100  PRINT     *1,"|",*14,NUMBER,*17,PRNTDESC,*50,S1NUM:
                    *62,"| ",*67,XCMSEPS,*80,"|"
.
PUSP2200  ADD       XCMSEPS,ST1P2CC          * Subtotal Sect. 1 Part 2 Col. C
.
PUSP2300  COMPARE   TEN,FXREF
          GOTO      PUSP2000 IF LESS
.
.         Print Subtotals for Part 2
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          PRINT     *1,"|",*14,NUMBER,*17,"Sub Total (=6 to 11)":
                    *50,S1NUM:
                    *62,"| ",*67,ST1P2CC,*80,"|"
.
          MOVE      ST1P2CC,T1CC
          ADD       ST1P1CC,T1CC              * Total Section 1 Column C
.
.         Print Totals for seperations
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          PRINT     *1,"|",*14,NUMBER,*17,"TOTAL (=5+12)":
                    *50,S1NUM,*62,"| ",*67,T1CC,*80,"|"
.
PUSP9999  RETURN
+
.*******************************************************************************
.*                              PUBD0000                  CALLED BY : PPUB0000 *
.*      Public Hospital : Print section 2 - Bed Day (Include Same Day)         *
.*******************************************************************************
PUBD0000  PACK      KEY2,SP2
          CALL      RSTEMPA2                 * Positional Read
.
          CALL      UND80
          PRINT     *1,"| PATIENT DAYS (Includes Same Day)":
                    *62,"|",*80,"|"
.
PUBD1000  CALL      RKTEMPA2                 * Read next record
          BRANCH    OVRCD,PUBD9999
.
          STRIP     XREF
          MOVE      XREF,FXREF               * XREF to a form XREF
          LOAD      PRNTDESC,FXREF,PUBREF01,PUBREF02,PUBREF03,PUBREF04
.
          MOVE      XCMBDAY,TOTBDAY          * Calcalate YTD bed days
          ADD       XLMYTDB,TOTBDAY
.
.         Print Part 2 details XREF field 1 - 4
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          COMPARE   TEN4,NUMBER              * 14th Number ?
          GOTO      PUBD1100 IF NOT EQUAL    * No. So do not print "Acute"
.
          PRINT     *1,"| ACUTE",*14,NUMBER,*17,PRNTDESC,*50,S1NUM:          
                    *62,"| ",*67,XCMBDAY,*80,"|"
          GOTO      PUBD1200                 * Add Subtotals
.
PUBD1100  PRINT     *1,"|",*14,NUMBER,*17,PRNTDESC,*50,S1NUM:        
                    *62,"| ",*67,XCMBDAY,*80,"|"
.
PUBD1200  ADD       XCMBDAY,ST2P1CC          * SubTotal Section 2 Part 1 Col. C
.
PUBD1300  COMPARE   FOUR,FXREF
          GOTO      PUBD1000 IF LESS
.
.         Print Part 1 Subtotal details  XREF field 1 - 4 
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          PRINT     *1,"|",*14,NUMBER,*17,"Sub Total (=14 to 17)":
                    *50,S1NUM,*62,"| ",*67,ST2P1CC:
                    *80,"|"
.
PUBD2000  CALL      RKTEMPA2                 * Read next record 
          BRANCH    OVRCD,PUBD9999
.
          STRIP     XREF
          MOVE      XREF,FXREF               * XREF to a form XREF
          LOAD      PRNTDESC,FXREF,NOTHING,NOTHING,NOTHING,NOTHING:
                                   PUBREF05,PUBREF06,PUBREF07,PUBREF08:
                                   PUBREF09,PUBREF10
.
          MOVE      XCMBDAY,TOTBDAY          * Calcalate YTD bed days
          ADD       XLMYTDB,TOTBDAY
.
.         Print Part 2 details XREF field 5 -10
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          COMPARE   TEN9,NUMBER              * 19th Number ?
          GOTO      PUBD2100 IF NOT EQUAL    * No. So do not print "Non-Acute"
.
          PRINT     *1,"| NON-ACUTE",*14,NUMBER,*17,PRNTDESC,*50,S1NUM:
                    *62,"| ",*67,XCMBDAY,*80,"|"
          GOTO      PUBD2200                 * Add Subtotals
.
PUBD2100  PRINT     *1,"|",*14,NUMBER,*17,PRNTDESC,*50,S1NUM:
                    *62,"| ",*67,XCMBDAY,*80,"|"
.
PUBD2200  ADD       XCMBDAY,ST2P2CC          * SubTotal Sect. 2 Part 2 Col. C
.
PUBD2300  COMPARE   TEN,FXREF
          GOTO      PUBD2000 IF LESS
.
.         Print Part 2 Subtotal details  XREF field 5 - 10 
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          PRINT     *1,"|",*14,NUMBER,*17,"Sub Total(=19 to 24)":
                    *50,S1NUM,*62,"| ",*67,ST2P2CC:
                    *80,"|"
.
          MOVE      ST2P2CC,T2CC                 
          ADD       ST2P1CC,T2CC              * Total Section 2 Column C
.
.         Print total details for Section 2
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          PRINT     *1,"| ",*14,NUMBER,*17,"TOTAL (=18+25)":
                    *50,S1NUM,*62,"| ",*67,T2CC:
                    *80,"|"
PUBD9999  RETURN
+
.*******************************************************************************
.*                              PUSD0000                  CALLED BY : PPUB0000 *
.*      Public Hospital : Print section 3 - Same Day Sepeartions               *
.*******************************************************************************
PUSD0000  CALL      UND80
          PACK      KEY2,SP2
          CALL      RSTEMPA2                 * Positional Read
.
          PRINT     *1,"| SAME DAY SEPARATIONS":
                    *62,"|",*80,"|"
.
PUSD1000  CALL      RKTEMPA2                 * Read next record
          BRANCH    OVRCD,PUSD9999
.
          STRIP     XREF
          MOVE      XREF,FXREF               * XREF to a form XREF
          LOAD      PRNTDESC,FXREF,PUBREF11,PUBREF12,PUBREF13,PUBREF14
.
          MOVE      XCMSAME,TOTSAME          * Calcalate YTD bed days
          ADD       XLMYTDD,TOTSAME
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          PRINT     *1,"|",*14,NUMBER,*17,PRNTDESC,*50,S1NUM:
                    *62,"| ",*67,XCMSAME,*80,"|"
.
          ADD       XCMSAME,T3CC             * Total Section 3 Column C
.
PUSD1200  COMPARE   FOUR,FXREF
          GOTO      PUSD1000 IF LESS
.
.         Print total of same day seperations
.
          ADD       ONE,NUMBER
          PACK      S1NUM,S1PRE,NUMBER,RIGHTB
          SQUEEZE   S1NUM
.
          PRINT     *1,"|",*14,NUMBER,*17,"TOTAL (=27 to 30)":
                    *50,S1NUM,*62,"| ",*67,T3CC:
                    *80,"|"
.
PUSD9999  RETURN
+
.*******************************************************************************
.*                              PUUN0000                  CALLED BY : PPUB0000 *
.*      Public Hospital : Print section 4 - Unqualified Newborns               *
.*******************************************************************************
PUUN0000  CALL      UND80
          PRINT     *1,"| UNQUALIFIED NEWBORNS",*62,"|",*80,"|"
          PRINT     *1,"|",*14,"32",*17,"Number of Entirely Unqualified ":
                    "Episodes",*62,"|",*67,UNQLNBEP,*80,"|"
          PRINT     *1,"|",*14,"33",*17,"Number of Unqualified Days":
                    *62,"|",*67,UNQLNBDY,*80,"|"
PUUN9999  RETURN
+
.*******************************************************************************
.*                              PUTL0000                  CALLED BY : PPUB0000 *
.*      Public Hospital : Print Tail section                                   *
.*******************************************************************************
PUTL0000  CALL      UND80
          PRINT     *1," 34 Average Available Beds for the Month",*N:
                    *1," 35 Reconciliation has been completed between ":
                       "PRS/PRS2 Systems and the ",*N:
                   *1,"    hospital's aggregate YTD totals for the month prior":
                       " to Current Month.",*N:
                    *1,"    (Tick one)   ____ Yes   ____ No ":
                    *55,"Date Printed : ";
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          PRINT     *70,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          CALL      UND80
          PRINT     *1," SIGNED (Chief Executive Officer) : ":
                    *62,"Date : __/__/____",*N:
                    *62,"306 FORM S1"
PUTL9999  RETURN
+
.*******************************************************************************
.*                              HPRI0000                  CALLED BY :  ML1000  *
.*                Print header for the Private Hospital                        *
.*-----------------------------------------------------------------------------*
.*             PRIVATE HOSPITAL QUARTERLY RETURN - ADMITTED PATIENTS           *
.*                                                                             *
.*        Hospital xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Hospital Code xx        *
.*        11                                           56                      *
.*                     Year xxxx          Quarter Ending                       *
.*                     24                 43                                   *
.*-----------------------------------------------------------------------------*
.*                                         |YTD     |Adjusted|Current |YTD     *
.*                                         |Advised |YTD for |Quarter |Current *
.*                                         |for Prev|Previous|        |Quarter *
.*                                         |Quarter |Quarter |        |(D=B+C) *
.*                                         |  (A)   |  (B)   |  (C)   |  (D)   *
.*-----------------------------------------44-------53-------62-------71-------*
.*                                                                             *
.*******************************************************************************
. After 01/07/2002
.*******************************************************************************
.*                              HPRI0000                  CALLED BY :  ML1000  *
.*                Print header for the Private Hospital                        *
.*-----------------------------------------------------------------------------*
.*             PRIVATE HOSPITAL MONTHLY RETURN - ADMITTED PATIENTS             *
.*                                                                             *
.*        Hospital xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx Hospital Code xx        *
.*        11                                           56                      *
.*                     Year xxxx          Month Ending: xx/xx                  *
.*                     24                 43                                   *
.*-----------------------------------------------------------------------------*
.*                                         |YTD     |Adjusted|Current |YTD     *
.*                                         |Advised |YTD for |Quarter |Current *
.*                                         |for Prev|Previous|        |Quarter *
.*                                         |Quarter |Quarter |        |(D=B+C) *
.*                                         |  (A)   |  (B)   |  (C)   |  (D)   *
.*-----------------------------------------44-------53-------62-------71-------*
.*                                                                             *
.*******************************************************************************
HPRI0000  DISPLAY   *P1:24,*EL,"Generating Report"
          PRINT     *F 
          CALL      UND80
.
          BRANCH    NEWFILE,HPRI1000
.
          PRINT     *16,"PRIVATE HOSPITAL QUARTERLY RETURN - ADMITTED PATIENTS":
                    *N,*N:
                    *7,"Hospital :",*18,CNAME,*54,"Hospital Code :",*70,CFILENO
.
          UNPACK    ENDRNG,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,FCMON
          LOAD      MONTH,FCMON,MONTH1,MONTH2,MONTH3,MONTH4,MONTH5,MONTH6:
                                MONTH7,MONTH8,MONTH9,MONTH10,MONTH11,MONTH12
          PRINT     *23,"Year :",*31,CCENT,CYEAR,*42:
                    "Quarter Ending :",*59,MONTH
.
          CALL      UND80
.
          PRINT     *1,"|INPATIENT TYPE":
                    *44,"|YTD",*53,"|Adjusted",*62,"|Current":
                    *71,"|YTD",*80,"|",*N:
                    *1,"|",*44,"|Advised",*53,"|YTD for",*62,"|Quarter":
                    *71,"|Current",*80,"|",*N:
                    *1,"|",*44,"|for Prev",*53,"|Previous",*62,"|":
                    *71,"|Quarter",*80,"|",*N:
                    *1,"|",*44,"|Quarter",*53,"|Quarter",*62,"|":
                    *71,"|(D=B+C)",*80,"|",*N:
                    *1,"|",*44,"|  (A)",*53,"|  (B)",*62,"|  (C)":
                    *71,"|  (D)",*80,"|" 
          CALL      UND80
          GOTO      HPRI9999
.
HPRI1000  PRINT     *16,"PRIVATE HOSPITAL MONTHLY RETURN - ADMITTED PATIENTS":
                    *N,*N:
                    *7,"Hospital :",*18,CNAME,*54,"Hospital Code :",*70,CFILENO
.
          UNPACK    ENDRNG,CCENT,CYEAR,CMON,CDAY
          PACK      DIM5,CDAY,SLASH,CMON
          PRINT     *23,"Year :",*31,CCENT,CYEAR,*42:
                    "Month Ending :",*59,DIM5 
.
          CALL      UND80
.
          PRINT     *1,"|INPATIENT TYPE":
                    *44,"|YTD",*53,"|Adjusted",*62,"|Current":
                    *71,"|YTD",*80,"|",*N:
                    *1,"|",*44,"|Advised",*53,"|YTD for",*62,"|Month":
                    *71,"|Current",*80,"|",*N:
                    *1,"|",*44,"|for Prev",*53,"|Previous",*62,"|":
                    *71,"|Month",*80,"|",*N:
                    *1,"|",*44,"|Month",*53,"|Month",*62,"|":
                    *71,"|(D=B+C)",*80,"|",*N:
                    *1,"|",*44,"|  (A)",*53,"|  (B)",*62,"|  (C)":
                    *71,"|  (D)",*80,"|" 
          CALL      UND80
HPRI9999  RETURN
+
.*******************************************************************************
.*                              PPRI0000                  CALLED BY : ML1000   *
.*                Print the details for the private hospital                   *
.*-----------------------------------------------------------------------------*
.* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx | xxxxxx | xxxxxx | xxxxxx | xxxxxx *
.* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx | xxxxxx | xxxxxx | xxxxxx | xxxxxx *
.* xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx | xxxxxx | xxxxxx | xxxxxx | xxxxxx *
.*-----------------------------------------44-------53-------62-------71-------*
.*                                                                             *
.*******************************************************************************
PPRI0000  MOVE      ZERO,NUMBER 
.
          MOVE      ONE,PSECTION             * Printing Section 1
          CALL      PRSP0000                 * Print Private Separations
.
          MOVE      TWO,PSECTION             * Printing Section 2
          CALL      PRBD0000                 * Print Private Bed Days
.
          MOVE      THREE,PSECTION           * Printing Section 3
          CALL      PRSD0000                 * Print Private Same Day Separation
.
          CALL      PRTL0000                 * Print Tail Section
.
PPRI9999  RETURN
+
.*******************************************************************************
.*                               PRSP0000                                      *
.*     Private Hospital : Print section 1 - Seperations (Include Same Day)     *
.*******************************************************************************
PRSP0000  PACK      KEY2,SP2
          CALL      RSTEMPA2                 * Postional read
          PACK      KEY10,SP10    
          CALL      RSPRADF1                 * Postional read
.
          PRINT     *1,"|SEPARATIONS (Includes Same Day)",*44,"|",*53,"|":
                    *62,"|",*71,"|",*80,"|"
.
PRSP1000  CALL      RKTEMPA2                 * Read next record
          BRANCH    OVRCD,PRSP9999
          CALL      RAPF0000                 * Read ADF record
.
          STRIP     XREF
          MOVE      XREF,FXREF
          LOAD      PRNTDESC,FXREF,PRIREF01,PRIREF02,PRIREF03,PRIREF04
.
.         Print Part 1 details XREF field 1 - 4
.
          MOVE      XCMSEPS,TOTSEPS          * Calculate YTD calculations
          ADD       XLMYTDS,TOTSEPS
.
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,PRNTDESC,*44,"| ",PRAFSEPS:
                    *53,"| ",XLMYTDS,*62,"| ",XCMSEPS,*71,"| ",TOTSEPS,*80,"|"
.
.         Calcuate Subtotals for Part 1 XREF field 1 - 4
. 
.
          ADD       PRAFSEPS,ST1P1CA          * Subtotal Section 1 Part 1 Col. A
          ADD       XLMYTDS,ST1P1CB           * Subtotal Section 1 Part 1 Col. B
          ADD       XCMSEPS,ST1P1CC           * Subtotal Section 1 Part 1 Col. C
          ADD       TOTSEPS,ST1P1CD           * Subtotal Section 1 Part 1 Col. D
.
.         Write to file the YTD Advised for Previous Month
.
          MATCH     ENDRNG,EDATCM            *      End Rng = End Curnt Mon/Qtr?
          GOTO      PRSP1100 IF NOT EQUAL    *      No. Continue    
          CALL      WAPF0000                 *      Yes. Write Adv. Prev. Fiqure
.
PRSP1100  COMPARE   FOUR,FXREF               * Print Subtotals ?
          GOTO      PRSP1000 IF LESS         * No. Read next record
.
.         Print Subtotals for Part 1 XREF field 1 - 4
. 
          ADD       ONE,NUMBER               * Yes. Print Subtotals
          PRINT     *1,"|",*12,NUMBER,*15,"Sub Total (=1+2+3+4)":
                    *44,"| ",ST1P1CA,*53,"| ",ST1P1CB,*62,"| ",ST1P1CC:
                    *71,"| ",ST1P1CD,*80,"|",*N:
                    *1,"|Patients Treated & Paid Under Contract to":
                    *44,"|",*53,"|",*62,"|",*71,"|",*80,"|",*N:
                    *1,"|Public Sector",*44,"|",*53,"|",*62,"|":
                    *71,"|",*80,"|"
.
PRSP2000  CALL      RKTEMPA2                 * Read next record
          BRANCH    OVRCD,PRSP9999
          CALL      RAPF0000                 * Read ADF record
.
          STRIP     XREF
          MOVE      XREF,FXREF
          LOAD      PRNTDESC,FXREF,NOTHING,NOTHING,NOTHING,NOTHING:
                                   PRIREF05,PRIREF06
. 
          MOVE      XCMSEPS,TOTSEPS          * Add Total Seperations YTD
          ADD       XLMYTDS,TOTSEPS
.
.         Print details for Part 2 XREF field 5 - 6
. 
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,PRNTDESC,*44,"| ",PRAFSEPS:
                    *53,"| ",XLMYTDS,*62,"| ",XCMSEPS,*71,"| ",TOTSEPS,*80,"|"
.
.         Calcuate Subtotals for Part 2 XREF field 5 - 6
. 
          ADD       PRAFSEPS,ST1P2CA         * Subtotal Sect. 1 Part 2 Col. A
          ADD       XLMYTDS,ST1P2CB          * Subtotal Sect. 1 Part 2 Col. B
          ADD       XCMSEPS,ST1P2CC          * Subtotal Sect. 1 Part 2 Col. C
          ADD       TOTSEPS,ST1P2CD          * Subtotal Sect. 1 Part 2 Col. D
.
.         Write to file the YTD Advised for Previous Month
.
          MATCH     ENDRNG,EDATCM            *      End Rng = End Curnt Mon/Qtr?
          GOTO      PRSP2100 IF NOT EQUAL    *      No. Continue    
          CALL      WAPF0000                 *      Yes. Write Adv. Prev. Fiqure
.
PRSP2100  COMPARE   SIX,FXREF                * Print Part 2 Subtotals ?
          GOTO      PRSP2000 IF LESS         * No. Read next record
.
.         Print Subtotals for Part 2 XREF field 5 - 6
.
          ADD       ONE,NUMBER               * Yes. Print Part 2 Subtotals
          PRINT     *1,"|",*12,NUMBER,*15,"Sub Total (=6+7)":
                    *44,"| ",ST1P2CA,*53,"| ",ST1P2CB,*62,"| ",ST1P2CC:
                    *71,"| ",ST1P2CD,*80,"|"
.
.         Calculate Totals for section 1
. 
          MOVE      ST1P2CA,T1CA
          ADD       ST1P1CA,T1CA              * Total Section 1 Column A
          MOVE      ST1P2CB,T1CB
          ADD       ST1P1CB,T1CB              * Total Section 1 Column B
          MOVE      ST1P2CC,T1CC
          ADD       ST1P1CC,T1CC              * Total Section 1 Column C
          MOVE      ST1P2CD,T1CD
          ADD       ST1P1CD,T1CD              * Total Section 1 Column D
.
.         Print Totals for section 1
. 
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,"TOTAL (=5+8)":
                    *44,"| ",T1CA,*53,"| ",T1CB,*62,"| ",T1CC:
                    *71,"| ",T1CD,*80,"|"
.
PRSP9999  RETURN
+
.*******************************************************************************
.*                               PRBD0000                                      *
.*     Private Hospital : Print section 2 - Patient Days (Include Same Day)    *
.*******************************************************************************
PRBD0000  PACK      KEY2,SP2
          CALL      RSTEMPA2                 * Postional read
          PACK      KEY10,SP10    
          CALL      RSPRADF1                 * Postional read
.
          CALL      UND80
          PRINT     *1,"|PATIENT DAYS (Includes Same Day)",*44,"|",*53,"|":
                    *62,"|",*71,"|",*80,"|"
.
PRBD1000  CALL      RKTEMPA2                 * Read Next record 
          BRANCH    OVRCD,PRBD9999
          CALL      RAPF0000                 * Read ADF record
. 
          STRIP     XREF
          MOVE      XREF,FXREF
          LOAD      PRNTDESC,FXREF,PRIREF01,PRIREF02,PRIREF03,PRIREF04
.
          MOVE      XCMBDAY,TOTBDAY          * Claculate YTD bed days
          ADD       XLMYTDB,TOTBDAY
.
.         Print Part 1 details for XREF filed 1 - 4
.
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,PRNTDESC,*44,"| ",PRAFBDAY:
                    *53,"| ",XLMYTDB,*62,"| ",XCMBDAY,*71,"| ",TOTBDAY,*80,"|"
.
.         Calculate Subtotals Part 1 details for XREF field 1 - 4
.
.
          ADD       PRAFBDAY,ST2P1CA         * Subtotal Section 2 Part 1 Col. A
          ADD       XLMYTDB,ST2P1CB          * Subtotal Section 2 Part 1 Col. B
          ADD       XCMBDAY,ST2P1CC          * Subtotal Section 2 Part 1 Col. C
          ADD       TOTBDAY,ST2P1CD          * Subtotal Section 2 Part 1 Col. C
.
.         Write to file the YTD Advised for Previous Month
.
          MATCH     ENDRNG,EDATCM            *      End Rng = End Curnt Mon/Qtr?
          GOTO      PRBD1100 IF NOT EQUAL    *      No. Continue    
          CALL      WAPF0000                 *      Yes. Write Adv. Prev. Fiqure
.
PRBD1100  COMPARE   FOUR,FXREF               * Print Subtotals ?
          GOTO      PRBD1000 IF LESS         * No. Read next record
.
.         Print Subtotals Part 1 details for XREF field 1 - 4
.
          ADD       ONE,NUMBER               * Yes. Print subtotals.
          PRINT     *1,"|",*12,NUMBER,*15,"Sub Total (=10+11+12+13)":
                    *44,"| ",ST2P1CA,*53,"| ",ST2P1CB,*62,"| ",ST2P1CC:
                    *71,"| ",ST2P1CD,*80,"|",*N:
                    *1,"|Patients Treated & Paid Under Contract to":
                    *44,"|",*53,"|",*62,"|",*71,"|",*80,"|",*N:
                    *1,"|Public Sector",*44,"|",*53,"|",*62,"|":
                    *71,"|",*80,"|"
.
PRBD2000  CALL      RKTEMPA2                 * Read next record
          BRANCH    OVRCD,PRBD9999
          CALL      RAPF0000                 * Read ADF record
. 
          STRIP     XREF
          MOVE      XREF,FXREF
          LOAD      PRNTDESC,FXREF,NOTHING,NOTHING,NOTHING,NOTHING:
                                   PRIREF05,PRIREF06
.
          MOVE      XCMBDAY,TOTBDAY          * Claculate YTD bed days
          ADD       XLMYTDB,TOTBDAY
.
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,PRNTDESC,*44,"| ",PRAFBDAY:
                    *53,"| ",XLMYTDB,*62,"| ",XCMBDAY,*71,"| ",TOTBDAY,*80,"|"
.
.         Calculate SubTotals for Part 2 XREF field 5 - 6
.
          ADD       PRAFBDAY,ST2P2CA         * Subtotal Sect. 2 Part 2 Col. A
          ADD       XLMYTDB,ST2P2CB          * Subtotal Sect. 2 Part 2 Col. B
          ADD       XCMBDAY,ST2P2CC          * Subtotal Sect. 2 Part 2 Col. C
          ADD       TOTBDAY,ST2P2CD          * Subtotal Sect. 2 Part 2 Col. C
.
.         Write to file the YTD Advised for Previous Month
.
          MATCH     ENDRNG,EDATCM            *      End Rng = End Curnt Mon/Qtr?
          GOTO      PRBD2100 IF NOT EQUAL    *      No. Continue    
          CALL      WAPF0000                 *      Yes. Write Adv. Prev. Fiqure
.
PRBD2100  COMPARE   SIX,FXREF                * Print Subtotals ?
          GOTO      PRBD2000 IF LESS         * No. Read Next record
.
.        Print SubTotals for Part 2 XREF field 5 - 6
.
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,"Sub Total (=15+16)":
                    *44,"| ",ST2P2CA,*53,"| ",ST2P2CB,*62,"| ",ST2P2CC:
                    *71,"| ",ST2P2CD,*80,"|"
.
.         Calculate Totals for section 2
. 
          MOVE      ST2P2CA,T2CA
          ADD       ST2P1CA,T2CA              * Total Section 2 Column A
          MOVE      ST2P2CB,T2CB
          ADD       ST2P1CB,T2CB              * Total Section 2 Column B
          MOVE      ST2P2CC,T2CC
          ADD       ST2P1CC,T2CC              * Total Section 2 Column C
          MOVE      ST2P2CD,T2CD
          ADD       ST2P1CD,T2CD              * Total Section 2 Column D
.
.         Print Totals for section 2
. 
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,"TOTAL (=14+17)",*44,"| ",T2CA:
                    *53,"| ",T2CB,*62,"| ",T2CC,*71,"| ",T2CD,*80,"|"
.
PRBD9999  RETURN
+
.*******************************************************************************
.*                               PRSD0000                                      *
.*     Private Hospital : Print section 3 - Bed Days                           *
.*******************************************************************************
PRSD0000  PACK      KEY2,SP2
          CALL      RSTEMPA2                 * Postional Read 
          PACK      KEY10,SP10    
          CALL      RSPRADF1                 * Postional read
.
          CALL      UND80
          PRINT     *1,"|SAME DAY SEPARATIONS",*44,"|",*53,"|":
                    *62,"|",*71,"|",*80,"|"
.
          CALL      RKTEMPA2                 * Store first variable
          BRANCH    OVRCD,PRSD9999
          MOVE      XCMSAME,ACMSAME
          MOVE      XLMYTDD,ALMSAME
          CALL      RAPF0000                 * Skip first record
          MOVE      PRAFSAME,ARAFSAME
PRSD1000  CALL      RKTEMPA2                 * Read next record
          BRANCH    OVRCD,PRSD9999
.
          MOVE      XREF,FXREF
          COMPARE   TWO,FXREF
          GOTO      PRSD1110 IF NOT EQUAL
          ADD       ACMSAME,XCMSAME
          ADD       ALMSAME,XLMYTDD
          ADD       ARAFSAME,PRAFSAME
.
PRSD1110  CALL      RAPF0000                 * Read ADF record
.
          STRIP     XREF
          MOVE      XREF,FXREF
          LOAD      PRNTDESC,FXREF,NOTHING,PRIREF07,PRIREF08,PRIREF09
.
          MOVE      XCMSAME,TOTSAME          * Add Totals YTD.
          ADD       XLMYTDD,TOTSAME
.
.         Print Part 1 details for XREF filed 2 - 4
.
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,PRNTDESC,*44,"| ",PRAFSAME:
                    *53,"| ",XLMYTDD,*62,"| ",XCMSAME,*71,"| ",TOTSAME,*80,"|"
.
.         Calculate Subtotals Part 1 details for XREF field 2 - 4
.
          ADD       PRAFSAME,ST3P1CA         * Subtotal Section 3 Part 1 Col. A
          ADD       XLMYTDD,ST3P1CB          * Subtotal Section 3 Part 1 Col. B
          ADD       XCMSAME,ST3P1CC          * Subtotal Section 3 Part 1 Col. C
          ADD       TOTSAME,ST3P1CD          * Subtotal Section 3 Part 1 Col. C
.
.         Write to file the YTD Advised for Previous Month
.
          MATCH     ENDRNG,EDATCM            *      End Rng = End Curnt Mon/Qtr?
          GOTO      PRSD1100 IF NOT EQUAL    *      No. Continue    
          CALL      WAPF0000                 *      Yes. Write Adv. Prev. Fiqure
.
PRSD1100  COMPARE   FOUR,FXREF               * Print subtotals ?
          GOTO      PRSD1000 IF LESS         * No. Read next record
.
.         Print Subtotals Part 1 details for XREF field 2 - 4
.
          ADD       ONE,NUMBER               * Yes. Print subtotals.
          PRINT     *1,"|",*12,NUMBER,*15,"Sub Total (=19+20+21)":
                    *44,"| ",ST3P1CA,*53,"| ",ST3P1CB,*62,"| ",ST3P1CC:
                    *71,"| ",ST3P1CD,*80,"|",*N:
                    *1,"|Patients Treated & Paid Under Contract to":
                    *44,"|",*53,"|",*62,"|",*71,"|",*80,"|",*N:
                    *1,"|Public Sector",*44,"|",*53,"|",*62,"|":
                    *71,"|",*80,"|"
.
PRSD2000  CALL      RKTEMPA2                 * Read next record.
          BRANCH    OVRCD,PRSD9999
          CALL      RAPF0000                 * Read ADF record
.
          STRIP     XREF
          MOVE      XREF,FXREF
          LOAD      PRNTDESC,FXREF,NOTHING,NOTHING,NOTHING,NOTHING:
                                   PRIREF10,PRIREF11
.
          MOVE      XCMSAME,TOTSAME          * Add total YTD.
          ADD       XLMYTDD,TOTSAME
.
.         Print Part 2 details for XREF filed 5 - 6
.
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,PRNTDESC,*44,"| ",PRAFSAME:
                    *53,"| ",XLMYTDD,*62,"| ",XCMSAME,*71,"| ",TOTSAME,*80,"|"
.
.         Calculate Subtotals Part 2 details for XREF field 5 - 6
.
          ADD       PRAFSAME,ST3P2CA         * Subtotal Sect. 3 Part 2 Col. A
          ADD       XLMYTDD,ST3P2CB          * Subtotal Sect. 3 Part 2 Col. B
          ADD       XCMSAME,ST3P2CC          * Subtotal Sect. 3 Part 2 Col. C
          ADD       TOTSAME,ST3P2CD          * Subtotal Sect. 3 Part 2 Col. C
.
.         Write to file the YTD Advised for Previous Month
.
          MATCH     ENDRNG,EDATCM            *      End Rng = End Curnt Mon/Qtr?
          GOTO      PRSD2100 IF NOT EQUAL    *      No. Continue    
          CALL      WAPF0000                 *      Yes. Write Adv. Prev. Fiqure
.
PRSD2100  COMPARE   SIX,FXREF                * Print subtotals ?
          GOTO      PRSD2000 IF LESS         * No. Read next record
.
.         Print Subtotals Part 2 details for XREF field 5 - 6
.
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,"Sub Total (=23+24)":
                    *44,"| ",ST3P2CA,*53,"| ",ST3P2CB,*62,"| ",ST3P2CC:
                    *71,"| ",ST3P2CD,*80,"|"
.
.         Calculate Totals for section 3
. 
          MOVE      ST3P2CA,T3CA
          ADD       ST3P1CA,T3CA             * Total Section 3 Column A
          MOVE      ST3P2CB,T3CB
          ADD       ST3P1CB,T3CB             * Total Section 3 Column B
          MOVE      ST3P2CC,T3CC
          ADD       ST3P1CC,T3CC             * Total Section 3 Column C
          MOVE      ST3P2CD,T3CD
          ADD       ST3P1CD,T3CD             * Total Section 3 Column D
.
.         Print Totals for section 3
. 
          ADD       ONE,NUMBER
          PRINT     *1,"|",*12,NUMBER,*15,"TOTAL (=22+25)":
                    *44,"| ",T3CA,*53,"| ",T3CB,*62,"| ",T3CC:
                    *71,"| ",T3CD,*80,"|"
PRSD9999  RETURN
+
.*******************************************************************************
.*                               PRTL0000                                      *
.*     Private Hospital : Print Tail section                                   *
.*******************************************************************************
PRTL0000  CALL      UND80
          PRINT     *1,"27 Average Available Beds ",*N:
                    *1,"28 Reconciliation has been completed between ":
                       "'Adjusted YTD for Previous Month'",*N:
                    *1,"   (Column B) and the PRS/PRS2 system and all":
                       " necessary action taken",*N:
                    *1,"   (Tick one)   ____ Yes   ____ No ":
                    *55,"Date Printed : ";
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          PRINT     *70,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          CALL      UND80
          PRINT     *1,"SIGNED (Chief Executive Officer) : ":
                    *62,"Date : __/__/____",*N:
                    *62,"FORM P1"
.
PRTL9999  RETURN
+
.*******************************************************************************
.*                               RAPF0000                                      *
.* Read the YTD Current Qurater figures and ensure that it matches the         *
.* current XREF variable                                                       *
.*******************************************************************************
RAPF0000  PACK      KEY10,EDATLM,XREF 
          CALL      RDPRADF1                 * Read record
          BRANCH    OVRCD,RAPF1000           * Record does not exist
.
          MATCH     EDATLM,PRAFDATE          * Figures for YTD previous month
          GOTO      RAPF1000 IF NOT EQUAL    * No.
.
          MATCH     XREF,PRAFREFR            * Yes. Reference fields the same ?
          GOTO      RAPF9999 IF EQUAL        * Yes
.
          CALL      RPPRADF1                 * Reposition at current ADF record
.
RAPF1000  BRANCH    PSECTION,RAPF1100,RAPF1200,RAPF1300  * No. Print zeros
.
RAPF1100  MOVE      ZERO,PRAFSEPS            * Init. Separations to zero
          GOTO      RAPF9999                 * Exit Routine
.
RAPF1200  MOVE      ZERO,PRAFBDAY            * Init. Bed days to zero
          GOTO      RAPF9999                 * Exit Routine
.
RAPF1300  MOVE      ZERO,PRAFSAME            * Init. Same day Seps. to zero
.
RAPF9999  RETURN
.*******************************************************************************
.*                               WAPF0000                                      *
.* Write the YTD Current Quarter figures to file for the next report as the    *
.* YTD Advised figures for the Previous Quarter.                               *
.*******************************************************************************
WAPF0000  PACK      KEY10,EDATCM,XREF
          CALL      RDPRADF1
          BRANCH    OVRCD,WAPF1000
.
.         Update old record
.
          MOVE      XREF,PRAFREFR
          MOVE      ENDRNG,PRAFDATE
          BRANCH    PSECTION,WAPF0100,WAPF0200,WAPF0300
.
WAPF0100  MOVE      TOTSEPS,PRAFSEPS         * Update Separations
          GOTO      WAPF0900
WAPF0200  MOVE      TOTBDAY,PRAFBDAY         * Update Bed Days
          GOTO      WAPF0900
WAPF0300  MOVE      TOTSAME,PRAFSAME         * Update Same Day Separations
.
WAPF0900  CALL      UPPRADF1                 * Update file
          GOTO      WAPF9999                 * Exit Routine
.
.         Write new record 
.
WAPF1000  MOVE      XREF,PRAFREFR
          MOVE      ENDRNG,PRAFDATE
          BRANCH    PSECTION,WAPF1100,WAPF1200,WAPF1300
.
WAPF1100  MOVE      TOTSEPS,PRAFSEPS         * Write Separations
          GOTO      WAPF1900
WAPF1200  MOVE      TOTBDAY,PRAFBDAY         * Write Bed Days
          GOTO      WAPF1900
WAPF1300  MOVE      TOTSAME,PRAFSAME         * Write Same Day Separations
.
WAPF1900  PACK      KEY10,PRAFDATE,PRAFREFR
          CALL      WRPRADF1                 * Write file
.
WAPF9999  RETURN
+
.*******************************************************************************
.*                               EREP0000                                      *
.*                            End the Report                                   *
.*******************************************************************************
EREP0000  DISPLAY   *P1:24,*EL,*B,"Report Completed. ";
          CALL      HITENTER
.
EREP9999  RETURN
+
.**************************************************************************
.*  Calculate RTIO leave days                                             *
.**************************************************************************
RTDY0000  MOVE      ZERO,RTDAYS
          PACK      KEY30,AADMNO,SP20
          CALL      RDSTRAN2
RTDY1000  CALL      RDKTRAN2
          BRANCH    OVRCD,RTDY9999
.
          MATCH     TADMN,AADMNO
          GOTO      RTDY9999 IF NOT EQUAL
.
          MATCH     FROMDATE,TDATE
          GOTO      RTDY1000 IF LESS        * To date < transfer date
.
          MATCH     TDATE,TODATE
          GOTO      RTDY9999 IF LESS        * To date < transfer date
.
          MATCH     ANSL,TMOVE
          GOTO      RTDY1000 IF NOT EQUAL
.
          MATCH     SP70,PTTRLTYP
          GOTO      RTDY1000 IF EQUAL
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,RTDY1000
.
          MATCH     "3",TCINDC2
          GOTO      RTDY1000 IF NOT EQUAL
.
          PACK      SKEY30,DTADMN,TDATE,TTIME,TWARD,TBED,SP70
          PACK      STDATE,TDATE
          PACK      STTIME,TTIME
.
          MATCH     TDATE,ADATE
          IF        @EQUAL
            GOTO      RTDY1000
          ENDIF
.
          PACK      KEY30,DTADMN,TDATE,SP70
          CALL      RDSTRAN2
RTDY2000  CALL      RDKTRAN2
          BRANCH    OVRCD,RTDY3000
.
          MATCH     TADMN,AADMNO
          GOTO      RTDY3000 IF NOT EQUAL
.
          MATCH     TDATE,STDATE
          GOTO      RTDY3000 IF NOT EQUAL
.
          MATCH     STTIME,TTIME
          GOTO      RTDY3000 IF NOT LESS
.
          MATCH     ANSR,TMOVE
          GOTO      RTDY2000 IF NOT EQUAL
.
          ADD       ONE,RTDAYS
.
RTDY3000  MOVE      SKEY30,KEY30
          CALL      RDTRAN2
          GOTO      RTDY1000
.
RTDY9999  RETURN
+
.*******************************************************************************
.*                       I/O Routines for the temp files                       *
.*******************************************************************************
.
. TEMPFILE1
.
RSTEMPA1  RESET     KEY8 
          READ      TMPFILE1,KEY8;;
          RETURN
.
RATEMPA1  RESET     KEY8
          MOVE      ZERO,OVRCD
          READ      TMPFILE1,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMPA1  RESET     KEY8
          MOVE      ZERO,OVRCD
          READ      TMPFILE1,KEY8;DTEMPADM
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPADM,TEMPADMN
          RETURN
.
RKTEMPA1  RESET     KEY8
          MOVE      ZERO,OVRCD
          READKS    TMPFILE1;DTEMPADM
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPADM,TEMPADMN
          RETURN
.
RPTEMPA1  RESET     KEY8
          MOVE      ZERO,OVRCD
          READKP    TMPFILE1;DTEMPADM
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPADM,TEMPADMN
          RETURN
.
UPTEMPA1  UPDATE    TMPFILE1;TEMPADMN
          RETURN
.
WRTEMPA1  MOVE      TEMPADMN,DTEMPADM 
          WRITE     TMPFILE1,KEY8;DTEMPADM
          RETURN
.
. TMPFILE2
.
RSTEMPA2  RESET     KEY2 
          READ      TMPFILE2,KEY2;;
          RETURN
.
RATEMPA2  RESET     KEY2
          MOVE      ZERO,OVRCD
          READ      TMPFILE2,KEY2;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMPA2  RESET     KEY2
          MOVE      ZERO,OVRCD
          READ      TMPFILE2,KEY2;XREF,XLMYTDS,XLMYTDB,XLMYTDD:
                                 XCMSEPS,XCMBDAY,XCMSAME
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMPA2  RESET     KEY2
          MOVE      ZERO,OVRCD
          READKS    TMPFILE2;XREF,XLMYTDS,XLMYTDB,XLMYTDD:
                                 XCMSEPS,XCMBDAY,XCMSAME
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMPA2  RESET     KEY2
          MOVE      ZERO,OVRCD
          READKP    TMPFILE2;XREF,XLMYTDS,XLMYTDB,XLMYTDD:
                                 XCMSEPS,XCMBDAY,XCMSAME
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMPA2  UPDATE    TMPFILE2;XREF,XLMYTDS,XLMYTDB,XLMYTDD:
                                 XCMSEPS,XCMBDAY,XCMSAME
          RETURN
.
WRTEMPA2  WRITE     TMPFILE2,KEY2;XREF,XLMYTDS,XLMYTDB,XLMYTDD:
                                 XCMSEPS,XCMBDAY,XCMSAME
          RETURN
.
          INC       STDGENCD
          INC       PATACCIO/INC             * Account class file
          INC       PATASFIO/INC
          INC       PATCHCIO/INC             * Codes changes file
          INC       PATCODIO/INC             * Patient Codes file
          INC       PATDSCIO/INC             * Discharge file
          INC       PATMI1IO/INC             * Admissions file
          INC       PMSVX1IO/INC             * Admissions file
          INC       PATTRNIO/INC             * Transfer file
          INC       PRSADFIO/INC
          INC       PATMA1IO/INC
          INC       PRS2DAYS
          INC       PRS2CLAS
          INC       CALCAGE
          INC       CALCDAYS
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
+
.


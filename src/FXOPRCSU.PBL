.***************************************************************************
.* System    :   Theatre Management System                                 *
.* Program   :   FXOPRCSU                                                  *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   26/09/2006                                                *
.* Author    :   Peter Vela                                                *
.* Function  :   This program will loop the(oprcsuaf) file and             *
.*               assign zero to OPCSSTIM Session Time Used (shared)        *
.*                                                                         *
.* Mods      :   V9.07.01 26/09/2006 Peter Vela                            *
.*                        Created Fixit                                    *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRCSUFD/INC
.
. VARIABLES
. -------------------------
COUNTR    FORM      8
CURRDATE  DIM       8
.
PRGID     INIT      "FXOPRCSU"
PRGNAM    INIT      "Fixit for Theatre Session Usage - Assign zero to OPCSSTIM"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"oprcsuaf";
          OPEN      OPRCSUA1,"oprcsuaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit":
                    *P1:15,*V2LON,*HOFF,"NOTE: Create backups of oprcsuaf.dat and oprcsuaf.idx"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing: ";
          MOVE      ZERO,COUNTR                  * initialise record counter
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY16,SP70
          CALL      RSOPCSU1
PROC1000  CALL      RKOPCSU1
          BRANCH    OVRCD,PROC9000
.
          MOVE      ZERO,OPCSSTIM
.
          ADD       ONE,COUNTR
          CALL      UPOPCSU1                    * Update oprcsuaf
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit completed.  ",COUNTR," records updated.":
                    "  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OPRCSUIO/INC

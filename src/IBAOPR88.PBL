.******************************************************************************
.* System    :  OPERATING ROOMS                                               *
.* Program   :  IBAOPR88                                                      *
.* Desc      :  Doctor Preference Master Listing                              *
.******************************************************************************
.* Date      :  15/10/1990                                                    *
.* Author    :  Justin Coates                                                 *
.* Function  :  Produce a master listing for a range of doctors               *
.* Mods      :                                                                *
.******************************************************************************
.* V11.04.01 24/04/2024  Thanh T            TSK 0923560                       *
.*           Changed GINF0000 to add hosp id when calling RDOPDPH1            *
.* V11.03.01 23/01/2023  Thanh T            TSK 0919770                       *
.*           Changed REPT0700, GINF0000, COMM1100 to caters for adding        *
.*           Hospital field changes                                           *
.******************************************************************************
.*        V10.15.01 21/11/2019  Peter Vela  Task 0871644                      *
.*                  Change key to read OPRPCMFD                               *
.*        V9.04.01  17/02/2005  Lina Vo     CAR 56404                         *
.*                  Changed OPRDPAFD to OPRDPHFD.                             *
.*                  Changed OPRDPBFD to OPRDPFFD.                             *
.*                  Changed program to read opriteaf, oprdpfaf       with new *
.*                  key size.                                                 *
.*        V5.01.04  26/05/1994  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.
          INC       STD001FD                * standard variables
          INC       OPRDPHFD/INC            * doctor preference header file 
          INC       OPRDPFFD/INC            * doctor preference details file
          INC       OPRITEFD/INC            * item file
          INC       OPRPCMFD/INC            * comments file
          INC       OPRTRAFD/INC            * tray file
          INC       PATCODFD/INC            * codes file
          INC       PATDO1FD/INC            * doctors file
+
.
. ------- program variables -------
.
CNTPREF   FORM      4         * number of preferences for the doctor
CODE      DIM       2         * patdscod
DIM3      DIM       3         * patdsdoc
DOCCODE   DIM       6
ITEMDESC  DIM       30        * tray/item description
DESC30    DIM       30        
.
.
LASTPREF  DIM       9         * last pref code for comments printing
.
PREDDOCT  DIM       6         * ending doctor code for print
PREDPREF  DIM       9         * ending preference code for print
PREVCLSS  DIM       3         * previous class
PREVDOCT  DIM       6         * previous doctor
PREVPREF  DIM       9         * previous preference
PRSTDOCT  DIM       6         * starting doctor code for print
PRSTPREF  DIM       9         * starting preference code for print
PTCNDCQS  FORM      1
.
QUPER     FORM      1         * ? performed flag
.
STOPDOCT  DIM       6         * ending doctors code
STOPNAME  DIM       40        * ending doctors name
STOPPREF  DIM       9         * ending preference code
STRTDOCT  DIM       6         * starting doctors code
STRTNAME  DIM       40        * starting doctors name
STRTPREF  DIM       9         * starting preference code
.
. ------- program constants -------
.
CATOG     INIT      "OG"      * catogory for preference class
FINISH    INIT      "Finish"  
START     INIT      "Start "  
Z6        INIT      "zzzzzz"
.
PRGID     INIT      "IBAOPR88"
PRGNAM    INIT      "Doctor Preference Master Listing"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      MENU0000                * enter options
          BRANCH    EXIT,ML9999             * exit selected
.
ML2000    CALL      DOIT0000                * do the report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*********************************************************************
INIT0000  CALL      DISPHEAD  
          MOVE      ONE,CNEWDS
.
          DISPLAY   *P50:24,"Opening oprdphaf";
          OPEN      OPRDPHA1,"oprdphaf"     * doctor preference header file
.
          DISPLAY   *P58:24,"oprdpfaf";
          OPEN      OPRDPFA1,"oprdpfaf"     * doctor preference details file
.
          DISPLAY   *P58:24,"opriteaf";
          OPEN      OPRITEA1,"opriteaf"     * item file
.
          DISPLAY   *P58:24,"oprpcmaf";
          OPEN      OPRPCMA1,"oprpcmaf"     * comments file
.
          DISPLAY   *P58:24,"oprtryaf";
          OPEN      OPRTRYA1,"oprtryaf"     * tray file
.
          DISPLAY   *P58:24,"patcodes";
          OPEN      PATCODE1,"patcodes"     * codes file
.
          DISPLAY   *P58:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"     * doctors file
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P58:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
INIT9999  RETURN
+
.*********************************************************************
.*                  MENU0000                    Called by : ML1000   *
.*        Returns : EXIT = 1      exit selected                      *
.*********************************************************************
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*P1:5,ONE,*HOFF:
                    *P2:4," = Exit":
                    *P2:5," = Produce Listing":
                    *P1:7,"Select Option :";
.
MENU1000  KEYIN     *P17:7,*DV,UNDLN1,*P17:7,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL       * exit selected
.
          BRANCH    MOPTION,MENU8000        * valid answer
          BEEP
          GOTO      MENU1000                * invalid answer
.
MENU8000  MOVE      FALSE,EXIT              * valid
          GOTO      MENU9999
.
MENU9000  MOVE      TRUE,EXIT               * nothing entered
.
MENU9999  RETURN
+
.*********************************************************************
.*                  DOIT0000                    Called by : ML2000   *
.*        Enter the required information to produce the report       *
.*********************************************************************
DOIT0000  CALL      CLRV0000                * clear variables
          CALL      DISP0000                * display the screen
          CALL      DOCT0000                * enter the doctor ranges
          CALL      PREF0000                * enter the preference ranges
.
          CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,DOIT1000,DOIT0000,DOIT9999
.                         Yes      No       Cancel
.
DOIT1000  CALL      REPT0000                * produce the report
.
DOIT9999  RETURN
+
.*********************************************************************
.*                  CLRV0000                    Called by : DOIT0000 *
.*        Clear the input variables                                  *
.*********************************************************************
CLRV0000  UNPACK    SP20,STRTDOCT,STOPDOCT
          UNPACK    SP20,STRTPREF,STOPPREF
          UNPACK    SP20,PRSTDOCT,PREDDOCT
          UNPACK    SP20,PRSTPREF,PREDPREF
          PACK      STRTNAME,SP20,SP20
          PACK      STOPNAME,SP20,SP20
CLRV9999  RETURN
+
.*********************************************************************
.*                  DISP0000                    Called by : DOIT0000 *
.*        Display the screen and values                     RDIS0000 *
.*********************************************************************
DISP0000  DISPLAY   *P1:8,*EF:
                    *P1:10,"Starting Doctor Code : ",*V2LON,PRSTDOCT,*HOFF:
                    SP5,STRTNAME:
                    *P1:11,"Ending   Doctor Code : ",*V2LON,PREDDOCT,*HOFF:
                    SP5,STOPNAME:
                    *P1:13,"Starting Preference Code : ",*V2LON,PRSTPREF,*HOFF:
                    *P1:14,"Ending   Preference Code : ",*V2LON,PREDPREF;
DISP9999  RETURN
+
.*********************************************************************
.*                  DOCT0000                    Called by : DOIT0000 *
.*        Keyin the doctor range for the report                      *
.*        Returns : STRTDOCT      starting doctor code               *
.*                  STOPDOCT      ending   doctor code               *
.*********************************************************************
.
.         enter the Starting Doctor Code
.
DOCT0000  MOVE      TEN,CVERT               * row for input
          MOVE      "24",CCOL               * column for input
.
          CALL      KDOC0000                * enter doc code
          BRANCH    EXIT,DOCT1000           * nothing entered
.
          MOVE      KEY6,STRTDOCT           * starting doct code
          GOTO      DOCT2000
.
DOCT1000  PACK      STRTDOCT,SP6            * start
          MOVE      START,PRSTDOCT
          MOVE      SP30,STRTNAME
          DISPLAY   *P24:10,*EL,*V2LON,START;
          GOTO      DOCT5000
.
DOCT2000  MOVE      PACFNAME,STRTNAME       * starting name
          MOVE      STRTDOCT,PRSTDOCT
          DISPLAY   *P24:10,*EL,*V2LON,STRTDOCT,*HOFF,SP5,STRTNAME;
.
.         enter the Ending Doctor Code
.
DOCT5000  MOVE      TEN1,CVERT              * row for input
          MOVE      "24",CCOL               * column for input
.
          CALL      KDOC0000                * enter doc code
          BRANCH    EXIT,DOCT6000           * nothing entered
          MOVE      KEY6,STOPDOCT           * ending doct code
          GOTO      DOCT7000
.
DOCT6000  PACK      STOPDOCT,Z6             * finish
          MOVE      FINISH,PREDDOCT
          MOVE      SP30,STOPNAME
.
          DISPLAY   *P24:11,*EL,*V2LON,FINISH;
          GOTO      DOCT9999
.
DOCT7000  DISPLAY   *P24:11,*EL,*V2LON,STOPDOCT;
.
.         check that ending code is after starting code
.
          MATCH     STRTDOCT,STOPDOCT
          GOTO      DOCT8000 IF NOT LESS    * ending >= starting -> OK
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Doctor must be > or = to Starting Doctor **  ";
          CALL      HITENTER 
          GOTO      DOCT5000
.
DOCT8000  MOVE      PACFNAME,STOPNAME       * ending name
          MOVE      STOPDOCT,PREDDOCT
          DISPLAY   SP5,STOPNAME;
.
DOCT9999  RETURN
+
.*********************************************************************
.*                  PREF0000                    Called by : DOIT0000 *
.*        Keyin the preference range for the report                  *
.*        Returns : STRTPREF      starting preference code           *
.*                  STOPPREF      ending   preference code           *
.*********************************************************************
.
.         enter starting preference code
.
PREF0000  KEYIN     *P28:13,*DV,UNDLN9,*P28:13,*V2LON,STRTPREF:
                    *P28:13,*DV,STRTPREF;
.
          ENDSET    STRTPREF
          APPEND    SP9,STRTPREF
          RESET     STRTPREF
.
          MATCH     QUEST,STRTPREF
          GOTO      PREF0000 IF EQUAL       * ? entered which is not allowed
          MOVE      STRTPREF,PRSTPREF
.
          MATCH     SP9,STRTPREF
          GOTO      PREF2000 IF NOT EQUAL   * a code entered
.
.         default to start
.
          DISPLAY   *P28:13,*V2LON,START;
          PACK      PRSTPREF,START,SP9
.
.         enter ending preference
.
PREF2000  KEYIN     *P28:14,*DV,UNDLN9,*P28:14,*V2LON,STOPPREF:
                    *P28:14,*DV,STOPPREF;
.
          ENDSET    STOPPREF
          APPEND    SP9,STOPPREF
          RESET     STOPPREF
.
          MATCH     QUEST,STOPPREF
          GOTO      PREF2000 IF EQUAL       * ? entered which is not allowed
          MOVE      STOPPREF,PREDPREF
.
          MATCH     SP9,STOPPREF
          GOTO      PREF3000 IF NOT EQUAL   * a code entered
.
.         default to finish
.
          PACK      STOPPREF,Z6,Z6
          PACK      PREDPREF,FINISH,SP9
          DISPLAY   *P28:14,*V2LON,FINISH;
          GOTO      PREF9999
.
.         check ending preference is after starting
.
PREF3000  MATCH     STRTPREF,STOPPREF
          GOTO      PREF9999 IF NOT LESS    * ending >= starting -> OK
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Preference must be >= Starting Preference **  ";
          CALL      HITENTER 
          GOTO      PREF2000
.
PREF9999  RETURN
+
.*********************************************************************
.*                  REPT0000                    Called by : DOIT1000 *
.*        Produce the report                                         *
.*        Loop through OPRDPFFD/oprdpfaf using the first index       *
.*********************************************************************
REPT0000  MOVE      ZERO,CPAGENO            * initialize page counter
          CALL      IBACLOCK                * get current time
          MOVE      SP9,PREVPREF            * clear previous preference
          MOVE      SP6,PREVDOCT            *   "      "     doctor
          MOVE      SP3,PREVCLSS            *   "      "     class
          MOVE      ZERO,CNTPREF            *   "   preference count
          MOVE      ONE,CNOUNDLN            * no underlines for previous page
          MOVE      "60",CLNO               * to force a new page
.
          DISPLAY   *P1:24,*EL,"Doctor Code :",*P28:24,"Preference :":
                    *P60:24,"Scanning:";
.
. ------- determine starting position -------
.
          MATCH     SP6,STRTDOCT
          GOTO      REPT0700 IF NOT EQUAL   * have a starting doctor
.
.         start at first doctor and first preference
.
          PACK      KEY25,SP30
          GOTO      REPT0900                * do a positional read
.
.         have a starting doctor and a starting preference
.
REPT0700  PACK      KEY37,STRTDOCT,STRTPREF,SP70
.
REPT0900  CALL      RSOPDPF1
.
. ------  next read on doctor preference details file -------
.
REPT1000  CALL      RKOPDPF1
          BRANCH    OVRCD,REPT9000          * end of report
.
          DISPLAY   *P16:24,*V2LON,OPDFCODE,*HOFF,*P70:24,OPDFPREF;
.
.         check record details against criteria entered
.
          MATCH     OPDFCODE,STOPDOCT
          GOTO      REPT9000 IF LESS        * have finished the range
.
          MATCH     STRTPREF,OPDFPREF
          GOTO      REPT1000 IF LESS        * preference is before range
.
          MATCH     OPDFPREF,STOPPREF
          GOTO      REPT1000 IF LESS        * preference is after range
.
. ------- have a valid record -------
.
          DISPLAY   *P41:24,*V2LON,OPDFPREF;
.
          CALL      GINF0000                * get all the information
.
          MATCH     OPDFCODE,PREVDOCT       * different doctor to last one ?
          GOTO      REPT2000 IF EQUAL       * same doctor
.
. ------- have a different doctor -------
.
          MOVE      PREVPREF,LASTPREF       * keep lat pref fpr comments print
          MOVE      OPDFPREF,PREVPREF       * set up previous preference
          MOVE      OPDFCLSS,PREVCLSS       * set up previous class
.
.         see if at first doctor
.
          MATCH     SP6,PREVDOCT
          GOTO      REPT1600 IF NOT EQUAL   * just print table heading
.
.         at the very first doctor
.
          CALL      NEWP0000                * print new page as first doctor
          GOTO      REPT1800
.
.         at a new doctor, but not the first
.
REPT1600  CALL      COMM0000                * print comments
          CALL      PEND0000                * print the end details for a doct
.
          COMPARE   "50",CLNO
          GOTO      REPT1700 IF LESS        * will fit on current page
          CALL      NEWP0000                * print new page
          GOTO      REPT1800
.
REPT1700  CALL      PTBH0000                * print a new table heading
.
REPT1800  MOVE      OPDFCODE,PREVDOCT       * change previous doctor
          MOVE      ONE,CNTPREF             * reset preference counter
          GOTO      REPT1000                * get next record
.
. ------- same doctor, check if a different preference -------
.
REPT2000  MATCH     OPDFPREF,PREVPREF
          GOTO      REPT4000 IF EQUAL       * same preference
          MOVE      PREVPREF,LASTPREF
          CALL      COMM0000                * print comments for the last pref.
.
.         have a different preference
.
          COMPARE   "55",CLNO
          GOTO      REPT2100 IF LESS        * will fit on current page
          CALL      UND132                  * finish off last page
          CALL      NEWP0000                * print new page
          GOTO      REPT2200
.
REPT2100  CALL      PPRE0000                * print preference details
.
REPT2200  MOVE      OPDFPREF,PREVPREF       * change previous preference
          MOVE      OPDFCLSS,PREVCLSS       * change previous class
          ADD       ONE,CNTPREF             * increment number of preferences
          GOTO      REPT1000                * get next record
.
. ------- same preference, check if a different class -------
.
REPT4000  MATCH     OPDFCLSS,PREVCLSS
          GOTO      REPT6000 IF EQUAL       * same class
.
.         have a different class
.
          COMPARE   "56",CLNO
          GOTO      REPT4100 IF LESS        * will fit on current page
          CALL      UND132                  * finish off last page
          CALL      NEWP0000                * print new page
          GOTO      REPT4200
.
REPT4100  CALL      PCLS0000                * print class details
.
REPT4200  MOVE      OPDFCLSS,PREVCLSS       * change previous class
          GOTO      REPT1000                * get next record
.
. ------- have the same class, so just print the item details -------
.
REPT6000  COMPARE   "56",CLNO
          GOTO      REPT6100 IF LESS        * will fit on current page
          CALL      UND132                  * finish off last page
          CALL      NEWP0000                * print new page
          GOTO      REPT1000
.
REPT6100  CALL      PITM0000                * print item details
          GOTO      REPT1000
.
. ------- end of listing -------
.
REPT9000  COMPARE   "54",CLNO
          GOTO      REPT9100 IF LESS
          CALL      UND132                  * finish off last page
          CALL      NEWP0000
.
REPT9100  MOVE      PREVPREF,LASTPREF
          CALL      COMM0000                * print comments for the last pref.
          CALL      PEND0000                * print doctor summary details
.
          PRINT     *N,*N,"***  End of Listing  ***"
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Listing Generation Completed **",*W2;
.
REPT9999  RETURN
+
.*********************************************************************
.*                  KDOC0000                    Called by : DOCT0000 *
.*        Keyin the Doctor code                                      *
.*        Para's  : CCOL          column for keyin                   *
.*                  CVERT         row for keyin                      *
.*        Returns : KEY6          the doctor code                    *
.*                  PACFNAME      the doctor name                    *
.*                  EXIT = 1      only return hit                    *
.*********************************************************************
KDOC0000  MOVE      SP10,KEY6
          MOVE      CCOL,ECOL
          MOVE      CVERT,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KDOC4000,KDOC0000
.
          MOVE      DCODE,KEY6
.
KDOC2000  CALL      FDOC0000                * set up description
          MOVE      FALSE,EXIT              * valid code
          GOTO      KDOC9999
.
. ------ only enter hit ------
.
KDOC4000  PACK      PACFNAME,SP30,SP30,SP5
          MOVE      TRUE,EXIT               * only enter hit
.
KDOC9999  RETURN
+
.*********************************************************************
.*                  FDOC0000                    Called by : KDOC2000 *
.*        Para's  : KEY6          the doctor code           GINF0000 *
.*        Returns : PACFNAME      the doctor name                    *
.*********************************************************************
FDOC0000  PACK      PACFNAME,SP30,SP30,SP5
          MATCH     SP6,KEY6
          GOTO      FDOC9999 IF EQUAL       * no name to format
.
          CALL      RDDOCT1                 * read doctor file
          BRANCH    OVRCD,FDOC9999          * invalid name
.
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DTITL,PACTITLE          * title
          MOVE      TWO,PACFRMT             * format
          CALL      PACNAME                 * format the name
.
FDOC9999  RETURN
+
.*********************************************************************
.*                  RDIS0000                    Called by : KDOC5000 *
.*        Redisplay the screen after a ? mark                        *
.*********************************************************************
RDIS0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*P1:5,ONE,*HOFF:
                    *P2:4," = Exit":
                    *P2:5," = Produce report":
                    *P1:7,"Select Option : ",*V2LON,MOPTION;
.
          CALL      DISP0000                * display prompts
.
RDIS9999  RETURN
+
.*********************************************************************
.*                  NEWP0000                    Called by : REPT0000 *
.*        Print a new page for the report                            *
.*********************************************************************
NEWP0000  CALL      HEAD0000                * print main heading
          CALL      PTBH0000                * print table heading
.
NEWP9999  RETURN
+
.*********************************************************************
.*                  HEAD0000                    Called by : NEWP0000 *
.*        Main Heading routine                              COMM0000 *
.*********************************************************************
HEAD0000  CALL      HEAD132
          PRINT     *40,"Doctor Code From : ",PRSTDOCT,SP3,STRTNAME,*N:
                    *40,"            To   : ",PREDDOCT,SP3,STOPNAME,*N,*N:
                    *40,"Preference  From : ",PRSTPREF,*N:
                    *40,"            To   : ",PREDPREF
          MOVE      "8",CLNO                * set up line number
.
HEAD9999  RETURN
+
.*********************************************************************
.*                  PTBH0000                    Called by : REPT0000 *
.*        Print a table heading                             NEWP0000 *
.*        Note :    cant add to number of preferences here as this   *
.*                  may be called when the same preference goes over *
.*                  a page                                           *
.*********************************************************************
PTBH0000  MOVE      OPDHDESC,DESC30
          PRINT     *N,"Doctor : ",OPDFCODE,SP3,PACFNAME:
                    *N,"*------------------------------------------------":
                       "-------------------------------------------------":
                       "---------------------------------*":
                    *N,"| Preference | Description ",*47,"| Class",*75,"| Item":
                       *94,"| Item Description",*126,"| Qty |":
                    *N,"*------------------------------------------------":
                       "-------------------------------------------------":
                       "---------------------------------*":
                    *N,"| ",OPDFPREF,"  | ",DESC30," | ",OPDFCLSS,SP2,TDESC:
                       " | ",OPDFTYPE,SP1,OPDFITEM,"| ",ITEMDESC,"| ":
                       OPDFQTY,*132,"|"
.
          ADD       SIX,CLNO                * increment line number
.
PTBH9999  RETURN
+
.*********************************************************************
.*                  GINF0000                    Called by : REPT0000 *
.*        Obtains all the printable information about a record       *
.*        Is called once a valid item is read so all print routines  *
.*        will have valid information to print                       *
.*********************************************************************
GINF0000  MOVE      OPDFCODE,KEY6           * doctor code
          CALL      FDOC0000                * get docs name, PACFNAME
.
.         get preference description, OPDHDESC
.
          MOVE      SP70,OPDHDESC                * clear desc
          PACK      KEY18,OPDFCODE,OPDFPREF,OPDFHOSP,SP70 
.                                                * key of doct & pref & hosp
          CALL      RDOPDPH1                     * get description. OPDHDESC
.
.         get class description, TDESC
.
          MOVE      SP20,TDESC              * clear desc
          MATCH     SP3,OPDFCLSS
          GOTO      GINF1000 IF EQUAL       * no code
.
          PACK      KEY5,CATOG,OPDFCLSS     * key of OG and code
          CALL      RDCODE1                 * read codes file
.
.         get item description, ITEMDESC
.
GINF1000  MOVE      OPDFTYPE,FORM1          * get type as a form
          BRANCH    FORM1,GINF2000          * using items
.
.         item is a tray
.
          PACK      KEY18,OPDFITEM,OPDFHOSP,SP6
          PACK      OPTHDESC,SP20,SP20      * clear desc
          CALL      RDOPTRA1                * read tray file
          MOVE      OPTHDESC,ITEMDESC       * tray description
          MOVE      ANST,OPDFTYPE           * set up as tray
          GOTO      GINF9999
.
GINF2000  PACK      KEY15,OPDFITEM,SP70
          PACK      OPITDESC,SP30,SP30      * clear desc
          CALL      RDOPITE1                * read item file
          MOVE      OPITDESC,ITEMDESC       * item description
          MOVE      ANSI,OPDFTYPE           * set up as item
.
GINF9999  RETURN
+
.*********************************************************************
.*                  PPRE0000                    Called by : REPT2100 *
.*        Print all the information about a preference               *
.*        Called when have the same doctor but different preference  *
.*********************************************************************
PPRE0000  MOVE      OPDHDESC,DESC30
          PRINT     *1,"*------------------------------------------------":
                       "-------------------------------------------------":
                       "---------------------------------*":
                    *N,"| ",OPDFPREF,*14,"| ",DESC30,*47,"| ",OPDFCLSS,SP2:
                       TDESC,*75,"| ",OPDFTYPE,SP1,OPDFITEM:
                       *94,"| ",ITEMDESC,*126,"|",*128,OPDFQTY,*132,"|"
          ADD       TWO,CLNO                * inc line counter
.
PPRE9999  RETURN
+
.*********************************************************************
.*                  PCLS0000                    Called by : REPT4100 *
.*        Print the Class/Desc/Item/Desc/Quantity                    *
.*        Called when have the same doctor & pref but different class*
.*********************************************************************
PCLS0000  PRINT     *1,"|",*14,"|",*47,"| ",OPDFCLSS,SP2,TDESC:
                    *75,"| ",OPDFTYPE,SP1,OPDFITEM:
                    *94,"| ",ITEMDESC,*126,"|",*128,OPDFQTY,*132,"|"
          ADD       ONE,CLNO                * inc line counter
.
PCLS9999  RETURN
+
.*********************************************************************
.*                  PITM0000                    Called by : REPT6100 *
.*        Print the Item Code/Description/Quantity only              *
.*        Called when have the same doctor & preference & class      *
.*********************************************************************
PITM0000  PRINT     *1,"|",*14,"|",*47,"|",*75,"| ",OPDFTYPE,SP1,OPDFITEM:
                    *94,"| ",ITEMDESC,*126,"|",*128,OPDFQTY,*132,"|"
          ADD       ONE,CLNO                * inc line counter
.
PITM9999  RETURN
+
.*********************************************************************
.*                  COMM0000                    Called by : REPT4200 *
.*         Print comments for each comb of doctor & preference       *
.*********************************************************************
COMM0000  COMPARE   "56",CLNO
          GOTO      COMM1000 IF LESS
          CALL      UND132
          CALL      HEAD0000
          MOVE      OPDHDESC,DESC30
          PRINT     *N,"Doctor : ",OPDFCODE,SP3,PACFNAME:
                    *N,"*------------------------------------------------":
                       "-------------------------------------------------":
                       "---------------------------------*":
                    *N,"| Preference | Description ",*47,"| Class",*75,"| Item":
                       *94,"| Item Description",*126,"| Qty |":
                    *N,"*------------------------------------------------":
                       "-------------------------------------------------":
                       "---------------------------------*":
                    *N,"| ",OPDFPREF,"  | ",DESC30," |Comments : ";
          ADD       FIVE,CLNO
          GOTO      COMM1100
.
COMM1000  PRINT     *1,"|",*14,"|",*47,"|--------------------------------":
                    "----------------------------------------------------|"
          ADD       ONE,CLNO
.
          PRINT     *1,"|",*14,"|",*47,"|","Comments : ";
.
COMM1100  MOVE      PREVDOCT,OPPCCODE
          MOVE      LASTPREF,OPPCPREF
          PACK      KEY24,OPPCCODE,OPPCPREF,SP70
          CALL      RSOPPCM1
.
.         check if there is any comments for this doctor/preference combination
.
          CALL      RKOPPCM1
          BRANCH    OVRCD,COMM8000
.
          MATCH     PREVDOCT,OPPCCODE             * same doctor?
          GOTO      COMM8000 IF NOT EQUAL
.
          MATCH     LASTPREF,OPPCPREF             * same preference?
          GOTO      COMM8000 IF NOT EQUAL
          GOTO      COMM2100                      * yes, comments exist
.
COMM2000  CALL      RKOPPCM1
          BRANCH    OVRCD,COMM9999
.
          MATCH     PREVDOCT,OPPCCODE             * same doctor?
          GOTO      COMM9999 IF NOT EQUAL
.
          MATCH     LASTPREF,OPPCPREF             * same preference?
          GOTO      COMM9999 IF NOT EQUAL
.
COMM2100  COMPARE   ONE,OPPCLINE                  * 1st description line ?
          GOTO      COMM3000 IF NOT EQUAL
.
COMM2200  PRINT     *60,OPPCDESC,*132,"|"
          ADD       ONE,CLNO                      * increment # lines
          GOTO      COMM2000
.
COMM3000  COMPARE   "57",CLNO
          GOTO      COMM3100 IF LESS
          CALL      UND132
          CALL      HEAD0000
          MOVE      OPDHDESC,DESC30
          PRINT     *N,"Doctor : ",OPDFCODE,SP3,PACFNAME:
                    *N,"*------------------------------------------------":
                       "-------------------------------------------------":
                       "---------------------------------*":
                    *N,"| Preference | Description ",*47,"| Class",*75,"| Item":
                       *94,"| Item Description",*126,"| Qty |":
                    *N,"*------------------------------------------------":
                       "-------------------------------------------------":
                       "---------------------------------*":
                    *N,"| ",OPDFPREF,"  | ",DESC30," |Comments : ";
          ADD       FIVE,CLNO
          GOTO      COMM2200
.
COMM3100  PRINT     *1,"|",*14,"|",*47,"|",*60,OPPCDESC,*132,"|"
          ADD       ONE,CLNO                      * increment # lines
          GOTO      COMM2000
.
.         no comments at all
.
COMM8000  PRINT     *132,"|"
          ADD       ONE,CLNO
.
COMM9999  RETURN
+
.*********************************************************************
.*                  PEND0000                    Called by : REPT1600 *
.*        Print the ending details for a doctor             REPT9000 *
.*********************************************************************
PEND0000  PRINT     *1,"*------------------------------------------------":
                       "-------------------------------------------------":
                       "---------------------------------*",*N:
                    *N,"Total number of preferences for this doctor = ":
                       CNTPREF,"."
          ADD       THREE,CLNO              * increment line number
.
PEND9999  RETURN
+
.*********************************************************************
.
          INC       STD001IO                * standard IOs
.
          INC       PATCODKY                * "?" for codes
          INC       PATDOCKY                * "?" for doctors
.
          INC       OPRDPHIO/INC            * doctor preference header file 
          INC       OPRDPFIO/INC            * doctor preference details file
          INC       OPRITEIO/INC            * item file
          INC       OPRPCMIO/INC            * comments file
          INC       OPRTRAIO/INC            * tray file
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
+

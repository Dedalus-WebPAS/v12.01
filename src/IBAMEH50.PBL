.******************************************************************************
.* Program        : IBAMEH50                                                  *
.* Description    : Status Review Due Report                                  *
.*                                                                            *
.* Author         : ROD    (02/12/91)                                         *
.* Function       : To report the patients whose next legal status            *
.*                  review is due.                                            *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.00.01 19/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V10.04.02 15/06/2014  Jill Parkinson CAR 301639                     *
.*                  Added call to KILL0000 before exiting                     *
.*        V10.04.01 28/04/2014  Steve Armstrong  CAR 261630                   *
.*                  Mods for non-port based tempfile use.                     *
.******************************************************************************
.*        V10.01.01 10/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*        V9.02.00  25/10/2002  Lina Vo           SRF 22733                   *
.*                  Ported to V9                                              *
.******************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.******************************************************************************
.*        V5.07.01  12/09/1999  Steve Downing                                 *
.*                  Redundant code removed from end date calulations          *
.*        V5.07.B01 22/01/1999  Nicole Harrington  507 rebound                *
.*                  Mods to  line up date with header                         *
.*        V5.07.B01 19/01/1999  Nicole Harrington  507 rebound 052            *
.*                  Mods to print century                                     *
.*        V5.04.02  10/01/1997  Steve Armstrong  MEH Changes                  *
.*                  Mods to cater for new CMH MHVISTAT statuses               *
.*        V5.04.01  07/11/1996  Howellsy                                      *
.*                  Added Review Time & Legal Status Time.                    *
.*        V5.03.B1  23/06/1995    Justin Coates  SRF 134563                   *
.*                  changed to ignore patients who's status is informal       *
.*        V5.01.002 23/07/92  ROD                                             *
.*                  Use latest legal status not PTMXLS                        *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       MEHVI1FD/INC
          INC       MEHLEGFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATDO1FD/INC
          INC       PATCODFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
CMDLINE   DIM       80
CURRDATE  DIM       8
ENDDATE   DIM       8
FNAME     DIM       8
JWKYR     FORM      2
NDAYS     FORM      2
NOPATS    FORM      4
STRTCENT  FORM      2
STRTDAYS  FORM      3
STRTLEAP  FORM      1
STRTYR    FORM      2
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
CATLS     INIT      "LS"
.
. Temp file vars
MEHTMPA1  IFILE     SQL, FIXED=72, KEY="U1-8,9-38,39-46"
XXRDATE   DIM       8         1       Review date
XXNAME    DIM       30        9       Patients Name
XXADMN    DIM       8        39       Admission number
XXURNO    DIM       8        47       U/R Number
XXWARD    DIM       3        50       Ward
XXLSTAT   DIM       3        53       Legal Status
XXADOCT   DIM       16       56       Attending Doctor
.              Rec length    72
.
PRGID     INIT      "IBAMEH50"
PRGNAM    INIT      "Status Review Due Report"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
ML0500    CALL      CRET0000                      * create temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      NDAY0000                      * Get Days until next review
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000    * Yes, No, Cancel
.
ML5000    CALL      LOAD0000                      * Load data into temp file
.
          CALL      RPRT0000                      * Print Report
          GOTO      ML0500
.
ML9999    CALL      KILL0000
          CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"mehlegaf"
          OPEN      MEHLEGA1,"mehlegaf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"mehvi1af"
          OPEN      MEHVI1A3,"mehvi1af"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P1:24,*EL;
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD      * get todays date
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Date/Surname"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* NDAY0000  :  Get Number of days until next review                        *
.****************************************************************************
NDAY0000  DISPLAY   *P1:9,"Number of days until the next review : "
.
NDAY1000  KEYIN     *P1:24,*EL,*P40:9,*DV,UNDLN2:
                    *P40:9,*V2LON,NDAYS:
                    *P40:9,*DV,NDAYS
.
          IF        NDAYS < 0
                      BEEP
                      GOTO    NDAY1000
          ENDIF
.
          CALL      CDAT0000                      * convert to a date
.
NDAY9999  RETURN
+
.****************************************************************************
.* CDAT0000  :  Calculate an end date given a start date and duration       *
.****************************************************************************
CDAT0000  CALL      IBACLOCK                * convert start date to julian days
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CDAY,DD                 * set up start day
          MOVE      CMON,MM                 *              month
          MOVE      CYEAR,YY                *              year
          MOVE      CCENT,CC                *              century
.
          CALL      DMYCON                  * get julian day
          MOVE      JULDAY,STRTDAYS         * get current julian day
          MOVE      JULYR,STRTYR            * get current julian year
          MOVE      JULCC,STRTCENT          * get current julian century
          MOVE      LEAPYR,STRTLEAP         * get leap year flag (1=leap)
.
.         add the number of days
.
          ADD       NDAYS,STRTDAYS          * get the total days for the year
.
.         convert back to calender date
.
          MOVE      STRTDAYS,JWKDAY         * work days
          MOVE      JULYR,JWKYER            * work year
          MOVE      JULCC,JWKCC             * work century
          CALL      JULCON
.
.         set up end date
.
CDAT5000  PACK      ENDDATE,CC,YY,MM,DD
          REP       " 0",ENDDATE
.
CDAT9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  DISPLAY   *P30:24,"Printing...";
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      TEN,CLNO                      * line counter
          MOVE      ZERO,NOPATS                   * Number of patients printed
.
          CALL      HEAD80                        * Print 3 line header
.
          PRINT     *20,"Legal Status Review Due in ",NDAYS," Days",*N
.
          CALL      PTHD0000                      * print table headings
.
. Now loop thru temp file and print data
.
          PACK      KEY46,SP20,SP20,SP6
          CALL      RSTEMP1
RPRT1000  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT8000                * no more records
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT2000 IF LESS
.
. set up for new page
.
          CALL      UND80
          CALL      HEAD80
          PRINT     *20,"Legal Status Review Due in ",NDAYS," Days",*N
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
RPRT2000  UNPACK    XXRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *1,"|",XXURNO,*11,"| ",XXNAME,*41,"| ",XXWARD,"| ",XXLSTAT:
                    *53,"|",CPCDATE,"|",XXADOCT,*80,"|"
.
          ADD       ONE,CLNO                      * increment line counter
          ADD       ONE,NOPATS                    * increment no. patients
          GOTO      RPRT1000
.
. we have finished printing patient data so print end of report details
.
RPRT8000  CALL      UND80
          PRINT     *1,"Number of patients = ",NOPATS,*N:
                    *N,"*** End of Report ***"
.
RPRT9999  RETURN
+
.***************************************************************************
.*  LOAD0000  :  Load data into temp file                                  *
.***************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading data... [        ]"
          PACK      KEY16,CURRDATE,SP20
          CALL      RSMHVIS3
.
LOAD1000  CALL      RKMHVIS3                      * get next record
          BRANCH    OVRCD,LOAD9000                * no more records
.
          MATCH     MHVILSRV,ENDDATE              * in date range?
          GOTO      LOAD9000 IF LESS
.
          COMPARE   MHVISTAT,SIX                  * ignore cmh records
          GOTO      LOAD1000 IF LESS
.
. get master details
.
          PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD1000                * ignore if no admission rec
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD1000                * ignore if no master rec
.
          MOVE      ADOCTA,KEY6                   * get attending doctor name
          CALL      RDDOCT1
          IF        OVRCD = 1
            PACK      XXADOCT,SP30
          ELSE
            MOVE      DGNAME,ANS
            STRIP     DSNAME
            PACK      XXADOCT,DSNAME,SP1,ANS
          ENDIF
.
          MOVE      PURNO,XXURNO
          MOVE      AADMNO,XXADMN
          STRIP     PSNAME
          PACK      XXNAME,PSNAME,COMMA,SP1,PGNAME
          MOVE      AWARD,XXWARD
          MOVE      MHVILSRV,XXRDATE
.
. get latest legal status
. if it is informal, ignore the patient as they shouldn't have a 
. review date
.
          MOVE      SP3,XXLSTAT
          PACK      KEY21,MHVIADMN,ZED20
          CALL      RSMHLEG1
LOAD5000  CALL      RPMHLEG1
          BRANCH    OVRCD,LOAD5500
.
          MATCH     MHVIADMN,MHLEADMN             * same admission no.?
          GOTO      LOAD5500 IF NOT EQUAL
.         
          MOVE      MHLESTAT,XXLSTAT
          PACK      KEY5,CATLS,MHLESTAT
          CALL      RDCODE1
          BRANCH    OVRCD,LOAD1000
.
          MATCH     "I",TCINDC1
          GOTO      LOAD1000 IF EQUAL       * ignore Informal patients
.
LOAD5500  DISPLAY   *P47:24,MHVIADMN;
.
          PACK      KEY28,XXRDATE,XXNAME,MHVIADMN
          CALL      WRTEMP1
          GOTO      LOAD1000
.
LOAD9000  DISPLAY   *P1:24,*EL
.
LOAD9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
PTHD0000  CALL      UND80
          PRINT     *1,"|",*11,"|",*41,"|",*46,"|Legal|  Review  |",*80,"|",*N:
                    *1,"| U/R No. | Patient Name",*41,"|Ward|Stat.|   Date   |":
                    "Attending Doctor|"
          CALL      UND80
.
PTHD9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      TFILENAM                     * get new tempfile name
          MOVE      TFILNAME,FNAME
.
          CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 72 U1-8,9-38,39-46",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,FNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RSTEMP1   RESET     KEY46
          READ      MEHTMPA1,KEY46;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;XXRDATE,XXNAME,XXADMN,XXURNO,XXWARD,XXLSTAT,XXADOCT
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY46
          WRITE     MEHTMPA1,KEY46;XXRDATE,XXNAME,XXADMN,XXURNO,XXWARD,XXLSTAT:
                                   XXADOCT
          RETURN
.
.
          INC       STD001IO
.
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       MEHLEGIO/INC
          INC       MEHVI1IO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC

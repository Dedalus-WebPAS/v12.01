.*****************************************************************************
.*    Program      : IBAOUT24                                                *
.*    Description  : Block Outcoming for Outpatient Attendances              *
.*                                                                           *
.*    Author       : Max White                                               *
.*    Date         : 09/03/1995                                              *
.*                                                                           *
.*    Modifications:                                                         *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2        *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V9.02.01  31/01/2003  Sylvek Litewka  SRF 22709                    *
.*                  Modified to open the outpatient audit files with site    *
.*                  prefix.                                                  *
.*****************************************************************************
.*        V5.08.03  10/10/2000  Ebon Clements  5.08 Rebound                   *
.*                  Open controlf file in INIT label                         *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 13/03/2000  Steve Armstrong   5.08 Mods                  *
.*                  Recompiled for changes to OUTCLIFD (effective date)      *
.*****************************************************************************
.*        V5.07.00  30/10/1998    Jim Stathopoulos                           *
.*                  507 Changes                                              *
.*****************************************************************************
.*        V5.01.01  09/03/1995    Max White     SRF 441100                   *
.*                  Original Program.                                        *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       OUTAUXFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCONFD/INC
          INC       OUTCLIFD/INC
          INC       OUTOSFFD/INC
.
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC            * National HCP file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATPR1FD/INC
.
AUDIFLAG  FORM      1                        * audit flag
ATTENDED  INIT      "Attended"
BOOKED    INIT      "Booked  "
CODE      DIM       2
DIM40     DIM       40
DNOATTN   INIT      "Not Attd"
NOATTEND  INIT      "DNA     "
STATUS    DIM       8 
CLINDESE  DIM       40
CLINDESS  DIM       40
KYHOSP    DIM       3
KYCLINE   DIM       6
KYCLINS   DIM       6
MODFLAG   FORM      1                       * Modifying or Inserting Params ?
FDDATE    DIM       10
FNDDATA   FORM      1
FROMDATE  DIM       8
TDDATE    DIM       10
TODATE    DIM       8
ORIGSTAT  FORM      1                        * Saved OBASTAT
DISCHST   DIM       3
OUTCATT   DIM       3
OUTCDNA   DIM       3
OUTCOME   DIM       3 
PATNAME   DIM       26
PRDOB     DIM       8
SAVSESS   DIM       25
SAVCLIN   DIM       6
SAVHOSP   DIM       3
SAVSITE   DIM       6
SAVDATE   DIM       8
SAVTIME   DIM       5
TODAY     DIM       8
VSITE     DIM       6                        * Working Site
VCLIN     DIM       6                        * Working Clinic id
VDATE     DIM       8                        * Working Clinic date
VSTRT     DIM       5                        * Working clinic start time
VSLOT     DIM       3                        * Working Slot Number
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT24"
PRGNAM    INIT      "Block Outcoming for Outpatients"
VERSION   INIT      "V12.01.00"
+
.***********************************************************************
.*  ML0000  :  Mainline code                                           *
.***********************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * menu
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      PROC0000                      * process keyin fields
          BRANCH    EXIT,ML1000                   * nothing input
.
ML5000    CALL      OUTC0000                      * Default Outcomes
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.***********************************************************************
.*  INIT0000  :  Initialisation                                        *
.***********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P47:24,*EL,"Opening";
.
          DISPLAY   *P55:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P55:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P55:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P55:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P55:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P55:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P55:24,CFNAME
          CALL      OPOTBB11
.
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P55:24,CFNAME
          OPEN      OUTBOKA1,CFNAME
.
          DISPLAY   *P55:24,"outosfaf"
          OPEN      OUTOSFA1,"outosfaf"
          
          DISPLAY   *P55:24,"controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY1;*51,HOAUDA,HOAUDB,*64,HOAUDN
.
          BRANCH    HOAUDA,INIT6000          * Bookings audit off ?
          PACK      CFNAME,UID,FILAUDBA
          DISPLAY   *P55:24,CFNAME;
          OPEN      OUTAUDBA,CFNAME
.
INIT6000  BRANCH    HOAUDB,INIT7000          * Bookings detail audit off ?
          PACK      CFNAME,UID,FILAUBB1
          DISPLAY   *P55:24,CFNAME;
          OPEN      OUTAUBB1,CFNAME
.
INIT7000  MOVE      SP1,AUMODE
          MOVE      ANSN,AUTYPE              * Non-attendees
          CALL      STRTAUDX
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.***********************************************************************
.*  MENU0000  :  Main Menu                                             *
.***********************************************************************
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Perform Block Outcoming":
                    *P1:7,"Select option ? "
.
MENU1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION;
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999
          IF        MOPTION<>0
            BEEP
            GOTO      MENU1000
          ENDIF
          MOVE      ONE,EXIT                      * Exit
.
MENU9999  RETURN
+
.**************************************************************************
.*  PROC0000  :   process keyin fields                                    *
.**************************************************************************
PROC0000  CALL      DSCR0000                      * display screen
          MOVE      ZERO,MODFLAG                  * get all params initially
          MOVE      ZERO,CCITEM
.
PROC1000  CALL      KHOS0000                      * keyin hospital
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
          BRANCH    MODFLAG,PROC8900              * modifying single param ?
.
PROC2000  CALL      KCLF0000                      * keyin Clinic Id from
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
          BRANCH    MODFLAG,PROC8900              * modifying single param ?
.
PROC3000  CALL      KCLT0000                      * keyin Clinic Id to
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
          BRANCH    MODFLAG,PROC8900              * modifying single param ?
.
PROC4000  CALL      KDTF0000                      * keyin date from
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
          BRANCH    MODFLAG,PROC8900              * modifying single param ?
.
PROC5000  CALL      KDTT0000                      * keyin date to
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
          BRANCH    MODFLAG,PROC8900              * modifying single param ?
.
PROC6000  CALL      KOTA0000                      * keyin Attendance Outcome
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
          BRANCH    MODFLAG,PROC8900              * modifying single param ?
.
PROC7000  CALL      KOTD0000                      * keyin DNA Outcome
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
          BRANCH    MODFLAG,PROC8900              * modifying single param ?
.
PROC8000  CALL      KDSS0000                      * keyin Discharge Status
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
          BRANCH    MODFLAG,PROC8900              * modifying single param ?
.
. Select Item, (P)ost, (C)ancel ?
.
PROC8900  MOVE      ONE,MODFLAG                   * modify individual parameters
          CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      PROC8950 IF EQUAL             * (P)ost
          GOTO      PROC9000 IF LESS              * (C)ancel
.
          BRANCH    CCITEM,PROC1000:              * Hospital
                           PROC2000,PROC3000:     * Clinic Id Range
                           PROC4000,PROC5000:     * Date Range
                           PROC6000:              * Attendance Outcome
                           PROC7000:              * DNA Outcome
                           PROC8000               * Discharge Status
.
          BEEP
          GOTO      PROC8900                      * Invalid Response entered
.
PROC8950  MOVE      ZERO,EXIT                     * all is Ok. so continue
          GOTO      PROC9999
.
PROC9000  MOVE      ONE,EXIT                      * return to menu
.
PROC9999  RETURN
+
.**************************************************************************
.*  DSCR0000  :   Display screen layout                                   *
.**************************************************************************
DSCR0000  DISPLAY   *P1:9,*EF:
                    *P2:9,*V2LON,"1. ",*HOFF,"Hospital           :":
                    *P2:11,*V2LON,"2. ",*HOFF,"Clinic Id from     :":
                    *P2:12,*V2LON,"3. ",*HOFF,"          to       :":
                    *P2:14,*V2LON,"4. ",*HOFF,"Clinic Date from   :":
                    *P2:15,*V2LON,"5. ",*HOFF,"            to     :":
                    *P2:17,*V2LON,"6. ",*HOFF,"Attendance Outcome :":
                    *P2:18,*V2LON,"7. ",*HOFF,"DNA Outcome        :":
                    *P2:19,*V2LON,"8. ",*HOFF,"Discharge Status   :"
DSCR9999  RETURN
+
.***********************************************************************
.*  KHOS0000  :  Keyin hospital                                        *
.***********************************************************************
KHOS0000  MOVE      "9",EVERT
          MOVE      "26",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          DISPLAY   *P26:9,*EL
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS8000,KHOS9000
.
          MOVE      KEY3,KYHOSP
          DISPLAY   *P33:9,PTHSNAME
          MOVE      PTHSNAME,CNAME
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS8000  DISPLAY   *P26:9,*V2LON,"All"
          MOVE      SP3,KYHOSP
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KHOS9999  RETURN
+
.**************************************************************************
.*  KCLF0000  :  Keyin Clinic Id from                                     *
.**************************************************************************
KCLF0000  MOVE      "26",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          DISPLAY   *P26:11,*EL
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLF2000,KCLF9000,KCLF2000    * nothing or EXITCHAR
.
          MOVE      OCACLIN,KYCLINS                    * valid input
          DISPLAY   *P33:11,OCADESC
          PACK      CLINDESS,OCACLIN,SP2,OCADESC
          GOTO      KCLF5000
.
KCLF2000  MOVE      SP6,KYCLINS                        * nothing input
          DISPLAY   *P26:11,*V2LON,"Start"
          MOVE      "Start",CLINDESS                   * print var
.
. check if valid range
.
KCLF5000  MATCH     KYCLINE,SP8                        * Any Clinic Id to ?
          GOTO      KCLF8000 IF EQUAL
.
          MATCH     KYCLINS,KYCLINE
          GOTO      KCLF8000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Clinic Id range.  ";
          CALL      HITENTER
          GOTO      KCLF0000
.
KCLF8000  MOVE      ZERO,EXIT                          * All is howdy dudey
          GOTO      KCLF9999
.
KCLF9000  MOVE      ONE,EXIT                           * EXITCHAR
.
KCLF9999  RETURN
+
.**************************************************************************
.*  KCLT0000  :  Keyin Clinic Id to                                       *
.**************************************************************************
KCLT0000  MOVE      "26",ECOL
          MOVE      "12",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          DISPLAY   *P26:12,*EL
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLT6000,KCLT9000,KCLT6000    * nothing or EXITCHAR
.
          MOVE      OCACLIN,KYCLINE                    * valid input
          DISPLAY   *P33:12,OCADESC
          PACK      CLINDESE,OCACLIN,SP2,OCADESC
.
. check if valid range
.
          MATCH     KYCLINS,KYCLINE
          GOTO      KCLT8000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Clinic Id range.  ";
          CALL      HITENTER
          GOTO      KCLT0000
.
KCLT6000  MOVE      "zzzzzz",KYCLINE                   * nothing input
          DISPLAY   *P26:12,*V2LON,"Finish"
          MOVE      "Finish",CLINDESE
.
KCLT8000  MOVE      ZERO,EXIT                          * All is howdy dudey
          GOTO      KCLT9999
.
KCLT9000  MOVE      ONE,EXIT                           * EXITCHAR
.
KCLT9999  RETURN
+
.**************************************************************************
.*  KDTF0000  :   Keyin date from                                         *
.**************************************************************************
KDTF0000  PACK      TODAY,CCC,CYY,CMM,CDD                     * get todays date
          REP       " 0",TODAY
.
          UNPACK    TODAY,CCENT                               * default cent
          UNPACK    SP6,CYEAR,CMON,CDAY                       * no default date
          MOVE      "26",CCOL
          MOVE      TEN4,CVERT
          MOVE      ZERO,CDEFDTE
.
KDTF1000  CALL      KEYDATE
          BRANCH    OVRCD,KDTF1000                            * anything input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          PACK      FDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR  * display var
          REP       " 0",FROMDATE 
          REP       " 0",FDDATE 
.
          MATCH     FROMDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date can't be in the future.  ";
            CALL      HITENTER
            GOTO      KDTF0000
          ENDIF
.
          MATCH     TODATE,SP8
          GOTO      KDFT8000 IF EQUAL
.
          MATCH     FROMDATE,TODATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Invalid Clinic Date range.   ";
            CALL      HITENTER
            GOTO      KDTF0000
          ENDIF
.
KDFT8000  MOVE      ZERO,EXIT                                 * Valid range
          GOTO      KDTF9999
.
KDTF9000  MOVE      ONE,EXIT                                  * nothing input
.
KDTF9999  RETURN
+
.**************************************************************************
.*  KDTT0000  :   Keyin date to                                           *
.**************************************************************************
KDTT0000  PACK      TODAY,CCC,CYY,CMM,CDD                     * get todays date
          REP       " 0",TODAY
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY            * fromdate default
          MOVE      "26",CCOL
          MOVE      TEN5,CVERT
          MOVE      ZERO,CDEFDTE
.
KDTT6000  CALL      KEYDATE
          BRANCH    CQUEST,KDTT6000
          BRANCH    OVRCD,KDTT9000                            * anything input
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          PACK      TDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR  * display var
          REP       " 0",TODATE 
          REP       " 0",TDDATE 
.
          MATCH     TODATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date can't be in the future.   ";
            CALL      HITENTER
            GOTO      KDTT0000
          ENDIF
.
          MATCH     FROMDATE,TODATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Invalid Clinic Date range.   ";
            CALL      HITENTER
            GOTO      KDTT0000
          ENDIF
.
          MOVE      ZERO,EXIT                                 * Valid range
          GOTO      KDTT9999
.
KDTT9000  MOVE      ONE,EXIT                                  * nothing input
.
KDTT9999  RETURN
+
.*****************************************************************************
.*                              KOTA0000                                     *
.*                     Keyin the Outcome for Attendances                     *
.*****************************************************************************
KOTA0000  MOVE      ZERO,EXIT
          MOVE      ONE,CKYIMAND
          MOVE      SP3,CKYIDEF3
          MOVE      TEN7,EVERT
          MOVE      "26",ECOL
          MOVE      "OZ",CODE
.
          DISPLAY   *PECOL:EVERT,*EL
.
          CALL      PATCODKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KOTA9999
          ENDIF
.
          MATCH     "1",TCINDC1
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"Cannot Enter a DNA Outcome.  ";
            CALL      HITENTER
            GOTO      KOTA0000
          ENDIF
.
          MOVE      ACODE,OUTCATT
          DISPLAY   *P33:EVERT,TDESC
.
          MOVE      ZERO,EXIT
.
KOTA9999  RETURN
+
.*****************************************************************************
.*                              KDSS0000                                     *
.*                     Keyin the Default Discharge Status                    *
.*****************************************************************************
KDSS0000  MOVE      ZERO,EXIT
          MOVE      ONE,CKYIMAND
          MOVE      SP3,CKYIDEF3
          MOVE      TEN9,EVERT
          MOVE      "26",ECOL
          MOVE      "DO",CODE
.
          DISPLAY   *PECOL:EVERT,*EL
.
          CALL      PATCODKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KDSS9999
          ENDIF
.
          MOVE      ACODE,DISCHST
          DISPLAY   *P33:EVERT,TDESC
.
          MOVE      ZERO,EXIT
.
KDSS9999  RETURN
+
.*****************************************************************************
.*                              KOTD0000                                     *
.*                      Keyin the Outcome for DNA's                          *
.*****************************************************************************
KOTD0000  MOVE      ZERO,EXIT
          MOVE      ONE,CKYIMAND
          MOVE      SP3,CKYIDEF3
          MOVE      TEN8,EVERT
          MOVE      "26",ECOL
          MOVE      "OZ",CODE
.
          DISPLAY   *PECOL:EVERT,*EL
.
          CALL      PATCODKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KOTD9999
          ENDIF
.
          MATCH     "1",TCINDC1
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"This is not a DNA Outcome.  ";
            CALL      HITENTER
            GOTO      KOTD0000
          ENDIF
.
          MOVE      ACODE,OUTCDNA
          DISPLAY   *P33:EVERT,TDESC
.
          MOVE      ZERO,EXIT
.
KOTD9999  RETURN
+
.***********************************************************************
.*  OUTC0000  :  Default Outcomes & Print Report                       *
.***********************************************************************
OUTC0000  DISPLAY   *P1:24,*EL,*P35:24,"Scanning...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,FNDDATA
          MOVE      SP3,SAVHOSP
          MOVE      SP6,SAVSITE
          MOVE      SP6,SAVCLIN
          MOVE      SP8,SAVDATE
          MOVE      SP5,SAVTIME
.
          PACK      KEY34,KYHOSP,IDDD,SP20,SP20
          CALL      RSOTOSF1
OUTC1000  CALL      RKOTOSF1
          BRANCH    OVRCD,OUTC9000
.
          MATCH     SP3,KYHOSP
          IF        !@EQUAL
            MATCH     KYHOSP,OTOSHOSP               * same hospital?
            GOTO      OUTC9000 IF NOT EQUAL
.
            MATCH     IDDD,OTOSSITE                 * same site?
            GOTO      OUTC9000 IF NOT EQUAL
          ELSE
            MATCH     IDDD,OTOSSITE                 * same site?
            GOTO      OUTC1000 IF NOT EQUAL
          ENDIF
.
          MATCH     KYCLINS,OTOSCLID                * clinic Id in range?
          GOTO      OUTC1000 IF LESS
.
          MATCH     OTOSCLID,KYCLINE
          GOTO      OUTC1000 IF LESS
.
          MATCH     FROMDATE,OTOSDATE               * before period ?
          GOTO      OUTC1000 IF LESS                * yes, ignore
.
          MATCH     OTOSDATE,TODATE                 * after period ?
          GOTO      OUTC1000 IF LESS                * yes, ignore
.
. now get all corresponding boka records
.
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP30
          CALL      RDSBOKA1
OUTC2000  CALL      RDKBOKA1
          BRANCH    OVRCD,OUTC1000
.
          DISPLAY   *P50:24,OTOSHOSP,SP1,OBACLIN,OBADATE,OBATIME
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,KEY28
          GOTO      OUTC1000 IF NOT EQUAL
.
. make sure slot is booked/attended/Not attended
.
          IF        (OBASTAT<>1) & (OBASTAT<>4) & (OBASTAT<>5)
            GOTO      OUTC2000
          ENDIF
.
. save outbokaf key fields
.
          MOVE      OBASITE,VSITE
          MOVE      OBACLIN,VCLIN
          MOVE      OBADATE,VDATE
          MOVE      OBASTRT,VSTRT
          MOVE      OBASLOT,VSLOT
.
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,OUTC2000
.
          MATCH     SP3,OTBBOUTC                  * outcome code blank?
          GOTO      OUTC2000 IF NOT EQUAL         * no, ignore
.
. get patient details
.
          MATCH     ZEROUR,OBAURNO
          IF        @EQUAL
            PACK      KEY8,OBAOUTNO
            CALL      RDPRAM1
            BRANCH    OVRCD,OUTC2000
          ELSE
            PACK      KEY8,OBAURNO
            CALL      RDMAST1
            BRANCH    OVRCD,OUTC2000
          ENDIF
.
. insert default outcome (if specified)
.
          MOVE      OBASTAT,ORIGSTAT        * Save OBASTAT
          MOVE      SP3,OUTCOME
.
          COMPARE   FOUR,OBASTAT
          GOTO      OUTC3000 IF EQUAL
.
. Outcome DNA (OBASTAT = 1 or 5)
.
          MATCH     SP3,OUTCDNA             * DNA Outcomes Required ?
          GOTO      OUTC5000 IF EQUAL       * No, don't update
.
          MOVE      OUTCDNA,OUTCOME         * Default DNA Outcome
.
          GOTO      OUTC4000
.
. Outcome Attendance (OBASTAT = 4)
.
OUTC3000  MATCH     SP3,OUTCATT             * Attendance Outcomes Required ?
          GOTO      OUTC5000 IF EQUAL       * No, don't update
.
          MOVE      OUTCATT,OUTCOME         * Default Attendance Outcome
.
. Update Booking record(s) with defaulted outcome/status/discharge status
.
OUTC4000  CALL      UPBB0000                * update outbb1af details
          CALL      UPBA0000                * update outbokaf details
.
. check if we need a new page
.
OUTC5000  MATCH     SAVHOSP,OTOSHOSP
          IF        @EQUAL
            MATCH     SAVSITE,OTOSSITE
            IF        @EQUAL
              MATCH     SAVCLIN,OTOSCLID
              IF        @EQUAL
                 COMPARE   "55",CLNO
                 IF        @LESS
                   MATCH     OTOSDATE,SAVDATE 
                   IF        !@EQUAL
                     CALL      UND80
                     CALL      TABL0000
                     GOTO      OUTC8000
                   ELSE
                     MATCH     OTOSTIME,SAVTIME
                     GOTO      OUTC8000 IF EQUAL
                     CALL      UND80
                     CALL      TABL0000
                     GOTO      OUTC8000
                   ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
.          MATCH     KEY25,SAVSESS
.          IF        @EQUAL
.            COMPARE   "55",CLNO
.            GOTO      OUTC8000 IF LESS
.          ENDIF
.
          CALL      PAGE0000                      * print a new page
.
. print a record
.
OUTC8000  CALL      FRMT0000                      * format data for printing
.
          PRINT     *1,"|  ",OBASLOT,*8,"| ",OBAURNO,*19,"| ",PATNAME,*48:
                    "| ",PRDOB,*59,"| ",STATUS,SP3,OUTCOME,*80,"|"
...       PACK      SAVSESS,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME   * save session
          MOVE      OTOSHOSP,SAVHOSP
          MOVE      OTOSSITE,SAVSITE
          MOVE      OTOSCLID,SAVCLIN
          MOVE      OTOSDATE,SAVDATE
          MOVE      OTOSTIME,SAVTIME
.
          ADD       ONE,CLNO
          MOVE      ONE,FNDDATA  
          GOTO      OUTC2000
.
. no more records to print
.
OUTC9000  IF        FNDDATA=1
            CALL      UND80
            PRINT     *N,*1,"*** End of report ***" 
            GOTO      OUTC9999
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,"No data found.  ";
          CALL      HITENTER
.
OUTC9999  RETURN
+
.****************************************************************************
.*                              UPBB0000                                    *
.*              Update bookings details file outbb1af                       *
.****************************************************************************
UPBB0000  MOVE      TWO,AUDIFLAG
          CALL      AUDOTBOB                * Write a 'before' audit record
.
          MOVE      OUTCOME,OTBBOUTC
          PACK      KEY8,OBAOUTNO
          CALL      UPBOKB1                 * Update with outcome details
.
          MOVE      THREE,AUDIFLAG
          CALL      AUDOTBOB                * Write a 'change' audit record
.
UPBB9999  RETURN
+
.****************************************************************************
.*                              UPBA0000                                    *
.*                Update bookings file outbokaf (if required)               *
.****************************************************************************
UPBA0000  PACK      KEY28,VSITE,VCLIN,VDATE,VSTRT,VSLOT
.
          CALL      RDBOKA1
          BRANCH    OVRCD OF UPBA8000           * Bookings record disappeared *
.
          MATCH     OBADISCH,SP3
          IF        !@EQUAL & ORIGSTAT <> 1
            GOTO      UPBA9000                  * No update required
          ENDIF
.
          MOVE      TWO,AUDIFLAG                * set flag to write a b audit
          CALL      AUDOTBOA                    * write audit
.
          IF        ORIGSTAT = 1
            MOVE      FIVE,OBASTAT              * Update Booked to DNA Status
          ENDIF
.
          MATCH     SP3,OBADISCH                * Discharge Status present ?
          GOTO      UPBA2000 IF NOT EQUAL       * Yes, leave as is.
.
          MOVE      DISCHST,OBADISCH            * Update discharge status
.
UPBA2000  CALL      UPBOKA1
.
          MOVE      THREE,AUDIFLAG              * set flag to write a c audit
          CALL      AUDOTBOA                    * write audit
.
          COMPARE   ONE,ORIGSTAT                * Status changed from booked
          GOTO      UPBA9000 IF NOT EQUAL       * No, Non-att. audit not reqd.
.
.        Write Non-attendance Audit
.
          PACK      AUITEM,OBASITE,SLASH,OBACLIN,SLASH,OBADATE,SLASH:
                           OBASTRT,SP20
.
          BRANCH    OBASTAT TO UPBA3000         * Booked slot *
          PACK      AUCONBEF,OBASLOT,SLASH,DNOATTN,SLASH,OBAATTN
          GOTO      UPBA4000
.
UPBA3000  PACK      AUCONBEF,OBASLOT,SLASH,BOOKED
.
UPBA4000  PACK      AUCONAFT,OBASLOT,SLASH,DNOATTN,SLASH,OBAATTN:
                    SLASH,TDESC
          CALL      WRITAUDX
          GOTO      UPBA9000
.
UPBA8000  MOVE      ONE,EXIT
          GOTO      UPBA9999
.
UPBA9000  MOVE      ZERO,EXIT
          GOTO      UPBA9999
.
UPBA9999  RETURN
+
.*************************************************************************
.*  FRMT0000  :  Format data for printing                                *
.*************************************************************************
FRMT0000  MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      SP4,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME              * shorten name
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,PRDOB
.
          LOAD      STATUS,ORIGSTAT,BOOKED,SP1,SP1,ATTENDED,NOATTEND
.
FRMT9999  RETURN
+
.*************************************************************************
.*  PAGE0000  :  print report header                                     *
.*************************************************************************
PAGE0000  MATCH     SP3,KYHOSP
          IF        @EQUAL
            PACK      KEY3,OTOSHOSP
            CALL      RDPTHSP1
            MOVE      PTHSNAME,CNAME
          ENDIF
.
          CALL      HEAD80
.
          PRINT     *18,"  Clinic Id from : ",CLINDESS:
                    *N,*18,"              to : ",CLINDESE:
                    *N,*18,"Attendances from : ",FDDATE," to ",TDDATE
.
          MOVE      FIVE,CLNO
          CALL      TABL0000
.
PAGE9999  RETURN
+
.*************************************************************************
.*  TABL0000  :  print Table                                             *
.*************************************************************************
TABL0000
.
. print table heading
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *N,*1,"Clinic: ",OBACLIN,SP3,"Date: ",CPCDATE,SP3:
                    "Time: ",OTOSTIME
          CALL      UND80
          PRINT     *1,"| Slot |  U/R No. | Patient Name",*48,"|  D.O.B.  | ":
                    "Status     Outcome",*80,"|"
          CALL      UND80
.
          ADD      FIVE,CLNO
.
TABL9999  RETURN
+
.****************************************************************************
.         Audits for patient outpatients bookings file (outaudba)
.****************************************************************************
AUDOTBOA  BRANCH    HOAUDA,OTBOA999
.
          COMPARE   THREE,AUDIFLAG     * is it a after ?
          GOTO      OTBOA200 IF EQUAL
.
          COMPARE   FIVE,AUDIFLAG      * is it a delete ?
          GOTO      OTBOA600 IF EQUAL
.
          CALL      IBACLOCK           * get current date
          PACK      OTBAAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OTBAAUDD
          CLOCK     TIME,OTBAAUDT      * clock current time
          MOVE      ONE,OTBAAUDS       * set print status
          MOVE      PASSCODE,OTBAAUDO  * get user ID
          MOVE      PORT,OTBAAUDP      * get port number
.
          BRANCH    AUDIFLAG,OTBOA200,OTBOA200,OTBOA200,OTBOA200,OTBOA600
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set";
          GOTO      OTBOA999
.
OTBOA200  MOVE      AUDIFLAG,OTBAAUDR
          REPLACE   "1A2B3C4D",OTBAAUDR
          PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,OTBAAUDR  
          CALL      AWOTBOA           * write to audit file
          GOTO      OTBOA999
.
OTBOA600  PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,ANSB
          CALL      ADOTBOA           * delete audit file
.
OTBOA999  RETURN
+
.****************************************************************************
.                                 AUDOTBOB
.    Audits for patient outpatients bookings file (outaudba)
.****************************************************************************
AUDOTBOB  BRANCH    HOAUDB,OTBOB999
.
          COMPARE   THREE,AUDIFLAG     * is it a delete ?
          GOTO      OTBOB200 IF EQUAL
.
          COMPARE   FIVE,AUDIFLAG      * is it a delete ?
          GOTO      OTBOB600 IF EQUAL
.
          CALL      IBACLOCK           * get current date
          PACK      OTBBAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OTBBAUDD
          CLOCK     TIME,OTBBAUDT      * clock current time
          MOVE      ONE,OTBBAUDS       * set print status
          MOVE      PASSCODE,OTBBAUDO  * get user ID
          MOVE      PORT,OTBBAUDP      * get port number
.
          BRANCH    AUDIFLAG,OTBOB200,OTBOB200,OTBOB200,OTBOB200,OTBOB600
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set";
          GOTO      OTBOB999
.
OTBOB200  MOVE      AUDIFLAG,OTBBAUDR
          REPLACE   "1A2B3C4D",OTBBAUDR
          PACK      KEY19,OTBBAUDD,OTBBAUDT,OTBBAUDP,OTBBAUDR  
          MOVE      IDDD,OTBBSITE
          CALL      AWOTBOB           * write to audit file
          GOTO      OTBOB999
.
OTBOB600  PACK      KEY19,OTBBAUDD,OTBBAUDT,OTBBAUDP,ANSB
          CALL      ADOTBOB           * delete audit file
.
OTBOB999  RETURN
+
          INC       STD001IO
.
          INC       KYOUTCLI
          INC       OUTDSCLN
          INC       OUTAUXIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTOSFIO/INC
.
          INC       PATCODKY
          INC       PATHSPIO/INC
          INC       PATHSPKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       PATMA1IO/INC
          INC       PATPR1IO/INC
................................................................................

.******************************************************************************
.* System    :   System Performance                                           *
.* Program   :   SYSTPERF                                                     *
.* Desc      :   Measure system processing time                               *
.******************************************************************************
.* Date      :   13/09/2012                                                   *
.* Author    :   Ania Pietrusewicz                                            *
.* Function  :   INPUT: Number of rows to process (N).                        *
.*               1. Creates temp table.                                       *
.*               2. Writes N rows from pattran into the temp table.           *
.*               3. Does additional processing for each record.               *
.*               OUTPUTS: Time taken for each step above                      *
.* Mods      :                                                                *
.*               V10.03.01 13/09/2012 Ania CAR 273211                         *
.*                         Created Program                                    *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       PMSPX2FD/INC
          INC       WEBSECFD/INC
.
.
. 	  TEMPORARY FILE DEFINITION
. 	  -------------------------
.
.         Filename: tmpfile
.
TMPFILE1  IFILE SQL, FIXED=59, KEY="U1-7"
.
.NAME     TYPE    LENGTH    PHYSICAL    START    DESCRIPTION
.----     ----    ------    --------    -----    -----------
.DROWCNT  DIM       7           7         1      Used as a unique key
.TADM     DIM       8           8         8      Admission number
.TURNO    DIM       8           8         16     U/R number
.TWARD    DIM       3           3         24     Transfer ward number
.TBED     DIM       3           3         27     Transfer bed number
.TDATE    DIM       8           8         30     Transfer date
.TTIME    DIM       8           8         38     Transfer time
.TMOVE    DIM       1           1         46     Movement type
.TATYPE   DIM       3           3         47     Admission type (CAT A)
.TADOCT   DIM       6           6         50     Attending doctor code
.TDEPT    DIM       3           3         56     Department code (CAT AC)
.
.End of record                             59
.
.
.         REDEFINED VARIABLES 
.         -------------------
.
ROWCNT    FORM      7                            Redefines DROWCNT
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CMDLINE   DIM       80
CURRTIME  DIM       8
DROWCNT   DIM       7
FILENAME  DIM       8
NROWS     FORM      7
.
.
. PROGRAM CONSTANTS
. -----------------

INDEX1    INIT      " 59 U1-7"
ISBUILD   INIT      "isbuild "
ISCLEAR   INIT      "isclear "
ISERASE   INIT      "iserase "
.
PRGID     INIT      "SYSTPERF"
PRGNAM    INIT      "System Performance Test"
VERSION   INIT      "V12.01.00"
.
+
.******************************************************************************
.*                              MAIN0000                                      *
.*                      Controlling Logic (Mainline code)                     *
.******************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * If 0 chosen, EXIT is set to 1
.
          CALL      INPT0000               * select N records to process
          BRANCH    EXIT,MAIN1000          * If 0 chosen, go back to options
.
          CALL      CREA0000               * Create temp table
.
          CALL      WRIT0000               * Write records to temp table
.
          CALL      PROC0000               * Perform dummy record processing
.
          CALL      READ0000               * Perform read only from PATTRAN
.
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.******************************************************************************
.*                                INIT0000             Called by: ML0000      *
.*                             initialisation                                 *
.*  The initialisation routine is used to display headings and open files.    *
.******************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,"opening:":
                    *P64:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pattranf"
          OPEN      PATTRAN1,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"websecaf"
          OPEN      WEBSECA1,"websecaf"     
.
          PACK      FILENAME,FILENAME,PORT
.
INIT9999  RETURN
+
.******************************************************************************
.*                                OPTN0000             Called by: MAIN0000    *
.*                        Get user options or exit                            *                                    
.*                   Select 0 to exit or 1 to run program                     *
.******************************************************************************
.
OPTN0000  DISPLAY   *P1:24,*EL,*V2LON,"0",*HOFF," = Exit ":
                    *V2LON,"1",*HOFF," = Run"
.
OPTN1000  KEYIN     *P1:23,*EL,"Select option: ",*V2LON,COPTION,*HOFF
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL           
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                INPT0000                Called by: MAIN1000 *
.*      Get number of rows to process or zero to go back to main menu         *
.******************************************************************************
.
INPT0000  KEYIN     *P1:24,*EL,*V2LON,"0",*HOFF," = Back to menu prompt":
                    *P1:23,*EL,"Number of rows to process:",*DE,*V2LON,NROWS
.
          COMPARE   ZERO,NROWS
          GOTO      INPT9500 IF EQUAL           
.
INPT9000  MOVE      ZERO,EXIT
          GOTO      INPT9999
.
INPT9500  MOVE      ONE,EXIT
.
INPT9999  RETURN
+

.******************************************************************************
.*                               CREA0000                 Called by: MAIN0000 *
.*                      Creates a temporary table                             *
.******************************************************************************
.
CREA0000  CLOCK     TIME,CURRTIME
          DISPLAY   *P1:6,*EF,*V2LON,"Creating temporary table",*HOFF:
                    *P1:7,"Started at: ",CURRTIME
.
          PACK      CMDLINE,ISERASE,FILENAME
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,ISBUILD,FILENAME,INDEX1
          EXECUTE   CMDLINE,TASKID
.
          DISPLAY   *P56:24,"Opening:",FILENAME
          OPEN      TMPFILE1,FILENAME

CREA1000  MOVE      SP7,KEY7
          READ      TMPFILE1,KEY7;;
          READKS    TMPFILE1;ANS
          GOTO      CREA2000 IF OVER
.
          MOVE      ROWCNT,KEY7
          DELETE    TMPFILE1,KEY7
          GOTO      CREA1000               
.
CREA2000  CLOCK     TIME,CURRTIME
          DISPLAY   *P1:8,"Finished at:",CURRTIME
.
CREA9999  RETURN
+
.******************************************************************************
.*                               WRIT0000                 Called by: MAIN0000 *
.*            Writes records from pattranf to temporary table                 *
.******************************************************************************
.
WRIT0000  MOVE      ZERO,ROWCNT
.
          CLOCK     TIME,CURRTIME
          DISPLAY   *P1:10,*V2LON,"Writing temporary records",*HOFF:
                    *P1:11,"Started at: ",CURRTIME
.
WRIT1000  PACK      KEY30,SP30
          CALL      RDSTRAN1
WRIT2000  CALL      RDKTRAN1
          BRANCH    OVRCD,WRIT1000
.
          ADD       ONE,ROWCNT
          MOVE      ROWCNT,DROWCNT
.
          IF        (ROWCNT%10000)=1
            DISPLAY   *P56:24,*EL,"Writing U/R: ",TURNO
          ENDIF
.
          WRITE     TMPFILE1,KEY7;DROWCNT,TADMN,TURNO,TWARD,TBED:
                             TDATE,TTIME,TMOVE,TATYPE,TADOCT,TDEPT       
.     
          COMPARE   NROWS,ROWCNT
          GOTO      WRIT2000 IF NOT EQUAL        
.
          CLOCK     TIME,CURRTIME
          DISPLAY   *P1:12,"Finished at:",CURRTIME
.
WRIT9999  RETURN
+
.******************************************************************************
.*                               PROC0000                 Called by: MAIN0000 *
.*   Performs dummy reads on multiple tables using U/Rs from the temp table   *
.******************************************************************************
.
PROC0000  MOVE      ZERO,ROWCNT
.
          CLOCK     TIME,CURRTIME
          DISPLAY   *P1:14,*V2LON,"Processing temporary records",*HOFF:
                    *P1:15,"Started at: ",CURRTIME
.
PROC1000  MOVE      SP7,KEY7
          READ      TMPFILE1,KEY7;;
PROC2000  READKS    TMPFILE1;DROWCNT,TADMN,TURNO,TWARD,TDATE:
                             TTIME,TMOVE,TATYPE,TADOCT,TDEPT
          GOTO      PROC1000 IF OVER
.
          MOVE      DROWCNT,ROWCNT
          ADD       ONE,ROWCNT
.
          IF        (ROWCNT%10000)=1
            DISPLAY   *P56:24,*EL,"Processing U/R:",TURNO
          ENDIF
.
          PACK      KEY5,ANSA,SP1,TATYPE
          CALL      RDCODE1
.
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
.         
          PACK      KEY8,TURNO
          CALL      RDMAST1
.
          PACK      KEY8,TURNO
          CALL      RDPTMSX1
.
          PACK      KEY6,TADOCT
          CALL      RDDOCT1
.
          PACK      KEY10,PTTRUCID
          CALL      RDWBSE1
.
          COMPARE   NROWS,ROWCNT
          GOTO      PROC2000 IF NOT EQUAL
.
          CLOCK     TIME,CURRTIME
          DISPLAY   *P1:16,"Finished at:",CURRTIME
.
          CLOSE     TMPFILE1,DELETE
.
PROC9999  RETURN
+
.******************************************************************************
.*                               READ0000                 Called by: MAIN0000 *
.*     Performs dummy reads on PATTRAN to measure database read speeds.       *
.******************************************************************************
.
READ0000  MOVE      ZERO,ROWCNT
.
          CLOCK     TIME,CURRTIME
          DISPLAY   *P1:18,*V2LON,"Processing database reads",*HOFF:
                    *P1:19,"Started at: ",CURRTIME
.
READ1000  PACK      KEY30,SP30
          CALL      RDSTRAN1
READ2000  CALL      RDKTRAN1
          BRANCH    OVRCD,READ1000
.
          ADD       ONE,ROWCNT
          COMPARE   NROWS,ROWCNT
          GOTO      READ2000 IF NOT EQUAL          
.
          CLOCK     TIME,CURRTIME
          DISPLAY   *P1:20,"Finished at:",CURRTIME:
                    *P1:22,"Processed ",NROWS," rows.",*N,*N,*EF
.
          CALL      HITENTER
.
READ9999  RETURN

. =============================================================================
.  I/O Includes
. =============================================================================
.
          INC       STD001IO1 
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PMSPX2IO/INC
          INC       WEBSECIO/INC
+

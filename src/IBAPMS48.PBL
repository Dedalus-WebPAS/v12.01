.******************************************************************************
.* System   :  Patient                                                        *
.* Program  :  IBAPMS48                                                       *
.* Desc     :  Possible U/R Duplication Report                                *
.******************************************************************************
.* Date     :  18/09/89                                                       *
.* Author   :  S O'Gorman                                                     *
.* Comment  :                                                                 *
.* Mods     :                                                                 *
.*        V10.08.01 16/11/2016  Ebon Clements  TSK 0323826                    *
.*                  Removed upper case and squeeze from surname - BASE1000    *
.*        V10.05.01 11/12/2014  Ebon Clements  CAR 244435                     *
.*                  Fixed alias display of DOB and sex - MTCH2900             *
.*        V10.03.01 26/11/2013  Peter Vela CAR 244435                         *
.*                  Fixed alias display MTCH2900                              *
.*        V10.02.01 22/06/2011  Steve Armstrong   CAR 240722                  *
.*                  Mods to cater for changes to PATGSRFD:                    *
.*                       - GSRSNAM & GSRGNAM extended to 40 chars.            *
.*                       - PTGSGNM2 added.                                    *
.*                       - all indexes changed in length.                     *
.******************************************************************************
.*        V9.02.00  08/09/2001  Glenn Saunders                                *
.*                  Change keys for patgsrn reads to reflect enhanced indexes.*
.******************************************************************************
.*        V5.10.B01 28/03/2001  Glenn Saunders                                *
.*                  Remove access to PTCNSNDX and associated processing.      *
.*                  Remove all references to PATSUR file (old surname file).  *
.******************************************************************************
.*        V5.09.B02 16/01/2001 J.Tan                                          *
.*                  Mods to check record in the Link file                     *
.******************************************************************************
.*        V5.08.02  25.10.2000 J.Tan                                          *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                   Fixed global recompile bugs                              *
.******************************************************************************
.*             V5.07.01  26/07/1999  Steve Armstrong                          *
.*                       Added new features to customise matching             *
.*                       Rewrote most of the code to standard                 *
.*             V5.07.B02 27/01/1999 Jim Stathopoulos                          *
.*                       Report Modification                                  *
.*                  09/11/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*             V5.03.01  15/12/95 J.Tan     SRF 613207                        *
.*                       Fixed re-setting the todate in extracting data       *
.*             V5.03.B2  21/06/1995    Matt Surridge  SRF 640231              *
.*                  Fixed th branch to next record after printing a match.    *
.*             V5.03.B1  29/05/1995    Justin Coates  SRF 640231              *
.*                  changed to check for new page after 53 lines              *
.******************************************************************************
.*             V5.01.02  26/10/89  Steve O'Gorman                             *
.*                       Added Screen Display During Processing               *
.*             V5.01.03  27/10/89  Steve O'Gorman                             *
.*                       Fixed Check On TODATE                                *
.*             V5.01.04  17/11/89  Steve O'Gorman                             *
.*                       Fixed D.O.B for 1st patient after heading            *
.*             V5.01.05  19/11/89  Steve O'Gorman                             *
.*                       Openned CONTROLF to fix midnight run bug             *
.*             V5.01.06  02/02/90  J.Tan                   SRF 103272         *
.*                       Fixed to print a possible duplication only           *
.*             V5.01.07  24/08/90  i chung                 SRF 106243         *
.*                       Fixed page overflow problem                          *
.*             V5.01.16  23/07/90 Paul Duncan                                 *
.*                       implemented new surname file                         *
.*             V5.01.121 16/11/90 Darren Jones / Paul Duncan                  *
.*                       Fix problem with given name soundex                  *
.*             V5.01.122 19/12/90 D.Di.Paola     SRF 107397                   *
.*                       Recompile for PATURAFD...                            *
.*             V5.01.123 03.03.93 Neeriem "I Hate Support" Dye                *
.*                       Modify to print full address                         *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATGSRFD/INC
          INC       PATMA1FD/INC
          INC       PATLINFD/INC
          INC       PATURAFD/INC
          INC       PMSPX2FD/INC
+
.
. LOCAL VARIABLES
. ---------------
.
ADDRESS   DIM       41
.
BASDOB    DIM       8
BASKEY    DIM       10
BASSEX    DIM       1
BASSNAME  DIM       40
BASGNAME  DIM       40
BASURNO   DIM       8
.
CHARNUMB  FORM      2
.
DIM3      DIM       3
DIM13     DIM       13
DIM40     DIM       40
DIM50     DIM       50
DOBFLAG   FORM      1             * dob matching flag
.                                     0 = matching
.                                     1 = not matching
.
FROMDATE  DIM       8
FRSTNAME  DIM       40
FIRST     FORM      1             * first record indicator
.                                     0 = first record (original pmi record)
.                                     1 = other record (matching pmi records)
.
GNAMFLAG  FORM      1             * given name matching flag
.                                     0 = matching
.                                     1 = not matching
.
LINE      DIM       50
.
RECTYPE   DIM       1
.
SEXFLAG   FORM      1             * sex matching flag
.                                     0 = matching
.                                     1 = not matching
.
TODATE    DIM       8
.
.
. PROGRAM CONSTANTS
. -----------------
. 
CODEC     INIT      "C "
LINE65    INIT      "-----------------------------------":
                    "------------------------------"
MAC       INIT      "MAC"
MC        INIT      "MC"
PIPE      INIT      "|"
.
.
PRGID     INIT      "IBAPMS48"
PRGNAM    INIT      "Possible U/R Duplication Report"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                           MAINLINE                                        *
.*                  Controlling routine for program code                     *
.*****************************************************************************
.
ML0000    CALL      INIT0000                     * Initialization
.
ML1000    CALL      SCRN0000                     * get program option
          BRANCH    EXIT,ML9999                  * exit selected
.
ML2000    CALL      SDAT0000                     * get start report date
          BRANCH    EXIT,ML1000                  * nothing entered
.
          CALL      EDAT0000                     * get end report date
          BRANCH    EXIT,ML2000                  * nothing entered
.
          CALL      CUST0000                     * get matching customisation
.
          CALL      CONTQST                      * Ok. to Continue ?
          BRANCH    CEXIT,ML5000:                * yes
                          ML2000:                * no
                          ML1000                 * cancel
.
ML5000    CALL      EXTR0000                     * Extract Data
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*****************************************************************************
.*                           INIT0000                  Called by: ML0000     *
.*             Initialise variables and open files                           *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"Opening paturadf"
          OPEN      PATURAD1,"paturadf"
.
          DISPLAY   *P72:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P72:24,"patlinkf"
          OPEN      PATLINK1,"patlinkf"
.
          DISPLAY   *P72:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P72:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P72:24,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          DISPLAY   *P72:24,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          DISPLAY   *P72:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                            SCRN0000                 Called by: ML0000     *
.*                       Main Option Screen                                  *
.* Returns: EXIT  0 = run report                                             *
.*                1 = exit selected                                          *
.*****************************************************************************
.
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Run Duplication Report"
.
SCRN1000  KEYIN     *P1:7,*EL,"Select option : ":
                    *P17:7,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION                  * exit selected ?
          GOTO      SCRN9500 IF EQUAL            * yes
.
          BRANCH    OPTION,SCRN9000              * run report
.
          BEEP                                   * invalid input
          GOTO      SCRN1000
.
SCRN9000  MOVE      ZERO,EXIT
          GOTO      SCRN9999
.
SCRN9500  MOVE      ONE,EXIT
.
SCRN9999  RETURN
+
.*****************************************************************************
.*                            SDAT0000                 Called by: ML0000     *
.*                    Keyin Start Date for the Report                        *
.* Returns: FROMDATE = start date (ccyymmdd)                                 *
.*          EXIT   0 = valid date entered                                    *
.*          EXIT   1 = nothing entered                                       *
.*****************************************************************************
.
SDAT0000  DISPLAY   *P1:9,*EF,"From Date :";
.
          UNPACK    SP30,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      NINE,CVERT
          MOVE      TEN3,CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          CALL      KEYDATE                      * keyin date
          BRANCH    OVRCD,SDAT9500               * nothing entered
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          MOVE      ZERO,EXIT
          GOTO      SDAT9999
.
SDAT9500  MOVE      ONE,EXIT
.
SDAT9999  RETURN
+
.*****************************************************************************
.*                            EDAT0000                 Called by: ML0000     *
.*                    Keyin End Date for the Report                          *
.* Returns: TODATE = end date (ccyymmdd)                                     *
.*          EXIT   0 = valid date entered                                    *
.*          EXIT   1 = nothing entered                                       *
.*****************************************************************************
.
EDAT0000  DISPLAY   *P1:10,*EF,"To Date   : ";
.
          UNPACK    SP30,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      TEN,CVERT
          MOVE      TEN3,CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          CALL      KEYDATE                      * keyin date
          BRANCH    OVRCD,EDAT9500               * nothing entered
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
.         Make sure date range is valid
.
          MATCH     FROMDATE,TODATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"To Date must not be less than ":
                      "From Date.  ";
            CALL      HITENTER
            GOTO      EDAT0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      EDAT9999
.
EDAT9500  MOVE      ONE,EXIT
.
EDAT9999  RETURN
+
.*****************************************************************************
.*                            CUST0000                 Called by: ML0000     *
.*                   Customise the extract                                   *
.* Returns: GNAMFLAG  0 = matching on first given name                       *
.*                    1 = not matching on first given name                   *
.*          CHARNUMB    = number of characters to match on first given name  *
.*                        ( 0 = all)                                         *
.*          SEXFLAG   0 = matching on sex                                    *
.*                    1 = not matching on sex                                *
.*          DOBFLAG   0 = matching on dob                                    *
.*                    1 = not matching on dob                                *
.*****************************************************************************
.
.         Check if we are matching on given name, and if so, see how many
.         characters to match on
.
CUST0000  DISPLAY   *P1:13,*ULON,SP30,SP2,"Matching Criteria",SP30,SP1
          MOVE      TEN5,CVERT                   * set row
          MOVE      ANSY,ANS                     * set default answer
          KEYIN     *P1:CVERT,"Match on First Given Name (",*V2LON,*DV,ANSY:
                    *HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") :":
                    *P35:CVERT,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            DISPLAY   *P35:CVERT,*EL,*V2LON,DYES
            MOVE      ZERO,GNAMFLAG
            ADD       ONE,CVERT                  * increment line count
            DISPLAY   *P1:24,*EL,"<ENTER> to match on ALL characters";
CUST0500    KEYIN     *P1:CVERT,"Number of characters to match   :":
                      *P35:CVERT,*V2LON,CHARNUMB
.
            COMPARE   ZERO,CHARNUMB 
            GOTO      CUST0500 IF LESS           * negative number entered
            IF        @EQUAL
              DISPLAY   *P35:CVERT,*V2LON,"All"  * zero entered
            ENDIF
            DISPLAY   *P1:24,*EL;
            GOTO      CUST1000
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            DISPLAY   *P35:CVERT,*EL,*V2LON,DNO
            MOVE      ONE,GNAMFLAG
            GOTO      CUST1000
          ENDIF
.
          BEEP                                   * invalid input
          GOTO      CUST0000
.
.         Check if matching on sex
.
CUST1000  ADD       ONE,CVERT                    * increment row
          MOVE      ANSY,ANS                     * set default answer
          KEYIN     *P1:CVERT,"Match on Sex              (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") :":
                    *P35:CVERT,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            DISPLAY   *P35:CVERT,*EL,*V2LON,DYES
            MOVE      ZERO,SEXFLAG
            GOTO      CUST2000
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            DISPLAY   *P35:CVERT,*EL,*V2LON,DNO
            MOVE      ONE,SEXFLAG
            GOTO      CUST2000
          ENDIF
.
          BEEP                                   * invalid input
          GOTO      CUST1000
.
.         Check if matching on date of birth
.
CUST2000  ADD       ONE,CVERT                    * increment row
          MOVE      ANSY,ANS                     * set default answer
          KEYIN     *P1:CVERT,"Match on D.O.B.           (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") :":
                    *P35:CVERT,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            DISPLAY   *P35:CVERT,*EL,*V2LON,DYES
            MOVE      ZERO,DOBFLAG
            GOTO      CUST9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            DISPLAY   *P35:CVERT,*EL,*V2LON,DNO
            MOVE      ONE,DOBFLAG
            GOTO      CUST9999
          ENDIF
.
          BEEP                                   * invalid input
          GOTO      CUST2000
.
CUST9999  RETURN
+
.*****************************************************************************
.*                            EXTR0000                 Called by: ML0000     *
.*                          Extract Data                                     *
.*****************************************************************************
.
EXTR0000  DISPLAY   *P1:24,*EL,"U/R Number:",*P30:24,"Scanning:";
.
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          MOVE      ZERO,CLNO
          CALL      IBACLOCK
.
          CALL      HEAD0000                     * print header
.
          MOVE      ANSA,RECTYPE                 * set for "Add" records
          PACK      KEY17,RECTYPE,FROMDATE,SP20
          CALL      RDSURAD1                     * position on record type
EXTR1000  CALL      RDKURAD1                     * read next record
          BRANCH    OVRCD,EXTR9000               * eof - finished
.
          MATCH     RECTYPE,URRTYPE              * correct record type still ?
          GOTO      EXTR5000 IF NOT EQUAL        * no - finished this type
.
          MATCH     URDATE,TODATE                * within date range ?
          GOTO      EXTR5000 IF LESS             * no - finished this type
.
          DISPLAY   *P13:24,*V2LON,URURNO;       * valid record found
.
          PACK      KEY8,URURNO
          CALL      RDMAST1                      * U/R on file ?
          BRANCH    OVRCD,EXTR1000               * no - ignore
.
          CALL      CLPMSPX2
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
.
          MOVE      ZERO,FIRST                   * set flag to first pmi record
          CALL      BASE0000                     * get base patient details
.
          MOVE      BASSNAME,GSRSNAM             * get pt. surname soundex key
          CALL      SOUNDEX
          MOVE      GSRSKEY,BASKEY               * save soundex key
.
          CALL      MTCH0000                     * Match to Surname File
.
          CALL      MAC0000                      * MAC to MC Conversion
.
          CALL      MC0000                       * MC to MAC Conversion
.
.         The search for matches has been completed for this patient
.         so check if any matches were found
.
          COMPARE   ZERO,FIRST                   * any matches found ?
          GOTO      EXTR1000 IF EQUAL            * no - get next record
.
          PRINT     *N,ASK,LINE65,LINE65,ASK;    * yes - print end line
          ADD       ONE,CLNO                     * increment line count
          GOTO      EXTR1000                     * get next record
.
.         Check if we have been processing "Change" records to determine
.         if we have finished
.
EXTR5000  MATCH     ANSC,RECTYPE                 * finished "C" records ?
          GOTO      EXTR9000 IF EQUAL            * yes - finished extraction
.
          MOVE      ANSC,RECTYPE                 * no - set up for "C" records
          PACK      KEY17,RECTYPE,FROMDATE,SP20
          CALL      RDSURAD1                     * position in file
          GOTO      EXTR1000                     * read next record
.
EXTR9000  PRINT     *N,*N,"***  End of Report  ***";
.
EXTR9999  RETURN
+
.*****************************************************************************
.*                            LINK0000                 Called by: EXTR0000   *
.*                    Check with Link file                                   *
.*****************************************************************************
LINK0000  MOVE      ZERO,EXIT
          PACK      KEY16,BASURNO,SP20
          CALL      RDSLINK1
LINK1000  CALL      RDKLINK1
          BRANCH    OVRCD,LINK9999
.
          MATCH     BASURNO,LINKFUR         * Same U/R ?
          GOTO      LINK9999 IF NOT EQUAL
.
          PACK      KEY5,ANSL,ANSU,LINKREA
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     ANSN,TCINDC1         * check for multiple births
            GOTO      LINK9000 IF EQUAL
          ENDIF
          GOTO      LINK1000
.
LINK9000  MOVE      ONE,EXIT
LINK9999  RETURN
+
.*****************************************************************************
.*                            BASE0000                 Called by: EXTR0000   *
.*                    Save Base Names, Sex & U/R                             *
.* Returns: BASSEX   = saved patient sex                                     *
.*          BASURNO  = saved patient U/R no.                                 *
.*          BASGNAME = saved patient given name                              *
.*          BASSNAME = saved patient surname                                 *
.*          BASDOB   = saved patient dob                                     *
.*          LINE     = saved patient surname                                 *
.*****************************************************************************
.
BASE0000  MOVE      PSEX,BASSEX                  * save sex of patient
          MOVE      PBDATE,BASDOB                * save dob
          MOVE      PURNO,BASURNO                * save U/R no. of patient
.
          BRANCH    GNAMFLAG,BASE1000            * not matching on given name
.
.         Get the first given name of the patient by checking each character
.         from the start until a blank character is found.
.
          MOVE      PMPXFNAM,DIM50
          STRIP     DIM50
.
.         Check to see if a specific length match has been requested
.         for first given name, and if so, set for that length.  This only
.         needs to be done where the length of the name is > number of
.         characters selected.
.
          IF        CHARNUMB > 0
            MOVELPTR  DIM50,FORM2
            IF        FORM2 > CHARNUMB
              SETLPTR   DIM50,CHARNUMB
            ENDIF
          ENDIF
.
.         The patient's first name has been found, now pad out the name with
.         spaces and save
.
          PACK      BASGNAME,DIM50,SP70          * save first name of patient
BASE1000  MOVE      PTMASNAM,BASSNAME            * save surname of patient
.
BASE9999  RETURN
+
.*****************************************************************************
.*                              MAC0000                Called by: EXTR0000   *
.*                    Convert MAC to MC in Surname                           *
.* Requires: BASSNAME = saved patient surname                                *
.*****************************************************************************
.
MAC0000   MOVE      BASSNAME,DIM3
          MATCH     MAC,DIM3                     * "MAC" in surname ?
          GOTO      MAC9999 IF NOT EQUAL         * no - finished
.
          UNPACK    BASSNAME,DIM3,DIM50          * change to "MC"
          CLEAR     BASSNAME
          PACK      BASSNAME,MC,DIM50
          RESET     BASSNAME
.
          MOVE      BASSNAME,GSRSNAM             * get soundex key for surname
          CALL      SOUNDEX
          MOVE      GSRSKEY,BASKEY
.
          CALL      MTCH0000                     * check for any matches
.
MAC9999   RETURN
+
.*****************************************************************************
.*                               MC0000                Called by: EXTR0000   *
.*                    Convert MC to MAC in Surname                           *
.* Requires: BASSNAME = saved patient surname                                *
.*****************************************************************************
.
MC0000    MOVE      BASSNAME,DIM3
          MATCH     MC,DIM3                      * "MC" in surname ?
          GOTO      MC9999 IF NOT EQUAL          * no - finished
.
          UNPACK    BASSNAME,DIM2,DIM50          * change "MC" to "MAC"
          CLEAR     BASSNAME
          PACK      BASSNAME,MAC,DIM50
          RESET     BASSNAME
.
          MOVE      BASSNAME,GSRSNAM             * get soundex key for surname
          CALL      SOUNDEX
          MOVE      GSRSKEY,BASKEY
.
          CALL      MTCH0000                     * check for any matches
.
MC9999    RETURN
+
.*****************************************************************************
.*                            MTCH0000                 Called by: EXTR0000   *
.*                Match Soundex Key on Surname File               MAC0000    *
.* Requires: BASKEY = patient's soundex key                       MC0000     *
.*           BASSEX   = base patient sex                                     *
.*           BASURNO  = base patient U/R no.                                 *
.*           BASGNAME = base patient given name                              *
.*           BASSNAME = base patient surname                                 *
.*           BASDOB   = base patient dob                                     *
.*****************************************************************************
.
MTCH0000  PACK      KEY34,BASKEY,SP70
          CALL      RSPTGSR3                     * position on soundex key
MTCH0300  CALL      RKPTGSR3                     * read next record
          BRANCH    OVRCD,MTCH9999               * eof - finished
.
          MATCH     BASKEY,GSRSKEY               * same soundex key still ?
          GOTO      MTCH9999 IF NOT EQUAL        * no - finished
.
          MOVE      GSRSNAM,LINE
          SQUEEZE   LINE                         * remove blanks from surname
          REP       UPPLOW,LINE                  * convert all chrs to uppercase
.
          MOVE      LINE,GSRSNAM
          MATCH     BASSNAME,GSRSNAM             * same surname ?
          GOTO      MTCH0300 IF NOT EQUAL        * no - ignore
.
          MOVE      GSRGNAM,DIM50                * yes - load given name
.
.         We have a matching surname, so see if the first given name matches
.
MTCH1000  BRANCH    GNAMFLAG,MTCH2000            * not matching on given name
.
          STRIP     DIM50
.
.         Check to see if a specific length match has been requested
.         for first given name, and if so, set for that length.  This only
.         needs to be done where the length of the name is > number of
.         characters selected.
.
          IF        CHARNUMB > 0
            MOVELPTR  DIM50,FORM2
            IF        FORM2 > CHARNUMB
              SETLPTR   DIM50,CHARNUMB
            ENDIF
          ENDIF
.
          PACK      FRSTNAME,DIM50,SP70          * load first name
.
          MATCH     BASGNAME,FRSTNAME            * same first name ?
          GOTO      MTCH8000 IF NOT EQUAL        * no - ignore
.
.         First given name also matches, so now check other variables
.
MTCH2000  MOVE      GSRURNO,KEY8
          CALL      RDMAST1                      * PMI record on file ?
          BRANCH    OVRCD,MTCH8000               * no - ignore
          CALL      RDPMPX21
.
          IF        SEXFLAG = 0
            MATCH     BASSEX,PSEX                * same sex ?
            GOTO      MTCH8000 IF NOT EQUAL      * no - ignore
          ENDIF
.
          IF        DOBFLAG = 0
            MATCH     BASDOB,PBDATE              * same dob ?
            GOTO      MTCH8000 IF NOT EQUAL      * no - ignore
          ENDIF
.
          MATCH     BASURNO,GSRURNO              * same U/R number ?
          GOTO      MTCH8000 IF EQUAL            * yes - ignore
.
          CALL      LINK0000                     * Check with Link File
          BRANCH    EXIT,MTCH8000
.
.         A match has been found so check if there is room on the page
.
          COMPARE   FIFTY3,CLNO                  * page full ?
          IF        !@LESS
            PRINT     *N,*1,PIPE," Continued",*12,PIPE,*55,PIPE,*68,PIPE:
                    *72,PIPE,*116,PIPE,*132,PIPE:
                    *N,*1,ASK,LINE65,LINE65,ASK;
            CALL      HEAD0000                   * print page header
          ENDIF
.
.         Check whether this is the first matching record, or subsequent matched
.         records
.
          BRANCH    FIRST,MTCH2900               * subsequent matching record
.
.         The first matching record for our base pmi record has been found,
.         so print the base pmi details
.
          PACK      KEY8,BASURNO
          CALL      RDMAST1                      * read base pmi record
          CALL      RDPMPX21
.
          GOTO      MTCH3000
.
.         Format the patient details for printing
.
MTCH2900  PACK      PACSNAME,GSRSNAM,SP70        * Print alias details
          STRIP     GSRGNAM
          PACK      PACGNAME,GSRGNAM,SP1,PTGSGNM2,SP70
          MOVE      GSRDOB,PBDATE
          MOVE      GSRSEX,PSEX
          GOTO      MTCH3100
.
MTCH3000  MOVE      PSNAME,PACSNAME              * format patient name
          MOVE      PGNAME,PACGNAME
.
MTCH3100  MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM40
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          CALL      ADDR0000                     * format patient's address
.
.         Get description for country of birth
.
          MOVE      SP20,TDESC
          MATCH     SP3,PCONT                    * country of birth blank ?
          IF        !@EQUAL
            MOVE      "Unknown code",TDESC       * no
            PACK      KEY5,CODEC,PCONT
            CALL      RDCODE1
          ENDIF
          MOVE      TDESC,DIM13                  * save cob description
.
          DISPLAY   *P40:24,*V2LON,GSRURNO;
.
          PRINT     *N,*1,PIPE,SP1,PURNO:
                    *12,PIPE,SP1,DIM40:
                    *55,PIPE,SP1,CPCDATE:
                    *68,PIPE,SP1,PSEX:
                    *72,PIPE,SP1,ADDRESS:
                    *116,PIPE,SP1,DIM13:
                    *132,PIPE;
.
          ADD       ONE,CLNO                     * increment line count
          BRANCH    FIRST,MTCH8000               * subsequent matching reord
.
.         Print blank line after base pmi record
.
          PRINT     *N,*1,PIPE,*12,PIPE,*55,PIPE,*68,PIPE:
                    *72,PIPE,*116,PIPE,*132,PIPE;
.
          ADD       ONE,CLNO                     * increment line count
          MOVE      ONE,FIRST                    * set flag for subequent pts
          GOTO      MTCH2000                     * print first matching record
.
.         Get the next record in the patgsrnf file
.
MTCH8000  GOTO      MTCH0300                     * using surname/given name file
.
MTCH9999  RETURN
+
.*****************************************************************************
.*                             ADDR0000                Called by: MTCH0000   *
.*                   Format Patients Address                                 *
.* Requires: PADD1 - patient address line 1                                  *
.*           PADD2 - patient address line 2                                  *
.*           PADD3 - patient address line 3                                  *
.*           PADD4 - patient address line 4                                  *
.*           PPOST - patient postcode                                        *
.* Returns: ADDRESS - formatted patient address                              *
.*****************************************************************************
.
ADDR0000  PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
.
ADDR9999  RETURN
+
.*****************************************************************************
.*                            HEAD0000                 Called by: EXTR0000   *
.*                         Report Heading                         MTCH0000   *
.*****************************************************************************
.
HEAD0000  CALL      HEAD132
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *N,*40,"From: ",CPCDATE;
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      DYES,DIM3
          LOAD      DIM3,GNAMFLAG,DNO
          PRINT     "     To: ",CPCDATE:
                    *N,*40,"Matching on First Given Name: ";
.
          IF        GNAMFLAG = 0
            IF        CHARNUMB = 0
              PRINT     *70,DIM3,*80,"Number of Characters: ALL"
            ELSE
              PRINT     *70,DIM3,*80,"Number of Characters: ",CHARNUMB
            ENDIF
          ELSE
            PRINT     *70,DIM3
          ENDIF
.
          MOVE      DYES,DIM3
          LOAD      DIM3,SEXFLAG,DNO
          PRINT     *40,"Matching on Sex             : ",DIM3
.
          MOVE      DYES,DIM3
          LOAD      DIM3,DOBFLAG,DNO
          PRINT     *40,"Matching on D.O.B.          : ",DIM3:
                    *N,*1,ASK,LINE65,LINE65,ASK:
                    *N,*1,PIPE," U/R No.":
                    *12,PIPE," Name":
                    *55,PIPE," D.O.B.":
                    *68,PIPE,"Sex":
                    *72,PIPE," Permanent Address":
                    *116,PIPE," Birthplace":
                    *132,PIPE:
                    *N,*1,ASK,LINE65,LINE65,ASK;
.
          MOVE      TEN1,CLNO                    * set line count
.
HEAD9999  RETURN
+
.*****************************************************************************
.*        I/O Includes                                                       *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       CLPMSPX2
          INC       PATSNDX
          INC       PATSNX2
.
          INC       PATCODIO/INC
          INC       PATGSRIO/INC
          INC       PATMA1IO/INC
          INC       PATLINIO/INC
          INC       PATURAIO/INC
          INC       PMSPX2IO/INC
............................................................................

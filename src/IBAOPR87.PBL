.**********************************************************************
.* System   : Operating Room System                                   *
.* Program  : IBAOPR87                                                *
.* Desc     : Tray File Master List                                   *
.**********************************************************************
.* Date     :  13/12/89                                               *
.* Author   :  K.Peak                                                 *
.* Mods.    :                                                         *
.**********************************************************************
.* V11.04.01 24/04/2024  Thanh T            TSK 0923560               *
.*           Changed PRNT0000 to add hosp id when calling RDOPTRA1    *
.* V11.03.01 23/01/2023  Thanh T.           TSK 0919770               *
.*           Changed VTRY0000, PRNT0000 to cater for adding Hospital  *
.*           field changes                                            *
.**********************************************************************
.*    9.04.01  17/02/2005  Lina Vo     CAR 56404                      *
.*             Changed program to read opriteaf, oprtryaf & oprtrybf  *
.*             with new key size.                                     *
.*             29/03/90  Bill Dew  Major fix it.                      *
.*             Fix program to work.                                   *
.*             V5.01.10 Paul Duncan                                   *
.*          :  Alter program to new specs                             * 
.**********************************************************************
.
.$$$$$
.    Operating Room System Tray File Master List (IBAOPR87 )
.    -------------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPRTRAA1
.               OPRTRBA1
.               PATCODE1
.               CONTROLF
.
.    - Clear Input Variables
.
.    - Display Screen Format
.
.    - Input Range 
.      If "?" entered, call OPRTRADS routine
.
.    - Read through appropriate file
.
.    - Print Report
.
.    - END
.$$$$$
.
.*****************************************************************************
          INC       STD001FD
          INC       OPRTRAFD/INC           * Tray Header File
          INC       OPRTRBFD/INC           * Tray Detail File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       OPRITEFD/INC       * Instrument item header file
.         INC       PATCODFD/INC           * patient codes file
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CLSSCODE  DIM       3
CLSSDESC  DIM       30
.
CODE      DIM       5
.
DIM6      DIM       6
DIM15     DIM       15              * used in OPRTRADS
STARTCR   DIM       6                  * start code 
ENDCR     DIM       6                  * end code
TRAYCNT   FORM      4
TRAYCOD1  DIM       6                  * starting tray code range
TRAYCOD2  DIM       6                  * ending tray code range
TRAYDES1  DIM       30                 * start tray description
TRAYDES2  DIM       30                 * end tray description
ITEMCODE  DIM       6
ITEMDESC  DIM       50
INDEX     FORM      3
LINE80    DIM       80
SCRNFLAG  FORM      1                  * scrnflag is a flag signalling redisplay
.
.
.----------------------------------< CONSTANTS >--------------------------------
NUM55     FORM      "55"
.
PRGID     INIT      "IBAOPR87"
PRGNAM    INIT      "Tray Master Listing"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000
          CALL      INIT0000               * initialization and open files
ML1000
          CALL      MENU0000               * select option         
          BRANCH    OPTION,ML1111
          GOTO      ML9999
ML1111
          CALL      CLRV0000               * clear input variables
          CALL      REDP1111
          CALL      RNGE0000               * select range
          CALL      CONTQST
          BRANCH    CEXIT,ML3333,ML1111,ML1000
ML3333
          CALL      PRNT0000               * print tray item master listing
          GOTO      ML1000
ML9999
          CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
.         DISPLAY   *P56:24,"Opening controlf";
.         OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P56:24,"Opening oprtryaf";
          OPEN      OPRTRYA1,"oprtryaf"
          OPEN      OPRTRYA2,"oprtryaf"
.
          DISPLAY   *P64:24,"oprtrybf";
          OPEN      OPRTRYB1,"oprtrybf"
.
          DISPLAY   *P64:24,"opriteaf";
          OPEN      OPRITEA1,"opriteaf"
.
.         DISPLAY   *P64:24,"patcodes";
.         OPEN      PATCODE1,"patcodes"
.
          MOVE      ONE,CNEWDS         * set using new ? routine
          MOVE      ONE,CNOUNDLN
INIT9999  RETURN
+
.**********************************************************************
.*                             CLRV0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLRV0000
          PACK      LINE80,SP30,SP30,SP20
          UNPACK    LINE80,STARTCR,ENDCR,TRAYDES1,TRAYDES2
          UNPACK    LINE80,TRAYCOD1,TRAYCOD2
          MOVE      ZERO,TRAYCNT
          RETURN
.*****************************************************************************
.                                 MENU0000
.    Menu driver routine
.*****************************************************************************
MENU0000
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print Tray Master Listing."
.
MENU1111  KEYIN     *P1:7,*EL,"Select Option : _",*P17:7,*V2LON,OPTION
          COMPARE   ZERO,OPTION
          GOTO      MENU9999 IF EQUAL
          BRANCH    OPTION,MENU9000
          BEEP
          GOTO      MENU1111
MENU9000
MENU9999  RETURN
.*******************************************************************************
.                                 REDP0000
.    Redisplay Screen
.*******************************************************************************
REDP0000
          BRANCH    SCRNFLAG,REDP9999
          MOVE      ONE,SCRNFLAG
REDP1111
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print Tray Master Listing.":
                    *P1:7,"Select Option : ",*V2LON,OPTION,*HOFF:
                    *P4:10,"Start Tray Code : ":
                    *P4:11,"  End Tray Code : ":
                    *P22:10,*EL,*V2LON,STARTCR,*HOFF,SP6,TRAYDES1:
                    *P22:11,*EL,*V2LON,ENDCR,*HOFF,SP6,TRAYDES2
.
REDP9999  RETURN
.*******************************************************************************
.                                 RNGE0000
.    Select tray code range
.*******************************************************************************
RNGE0000
          MOVE      ONE,SCRNFLAG
RNGE1000
          DISPLAY   *P22:10,UNDLN6,*P22:10;
RNGE1111  
          CALL      GITM0000           * get starting clinic code
          MOVE      ITEMCODE,TRAYCOD1
.
          MATCH     SP6,TRAYCOD1
          GOTO      RNGE2222 IF NOT EQUAL
          MOVE      "Start",STARTCR
          MOVE      SP30,TRAYDES1
          CALL      REDP0000           * test for redisplay
          GOTO      RNGE4444           * get item2
RNGE2222  
          CALL      VTRY0000           * validate the TRAYCOD1
          BRANCH    EXIT,RNGE1000,RNGE1111
          CALL      REDP0000           * test for redisplay
          MOVE      ITEMCODE,STARTCR   * copy the code for display purposes
          MOVE      ITEMDESC,TRAYDES1  * copy description into work desc
RNGE4444                               * enter item2 info
          DISPLAY   *P22:10,*V2LON,STARTCR,*HOFF,SP6,TRAYDES1
RNGE5000
          DISPLAY   *P22:11,UNDLN6,*P22:11;
RNGE5555  
          CALL      GITM0000           * get item 2 and do ? routines 
          MOVE      ITEMCODE,TRAYCOD2
          MATCH     SP6,TRAYCOD2       * test for return hit means END
          GOTO      RNGE6666 IF NOT EQUAL
          MOVE      "ZZZZZZ",TRAYCOD2  * set item to highest ascii value
          MOVE      "Finish",ENDCR     * set description for end code
          MOVE      SP30,TRAYDES2      * clear description doctor 2
          CALL      REDP0000           * test for redisplay
          GOTO      RNGE7777
RNGE6666
          CALL      VTRY0000           * validate TRAYCOD2
          BRANCH    EXIT,RNGE5000,RNGE5555
          MATCH     TRAYCOD1,TRAYCOD2  * test item2 appears after item1 in file
          GOTO      RNGE8000 IF LESS 
          CALL      REDP0000           * test for redisplay
          MOVE      ITEMCODE,ENDCR     * copy the code for display reasons
          MOVE      ITEMDESC,TRAYDES2  * copy description into work desc
RNGE7777
          DISPLAY   *P22:11,*V2LON,ENDCR,*HOFF,SP6,TRAYDES2
          GOTO      RNGE9999
.
RNGE8000  DISPLAY   *P1:24,*B,*EL,"Invalid codes.  ":
                    "End Code must come after Start Code.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,RNGE5000
          GOTO      RNGE5555
.
RNGE9999  RETURN
.*****************************************************************************
.                                 GITM0000
.    get doctor type store item in ITEMCODE and checks for ? routines
.*****************************************************************************
GITM0000
          COMPARE   ZERO,SCRNFLAG
          GOTO      GITM4444 IF EQUAL
GITM1000
          KEYIN     *V2LON,ITEMCODE    * user enter clinic code
          ENDSET    ITEMCODE
          APPEND    SP6,ITEMCODE
          RESET     ITEMCODE
.
          CMATCH    "?",ITEMCODE
          GOTO      GITM9999 IF NOT EQUAL
          MOVE      ZERO,SCRNFLAG      * signal redisplay required
          CALL      OPRTRADS           * tray code ? routine
GITM4444
          DISPLAY   *P1:24,*EL,"Tray code : ______",*P13:24;
          GOTO      GITM1000
.
GITM9999  RETURN
.*****************************************************************************
.                                 VTRY0000
.    SCRNFLAG   0   1
.    EXIT       2   1   0 if data is valid
.*****************************************************************************
VTRY0000
          PACK      KEY18,ITEMCODE,SP70
          CALL      RDOPTRA1           * test menu code against codes file
          BRANCH    OVRCD,VTRY8888
          MOVE      OPTHDESC,ITEMDESC
          GOTO      VTRY9990
.
VTRY8888  DISPLAY   *P1:24,*B,*EL,"Code is not on file.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,VTRY8899
          MOVE      TWO,EXIT           * exit=2 if scrnflag=0 ? routine
          GOTO      VTRY9999
VTRY8899  MOVE      ONE,EXIT           * exit=1 if no ? routine used
          GOTO      VTRY9999
VTRY9990  MOVE      ZERO,EXIT          * exit=0 input valid
VTRY9999  RETURN
.*****************************************************************************
.                                 PRNT0000
.    Print Tray and item master listing
.*****************************************************************************
PRNT0000
          DISPLAY   *P1:24,*EL,"Tray code : ",*P60:24,*V2LON,"*** Printing ***"
          MOVE      SP6,ITEMCODE       * set the last traycode
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000           * print heading
.
          PACK      KEY33,TRAYCOD1,SP70
          CALL      RSOPTRB1           * read from starting tray code range
PRNT2222
          CALL      RKOPTRB1
          BRANCH    OVRCD,PRNT9000
.
          MATCH     OPTDCODE,TRAYCOD2  * test if fptr has reached end range
          GOTO      PRNT9000 IF LESS
.
          COMPARE   CLNO,NUM55         * test for a printed page
          CALL      NPAG0000 IF LESS   * do setup next page routine
.
          MATCH     OPTDCODE,ITEMCODE  * test if same as last code 
          GOTO      PRNT4444 IF EQUAL
.
          ADD       ONE,TRAYCNT
.
          MOVE      ZERO,INDEX         * zero the instrument code 
          MOVE      OPTDCODE,ITEMCODE  * set the new tray code
          MOVE      SP30,OPTHDESC
          PACK      KEY18,OPTDCODE,OPTDCODE,SP70
          CALL      RDOPTRA1           * get tray description
PRNT4444  
.
          PACK      KEY15,OPTDITEM,SP70
          PACK      OPITDESC,SP30,SP30
          CALL      RDOPITE1           * get item description
.
PRNT5000  ADD       ONE,INDEX          * increment the instrument counter
          DISPLAY   *P13:24,OPTDCODE   * show operator were doing something
          CALL      PREC0000
          GOTO      PRNT2222
PRNT9000
          CALL      DRAWLINE
          PRINT     *N,"Number of Trays = ",TRAYCNT:
                    *N,*N,"***** End of Report *****"
PRNT9999  RETURN
.****************************************************************************** 
.                                 PREC0000
.    print a record depending on conditions
.****************************************************************************** 
PREC0000
          BRANCH    INDEX,PREC1111
          GOTO      PREC5555
PREC1111
          CALL      DRAWLINE
          ADD       ONE,CLNO
          MOVE      ITEMCODE,DIM6
PREC5555
          MOVE      SP3,KEY3
          COMPARE   ZERO,OPTDQTY
          GOTO      PREC6666 IF EQUAL
          MOVE      OPTDQTY,KEY3
PREC6666
          PRINT     *12,"| ",DIM6,*21,"| ",OPTHDESC,*53,"| ":
                    OPITCODE,SP2,OPITDESC,*114,"| ",SP3,KEY3,*125,"|"
          ADD       ONE,CLNO
          MOVE      SP30,OPTHDESC
          MOVE      SP30,CLSSDESC
          MOVE      SP6,DIM6
PREC9999  RETURN
.****************************************************************************** 
.    Next PAGe.  set the next screen
NPAG0000
          CALL      DRAWLINE
          CALL      HEAD0000
          MOVE      SP6,ITEMCODE
          SUB       ONE,TRAYCNT
          MOVE      SP6,CLSSCODE
.    The above line clears the previous tray code store in ITEMCODE.  This
.    effectively signals the print routine to redisplay the tray description
.    and code on the next page.  Believe it or Not!
NPAG9999  RETURN
.****************************************************************************** 
.                                 HEAD0000
.    This routine prints standard IBA heading 80 column format.  It sets the
.    time increments the page number and reset the line counter
.****************************************************************************** 
HEAD0000
          MOVE      EIGHT,CLNO         * set the printed line counter
          CLOCK     TIME,CTIMEIS       * get current system time
          CALL      HEAD132
.
          PRINT     *40,"Tray Code From : ",STARTCR,SP3,TRAYDES1
          PRINT     *40,"Tray Code To   : ",ENDCR,SP3,TRAYDES2,*N
          CALL      DRAWLINE
          PRINT     *12,"| Tray",*21,"| Tray Description":
                    *53,"| Item",*114,"| Quantity |"
HEAD9999  RETURN
.
DRAWLINE  PRINT     *12,"*-------------------------------------------------------":
                    "---------------------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.******************************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
          INC       STD001IO
          INC       OPRTRADS                * tray codes file routine 
.         INC       PATCODKY                * patient codes file routine
.         INC       PATCODIO/INC            * patient i/o for codes
          INC       OPRTRAIO/INC            * tray header i/o   
          INC       OPRTRBIO/INC            * tray detail i/o   
          INC       OPRITEIO/INC       * Instrument item header file
.**********************************************************************

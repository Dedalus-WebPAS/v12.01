.******************************************************************************
.* System         : Operating Rooms         Release 5.01                      *
.* Program        : IBAOPR74.PBL                                              *
.* Name           : Suspended Session List                                    *
.******************************************************************************
.* Date           : 23/12/1991                                                *
.* Author         : Justin Coates                                             *
.* Function       : To report all suspended sessions in a date range.         *
.* Modifications  :                                                           *
.******************************************************************************
.*       V11.03.02 20/04/2023  Thanh T       TSK 0909393                      *
.*                 Changed EXTR0000 to setup KEY23 with blank hospital id     *
.*       V11.03.01 20/03/2023   PJ Le Febour TSK 0909393a                     *
.*                 Amendments KEY20 to KEY23 for theatre index changes        *
.*       V10.04.01 23/06/2014   J.Tan        CAR 261630                       *
.*                 Removed port number from temp file name                    *
.*        V5.01.002 26/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.
.$$$$$
.         IBAOPR74.PBL        Suspended Sessions List
.         ------------        -----------------------
.         OPTN0000 - Get the ordering of the report
.         DATE0000 - Enter the date range
.         CLIN0000 - Enter the clinic range
.         EXTR0000 - Get all the suspended sessions
.         REPT0000 - Print the report
.$$$$$
.
.         FD's required
.
          INC       STD001FD/PBL            * standard variables
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRCLIFD/INC            * clinic file
          INC       OPRCONFD/INC            * control file
          INC       OPROPRFD/INC            * op. rooms file
          INC       OPRSESFD/INC            * session file
          INC       PATCODFD/INC
          INC       PATDO1FD/INC            * doctor file
.
.         program variables
.
CDFRCLIN  DIM       6         * from date clinic/surgeon
CDTOCLIN  DIM       6         * to   date clinic/surgeon
CFNAMEA   DIM       8         * temp file A name
CMDLINE   DIM       50
CNTSUSP   FORM      4         * number of suspended sessions
CURRDATE  DIM       8
DATESTRT  DIM       8         * from date code
DATESTOP  DIM       8         * to   date code
DATE10A   DIM       10        * from date description dd/mm/ccyy
DATE10B   DIM       10        * to   date description dd/mm/ccyy
DIM3      DIM       3         * patdsdoc
DXFRCLIN  DIM       30        * from date clinic/surgeon description
DXTOCLIN  DIM       30        * to   date clinic/surgeon description
DOCCODE   DIM       6       
KDOCT     DIM       6         * keyin clinic/surgeon code
KDOCTD    DIM       30        * keyin clinic/surgeon description
PTCNDCQS  FORM      1
SCRNFLAG  FORM      1         * redisplay flag
.
.         temp file definition
.
KEYVAR    DIM       15
KEYVAR1   INIT      "U1-6,7-14,15-19"       * clinic/date/time
KEYVAR2   INIT      "U7-14,15-19,1-6"       * date/time/clinic
TEMPA     IFILE     SQL, FIXED=56, KEY=KEYVAR
.
.Name     Type      Length  Phys.  Start  Description
.-------  -----     ------  -----  -----  -------------------------------------
XCLIN     DIM       6         6        1  Clinic/Surgeon code
XSESDT    DIM       8         8        7  Date of session
XTIME     DIM       5         5       15  Time of session
XDESC     DIM       30       30       20  Clinic/Surgeon desc
XTHET     DIM       6         6       50  Theatre code
.                                     56  End of Record
.
.         program constants
.
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
LENGTH    INIT      " 56 "
.
PRGID     INIT      "IBAOPR74"
PRGNAM    INIT      "Suspended Session List"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  MAINLINE                                         *
.*********************************************************************
ML0000    CALL      INIT0000                * initialization
.
ML1000    CALL      OPTN0000                * enter option
          BRANCH    EXIT,ML9999             * exit
.
ML2000    CALL      DATE0000                * enter the dates
          BRANCH    EXIT,ML1000             * nothing entered
.
ML3000    CALL      CLIN0000                * enter the clinic/surgeon
          BRANCH    EXIT,ML2000             * nothing entered
.
          CALL      CONTQST
          BRANCH    CEXIT,ML4000,ML2000,ML1000
.                         Yes    No     Cancel
.
.         produce the report
.
ML4000    CALL      CTPA0000                * create temp file
          CALL      EXTR0000                * get the suspended sessions
          CALL      REPT0000                * produce the report
          CALL      DTPA0000                * delete temp file
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
          DISPLAY   *P58:24,"oprsesaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA3,"oprsesaf"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P58:24,"oprcliaf"
          OPEN      OPRCLIA1,"oprcliaf"
          OPEN      OPRCLIA2,"oprcliaf"
          DISPLAY   *P58:24,"opropraf"
          OPEN      OPROPRA1,"opropraf"
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,FORTY;*81,OPCNODSC,OPCNCDSC,OPCNCLSU
          READ      CONTROLF,FORTY2;*105,OPCNR74A
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ONE,CNEWDS
          MOVE      ONE,CCANLDTE            * blank cancel default
          MOVE      ONE,CDEFDTE             * enter once for default
          MOVE      ZERO,CHIGHLT            * use highlight
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEA

          ENDSET    OPCNCDSC
INIT9000  MATCH     SP1,OPCNCDSC
          GOTO      INIT9100 IF NOT EQUAL   * no more spaces
          BUMP      OPCNCDSC,-1
          GOTO      INIT9000 IF NOT EOS     * no more letters
INIT9100  LENSET    OPCNCDSC
          RESET     OPCNCDSC
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        Enter the report option                                    *
.*********************************************************************
OPTN0000  HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF:
                    *V2LON,*P1:4,"0",*P1:5,"1",*P1:6,"2",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= by ",OPCNCDSC:
                    *P3:6,"= by Date/Time":
                    *P1:8,"Select option : "
.
OPTN1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN8100,OPTN8200
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN8100  DISPLAY   *P51:2,*V2LON,"- By ",*+,OPCNCDSC,SP1
          CLEAR     CPHDROPT
          APPEND    "- By ",CPHDROPT
          APPEND    OPCNCDSC,CPHDROPT
          APPEND    SP30,CPHDROPT
          RESET     CPHDROPT
          MOVE      KEYVAR1,KEYVAR          * set key for temp file
          GOTO      OPTN9999 
.
OPTN8200  DISPLAY   *P51:2,*V2LON,"- By Date/Time "
          MOVE      "- By Date/Time",CPHDROPT
          MOVE      KEYVAR2,KEYVAR          * set key for temp file
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  DATE0000                    Called by : ML3000   *
.*        Enter the date ranges                                      *
.*        Returns : EXIT = 1      exit chosen                        * 
.*                  DATESTRT      starting date  ccyymmdd            *
.*                  DATESTOP      ending date    ccyymmdd            *
.*                  DATE10A       starting date  dd/mm/ccyy          *
.*                  DATE10B       ending date    dd/mm/ccyy          *
.*********************************************************************
DATE0000  DISPLAY   *P1:10,*EF:
                    *P1:11,"Starting date : ",*EL:
                    *P1:12,"Ending   date : ",*EL:
                    *P1:14,"Starting ",*+,OPCNCDSC," : ":
                    *P1:15,"Ending   ",*+,OPCNCDSC," : "
.
.         enter starting date
.
DATE1000  MOVE      "11",CVERT              * row
          MOVE      "17",CCOL               * column
          UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD,DATE9000          * nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      DATESTRT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATESTRT           * set up start date
          CALL      PACDATE
          MOVE      CPCDATE,DATE10A         * formatted start date
.
.         enter ending date (default to starting date)
.
DATE2000  MOVE      "12",CVERT              * row
          MOVE      "17",CCOL               * column
          UNPACK    DATESTRT,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,DATE9000          * nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      DATESTOP,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATESTOP           * set up end date
          CALL      PACDATE
          MOVE      CPCDATE,DATE10B         * formatted end date
.
          MATCH     DATESTRT,DATESTOP
          GOTO      DATE8000 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER 
          GOTO      DATE2000
.
DATE8000  MOVE      ZERO,EXIT               * valid dates
          GOTO      DATE9999
.
DATE9000  MOVE      ONE,EXIT                * exit selected
.
DATE9999  RETURN
+
.*********************************************************************
.*                  CLIN0000                    Called by : ML3000   *
.*        Enter the range of clinic/surgeon codes                    *
.*********************************************************************
CLIN0000  DISPLAY   *P1:13,*EF:
                    *P1:14,"Starting ",*+,OPCNCDSC," : ":
                    *P1:15,"Ending   ",*+,OPCNCDSC," : "
          MOVELPTR  OPCNCDSC,CCOL1
          ADD       "13",CCOL1              * keyin column
.
.         enter starting clinic/surgeon
.
          MOVE      "14",CVERT              * row
          MOVE      CCOL1,CCOL              * column
          MOVE      SP6,CDFRCLIN            * clear the clinic
          PACK      DXFRCLIN,SP20,SP20      * clear the description
.
          CALL      KDOC0000                * enter the docs code
.
          MOVE      KDOCT,CDFRCLIN          * set up code
          MOVE      KDOCTD,DXFRCLIN         * set up description
          DISPLAY   *PCCOL1:14,*V2LON,CDFRCLIN,*HOFF,SP5,DXFRCLIN;
          MATCH     SP6,CDFRCLIN
          GOTO      CLIN5000 IF NOT EQUAL   * code entered
.
          DISPLAY   *PCCOL1:14,*V2LON,"Start",*EL;
.
.         enter ending clinic/surgeon
.
CLIN5000  MOVE      "15",CVERT              * row
          MOVE      CCOL1,CCOL              * column
          MOVE      SP6,CDTOCLIN            * clear the clinic
          PACK      DXTOCLIN,SP20,SP20      * clear the description
.
          CALL      KDOC0000                * enter the docs code
          MOVE      ZERO,EXIT
.
          MOVE      KDOCT,CDTOCLIN          * set up code
          MOVE      KDOCTD,DXTOCLIN         * set up description
          DISPLAY   *PCCOL1:15,*V2LON,CDTOCLIN,*HOFF,SP5,DXTOCLIN;
          MATCH     SP6,CDTOCLIN
          GOTO      CLIN8000 IF NOT EQUAL   * code entered
.
          DISPLAY   *PCCOL1:15,*V2LON,"Finish",*EL;
          MOVE      "zzzzzz",CDTOCLIN
.
CLIN8000  MATCH     CDFRCLIN,CDTOCLIN
          GOTO      CLIN9999 IF NOT LESS    * valid codes
.
          DISPLAY   *P1:24,*B,"Starting code must be before Ending code.  ",*EL;
          CALL      HITENTER
          GOTO      CLIN0000
.
CLIN9999  RETURN
+
.*********************************************************************
.*                  EXTR0000                    Called by : ML4000   *
.*        Extract all suspended sessions                             *
.*********************************************************************
EXTR0000  DISPLAY   *P1:24,"Extracting Sessions : ",*EL;
          PACK      KEY23,ONE,CDFRCLIN,SP3,DATESTRT,SP70
          CALL      RSOPSES3
.
EXTR1000  CALL      RKOPSES3
          BRANCH    OVRCD,EXTR9999
.
          COMPARE   ONE,OPSESUSP
          GOTO      EXTR9999 IF NOT EQUAL   * no more suspended sessions
.
          MATCH     OPSECLIN,CDTOCLIN
          GOTO      EXTR9999 IF LESS        * clinic too late
.
          MATCH     DATESTRT,OPSEDATE
          GOTO      EXTR1000 IF LESS        * before start date
.
          MATCH     OPSEDATE,DATESTOP
          GOTO      EXTR1000 IF LESS        * after end date
.
          UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P23:24,CPCDATE
          MOVE      OPSEDATE,XSESDT         * date
          MOVE      OPSETIME,XTIME          * time
          MOVE      OPSECLIN,XCLIN          * clinic
          MOVE      OPSETHET,XTHET          * theatre
          MOVE      OPSECLIN,KDOCT          * to get clinic/surg name
          CALL      FNAM0000
          MOVE      KDOCTD,XDESC            * doctor/clinic desc
          CALL      WRTEMPA
          GOTO      EXTR1000
.
EXTR9999  RETURN
+
.*********************************************************************
.*                  REPT0000                    Called by : ML4000   *
.*        Print the report.                                          *
.*********************************************************************
REPT0000  DISPLAY   *P1:24,"Printing details : ",*EL;
          CLOCK     TIME,CTIMEIS            * current time
          MOVE      ZERO,CPAGENO            * page number
          MOVE      ZERO,CNTSUSP            * clear counter
          CALL      HEAD0000                * print heading
.
          MOVE      SP20,KEY19
          CALL      RSTEMPA
.
REPT1000  CALL      RKTEMPA 
          BRANCH    OVRCD,REPT9000          * end of file
.
          CALL      PLIN0000                * print a line of report
          GOTO      REPT1000
.
REPT9000  CALL      PEND0000                * print end of report
.
REPT9999  RETURN
+
.*********************************************************************
.*                  FNAM0000                    Called by : EXTR0000 *
.*        Format Clinic/Surgeon name                                 *
.*********************************************************************
FNAM0000  PACK      KEY6,KDOCT              * set up the key
          MOVE      "Invalid Code",KDOCTD
          BRANCH    OPCNCLSU,FNAM5000       * using doctor codes
.
.         validate using clinic file
.
          CALL      RDOPCLI1                * read clinic file
          BRANCH    OVRCD,FNAM9999          * invalid code
          MOVE      OPCLDESC,KDOCTD         * use clinic description
.
          MATCH     SP6,OPCLDOCT
          GOTO      KDOC9999 IF EQUAL       * no docotor code to use
          MOVE      OPCLDOCT,KEY6           * use the doctor code
.
.         validate using doctors file
.
FNAM5000  CALL      RDDOCT1                 * read docs file
          BRANCH    OVRCD,KDOC9999          * invalid code
.
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DTITL,PACTITLE          * title
          MOVE      TWO,PACFRMT             * SURNAME, TITLE GIVEN
          CALL      PACNAME                 * format the name
          MOVE      PACFNAME,KDOCTD         * set up doctors name
.
FNAM9999  RETURN
+
.*********************************************************************
.*                  KDOC0000                    Called by : CLIN0000 *
.*        Common Keyin for the surgeon or clinic code                *
.*        Para's  : OPCNCLSU      whether to use clinic or surgeon   *
.*                  CVERT         row for input                      *
.*                  CCOL          column for input                   *
.*        Returns : KDOCT         the code entered                   *
.*                  KDOCTD        the description                    *
.*                  EXIT = 1      nothing entered                    *
.*********************************************************************
KDOC0000  MOVE      ONE,SCRNFLAG            * set ? as not performed
.
KDOC1000  KEYIN     *PCCOL:CVERT,*EL,*DV,UNDLN6:
                    *PCCOL:CVERT,*V2LON,KDOCT:
                    *PCCOL:CVERT,*DV,KDOCT;
.
          ENDSET    KDOCT
          APPEND    SP6,KDOCT
          RESET     KDOCT
.
          MATCH     SP6,KDOCT
          GOTO      KDOC5000 IF EQUAL       * nothing entered
.
          CMATCH    QUEST,KDOCT
          GOTO      KDOC2000 IF NOT EQUAL   * validate what was entered
.
.         ? entered, call required routine according to OPCNCLSU
.
          BRANCH    OPCNCLSU,KDOC1500       * using doctor codes
.
          CALL      OPRCLIDS                * ? for clinics
          GOTO      KDOC1600
.
KDOC1500  CALL      GETSCR00 
          MOVE      SP10,KDOCT
          CALL      PATDOCDS                * ? for doctors
          CALL      PUTSCR00
          BRANCH    EXIT,KDOC5000
.
. ------- doctor selected from ? --------------------------------
.
          MOVE      FALSE,EXIT                    * valid input
          MOVE      DOCCODE,KDOCT
          PACK      KEY6,KDOCT
          CALL      RDDOCT1
          GOTO      KDOC2000
.
KDOC1600  MOVE      "24",CVERT
          MOVE      "14",CCOL
          MOVE      ZERO,SCRNFLAG
          DISPLAY   *P1:24,"Enter Code : ",*EL;
          GOTO      KDOC1000
.
.         validate what was entered, according to OPCNCLSU
.
KDOC2000  CALL      FNAM0000                * set up name
          BRANCH    OVRCD,KDOC4000          * invalid code
.
KDOC3000  MOVE      FALSE,EXIT              * valid answer
          GOTO      KDOC6000                * check for redisplay
.
.         invalid code entered
.
KDOC4000  DISPLAY   *P1:24,*EL,*B,"Invalid Code entered.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,KDOC1000       * input on original line
          DISPLAY   *P1:24,"Enter Code : ",*EL;
          GOTO      KDOC1000
.
.         nothing entered
.
KDOC5000  PACK      KDOCTD,SP20,SP20,SP20   * clear doctor description
          MOVE      TRUE,EXIT               * set for nothing entered
.
.         check for redisplay
.
KDOC6000  BRANCH    SCRNFLAG,KDOC9999
.
          DISPLAY   *P1:3,*EF:
                    *V2LON,*P1:4,"0",*P1:5,"1",*P1:6,"2",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= by ",OPCNCDSC:
                    *P3:6,"= by Date":
                    *P1:8,"Select option : ":
                    *P1:11,"Starting date : ",*V2LON,DATE10A,*HOFF:
                    *P1:12,"Ending   date : ",*V2LON,DATE10B,*HOFF:
                    *P1:14,"Starting ",*+,OPCNCDSC," : ",*V2LON,CDFRCLIN,*HOFF:
                    SP5,DXFRCLIN:
                    *P1:15,"Ending   ",*+,OPCNCDSC," : ",*V2LON,CDTOCLIN,*HOFF:
                    SP5,DXTOCLIN;
.
          MATCH     SP6,CDFRCLIN
          GOTO      KDOC9999 IF NOT EQUAL   * have a code
.
          DISPLAY   *PCCOL1:14,*V2LON,"Start",*EL;
.
KDOC9999  RETURN
+
.*********************************************************************
.*                  HEAD0000                    Called by : REPT0000 *
.*        Print the page heading                            PLIN1000 *
.*********************************************************************
HEAD0000  BRANCH    OPCNR74A,HEAD1000
          GOTO      HEAD9999
.
HEAD1000  CALL      HEAD80 
          PRINT     *20,"Start Date : ",DATE10A
          PRINT     *20,"End   Date : ",DATE10B,*N
          PRINT     *20,*+,OPCNCDSC," Code : ";
          MATCH     SP6,CDFRCLIN
          GOTO      HEAD1100 IF NOT EQUAL   * have a code
.
          PRINT     "Start to ";
          GOTO      HEAD1200
.
HEAD1100  PRINT     CDFRCLIN," to ";
.
HEAD1200  MATCH     "zzzzzz",CDTOCLIN
          GOTO      HEAD1400 IF NOT EQUAL
.
          PRINT     "Finish",*N
          GOTO      HEAD1500
.
HEAD1400  PRINT     CDTOCLIN,*N
.
HEAD1500  CALL      UND80 
          PRINT     "|    Date    | Time  | ",OPCNCDSC,SP20,SP3,"| ":
                    OPCNODSC," |"
          CALL      UND80 
          MOVE      "10",CLNO
          GOTO      HEAD9999
.
HEAD9999  RETURN
+
.*********************************************************************
.*                  PLIN0000                    Called by : REPT1000 *
.*        Print a line of the report                                 *
.*********************************************************************
PLIN0000  BRANCH    OPCNR74A,PLIN1000
          GOTO      PLIN9999
.
.         print line of the report
.
PLIN1000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * print heading
.
          UNPACK    XSESDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,"| ",CPCDATE," | ",XTIME," | ",XCLIN,SP1,XDESC:
                    " | ",XTHET,"          |"
          ADD       ONE,CLNO
          ADD       ONE,CNTSUSP
          DISPLAY   *P20:24,CPCDATE
          GOTO      PLIN9999
.
PLIN9999  RETURN
+
.*********************************************************************
.*                  PEND0000                    Called by : REPT9000 *
.*        Print the end of the report                                *
.*********************************************************************
PEND0000  BRANCH    OPCNR74A,PEND1000
          GOTO      PEND9999
.
PEND1000  CALL      UND80
          PRINT     "Total number of Suspended Sessions : ",CNTSUSP:
                    *N,*N,"*** End of Report ***",*N
          GOTO      PEND9999
.
PEND9999  RETURN
+
.*********************************************************************
.*                  CTPA0000                    Called by : ML4000   *
.*        Create a temp file                                         *
.*********************************************************************
CTPA0000  CALL      DTPA0000                * delete temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEA,LENGTH,KEYVAR,SP30 
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPA,CFNAMEA           * open the temp file
.
CTPA9999  RETURN
+
.*********************************************************************
.*                  DTPA0000                    Called by : CTPA0000 *
.*        Delete temp file A                                ML4000   *
.*********************************************************************
DTPA0000  CLOSE     TEMPA                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEA * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
.
DTPA9999  RETURN
+
.*********************************************************************
.         temp file I/O
.*********************************************************************
RSTEMPA   RESET     KEY17
          READ      TEMPA,KEY17;;
          RETURN
.
RKTEMPA   MOVE      ZERO,OVRCD
          RESET     KEY17
          READKS    TEMPA;XCLIN,XSESDT,XTIME,XDESC,XTHET
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMPA   MOVE      ZERO,OVRCD
          RESET     KEY17
          WRITE     TEMPA,KEY17;XCLIN,XSESDT,XTIME,XDESC,XTHET
          RETURN
+
.
.         I/O's required
.
          INC       STD001IO/PBL            * standard routines
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       OPRCLIIO/INC            * clinic file
          INC       OPRCLIDS/PBL            * ? for clinic file
          INC       OPROPRIO/INC            * op. rooms file
          INC       OPRSESIO/INC            * session file
          INC       PATCODIO/INC
          INC       PATDO1IO/INC            * doctor file
          INC       PATDOCDS/PBL            * ? for doctor file
          INC       PATDOCKY/PBL            * ? for doctor file
...............................................................................

.***************************************************************************
.* System    :   Waiting List                                              *
.* Program   :   FXWLMHOS                                                  *
.* Desc      :   This program will populate WTWMIHSP with a keyed in       *
.*               hospital ID                                               *
.***************************************************************************
.* Date      :   17/12/2012                                                *
.* Author    :   Jill Parkinson                                            *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATHSPFD/INC
          INC       WATTR1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "FXWLMHOS"
PRGNAM    INIT      "Fix WL Intended Hospital     "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATHSPA1,"pathspaf"
.
          OPEN      WATTR1A1,"wattr1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:12,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      "12",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,PROC9999,PROC9999
          DISPLAY   *P15:21,*V2LON,PTHSNAME;
.
          DISPLAY   *P1:24,*EL,"Processing Record: ";
.
          PACK      KEY19,SP70
          CALL      RDSTREA1
PROC1000  CALL      RDKTREA1
          BRANCH    OVRCD,PROC9000
.
          DISPLAY   *P30:24,*EL,*V2LON,WMURNO,WMCODE,DWMCNT;
.
          MATCH     SP70,WTWMIHSP
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      WTWMIHSP,PTHSHOSP
          CALL      UPTREA1
.
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:20,*EL,"Processing waiting list file complete.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATHSPKY
          INC       PATHSPIO/INC
          INC       WATTR1IO/INC

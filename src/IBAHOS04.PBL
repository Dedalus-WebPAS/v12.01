. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAHOS04                                            *
. *                                                                           *
. *     FUNCTION:         OUTSTANDING DEBTORS REPORT                          *
. *                                                                           *
. *     DATE WRITTEN:     07/04/88                                            *
. *                                                                           *
. *     AUTHOR:           PAUL LOO                                            *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
. *       V12.00.01  16/05/2025  Ebon Clements TSK 0955096                    *
. *                  Alphanueric visit number changes                          *
. *       V10.13.01  05/12/2018  Don Nguyen    TSK 0838558                    *
. *                  Deleted temp file (ROLLFILE) after processing            *
. *       V10.05.01  01/01/2015  Ania P        CAR 261630                     *
. *                  Removed use of PORT for temp file naming                 *
. *       V10.00.01  24/05/2010  J.Tan         CAR 219470                     *
. *                  Fixed calculating outstanding amt to include GST Journals*
. *        V9.12.01  08/10/2009  WeeLee Koo    CAR 207260                     *
. *                  Added From and To Date to PRTHED3                        *
. *        V9.09.01  29/10/2007  Ebon Clements CAR 152262                     *
. *                  Removed hard coded (MAB) from MABCODE                    *
. *        V9.04.03  29/08/2005  Mike Laming   CAR 73905                      *
. *                  Change WADMNO to FORM 8 (was still FORM 6 from 1989)     *
. *        V9.04.02  01/08/2005  J.Tan         CAR 69331                      *
. *                  Mods for multi hospital                                  *
. *        V9.04.01  07/03/2005  Mike Laming   CAR 59041                      *
. *                  Add new column for "Credit Note" totals                  *
. *        V9.03.02  13/08/2003  Lina Vo       CAR 41196                      *
. *                  Recompiled for big changes in PATSELFN.                  *
. *        V9.03.01  04/06/2003  Dean Cramer   CAR 38200                      *
. *                  Fixed heading in Outpatient options                      *
. *        V5.08.01  28/08/2000  Caleb Sharman                                *
. *                  Changed Health Fund variables to be 8 chars              *
. *        V5.07.B02 27/01/1999  N Harrington                                 *
. *                  507 rebounds                                             *
. *        V5.07.B01 17/11/1998 Jim Stathopoulos                              *
. *                  507 Changes                                              *
. *****************************************************************************
. *                       W4.01 16/06/88 G.Williams (I.B.A)                   *
. *                             New method of deter. claims                   *
. *                       W4.02 07/07/88 K.Peak     (I.B.A)                   *
. *                             Added print totals                            *
. *                       W4.03 16/01/89 K.Peak     (I.B.A)                   *
. *                             Added new Surname Sequence                    *
. *                             with claim code                               *
. *                       W4.04 31/01/89 K.Peak     (I.B.A)                   *
. *                             Added PATSELFN                                *
. *                       V5.00 25/04/89 J.Tan                                *
. *                             Ignore Cancelled Invoices                     *
. *                             SRF 3596                                      *
. *                       V5.01 05/05/89 Graeme Williams                      *
. *                             Added PINVCADM to inv file                    *
. *                             SRF 4219                                      *
. *                       V5.02 17/05/89 DIG                                  *
. *                             Changed to standard and                       *
. *                             modified print for 8 digit                    *
. *                             admission numbers etc                         *
. *                       V5.03 22/05/89 J.Tan                                *
. *                             Fixed Surname & Claim Seq.                    *
. *                             SRF 100660                                    *
. *                       V5.04 27/05/89 J.Tan                                *
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMI1FD).     SRF 100750                    *
. *                       V5.05 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                       V5.06 30/06/89 DIG                                  *
. *                             Re-Compiled for changes to                    *
. *                             OUTSITFD                                      *
. *                    V5.01.01 04/07/89 M.Ohleiter                           *
. *                             Changes for 8 char ur and                     *
. *                             admiss - release 5.01                         *
. *                    V5.01.02 17/07/89 J.Tan                                *
. *                             Mods to cont. after error                     *
. *                             SRF 101629                                    *
. *                    V5.01.03 25/08/89 K.Peak                               *
. *                             Fixed I * L on Y to post                      *
. *                    V5.01.04 21/09/89 K.Peak                               *
. *                             Fixed alignment of report                     *
. *                             SRF 102363                                    *
. *                    V5.01.05 20/11/89 J.Tan                                *
. *                             Add other financial option                    *
. *                             in PATSELFN.    SRF 102252                    *
. *                   V5.01.121 09/07/92 Peter Eddey                          *
. *                             srf 114050 - added                            *
. *                             date range.                                   *
. *       V5.03.B1  06/06/95  Paul Howells                  SRF 136735        *
. *                 Recompiled for PATSELFN                                   *
. *                                                                           * 
. *****************************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
          INC       IBAPASFD/INC
          INC       IBASECFD/INC
          INC       OUTSITFD/INC
          INC       PATCODFD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATSELDT/INC
          INC       PATWC1FD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.        EXTRA   VARIABLES
.
SORTSTR   DIM       20
SORT1     INIT      "u1-37"
SORT2     INIT      "u38-40,1-37"                                      *C-59041
.
WORKFILE  IFILE     SQL, FIXED=99, KEY=SORTSTR                         *C-59041
ROLLFILE  FILE      ASCII, FIXED=256
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.WSNAME         DIM       20         20             1
.WGNAME         DIM        1          1            21
DWADMNO         DIM        8          8            22
.WINVNO         DIM        8          8            30
.WCLAIM         DIM        3          3            38
.WINVTOT        FORM       8.2        6            41
.WFDINS         DIM        6          6            47
.WFUNP          FORM       6.2        5            53
.WINSP          FORM       6.2        5            58
.WPATP          FORM       6.2        5            63
.WJOUP          FORM       8.2        6            68
.WCRED          FORM       8.2        6            74                  *I-59041
.WBALANCE       FORM       9.2        7            80
.WSPARE         DIM        12         12           87
. ----- EOR 99 -----
.
ZEROES    FORM      " .00"
.
CMDLINE   DIM       30
FNAME     DIM       8
TEMPFILE  DIM       8
DATFILEW  INIT      "HOS04A"
TMPFILEH  INIT      "HOS04B"
.
COUNT     FORM      2
CODE      DIM       2
CODECL    INIT      "CL"
.
DIM8      DIM       8
DIM22     DIM       22
.
FIVE9     FORM      "59"
FROMDATE  DIM       8
.
PNAME     DIM       22
.
SAVPG     FORM      4
SAVCLM    DIM       3
.
TODATE    DIM       8
.
LNO       FORM      2
LN55      FORM      "55"
.
. ----- Variables from tempfile WORKFILE
.
WADMNO    DIM       8                                                  *C-73905
WSNAME    DIM      20
WGNAME    DIM       1
WINVNO    DIM       8 
WINVTOT   FORM      8.2
WFDINS    DIM       6 
WFUNP     FORM      6.2
WINSP     FORM      6.2
WPATP     FORM      6.2
WJOUP     FORM      8.2                                                *C-59041
WCRED     FORM      8.2                                                *I-59041
WBALANCE  FORM      9.2
WCLAIM    DIM       3
WSPARE    DIM       12                                                 *I-59041
.
TINVTOT   FORM      8.2
TFUNP     FORM      6.2
TINSP     FORM      6.2
TPATP     FORM      6.2
TJOUP     FORM      8.2                                                *C-59041
TCRED     FORM      8.2                                                *I-59041
TBALANCE  FORM      9.2
.
GINVTOT   FORM      8.2
GFUNP     FORM      6.2
GINSP     FORM      6.2
GPATP     FORM      6.2
GJOUP     FORM      8.2                                                *C-59041
GCRED     FORM      8.2                                                *I-59041
GBALANCE  FORM      9.2
.
COTHSFN   FORM      1
CMABINS   DIM       6       SECTOR 13, POS 188
CVETINS   DIM       6       SECTOR 13, POS 194
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
.
PRGID     INIT      "IBAHOS04"
PRGNAM    INIT      "Outstanding Debtors Report"
VERSION   INIT      "V12.01.00"
.
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patinvrf";
          OPEN      PATINVR2,"patinvrf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patwc1af";
          OPEN      PATWC1A1,"patwc1af"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN3;*188,CMABINS,*194,CVETINS
          READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAME
.
          CALL      TFILENAM
          MOVE      TFILNAME,TEMPFILE
.
MENU      MOVE      SP3,SAVCLM
          MOVE      SP4,SAVPG
.
BEGIN     HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Surname Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Surname Sequence with ":
                                               "Claim Code"
.
          KEYIN	    *P1:8,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF KSORT10,KSORT20
          DISPLAY   *P35:8,*EL,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      BEGIN 
.
KSORT10   MOVE      SORT1,SORTSTR        * Surname sequence only
          GOTO      KADAT   
.
KSORT20   MOVE      SORT2,SORTSTR        * Claim code and surname
.
.         Keyin from date
.
KADAT     DISPLAY   *P1:12,*EF,"From Date : ":
                        *P1:14,"To Date   :"
.
          MOVE      TEN2,CVERT
          MOVE      TEN5,CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          PACK      FROMDATE,CCC,CYY,CMM,CDD
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      "45",ECOL           * Error message
          MOVE      CVERT,EVERT
          CALL      KEYDATE
          BRANCH    OVRCD OF BEGIN
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
.         Keyin todate
.
          MOVE      TEN4,CVERT
          MOVE      TEN5,CCOL
.
          MOVE      "45",ECOL          * Error message
          MOVE      CVERT,EVERT
          CALL      KEYDATE
          BRANCH    OVRCD OF KADAT
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      INVDATE IF LESS
.
          CALL      KHOS0000
          BRANCH    EXIT,KADAT
.
          CALL      CONTQST
          BRANCH    CEXIT,CONFIRM,KADAT,MENU
.
CONFIRM   CALL      PATSELFN
          BRANCH    EXIT,BEGIN
.
          KEYIN     *P1:24,*EL,"Ok to continue (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS,*C,*EL
.
          REP       UPPLOW,ANS                                         *I-59041
          CMATCH    ANSY,ANS
          GOTO      PROCESS IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      BEGIN IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.
INVDATE   DISPLAY   *P1:24,*EL,*P20:24,*V2LON,*B:
                    "** Fromdate must be less than Todate **",*W3;
          GOTO      KADAT
.
PROCESS   DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialise Work File **",*W2;
          CALL      CREATWRK
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Process Outstanding Debtors **",*W2;
          DISPLAY   *P1:24,*EL,*P10:24:
                    "Processing Invoice Number :",*P50:24,"Scanning :"
.
          PACK      KEY16,FROMDATE,SP20
          CALL      RDSINV2 
LOPINV    CALL      RDKINV2
          BRANCH    OVRCD OF ENDINV
.
          MATCH     PINVDTE,TODATE
          GOTO      ENDINV IF LESS
.
.         Ignore  cancelled invoice
.
          MATCH     ZEROVISN,PINVADM
          GOTO      LOPINV IF EQUAL
.
          DISPLAY   *P61:24,PINVNO;
.
          COMPARE   FSTSYS,PINVSYS
          GOTO      LOPINV IF LESS
.
          COMPARE   PINVSYS,FEDSYS
          GOTO      LOPINV IF LESS
.
          MATCH     FSTSITE,PINVSITE
          GOTO      LOPINV IF LESS
.
          MATCH     PINVSITE,FEDSITE
          GOTO      LOPINV IF LESS
.
          MATCH     SP3,FSTDEPT
          GOTO      CNTI IF EQUAL
.
          MOVE      PINVSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,LOPINV
.
          MATCH     FSTDEPT,OSTSYST
          GOTO      LOPINV IF LESS
.
          MATCH     OSTSYST,FEDDEPT
          GOTO      LOPINV IF LESS
.
CNTI      MOVE      PINVAMT,WBALANCE
          ADD       PINVHFDA,WBALANCE
          ADD       PINVOTHA,WBALANCE
          ADD       PINVPATA,WBALANCE
          ADD       PTINCRTT,WBALANCE       * already negative         *I-59041
          ADD       PINVJOUR,WBALANCE
          IF        IBCNUGST=2 | IBCNUGST=3
            ADD       PTINGSTJ,WBALANCE
          ENDIF
.
          COMPARE   ZEROES,WBALANCE
          GOTO      LOPINV IF EQUAL
.
          DISPLAY   *P38:24,*V2LON,PINVNO;
.
          PACK      PSNAME WITH SP10,SP10
          PACK      PGNAME WITH SP10,SP10,SP10
.
          MOVE      PINVUR,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMAST
.
          COMPARE   ONE,IBCNMHOS
          GOTO      CNTI100 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      CNTI100 IF EQUAL
.
          MOVE      PINVADM,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LOPINV
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LOPINV IF NOT EQUAL
.
CNTI100   MOVE      PGNAME,WGNAME
          MOVE      PINVADM,DWADMNO
          MOVE      PSNAME,WSNAME
          PACK      KEY37 WITH WSNAME,WGNAME,DWADMNO,PINVNO
.
          MOVE      PINVCLM,WCLAIM
          MOVE      PINVADM,WADMNO
          MOVE      PINVNO,WINVNO
          MOVE      PINVAMT,WINVTOT
          MOVE      PINVHFDA,WFUNP
          MOVE      PINVOTHA,WINSP
          MOVE      PINVPATA,WPATP
          MOVE      PINVJOUR,WJOUP
          IF        IBCNUGST=2 | IBCNUGST=3
            ADD       PTINGSTJ,WJOUP
          ENDIF
          MOVE      PTINCRTT,WCRED                                     *I-59041
.
          UNPACK    SP5 WITH TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
          MOVE      "CL",TCODE
          PACK      KEY5 WITH TCODE,PINVCLM
          CALL      RDCODE1
.
          MOVE      ZERO,COUNT
CLMLOOP   ADD       ONE,COUNT
          COMPARE   SIX,COUNT
          GOTO      NOTACLM IF NOT LESS
.
          LOAD      ANS USING COUNT OF TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          CMATCH    "W",ANS
          GOTO      WORKCMP IF EQUAL
.
          CMATCH    "M",ANS
          GOTO      MABCODE IF EQUAL
.
          CMATCH    "V",ANS
          GOTO      VETCODE IF EQUAL
.
          GOTO      CLMLOOP
.
NOTACLM   MOVE      SP6,AFUNDH
          MOVE      PINVADM,KEY8
          CALL      RDMISS1
.
          MOVE      AFUNDH,WFDINS
          GOTO      OKWRT
.
WORKCMP   MOVE      "(WC)  ",WFDINS
          MOVE      PINVADM,KEY8
          CALL      RDWCOM1
          BRANCH    OVRCD OF OKWRT
.
          MATCH     SP6,WCICODE
          GOTO      OKWRT IF EQUAL
.
          MOVE      WCICODE,WFDINS
          GOTO      OKWRT
.
MABCODE   PACK      WFDINS,LBRACK,PINVCLM,RBRACK
...       MOVE      "(MAB) ",WFDINS
          MATCH     SP6,CMABINS
          GOTO      OKWRT IF EQUAL
          MOVE      CMABINS,WFDINS
          GOTO      OKWRT
.
VETCODE   MOVE      "(VET) ",WFDINS
          MATCH     SP6,CVETINS
          GOTO      OKWRT IF EQUAL
.
          MOVE      CVETINS,WFDINS
.
OKWRT     CALL      WRWORK1
          GOTO      LOPINV
.
.      PRINT OUTSTANDING DEBTOR REPORT
.
ENDINV 
        MOVE         ZERO,TINVTOT
        MOVE         ZERO,TFUNP
        MOVE         ZERO,TINSP
        MOVE         ZERO,TPATP
        MOVE         ZERO,TJOUP
        MOVE         ZERO,TCRED                                        *I-59041
        MOVE         ZERO,TBALANCE
.
        MOVE         ZERO,GINVTOT
        MOVE         ZERO,GFUNP
        MOVE         ZERO,GINSP
        MOVE         ZERO,GPATP
        MOVE         ZERO,GJOUP
        MOVE         ZERO,GCRED                                        *I-59041
        MOVE         ZERO,GBALANCE
        MOVE         SP3,SAVCLM
.
        DISPLAY      *P1:24,*EL,*P10:24,*V2LON:
                     "** Generating Outstanding Debtor Report **":
                     *W2,*C,*EL
.
        DISPLAY      *P1:24,*EL,*P10:24:
                     "Printing Patient Name :"
.
        MOVE         ZERO,CPAGENO
        MOVE         "60",LNO
.
        PACK         KEY37 WITH SP30,SP5
        CALL         RDSWORK1
PRTLOP  CALL         RDKWORK1
        BRANCH       OVRCD OF EOPRT
. 
        PACK         DIM22 WITH WGNAME,DOT,WSNAME
        MOVE         WADMNO,DIM8
.
        DISPLAY      *P34:24,*V2LON,DIM22;
.
        COMPARE      ZERO,CPAGENO
        GOTO         NOTFST IF NOT EQUAL
.
        MOVE         WADMNO,PINVADM       **** SAVE ADMISSION NUMBER
        MOVE         WADMNO,DIM8
        GOTO         OKCONT
.
NOTFST  MATCH        WADMNO,PINVADM
        GOTO         NOTSAM IF NOT EQUAL
.
        MOVE         SP8,DIM8
        MOVE         SP30,DIM22
        GOTO         OKCONT
.
NOTSAM  MOVE         WADMNO,PINVADM       **** SAVE ADMISSION NUMBER
.
OKCONT  MULT         SEQ,WFUNP
        MULT         SEQ,WINSP
        MULT         SEQ,WPATP
        MULT         SEQ,WJOUP
        MULT         SEQ,WCRED              * show it as posative       I-59041
.
        BRANCH       OPTION,OKCNT1
.
        MATCH        WCLAIM,SAVCLM
        GOTO         OKCNT1 IF EQUAL
.
        MOVE         WCLAIM,SAVCLM
.
        COMPARE      ZERO,CPAGENO
        GOTO         OKCNT1 IF EQUAL
.
        CALL         SUBTOT
        CALL         PRTHED0
.
OKCNT1  COMPARE      LN55,LNO
        CALL         PRINTH IF NOT LESS
.
        PRINT        *1,"|",DIM8,"| ",DIM22," |",WINVNO,"|",WINVTOT:   *C-59041
                     "| ",WFDINS,"|",WFUNP,"|",WINSP:                  *C-59041
                     "|",WPATP,"|",WJOUP,"|",WCRED,"|",WBALANCE," |"   *C-59041
.
        ADD          WINVTOT,TINVTOT
        ADD          WFUNP,TFUNP
        ADD          WINSP,TINSP
        ADD          WPATP,TPATP
        ADD          WJOUP,TJOUP
        ADD          WCRED,TCRED                                       *I-59041
        ADD          WBALANCE,TBALANCE
.
        ADD          ONE,LNO
        GOTO         PRTLOP
.
EOPRT   COMPARE      ZERO,CPAGENO
        GOTO         NODEBT IF EQUAL
.
        COMPARE      ONE,OVRCD
        GOTO         PRTLIN IF NOT EQUAL
.
        CALL         LINE
.
PRTLIN  BRANCH       OPTION OF PRTTOT,PRTGRD
.
.******************************************** Start of Changes         *C-59041
PRTTOT  PRINT        *35,"|TOTAL   |",TINVTOT,*56,"|",*64,"|",TFUNP:
                     *74,"|",TINSP,*84,"|",TPATP,*94,"|",TJOUP:   
                     *106,"|",TCRED,*118,"|",TBALANCE,*132,"|"      
.
        GOTO         CONPRT
.
PRTGRD  CALL         SUBTOT
        PRINT        *35,"|G/TOTAL |",GINVTOT,*56,"|",*64,"|",GFUNP:
                     *74,"|",GINSP,*84,"|",GPATP,*94,"|",GJOUP:
                     *106,"|",GCRED,*118,"|",GBALANCE,*132,"|"
.
CONPRT  PRINT        *35,"*-----------------------------------------":
                          "-----------------------------------------":
                          "--------------*":    
.********************************************   End of Changes         *C-59041
                     *N,*N,"*** End Of Report ***"
.
        DISPLAY      *P1:24,*EL,*P10:24,*V2LON:
                     "** Generation Completed Successfully **":
                     *W2,*C,*EL
        CALL         DELETWRK
        GOTO         BEGIN
.
SUBTOT  COMPARE      ONE,OVRCD
        GOTO         SBTOT1 IF EQUAL
.        
        CALL         LINE
.
.******************************************** Start of Changes         *C-59041
SBTOT1  PRINT        *35,"|TOTAL   |",TINVTOT,*56,"|",*64,"|",TFUNP:
                     *74,"|",TINSP,*84,"|",TPATP,*94,"|",TJOUP:
                     *106,"|",TCRED,*118,"|",TBALANCE,*132,"|":
                     *N,*35,"*-----------------------------------------":
                          "-----------------------------------------":
                          "--------------*"    
.********************************************   End of Changes         *C-59041
.
        ADD          TINVTOT,GINVTOT
        ADD          TFUNP,GFUNP
        ADD          TINSP,GINSP
        ADD          TPATP,GPATP
        ADD          TJOUP,GJOUP
        ADD          TCRED,GCRED                                       *I-59041
        ADD          TBALANCE,GBALANCE
.
        MOVE         ZERO,TINVTOT
        MOVE         ZERO,TFUNP
        MOVE         ZERO,TINSP
        MOVE         ZERO,TPATP
        MOVE         ZERO,TJOUP
        MOVE         ZERO,TCRED                                        *I-59041
        MOVE         ZERO,TBALANCE
        RETURN
.
NODEBT  DISPLAY      *P1:24,*EL,*P10:24,*V2LON:
                     "** No Outstanding Debtors **":
                     *W3,*C,*EL
        CALL         DELETWRK
        GOTO         BEGIN
.
PRINTH  COMPARE     ZERO,CPAGENO
        CALL        LINE IF NOT EQUAL
.
PRTHED0 UNPACK      FROMDATE,XCENT,XYEAR,XMON,XDAY
        UNPACK      TODATE,XCENT1,XYEAR1,XMON1,XDAY1
        BRANCH      OPTION OF PRTHED1,PRTHED2
.
PRTHED1 MOVE        ONE,CNOUNDLN
        MOVE        "SURNAME SEQUENCE",CPHDROPT
        CALL        IBACLOCK
        CALL        HEAD132
        PRINT       *N,*40,"From Date : ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    " to ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1,*N;
        GOTO        CPRINTH
.
PRTHED2 MOVE        ONE,CNOUNDLN
        MOVE        SP20,TDESC
.
        MATCH       SP3,WCLAIM
        GOTO        PRTHED3 IF EQUAL
.
        PACK        KEY5 WITH CODECL,WCLAIM
        CALL        RDCODE1
.
PRTHED3 CALL        IBACLOCK
        CALL        HEAD132
        PRINT       *40,"SURNAME SEQUENCE ":
                        "WITH CLAIM CODE":
                    *N,*40,"From Date : ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    " to ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *N,*40,"CLAIM CODE - ",WCLAIM,"   ",TDESC,*N
.
CPRINTH IF          IBCNMHOS =1
          PRINT       *40,"Hospital : ",PTHSNAME
        ELSE
          PRINT       *N;
        ENDIF
.
        CALL        PRTSEL
        CALL        LINE
.
.******************************************** Start of Changes         *C-59041
        PRINT       *1,"| Adm No | Patient Name":
                    *35,"| Invoice|   Invoice | Fund/ |   Fund  |Insurance":
                    *84,"| Patient | Journal  |    Credit  |   Balance   |":
                    *N,*1,"|        |":
                    *35,"| Number |   Total   | Ins.  |   Paid  |    Paid ":
                    *84,"|  Paid   |  Amount  |    Note    |   Amount    |"
.********************************************   End of Changes         *C-59041
        CALL        LINE
.
        MOVE        TEN,LNO
        RETURN
.
LINE    PRINT       *1,"*-----------------------------":
                       "------------------------------":
                       "------------------------------":
                       "------------------------------":
                       "-----------*"
        RETURN
.
INVMAST DISPLAY    *P1:23,*EL,*B,"U/R No.: ",*V2LON,PINVUR:
                   "  *** Master File Missing ":
                     "- Contact IBA *** ",*W2;
        GOTO       LOPINV
.
RDSWORK1 RESET     KEY37
         READ      WORKFILE,KEY37;;
         RETURN  
.
RDWORK1  MOVE      ZERO,OVRCD
         RESET     KEY37
         READ      WORKFILE,KEY37;WSNAME,WGNAME,DWADMNO,WINVNO,WCLAIM: *C-59041
                                  WINVTOT,WFDINS,WFUNP,WINSP,WPATP:    *C-59041
                                  WJOUP,WCRED,WBALANCE,WSPARE          *C-59041
         GOTO      OVERCOND IF OVER
         MOVE      DWADMNO,WADMNO
         RETURN  
.
RDKWORK1 MOVE      ZERO,OVRCD
         READKS    WORKFILE;WSNAME,WGNAME,DWADMNO,WINVNO,WCLAIM:       *C-59041
                            WINVTOT,WFDINS,WFUNP,WINSP,WPATP:          *C-59041
                            WJOUP,WCRED,WBALANCE,WSPARE                *C-59041
         GOTO      OVERCOND IF OVER
         MOVE      DWADMNO,WADMNO
         RETURN  
.
.
WRWORK1  RESET     KEY37
         MOVE      ZERO,OVRCD
         WRITE     WORKFILE,KEY37;KEY37,WCLAIM:                        *C-59041
                            WINVTOT,WFDINS,WFUNP,WINSP,WPATP:          *C-59041
                            WJOUP,WCRED,WBALANCE,WSPARE                *C-59041
         RETURN  
.
.
UPWORK1  UPDATE    WORKFILE;WSNAME,WGNAME,DWADMNO,WINVNO,WCLAIM:       *C-59041
                            WINVTOT,WFDINS,WFUNP,WINSP,WPATP:          *C-59041
                            WJOUP,WCRED,WBALANCE,WSPARE                *C-59041
         RETURN  
.
.
.
CREATWRK MOVE       ZERO,OVRCD
.
         CALL       DELETWRK
         PREP       ROLLFILE,TEMPFILE
.
         BRANCH     OPTION OF IDXSUR
.
         WRITE      ROLLFILE,SEQ;"isbuild ",FNAME," 99 ","u38-40,1-37" *C-59041
         GOTO       IDXNOW
.
IDXSUR   WRITE      ROLLFILE,SEQ;"isbuild ",FNAME," 99 ","u1-37"       *C-59041
.
IDXNOW   WEOF       ROLLFILE,SEQ
.
         CALL       MAINWRK1
         OPEN       WORKFILE,FNAME
         PACK       WSPARE,SP20                                        *I-59041
         RETURN
.
.
DELETWRK CLOSE      WORKFILE
         CLEAR      CMDLINE
         APPEND     "iserase ",CMDLINE
         APPEND     FNAME,CMDLINE
         RESET      CMDLINE
         EXECUTE    CMDLINE,TASKID
         RETURN
.
MAINWRK1 CLEAR      CMDLINE
         APPEND     "sh ",CMDLINE
         APPEND     TEMPFILE,CMDLINE
         APPEND     ".txt",CMDLINE
         RESET      CMDLINE
.
         DISPLAY    *P1:18,*EF;
         EXECUTE    CMDLINE,TASKID
         DISPLAY    *P1:18,*EF;
.
         CLOSE      ROLLFILE,DELETE
         RETURN
.
PRTSEL    COMPARE   ZERO,FSTSYS
          GOTO      AEHEAD IF NOT EQUAL
.
          PRINT     *45,"CONSOLIDATED REPORT",*N,*N;
          COMPARE   ZERO,FSTSYS
          RETURN    IF EQUAL
.
AEHEAD    COMPARE   ONE,FSTSYS
          GOTO      OPHEAD IF NOT EQUAL
.
          PRINT     *45,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          COMPARE   ONE,FSTSYS
          RETURN    IF EQUAL
.
OPHEAD    COMPARE   TWO,FSTSYS
          GOTO      IPHEAD IF NOT EQUAL
.
          PRINT     *45,"OUTPATIENT REPORT",*N;
          MATCH     SP6,FSTSITE
          GOTO      ALSITE IF NOT EQUAL
.
          PRINT     *45,"ALL SITES";
          GOTO      OPDEPT
.
ALSITE    PRINT     *45,"SITE ",FSTSITE;
.
OPDEPT    MATCH     SP3,FSTDEPT
          GOTO      ALDEPT IF NOT EQUAL
.
          PRINT     *59,"ALL DEPARTMENTS",*N,*N;
          GOTO      PRTSEL9
.
ALDEPT    PRINT     *59,"DEPARTMENT ",FSTDEPT,*N,*N;
          GOTO      PRTSEL9
.
IPHEAD    COMPARE   THREE,FSTSYS
          GOTO      OTHEAD IF NOT EQUAL
.
          PRINT     *45,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
OTHEAD    PRINT     *45,"OTHER FINANCIAL REPORT",*N,*N;
.
PRTSEL9   RETURN 
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:17,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN7,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:17,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATSELFN
          INC       PATHSPKY
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       PATWC1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATINVIO/INC
          INC       OUTSITIO/INC
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
ENDIT    CHAIN     PGM
         STOP
.

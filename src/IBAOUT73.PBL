.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT73.PBL                                              *
.* Name           : Attendance Register Listing                               *
.******************************************************************************
.* Date           : 18/11/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To print all the details from the OUTHARFD.               *
.* Modifications  :                                                           *
.******************************************************************************
.*        V11.02.01 09/02/2022  Thanh T          TSK 0905641                  *
.*                  Recompiled as OUTMA1FD changes                            *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.05.01 17/12/2014  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.09.01  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 15/03/2000  Steve Armstrong     5.08 Mods                 *
.*                  Recompiled for changes to OUTCLIFD (effective date)       *
.******************************************************************************
.*        V5.01.02  04/01/1993  Graeme Williams            SRF 119697         *
.*                  Print in attendance time order, not slot number order     *
.*        V5.01.03  04/01/1993  Graeme Williams            SRF 119697         *
.*                  Removed the U/R number from OUTHARFD.                     *
.*                  Use a temp file to determine what has to be deleted       *
.*        V5.01.04  02/03/93  ROD                                             *
.*                  recompiled for new outma1af                               *
.******************************************************************************
.
          INC       STD001FD/PBL
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC            * booking file
          INC       OUTCLIFD/INC            * clinic file
          INC       OUTCONFD/INC            * system parameters
          INC       OUTCTYFD/INC            * clinic type file
          INC       OUTHARFD/INC            * attend. reg file
          INC       OUTMA1FD/INC            * master file
          INC       PATDO1FD/INC            * doctor file
          INC       PATMA1FD/INC            * master file
          INC       PATPR1FD/INC            * pre-attendance master file
          INC       PMSHCPFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
TEMPFILE  FILE      ASCII, FIXED=29
.
CURRDATE  DIM       8         * current date
DIM40     DIM       40        * for outdscln
DXCLIN    DIM       20
DXDATE    DIM       10
FNAMET    DIM       8
KCOL      FORM      3         * keyin column
NAME      DIM       30
VCLIN     DIM       6         * clinic id
VDATE     DIM       8         * clinic date
VSITE     DIM       6         * current site
VTIME     DIM       5         * clinic start time
.
. for outdsmas
.
DDAY      DIM       9
SHCTYP    DIM       20
.
FINISH    INIT      "Finish"
START     INIT      "Start "
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT73"
PRGNAM    INIT      "Attendance Register Listing"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.         MAINLINE CODE
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SCRN0000                * display screen
          CALL      KCLI0000                * enter clinic
          BRANCH    EXIT,ML9999
          CALL      KDAT0000                * enter date
          CALL      KTIM0000                * enter time
.
          CALL      VDET0000                * check if valid
          BRANCH    EXIT,ML1000             * invalid session
.  
          CALL      CONTQST
          BRANCH    CEXIT,ML5000,ML1000,ML9999
.
ML5000    CALL      DPAT0000                * display all patients
          CALL      DELR0000                * see if to delete records
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.         initilization
.*********************************************************************
INIT0000  CALL      DISPHEAD
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P58:24,"patpraxf"
.
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME,UID,FILHARA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTHARA1,CFNAME
          OPEN      OUTHARA2,CFNAME
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILMA1A1
          DISPLAY   *P58:24,CFNAME
          CALL       OPOTMA11
.
          PACK      CFNAME,UID,FILCTYA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      TEN5,KCOL
          MOVE      IDDD,VSITE
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAMET
.
INIT9999  RETURN
+
.*********************************************************************
.         display the screen
.*********************************************************************
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"Clinic ID   : ":
                    *P1:5,"Clinic Date : ":
                    *P1:6,"Clinic Time : "
.
          UNPACK    SP30,VCLIN,DXCLIN
          UNPACK    SP30,VDATE,DXDATE
.
SCRN9999  RETURN
+
.*********************************************************************
.         enter the clinic id : VCLIN
.*********************************************************************
KCLI0000  MOVE      ZERO,CKYIMAND           * not mandatry
          MOVE      SP6,CKYIDEF6            * no default
          MOVE      FOUR,EVERT              * set row
          MOVE      KCOL,ECOL               * set column
          MOVE      SP6,OCACLIN
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLI9100,KCLI9100,KCLI9100
.
          MOVE      OCACLIN,VCLIN           * set clinic code
          MOVE      OCADESC,DXCLIN
          DISPLAY   *P30:4,OCADESC
          GOTO      KCLI9999
.
KCLI9100  MOVE      ONE,EXIT
.
KCLI9999  RETURN
+
.*********************************************************************
.         enter the clinic date : VDATE
.*********************************************************************
KDAT0000  UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ONE,CDEFDTE
          MOVE      KCOL,CCOL
          MOVE      "5",CVERT
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00                * save the screen
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT2000         * ? entered
          BRANCH    OVRCD,KDAT0000          * enter a date
.
          CALL      FRESCR00                * free the screen
          PACK      VDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",VDATE
          CALL      PACDATE
          MOVE      CPCDATE,DXDATE
          GOTO      KDAT9999
.
KDAT2000  CALL      OUTDSMAS
          CALL      PUTSCR00
          GOTO      KDAT0000
.
KDAT9999  RETURN
+
.*********************************************************************
.         enter the clinic time : VTIME
.*********************************************************************
KTIM0000  CALL      DEFT0000
          BRANCH    EXIT,KTIM5000           * have a def. time
.
          UNPACK    SP4,CHOUR,CMIN
          MOVE      KCOL,CCOL
          MOVE      "6",CVERT
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00                * save the screen
          CALL      KEYTIME
          BRANCH    CQUEST,KTIM2000         * ? entered
          BRANCH    OVRCD,KTIM9999          * exit
.
          CALL      FRESCR00                * free the screen
          PACK      VTIME,CHOUR,COLON,CMIN
          REP       " 0",VTIME
.
KTIM5000  DISPLAY   *PKCOL:6,*V2LON,VTIME
          MOVE      ZERO,OVRCD
          GOTO      KTIM9999
.
KTIM2000  CALL      OUTDSMAS
          CALL      PUTSCR00
          GOTO      KTIM0000
.
KTIM9999  RETURN
+
.*********************************************************************
.         see if there is a default time : VTIME
.*********************************************************************
DEFT0000  MOVE      ZERO,FORM2
          MOVE      ZERO,EXIT               * make it enter time
          PACK      KEY28,VSITE,VCLIN,VDATE,SP30
          CALL      RDSBOKA1
.
DEFT1000  CALL      RDKBOKA1
          BRANCH    OVRCD,DEFT9999
.
          MATCH     VSITE,OBASITE
          GOTO      DEFT9999 IF NOT EQUAL
          MATCH     VCLIN,OBACLIN
          GOTO      DEFT9999 IF NOT EQUAL
          MATCH     VDATE,OBADATE
          GOTO      DEFT9999 IF NOT EQUAL
.
          MOVE      OBASTRT,VTIME           * default time
          ADD       ONE,FORM2
.
          MOVE      ONE,EXIT                * set to not enter time
          COMPARE   TWO,FORM2
          GOTO      DEFT1000 IF NOT EQUAL   * not on second time
.
          MOVE      ZERO,EXIT
          MOVE      SP5,VTIME
.         
DEFT9999  RETURN
+
.*********************************************************************
.         validate that the session is OK
.*********************************************************************
VDET0000  PACK      KEY28,VSITE,VCLIN,VDATE,VTIME,SP10
          CALL      RDSBOKA1
          CALL      RDKBOKA1
          BRANCH    OVRCD,VDET9000
.
          MATCH     VSITE,OBASITE
          GOTO      VDET9000 IF NOT EQUAL
          MATCH     VCLIN,OBACLIN
          GOTO      VDET9000 IF NOT EQUAL
          MATCH     VDATE,OBADATE
          GOTO      VDET9000 IF NOT EQUAL
          MATCH     VTIME,OBASTRT
          GOTO      VDET9000 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      VDET9999
.
VDET9000  DISPLAY   *P1:24,*B,"Invalid session details.  ",*EL;
          CALL      HITENTER
          MOVE      ONE,EXIT
.
VDET9999  RETURN
+
.*********************************************************************
.         display all the patients in the session selected
.*********************************************************************
DPAT0000  CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
.
          CALL      HEAD0000                 * heading for first page
.
.         Create a temp file to store the keys of the records to be deleted
.
          PREP      TEMPFILE,FNAMET
.
.         Loop through the attendances
.
          MOVE      ZERO,CCITEM
          PACK      KEY33,VSITE,VCLIN,VDATE,VTIME,SP30
          CALL      RDOTHAR2
          COMPARE   ZERO,OVRCD
          GOTO      DPAT2000 IF EQUAL       * valid read
.
DPAT1000  CALL      RKOTHAR2
          BRANCH    OVRCD,DPAT9000          * end of file
.
DPAT2000  MATCH     VSITE,OTHASITE
          GOTO      DPAT9000 IF NOT EQUAL   * different site
          MATCH     VCLIN,OTHACLIN
          GOTO      DPAT9000 IF NOT EQUAL   * different clinic
          MATCH     VDATE,OTHADATE
          GOTO      DPAT9000 IF NOT EQUAL   * different date
          MATCH     VTIME,OTHASTRT
          GOTO      DPAT9000 IF NOT EQUAL   * different start time
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * print new page
.
          PACK      KEY28,OTHASITE,OTHACLIN,OTHADATE,OTHASTRT,OTHASLOT
          CALL      RDBOKA1
          IF        OVRCD = 1
            MOVE      SP8,OBAURNO
            MOVE      "Missing Booking Details",NAME
            MOVE      SP30,PTMXCHNM
          ELSE
            MOVE      OBAURNO,KEY8
            CALL      RDMAST1
            IF        OVRCD = ZERO
              MOVE      PSNAME,PACSNAME
              MOVE      PGNAME,PACGNAME
              MOVE      SP10,PACTITLE
              MOVE      TWO,PACFRMT
              CALL      PACNAME
              MOVE      PACFNAME,NAME
            ELSE
              MATCH     ZEROUR,OBAURNO
              IF        @EQUAL
                MOVE      OBAOUTNO,KEY8
                CALL      RDPRAM1
                IF        OVRCD = ZERO
                  MOVE      PSNAME,PACSNAME
                  MOVE      PGNAME,PACGNAME
                  MOVE      SP10,PACTITLE
                  MOVE      TWO,PACFRMT
                  CALL      PACNAME
                  MOVE      PACFNAME,NAME
                ELSE
                  MOVE      "Missing PMI Details",NAME
                ENDIF
              ELSE
                MOVE      "Invalid U/R Number",NAME
              ENDIF
            ENDIF
          ENDIF
.
          ADD       ONE,CCITEM
          PRINT     *1,"|  ",CCITEM,*10,"|   ",OTHAATTM,*23,"| ",OBAURNO:
                    *34,"| ",NAME,*67,"| ",PTMXCHNM," | "
          ADD       ONE,CLNO
          CALL      UNDL0000 
.
.         Store the key in the temp file, for later deletion
.
          PACK      KEY28,OTHASITE,OTHACLIN,OTHADATE,OTHASTRT,OTHASLOT
          WRITE     TEMPFILE,CCITEM;KEY28
          GOTO      DPAT1000
.
DPAT9000  PRINT     *N,*N,"*** End of Report ***",*N
                    
DPAT9999  RETURN
+
.*********************************************************************
.         see if to delete the records
.*********************************************************************
DELR0000  DISPLAY   *P1:24,*B,"Do you wish to close off Register ? ",*EL;
          KEYIN     *P37:24,*DV,UNDLN1:
                    *P37:24,*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     ANSN,ANS
          GOTO      DELR9000 IF EQUAL       * dont delete
          MATCH     ANSY,ANS
          GOTO      DELR0000 IF NOT EQUAL   * invalid
.
DELR2000  COMPARE   ONE,CCITEM
          GOTO      DELR9000 IF LESS
.
          READ      TEMPFILE,CCITEM;KEY28
          GOTO      DELR9000 IF OVER
          CALL      DEOTHAR1
.
          SUB       ONE,CCITEM
          GOTO      DELR2000
.
.         Delete temporary file
.
DELR9000  CLOSE     TEMPFILE,DELETE
.
DELR9999  RETURN
+
.*********************************************************************
.         print report heading
.*********************************************************************
HEAD0000  CALL      HEAD132
          PRINT     *40,"Clinic Id   : ",VCLIN,SP4,DXCLIN
          PRINT     *40,"Clinic Date : ",DXDATE,"  Clinic Time : ",VTIME
          CALL      UNDL0000 
          PRINT     *1,"| Serial | Attendance | U/R No.  | ":
                    "Name                           | Chinese Name         |"
          PRINT     *1,"|  No.   |    Time    |          | ":
                    "                               |                      |"
          CALL      UNDL0000 
          MOVE      NINE,CLNO
HEAD9999  RETURN
+
.*********************************************************************
.         print underline
.*********************************************************************
UNDL0000  PRINT     *1,"*--------------------------------------------":
                       "--------------------------------------------*"
          ADD       ONE,CLNO
UNDL9999  RETURN
+
          INC       STD001IO/PBL
          INC       KYOUTCLI                * keyin from outclifd
          INC       IBASEQIO/INC
          INC       OUTDSCLN/PBL            * ? for clinics
          INC       OUTDSMAS/PBL            * ? for dates/time
          INC       OUTHARIO/INC            * attend. reg file
          INC       OUTBOAIO/INC            * booking file
          INC       OUTCLIIO/INC            * clinic file
          INC       OUTCTYIO/INC            * clinic type file
          INC       OUTMA1IO/INC            * master file
          INC       PATDO1IO/INC            * doctor file
          INC       PATMA1IO/INC            * master file
          INC       PATPR1IO/INC            * pre-attendance master file
          INC       PMSHCPIO/INC
          INC       TFILENAM
          INC       WEBERRIO/INC


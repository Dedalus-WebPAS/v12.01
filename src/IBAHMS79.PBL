.******************************************************************************
.* System         : Patient                                                   *
.* Program        : IBAHMS79                                                  *
.* Function       : Hospital file                 Master Listing              *
.******************************************************************************
.* Date           : 19.02.93                                                  *
.* Author         : Peter Eddey                                               *
.* Modifications  :                                                           *
.*        V10.02.01 05/04/2011  Jill Parkinson CAR 239574                     *
.*                  Changed key for ibaprtaf                                  *
.*        V5.02.02  16/05/1995    Steve Armstrong                             *
.*                  added pthshsno                                            *
.*        V5.02.01  10/05/1995    Justin Coates    (Taranaki Financial Intf)  *
.*                  added keyin of pthspact                                   *
.******************************************************************************
.*               V5.01.01  23/02/93  ROD                                      *
.*                         Use standard PATHSPKY keyin                        *
.*               V5.01.02  25/05/1994 Ian Rutt                                *
.*                         Fixed Global Recompile Bugs                        *
.*               V5.01.03  20/07/1994  Rob Leonard                            *
.*                         Fixed hospital keyin                               *
.******************************************************************************
.
          INC       STD001FD
          INC       PATHSPFD/INC
          INC       IBAPRTFD/INC
.
.*********************************************************************
.*                       Data Variables Required                     *
.*********************************************************************
.
COUNT     FORM      1
DIM3      DIM       3
ENDCODE   DIM       3
ENDDESC   DIM       50 
OLDTYPE   DIM       3
PRINTER   DIM       13
PROCTYPE  DIM       3
PROCDESC  DIM       30
STRTCODE  DIM       3
STRTDESC  DIM       50
VARNUMB   FORM      3
ROW       FORM      2
SP50      DIM       50
.
PRGID     INIT      "IBAHMS79"
PRGNAM    INIT      "Hospital File Master Listing"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                         ML0000                                    *
.*                     Mainline Code                                 *
.*********************************************************************
.
ML0000
          CALL      INIT0000             * initialization routine
          BRANCH    EXIT,ML9999      
ML0500
          CALL      MENU0000             * program menu
          BRANCH    EXIT,ML1000
          GOTO      ML9999
.
.***** Print listing by Clinic Type *****
.
ML1000
          CALL      DSCR0000             * display the screen
.
          CALL      IHOS0000             * input hospital codes
          BRANCH    EXIT,ML0500
.
          CALL      PRNT0000             * print the report
          GOTO      ML0500
.
ML9999
          CHAIN     PGM
.
.********************************************************************
.*                         INIT0000                                 *
.*                    Initialization routine                        *
.********************************************************************
.
INIT0000
          
          MOVE      ONE,CNEWDS
          CALL      DISPHEAD
.
          MOVE      ZERO,EXIT
.
INIT1000
.
          DISPLAY   *P54:24,*EL,"Opening pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P54:24,*EL,"Opening ibaprtaf"
          OPEN      IBAPRTA1,"ibaprtaf"
.
          OPEN      CONTROLF,"controlf"
          PACK      SP50,SP30,SP20
.
INIT9999  RETURN
.
.********************************************************************
.*                         DMEN0000                                 *
.*                  Display the programs menu                       *
.********************************************************************
.
DMEN0000
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0 ",*HOFF,"= Exit":
                    *P1:5,*V2LON,"1 ",*HOFF,"= Hospital ID sequence":    
                    *P1:7,"Select Option ? "
.
DMEN9999  RETURN
.
.********************************************************************
.*                         DSCR0000                                 *
.*                  Display the screen prompts                      *
.********************************************************************
.
DSCR0000
          DISPLAY   *P1:8,*EF:
                    *P1:10,"From Hospital  Id    :":  
                    *P1:11,"  To Hospital  Id    :"
.
DSCR9999  RETURN
.********************************************************************
.*                         MENU0000                                 *
.*                  Handle the programs menu                        *
.********************************************************************
.
MENU0000
          CALL      DMEN0000             * display the menu
MENU1000
          KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,EXIT:  
                    *P17:7,*DV,EXIT  
.
          COMPARE   ZERO,EXIT            * test if exit selected
          GOTO      MENU9999 IF EQUAL
.
.**** Test for valid options 1 or 2 ****
.
          BRANCH    EXIT,MENU9999
.
          BEEP                           * invalid option so re-keyin
          GOTO      MENU1000
.
.
MENU9999  RETURN
.
.**********************************************************************
.*                       IHOS0000                                     *
.*             Input the required hospital codes                      *
.**********************************************************************
IHOS0000  MOVE      ZERO,EXIT
.
          MOVE      TEN,ROW                           * set the row
          CALL      KHOS0000                          * keyin the hospital
          IF        EXIT=2
            MOVE      ONE,EXIT                        * EXITCHAR input
            GOTO      IHOS9999
          ENDIF
          IF        EXIT = 1
            DISPLAY   *P24:10,*V2LON,"Start"          * start of file
            MOVE      SP3,STRTCODE
          ELSE
            MOVE      PTHSNAME,STRTDESC
            MOVE      PTHSHOSP,STRTCODE
          ENDIF
.
          MOVE      TEN1,ROW                          * set the row
          CALL      KHOS0000                          * keyin the hospital
          IF        EXIT=2
            MOVE      ONE,EXIT                        * EXITCHAR input
            GOTO      IHOS9999
          ENDIF
          IF        EXIT = 1
            DISPLAY   *P24:11,*V2LON,"Finish"         * end of file
            MOVE      "zzz",ENDCODE
            MOVE      ZERO,EXIT
          ELSE
            MOVE      PTHSHOSP,ENDCODE
            MOVE      PTHSNAME,ENDDESC
          ENDIF
.
          MATCH     STRTCODE,ENDCODE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Hospital codes must be in sequence. ";
            CALL      HITENTER
            DISPLAY   *P24:10,*EL,*P24:11,*EL
            GOTO      IHOS0000
          ENDIF
.
          CALL      CONTQST
          BRANCH    CEXIT,IHOS9999,IHOS5000,IHOS9000
.
IHOS5000  DISPLAY   *P24:11,*EL,*P24:10,*EL
          GOTO      IHOS0000
.
IHOS9000  MOVE      ONE,EXIT
.
IHOS9999  RETURN
+
.****************************************************************************
.*                           KHOS0000                                       *
.*                  Keyin the hospital code                                 *
.****************************************************************************
KHOS0000  MOVE      "24",ECOL
          MOVE      ROW,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
.
          CALL      PATHSPKY                      * keyin a hospital code
.
          COMPARE   TWO,EXIT                      * EXITCHAR?
          GOTO      KHOS9999 IF EQUAL
.
          MOVE      KEY3,PTHSHOSP
          DISPLAY   *P28:ROW,PTHSNAME
.
KHOS9999  RETURN
+
.*********************************************************************
.*                        PRNT0000                                   *
.*                   Print the master listing                        *
.*********************************************************************
.
PRNT0000
          MOVE      ONE,CNOUNDLN
          MOVE      ZERO,CPAGENO
          DISPLAY   *P1:24,*EL,*V2LON,"***** Printing Report *****"
          CALL      HEAD0000             * print the heading
.
.****** Position correctly in the file *****
.
          PACK      STRTCODE,STRTCODE,SP3
          MATCH     SP3,STRTCODE
          IF        @EQUAL
            PACK      KEY3,STRTCODE,SP30
            CALL      RSPTHSP1
          ELSE
            PACK      KEY3,STRTCODE,SP30
            CALL      RSPTHSP1
            CALL      RPPTHSP1
            IF        OVRCD = 1
              MOVE      SP3,KEY3
              CALL      RSPTHSP1
            ENDIF
          ENDIF
PRNT1000
          CALL      RKPTHSP1             * get next record
          BRANCH    OVRCD,PRNT8000
.
          MATCH     PTHSHOSP,ENDCODE
          GOTO      PRNT8000 IF LESS
.
          PACK      PRTDESC,SP30,SP30
          PACK      KEY6,PTHSMRPR,SP70
          CALL      RDPRTA1 
.
          PRINT     "|",*3,PTHSHOSP,*8,PTHSNAME,*59,"|",*61,PTHSADD1,*97,"|":
                    *100,"Tel : ",PTHSTELE,*132,"|"
          PRINT     "|",*59,"|",*61,PTHSADD2,*97,"|",*132,"|"
          PRINT     "|",*3,"Unit",*18,":",*21,PTHSUNIT,*59,"|":
                    *61,PTHSADD3,*97,"|",*100,"Fax : ",PTHSFAXN,*132,"|"
          PRINT     "|",*3,"M.R. Printer",*18,":",*21,PTHSMRPR,*31,PRTDESC:
                    *59,"|",*61,PTHSADD4,*97,"|",*132,"|"
          PRINT     "|",*3,"Hlth Dept Code",*18,":",*21,PTHSHDPC:
                    *35,"Hospital No.",*48,":",*50,PTHSNUMB:
                    *59,"|",*88,PTHSPCOD,*97,"| Posting Acc : ",PTHSPACT:
                    *132,"|"
          
          CALL      UND132
          ADD       FIVE,CLNO
PRNT3000
          COMPARE   "55",CLNO
          GOTO      PRNT1000 IF LESS 
.
          CALL      HEAD0000
          GOTO      PRNT1000
.
PRNT8000
          PRINT     " ",*N:
                    "***** End of Report *****"
          DISPLAY   *P1:24,*EL
.
PRNT9999  RETURN
.
.*********************************************************************
.*                         HEAD0000                                  *
.*                   Print the required heading                      *
.*********************************************************************
.*
HEAD0000
          CALL      IBACLOCK
          CALL      HEAD132 
          PRINT     " "
          MATCH     SP3,STRTCODE
          IF        @EQUAL
            PRINT     *40,"From Hospital  ID  : Start"
          ELSE 
            PRINT     *40,"From Hospital  ID  : ",STRTCODE,SP2,STRTDESC
          ENDIF
          MATCH     "zzz",ENDCODE
          IF        @EQUAL
            PRINT     *40,"               To  : Finish",*N
          ELSE 
            PRINT     *40,"               To  : ",ENDCODE,SP2,ENDDESC,*N
          ENDIF
.
          CALL      UND132 
          PRINT     "|",*3,"Hospital",*59,"|",*97,"|",*132,"|"
          PRINT     "|",*3,"ID",*8,"Name ",*59,"| Hospital Address":
                    *97,"| Phone Details ",*132,"|"
          CALL      UND132 
HEAD9999 
          ADD       FIVE,CLNO
          RETURN
.
.*********************************************************************
.*                 I/O and other includes required                   *
.*********************************************************************
.
          INC       STD001IO
          INC       PATHSPIO/INC
          INC       IBAPRTIO/INC
          INC       PATHSPKY

.----------------------------------------------------------------------
. Program       : PATWEB14
.
. Function      : eAdmission Web server functions
.
. Modifications :
.----------------------------------------------------------------------
. V12.00.02 12/11/2025  Ebon Clements  TSK 0960938
.           Delete correspondence audit when unlinking UR/Visit - UNDC3000
. V12.00.01 19/05/2025  Ebon Clements    TSK 0960463
.           Recompiled for UPEADMVS - eAdmission document update by
. V11.03.02 13/09/2023  Ebon Clements    TSK 0936987
.           Added clear of theatre booking details (oprdetaf) if no theatre
.           booking exists for a booking link - BKRW1000
. V11.03.01 08/06/2023  Ebon Clements    TSK 0933621
.           Added theatre booking hospital id to linkVisit() - BKRW7000
. V11.02.01 10/02/2022  Thanh T          TSK 0889899
.           Recompiled as WATTR1FD changes
. V11.01.03 12/08/2021  Ebon Clements   Task 0907591
.           Fixed medical booking expected ward display - BKRW1000
. V11.01.02 05/08/2021  Ebon Clements   Task 0907591
.           Added ward code output - BOOK0000, PREA0000, UNSW0000 & CURA0000
. V11.01.01 15/07/2021  Ebon Clements   Task 0908003
.           Added clear of preferred names if not linked to a U/R - PMRW0000
. V11.00.08 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V11.00.07 09/09/2020  Ebon Clements TSK 0896752
.           Added filtrdat - Filter eAmissions by registraton received date
. V11.00.06 19/08/2020  Ebon Clements TSK 0892153
.           Added dummy blank output field 23 for document link view - PMRW0000
. V11.00.05 03/08/2020  Don Nguyen    TSK 0891206
.           Fixed logic for renaming eAdmission documents when unlinking a visit
. V11.00.04 27/07/2020 Ebon Clements  TSK 0892153
.           Added hyper link document functionality to eAdmission list
.           PMRW1150
. V11.00.03 04/06/2020 Don Nguyen     TSK 0881876
.           Recompiled for PMSEDBXD - added 'VerifiedAddress'
.           Removed 'Street Number' from list row output (PMRW0000) since it's
.           already part of the 'Street Name' data loaded (eadmnxml.us1).
. V11.00.02 18/05/2020  Ebon Clements TSK 0875124
.           Added W/L case keys to linkVisitWaitList - UNSR0000
. V11.00.01 27/02/2020  Ebon Clements TSK 0875124
.           Added link to current admission list tag - CURA0000
. V10.15.04 28/01/2020  Ebon Clements TSK 0875124
.           Added unscheduled waiting list by U/R display - UNSW0000
. V10.15.03 23/02/2020  Ebon Clements TSK 0875124
.           Added registration recived date option to the
.           eAdmission list - Index 7 PMSEHBFD - GETEAD00
. V10.15.02 22/02/2020  Ebon Clements TSK 0875124
.           Added pmilevel cgi for patient level eAdmission search
.           filter default
. V10.15.01 21/01/2020  Ebon Clements TSK 0875124
.           Added ADFR0013 Claim Code to eAdmission list - GETEAD00
. V10.13.03 14/01/2019  Richa Phogat  TSK 0868729
.           Added EAHD0090
. V10.13.02 14/11/2018  Tracey Nguyen            TSK 0865219
.           Replaced double quotes(") with single quote (') for DIM40
.           to fix javascript error - GTPROP00
. V10.13.01 13/11/2018  Tracey Nguyen            TSK 0865219
.           Replaced double quotes(") with single quote (') for street name
.           to fix javascript error - PMRW1070
.----------------------------------------------------------------------
. V10.12.03 21/05/2018  Ebon Clements TSK 0853079
.           Added ADFR0029 Admission Reason to eAdmission list - GETEAD00
. V10.12.02 17/04/2018  B.G.Ackland   TSK 0855571
.           Fix link to openDocument
. V10.12.01 31/01/2018  Ebon Clements TSK 0845295
.           Added eReferral clinical document functionality
. V10.11.08 18/12/2017  Peter Vela    TSK 0850270
.           Recompiled for PMSEDBXD
. V10.11.07 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.06 21/09/2017  Don Nguyen    TSK 0840948
.           Modified DOCD0000 and DOCS0000 to encapsulate 'docid' values
.           properly in double-quotes.
.           Increased size of DOCREF to 200 and trimmed the HTML output of it.
. V10.11.05 18/08/2017  Don Nguyen    TSK 0840948
.           Replaced FLNAME50 with FILEPATH to cater for a long file path
. V10.11.04 11/08/2017  Don Nguyen    TSK 0840948
.           Added remote script routine PRTDCS00 to print eAdmission docs.
.           Modified PRNS0000 to use Patient Correspondence Visit Number
.           (OBPCCVIS) as part of the filename instead of the UR;
.           eAdmission clinical documents are stored by admission number.
.           Fixed TMPPRNT (Printer ID) to be size 6 instead of 3.
. V10.11.03 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.02 26/07/2017  Thanh T.       TSK 0821710
.           Recompiled PASMISTM
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
.----------------------------------------------------------------------
. V10.10.02 10/05/2017 Richa Phogat   TSK 0302261
.           Update status when a visit is linked with an eadmission
. V10.10.01 10/05/2017 Richa Phogat   TSK 0302261
.           Added functionality to link Visit to a UR and link eAdmission 
.           documents (if exists) to the visit 
. V10.08.01 08/09/2016 Jill Parkinson TSK 0808513 
.           Added packing of PTMASNAM for eadmission list so correct surname  
. V10.06.02 14/07/2015 Nicole Torrisi CAR 302959
.           Removed check on History Received when determining Priority Colour
. V10.06.01 10/05/2015 Jill Parkinson CAR 316167
.           Added output of THCSCOD in BKRW0000 and check for non episoft
.           Added output of ADMISNID
. V10.05.01 16/12/2014 Jill Parkinson CAR 309775      
.           Mods to show all bookings and preadmissions in BOOK000 and PREA0000
. V10.04.08 08/05/2014 Patrick Adair        Car 298587
.           reset histdate
. V10.04.07 22/04/2014 Patrick Adair        Car 298587
.           only store history date for type 999 documents and amend document
.           link criteria
. V10.04.06 16/04/2014  Ebon Clements       CAR 299834
.           Fixed hospital filter cgi test -  HOSPFLAG
. V10.04.05 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.04 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
. V10.04.03 03/03/2014  Jill Parkinson      CAR 295333
.           Reinstated alert of "Sent to Printer", removed NXTPAG00 instead
. V10.04.02 18/02/2014  Jill Parkinson      CAR 295333
.           Removed alert of "Sent to Printer" to stop internal server errors
. V10.04.01 06/02/2014  Patrick Adair       CAR 293445
.           amended to allow addition of documents to processed patient
.                                           CAR 295558
.           output dummy field to prevent tablesort js errors
.                                           CAR 293780
.           amended to set clinical documents icon flag
.----------------------------------------------------------------------
. V10.03.19 02/12/2013  Patrick Adair       CAR 293773
.           Corrected processing for EOF in GETEAD00
. V10.03.18 02/10/2013  Ania P              CAR 292100
.           Increased size of NUMDAYS to stop truncation if admission
.           is more than 100 days in future
. V10.03.17 13/08/2013  Jill Parkinson      CAR 289926
.           Changed PRRW0000 to use index 3 instead of index 6
.           Changed PMRW0000 to use patma1af fields if linked to a UR Number
. V10.03.16 30/07/2013  Patrick Adair       CAR 289389
.           Recoded document workflow to be visit based not UR based
. V10.03.15 24/07/2013  Patrick Adair       CAR 289206
.           Removed Mental Health Admission routines - not part of eAdmissions
. V10.03.14 15/07/2013  Patrick Adair       CAR 288635
.           Corrected table htmllink to allow theater bookings to function
. V10.03.13 12/07/2013  Patrick Adair       CAR 286872
.           Added processing to not display unlinked visit numbers
. V10.03.12 20/06/2013  Patrick Adair       CAR 286557
.           Added processing to move eAdmission documents on new registration
. V10.03.11 18.06.2013  Patrick Adair       CAR 286958
.           Added missing visit to preadmission/booking list
. V10.03.10 14.06.2013  Patrick Adair       CAR 286555
.           Amended BKTYPE from 2 to 3 characters
. V10.03.09 20.05.2013  Patrick Adair       CAR 285158
.           Changes for New Requirements as per Spec
. V10.03.08 03.05.2013  Saroeun Soeur       CAR 280425
.           fixed printing PRNS0000
. V10.03.07 03.05.2013  Saroeun Soeur       CAR 280425
.           Recompiled for PMSEDBXD - GPNotify
. V10.03.06 01.05.2013  Saroeun Soeur       CAR 280425
.           add ability to add history
. V10.03.05 30.04.2013  Saroeun Soeur       CAR 280425
.           added webserver HSTD0000
. V10.03.04 30.04.2013  Saroeun Soeur       CAR 280425
.           remove DOCS0000 input date
. V10.03.03 22.04.2013  Saroeun Soeur       CAR 280425
.           Added reg received date to staging screen PMRW0000
. V10.03.02 18.04.2013  Saroeun Soeur       CAR 280425
.           Add document history
. V10.03.01 05.04.2013  B.G.Ackland         CAR 280425
.           Change Tables to use our Unique ID for Relationship
. V10.03.00 28/03/2013  Saroeun Soeur       CAR 280425
.           Created eAdmission Web Server
.
.----------------------------------------------------------------------
          INC       IBAWEBDF
          INC       WEBDATDF
.
          INC       COMERHFD/INC
          INC       PATDO1FD/INC   * Doctor File
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       OBSAUDFD/INC
          INC       OUTSITFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       MLTSECFD/INC
          INC       PMSEHBFD/INC   * eAdmission Patient Header
          INC       PMSEDBXD/INC   * eAdmission XML Field Names
          INC       PMSEDBFD/INC   * eAdmission Patient Details
          INC       BOKRC1FD/INC
          INC       OBSECOFD/INC   * eAdmission Patient Documents
          INC       OBSPCOFD/INC
          INC       OBSPCTFD/INC
          INC       OPRDEAFD/INC
          INC       PATMISTD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
.
.----------------------------------------------------------------------
.
ADDRESS   DIM       80
ADMINDTE  DIM       8
ADMISNID  DIM       20
ADMITVMO  DIM       50
ADMREASN  DIM       50
ADOCNAME  DIM       50
ARRCOUNT  FORM      2
.
BKDESC    DIM       20
BKTYPE    DIM       3
BOOKICON  DIM       50                           * Display Booking Icon in Table
BOOKLINK  DIM       100                          * Url to Booking Template
.
CALCDAT1  DATE      8
CALCDAT2  DATE      8
CLMDESC   DIM       20
CMDLINE   DIM       200
COUNT     FORM      3
CURRDATE  DIM       8       * Current Date
CORSP001  DIM       100
CORSP003  DIM       3
.
DEFTVIEW  FORM      1
DIM10     DIM       10
DIM11     DIM       11
DIM20     DIM       20
DIM3      DIM       3
DIM4      DIM       4
DIM4A     DIM       4
DIM40     DIM       40
DIM40A    DIM       40
DIM40B    DIM       40
DIM8      DIM       8
DISPCOMP  DIM       20
DISPEAID  DIM       20
DISPECCD  DIM       8
DISPPCCD  DIM       8
DISPPROB  DIM       80
DISPPRTY  DIM       20
DISPVISN  DIM       8
DISPSTAT  DIM       20
DOCCOUNT  FORM      2
DOCEXIST  FORM      1
DOCMNTID  DIM       20[99]
DOCREF    DIM       200
DOCTCODE  DIM       8
.
EREFRLID  DIM       20
.
FILTFLAG  DIM       1
FILTRDAT  FORM      1
FILTHOSP  DIM       1
FILTSNAM  DIM       40
FIRSTFLG  DIM       1
FLDRLINK  DIM       100
FLNAME50  DIM       50
FILEPATH  DIM       150
FORM3     FORM      3
FORM8     FORM      8
FULLNAME  DIM       80
.
HISDATE   DIM       11
HISTODDT  DIM       8
HISTODTY  DIM       3
HOSPFLAG  DIM       3
HYPERDOC  FORM      1
HYPERURL  DIM       650
.
INDEXNO7  FORM      1
.
LINKDOC   DIM       100
.
MBSDESC   DIM       70
.
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
NUMDAYS   FORM      3
NWFILENM  DIM       100
.
OLDFILEN  DIM       100
ONLINEID  DIM       20
.
PMILEVEL  DIM       1
POSTCODE  DIM       6
PRINTRID  DIM       20[99]
PRIOFLAG  DIM       20
PRIORCLR  DIM       20
PRIORSRT  DIM       1
PRNCOUNT  FORM      2
PSAGECON  DIM       3
PSAGETYP  DIM       3
.
RSTRVISN  DIM       8
.
SESSKEYS  DIM       36
SHOWFLAG  FORM      1
STARTKEY  DIM       10      * Starting Key
STATCODE  DIM       1
STATDSC   DIM       20
STATUS    DIM       8
STORURNO  DIM       8
STORVISN  DIM       8
STREETNM  DIM       20
STREETNO  DIM       6
SUBURB    DIM       20
.
TMPCOUNT  FORM      2
TMPCRSID  DIM       4        * Correspondance ID
TMPPRNT   DIM       6        * Printer ID
TMPURNO   DIM       8        * UR
TMPVISNO  DIM       8        * Visit Number
TODAY     DIM       8
.
UNITDESC  DIM       20
UPDTTYPE  DIM       1       * Update Type
.
VARCOUNT  FORM      2
VARIABLE  DIM       8
.
WEBSRVNO  DIM       3
YEARFDAT  DIM       8  
YEARLDAT  DIM       8  
.
.
. Remote script print variables
PRNTDTYP  DIM       1        * Document Type
.                                1 - Past Medical History
.                                2 - Consent Form
.                                3 - Supporting Documents
PRNTURNO  DIM       8        * UR Number
PRNTVISN  DIM       8        * Visit Number
PRNTPRTC  DIM       6        * Printer Code
PRNTCPYS  FORM      2        * Number of Copies
.
.---------------------------------- CONSTANTS ----------------------------------
.
ANSADFR   INIT      "ADFR"
ADFRADMR  INIT      "ADFR0029"
.
CATD1     INIT      "D1"
CATD2     INIT      "D2"
CATD3     INIT      "D3"
CATD4     INIT      "D4"
CATD5     INIT      "D5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
CATBK     INIT      "BK"
CATCL     INIT      "CL"
CATWI     INIT      "wi"
.
CNVQUOTE  INIT      "#"'"                                      *T0865219
.
DASH      INIT      "-"
.
GREEN     INIT      "Green"
GREY      INIT      "Grey"
RED       INIT      "Red"
YELLOW    INIT      "Yellow"
.
STATUS1   INIT      "Pending"
STATUS2   INIT      "Processed"
STATUS3   INIT      "Processing"
STATUS4   INIT      "Process Cancelled"
STATUS5   INIT      "Ignore"
STATUS6   INIT      "Cancelled"
STATUS7   INIT      "Booked"
.
TOLOCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
.----------------------------------------------------------------------
PRGID     INIT      "PATWEB14"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "eAdmission Web Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN9000,MAIN9000,MAIN1300,MAIN1400,MAIN1400:
                             MAIN1600,MAIN1700
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1300  MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          CALL      RDPMPX21
          BRANCH    EXIT,MAIN1000
.
          MATCH     "P",UPDTTYPE
          IF        @EQUAL
            CALL      PRNS0000
            PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
            MOVE      "Sent to Printer",WEBTITLE
            CALL      WEBERR00   * Create Output File and Write HTML Page Header
.           CALL      NXTPAG00
            GOTO      MAIN1000
          ENDIF
.
          PACK      KEY20,ADMISNID
          CALL      RDPMEHB1
          IF        OVRCD=1
            CALL      CLREHB00
          ENDIF
.
          PACK      KEY20,EREFRLID,SP70
          CALL      RDCMERH1                * Read eReferral header
          IF        OVRCD=1
            CALL      CLCOMERH
          ENDIF
.
          CALL      HEADER00
.
          GOTO      MAIN1000
.
MAIN1400  CLEAR     WEBTITLE
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "eAdmission - Online Pre-Admission List",WEBTITLE
.
          CALL      PMIL0000                * PMI level search defaults
.
          CALL      HEADER00
          GOTO      MAIN1000
.
MAIN1600  CLEAR     WEBTITLE
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "eAdmission - Online Pre-Admission List",WEBTITLE
          PACK      KEY20,ADMISNID,SP70
          CALL      RDPMEHB1
          IF        OVRCD<>1
            CALL      HEADER00
          ELSE
            GOTO      MAIN9000
          ENDIF
.
          GOTO      MAIN1000
.
MAIN1700  CALL      WEBSR000   * remote server for eAdmission
          GOTO      MAIN1000
.
MAIN9000  PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
+
.------------------------------------------------------------
.         WEBSR000 - webservice for eAdmission
.------------------------------------------------------------
WEBSR000  MOVE      ZERO,EXIT
          CALL      XMLHED00
          BRANCH    EXIT,WEBSR999
.
          MOVE      WEBSRVNO,FORM3
          BRANCH    FORM3,WEBSR010,WEBSR020,WEBSR030,WEBSR040,WEBSR050:
                          WEBSR060,WEBSR070
.
          GOTO      WEBSR900
.
WEBSR010  CALL      GTPROP00  * get eAdmission properties
          GOTO      WEBSR900
.
WEBSR020  CALL      SETS0000  * set eAdmission status
          GOTO      WEBSR900
.
WEBSR030  CALL      PRTDCS00  * print eAdmission documents for the visit
          GOTO      WEBSR900
.
WEBSR040  CALL      HSDT0000  * get history date
          GOTO      WEBSR900
.
WEBSR050  CALL      ULNKU000  * Unlink URNO
          GOTO      WEBSR900
.
WEBSR060  CALL      ULNKV000  * Unlink Visit
          GOTO      WEBSR900
.
WEBSR070  CALL      VISIT000  * Link Visit to UR
          GOTO      WEBSR900
.
WEBSR900  CALL      XMLEND00
.
WEBSR999  RETURN
+
.------------------------------------------------------------
.
.------------------------------------------------------------
HSDT0000  REP       "+ ",ADMISNID
          PACK      KEY20,ADMISNID,SP70
          CALL      RDPMEHB1
          BRANCH    OVRCD,SETS9000
.
          MATCH     PMHBUNIQ,ADMISNID
          GOTO      SETS9000 IF NOT EQUAL
.
          MOVE      HISTODTY,PMHBLDTY
.
          MATCH   "999",PMHBLDTY
          IF      @EQUAL
            MOVE      HISTODDT,PMHBLDDT
          ENDIF
.
          CALL      UPPMEHB1
.
HSDT9000  WRITE     HTMLFILE;"finished";
.
HSDT9999  RETURN
+
.------------------------------------------------------------
.         document print
.------------------------------------------------------------
PRNS0000  MOVE      ONE,TMPCOUNT
.
PRNS1000  IF        TMPCOUNT <= DOCCOUNT
            UNPACK    DOCMNTID[TMPCOUNT],TMPURNO,TMPVISNO,TMPCRSID
            UNPACK    PRINTRID[TMPCOUNT],TMPPRNT
.
            REP       "+ ",TMPURNO
            REP       "+ ",TMPVISNO
            REP       "+ ",TMPCRSID
.
            PACK      KEY20,TMPURNO,TMPVISNO,TMPCRSID,SP70
            CALL      RDOBPC1
            BRANCH    OVRCD,PRNS2000
.
            MATCH     TMPURNO,OBPCURNO
            GOTO      PRNS2000 IF NOT EQUAL
.
            MATCH     TMPCRSID,OBPCCPID
            GOTO      PRNS2000 IF NOT EQUAL
.
            PACK      KEY4,OBPCSLID,SP70
            CALL      RDOBPT1
            BRANCH    OVRCD,PRNS2000
.
            STRIP     OBPTSDIR
            REP       " 0",OBPCURNO
            STRIP     OBPCURNO
            REP       " 0",OBPCCPID
            STRIP     OBPCCPID
            STRIP     OBPCFEXT
            STRIP     OBPCCVIS
            REP       " 0",OBPCCVIS
.
            CLEAR     FILEPATH
            RESET     FILEPATH
            PACK      FILEPATH,OBPTSDIR,OBPCCVIS,DASH,OBPCCPID,OBPCFEXT
            STRIP     FILEPATH
.
            CLEAR     CMDLINE
            APPEND    "eadhsprt.us1 ",CMDLINE
            APPEND    FILEPATH,CMDLINE           * filename
            APPEND    " ",CMDLINE
            APPEND    TMPPRNT,CMDLINE            * printer
            RESET     CMDLINE
            EXECUTE   CMDLINE,TASKID
.
PRNS2000    ADD     ONE,TMPCOUNT
.
            GOTO    PRNS1000
          ENDIF
.
PRNS9999  RETURN
+
.------------------------------------------------------------
.         call us1 script to move file to new directory
.         will new file name
.------------------------------------------------------------
MVDC0000  CLEAR     CMDLINE
          APPEND    "eadhspdf.us1 ",CMDLINE
          APPEND    OLDFILEN,CMDLINE
          APPEND    ".pdf",CMDLINE             * old filename
          APPEND    " ",CMDLINE
          APPEND    NWFILENM,CMDLINE           * new filename
          APPEND    ".pdf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MATCH     "0       ",TASKID
MVDC9999  RETURN
+
.------------------------------------------------------------
.
.------------------------------------------------------------
SETS0000  REP       "+ ",ADMISNID
          PACK      KEY20,ADMISNID,SP70
          CALL      RDPMEHB1
          BRANCH    OVRCD,SETS9000
.
          MATCH     PMHBUNIQ,ADMISNID
          GOTO      SETS9000 IF NOT EQUAL
.
          MOVE      STATCODE,PMHBSTAT
.
          MATCH     URNUMBER,SP70
          GOTO      SETS0100 IF EQUAL
.
          MATCH     "UNK",URNUMBER
          IF        @EQUAL
            MOVE      SP70,PMHBURNO
            GOTO      SETS0100
          ENDIF
.
          MATCH     URNUMBER,PMHBURNO
          GOTO      SETS0100 IF EQUAL
.
          MOVE      URNUMBER,PMHBURNO
.
SETS0100  MATCH     ADMISSNO,SP70
          IF        !@EQUAL
            MATCH     "UNK",URNUMBER
            IF        @EQUAL
              MOVE      SP70,PMHBVISN
            ELSE
              MOVE      ADMISSNO,PMHBVISN
            ENDIF
          ENDIF
.
          CALL      UPPMEHB1
.
          WRITE     HTMLFILE;"true";
.
          GOTO      SETS9999
.
SETS9000  WRITE     HTMLFILE;"false";
.
SETS9999  RETURN
+
.-------------------------------------------------------------------------------
. Clear URNO and Visit Number on pmsehbaf and reset documents to eadmisnid
.-------------------------------------------------------------------------------
ULNKU000  REP       "+ ",ADMISNID
          PACK      KEY20,ADMISNID,SP70
          CALL      RDPMEHB1
          BRANCH    OVRCD,ULNKU900
.
          MOVE      ONE,PMHBSTAT
          PACK      STORURNO,PMHBURNO,SP70
          PACK      STORVISN,PMHBVISN,SP70
          PACK      PMHBURNO,SP70
          PACK      PMHBVISN,SP70
.
          CALL      UPPMEHB1
.
          CALL      UNDC0000
.
          PACK      KEY20,STORURNO,SP70
          CALL      RSOBPC1
          CALL      RKOBPC1
          BRANCH    OVRCD,ULNKU800
.
          MATCH     OBPCURNO,STORURNO
          GOTO      ULNKU800 IF NOT EQUAL
.
          GOTO      ULNKU850
.
ULNKU800  PACK      KEY8,STORURNO           * validate UR
          CALL      RDMAST1
          BRANCH    OVRCD,ULNKU850
.
          MATCH     "1",PTMXSIN4
          IF        @EQUAL
            PACK      KEY8,STORURNO         * validate UR
            CALL      RLPTMAS1
            BRANCH    OVRCD,ULNKU850,ULNKU850
            MOVE      "0",PTMXSIN4
            CALL      UPMAST1
            CALL      UUPTMAS1
          ENDIF
.
ULNKU850  WRITE     HTMLFILE;"true";
.
          GOTO      ULNKU999
.
ULNKU900  WRITE     HTMLFILE;"false";
.
ULNKU999  RETURN
+
.------------------------------------------------------------
. Clear Visit Number on pmsehbaf
.------------------------------------------------------------
ULNKV000  REP       "+ ",ADMISNID
          PACK      KEY20,ADMISNID,SP70
          CALL      RDPMEHB1
          BRANCH    OVRCD,ULNKV900
.
          MOVE      THREE,PMHBSTAT
          PACK      STORURNO,PMHBURNO,SP70
          PACK      STORVISN,PMHBVISN,SP70
          PACK      PMHBVISN,SP70
.
          CALL      UPPMEHB1
.
          CALL      UNDC0000
.
          PACK      KEY20,STORURNO,SP70
          CALL      RSOBPC1
          CALL      RKOBPC1
          BRANCH    OVRCD,ULNKV800
.
          MATCH     OBPCURNO,STORURNO
          GOTO      ULNKV800 IF NOT EQUAL
.
          GOTO      ULNKV850
.
ULNKV800  PACK      KEY8,STORURNO           * validate UR
          CALL      RDMAST1
          BRANCH    OVRCD,ULNKV850
.
          MATCH     "1",PTMXSIN4
          IF        @EQUAL
            PACK      KEY8,STORURNO         * validate UR
            CALL      RLPTMAS1
            BRANCH    OVRCD,ULNKV850,ULNKV850
            MOVE      "0",PTMXSIN4
            CALL      UPMAST1
            CALL      UUPTMAS1
          ENDIF
.
ULNKV850  WRITE     HTMLFILE;"true";
.
          GOTO      ULNKV999
.
ULNKV900  WRITE     HTMLFILE;"false";
.
ULNKV999  RETURN
+
.------------------------------------------------------------
. Update Visit Number on pmsehbaf.pmhbvisn
.------------------------------------------------------------
VISIT000  PACK      STORURNO,URNUMBER,SP70
          PACK      STORVISN,ADMISSNO,SP70
          REP       "+ ",ADMISNID
          PACK      KEY20,ADMISNID,SP70
          CALL      RDPMEHB1
          BRANCH    OVRCD,VISIT900
.
          MATCH     PMHBURNO,STORURNO
          GOTO      VISIT910 IF NOT EQUAL
.
          MOVE      ZERO,F1
          MOVE      TWO,PMHBSTAT
          MOVE      PMHBSTAT,KEY1
          MOVE      ADMISSNO,KEY8
          PROC      UPEADMVS               * Update eAdmission Details
.
VISIT900  WRITE     HTMLFILE;"false";
          GOTO      VISIT999
.
VISIT910  DISPLAY   "*** UR Number are not same ***"
.
VISIT999  RETURN
+
.------------------------------------------------------------------------------
. Print all the corresponding eAdmission documents for the patient visit
.------------------------------------------------------------------------------
PRTDCS00  REP       "+ ",PRNTURNO
          REP       "+ ",PRNTVISN
.
          MOVE      SP1,DIM1
          MATCH     "1",PRNTDTYP
          IF        @EQUAL
            MOVE      "M",DIM1         * Past Medical History
          ENDIF
.
          MATCH     "2",PRNTDTYP
          IF        @EQUAL
            MOVE      "C",DIM1         * Consent Form
          ENDIF
.
          MATCH     "3",PRNTDTYP
          IF        @EQUAL
            MOVE      "S",DIM1         * Supporting Documents
          ENDIF
.
          PACK      KEY20,PRNTURNO,PRNTVISN,SP70
          CALL      RSOBPC1
PRTDCS10  CALL      RKOBPC1
          BRANCH    OVRCD,PRTDCS99
.
          MATCH     OBPCURNO,PRNTURNO
          GOTO      PRTDCS99 IF NOT EQUAL
.
          MATCH     OBPCCVIS,PRNTVISN
          GOTO      PRTDCS99 IF NOT EQUAL
.
          PACK      KEY5,CATwi,OBPCCTYP,SP10
          CALL      RDCODE1
          BRANCH    OVRCD,PRTDCS10
.
          MATCH     TCINDC2,DIM1            * matching document type?
          GOTO      PRTDCS10 IF NOT EQUAL
.
          PACK      KEY4,OBPCSLID,SP70
          CALL      RDOBPT1
          BRANCH    OVRCD,PRTDCS10
.
          STRIP     OBPTSDIR
          REP       " 0",OBPCURNO
          STRIP     OBPCURNO
          REP       " 0",OBPCCPID
          STRIP     OBPCCPID
          STRIP     OBPCFEXT
.
          STRIP     OBPCCVIS
          REP       " 0",OBPCCVIS
.
          CLEAR     FILEPATH
          RESET     FILEPATH
          PACK      FILEPATH,OBPTSDIR,OBPCCVIS,DASH,OBPCCPID,OBPCFEXT
          STRIP     FILEPATH
.
          CLEAR     CMDLINE
          APPEND    "eadhsprt.us1 ",CMDLINE
          APPEND    FILEPATH,CMDLINE             * filename
          APPEND    " ",CMDLINE
          APPEND    PRNTPRTC,CMDLINE             * printer code
          APPEND    " ",CMDLINE
          APPEND    PRNTCPYS,CMDLINE             * copies
.
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MATCH     "0",TASKID
          GOTO      PRTDCS99 IF NOT EQUAL
.
          GOTO      PRTDCS10
.
PRTDCS99  RETURN
+
.------------------------------------------------------------------------------
. Rename eAdmission Documents from ADMISNID to URNO, copy details to OBSPCOAF
. and update URNO on OBSECOAF
.------------------------------------------------------------------------------
UNDC0000  PACK      KEY24,PMHBEAID,SP70
          CALL      RSOBECO1
UNDC1000  CALL      RKOBECO1
          BRANCH    OVRCD,UNDC9999
.
          MATCH     PMHBEAID,OBECEAID
          GOTO      UNDC9999 IF NOT EQUAL
.
          MATCH     OBECURNO,STORURNO
          GOTO      UNDC1000 IF NOT EQUAL
.
          PACK      KEY20,STORURNO,STORVISN,SP70
          CALL      RSOBPC1
UNDC2000  CALL      RKOBPC1
          BRANCH    OVRCD,UNDC1000
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
          PACK      TODAY,DIM8,SP70
          CLOCK     TIME,CTIMEIS
.
          MATCH     OBECURNO,OBPCURNO
          GOTO      UNDC1000 IF NOT EQUAL
.
          MATCH     OBECEAID,OBPCEAID
          GOTO      UNDC2000 IF NOT EQUAL
.
          PACK      KEY20,OBPCURNO,OBPCCVIS,OBPCCPID
          CALL      DEOBPC1
.
. Delete audit records
.
UNDC3000  PACK      KEY47,OBPCURNO,OBPCCVIS,OBPCCPID,SP70
          CALL      RSOBAUD1
          CALL      RKOBAUD1
          BRANCH    OVRCD,UNDC4000
.
          MATCH     OBPCURNO,OBAUURNO            * U/R
          GOTO      UNDC4000 IF NOT EQUAL
.
          MATCH     OBPCCVIS,OBAUVISN            * Visit
          GOTO      UNDC4000 IF NOT EQUAL
.
          MATCH     OBPCCPID,OBAUCORR            * Correspondence id
          GOTO      UNDC4000 IF NOT EQUAL
.
          PACK      KEY47,OBAUURNO,OBAUVISN,OBAUCORR,OBAUDATE,OBAUTIME:
                          OBAUWEBU,OBAUTYPE,SP70
          CALL      DEOBAUD1
.
          GOTO      UNDC3000
.
UNDC4000  PACK      KEY24,PMHBEAID,OBECCPID,SP70
          MOVE      SP70,OBECURNO
          CALL      UPOBECO1
.
          MOVE      STORVISN,RSTRVISN
          MOVE      PMHBEAID,DISPEAID
          MOVE      OBPCCPID,DISPPCCD
          MOVE      OBECCPID,DISPECCD
          REP       " 0",RSTRVISN
          REP       " 0",DISPEAID
          REP       " 0",DISPPCCD
          REP       " 0",DISPECCD
.
          PACK      KEY4,OBECSLID,SP70
          CALL      RDOBPT1
          STRIP     OBPTSDIR
.
          PACK      NWFILENM,OBPTSDIR,DISPEAID,DASH,DISPECCD,SP70
          PACK      OLDFILEN,OBPTSDIR,RSTRVISN,DASH,DISPPCCD,SP70
          STRIP     OLDFILEN
          STRIP     NWFILENM
          CALL      MVDC0000
.
          GOTO      UNDC2000
.
UNDC9999  RETURN
+
.------------------------------------------------------------
.         GTPROP00 - get eAdmission Properties
.------------------------------------------------------------
GTPROP00  WRITE     HTMLFILE;"[";
          REP       "+ ",ADMISNID
          PACK      KEY20,ADMISNID,SP70
          CALL      RDPMEHB1
          IF        OVRCD<>1
            MOVE      ANSY,FIRSTFLG
            PACK      KEY28,PMHBUNIQ,SP70
            CALL      RAPMEDB1
GTPROP10    CALL      RKPMEDB1
            BRANCH    OVRCD,GTPROP90
.
            MATCH     PMHBUNIQ,PMDBUNIQ
            GOTO      GTPROP90 IF NOT EQUAL
.
            MATCH     SP70,PMDBWBID
            GOTO      GTPROP10 IF EQUAL
.
            MOVE      PMDBVALU,DIM40
            REP       CNVQUOTE,DIM40             *Converts " to '  *T0865219
            STRIP     DIM40
            REP       TOLOCASE,PMDBWBID
            MATCH     ANSY,FIRSTFLAG
            IF        @EQUAL
              MOVE      ANSN,FIRSTFLG
              WRITE     HTMLFILE;"{#"name#":#"",PMDBWBID,"#",#"value#":#"",DIM40,"#"}";
            ELSE
              WRITE     HTMLFILE;",{#"name#":#"",PMDBWBID,"#",#"value#":#"",DIM40,"#"}";
            ENDIF
            GOTO      GTPROP10
.
          ENDIF
.
GTPROP90  WRITE     HTMLFILE;"]";
.
GTPROP99  RETURN
+
.------------------------------------------------------------
.
.------------------------------------------------------------
HEADER00  CALL      CURPER00
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,HEADER99
.
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
HEADER99  RETURN
+
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
+
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
+
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM
          OPEN      COMERHA1,"comerhaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      MLTSECA1,"mltsecaf"
.
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSEHBA1,"pmsehbaf"
          OPEN      PMSEHBA2,"pmsehbaf"
          OPEN      PMSEHBA3,"pmsehbaf"
          OPEN      PMSEHBA6,"pmsehbaf"
          OPEN      PMSEDBA1,"pmsedbaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PATMI1A8,"patmi1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          OPEN      OBSAUDA1,"obsaudaf"
          OPEN      OBSECOA1,"obsecoaf"
          OPEN      OBSPCOA1,"obspcoaf"
          OPEN      OBSPCTA1,"obspctaf"
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      WATPROA1,"watproaf"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTX1A1,"wattx1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND19;*182,PTCNEPIS
.
INIT9999  RETURN
+
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,VIEWTYPE
          MOVE      SP70,FILTHOSP
          MOVE      SP70,ADMISNID
          MOVE      SP70,WEBSRVNO
          MOVE      SP70,STATCODE
          MOVE      SP70,FILTFLAG
          MOVE      SP70,PRIOFLAG
          MOVE      SP70,HOSPFLAG
          MOVE      SP70,FILTSNAM
          MOVE      ZERO,SHOWFLAG
          CALL      CDOC0000
          CALL      CPRT0000
.
          MOVE      SP70,PRNTDTYP
          MOVE      SP70,PRNTURNO
          MOVE      SP70,PRNTVISN
          MOVE      SP70,PRNTPRTC
          MOVE      ZERO,PRNTCPYS
          MOVE      SP70,EREFRLID
          MOVE      ZERO,DEFTVIEW
          MOVE      SP70,PMILEVEL
          MOVE      ZERO,FILTRDAT
.
CLRPAR99  RETURN
+
.------------------------------------------------------------
.         clear document array
.------------------------------------------------------------
CDOC0000  MOVE      ZERO,DOCCOUNT
          MOVE      ONE,ARRCOUNT
CDOC1000  MOVE      SP70,DOCMNTID[ARRCOUNT]
          ADD       ONE,ARRCOUNT
          IF        ARRCOUNT>98
            GOTO      CDOC9999
          ENDIF
          GOTO      CDOC1000
.
CDOC9999  RETURN
+
.------------------------------------------------------------
.         clear printer array
.------------------------------------------------------------
CPRT0000  MOVE      ZERO,PRNCOUNT
          MOVE      ONE,ARRCOUNT
CPRT1000  MOVE      SP70,PRINTRID[ARRCOUNT]
          ADD       ONE,ARRCOUNT
          IF        ARRCOUNT>98
            GOTO      CPRT9999
          ENDIF
          GOTO      CPRT1000
.
CPRT9999  RETURN
+
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "websrvno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WEBSRVNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "statcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STATCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "histodty=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,HISTODTY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "histoddt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,HISTODDT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filthosp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTHOSP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admisnid=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISNID
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtsnam=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTSNAM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "hospflag=",QRYNAME
          IF        @EQUAL
            PACK      HOSPFLAG,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prioflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PRIOFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "showflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SHOWFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "printrid=",QRYNAME
          IF        @EQUAL
            ADD       ONE,PRNCOUNT
            MOVE      QRYDATA,PRINTRID[PRNCOUNT]
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "docmntid=",QRYNAME
          IF        @EQUAL
            ADD       ONE,DOCCOUNT
            MOVE      QRYDATA,DOCMNTID[DOCCOUNT]
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prntdtyp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PRNTDTYP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prnturno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PRNTURNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prntvisn=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PRNTVISN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prntprtc=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PRNTPRTC
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prntcpys=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PRNTCPYS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "erefrlid=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EREFRLID
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "deftview=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEFTVIEW
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmilevel=",QRYNAME
          IF        @EQUAL
            PACK      PMILEVEL,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtrdat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTRDAT
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------
. Process Fields
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD10,PROFLD10,PROFLD40,PROFLD40:
                             PROFLD60
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13,PROFLD15:
                             PROFLD16,PROFLD17,PROFLD18
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD12  CALL      MISF0000    * Admission File Info
          GOTO      PROFLD99
.
PROFLD13  CALL      DSCF0000    * Discharge File Info
          GOTO      PROFLD99
.
.... note this is NOT called in PROFLD10 above
PROFLD14  CALL      EADM0000    * eAdmission
          GOTO      PROFLD99
.
PROFLD15  CALL      TABL0000
          GOTO      PROFLD99
.
PROFLD16  CALL      EAHD0000
          GOTO      PROFLD99
.
PROFLD17  BRANCH    FLDITMNO,DOCS0000,DOCD0000
          GOTO      PROFLD99
.
PROFLD18  CALL      EAHD0000
          GOTO      PROFLD99
.
PROFLD40  BRANCH    FLDSETNO,PROFLD41
          GOTO      PROFLD99
.
PROFLD41  CALL      PAPR0000
          GOTO      PROFLD99
.
PROFLD60  BRANCH    FLDSETNO,PROFLD11:    * MASF0000 - Master File Info
                             PROFLD12:    * MISF0000 - Admission File Info
                             PROFLD13:    * DSCF0000 - Discharge File Info
                             PROFLD14:    * EADM0000 - eAdmission
                             PROFLD15:    * eAdmission Visit Link Info
                             PROFLD99:
                             PROFLD16
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.-------------------------------------------------------------
.         Document history list
.-------------------------------------------------------------
DOCD0000  MOVE      ZERO,COUNT
          PACK      KEY24,ADMISNID,SP70
          CALL      RSOBECO1
DOCD1000  CALL      RKOBECO1
          BRANCH    OVRCD,DOCD9999
.
          MATCH     OBECEAID,ADMISNID
          GOTO      DOCD9999 IF NOT EQUAL
.
          PACK      KEY5,CATWI,OBECCTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,DOCD1000
.
          MATCH     "M",TCINDC2
          IF        @EQUAL
            GOTO      DOCD2000
          ENDIF
.
          MATCH     "C",TCINDC2
          IF        @EQUAL
            GOTO      DOCD2000
          ENDIF
.
          MATCH     "S",TCINDC2
          IF        @EQUAL
            GOTO      DOCD2000
          ENDIF
.
          GOTO      DOCD1000
.
DOCD2000  ADD       ONE,COUNT
          MOVE      OBECCPID,DIM4
          REP       " 0",DIM4
          PACK      KEY4,OBECSLID,SP70
          CALL      RDOBPT1
.
          STRIP     OBPTURLP
          MOVE      OBECEAID,DIM20
          REP       " 0",DIM20
          PACK      DOCREF,OBPTURLP,DIM20,DASH,DIM4,OBECFEXT
          STRIP     DOCREF
          UNPACK    OBECINDT,CCENT,CYEAR,DIM2,CDAY
          MOVE      DIM2,F2
          REP       " 0",CCENT
          REP       " 0",CYEAR
          REP       " 0",CDAY
.
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          REP       " +",OBECEAID
          REP       " +",OBECCPID
          MOVE      COUNT,DIM3
          REP       " 0",DIM3
.
          WRITE     HTMLFILE;"<tr id=rowid",DIM3,">":
                             "<td><a href='",*+,DOCREF,*-,"' target=_open>":
                             "<img align=absmiddle hspace=4 ":
                             "src=#"../images/ClinicalNote.gif#" /></a>":
                             CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td><td>":
                             TDESC,"<input type=hidden id=docid",DIM3:
                             " value=#"",OBPCURNO,OBPCCVIS,OBPCCPID,"#" /></td>"
.
          REP       "+ ",OBECEAID
          REP       "+ ",OBECCPID
.
          UNPACK    OBECINDT,CCENT,CYEAR,DIM2,CDAY
          MOVE      DIM2,F2
          REP       " 0",CCENT
          REP       " 0",CYEAR
          REP       " 0",CDAY
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          GOTO      DOCD1000
.
DOCD9999  RETURN
+
.-------------------------------------------------------------
.         Document history list
.-------------------------------------------------------------
DOCS0000  MOVE      ZERO,COUNT
          PACK      KEY20,URNUMBER,ADMISSNO,SP70
          CALL      RSOBPC1
DOCS1000  CALL      RKOBPC1
          BRANCH    OVRCD,DOCS9999
.
          MATCH     URNUMBER,OBPCURNO
          GOTO      DOCS9999 IF NOT EQUAL
.
          MATCH     ADMISSNO,OBPCCVIS
          GOTO      DOCS1000 IF NOT EQUAL
.
          PACK      KEY5,CATWI,OBPCCTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,DOCS1000
.
          MATCH     "M",TCINDC2
          IF        @EQUAL
            GOTO      DOCS2000
          ENDIF
.
          MATCH     "C",TCINDC2
          IF        @EQUAL
            GOTO      DOCS2000
          ENDIF
.
          MATCH     "S",TCINDC2
          IF        @EQUAL
            GOTO      DOCS2000
          ENDIF
.
          GOTO      DOCS1000
.
DOCS2000  ADD       ONE,COUNT
          MOVE      OBPCCPID,DIM4
          REP       " 0",DIM4
          PACK      KEY4,OBPCSLID,SP70
          CALL      RDOBPT1
.
          STRIP     OBPTURLP
          MOVE      OBPCCVIS,DIM8
          REP       " 0",DIM8
          PACK      DOCREF,OBPTURLP,DIM8,DASH,DIM4,OBPCFEXT
          STRIP     DOCREF
          UNPACK    OBPCINDT,CCENT,CYEAR,DIM2,CDAY
          MOVE      DIM2,F2
          REP       " 0",CCENT
          REP       " 0",CYEAR
          REP       " 0",CDAY
.
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          REP       " +",OBPCURNO
          REP       " +",OBPCCVIS
          REP       " +",OBPCCPID
          MOVE      COUNT,DIM3
          REP       " 0",DIM3
.
          WRITE     HTMLFILE;"<tr id=rowid",DIM3,">":
                             "<td><a href='",*+,DOCREF,*-,"' target=_open>":
                             "<img align=absmiddle hspace=4 ":
                             "src=#"../images/ClinicalNote.gif#" /></a>":
                             CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td><a href=#"javascript:ViewDoc('cliweb08.pbl?":
                             "reportno=01&template=003&detailky=":
                             OBPCURNO,OBPCCVIS,OBPCCPID,"');#"":
                             "><img align=absmiddle hspace=4":
                           " src=#"../images/historyreceived1.gif#"></a>",TDESC:
                             "<input type=hidden id=docid",DIM3:
                             " value=#"",OBPCURNO,OBPCCVIS,OBPCCPID,"#" /></td>"
.
          REP       "+ ",OBPCURNO
          REP       "+ ",OBPCCVIS
          REP       "+ ",OBPCCPID
.
          PACK      KEY10,OBPCINUS,SP70
          CALL      RDWBSE1
          IF        OVRCD<>1
            WRITE     HTMLFILE;"<td>",WBSENAM,"</td>":
                               "<td><select onchange='changePrinter(this);' ":
                               "id='prtid",DIM3,"'>"
          ELSE
            WRITE     HTMLFILE;"<td>&nbsp;</td>":
                               "<td><select onchange='changePrinter(this);' ":
                               "id='prtid",DIM3,"'>"
          ENDIF
          CALL      SELPRT00
          WRITE     HTMLFILE;"</select></td>"
.
          WRITE     HTMLFILE;"<td><input type='checkbox' ":
                             "onclick=#"if (this.checked) { add(this); }":
                             " else { remove(this); }#" ":
                             "id='prchk",DIM3,"' value=#"0#" /></td></tr>"
.
          GOTO      DOCS1000
.
DOCS9999  RETURN
+
.-------------------------------------------------------------
.
.-------------------------------------------------------------
TABL0000  BRANCH    FLDITMNO,TABL0010,TABL0020,TABL0030,TABL0040
.
          GOTO      TABL9999
.
TABL0010  CALL      BOOK0000    * booking file search
          CALL      PREA0000    * preadmission file search
          GOTO      TABL9999
.
TABL0020  CALL      ADMS0000    * List all current inpatient visits 
          GOTO      TABL9999
.
TABL0030  CALL      UNSW0000    * Unscheduled Waiting list search
          GOTO      TABL9999
.
TABL0040  CALL      CURA0000    * Link to current inpatient visits
          GOTO      TABL9999
.
TABL9999  RETURN;
+
.-------------------------------------------------------------
. Unscheduled Waiting List By U/R
.-------------------------------------------------------------
UNSW0000  MATCH     "1",PTCNEPIS
          GOTO      UNSW9999 IF NOT EQUAL
.
          PACK      KEY19,URNUMBER,SP70
          CALL      RDSTREA1
UNSW1000  CALL      RDKTREA1
          BRANCH    OVRCD,UNSW9999
.
          MATCH     WMURNO,URNUMBER
          GOTO      UNSW9999 IF NOT EQUAL
.
          COMPARE   ONE,WMSTAT
          GOTO      UNSW1000 IF NOT EQUAL
.
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RDWTTX11
          BRANCH    OVRCD,UNSW1000
.
          CALL      UNSR0000
.
          GOTO      UNSW1000
.
UNSW9999  RETURN
+
.-------------------------------------------------------------
. Unscheduled Waiting List By U/R - Rows
.-------------------------------------------------------------
UNSR0000  MOVE      SP70,DISPSTAT
          COMPARE   ONE,WMSTAT
          IF        @EQUAL
            MOVE      "WL - Unscheduled",DISPSTAT
          ENDIF
.
          MOVE      SP70,UNITDESC
          MATCH     SP70,WMUNIT
          IF        !@EQUAL
            PACK      KEY5,ANSW,ANSU,WMUNIT,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      WMUNIT,TDESC
            ENDIF
            MOVE      TDESC,UNITDESC
          ENDIF
.
          PACK      KEY6,WMDOCTOR,SP70
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE    SP70,DOCFNAME
          ENDIF
.
          MOVE      SP70,DISPPROB
          PACK      KEY9,WMCODE,SP70
          CALL      RDPROA1
          IF        OVRCD<>1
            MOVE      WPDESC,DISPPROB
          ENDIF
.
          MOVE      SP70,DISPPRTY
          MATCH     SP70,WMPTY
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WMPTY,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      WMPTY,TDESC
            ENDIF
            MOVE      TDESC,DISPPRTY
          ENDIF
.
          MOVE      SP70,DISPCOMP
          MATCH     SP70,WTTXCTYP
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,WTTXCTYP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      WTTXCTYP,TDESC
            ENDIF
            MOVE      TDESC,DISPCOMP
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkVisitWaitList('",HTMLLINK
          APPEND    WMBOOK,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    WMURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    WMURNO,HTMLLINK
          APPEND    WMCODE,HTMLLINK
          APPEND    WMCNT,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",WMDATE1,SP8,"#",":
                             "#"",DISPSTAT,"#",":
                             "#"",DISPPROB,"<br>Priority - ",DISPPRTY,"#",":
                             "#"",DISPCOMP,"<br>Unit - ",UNITDESC,"#",":
                             "#"",DOCFNAME,"#",":
                             "#"",WMBOOK,"#",":
                             "#"",SP1,"#"":
                             ")"
.
UNSR9999  RETURN
+
.-------------------------------------------------------------
.
.-------------------------------------------------------------
BOOK0000  PACK      KEY16,URNUMBER,SP70
          CALL      RSBKREC6
BOOK1000  CALL      RKBKREC6
          BRANCH    OVRCD,BOOK9999
.
          MATCH     BKREURNO,URNUMBER
          GOTO      BOOK9999 IF NOT EQUAL
.
.  *** removed lines below for CAR 309775 ***
....      PACK      CURRDATE,CCC,CYY,CMM,CDD 
....      MATCH     CURRDATE,BKREEDAT
....      GOTO      BOOK1000 IF LESS
.
          COMPARE   ZERO,BKRESTAT
          GOTO      BOOK1000 IF NOT EQUAL
.
          MOVE      SP70,DOCFNAME
          MOVE      SP70,ADOCNAME
          MOVE      BKREADOC,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            PACK      ADOCNAME,DOCFNAME
          ENDIF
.
          CALL      BKRW0000
.
          GOTO      BOOK1000
.
BOOK9999  RETURN
+
.-------------------------------------------------------------
.
.-------------------------------------------------------------
PREA0000  PACK      KEY24,URNUMBER,Z70
          CALL      RDSVISA2
PREA1000  CALL      RDPVISA2
          BRANCH    OVRCD,PREA9999
.
          MATCH     URNUMBER,PVIURNO
          GOTO      PREA9999 IF NOT EQUAL
.
          PACK      KEY17,PVIURNO,ONE,DPVIBILL,SP70
          CALL      RDPTMI18
          BRANCH    OVRCD,PREA1000
.
          COMPARE   ONE,ASTAT
          GOTO      PREA1000 IF NOT EQUAL
.
. **** removed lines below for CAR 309775 ***
....      PACK      CURRDATE,CCC,CYY,CMM,CDD
....      MATCH     CURRDATE,ADATE
....      GOTO      PREA1000 IF LESS
.
          MOVE      SP70,DOCFNAME
          MOVE      SP70,ADOCNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            PACK      ADOCNAME,DOCFNAME
          ENDIF
.
          MOVE      SP70,CLMDESC
          MATCH     SP70,ACLAIM
          IF        !@EQUAL
            PACK      KEY5,CATCL,ACLAIM,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,CLMDESC
            ENDIF
          ENDIF
.
          CALL      PRRW0000
.
          GOTO      PREA1000
.
PREA9999  RETURN
+
.-------------------------------------------------------------
.  List inpatient visits for a UR (patmiaf.dastat = '2')
.-------------------------------------------------------------
ADMS0000  PACK      KEY17,URNUMBER,SP70
          CALL      RSPTMI18
ADMS1000  CALL      RKPTMI18
          BRANCH    OVRCD,ADMS9999
.
          MATCH     AURNO,URNUMBER
          GOTO      ADMS9999 IF NOT EQUAL
.
          MATCH     "2",DASTAT
          GOTO      ADMS1000 IF NOT EQUAL
.
          MOVE      SP70,DOCFNAME
          MOVE      SP70,ADOCNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000             * Get Doctor name
            PACK      ADOCNAME,DOCFNAME    * Attending Doctor Name
          ENDIF
.
          PACK      HTMLLINK,SP70
. 
          CLEAR     HTMLLINK
.         APPEND    "javascript:linkVisitUR(",HTMLLINK
          APPEND    "javascript:linkVisitUR('",HTMLLINK
          APPEND    ADATE,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    ATIME,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    DAADMNO,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    AURNO,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK

          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",ADATE,ATIME,"#",":
                             "#"",DAADMNO,"#",":
                             "#"",DOCFNAME,"#"":
                             ")"

          GOTO      ADMS1000
.
ADMS9999  RETURN
+
.-------------------------------------------------------------
.
.-------------------------------------------------------------
PRRW0000  PACK      DISPVISN,SP70
.
          PACK      KEY37,URNUMBER,SP70
          CALL      RSPMEHB3
PRRW1000  CALL      RKPMEHB3
          BRANCH    OVRCD,PRRW3000
.
          MATCH     PMHBURNO,URNUMBER
          GOTO      PRRW3000 IF NOT EQUAL
.
          MATCH     PMHBVISN,SP70
          GOTO      PRRW1000 IF EQUAL
.
          MATCH     "2",PMHBSTAT
          GOTO      PRRW2000 IF EQUAL
.
          MATCH     "3",PMHBSTAT
          GOTO      PRRW2000 IF EQUAL
.
          GOTO      PRRW1000
.
PRRW2000  MATCH     PMHBVISN,DPVIBILL
          IF        @EQUAL
            PACK      DISPVISN,PMHBVISN
            GOTO      PRRW3000
          ENDIF
.
          GOTO      PRRW1000
.
PRRW3000  PACK      HTMLLINK,SP70
.
          CLEAR     HTMLLINK
          MATCH     "1",PTCNEPIS
          IF        @EQUAL
            APPEND    "javascript:linkVisit('IP','",HTMLLINK
          ELSE
            APPEND    "javascript:linkVisitNonEpisoft('IP','",HTMLLINK
          ENDIF
          APPEND    ADATE,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    ATIME,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    PVIURNO,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    DPVIBILL,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",ADATE,ATIME,"#",":
                             "#"Preadmission#",":
                             "#"",ADIAG1,"#",":
                             "#"",CLMDESC,"#",":
                             "#"",DOCFNAME,"#",":
                             "#"",DISPVISN,"#",":
                             "#"",PTMIXWRD,"#"":
                             ")"
.
PRRW9999  RETURN
+
.-------------------------------------------------------------
.
.-------------------------------------------------------------
BKRW0000  PACK      HTMLLINK,SP70
.
          MOVE      "Booked",DIM11
.
          MOVE      SP70,CLMDESC
          MATCH     SP70,BKRECLMT
          IF        !@EQUAL
            PACK      KEY5,CATCL,BKRECLMT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,CLMDESC
            ENDIF
          ENDIF
.
          PACK      DISPVISN,SP70
.
          PACK      KEY37,URNUMBER,SP70
          CALL      RSPMEHB3
BKRW0100  CALL      RKPMEHB3
          BRANCH    OVRCD,BKRW0300
.
          MATCH     PMHBURNO,URNUMBER
          GOTO      BKRW0300 IF NOT EQUAL
.
          MATCH     PMHBVISN,SP70
          GOTO      BKRW0100 IF EQUAL
.
          MATCH     "7",PMHBSTAT
          GOTO      BKRW0200 IF EQUAL
.
          GOTO      BKRW0100
.
BKRW0200  MATCH     PMHBVISN,DBKREBOO
          IF        @EQUAL
            PACK      DISPVISN,PMHBVISN
            GOTO      BKRW0300
          ENDIF
.
          GOTO      BKRW0100
.
BKRW0300  MATCH     SP70,DBKREBOO
          GOTO      BKRW1000 IF EQUAL
.
          PACK      KEY31,DBKREBOO,SP30
          CALL      RSOPDEA2
BKRW0500  CALL      RKOPDEA2                * get first booking
          BRANCH    OVRCD,BKRW1000          * no bookings
.
          MATCH     DBKREBOO,OPDAADMN
          GOTO      BKRW1000 IF NOT EQUAL   * no bookings for this visit
.
          COMPARE   THREE,OPDASTAT
          GOTO      BKRW0500 IF EQUAL       * cancelled booking for this visit
.
          GOTO      BKRW7000
.
BKRW1000  CALL      CLOPRDEA
          PACK      OPDADES1,SP70,SP70
          MOVE      BKREWARD,OPDAPOWD       * Medical booking use expected ward
.
BKRW7000  CLEAR     HTMLLINK
          PACK      KEY5,ANSB,ANSK,BKRETYPE,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,THCSCOD
          ENDIF
.
          MATCH     "1",PTCNEPIS
          IF        @EQUAL
            APPEND    "javascript:linkVisit('",HTMLLINK
          ELSE
            APPEND    "javascript:linkVisitNonEpisoft('",HTMLLINK
          ENDIF
          APPEND    BKRETYPE,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    OPDADATE,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    OPDATIME,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    BKREURNO,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    DBKREBOO,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    OPDACLIN,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    DOPDASTA,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    OPDACASE,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    OPDAUNIQ,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    THCSCOD,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    OPDAHOSP,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
BKRW8000  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",BKREEDAT,BKREATIM,"#",":
                             "#"",DIM11,"#",":
                             "#"",OPDADES1,"#",":
                             "#"",CLMDESC,"#",":
                             "#"",DOCFNAME,"#",":
                             "#"",DISPVISN,"#",":
                             "#"",OPDAPOWD,"#"":
                             ")"
.
BKRW9999  RETURN
+
.-------------------------------------------------------------
.
.-------------------------------------------------------------
EAHD0000  BRANCH    FLDITMNO,EAHD0010,EAHD0020,EAHD0030,EAHD0040,EAHD0050:
                             EAHD0060,EAHD0070,EAHD0080,EAHD0090
          WRITE     HTMLFILE;"Invalid Tag"
.
EAHD0010  WRITE     HTMLFILE;PMHBUNIQ;
          GOTO      EAHD9999
.
EAHD0020  WRITE     HTMLFILE;PMHBURNO;
          GOTO      EAHD9999
.
EAHD0030  WRITE     HTMLFILE;PMHBVISN;
          GOTO      EAHD9999
.
EAHD0040  PACK      KEY28,ADMISNID,ADFRADMR
          CALL      RDPMEDB1
          IF        OVRCD=1
            PACK      PMDBVALU,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70
          ENDIF
.
          WRITE     HTMLFILE;PMDBVALU;
          GOTO      EAHD9999
.
EAHD0050  MATCH     SP70,PMHBAVMO
          GOTO      EAHD9999 IF EQUAL
.
          MOVE      SP70,DOCFNAME
          MOVE      SP70,ADOCNAME
          MOVE      PMHBAVMO,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            PACK      ADOCNAME,DOCFNAME
          ENDIF
.
          WRITE     HTMLFILE;DOCFNAME;
          GOTO      EAHD9999
.
EAHD0060  MATCH     SP70,PMHBADAT
          GOTO      EAHD9999 IF EQUAL
.
          UNPACK    PMHBADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      EAHD9999
.
EAHD0070  WRITE     HTMLFILE;ADMISNID;
          GOTO      EAHD9999
.
EAHD0080  WRITE     HTMLFILE;EREFRLID;
          GOTO      EAHD9999
.
EAHD0090  READ      CONTROLF,HUND01;*41,CWEBPSTY
          WRITE     HTMLFILE;CWEBPSTY;
          GOTO      EAHD9999
.
EAHD9999  RETURN
+
.-------------------------------------------------------------
. Patient eAdmission Details
.-------------------------------------------------------------
EADM0000  MOVE      ZERO,DIM1
          UNPACK    DIM127,DIM1,DIM127
.
          MATCH     ".",DIM1
          GOTO      EADM2000 IF EQUAL
.
EADM1000  PACK      DIM4,FLDITMNO,DIM1
          MOVE      DIM4,FORM4
          PACK      VARIABLE,ANSADFR,FORM4,SP70
          REP       " 0.0",VARIABLE
          UNPACK    DIM127,DIM1,DIM1,DIM127
.
          GOTO      EADM3000
.
EADM2000  UNPACK    DIM127,DIM1,DIM127
          MOVE      FLDITMNO,FORM4
          PACK      VARIABLE,ANSADFR,FORM4,SP70
          REP       " 0.0",VARIABLE
          GOTO      EADM3000
.
EADM3000  PACK      KEY28,PMHBUNIQ,VARIABLE,SP70
          CALL      RDPMEDB1
          BRANCH    OVRCD,EADM9000
.
          MATCH     SP70,DIM1
          IF        @EQUAL
            MOVE      PMDBVALU,DIM10
            WRITE     HTMLFILE;DIM10;
            GOTO      EADM9999
          ENDIF
.
          MATCH     "A",DIM1
          IF        @EQUAL
            MOVE      PMDBVALU,DIM20
            WRITE     HTMLFILE;DIM20;
            GOTO      EADM9999
          ENDIF
.
          MATCH     "B",DIM1
          IF        @EQUAL
            MOVE      PMDBVALU,DIM40
            WRITE     HTMLFILE;DIM40;
            GOTO      EADM9999
          ENDIF
.
          MATCH     "D",DIM1
          IF        @EQUAL
            UNPACK    PMDBVALU,CCENT,CYEAR,DIM2,CDAY
            MOVE     DIM2,F2
            REP       " 0",CCENT
            REP       " 0",CYEAR
            REP       " 0",CDAY
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG:
                                 SEP,OCT,NOV,DEC
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ENDIF
.
          GOTO      EADM9999
.
EADM9000  WRITE     HTMLFILE;" "
.
EADM9999  RETURN
+
.-------------------------------------------------------------
. Patient Pre-Admission by Date Range
.-------------------------------------------------------------
PAPR0000  BRANCH    FLDITMNO,PAPR0100,PAPR0200,PAPR0300,PAPR0400,PAPR0500:
                             PAPR0600,PAPR0700,PAPR0800,PAPR0900,PAPR1000:
                             PAPR1100,PAPR1200,PAPR1300,PAPR1400,PAPR1500:
                             PAPR1600,PAPR1700,PAPR1800,PAPR1900
          GOTO      PAPR9999
.
PAPR0100  CALL      NEXT0000
          GOTO      PAPR9999
.
PAPR0200  CALL      PREV0000
          GOTO      PAPR9999
.
PAPR0300  CALL      CURSTD00
          GOTO      PAPR9999
.
PAPR0400  CALL      CUREND00
          GOTO      PAPR9999
.
PAPR0500  CALL      CURYR000
          GOTO      PAPR9999
.
PAPR0600  CALL      CURMON00
          GOTO      PAPR9999
.
PAPR0700  CALL      SELV0000
          GOTO      PAPR9999
.
PAPR0800  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      PAPR9999
.
PAPR0900  CALL      HOSSEL00
          GOTO      PAPR9999
.
PAPR1000  MOVE      ZERO,INDEXNO7           * Don't use index 7
          CALL      GETEAD00
          GOTO      PAPR9999
.
PAPR1100  WRITE     HTMLFILE;FILTFLAG;
          GOTO      PAPR9999
.
PAPR1200  WRITE     HTMLFILE;DEFTVIEW;
          GOTO      PAPR9999
.
PAPR1300  CALL      NXTY0000
          GOTO      PAPR9999
.
PAPR1400  CALL      PRVY0000
          GOTO      PAPR9999
.
PAPR1500  WRITE     HTMLFILE;FILTSNAM;
          GOTO      PAPR9999
.
PAPR1600  WRITE     HTMLFILE;PMILEVEL;
          GOTO      PAPR9999
.
PAPR1700  MOVE      ONE,INDEXNO7           * Use Index 7
          CALL      GETEAD00
          GOTO      PAPR9999
.
PAPR1800  WRITE     HTMLFILE;FILTRDAT;     * Registration date filter
          GOTO      PAPR9999
.
PAPR1900  IF        FILTRDAT=1      
            MOVE      ONE,INDEXNO7         * Use Index 7
            CALL      GETEAD00             * By registration received date
          ELSE
            MOVE      ZERO,INDEXNO7        * Don't use index 7
            CALL      GETEAD00             * By expected admission date
          ENDIF
          GOTO      PAPR9999
.
PAPR9999  RETURN
+
.------------------------------------------------------------
. Hospital Selection
.------------------------------------------------------------
HOSSEL00  PACK      KEY3,SP70
          CALL      RSPTHSP1
HOSSEL10  CALL      RKPTHSP1
          BRANCH    OVRCD,HOSSEL99
.
          PACK      KEY13,WBSEUID,SP70                * All hospital access
          CALL      RDMLSEC1
          IF        OVRCD=1
            PACK      KEY13,WBSEUID,PTHSHOSP,SP70     * This hospital access
            CALL      RDMLSEC1
            BRANCH    OVRCD,HOSSEL10
          ENDIF
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          MATCH     FILTHOSP,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
          GOTO      HOSSEL10
.
HOSSEL99  RETURN
+
.------------------------------------------------------------
. Previous Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"patweb14.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&viewtype=",VIEWTYPE:
                             "&filtrdat=",FILTRDAT:
                             "&deftview=",DEFTVIEW;
PREV9999  RETURN
+
.-------------------------------------------------------------
. Next Day, Week, Month
.-------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"patweb14.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",NXTFDATE:
                             "&viewtype=",VIEWTYPE:
                             "&lastdate=",NXTLDATE:
                             "&filtrdat=",FILTRDAT:
                             "&deftview=",DEFTVIEW;
NEXT9999  RETURN
+
.------------------------------------------------------------
. Previous Year -  Day, Week, Month
.------------------------------------------------------------
PRVY0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          UNPACK    FRSTDATE,DIM4,DIM4A
          MATCH     "0229",DIM4A            * 29th Feb
          IF        @EQUAL
             MOVE     "0228",DIM4A          * 28th Feb
          ENDIF
          MOVE      DIM4,F4
          SUB       ONE,F4                  * Sub one year
          MOVE      F4,DIM4
          PACK      YEARFDAT,DIM4,DIM4A,SP70
.
          UNPACK    LASTDATE,DIM4,DIM4A
          MATCH     "0229",DIM4A            * 29th Feb
          IF        @EQUAL
             MOVE     "0228",DIM4A          * 28th Feb
          ENDIF
          MOVE      DIM4,F4
          SUB       ONE,F4                  * Sub one year
          MOVE      F4,DIM4
          PACK      YEARLDAT,DIM4,DIM4A,SP70
.
          WRITE     HTMLFILE;"patweb14.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",YEARFDAT:
                             "&lastdate=",YEARLDAT:
                             "&viewtype=",VIEWTYPE:
                             "&filtrdat=",FILTRDAT:
                             "&deftview=",DEFTVIEW;
PRVY9999  RETURN
+
.-------------------------------------------------------------
. Next Year - Day, Week, Month
.-------------------------------------------------------------
NXTY0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          UNPACK    FRSTDATE,DIM4,DIM4A
          MATCH     "0229",DIM4A            * 29th Feb
          IF        @EQUAL
             MOVE     "0228",DIM4A          * 28th Feb
          ENDIF
          MOVE      DIM4,F4
          ADD       ONE,F4                  * Add one year
          MOVE      F4,DIM4
          PACK      YEARFDAT,DIM4,DIM4A,SP70
.
          UNPACK    LASTDATE,DIM4,DIM4A
          MATCH     "0229",DIM4A            * 29th Feb
          IF        @EQUAL
             MOVE     "0228",DIM4A          * 28th Feb
          ENDIF
          MOVE      DIM4,F4
          ADD       ONE,F4                  * Add one year
          MOVE      F4,DIM4
          PACK      YEARLDAT,DIM4,DIM4A,SP70
.
          WRITE     HTMLFILE;"patweb14.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",YEARFDAT:
                             "&viewtype=",VIEWTYPE:
                             "&lastdate=",YEARLDAT:
                             "&filtrdat=",FILTRDAT:
                             "&deftview=",DEFTVIEW;
NXTY9999  RETURN

.-------------------------------------------------------------
.         GETEAD00 - get the eAdmission List
.-------------------------------------------------------------
GETEAD00  IF        INDEXNO7=1           
            MOVE      ZERO,OVRCD         * Check if Index 7 is available
            TRAP      OVERCOND IF IO
            OPEN      PMSEHBA7,"pmsehbaf"
            IF        OVRCD=1
              WRITE     HTMLFILE;"alert('Registration date view ":
                                 "not available');"
              GOTO      GETEAD99
            ENDIF
          ENDIF
.
          IF        SHOWFLAG=0
            MATCH     SP70,HOSPFLAG
            IF        @EQUAL
              MOVE      WBSEHOSP,HOSPFLAG   * Default to users hosp
            ENDIF
          ENDIF
.
          PACK      KEY32,FRSTDATE,SP70
          IF        INDEXNO7=1
            CALL      RSPMEHB7
          ELSE
            CALL      RSPMEHB2
          ENDIF
GETEAD10  IF        INDEXNO7=1
            CALL      RKPMEHB7
          ELSE
            CALL      RKPMEHB2
          ENDIF
          BRANCH    OVRCD,GETEAD99
.
          IF        INDEXNO7=1
            MATCH     PMHBDATE,LASTDATE
            GOTO      GETEAD99 IF LESS
          ELSE
            MATCH     PMHBADAT,LASTDATE
            GOTO      GETEAD99 IF LESS
          ENDIF
.
GETEAD40  MATCH     SP70,HOSPFLAG
          IF        !@EQUAL
            MATCH     HOSPFLAG,PMHBHOSP
            GOTO      GETEAD10 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTSNAM
          IF        !@EQUAL & !@EOS
            PACK      KEY28,PMHBUNIQ,PMDB0049,SP70      * Surname
            CALL      RDPMEDB1
            BRANCH    OVRCD,GETEAD10
.
            MOVE      FILTSNAM,DIM40A
            REP       UPPLOW,DIM40A                     * Upper case 
            MOVE      PMDBVALU,DIM40B
            REP       UPPLOW,DIM40B
.
            MATCH     DIM40A,DIM40B                     * Matching surname
            GOTO      GETEAD10 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTFLAG
          GOTO      GETEAD90 IF EQUAL
.
          MATCH     FILTFLAG,PMHBSTAT
          GOTO      GETEAD10 IF NOT EQUAL
.
GETEAD90  CALL      PMRW0000
          GOTO      GETEAD10
.
GETEAD99  RETURN
+
.------------------------------------------------------------
.
.------------------------------------------------------------
PMRW0000  MOVE      PMHBADAT,CALCDAT1
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          MOVE      DIM8,CALCDAT2
.
          MOVE      SP70,PGNAME
          MOVE      SP70,PSEX
          MOVE      SP70,PTITL
          MOVE      SP70,PSNAME
          MOVE      SP70,PTMASNAM
          MOVE      SP70,PMPXPFSN
          MOVE      SP70,PMPXPFGN
.
          MOVE      SP70,STREETNO
          MOVE      SP70,PBDATE
          MOVE      SP70,STREETNM
          MOVE      SP70,SUBURB
          MOVE      SP70,POSTCODE
          MOVE      SP70,BKTYPE
          MOVE      SP70,ADMINDTE
          MOVE      SP70,ADMITVMO
          MOVE      SP70,STATDSC
          MOVE      SP70,ADMREASN
          MOVE      SP70,CLMDESC
          MOVE      ZERO,HYPERDOC
          PACK      HYPERURL,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70
          STRIP     HYPERURL
.
          MATCH     PMHBVISN,SP70
          GOTO      PMRW0500 IF NOT EQUAL
.
          DAYSEP    CALCDAT2,CALCDAT1,NUMDAYS
.
          IF        NUMDAYS < 2
            MOVE      RED,PRIORCLR
            MOVE      ONE,PRIORSRT
            GOTO      PMRW0600
          ENDIF
.
          IF        NUMDAYS = 2
            MOVE      YELLOW,PRIORCLR
            MOVE      TWO,PRIORSRT
            GOTO      PMRW0600
          ENDIF
.
          IF        NUMDAYS < 7
            MOVE      YELLOW,PRIORCLR
            MOVE      TWO,PRIORSRT
            GOTO      PMRW0600
          ENDIF
.
          IF        NUMDAYS = 7
            MOVE      YELLOW,PRIORCLR
            MOVE      TWO,PRIORSRT
            GOTO      PMRW0600
          ENDIF
.
          MOVE      GREY,PRIORCLR
          MOVE      FOUR,PRIORSRT
          GOTO      PMRW0600
.
PMRW0500  MOVE      GREEN,PRIORCLR
          MOVE      THREE,PRIORSRT
.
PMRW0600  PACK      VARIABLE,SP70,SP70
          MOVE      ONE,VARCOUNT
.
          MATCH     SP70,PRIOFLAG
          GOTO      PMRW1000 IF EQUAL
.
          MATCH     PRIOFLAG,PRIORCLR
          GOTO      PMRW9999 IF NOT EQUAL
.
PMRW1000  LOAD      VARIABLE,VARCOUNT,PMDB0049:      * 1 surname
                                      PMDB0042:      * 2 title
                                      PMDB0050:      * 3 given name
                                      PMDB0055:      * 4 gender
                                      PMDB0053:      * 5 dob
                                      PMDB0063:      * 6 Street No
                                      PMDB0064:      * 7 Street Name
                                      PMDB0065:      * 8 Suburb
                                      PMDB0069:      * 9 post code
                                      PMDB0003:      * 10 admission type
                                      PMDB0014:      * 11 admission date
                                      PMDB0025:      * 12 admitting doctor
                                      PMDB0029:      * 13 admission reason
                                      PMDB0013:      * 14 Claim code
                                      PMDB0298       * 15 Hyper Link Document
.
          PACK      KEY28,PMHBUNIQ,VARIABLE,SP70
          CALL      RDPMEDB1
          IF        OVRCD <> 1
            BRANCH    VARCOUNT,PMRW1010:          * 1
                               PMRW1020:          * 2
                               PMRW1030:          * 3
                               PMRW1040:          * 4
                               PMRW1050:          * 5
                               PMRW1060:          * 6
                               PMRW1070:          * 7
                               PMRW1080:          * 8
                               PMRW1090:          * 9
                               PMRW1100:          * 10
                               PMRW1110:          * 11
                               PMRW1120:          * 12
                               PMRW1130:          * 13
                               PMRW1140:          * 14
                               PMRW1150           * 15
          ENDIF
.
          GOTO      PMRW2000
.
PMRW1010  MOVE      PMDBVALU,PSNAME
          MOVE      PMDBVALU,PTMASNAM
          GOTO      PMRW2000
.
PMRW1020  MOVE      PMDBVALU,PTITL
          GOTO      PMRW2000
.
PMRW1030  MOVE      PMDBVALU,PGNAME
          GOTO      PMRW2000
.
PMRW1040  MOVE      PMDBVALU,PSEX
          GOTO      PMRW2000
.
PMRW1050  MOVE      PMDBVALU,PBDATE
          GOTO      PMRW2000
.
PMRW1060  MOVE      PMDBVALU,STREETNO
          GOTO      PMRW2000
.
PMRW1070  MOVE      PMDBVALU,STREETNM
          REP       CNVQUOTE,STREETNM          *Converts " to '     *T0865219
          GOTO      PMRW2000
.
PMRW1080  MOVE      PMDBVALU,SUBURB
          GOTO      PMRW2000
.
PMRW1090  MOVE      PMDBVALU,POSTCODE
          GOTO      PMRW2000
.
PMRW1100  MOVE      PMDBVALU,BKTYPE
          GOTO      PMRW2000
.
PMRW1110  MOVE      PMDBVALU,ADMINDTE
          GOTO      PMRW2000
.
PMRW1120  PACK      KEY10,PMDBVALU,SP70
          CALL      RDPMHCP1
          IF        OVRCD<>1
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,ADMITVMO
          ENDIF
          GOTO      PMRW2000
.
PMRW1130  MOVE      PMDBVALU,ADMREASN
          GOTO      PMRW2000
.
PMRW1140  PACK      PMDBVALU,PMDBVALU,SP70
          MATCH     SP70,PMDBVALU
          GOTO      PMRW2000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,PMDBVALU,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      PMDBVALU,TDESC
          ENDIF
          MOVE      TDESC,CLMDESC
          GOTO      PMRW2000
.
PMRW1150  PACK      PMDBVALU,PMDBVALU,SP70
          MATCH     SP70,PMDBVALU
          IF        !@EQUAL
            MOVE      ONE,HYPERDOC
            STRIP     PMDBVALU
            CLEAR     HYPERURL
            APPEND    "javascript:HDocLink('",HYPERURL
            APPEND    PMDBVALU,HYPERURL
            APPEND    "');",HYPERURL
            RESET     HYPERURL
            RESET     PMDBVALU
          ENDIF
          GOTO      PMRW2000
.
PMRW2000  ADD       ONE,VARCOUNT
.
          COMPARE   "16",VARCOUNT
          GOTO      PMRW1000 IF LESS
.
          CALL      IBACLOCK
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
.
          CALL      CALCAGE
          MOVE      PMHBURNO,PURNO
          MATCH     SP70,PURNO
          IF        !@EQUAL
            PACK      KEY8,PURNO,SP70
            CALL      RDMAST1
            CALL      RDPMPX21
          ENDIF
          CALL      PATLN000
.
          MOVE      SP70,BKDESC
          MATCH     BKTYPE,SP70
          GOTO      PMRW2050 IF EQUAL
.
          PACK      KEY5,CATBK,BKTYPE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      TDESC,BKDESC
          ENDIF
.
PMRW2050  MOVE      ZERO,FORM1
          MOVE      PMHBSTAT,FORM1
          LOAD      STATDSC,FORM1,STATUS1:     * pending
                                  STATUS2:     * processed
                                  STATUS3:     * processing
                                  STATUS4:     * process cancelled
                                  STATUS5:     * ignore
                                  STATUS6:     * cancelled
                                  STATUS7      * booked
.
          STRIP     PRIORCLR
.
          MOVE      SP70,HISDATE
          MOVE      ZERO,DOCEXIST
          PACK      LINKDOC,SP70
.
          MATCH     PMHBLDTY,SP70
          GOTO      PMRW3000 IF NOT EQUAL
.
          MATCH     PMHBVISN,SP70
          IF        !@EQUAL
            MOVE      ONE,DOCEXIST
            GOTO      PMRW4000
          ENDIF
.
          GOTO        PMRW8000
.
PMRW3000  MATCH     PMHBLDDT,SP70
          GOTO      PMRW3100 IF EQUAL
.
          UNPACK    PMHBLDDT,CCENT,CYEAR,DIM2,CDAY
          MOVE      DIM2,F2
          REP       " 0",CCENT
          REP       " 0",CYEAR
          REP       " 0",CDAY
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      HISDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
PMRW3100  MOVE      ONE,DOCEXIST
.
          MATCH     PMHBVISN,SP70
          GOTO      PMRW4000 IF NOT EQUAL
.
          PACK      LINKDOC,SP70
          PACK      KEY24,PMHBEAID,Z70,SP70
          CALL      RSOBECO1
          CALL      RPOBECO1
.
          MATCH     PMHBEAID,OBECEAID
          IF        @EQUAL
            PACK      KEY4,OBECSLID
            CALL      RDOBPT1
            STRIP     OBPTURLP
            APPEND    "javascript:openDocument('",LINKDOC
            REP       " +",PMHBEAID
            APPEND    PMHBEAID,LINKDOC
            APPEND    DASH,LINKDOC
            REP       " 0",OBECCPID
            APPEND    OBECCPID,LINKDOC
            APPEND    OBECFEXT,LINKDOC
            APPEND    "','",LINKDOC
            APPEND    OBPTSLID,LINKDOC
            APPEND    "');",LINKDOC
            REP       "+ ",PMHBEAID
            RESET     LINKDOC
          ENDIF
.
          GOTO        PMRW8000
.
PMRW4000  PACK      LINKDOC,SP70
          APPEND    "javascript:printDocument('",LINKDOC
          APPEND    PMHBURNO,LINKDOC
          APPEND    "','",LINKDOC
          APPEND    PMHBVISN,LINKDOC
          APPEND    "','",LINKDOC
          APPEND    PMHBUNIQ,LINKDOC
          APPEND    "');",LINKDOC
          RESET     LINKDOC
.
PMRW8000  MOVE      PMHBUNIQ,ONLINEID
          REP       " +",ONLINEID
.
          COMPARE   SEVEN,FORM1
          IF        @EQUAL
            CLEAR     BOOKICON
            APPEND    "<img src=../images/AppointmentIcon.gif /> ",BOOKICON
            RESET     BOOKICON
            CLEAR     BOOKLINK
            APPEND    "javascript:LinkToBook('",BOOKLINK
            APPEND    ONLINEID,BOOKLINK
            APPEND    "','",BOOKLINK
            APPEND    PMHBURNO,BOOKLINK
            APPEND    "','",BOOKLINK
            APPEND    PMHBVISN,BOOKLINK
            APPEND    "')",BOOKLINK
            RESET     BOOKLINK
          ENDIF
.
          CLEAR     FLDRLINK
          APPEND    "javascript:LinkTo('",FLDRLINK
          APPEND    ONLINEID,FLDRLINK
          APPEND    "','",FLDRLINK
          APPEND    PMHBURNO,FLDRLINK
          APPEND    "','",FLDRLINK
          APPEND    PMHBVISN,FLDRLINK
          APPEND    "')",FLDRLINK
          RESET     FLDRLINK
.
          WRITE     HTMLFILE;"t.addRow(#"",FLDRLINK,"#",":
                             "#"",PRIORCLR,"#",":          * priority colour
                             "#"",ZERO,"#",":              * folder colour
                             "#"",FORMATNM:                * fmtd name/addr
                                 "<br /><span style=#'padding-left:30px;#'>":
                                 STREETNM,SUBURB,POSTCODE,"#",":
                             "#"",BKDESC,"#",":            * booking type
                             "#"",PMHBDATE,"#",":          * reg received
                             "#"",HISDATE,"#",":           * history received
                             "#"",PMHBADAT,"#",":          * expect admiss dt
                             "#"",ADMITVMO,"#",":          * expect admiss dt
                             "#"",PMHBURNO,"#",":          * urno
                             "#"",PMHBVISN,"#",":          * admission no
                             "#"","#",":                   * reg received dt
                             "#"",STATDSC,"#",";           * status
.
          MATCH     PMHBLDTY,SP70
          IF        @EQUAL
            WRITE     HTMLFILE;"#"",ZERO,"#",":            * no history recvd
                               "#"",SP1,"#",";
          ELSE
            WRITE     HTMLFILE;"#"",DOCEXIST,"#",":        * history recvd
                               "#"",LINKDOC,"#",";
          ENDIF
.
          COMPARE   SEVEN,FORM1
          IF        @EQUAL
            WRITE     HTMLFILE;"#"",BOOKLINK,"#",":        * booked
                               "#"",BOOKICON,"#",";
          ELSE
            WRITE     HTMLFILE;"#"",SP1,"#",":             * not booked
                               "#"",SP1,"#",";
          ENDIF
.
          WRITE     HTMLFILE;"#"",SP1,"#",":        * Dummy for tablesort
                             "#"",ADMREASN,"#",":   * Admission Reason
                             "#"",PRIORSRT,"#",":   * Priority sort order
                             "#"",CLMDESC,"#",":    * Claim code
                             "#"",HYPERDOC,"#",":   * 21 - Hyperlink doc exists
                             "#"",HYPERURL,"#",":   * 22 - Doc hyperLink exists
                             "#"",SP1,"#");"        * 23 - used by 21 and 22
.
PMRW9999  RETURN
+
.-------------------------------------------------------------------------------
. Clear pmsehbaf (?)
.-------------------------------------------------------------------------------
CLREHB00  MOVE      SP70,PMHBUNIQ
          MOVE      SP70,PMHBDATE
          MOVE      SP70,PMHBTIME
          MOVE      SP70,PMHBSTAT
          MOVE      SP70,PMHBADAT
          MOVE      SP70,PMHBURNO
          MOVE      SP70,PMHBVISN
          MOVE      SP70,PMHBHOSP
          MOVE      SP70,PMHBUDAT
          MOVE      SP70,PMHBBTYP
          MOVE      SP70,PMHBAVMO
          MOVE      SP70,PMHBEAID
          MOVE      SP70,PMHBLDDT
          MOVE      SP70,PMHBLDTY
          MOVE      SP70,PMHBUTIM
          MOVE      SP70,PMHBWEBU
.
CLREHB99  RETURN
.
.-------------------------------------------------------------------------------
. Set PMI level eAdmission search filter defaults
.-------------------------------------------------------------------------------
PMIL0000  MATCH     "1",PMILEVEL            * Set PMI level search filters
          GOTO      PMIL9999 IF NOT EQUAL
.
          MOVE      ONE,FILTFLAG            * Pending
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          IF        OVRCD<>1
            CALL      RDPMPX21
            MATCH     SP70,PTMASNAM
            IF        !@EQUAL
              MOVE      PTMASNAM,FILTSNAM   * Surname
            ELSE
              MOVE      PSNAME,FILTSNAM
            ENDIF
            STRIP     FILTSNAM
          ENDIF
.
          MOVE      ZERO,VIEWTYPE           * Single day
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKREC1                * Expected date of booking
          IF        OVRCD<>1
            MOVE      BKREEDAT,LASTDATE
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          IF        OVRCD<>1
            MOVE      ADATE,LASTDATE        * Date of IP visit
          ENDIF
.
PMIL9999  RETURN
+
.-------------------------------------------------------------
.  List curren inpatient visits for a UR (patmiaf.dastat = '2')
.-------------------------------------------------------------
CURA0000  MATCH     "1",PTCNEPIS
          GOTO      CURA9999 IF NOT EQUAL
.
          PACK      KEY17,URNUMBER,SP70
          CALL      RSPTMI18
CURA1000  CALL      RKPTMI18
          BRANCH    OVRCD,CURA9999
.
          MATCH     AURNO,URNUMBER
          GOTO      CURA9999 IF NOT EQUAL
.
          MATCH     "2",DASTAT
          GOTO      CURA1000 IF NOT EQUAL
.
          MOVE      SP70,DOCFNAME
          MOVE      SP70,ADOCNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000             * Get Doctor name
            PACK      ADOCNAME,DOCFNAME    * Attending Doctor Name
          ENDIF
.
          MOVE      SP70,CLMDESC
          MATCH     SP70,ACLAIM
          IF        !@EQUAL
            PACK      KEY5,CATCL,ACLAIM,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,CLMDESC
            ENDIF
          ENDIF
.
          PACK      HTMLLINK,SP70
          CLEAR     HTMLLINK
          APPEND    "javascript:linkVisitCurrAdm('",HTMLLINK
          APPEND    DAADMNO,HTMLLINK
          APPEND    "'",HTMLLINK
          APPEND    ",'",HTMLLINK
          APPEND    AURNO,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          PACK      DISPVISN,SP70
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",ADATE,ATIME,"#",":
                             "#"Current IP#",":
                             "#"",ADIAG1,"#",":
                             "#"",CLMDESC,"#",":
                             "#"",DOCFNAME,"#",":
                             "#"",DISPVISN,"#",":
                             "#"",AWARD,"#"":
                             ")"
          GOTO      CURA1000
.
CURA9999  RETURN
+
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       CLCOMERH
          INC       WEBDATCD
.
          INC       APSMASIO/INC
          INC       BOKRC1IO/INC
          INC       COMERHIO/INC
          INC       MLTSECIO/INC
          INC       MRTMASIO/INC
          INC       NHIETHIO/INC
          INC       NHIMASIO/INC
          INC       OBSAUDIO/INC
          INC       OBSECOIO/INC   * eAdmission Patient Documents
          INC       OBSPCOIO/INC
          INC       OBSPCTIO/INC
          INC       OPRDEAIO/INC
          INC       OUTSITIO/INC
          INC       PATALRIO/INC
          INC       PATASFIO/INC
          INC       PATCMCIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATDADIO/INC
          INC       PATDGWIO/INC
          INC       PATDO1IO/INC   * Doctor File
          INC       PATDSCIO/INC
          INC       PATFN1IO/INC
          INC       PATHSPIO/INC
          INC       PATIN1IO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATRE1IO/INC
          INC       PATVADIO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PATWR1IO/INC
.
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSEDBIO/INC   * eAdmission Patient Details
          INC       PMSEHBIO/INC   * eAdmission Patient Header
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC
          INC       UPEADMVS       * Update eAdmission Visit Number
.
          INC       CALCAGE
          INC       CLNHIMAS       * Clear nhimasaf file variables
          INC       CLOPRDEA
          INC       CLPMSPX2
          INC       DAYOFWEK
          INC       NAMSTRCD
          INC       PATDOCCD
          INC       PATDOCTM
          INC       PATDSCTM
          INC       PATMASTM
          INC       PATMISTM
          INC       PATMSXTM
          INC       PATNAMCD      * Formate Patient Name
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PMSPX2TM

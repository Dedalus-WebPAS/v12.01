.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   IBAADT25                                                  *
.* Desc      :   Health Fund EDI Audit Table File Listing                  *
.***************************************************************************
.* Date      :   11/03/1999                                                *
.* Author    :   Andrew Peel                                               *
.* Function  :   This program prints & clears the EDI Audit Table          *
.* Mods      :                                                             *
.*        V5.08.00  06/07/2000  Greg Horvat                                *
.*                  Copied from release 6.07 version V6.07.00              *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
          INC       IBAPASFD/INC
          INC       PATEDAFD/INC 
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
DISDATE   DIM       10                      * Display Date Format DD/MM/CCYY
DISEDIS   DIM       28                      * Display EDI Sent Flag
DISOPER   DIM       35                      * Display Operator User Name
DISTIME   DIM       5                       * Display Time Format HH:MM
DSPEETP   DIM       11                      * Display EDI extract type
LINECNT   FORM      2                       * Count No. of Lines Printed
.
DSPEETP0  INIT      "Health Fund"
DSPEETP1  INIT      "TAC        "
.
PRGID     INIT      "IBAADT25"
PRGNAM    INIT      "Health Fund EDI Audit Table File Listing"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000                * initialisation and open files
.
ML0100    CALL      OPTN0000                * select options
          BRANCH    EXIT,ML9999             * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000   * proceed with Printing
.
ML1000    CALL      CONTQST                 * Ok to continue ?
          BRANCH    CEXIT,ML2000:           * yes
                          ML0100:           * no
                          ML0100            * cancel
.
ML2000    CALL      PRINT000                * Print Audit Report
.
ML9999    CHAIN     PGM                     * chain back to program
.
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                * Display Heading
          DISPLAY   *P56:24,"Opening patedaaf";
          OPEN      PATEDAA1,"patedaaf"
.
          DISPLAY   *P56:24,"Opening ihapassf";
          OPEN      IHAPASS1,"ihapassf"
.
INIT9999  RETURN
.
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run Print Option                       *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Print Audit": 
                    *P1:6,*V2LON,TWO,*HOFF,". Print & Clear Audit" 
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN5000,OPTN5000    * Option 1 or 2 ?      
.
          BEEP
          GOTO      OPTN0500
.
OPTN5000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.****************************************************************************
.*                                PRINT000             Called by: ML0000    *
.*                           Prints Audit Report.                           *
.****************************************************************************
PRINT000  CALL      HEAD0000                * Print Header
.          
          PACK      KEY8,SP8
PRINT300  CALL      RSPTEDA1                * Positional Read EDI Audit File 
PRINT400  CALL      RKPTEDA1                * Sequential Read EDI Audit File
          BRANCH    OVRCD,PRINT900
          CALL      LOAD0000                * Load details
          CALL      EDIS0000                * Convert EDI Sent Flag to a string
          PRINT     *1,"|  ",PTEDINVN,*15,"|  ",DISDATE,*31,"| ",DISTIME:
                    *47,"| ",DISOPER,*87,"| ",DISEDIS,*120,"| ",PTEDBTHN,"  |"
          ADD       ONE,LINECNT
          COMPARE   "55",LINECNT            * Max no. of lines per page
          CALL      HEAD0000 IF NOT LESS
          COMPARE   ONE,COPTION             * Print Only ?
          GOTO      PRINT400 IF EQUAL
.
          PACK      KEY8,PTEDINVN,SP8
          CALL      DEPTEDA1                * Delete Record
          GOTO      PRINT300
.
PRINT900  PRINT     *1,"*---------------------------------------": 
                    "----------------------------------------------":
                    "---------------------------------------------*"
          DISPLAY   *P40:23,*EL,*V2LON:
                    "** Report Generation Completed **"
          PRINT     *N,"  ***  END OF REPORT  ***" 
.
PRINT999  RETURN
.
.****************************************************************************
.*                                HEAD0000             Called by: PRINT000  *
.*               Print the Header for each page to the file.                *
.****************************************************************************
HEAD0000  MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
          PRINT     *N,*N,*1,"*---------------------------------------":
                    "----------------------------------------------":
                    "---------------------------------------------*":
                    *N,*1,"| Invoice No.",*15,"| Date Changed":
                    *31,"| Time Changed",*47,"| Operator":
                    *87,"| EDI Sent Flag",*120,"| Batch No. |":
                    *N,*1,"*---------------------------------------":
                    "----------------------------------------------":
                    "---------------------------------------------*"
.
          MOVE      ZERO,LINECNT
.
HEAD9999  RETURN
.
.****************************************************************************
.*                                LOAD0000             Called by: PRINT000  *
.*                              Load Details                                *
.****************************************************************************
LOAD0000  UNPACK    PTEDDECM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the date
          MOVE      CPCDATE,DISDATE
          REP       " 0",DISDATE
          MOVE      PTEDTECM,DISTIME        * Move Time to a DIM 5 (HH:MM) 
.
          MOVE      "User Unknown",DISOPER 
          PACK      KEY4,PTEDOPRR,SP4       * Key is Operator
          CALL      RDPASS1                 * Read Passcodes file
          BRANCH    OVRCD,LOAD1000
.
          MOVE      SECUSER,DISOPER
LOAD1000  MOVE      DSPEETP0,DSPEETP
          LOAD      DSPEETP,PTEDEETP,DSPEETP1 * EDI extract type
LOAD9000  MOVE      ZERO,EXIT
LOAD9999  RETURN
.
.****************************************************************************
.*                                EDIS0000             Called by: PRINT000  *
.*                     Convert EDI Sent Flag to a string                    *
.****************************************************************************
EDIS0000  BRANCH    PTEDEDSF,EDIS1000,EDIS2000,EDIS3000
.
          IF        PTEDEDSF=0
            MOVE      "Not Sent",DISEDIS
          ELSE
            MOVE      "Error NO VALUE !",DISEDIS
          ENDIF
          GOTO      EDIS9999
EDIS1000  MOVE      "Sent",DISEDIS
          GOTO      EDIS9999
EDIS2000  MOVE      "Rejected Claim",DISEDIS
          GOTO      EDIS9999
EDIS3000  MOVE      "On Hold/Exclude From Extract",DISEDIS
          GOTO      EDIS9999
.
EDIS9999  RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       IBAPASIO/INC
          INC       PATEDAIO/INC

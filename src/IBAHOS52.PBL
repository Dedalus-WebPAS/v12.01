.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAHOS52                                             *
.*                                                                            *
.*     FUNCTION:         TRANSFER REPORT                                      *
.*                                                                            *
.*     DATE WRITTEN:     06/06/1988                                           *
.*                                                                            *
.*     AUTHOR:           J.TAN                                                *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.******************************************************************************
.*        V12.00.01 21/05/2025 Nikitha B     TSK 0955096                      *
.*                  Alphanumeric changes                                      *
.*        V10.14.01 26/08/2019  Thanh T.        TSK 0877434                   *
.*                  Fixed issue with writing to TEMPSEQ when it is closed.    * 
.*                  Added PREP for TEMPSEQ file in INDZD. Removed PREP and    *
.*                  added CLOSE for TEMPSEQ file in SORTD.                    *
.******************************************************************************
.*        V10.13.01 05/12/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (TEMPSEQ) after processing.             *
.*                  Removed PORT from temp filenames (FNAMEG, FNAMEI).        *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.B02 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 12/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.******************************************************************************
.*        V5.00     18/05/1989  DIG                                           *
.*                  Changed to standard & modified print for 8 digit U/Rs etc.*
.*        V5.01     27/05/89  J.Tan        SRF 100750                         *
.*                  Fixed Mother Adm. to Form 8 (PATMI1FD)                    *
.*        V5.02     06/06/89  J.Tan        SRF 100809                         *
.*                  Mods Locking master file (PATIOMAS)                       *
.*        V5.03     10/06/1989  S.Barcham         SRF 100481                  *
.*                  Remove Discharges                                         *
.*                                                                            *
.*        V5.01.01  05/07/1989  K.Peak                                        *
.*                  Changed Admission Number and U/R to 8 characters          *
.*        V5.01.02  08/09/1989  K.Peak                                        *
.*                  Added sex to report layout                                *
.*        V5.01.03  05/02/90  J.Tan        SRF 103850                         *
.*                  Fixed the Tempfile                                        *
.*        V5.01.04  08/02/90  J.Tan        SRF 103897                         *
.*                  Fixed gndfile                                             *
.*        V5.01.05  27/04/1990  Steve O'Gorman                                *
.*                  Added Discharges which were previously removed to try and *
.*                  balance New Zealand                                       *
.*        V5.01.06  23/05/1990  Steve O'Gorman    SRF 104926                  *
.*                  Checked Sequences to cont. the same data.                 *
.*                  Added ward and doctor transfer totals.                    *
.*                  Added footnote reguarding change of both ward and doctor  *
.*        V5.01.121 27/11/1990  Glen Hobby                                    *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.122 05/02/1993  i chung           SRF 120531                  *
.*                  Fixed incorrect index definition for temp file (TEMPFILE) *
.* Release 5.02                                                               *
.*        V5.02.01  27/04/1995  Gabrielle         SRF 640242                  *
.*                  Changed program to create temp files using EXECUTE        * 
.*                  statment and not the old rollout files                    *
.******************************************************************************
.
          INC       STD001FD
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
.
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         TEMPORARY FILES DEFINITION
.
TEMPSEQ   FILE      ASCII, FIXED=256
SORT1     INIT      "u1-20,50-65,66-81,106-113"
SORT2     INIT      "u50-65,66-81,106-113"
SORT3     INIT      "u66-81,50-65,106-113"
SORTSTR   DIM       25
.
GNDFILE   IFILE     SQL, FIXED=10, KEY="u1-8"
TEMPFILE  IFILE     SQL, FIXED=114, KEY=SORTSTR
.
. -----  Temporary file layout -----
.
.NAME     TYPE    LENGTH   PHYSICAL    START     DESCRIPTION
.----     ----    ------   --------    -----     -----------
.PSNAME   DIM       20        20        1
.PTITL    DIM       4         4         21
.PGNAME   DIM       25        25        25
DTURNO    DIM       8         8         50
.DTADMN   DIM       8         8         58
.TDATE    DIM       8         8         66
.TTIME    DIM       8         8         74
.XOWARD   DIM       3         3         82
.XOBED    DIM       3         3         85
.XNWARD   DIM       3         3         88
.XNBED    DIM       3         3         91
.XODOC    DIM       6         6         94
.XNDOC    DIM       6         6         100
DUNIQUE   DIM       8         8         106
.End of Record                          114
+
.
.         VARIABLES DEFINITION
.
CHGWRD    FORM      1
CMDLINE   DIM       50
CUDATE    DIM       8
.
DIM6      DIM       6
DNAME1    DIM       16
DNAME2    DIM       16
.
FNAMEG    DIM       8
FNAMEI    DIM       8
FNAMER    DIM       8
FROMDATE  DIM       8                  * From Date in CCYYMMDD format
.
GNDDOCTR  FORM      5                  * Total of Doctor Transfers
GNDPAT    FORM      5                  * Total Number of Patients on Report
GNDWRDTR  FORM      5                  * Total of Ward Transfers
.
LNO       FORM      2
.
OLDBED    DIM       3
OLDDOCT   DIM       6
OLDMOVE   DIM       1
OLDWARD   DIM       3
OPCND     FORM      1
.
PAGENO    FORM      3
PSNAMEDS  DIM       19
.
SDIM6     DIM       6
SEC       FORM      2
STATUS    FORM      1
.
TODATE    DIM       8                  * To Date in CCYYMMDD format
UNIQUE    FORM      8
VERT      FORM      2
.
WDATE     DIM       8
WSTAT     FORM      1
.
XGNAME    DIM       13
XNBED     DIM       3
XNDOC     DIM       6
XNWARD    DIM       3
XOBED     DIM       3
XODOC     DIM       6
XOWARD    DIM       3
XDAY1     DIM       2
XDAY2     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
.
.         CONSTANTS DEFINITION
.
ELEVEN    FORM      "11"
.
SP12      INIT      "            "
SP14      INIT      "              "
SP50      INIT      "                                                  "
.
PRGID     INIT      "IBAHOS52"
PRGNAM    INIT      "Transfer Report"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
.         set up current date
.
          PACK      CUDATE,CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
.
.         set up KEYDATE input requirements
.
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
.         set up filename for rollout file
.
          CLEAR     FNAMER
          APPEND    "rhos52",FNAMER
          APPEND    FORM2,FNAMER
          RESET     FNAMER
          REP       " 0" IN FNAMER
.
          MOVE      PORT,DIM2
+
.
.         START OF PROGRAM
.
START     HLINE     *G37,2,48,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF," = Patient Surname Sequence":
                     *P1:6,*V2LON,TWO,*HOFF," = U/R Number Sequence":
                   *P1:7,*V2LON,THREE,*HOFF," = Transfer Date Sequence":
                    *P1:9,"Select Option : ";
.
START1    KEYIN     *P17:9,*EL,*DV,UNDLN1,*P17:9,*V2LON,OPTION;
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF SURNAM,URSEQU,DATSEQ
          BEEP
          GOTO      START1
.
SURNAM    DISPLAY   *P48:2,*V2LON,"- Surname Sequence ",*P1:3,*EF;
          MOVE      SORT1,SORTSTR
          GOTO      KDRANGE
.
URSEQU    DISPLAY   *P48:2,*V2LON,"- U/R Sequence ",*P1:3,*EF;
          MOVE      SP30,SORTSTR
          MOVE      SORT2,SORTSTR
          GOTO      KDRANGE
.
DATSEQ    DISPLAY   *P48:2,*V2LON,"- Transfer Date Sequence ",*P1:3,*EF;
          MOVE      SP30,SORTSTR
          MOVE      SORT3,SORTSTR
.
.         Keyin a range of dates
.
KDRANGE   DISPLAY   *P1:9,*EF,"Keyin  From Date  :":
                       *P1:11,"Keyin  To   Date  :";
          MOVE      TWENTY1,CCOL
          MOVE      NINE,CVERT
.
.         Keyin From Date
.
KADAT1    UNPACK    SP8,CDAY,CMON,CYEAR,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KADAT1
          BRANCH    OVRCD,START
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          MATCH     FROMDATE,CUDATE
          GOTO      INVDTE1 IF LESS
          UNPACK    FROMDATE,XCENT1,XYEAR1,XMON1,XDAY1
.
.         Keyin To Date
.
          MOVE      ELEVEN,CVERT
KADAT2    UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,KADAT2
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     TODATE,CUDATE
          GOTO      INVDTE2 IF LESS
.
          MATCH     FROMDATE,TODATE
          GOTO      INVDATG IF LESS
.
          UNPACK    TODATE,XCENT2,XYEAR2,XMON2,XDAY2
          GOTO      REKEY
.
INVDTE1   DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** From Date must be less than or = current date **    ";
          CALL      HITENTER
          GOTO      KADAT1
.
INVDTE2   DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Date must be less than or equal current date **   ";
          CALL      HITENTER
          GOTO      KADAT2
.
INVDATG   DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Date must be greater than or equal From Date **   ";
          CALL      HITENTER
          GOTO      KADAT2
+
.
REKEY     KEYIN     *P1:24,*EL," Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          MATCH     "Y" TO ANS
          GOTO      CNTCHK IF EQUAL
          MATCH     "N" TO ANS
          GOTO      KDRANGE IF EQUAL
          BEEP
          GOTO      REKEY
.
.         PROCESS START
.
CNTCHK    DISPLAY   *P1:24,*EL,*P21:24,*V2LON:
                    "***  Initialising Temporary Files  ***",*W2;
.
          MOVE      ZERO,UNIQUE
          MOVE      ZERO,GNDWRDTR
          MOVE      ZERO,GNDDOCTR
          CALL      INDZD
          BRANCH    OPCND OF ENDIT
          CLOCK     TIME,CTIMEIS
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "***  Processing Current Admissions  ***",*W2,*HOFF:
                    *P1:24,*EL,*P13:24,"Current Admission No. :":
                    *P52:24,"Scanning :";
          MOVE      TWO,WSTAT
          CALL      ADMFN
.
          DISPLAY   *P1:24,*EL,*P21:24,*V2LON:
                    "***  Processing On Leave Admissions  ***",*W2,*HOFF:
                    *P1:24,*EL,*P12:24,"On Leave Admission No. :":
                    *P52:24,"Scanning :";
          MOVE      FOUR,WSTAT
          CALL      ADMFN
.
          DISPLAY   *P1:24,*EL,*P21:24,*V2LON:
                    "***  Processing Discharge Admissions  ***",*W2,*HOFF:
                    *P1:24,*EL,*P10:24,"Discharged Admission No. :":
                    *P52:24,"Scanning :";
.
          PACK      KEY16,SP20
          CALL      RDSDSCH2
NXTDSH    CALL      RDKDSCH2
          BRANCH    OVRCD OF SORTD
          DISPLAY   *P63:24,DADMNO;
.
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      WDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",WDATE
.
.         If the patient discharged before the fromdate, ignore it
.
          MATCH     FROMDATE,WDATE
          GOTO      NXTDSH IF LESS
.
.         Read the admission data for discharged patient
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF NXTDSH
.
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
          PACK      WDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",WDATE
.
          MATCH     WDATE,TODATE
          GOTO      NXTDSH IF LESS
          CALL      TRANS  
          GOTO      NXTDSH
.
.         READ ADMISSIONS FILE
.
ADMFN     PACK      KEY9 WITH WSTAT,SP8
          CALL      RDSMISS2
NEXT5     CALL      RDKMISS2
          BRANCH    OVRCD OF CONT1A
.
          COMPARE   WSTAT,ASTAT
          GOTO      CONT1 IF NOT EQUAL
.
DSPA      DISPLAY   *P63:24,AADMNO;
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
          PACK      WDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",WDATE
.
.         If the patient admitted after the TODATE, ignore it
.
          MATCH     WDATE,TODATE
          GOTO      NEXT5 IF LESS
          CALL      TRANS  
          GOTO      NEXT5
.
.         Make sure we have actually finished
.
CONT1     CALL      RDKMISS2
          BRANCH    OVRCD OF CONT1A
.
          COMPARE   WSTAT,ASTAT
          GOTO      DSPA IF EQUAL
CONT1A    RETURN
+
.
.         Check if ward/bed or attending doctor was changed for this patient
.
TRANS     MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NOMAST
          CALL      INTVAR
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
NEXTTR    CALL      RDKTRAN2
          COMPARE   ONE,OVRCD
          RETURN    IF EQUAL
.
          MATCH     AADMNO,TADMN
          RETURN    IF NOT EQUAL
.
          MATCH     TDATE,TODATE
          RETURN    IF LESS
.
          MATCH     FROMDATE,TDATE
          GOTO      CHKTR IF LESS
.
          CMATCH    "C",TMOVE
          GOTO      NEXT20 IF EQUAL
.
          CMATCH    "R",TMOVE
          GOTO      NEXT50 IF NOT EQUAL
.
NEXT20    PACK      SDIM6 WITH OLDWARD,OLDBED
          PACK      DIM6 WITH TWARD,TBED
          MOVE      ZERO,CHGWRD
.
          MATCH     DIM6,SDIM6
          GOTO      NOCHWR IF EQUAL
.
          MOVE      OLDWARD,XOWARD
          MOVE      OLDBED,XOBED
          MOVE      ONE,CHGWRD
          ADD       ONE,GNDWRDTR            * Increment the Ward Transfer Count
.
NOCHWR    MOVE      TWARD,XNWARD
          MOVE      TBED,XNBED
.
          MATCH     TADOCT,OLDDOCT
          GOTO      NOCHDOC IF EQUAL
          MOVE      OLDDOCT,XODOC
          ADD       ONE,GNDDOCTR            * Increment Doctor Transfer Count
          GOTO      NEXT30
.
.         If ward/bed and att.doctor not change then ignore it
.
NOCHDOC   COMPARE   ZERO,CHGWRD
          GOTO      NEXT50 IF EQUAL
.
NEXT30    MOVE      TADOCT,XNDOC
          DISPLAY   *P37:24,*V2LON,TADMN;
.
          MOVE      TURNO,DTURNO
          MOVE      TADMN,DTADMN
          MOVE      UNIQUE,DUNIQUE
          WRITE     TEMPSEQ,SEQ;PSNAME,PTITL,PGNAME,DTURNO,DTADMN,TDATE,TTIME:
                                XOWARD,XOBED,XNWARD,XNBED,XODOC,XNDOC,DUNIQUE
          ADD       ONE,UNIQUE
.
NEXT50    CALL      INTVAR1
          CALL      SAVOLD
          GOTO      NEXTTR
.
.         The first record of Trans file should be Admission Move
.
CHKTR     CALL      SAVOLD
          MATCH     SP1,OLDMOVE
          GOTO      NEXTTR IF NOT EQUAL
.
          CMATCH    "A",TMOVE
          GOTO      NEXTTR IF EQUAL
.
          DISPLAY   *P1:24,*B,*EL,*P5:24,"Adm. ",*V2LON,TADMN,*HOFF:
                    " has invalid Transfer File record.  PLEASE":
                    " CONTACT I.B.A.",*W2,*P1:24,*EL;
          RETURN
+
.         Initialize the variables
.
INTVAR    MOVE      SP3,OLDWARD
          MOVE      SP3,OLDBED
          MOVE      SP6,OLDDOCT
          MOVE      SP1,OLDMOVE
.
INTVAR1   MOVE      SP3,XOWARD
          MOVE      SP3,XOBED
          MOVE      SP6,XODOC
          MOVE      SP3,XNWARD
          MOVE      SP3,XNBED
          MOVE      SP6,XNDOC
          RETURN
+
.         Save old variables
.
SAVOLD    MOVE      TWARD,OLDWARD
          MOVE      TBED,OLDBED
          MOVE      TADOCT,OLDDOCT
          MOVE      TMOVE,OLDMOVE
          RETURN
+
.
NOMAST    DISPLAY   *P1:22,*EL,*B,"** Patient Master Record missing for ":
                    "Admission : ",*V2LON,AADMNO,*HOFF,".  CONTACT I.B.A. **":
                    *W3;
          RETURN
+
.
.        Create an indexed file based on Port
.         for accumulating Admission Type totals
.
INDZD     MOVE      ZERO,STATUS
.
.         CLEAR     FNAMEI
.         APPEND    "hos52a",FNAMEI
.         APPEND    PORT,FNAMEI
.         RESET     FNAMEI
.         REP       " 0" IN FNAMEI
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
.         check if previously exists...if so delete it 
.
          CALL      KILLIDZ
.
          PREP      TEMPSEQ,FNAMEI
.
.         CLEAR     FNAMEG
.         APPEND    "gndidx",FNAMEG
.         APPEND    PORT,FNAMEG
.         RESET     FNAMEG
.         REP       " 0" IN FNAMEG
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEG
.
          CALL      CREAIDG
          OPEN      GNDFILE,FNAMEG
          RETURN
+
.
.         Index the temporary file
.
SORTD     WEOF      TEMPSEQ,SEQ
          CLOSE     TEMPSEQ
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 114 ",CMDLINE
          APPEND    SORTSTR,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "isbload ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ONE,STATUS
          DISPLAY   *P1:22,*EF;
.
          MOVE      ZERO,OPCND
          BRANCH    STATUS OF SORTFI
          MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** The Temporary file is EMPTY **    ";
          CALL      HITENTER
          GOTO      ENDP   
.
NOOPEN    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary file ",FNAMEI," not found **    ";
          CALL      HITENTER
          RETURN
+
.
SORTFI    TRAP      NOOPEN IF IO
          OPEN	    TEMPFILE,FNAMEI
          TRAPCLR   IO
.
          COMPARE   ONE TO OPCND
          GOTO      ENDP IF EQUAL
.
          DISPLAY   *P1:24,*EL,*P27:24,*V2LON:
                    "***  Generating Report  ***",*W2;
          MOVE      "60",LNO
          MOVE      ZERO,PAGENO
          MOVE      ZERO,GNDPAT
.
          DISPLAY   *P1:24,*EL,*P27:24,"Admission Number : ";
.
          READ      TEMPFILE,SP50;;
NEXTMP    READKS    TEMPFILE;PSNAME,PTITL,PGNAME,DTURNO,DTADMN,TDATE,TTIME:
                             XOWARD,XOBED,XNWARD,XNBED,XODOC,XNDOC,DUNIQUE
          GOTO      ENDP IF OVER
          MOVE      PSNAME,PSNAMEDS
          MOVE      DTURNO,TURNO
          MOVE      DTADMN,TADMN
          MOVE      DUNIQUE,UNIQUE
.
          DISPLAY   *P47:24,*V2LON,TADMN;
.
.         Calculate number of patient
.
          MOVE      TADMN,KEY8
          READ      GNDFILE,KEY8;KEY8
          GOTO      SAMEP IF NOT OVER
          ADD       ONE,GNDPAT
          WRITE     GNDFILE,KEY8;KEY8
.
SAMEP     COMPARE   "55",LNO
          CALL      HEAD IF NOT LESS
.
          UNPACK    TDATE,XCENT,XYEAR,XMON,XDAY
.
          MOVE      PGNAME,XGNAME
          MOVE      SP20,DNAME1
          MOVE      SP20,DNAME2
.
          MOVE      XODOC,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF GETDOC2
          MOVE      DGNAME,ANS
          PACK      DNAME1 WITH ANS,DOT,DSNAME
.
GETDOC2   MOVE      XNDOC,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF PRTREP
          MOVE      DGNAME,ANS
          PACK      DNAME2 WITH ANS,DOT,DSNAME
.
.         PRINT REPORT
.
PRTREP    BRANCH    OPTION OF PRT10,PRT20,PRT30
.
PRT10     MOVE      TURNO,KEY8
          CALL      RDMAST1
.
          PRINT      *1,"|",PSNAMEDS,*22,PTITL,*27,XGNAME,*40,"| ",PSEX:
                    *44,"|",TURNO,*53,"|",TADMN:
                    *62,"|",XDAY,SLASH,XMON,SLASH,XYEAR,*71,"|",TTIME:
                    *80,"| ",XOWARD,*85,"|",XOBED,*89,"| ",XNWARD,*94,"|",XNBED:
                    *98,"|",DNAME1,*115,"|",DNAME2,*132,"|"
          GOTO      PRT40 
.
PRT20     MOVE      TURNO,KEY8
          CALL      RDMAST1
.
          PRINT      *1,"|",TURNO,*10,"|",TADMN:
                    *19,"|",XDAY,SLASH,XMON,SLASH,XYEAR,*28,"|",TTIME:
                    *37,"|",PSNAMEDS,*58,PTITL,*63,XGNAME,*76,"| ",PSEX:
                    *80,"| ",XOWARD,*85,"|",XOBED,*89,"| ",XNWARD,*94,"|",XNBED:
                    *98,"|",DNAME1,*115,"|",DNAME2,*132,"|"
          GOTO      PRT40 
.
PRT30     MOVE      TURNO,KEY8
          CALL      RDMAST1
.
          PRINT      *1,"|",XDAY,SLASH,XMON,SLASH,XYEAR,*10,"|",TTIME:
                    *19,"|",TURNO,*28,"|",TADMN,*37,"|",PSNAMEDS,*58,PTITL:
                    *63,XGNAME,*76,"| ",PSEX,*80,"| ",XOWARD,*85,"|",XOBED:
                    *89,"| ",XNWARD,*94,"|",XNBED,*98,"|",DNAME1:
                    *115,"|",DNAME2,*132,"|"
.
PRT40     ADD       ONE,LNO
          GOTO      NEXTMP
.
ENDP      COMPARE   ZERO,PAGENO
          GOTO      ENDP10 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,*P26:24:
                    "***  No Report Generated  ***",*W2;
          GOTO      ENDP20
.
ENDP10    CALL      PRTLN
          ADD       ONE,LNO
          COMPARE   "49",LNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *N,"Total Number of Patients           : ",GNDPAT:
                    *N,"Total Number of Ward/Bed Transfers : ",GNDWRDTR:
                    *N,"Total Number of Doctor Transfers   : ",GNDDOCTR:
                    *N,*N,*N:
                    *N,"FOOTNOTE  :  If Doctor and Ward were changed on the":
                       " one record":
                    *N,"             then 1 is added to both Transfer Totals.":
                    *N,*N,*N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "***  Report Generation Completed  ***",*W2;
.
ENDP20    CALL      KILLIDZ
          GOTO      START
.
.         Create the temporary to count number of patients
.
CREAIDG   CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEG,CMDLINE
          APPEND    " 10 u1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MOVE      ONE,STATUS
          RETURN  
+
.         Deleting an indexed file based on port
.
KILLIDZ   CLOSE     TEMPFILE
          CLOSE     GNDFILE
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEG,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TEMPSEQ,DELETE
          RETURN
+
.         HEADINGS FOR OUTPUT
.
HEAD      COMPARE   ZERO,PAGENO
          GOTO      HEAD100 IF EQUAL
          CALL      PRTLN
.
HEAD100   ADD       ONE,PAGENO
.
          BRANCH    OPTION OF HEAD200,HEAD300,HEAD400
.
HEAD200   MOVE      "(Surname Sequence)",CPHDROPT
          GOTO      HEAD500
.
HEAD300   MOVE      "(U/R Number Sequence)",CPHDROPT
          GOTO      HEAD500
.
HEAD400   MOVE      "(Date Sequence)",CPHDROPT
.
HEAD500   CALL      IBACLOCK
          CALL      HEAD132            
          PRINT     *N,*40,"From Date : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                         *68,"To Date : ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2,*N
.
          CALL      PRTLN
          BRANCH    OPTION OF HEAD600,HEAD700,HEAD800
.
HEAD600   PRINT     *1,"|",*40,"|",*44,"|",*53,"|",*62,"|",*71,"|",*80,"|  Old":
                   *89,"|  New",*98,"|",*102,"Old",*115,"|",*119,"New",*132,"|":
                    *N,*1,"| Patient Name",*40,"|Sex",*44,"| U/R No":
                      *53,"| Adm No",*62,"|  Date",*71,"|  Time":
                      *80,"|Ward|BED",*89,"|WARD|BED":
                      *98,"|Attending Doctor",*115,"|Attending Doctor",*132,"|"
          GOTO      HEAD900
.
HEAD700   PRINT        *1,"|",*10,"|",*19,"|",*28,"|",*37,"|",*76,"|":
                      *80,"|  Old",*89,"|  New",*98,"|",*102,"Old":
                     *115,"|",*119,"New",*132,"|":
                    *N,*1,"| U/R No",*10,"| Adm No",*19,"|  Date",*28,"|  Time":
                      *37,"| Patient Name",*76,"|Sex",*80,"|Ward|BED":
                      *89,"|WARD|BED",*98,"|Attending Doctor":
                     *115,"|Attending Doctor",*132,"|"
          GOTO      HEAD900
.
HEAD800   PRINT     *1,"|",*10,"|",*19,"|",*28,"|",*37,"|",*76,"|",*80,"|  Old":
                   *89,"|  New",*98,"|",*102,"Old",*115,"|",*119,"New",*132,"|":
                    *N,*1,"|  Date",*10,"|  Time",*19,"| U/R No",*28,"| Adm No":
                      *37,"| Patient Name",*76,"|Sex":
                      *80,"|Ward|BED",*89,"|WARD|BED":
                      *98,"|Attending Doctor",*115,"|Attending Doctor",*132,"|"
.
HEAD900   CALL      PRTLN
          MOVE      NINE,LNO
          RETURN
+
.
PRTLN     PRINT     *1,"*-------------------------------------------------":
                       "--------------------------------------------------":
                       "-------------------------------*"
          RETURN
+
.
ENDIT     CHAIN     PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
.
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
          INC       TFILENAM
+

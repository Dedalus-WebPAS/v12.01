.----------------------------------------------------------------------
. Program       : PATWEB86
.
. Function      : Uncoded Discharges Web Server
.
. Modifications : 
.----------------------------------------------------------------------
. V12.00.01 23/05/2025  Ebon Clements  TSK 0955096
.           Alphanueric visit number changes
. V11.04.01 01/12/2023  Alina Bhari    TSK 0940151
.           Redirected to correct coding template if selected visits
.           has incompleted codes - DISCON00                     
. V11.02.03 18/08/2022  Jill Parkinson TSK 0902663
.           Corrected setting of WAHEAFLG to be in TODP0000    
. V11.02.02 09/08/2022  Alina Bhari    TSK 0902663
.           Listed the patients in Uncoded discharge list only
.           when showflag is set to 1. 
.           SHOWFLAG, CHKGOBTN, TODP0000, PADI2200, PADI2300
. V11.02.01 27/07/2022  Alina Bhari    TSK 0902663
.           For wahealth uncoded discharge List added                  
.           discharge summary -DISCON00                                
.           Added MRTPDSIO &MRTPDSFD, DISSUMRY,MRTPDSA1                
. V11.00.02 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V11.00.01 03/09/2020  J.Tan           TSK 0894098
.           Removed checking for PTCNHDPS
. V10.14.01 27/05/2019  Ebon Clements   TSK 0873485
.           Changed uncoded discharge list keep alive to 100 from 2000 records
.           TODP0000
. V10.12.01 03/04/2018  Ania P TSK 0850150
.           Fixed compare of user's hopsital to hospital filter in
.           Uncoded Discharge/TOPD4000
. V10.11.01 08/09/2017  Ebon Clements  TSK 0818097
.           Added excluded newborn functionality -  EXNB0000
. V10.09.01 01/03/2017  Ebon Clements  TSK 0833313
.           Check bad debt after multi hospital test - TODP0000 & TOAP0000
. V10.08.01 09/08/2016  Davin          TSK 0321234
.           Recompiled for LSTALERT - added ALRTIMG4 for Chronic Alerts
. V10.06.04 16/07/2015  Peter Vela         CAR 315864
.           Changed current location filter for medical records for WAHealth
.           to DISCON00
. V10.06.03 16/07/2015  Peter Vela         CAR 315864
.           Added current location filter for medical records for WAHealth
.           in TODP4000
. V10.06.02 02/07/2015  Peter Vela        CAR 315864
.           Fixed unpack in MLOC0000
. V10.06.01 12/06/2015  Ebon Clements     CAR 317684
.           Removed invalid doctor web error from CONSLT00
. V10.05.01 21/10/2014  Peter Vela        CAR 299176
.           Added Health Fund Group filter to Uncoded Discharge List
. V10.04.01 16/12/2013  J.Tan             CAR 294612
.           Added LOOPCNTR to TODP0000 routine
. V10.03.07 25/11/2013  Patrick Adair     CAR 199297
.           Exclude Residential Admissions from lists NZ only
.           (UNCD0010 & TODP3000)
. V10.03.06 12/04/2012  Don Nguyen        CAR 263404
.           Fixed Theatre Complete output value in sorted list (added THTRCOMP)
. V10.03.05 15/03/2012  Ebon Clements     CAR 261488
.           Fixed MR current location display when no linked MR - DISCON20
. V10.03.04 08/03/2012  Ebon Clements     CAR 261108
.           Added display of discharge ward description - DISCON00
. V10.03.03 30/01/2012  Mike Laming       CAR 259106
.           Mods to use PTHSRCTY instead of MRCNRCTY in "Red" Hospital fields.
. V10.03.02 08/12/2011  Mike Laming       CAR 248696
.           More mods to Red or Black Hospital Id (Added Cat TY test)
. V10.03.01 18/11/2011  Mike Laming       CAR 254569
.           More mods to Hospital Id (with colours)
. V10.02.10 26.09.2011  Saroeun Soeur    CAR 240110
.            LSEL0100 - display active destinations
. V10.02.09 15/09/2011  Peter Vela       CAR 250433
.           Initialised URNUMBER and ADMISSNO @ TOAP0000 
.           14/09/2011  Mike Laming      CAR 248698
.           Change Uncoded Dischs to display Red Hosp.Id. in DISCON00 if needed
. V10.02.08 22/08/2011  Ebon Clements    CAR 241939
.           Mod to GEPS0000 to generate Phantom Episode on Discharge
. V10.02.07 17/08/2011  Mike Laming      CAR 241939
.           Mod to GEPS0000 to generate Phantom Episode on Discharge
. V10.02.06 10/08/2011  Ebon Clements    CAR 241939
.           Added episode coding functionality
. V10.02.05 06/07/2011  Mike Laming      CAR 240724
.           Mods to MRT Tables for Hospital Id. - WA Health changes
. V10.02.04 23/06/2011  Peter McMullen    CAR 240725
.           Change ward selection list to sort by name, not code            
. V10.02.03 17/05/2011  Mike Laming       CAR 240716
.           Mods to MRT Tracking sort order - created include MRTRKSEQ, see
.           CALLs GTSRTIND & MRSRTORD. Var SORTORDR replaces TERMDIGT
. V10.02.02 10/05/2011  Davin          CAR 239539 WA Health
.           Changed to allow 3 alert indicators to be displayed (LSTALR00)
. V10.02.01 28/03/2011  Mike Laming   CAR 240107                      
.           Mods to PATECDaf & PATECOaf variables & Keys - file change
. V10.00.03 11/10/2010  Jill Parkinson CAR 207094
.           Added coding date to DISCON00
. V10.00.02 02/07/2010  Mike Laming    CAR 225138
.           Added Theatre Complete (PMVXATCP) to Sort List in DISCON00      
. V10.00.01 17/05/2010  Jill Parkinson CAR 216245
.           Removed dummy LSTALR00 label and added LSTALERT include instead
. V9.12.03  10/03/2010  Mike Laming    CAR 216245
.           Add Alerts Column to Patient List at DISCON00 - uses module LSTALERT
.           (had to add PMSPX1FD, PMSPX1TM, PMSPX1TD & remove "DEFPROC GETALIDS)
. V9.12.02  22/12/2009  Jill Parkinson CAR 207094
.           Added outstanding discharge summary view
. V9.12.01  17/12/2009  Peter McMullen CAR 210130
.           Convert to newer Dr name format routine DOCNM000
. V9.09.02  11/09/2007  Ebon Clements  CAR 149466
.           Changed limit reached to 150 records in TODP0000
. V9.09.01  28/08/2007  Peter Vela     CAR 148631
.           Modified Terminal Digit view for CABRINI Health @ DISCON00
. V9.08.01  31/01/2007  Peter Vela     CAR 127930
.           Recompiled for MRTMASFD
. V9.05.02  17/03/2006  Ebon Clements CAR 89155
.           Mods for multi hospital medical record locations
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.04.03  25/07/2005  Jill Habasque CAR 62162
.           Added read of mrtvisaf in MLOC0000   
. V9.04.02  06/01/2005  Lina Vo       CAR 54667
.           Added check for hospital in WSEL0000
. V9.04.01  27/08/2004  Jill Habasque CAR 53046
.           Added check for hospital in TODP0000
.----------------------------------------------------------------------
. V9.03.06  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      
.           - Add Sex Description routine SEXDSC00 with Code "R" - "Intersex"
. V9.03.05  25/05/2004 Jill Habasque CAR 50050
.           Added health fund to uncoded discharge list
. V9.03.04  17/03/2004 Peter Vela                CAR 13038
.           Changed call DOCNAM00 to DOCNM000 (parameter driven doctor name
.           format) for sort lists.
. V9.03.03  11/12/2003 Peter Vela                CAR 40355
.           Recompiled for PATNAMCD
. V9.03.02  06/11/2003 Sylvek Litewka            CAR 44328
.           Recompiled for PATECDFD and PATECOFD changes.
. V9.03.01  16/06/2003 Pat Dirito                CAR 39213
.           Uncoded discharge (DISCON00)screen now uses PatientLink javascript 
.           rather than url 
.----------------------------------------------------------------------
. V9.02.12  06/03/2003 Ebon Clements             SRF 35797
.           Changed CKAD0000 to use the new admission file key to check for
.           a current admission
. V9.02.11  27/11/2002 Sylvek Litewka            SRF 33543
.           Added procedure CONSLT00 to display consultant's 
.           code and formatted name. This procedure uses PATDOCTM.PBL.
.           Modified NEXTGR00 and PREVGR00 to include "doctcode" as
.           part of the URL. 
. V9.02.10  28/06/2002 Ebon Clements             SRF 19105
.           Recompiled for sigpipe trap
. V9.02.09  26/06/2002 Pat Dirito                SRF 18511
.           Now checking for Bad Debts in TODP0000
. V9.02.08  20/06/2002 Pat Dirito                SRF 17437
.           Modified TODP0000 set RECCOUNT to 50 
. V9.02.07  25/03/2002 Pat Dirito                SRF 17437
.           Modified TODP0000 for NEXTPAGE parameter.               
. V9.02.06  06.03.2001 Pat Dirito   
.           Modified MLOC0000 no need to check History File.
. V9.02.05  29.11.2001 Pat Dirito   
.           CKAD0000 was still not working properly. Now fixed.
. V9.02.04  05.11.2001 Pat Dirito   
.           CKAD0000 was being called before Admission record
.           read, now being called after Admission record read!
. V9.02.03  10.10.2001 Ebon Clements
.           Fix selection list for option type in uncoded 
.           discharge list
. V9.02.02  03.10.2001 Jill Habasque
.           Commented out replace of apostrophies with space.
. V9.02.01  05.09.2001 J.Tan
.           Mods Uncoded discharge list not to display data
.           until the option field is selected.
. V9.00.B01 27.04.2001 Ebon Clements 
.           Fixed format of patient name in uncoded discharges
. V5.10.03 27.04.2001 Ebon Clements 
.          Fixed age calculation when unknown date of death
. V5.10.02 22.03.2001 B.G.Ackland
.          Limit Output to 50
. V5.10.01 19/03/2001 Bronko Sosic     
.          Changes GETPATOO to check for dead Patients          
. V5.09.04 18/01/2001 J.Tan            
.          A new option Current Inpatients for Uncoded Discharge
. V5.09.03 21.12.2000 Pat Dirito       
.          Set default Episode no to 1 in Uncoded Discharge List
.          Recompiled IBAWEBCD
. V5.09.02 20.12.2000 Bronko Sosic     
.          Recompiled for MRTHISFD   
. V5.08.03 13/10/2000  J.Tan
.          Added report 2
. V5.08.02 16/08/2000  Caleb Sharman
.          Changed Health Fund variables to be 8 chars
. V5.08.01 25.07.2000 B.G.Ackland 
.          Fix Patient Name Output
. V5.07.04 28.01.2000 B.G.Ackland 
.          Recompiled for New Standards
. V5.07.03 20.12.1999 K.C.Nelson 
.          Recompiled for IBAWEBDF/IBAWEBCD           
. V5.07.02 07.12.1999 K.C.Nelson 
.          Added tag to display default doctor code 
. V5.07.01 03.12.1999 K.C.Nelson 
.          Added Admitting Diagnosis to addRow output
. V5.07.00 13.08.1999 K.C.Nelson 
.          Created Program
.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
          INC       PMSPX2TD/INC                                      *I-216245
          INC       PMSPX2FD/INC                                      *I-216245
.
          INC       OBSPCOFD/INC
          INC       OBSCNAFD/INC
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       PATCHCFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATECOFD/INC
          INC       PATFN1FD/INC
          INC       PATHGPFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSVX1FD/INC
          INC       PATWR1FD/INC
          INC       MRTCONFD/INC
          INC       MRTLOCFD/INC
          INC       MRTMASFD/INC
          INC       MRTVISFD/INC
          INC       MRTHISFD/INC
          INC       MRTPDSFD/INC   * Post Discharge Header
.
ADMDATE   DIM       11
ADMISTYP  DIM       3
ALRTLINK  DIM       127                                               *I-216245
ALRTIMGE  DIM       127                                               *I-216245
ALRTIMG2  DIM       127
ALRTIMG3  DIM       127
ALRTIMG4  DIM       127
ALRTIMGT  DIM       250
CHCDATA   DIM       30
CHKGOBTN  DIM       1                  * Check go button
CODINGDT  DIM       8
COLDAT01  DIM       30
COLDAT02  DIM       30
COLDAT03  DIM       30
COLDAT04  DIM       30
COLDAT05  DIM       30
COLDAT06  DIM       30
CLAIMCOD  DIM       3
CURRADMN  DIM       1
DAYTIME   DIM       3
DIM2A     DIM       2
DIM2B     DIM       2
DIM2C     DIM       2
DIM4      DIM       4
DIM5      DIM       5
DISPCART  DIM       20
DISSUMRY  DIM       20     *Discharge summary description for cat cp
DOCTCODE  DIM       6   
DSUMMARY  DIM       1   
DISCDOCT  DIM       6   
DSCDATE   DIM       12
EPSADATT  DIM       25
EPSCD001  FORM      2                       * Episode No (CGI Parameter)
EPSDDATT  DIM       25
EPSNUMBR  DIM       2       * Episode Number
EPISADAT  DIM       8
EPISATIM  DIM       8
EPISDDAT  DIM       8
EPISDTIM  DIM       8
FUNDCODE  DIM       6
FGRPCODE  DIM       6
GENCHCDS  FORM      1
GENITNOW  FORM      1
KEY8A     DIM       8
LOOPCNTR  FORM      4
LINKPRG2  DIM       8
LINKREP2  DIM       2
LINKTEM2  DIM       3
LNK2PRT   FORM      1
MRLOCNCD  DIM       4                                                 *C-240724
MRHOSPCD  DIM       3                                                 *I-240724
MRLOCATN  DIM       4
MRHOSPIT  DIM       3                                                 *I-240724
MRHOHOSP  DIM       3                                                 *I-240724
NEWTITLE  DIM       30
PATLINK   DIM       127     * Link to next patient info
RECCOUNT  FORM      3       * Counter
UNITCODE  DIM       3
URTDNO    DIM       8
UNCODKEY  DIM       16      * Next Record Key
WARDCODE  DIM       3
TITLE01   INIT      "Patient Discharges"
SAVEPKEY  DIM       24
SHOWFLAG  FORM      1                       * Go button 
SORTORDN  DIM       1                       * MR Tracking sort order indicator
SORTORDR  DIM       8                       * MR Tracking sort order value
T1A       DIM       1
T2A       DIM       2
T2B       DIM       2
T3A       DIM       3
T4A       DIM       4
T5A       DIM       5
THTRCOMP  DIM       6                   * Theatre Complete label
WACLFILT  DIM       1
WAHEAFLG  DIM       1                   * Wahealth Flag
.
.
CBB       INIT      ")"
OBB       INIT      "("
OBR       INIT      "<font color=red>("
CBR       INIT      ")</font>"
CATWI     INIT      "wi"
CATTY     INIT      "TY"
CODEA     INIT      "A "
CODEAC    INIT      "AC"
CODECL    INIT      "CL"
.-----------------------------------------------------------------------
PRGID     INIT      "PATWEB86"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Uncoded Discharges Web Server"
.-----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          CALL      GTSRTIND       * get MRT Tracking sort order indicator
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  MOVE      "Uncoded Discharges",WEBTITLE
          CALL      WEBHED00    * Heading
          BRANCH    EXIT,MAIN1000
          CALL      WEBBOD00
          CALL      WEBEND00    * End of Page
          GOTO      MAIN1000
.
MAIN1200  CALL      DSCLST00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Uncoded Discharges
.------------------------------------------------------------
UNCD0000  UNPACK    DIM127,DIM1,LINKPRG,DIM127    
          UNPACK    DIM127,DIM1,LINKREP,DIM127    
          UNPACK    DIM127,DIM1,LINKTEM,DIM127    
.
          MATCH     SP70,DOCTCODE
          IF        @EQUAL
            MOVE      WBSEDOC,DOCTCODE 
          ENDIF
.
          MATCH     SP70,DOCTCODE
          GOTO      UNCD9999 IF EQUAL
.
          IF        NORECORD=0
            MOVE      "50",NORECORD         * Set default no records to display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY16,SP70
          CALL      RDSMISS4
UNCD0010  CALL      RDKMISS4
          BRANCH    OVRCD,UNCD9999 
          MATCH     SP70,ACODDTE            * check coding date blank
          GOTO      UNCD9999 IF NOT EQUAL
.
          MATCH     "3",DASTAT            * check discharge status=discharged
          GOTO      UNCD0010 IF NOT EQUAL
.                                         * Ignore Residential Care NZ only
.         IF        PTCNHDPS<>1
.           GOTO      UNDC0100
.         ENDIF
.
          MATCH     "   ",ACLSS
          GOTO      UNDC0100 IF EQUAL
.
          PACK      KEY5,ANSP,SP1,ACLSS,SP20
          CALL      RDCODE1
          BRANCH    OVRCD,UNDC0100
.
          MATCH     ANSR,TCINDC7
          GOTO      UNCD0010 IF EQUAL
.
UNDC0100  MATCH     DOCTCODE,ADOCTA
          GOTO      UNCD0010 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      UNCD0010
          ENDIF
.
          CALL      ADMDAT00                * Admission date
          CALL      DSCDAT00                * Discharged date
.
          CALL      UCROW000                * Uncoded discarge Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      UNCD0010 IF NOT EQUAL
.
UNCD9999  RETURN
.------------------------------------------------------------
. Output Table Row for uncoded discharge patient 
.------------------------------------------------------------
UCROW000  PACK      KEY8,AURNO              * get master details
          CALL      RDMAST1 
          IF        OVRCD=0
            CALL      PATNAA00                * Format patient name
            MOVE      AURNO,URNUMBER
            MOVE      AADMNO,ADMISSNO
          ENDIF 
.
          PACK      KEY8,AADMNO               * get discharge details
          CALL      RDDSCH1
.
          MOVE      SP70,DOCFNAME
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE            * I CAR 13038
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000                  * end I CAR 13038
          ENDIF
.
          REP       " +",URNUMBER
          REP       " +",ADMISSNO
          REP       " 0",ADATE
          REP       " 0",DDATE
.
          WRITE      HTMLFILE;"t.addRow(#"",LINKPRG,".pbl?reportno=",LINKREP:
                              "&template=",LINKTEM,"&urnumber=",URNUMBER:
                              "&admissno=",ADMISSNO,"#",#"",PATFNAME:
                              "#",#"",DOCFNAME,"#",#"",ADATE,"#",#"",DDATE:
                              "#",#"",ADIAG1,SP1,ADIAG2,"#")"
.
UCROW999  RETURN
.------------------------------------------------------------
.  Format admission date
.------------------------------------------------------------
ADMDAT00  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL:
                               AUG,SEP,OCT,NOV,DEC
          PACK      ADMDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR 
ADMDAT99  RETURN
.------------------------------------------------------------
.  Format discharge date
.------------------------------------------------------------
DSCDAT00  UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL:
                               AUG,SEP,OCT,NOV,DEC
          PACK      DSCDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR 
DSCDAT99  RETURN
.
.------------------------------------------------------------
. Discharge Time
.------------------------------------------------------------
DISTIM00  UNPACK    DTIME,DIM2,DIM1,DIM2A
          MOVE      DIM2,F2
          IF        F2>=12
            MOVE      "pm",DIM2B
            IF        F2>=13
              ASSIGN    F2-12,F2
              MOVE      F2,DIM2
            ENDIF
          ELSE
            MOVE      "am",DIM2B 
            MOVE      F2,DIM2
          ENDIF
.
DISTIM99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      PATDO1A1,"patdo1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A4,"patmi1af"
          OPEN      PATMI1A8,"patmi1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PMSPX2A1,"pmspx2af"                               *I-216245
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
          OPEN      PATECDA1,"patecdaf"
          OPEN      PATECOA1,"patecoaf"
          OPEN      PATCHCO1,"patchcof"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATHGRP1,"pathgrpf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATWR1A7,"patwr1af"
          OPEN      PATTRAN2,"pattranf"
          OPEN      MRTLOCA1,"mrtlocaf"
          OPEN      MRTMASA1,"mrtmasaf"
          OPEN      MRTMASA2,"mrtmasaf"
          OPEN      MRTHISA1,"mrthisaf"
          OPEN      MRTVISA1,"mrtvisaf"
          OPEN      OBSCNAA1,"obscnaaf"
          OPEN      OBSPCOA2,"obspcoaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS        * Multi Hosp Indicator
          READ      CONTROLF,FIFTY9;*34,CTYPEUR
          READ      CONTROLF,SIXTY;*133,MRCNEPSC
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
          OPEN      MRTPDSA1,"mrtpdsaf"
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,DOCTCODE
          MOVE      ZERO,VIEWTYPE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,WARDCODE
          MOVE      SP70,UNITCODE
          MOVE      SP70,CLAIMCOD
          MOVE      SP70,FUNDCODE
          MOVE      SP70,ADMISTYP
          MOVE      SP70,MRHOSPCD                                     *I-240724
          MOVE      SP70,MRLOCNCD
          MOVE      SP70,CURRADMN
          MOVE      SP70,UNCODKEY
          MOVE      SP70,DISCDOCT
          MOVE      ZERO,EPSCD001
          MOVE      SP70,FGRPCODE
          MOVE      SP70,DISSUMRY
          MOVE      SP70,WAHEAFLG
          MOVE      ZERO,SHOWFLAG
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "doctcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DOCTCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "discdoct=",QRYNAME
          IF        @EQUAL
            PACK      DISCDOCT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wardcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WARDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "unitcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UNITCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "claimcod=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CLAIMCOD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fundcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FUNDCODE
            PACK      FUNDCODE,FUNDCODE,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fgrpcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FGRPCODE
            PACK      FGRPCODE,FGRPCODE,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admistyp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISTYP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mrhospcd=",QRYNAME                               *I-240724
          IF        @EQUAL
            MOVE      QRYDATA,MRHOSPCD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mrlocncd=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,MRLOCNCD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "curradmn=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,CURRADMN
            PACK      CURRADMN,CURRADMN,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "uncodkey=",QRYNAME
          IF        @EQUAL
            PACK      UNCODKEY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "epscd001=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EPSCD001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "showflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SHOWFLAG
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Discharges by Date
.------------------------------------------------------------
DSCLST00  MOVE      "Uncoded Discharged Patient List",WEBTITLE
          CALL      CURPER00   
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,DSCLST99
.
          CALL      WEBBOD00   * Process Web Body
.
          CALL      WEBEND00   * Process End of HTML File
.
DSCLST99  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13 
          GOTO      PROFLD99
.
PROFLD11  CALL      UNCOD000           
          GOTO      PROFLD99
.
PROFLD12  CALL      MRUCD000           
          GOTO      PROFLD99
.
PROFLD13  CALL      CONSLT00
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21
          GOTO      PROFLD99
.
PROFLD21  CALL      PADI0000         * Patient discharges
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
UNCOD000  BRANCH    FLDITMNO,UNCOD010,UNCOD020,UNCOD030,UNCOD040
          GOTO      UNCOD999
.
UNCOD010  CALL      UNCD0000        * Table of Uncoded Discharges by Consultant 
          GOTO      UNCOD999
.
UNCOD020  CALL      NEXTGR00        * link to next group          
          GOTO      UNCOD999
.
UNCOD030  CALL      PREVGR00        * link to previous group      
          GOTO      UNCOD999
.
UNCOD040  CALL      CONSUL00        * Consultant
          GOTO      UNCOD999
.
UNCOD999  RETURN
.------------------------------------------------------------
MRUCD000  BRANCH    FLDITMNO,MRUCD010,MRUCD020,MRUCD030,MRUCD040:
                             MRUCD050,MRUCD060
          GOTO      MRUCD999
.
MRUCD010  CALL      CURPER00        * Determine Current Period 
          CALL      SELV0000        * Selection List of Dates 
          GOTO      MRUCD999
.
MRUCD020  CALL      NEXT0000  
          GOTO      MRUCD999
.
MRUCD030  CALL      PREV0000  
          GOTO      MRUCD999
.
MRUCD040  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      MRUCD999
.
MRUCD050  CALL      WSEL0000
          GOTO      MRUCD999
.
MRUCD060  MOVE      CODECL,CATEGORY
          MOVE      SP70,CODE
          CALL      CODSEL00
          GOTO      MRUCD999
.
MRUCD999  RETURN
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NEXT0000  MOVE      VIEWTYPE,F1
          BRANCH    F1,NEXT0100,NEXT0200
          WRITE     HTMLFILE;"SelectNextDay();";
          GOTO      NEXT9999
NEXT0100  WRITE     HTMLFILE;"SelectNextWeek();";
          GOTO      NEXT9999
NEXT0200  WRITE     HTMLFILE;"SelectNextMonth();";
          GOTO      NEXT9999
NEXT9999  RETURN
.------------------------------------------------------------
. Last Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      VIEWTYPE,F1
          BRANCH    F1,PREV0100,PREV0200
          WRITE     HTMLFILE;"SelectLastDay();";
          GOTO      PREV9999
PREV0100  WRITE     HTMLFILE;"SelectLastWeek();";
          GOTO      PREV9999
PREV0200  WRITE     HTMLFILE;"SelectLastMonth();";
          GOTO      PREV9999
PREV9999  RETURN
.
.------------------------------------------------------------
. Ward selection
.------------------------------------------------------------
WSEL0000  PACK      KEY29,SP70                                                                                                                     
          IF        IBCNMHOS = 1                                                                                                                   
            PACK      KEY29,SP3,WBSEHOSP,SP70                                                                                                      
          ENDIF                                                                                                                                    
          CALL      RDSWARD7                                                                                                                       
WSEL0100  CALL      RDKWARD7                                                                                                                       
          BRANCH    OVRCD,WSEL9999                                                                                                                 
.                                                                                                                                                  
.                         if we have a bed number we have passed all the ward                                                                      
.                         master records, so exit                                                                                                           
          MATCH     SP70,WBED                                                                                                                      
          GOTO      WSEL9999 IF NOT EQUAL                                                                                                          
.                                                                                                                                                  
          COMPARE   WACTIVE,ZERO                 *List only active wards                                                                           
          GOTO      WSEL0100 IF NOT EQUAL                                                                                                          
.                                                                                                                                                  
          IF        IBCNMHOS = 1                                                                                                                   
            MATCH      SP3,WBSEHOSP                                                                                                                
            IF        !@EQUAL                                                                                                                      
              MATCH     WBSEHOSP,PTWDHOSN                                                                                                          
              GOTO      WSEL9999 IF NOT EQUAL                                                                                                      
            ENDIF                                                                                                                                  
          ENDIF                                                                                                                                    
.                                                                                                                                                  
.                       write the select option.                                                                                                   
          WRITE     HTMLFILE;"<option value=#"",WWARD,"#"";                                                                                        
          MATCH     WARDCODE,WWARD
          IF        @EQUAL                                                                                                                         
            WRITE     HTMLFILE;" selected";                                                                                                        
          ENDIF                                                                                                                                    
          WRITE     HTMLFILE;">",WBDESC;
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        @EQUAL
              WRITE     HTMLFILE;" - ",PTWDHOSN;
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</option>"
          GOTO      WSEL0100                                                                                                                       
.                                                                                                                                                  
WSEL9999  RETURN
.
.------------------------------------------------------------
. Medical record location selection
.------------------------------------------------------------
LSEL0000  PACK      KEY7,SP70                                         *I-240724
          CALL      RSMRLOC1
LSEL0100  CALL      RKMRLOC1
          BRANCH    OVRCD,LSEL9999
.
          COMPARE   "1",MRLOSTAT
          GOTO      LSEL0100 IF EQUAL
.
          PACK      KEY6,QUOTE,MRLOCODE,QUOTE
          MATCH     MRLOCNCD,MRLOCODE
          IF        @EQUAL
            MATCH     SP3,MRHOSPCD
            GOTO      LSEL0101 IF NOT EQUAL
            WRITE     HTMLFILE;"<option value=",KEY6," selected>":
                               MRLODESC," (",MRLOCODE,")</option>";
          ELSE
LSEL0101    IF        IBCNMHOS=1
              MATCH     WBSEHOSP,MRLOHOSP
              GOTO      LSEL0100 IF NOT EQUAL
            ENDIF
            WRITE     HTMLFILE;"<option value=",KEY6,">",MRLODESC," (",MRLOCODE:
                               ")</option>";
          ENDIF
          GOTO      LSEL0100
.
LSEL9999  RETURN
.------------------------------------------------------------
. Current admission selection
.------------------------------------------------------------
LCUR0000  MATCH     "1",CURRADMN
          IF        @EQUAL
            WRITE     HTMLFILE;"<option>","":
                                 "</option>";
            WRITE     HTMLFILE;"<option value=1 selected>":
                               "Discharged","</option>";
            WRITE     HTMLFILE;"<option value=2>","Current Inpatients":
                               "</option>";
          ELSE
            MATCH     "2",CURRADMN
            IF        @EQUAL
              WRITE     HTMLFILE;"<option>","":
                                 "</option>";
              WRITE     HTMLFILE;"<option value=1>","Discharged":
                               "</option>";
              WRITE     HTMLFILE;"<option value=2 selected>":
                               "Current Inpatients","</option>";
            ELSE
              WRITE     HTMLFILE;"<option>","":
                                 "</option>";
              WRITE     HTMLFILE;"<option value=1>","Discharged":
                                 "</option>";
              WRITE     HTMLFILE;"<option value=2>","Current Inpatients":
                                 "</option>";
            ENDIF
          ENDIF
.
LCUR9999  RETURN
.
.------------------------------------------------------------
.  Link to next group of records
.------------------------------------------------------------
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          MOVE      DOCTCODE,KEY6
          REP       " +",KEY6
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"patweb86.pbl?":
                             "reportno=",F1:
                             "&template=",FLDPARA:
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&doctcode=",KEY6;
.
NEXTGR99  RETURN
.------------------------------------------------------------
.  Link to next group of records
.------------------------------------------------------------
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          MOVE      DOCTCODE,KEY6
          REP       " +",KEY6
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"patweb86.pbl?":
                             "reportno=",F1:
                             "&template=",FLDPARA:
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&doctcode=",KEY6;
PREVGR99  RETURN
.------------------------------------------------------------
. Consultant (doctor Code)
.------------------------------------------------------------
CONSUL00  MATCH     SP70,DOCTCODE
          GOTO      CONSUL50 IF EQUAL
.
          MOVE      DOCTCODE,KEY6
          CALL      RDDOCT1    
          BRANCH    OVRCD,CONSUL90
          GOTO      CONSUL80
.       
CONSUL50  MOVE      WBSEDOC,KEY6
          CALL      RDDOCT1    
          BRANCH    OVRCD,CONSUL90
.
CONSUL80  WRITE     HTMLFILE;DCODE;      * doctor code
          GOTO      CONSUL99
.
CONSUL90  MOVE      "Invalid Doctor Code",WEBTITLE
          MOVE      ONE,EXIT
          CALL      WEBERR00
. 
CONSUL99  RETURN  
.------------------------------------------------------------
. Consultant using PATDOCTM.PBL
.------------------------------------------------------------
CONSLT00  MATCH     SP70,DOCTCODE
          IF        @EQUAL
            MOVE      WBSEDOC,DOCTCODE
          ENDIF
.
          MOVE      DOCTCODE,KEY6
          CALL      RDDOCT1
          IF        OVRCD=1
            CALL      CLPATDOC
          ENDIF
.
          CALL      DOCF0000
          GOTO      CONSLT99
.
CONSLT99  RETURN
.------------------------------------------------------------
. Format Patient Name in Title Text
.------------------------------------------------------------
PATNAA00  CLEAR     DISPNAME
          MOVE      SP70,PATFNAME
          PACK      NAME35,PSNAME,SP70
          REP       UPPLOW,NAME35
          APPEND    NAME35,DISPNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN             
.------------------------------------------------------------
. Patient Discharges 
.------------------------------------------------------------
PADI0000  BRANCH    FLDITMNO,PADI0100,PADI0200,PADI0300,PADI0400:
                             PADI0500,PADI0600,PADI0700,PADI0800:
                             PADI0900,PADI1000,PADI1100,PADI1200:
                             PADI1300,PADI1400,PADI1500,PADI1600:
                             PADI1700,PADI1800,PADI1900,PADI2000:
                             PADI2100,PADI2200,PADI2300
.
PADI0100  CALL      CURPER00        * Determine Current Period 
          CALL      SELV0000
          GOTO      PADI9999
.
PADI0200  CALL      NEXT0000  
          GOTO      PADI9999
.
PADI0300  CALL      PREV0000  
          GOTO      PADI9999
.
PADI0400  CALL      CUREND00
          GOTO      PADI9999
.
PADI0500  CALL      CURYR000
          GOTO      PADI9999
.
PADI0600  CALL      CURMON00
          GOTO      PADI9999
.
PADI0700  MOVE      "0",WACLFILT
          CALL      TODP0000            * table for patient discharges
          IF        MRCNEPSC=1
            CALL      TOAP0000          * table of admitted patients
          ENDIF
          GOTO      PADI9999
.
PADI0800  WRITE     HTMLFILE;TEMPLATE;
          GOTO      PADI9999
.
PADI0900  WRITE     HTMLFILE;REPORTNO;
          GOTO      PADI9999
.
PADI1000  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      PADI9999
.
PADI1100  MOVE      CODEAC,CATEGORY
          MOVE      UNITCODE,CODE
          CALL      CODSEL00
          GOTO      PADI9999
.
PADI1200  MOVE      CODECL,CATEGORY
          MOVE      CLAIMCOD,CODE
          CALL      CODSEL00
          GOTO      PADI9999
.
PADI1300  MOVE      CODEA,CATEGORY
          MOVE      ADMISTYP,CODE
          CALL      CODSEL00
          GOTO      PADI9999
.
PADI1400  CALL      WSEL0000       * Ward selection
          GOTO      PADI9999
.
PADI1500  CALL      LSEL0000       * Medical Record Location selection
          GOTO      PADI9999
.
PADI1600  CALL      LCUR0000       * Current admission selection
          GOTO      PADI9999
.
PADI1700  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,PADI1710
          WRITE     HTMLFILE;FUNDCODE;
          GOTO      PADI9999
.
PADI1710  PACK      KEY14,FUNDCODE,ZERO,ZERO,ZERO,ZERO,SP10
          CALL      RDFUND1
          IF        OVRCD=0
            WRITE     HTMLFILE;HFNAME;
          ELSE
            WRITE     HTMLFILE;FUNDCODE;
          ENDIF
          GOTO      PADI9999
.
PADI1800  MOVE      DISCDOCT,DOCTCODE
          CALL      DOCDET00
          GOTO      PADI9999
.
PADI1900  WRITE     HTMLFILE;EPSCD001;           * Episode Number
          GOTO      PADI9999
.
PADI2000  CALL      HFGSEL00
          GOTO      PADI9999
.
PADI2100  MOVE      "1",WACLFILT
          CALL      TODP0000            * table for patient discharges
          IF        MRCNEPSC=1
            CALL      TOAP0000          * table of admitted patients
          ENDIF
          GOTO      PADI9999
.
PADI2200  MOVE      "0",WACLFILT
          CALL      TODP0000            * table for patient discharges
          IF        MRCNEPSC=1 & SHOWFLAG = 1
            CALL      TOAP0000          * table of admitted patients
          ENDIF
          GOTO      PADI9999
.
PADI2300  WRITE     HTMLFILE;SHOWFLAG;
          GOTO      PADI9999
.
PADI9999  RETURN
.------------------------------------------------------------
. Table of Patient Discharges 
.------------------------------------------------------------
TODP0000  MOVE      ZERO,DSUMMARY
          MOVE      ZERO,LOOPCNTR
          MOVE      ZERO,CHKGOBTN
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          UNPACK    DIM127,D1,LINKPRG2,DIM127
          UNPACK    DIM127,D1,LINKREP2,DIM127
          UNPACK    DIM127,D1,LINKTEM2,DIM127
          UNPACK    DIM127,D1,DSUMMARY,DIM127
          UNPACK    DIM127,D1,CHKGOBTN,DIM127
          UNPACK    DIM127,D1,WAHEAFLG,DIM127
          MOVE      ZERO,RECCOUNT            * Record counter
.
          MATCH     SP70,CURRADMN
          GOTO      TODP9999 IF EQUAL
.
          MATCH     "1",CHKGOBTN
          IF        @EQUAL
            COMPARE   ONE,SHOWFLAG
            GOTO      TODP9999 IF NOT EQUAL  * go button press
          ENDIF
.
          MATCH     SP70,UNCODKEY      * Next Group of Records Key!!
          IF        !@EQUAL
            PACK      KEY16,UNCODKEY,SP70
            CALL      RDSDSCH2
            BRANCH    OVRCD,TODP9999
            GOTO      TODP2000
          ENDIF
.
          PACK      KEY16,FRSTDATE,SP70 
          CALL      RDSDSCH2
TODP1000  CALL      RDKDSCH2
          BRANCH    OVRCD,TODP9999
.
TODP2000  MATCH     DDATE,LASTDATE
          GOTO      TODP9999 IF LESS
.
.  Write record number in a comment to keep apache listening if no matching
.  records are found per 100 records
.
          ADD       ONE,LOOPCNTR
          IF        (LOOPCNTR > 100)
            WRITE     HTMLFILE;"// ",LOOPCNTR
            MOVE      ZERO,LOOPCNTR
          ENDIF
.
          CMATCH    "2",CURRADMN
          IF        @EQUAL
            CALL      CKAD0000                * Check if currently admitted
            BRANCH    EXIT,TODP1000
          ENDIF
.
          PACK      KEY8,DADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,TODP1000            * missing visit extension record
.
          IF        IBCNMHOS<>1
            GOTO      TODP3000
          ENDIF
.
          MATCH     SP70,WBSEHOSP     
          GOTO      TODP3000 IF EQUAL
.
          MATCH     WBSEHOSP,PMVXMHOS
          GOTO      TODP1000 IF NOT EQUAL
.
TODP3000  PACK      KEY8,DADMNO,SP70
          CALL      RDVISA1
          BRANCH    OVRCD,TODP1000            * missing visit record
.
          MATCH     "00000000",PVIDATE        * Bad Debt? If so Ignore!
          GOTO      TODP1000 IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,TODP1000            * missing admission record
.
          CALL      EXNB0000                  * Excluded newborn
          BRANCH    EXIT,TODP1000
.                                         * Ignore Residential Care NZ only
.         IF        PTCNHDPS<>1
.           GOTO      TODP3500
.         ENDIF
.
          MATCH     "   ",ACLSS
          GOTO      TODP3500 IF EQUAL
.
          PACK      KEY5,ANSP,SP1,ACLSS,SP20
          CALL      RDCODE1
          BRANCH    OVRCD,TODP3500
.
          MATCH     ANSR,TCINDC7
          GOTO      TODP1000 IF EQUAL
.
TODP3500  MOVE      ONE,EPSCD001            * Episode number?
          MOVE      SP70,SAVEPKEY           * Clear save episode key
          MOVE      ZERO,GENCHCDS           * flag to generate phantom patchcof
          MOVE      ZERO,GENITNOW           * don't generate a patchcof yet
          IF        ASTAT = 3 & MRCNEPSC = 1
            MOVE      ONE,GENCHCDS          * set "Generate Discharge patchcof"
          ENDIF
          MOVE      " 0",KEY2
          PACK      CHCDATA,DADMNO,ADATE,ATIME,KEY2,ACARE,SP30
.
          MOVE      "CC",CATEGORY
          MOVE      ACARE,CODE                * Get care type description
          CALL      GETCOD00
          MOVE      TDESC,DISPCART
.
TODP4000  MATCH     "1",DSUMMARY
          IF        @EQUAL
            CALL      CHKDS000                * Check for disch summary
            BRANCH    F1,TODP1000
          ELSE
            IF        MRCNEPSC=1
              CALL      GEPS0000              * Get episode dates
              BRANCH    EXIT,TODP1000
.
              MATCH     SP70,CHCODCMP         * Check Coding Complete Date Blank
              GOTO      TODP1000 IF NOT EQUAL * for this episode 
            ELSE
              MATCH     SP70,ACODDTE          * Check Coding Complate Date Blank
              GOTO      TODP1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     SP3,CLAIMCOD
          IF        !@EQUAL
            MATCH     ACLAIM,CLAIMCOD         * claim type
            GOTO      TODP1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP3,FUNDCODE
          IF        !@EQUAL
            MATCH     AFUNDH,FUNDCODE         * health fund
            GOTO      TODP1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FGRPCODE
          IF        !@EQUAL
            PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP70
            CALL      RDFUND1
            BRANCH    OVRCD,TODP1000
.
            MATCH     FGRPCODE,PTHFHGRP
            GOTO      TODP1000 IF NOT EQUAL
.
          ENDIF
.
          MATCH     SP6,DISCDOCT
          IF        !@EQUAL
            MATCH     ADOCTA,DISCDOCT         * Attending doctor(discharge doc)
            GOTO      TODP1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP3,ADMISTYP
          IF        !@EQUAL
            MATCH     ATYPE,ADMISTYP          * admission type
            GOTO      TODP1000 IF NOT EQUAL
          ENDIF
.
          CALL      MLOC0000                  * Get medical record location
.
          MATCH     SP70,MRLOCNCD
          IF        !@EQUAL
            MATCH     MRHOSPIT,WBSEHOSP       * same hospital? 
            GOTO      TODP1000 IF NOT EQUAL 
            MATCH     MRLOCATN,MRLOCNCD       * same record location ?
            GOTO      TODP1000 IF NOT EQUAL
          ENDIF
.
          CALL      GTRN0000                  * get ward & unit from trans file
          MATCH     SP3,UNITCODE
          IF        !@EQUAL
            MATCH     TDEPT,UNITCODE          * check department
            GOTO      TODP1000 IF NOT EQUAL
          ENDIF
          MATCH     SP3,WARDCODE
          IF        !@EQUAL
            MATCH     TWARD,WARDCODE          * check discharge ward
            GOTO      TODP1000 IF NOT EQUAL
          ENDIF
.
          CALL      DSCDAT00
          CALL      ADMDAT00
.
          PACK      URNUMBER,AURNO 
          PACK      ADMISSNO,AADMNO
.
.   Set nextpage button if set page limit reached?
.
          ADD       ONE,RECCOUNT          * Increment Record Counter
          IF        RECCOUNT>150
            PACK      KEY16,DDATE,DADMNO,SP70
            WRITE     HTMLFILE;"NextRecordSet('",KEY16,"');"
            GOTO      TODP9999
          ENDIF
.
          CALL      DISCON00              * display discharge content
.
          IF        MRCNEPSC=1
            GOTO      TODP4000            * Get next episode for this discharge
          ELSE
            GOTO      TODP1000            * Get next discharge       
          ENDIF
.
TODP9999  RETURN
.
.------------------------------------------------------------
. Table of Patient Admissions with care type changes 
.------------------------------------------------------------
TOAP0000  MATCH     "2",CURRADMN
          GOTO      TOAP9999 IF NOT EQUAL
.
          PACK      KEY9,TWO,SP70
          CALL      RDSMISS2
TOAP1000  CALL      RDKMISS2
          BRANCH    OVRCD,TOAP9999
.
          COMPARE   TWO,ASTAT                 * Current admissions only
          GOTO      TOAP9999 IF NOT EQUAL
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,TOAP1000            * missing visit extension record
.
          IF        IBCNMHOS<>1
            GOTO      TOAP3000
          ENDIF
.
          MATCH     SP70,WBSEHOSP
          GOTO      TOAP3000 IF EQUAL
.
          MATCH     WBSEHOSP,PMVXMHOS
          GOTO      TOAP1000 IF NOT EQUAL
.
TOAP3000  PACK      KEY8,AADMNO,SP70
          CALL      RDVISA1
          BRANCH    OVRCD,TOAP1000            * missing visit record
.
          MATCH     "00000000",PVIDATE        * Bad Debt? If so Ignore!
          GOTO      TOAP1000 IF EQUAL
.
          CALL      EXNB0000                  * Excluded newborn
          BRANCH    EXIT,TOAP1000
.
          MOVE      ONE,EPSCD001            * Episode number?
          MOVE      SP70,SAVEPKEY           * Clear save episode key
          MOVE      ZERO,GENCHCDS           * flag to generate phantom patchcof
          MOVE      ZERO,GENITNOW           * don't generate a patchcof yet
          IF        ASTAT = 3 & MRCNEPSC = 1
            MOVE      ONE,GENCHCDS          * set "Generate Discharge patchcof"
          ENDIF
.
          MOVE      " 0",KEY2
          PACK      CHCDATA,PVIBILL,ADATE,ATIME,KEY2,ACARE,SP30
.
          MOVE      "CC",CATEGORY
          MOVE      ACARE,CODE                * Get care type description
          CALL      GETCOD00
          MOVE      TDESC,DISPCART
.
TOAP4000  MATCH     "1",DSUMMARY
          IF        @EQUAL
            CALL      CHKDS000                * Check for disch summary
            BRANCH    F1,TOAP1000
          ELSE
            IF        MRCNEPSC=1
              CALL      GEPS0000              * Get episode dates
              BRANCH    EXIT,TOAP1000
.
              MATCH     SP70,CHCODCMP         * Check Coding Complete Date Blank
              GOTO      TOAP1000 IF NOT EQUAL * for this episode
            ELSE
              MATCH     SP70,ACODDTE          * Check Coding Complate Date Blank
              GOTO      TOAP1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     SP3,CLAIMCOD
          IF        !@EQUAL
            MATCH     ACLAIM,CLAIMCOD         * claim type
            GOTO      TOAP1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP3,FUNDCODE
          IF        !@EQUAL
            MATCH     AFUNDH,FUNDCODE         * health fund
            GOTO      TOAP1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP6,DISCDOCT
          IF        !@EQUAL
            MATCH     ADOCTA,DISCDOCT         * Attending doctor(discharge doc)
            GOTO      TOAP1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP3,ADMISTYP
          IF        !@EQUAL
            MATCH     ATYPE,ADMISTYP          * admission type
            GOTO      TOAP1000 IF NOT EQUAL
          ENDIF
.
          CALL      MLOC0000                  * Get medical record location
.
          MATCH     SP70,MRLOCNCD
          IF        !@EQUAL
            MATCH     MRHOSPIT,MRHOSPCD       * same hospital?        *I-240724
            GOTO      TOAP1000 IF NOT EQUAL                           *I-240724
            MATCH     MRLOCATN,MRLOCNCD       * same record location ?
            GOTO      TOAP1000 IF NOT EQUAL
          ENDIF
.
          CALL      GTRN0000                  * get ward & unit from trans file
          MATCH     SP3,UNITCODE
          IF        !@EQUAL
            MATCH     TDEPT,UNITCODE          * check department
            GOTO      TOAP1000 IF NOT EQUAL
          ENDIF
          MATCH     SP3,WARDCODE
          IF        !@EQUAL
            MATCH     TWARD,WARDCODE          * check discharge ward
            GOTO      TOAP1000 IF NOT EQUAL
          ENDIF
.
          MOVE      SP70,ADMDATE
          MOVE      SP70,DSCDATE
          MOVE      AURNO,URNUMBER
          MOVE      AADMNO,ADMISSNO
.
          CALL      DISCON00              * display discharge content
.
          IF        MRCNEPSC=1
            GOTO      TOAP4000            * Get next episode for this admission
          ELSE
            GOTO      TOAP1000            * Get next current admission
          ENDIF
.
TOAP9999  RETURN

.
.---------------------------------------------------------------
.   Check if the discharged summary has been printed
.---------------------------------------------------------------
CHKDS000  MOVE      ZERO,F1
          PACK      KEY28,DPVIBILL,SP70
          CALL      RSOBPC2
CHKDS100  CALL      RKOBPC2
          BRANCH    OVRCD,CHKDS999
.
          MATCH     DPVIBILL,OBPCCVIS
          GOTO      CHKDS999 IF NOT EQUAL
.
          PACK      KEY5,CATWI,OBPCCTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKDS100
.
          MATCH     ANSD,TCINDC1
          GOTO      CHKDS100 IF NOT EQUAL
.
          MOVE      ONE,F1
.
CHKDS999  RETURN
+
.------------------------------------------------------------
. Get the episode start and end date/times append * if
. not same as admission/discharge date/time
.------------------------------------------------------------
GEPS0000  MATCH     SP70,SAVEPKEY
          IF        @EQUAL
            PACK      SAVEPKEY,PVIBILL,SP70
            MOVE      ADATE,EPISADAT
            MOVE      ATIME,EPISATIM
          ELSE
            UNPACK    SAVEPKEY,KEY8,EPISADAT,EPISATIM
          ENDIF
.
. Find this episose end date/time
.
          PACK      KEY24,SAVEPKEY,SP70
          CALL      RDCHCO1
          CALL      RDKCHCO1
          IF        OVRCD = 1 & GENCHCDS = 1
            MOVE      ONE,GENITNOW
            GOTO      GEPS1000
          ENDIF
          BRANCH    OVRCD,GEPS9000               * Check for care type changes
.                                                * to get episode number
          UNPACK    SAVEPKEY,KEY8
          MATCH     DCHADMN,KEY8                 * Correct admission number
          IF        !@EQUAL
            MOVE      ONE,GENITNOW
            BRANCH    GENCHCDS,GEPS1000
            GOTO      GEPS9000
          ENDIF
.
GEPS1000  IF        GENCHCDS = 1
            IF        GENITNOW <> 1
              PACK      CHCDATA,CHADMN,CHDATE,CHTIME,CHEPISNO,CHCODE,SP30
            ELSE
.                                           * build a dummy patchcof record
              UNPACK    CHCDATA,KEY8A,EPISADAT,EPISATIM,KEY2,KEY3
              MOVE      KEY8A,CHADMN
              MOVE      DDATE,CHDATE
              MOVE      DTIME,CHTIME
              UNPACK    SP70,CHCRDATE,CHCRTIME,CHCRUSID
              UNPACK    SP70,CHUPDATE,CHUPTIME,CHUPUSID
              MOVE      "CC",CHCATG
              MOVE      ACARE,CHCODE
              MOVE      KEY2,F2
              ADD       ONE,F2
              MOVE      F2,CHEPISNO
              MOVE      ACODDTE,CHCODCMP
              MOVE      ZERO,GENCHCDS
              MOVE      ZERO,GENITNOW
            ENDIF
          ENDIF
.
GEPS2000  MOVE      CHEPISNO,EPSCD001                  * Save current episode
          PACK      SAVEPKEY,CHADMN,CHDATE,CHTIME,SP70 * number and key
.
          MOVE      CHDATE,EPISDDAT              * Episode discharge date
          MOVE      CHTIME,EPISDTIM              * Episde discharge time
.
          MOVE      "CC",CATEGORY
          MOVE      CHCODE,CODE                  * Get care type description
          CALL      GETCOD00
          MOVE      TDESC,DISPCART
.
GEPS4000  MOVE      SP70,EPSADATT                * Episode start date & time
          UNPACK    EPISADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      EPSADATT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          ENDSET    EPSADATT
          APPEND    " at ",EPSADATT
          APPEND    EPISATIM,EPSADATT
.
          MATCH     ADATE,EPISADAT
          IF        @EQUAL
            MATCH    ATIME,EPISATIM
            IF       @EQUAL
              RESET    EPSADATT
              GOTO     GEPS5000
            ENDIF
          ENDIF
.
          APPEND    " *",EPSADATT
          RESET     EPSADATT
.
GEPS5000  MOVE      SP70,EPSDDATT                * Episode end date & time
          UNPACK    EPISDDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      EPSDDATT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          ENDSET    EPSDDATT
          APPEND    " at ",EPSDDATT
          APPEND    EPISDTIM,EPSDDATT
.
          MATCH     DDATE,EPISDDAT
          IF        @EQUAL
            MATCH    DTIME,EPISDTIM
            IF       @EQUAL
              RESET    EPSDDATT
              GOTO     GEPS8000
            ENDIF
          ENDIF
.
          APPEND    " *",EPSDDATT
          RESET     EPSDDATT
.
GEPS8000  MOVE      ZERO,EXIT
          GOTO      GEPS9999
.
GEPS9000  MOVE      ONE,EXIT
          GOTO      GEPS9999
.
GEPS9999  RETURN
+
.---------------------------------------------------------------
.   Check if the discharged patient is currently admitted
.---------------------------------------------------------------
CKAD0000  MOVE      ZERO,EXIT
.
          PACK      KEY17,DURNO,TWO,SP70
          CALL      RSPTMI18
CKAD1000  CALL      RKPTMI18
          BRANCH    OVRCD,CKAD9000          * not on file
.
          MATCH     DURNO,AURNO             * same U/R Number?
          GOTO      CKAD9000 IF NOT EQUAL   * No
.
          COMPARE   TWO,ASTAT
          GOTO      CKAD9000 IF NOT EQUAL
.
          GOTO      CKAD9999
.
CKAD9000  MOVE      ONE,EXIT                * Skip record - Not admitted
.
CKAD9999  RETURN
.---------------------------------------------------------------
.  Read most current Medical Record 
.------------------------------------------------------------
MLOC0000  UNPACK    SP70,MRLOCATN,MRHOSPIT,MRHOHOSP                   *C-240724
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDMRVS1
          BRANCH    OVRCD,MLOC1000
.
          PACK      KEY10,MRVSMKEY,SP70
          CALL      RDMRMAS2
          BRANCH    OVRCD,MLOC1000
          MOVE      MRMAHHOS,MRHOHOSP                                 *I-240724
          MOVE      MRMACHOS,MRHOSPIT                                 *I-240724
          MOVE      MRMACLOC,MRLOCATN
          GOTO      MLOC9999
.
MLOC1000  PACK      KEY20,PVIURNO,Z70                                 *C-240724
          CALL      RSMRMAS1
          CALL      RPMRMAS1
          BRANCH    OVRCD,MLOC9999        * no M/R tracking master record
.
          MATCH     PVIURNO,MRMAURNO
          GOTO      MLOC9999 IF NOT EQUAL
.
          MOVE      MRMAHHOS,MRHOHOSP                                 *I-240724
          MOVE      MRMACHOS,MRHOSPIT                                 *I-240724
          MOVE      MRMACLOC,MRLOCATN     * Location Save var 
.
MLOC9999  RETURN
.------------------------------------------------------------
.         Get discharge unit/ward
.------------------------------------------------------------
GTRN0000  PACK      KEY30,PVIBILL,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,GTRN9000
.
          MATCH     PVIBILL,TADMN
          GOTO      GTRN9000 IF NOT EQUAL   * different admission
.
          MATCH     ANSD,TMOVE
          GOTO      GTRN9999 IF EQUAL
.
GTRN9000  MOVE      SP10,TWARD
          MOVE      SP10,TDEPT
.
GTRN9999  RETURN
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NXTP0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.          
          WRITE     HTMLFILE;"patweb86.pbl":
                             "?reportno=",KEY2:    
                             "&template=",KEY3:
                             "&frstdate=",NXTFDATE:
                             "&viewtype=",VIEWTYPE:
                             "&lastdate=",NXTLDATE;
.
NXTP9999  RETURN
.
.------------------------------------------------------------
. Previous Day, Week, Month
.------------------------------------------------------------
PRVP0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.          
          WRITE     HTMLFILE;"patweb86.pbl":
                             "?reportno=",KEY2:    
                             "&template=",KEY3:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&viewtype=",VIEWTYPE;
.
PRVP9999  RETURN
.------------------------------------------------------------
. Discharge Table Contents 
.------------------------------------------------------------
DISCON00  CALL      GETPAT00  
          MOVE      SP70,DOCFNAME
          MOVE      SP70,CODINGDT
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000      
          ENDIF
.
          MOVE      SP70,PTEDDTCD
          MOVE      SP70,PTEODTCD
          CALL      GETEPS00        * Get first existing episode with Coding
.
.          MATCH     "1",WAHEAFLG
.          IF        !@EQUAL
          MATCH     " 0",EPSNUMBR
          GOTO      DISCON10 IF NOT EQUAL * Episode not found for this Admission
.          ENDIF
.
          REP       " +",ADMISSNO
          REP       " +",URNUMBER
.
          CLEAR     PATLINK
          APPEND    "javascript: PatientLink('",PATLINK
          APPEND    LINKPRG,PATLINK
          APPEND    ".pbl?reportno=",PATLINK
          APPEND    LINKREP,PATLINK
          APPEND    "&template=",PATLINK
          APPEND    LINKTEM,PATLINK
          APPEND    "&urnumber=",PATLINK
          APPEND    URNUMBER,PATLINK
          APPEND    "&admissno=",PATLINK
          APPEND    ADMISSNO,PATLINK
          APPEND    "&epscd001=",PATLINK
.
          IF        MRCNEPSC=1
             APPEND   EPSCD001,PATLINK
          ELSE 
            APPEND    "+1",PATLINK
          ENDIF
.
          APPEND    "','0')",PATLINK
          RESET     PATLINK
          REP       "+ ",ADMISSNO
          REP       "+ ",URNUMBER
          GOTO      DISCON20
.
DISCON10  REP       " +",ADMISSNO
          REP       " +",URNUMBER
          REP       " +",EPSNUMBR
          MATCH     SP70,PTEODTCD
          IF        !@EQUAL
            PACK      CODINGDT,PTEODTCD,SP70
          ENDIF
          MATCH     SP70,PTEDDTCD
          IF        !@EQUAL
            PACK      CODINGDT,PTEDDTCD,SP70
          ENDIF
          CLEAR     PATLINK
          APPEND    "javascript: PatientLink('",PATLINK
          APPEND    LINKPRG2,PATLINK
          APPEND    ".pbl?reportno=",PATLINK
          APPEND    LINKREP2,PATLINK
          APPEND    "&template=",PATLINK
          APPEND    LINKTEM2,PATLINK
          APPEND    "&urnumber=",PATLINK
          APPEND    URNUMBER,PATLINK
          APPEND    "&admissno=",PATLINK
          APPEND    ADMISSNO,PATLINK
          APPEND    "&epscd001=",PATLINK
          APPEND    EPSNUMBR,PATLINK
          APPEND    "','1')",PATLINK
          RESET     PATLINK
          REP       "+ ",ADMISSNO
          REP       "+ ",URNUMBER
          REP       "+ ",EPSNUMBR
.
DISCON20  MOVE      SP70,MRLODESC         * Clear Description
.                                         * Get Current Location Description
          MOVE      SP70,KEY70
.
          PACK      KEY7,MRHOSPIT,MRLOCATN,SP70                       *C-240724
          CALL      RDMRLOC1
          BRANCH    OVRCD,DISCON30
.
          PACK      KEY70,MRLODESC,SP70                     * start   *I-240724
          IF        IBCNMHOS = 1
            MOVE      SP70,KEY30
            PACK      KEY30,OBB,MRHOSPIT,CBB     * set default to black
            MATCH     MRHOHOSP,MRHOSPIT
            IF        !@EQUAL
              MOVE      SP3,PTHSHHOS
              PACK      KEY3,MRHOSPIT
              CALL      RDPTHSP1
              MATCH     PTHSHHOS,MRHOHOSP            * aca MRMAHHOS
              IF        !@EQUAL
                PACK      KEY30,OBR,MRHOSPIT,CBR     * set to red
              ELSE    
                UNPACK    SP20,TCINDC1,TCINDC2,TCINDC3,TCINDC4
                PACK      KEY5,CATTY,PTHSRCTY                         *C-259106
                CALL      RDCODE1
                MATCH     "B",TCINDC1
                IF        !@EQUAL
                  PACK      KEY30,OBR,MRHOSPIT,CBR   * set to red
                ENDIF
              ENDIF
            ENDIF
            RESET     KEY30
            PACK      KEY70,KEY30,MRLODESC,SP70  * format is "(HOS)Location"
.
            MATCH     "1",WACLFILT
            IF        @EQUAL
              MATCH     SP70,PMVXMHOS
              IF        !@EQUAL
                MATCH     SP70,MRHOSPIT
                IF        !@EQUAL
                  MATCH     PMVXMHOS,MRHOHOSP
                  IF        !@EQUAL
                    MATCH     PMVXMHOS,MRHOSPIT
                    IF        !@EQUAL
                      PACK      KEY70,SP70
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
.
          ENDIF    
          RESET     KEY70                                   * end     *I-240724
.
DISCON30  CALL      PATLN000    * Format Patient Name from System Parameter
.
          CALL      PATFLD00    * Returns KEY3 for folder colour code
          CALL      LSTALR00    * Patient Alerts (in LSTALERT.PBL)    *I-216245
.
          MOVE      URNUMBER,SORTORDR       *                         *I-240716
          CALL      MRSRTORD                * create sort order from SORTORDR
.
          MOVE      "&nbsp;",THTRCOMP
          MATCH     "1",PMVXATCP
          IF        @EQUAL
            MOVE      "Yes",THTRCOMP
          ENDIF
.
          STRIP     ALRTIMGE
          STRIP     ALRTIMG2
          STRIP     ALRTIMG3
          STRIP     ALRTIMG4
          PACK      ALRTIMGT,ALRTIMGE,ALRTIMG2,ALRTIMG3,ALRTIMG4
.
          PACK      KEY6,TWARD,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            MOVE      SP70,PTWDSDES
          ENDIF
.
          MOVE      SP70,DISSUMRY
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDMRPDS1
          IF        OVRCD=1
            MOVE    SP70,DISSUMRY
          ENDIF 
          MOVE      "CP",CATEGORY
          MOVE      MRPDSTAT,CODE           * Get display summary description
          CALL      GETCOD00
          MOVE      TDESC,DISSUMRY
.
          WRITE     HTMLFILE;"t.addRow(#"",PATLINK,"#",":  * 0
                             "#"",FORMATNM,"#",":          * 1
                             "#"",URNUMBER,"#",":          * 2
                             "#"",ADMISSNO,"#",":          * 3
                             "#"",ADMDATE,"#",":           * 4
                             "#"",DSCDATE,"#",":           * 5 
                             "#"",TDEPT,"#",":             * 6
                             "#"",TWARD,"#",":             * 7
                             "#"",PTDSDLOS,"#",":          * 8
                             "#"",KEY70,"#",":             * 9
                             "#"",SORTORDR,"#",":          * 10
                             "#"",PCEASE,PCASE,PTMXSIN1,PTMXSIN2,PTMXSIN3:
                                  PTMXSIN4,PTMXSIN5,"#",": * 11
                             "#"",ADATE,"#",":             * 12
                             "#"",DOCFNAME,"#",":          * 13
                             "#"",AFUNDH,"#",":            * 14
                             "#"",ALRTLINK,"#",":          * 15       *I-216245
                             "#"",ALRTIMGT,"#",":          * 16       *I-216245
                             "#"",KEY3,"#",":              * 17       *I-216245
                             "#"",THTRCOMP,"#",":          * 18       *I-225138
                             "#"",CODINGDT,"#",":          * 19
                             "#"",EPSADATT,"#",":               * 20
                             "#"",EPISADAT,EPISATIM,"#",":      * 21
                             "#"",EPSDDATT,"#",":               * 22
                             "#"",EPISDDAT,EPISDTIM,"#",":      * 23
                             "#"",PVIBILL," /",EPSCD001,"#",":  * 24
                             "#"",DISPCART,"#",":               * 25
                             "#"",PTWDSDES,"#",":               * 26
                             "#"",DISSUMRY,"#",":               * 27
                             "#"","uncodDisChrg","#")"                * 28
.
          MOVE      ZERO,LOOPCNTR
DISCON99  RETURN
.----------------------------------------------------------------
.  If using episode coding check if an episode has been coded
.  if not using episode coding
.  Get a Patients first existing Episode for this admission no.
.  Else return " 0" in EPSNUMBR
.----------------------------------------------------------------
GETEPS00  MOVE      SP70,EPSNUMBR  * Episode No - Return Variable
          MOVE      ZERO,ECDFLAG   * Disease Episode Flag
          MOVE      ZERO,ECOFLAG   * Operation Episode Flag
.
          IF        MRCNEPSC=1
            PACK      KEY13,ADMISSNO,EPSCD001,SP70  * Check Disease Coding file
          ELSE
            PACK      KEY13,ADMISSNO,SP70   * Check Disease Coding file
          ENDIF
          CALL      RSPTECD1
GETEPS10  CALL      RKPTECD1
          IF        OVRCD=1
            MOVE      ONE,ECDFLAG
            GOTO      GETEPS20
          ENDIF
.
          MATCH     ADMISSNO,DPTEDADM    * Check for Admission No
          IF        !@EQUAL
            MOVE      ONE,ECDFLAG
            GOTO      GETEPS20
          ENDIF
.
          IF        MRCNEPSC=1
            COMPARE   EPSCD001,PTEDEPNO   * Check for Episode Number
            IF        !@EQUAL
              MOVE      ONE,ECDFLAG
              GOTO      GETEPS20
            ENDIF
          ENDIF
.
          MATCH     SP70,PTEDCODE
          IF        !@EQUAL
            MOVE      PTEDEPNO,EPSNUMBR  * Only need First Episode found!
            GOTO      GETEPS99
          ENDIF
.
GETEPS20  PACK      KEY13,ADMISSNO,SP70   * Check Operations Coding File
          CALL      RSPTECO1
GETEPS21  CALL      RKPTECO1
          IF        OVRCD=1
            MOVE      ONE,ECOFLAG
            GOTO      GETEPS90
          ENDIF
.
          MATCH     ADMISSNO,DPTEOADM    * Check for Admission No
          IF        !@EQUAL
            MOVE      ONE,ECOFLAG
            GOTO      GETEPS90
          ENDIF
.
          IF        MRCNEPSC=1
            COMPARE   EPSCD001,PTEOEPNO   * Check for Episode Number
            IF        !@EQUAL
              MOVE      ONE,ECOFLAG
              GOTO      GETEPS90
            ENDIF
          ENDIF
.
          MATCH     SP70,PTEOCODE
          IF        !@EQUAL
            MOVE      PTEOEPNO,EPSNUMBR  * Only need First Episode found
            GOTO      GETEPS99
          ENDIF
.
          IF        ECDFLAG=1
            GOTO    GETEPS21
          ELSE
            GOTO    GETEPS10
          ENDIF
.
GETEPS90  MATCH     SP70,EPSNUMBR    * If nothing is found then default values
          IF        @EQUAL
            RESET     EPSNUMBR
            MOVE      " 0",EPSNUMBR
          ENDIF
.
GETEPS99  RETURN
.------------------------------------------------------------
. Get Patient
.------------------------------------------------------------
GETPAT00  MOVE      SP10,URTDNO
          MATCH     "       0",URNUMBER
          IF        !@EQUAL
            MOVE      URNUMBER,KEY8
            CALL      RDMAST1
            MOVE      OVRCD,MASTFLAG
            UNPACK    KEY8,DIM2A,DIM2B,DIM2C,KEY2
            PACK      URTDNO,DIM2C,DIM2B,DIM2A,KEY2
            PACK      KEY8,URNUMBER                                   *I-216245
            CALL      RDPMPX21                                        *I-216245
          ELSE
            MOVE      ADMISSNO,KEY8
            CALL      RDPRAM1
            MOVE      OVRCD,MASTFLAG
          ENDIF
.
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DISPSEX
.
          UNPACK    PBDATE,CDAY,CMON,CYEAR,CCENT
          CALL      IBACLOCK
          IF        PCEASE=1
            MATCH     SP70,PDECDTE
            IF        @EQUAL
              PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
            ELSE
              MOVE      PDECDTE,AGEDATE            * Date of Death
            ENDIF
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
.
          CALL      PATNAM00
.
          MOVE      AURNO,URNUMBER
          MOVE      AADMNO,ADMISSNO
.
          MOVE      ADMISSNO,KEY8
          CALL      RDVISA1
.
          MOVE      ADMISSNO,KEY8
          CALL      RDMISS1
          IF        OVRCD=0
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            MOVE      ASTAY,ADJDAYS
            CALL      ADJDAT00
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT:
                      NOV,DEC
            PACK      COLDAT04,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR 
          ENDIF
.
GETPAT99  RETURN
.------------------------------------------------------------
. Write Patient Age in Y,M,W or D
.------------------------------------------------------------
WRTAGE00  MATCH     "y",PSAGETYP
          GOTO      WRTAGE10 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEYRS," yrs";
          GOTO      WRTAGE99
.
WRTAGE10  MATCH     "d",PSAGETYP
          GOTO      WRTAGE20 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEDAY," days";
          GOTO      WRTAGE99
.
WRTAGE20  MATCH     "m",PSAGETYP
          GOTO      WRTAGE30 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEMNT," mths";
          GOTO      WRTAGE99
.
WRTAGE30  MATCH     "w",PSAGETYP
          GOTO      WRTAGE99 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEWKS," wks";
.
WRTAGE99  RETURN
.------------------------------------------------------------
. Format Patient Name <b>SURNAME</b>Title Given Names
.------------------------------------------------------------
PATNAM00  CLEAR     DISPNAME
...       REP       "' ",PSNAME
          REP       "#" ",PSNAME
...       REP       "' ",PGNAME
          REP       "#" ",PGNAME
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          APPEND    "<b>",DISPNAME
          APPEND    PSNAME,DISPNAME
          APPEND    "</b>",DISPNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN
+
.------------------------------------------------------------
. Health Fund Group Selection
.------------------------------------------------------------
HFGSEL00  PACK      KEY6,SP70
          CALL      RSPAHGP1
HFGSEL10  CALL      RKPAHGP1
          BRANCH    OVRCD,HFGSEL99
.
          MOVE      PTHGDESC,KEY50
          PACK      KEY8,QUOTE,PTHGCODE,QUOTE
          MATCH     FGRPCODE,PTHGCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY8," selected>":
                               KEY50,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY8,">",KEY50,"</option>"
          ENDIF
          GOTO      HFGSEL10
.
HFGSEL99  RETURN
+
.*****************************************************************************
.* ****** CAR 216245 - removed GETALIDS as PMSPX1TM also contains GETALIDS ***
.*  GETALTIDS
.*  Procedure to get an alternate ID
.*  Exit=0  No alternate ID
.*       1  Alternate ID's exist
.*****************************************************************************
.------------------------------------------------------------
          INC       IBAWEBCD
          INC       EXCLNEWB
          INC       PATCHCIO/INC
          INC       PATDSCIO/INC
          INC       PATDO1IO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATFN1IO/INC
          INC       PATHSPIO/INC
          INC       PATHGPIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC                                      *I-216245
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PATPR1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       MRTLOCIO/INC
          INC       MRTMASIO/INC
          INC       MRTVISIO/INC
          INC       MRTHISIO/INC
          INC       OBSCNAIO/INC
          INC       OBSPCOIO/INC
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       MRTPDSIO/INC   * Post Discharge Header  
.
          INC       CALCAGE
          INC       CLPATDOC
          INC       PATDOCTM
          INC       PATDOCCD                                      * I CAR 13038
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       WEBDATCD
          INC       PATNAMCD
          INC       PMSPX2TM                                          *I-216245
          INC       LSTALERT
          INC       MRTRKSEQ           * Medical Records Tracking Sort Order

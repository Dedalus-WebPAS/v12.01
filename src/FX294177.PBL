.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   FX294177                                                    *
.* Desc      :   Load PRIHREFD.PRHRTACC with first code of category "ta" that*
.*           :   has "R" in indicator 2 for Eclipse IMC                      *
.*****************************************************************************
.* Date      :   11/05/2016                                                  *
.* Author    :   Peter Vela                                                  *
.* Function  :   This program will loop through prihreff and populate        *
.*               PRHRTACC if empty with the first code in category "ta"      *
.*               that has a value of "R" in indicator 2.                     *
.*                                                                           *
.* Modifications :                                                           *
.*     V10.08.01 12/05/2016 Peter Vela TSK 0294177                           *
.*               Added validation value of blank or 0 in category ta ind 1   * 
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCODFD/INC
          INC       PRIHREFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
DIM4      DIM       4
DIM3      DIM       3
.
. PROGRAM CONSTANTS
. -----------------
CATta     INIT      "ta"
.
PRGID     INIT      "FX294177"
PRGNAM    INIT      "Fixit PRIHREFD.PRHRTACC"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"prihreff";
          OPEN      PRIHREF1,"prihreff"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,COUNTER                 * initialise counter
.
          MOVE      SP70,DIM3
.
          PACK      KEY5,CATta,SP70
          CALL      RDSCODE1
PROC0100  CALL      RDKCODE1
          BRANCH    OVRCD,PROC0200
.
          MATCH     SP70,ACODE
          GOTO      PROC0100 IF EQUAL
.
          MATCH     ANSR,TCINDC2
          GOTO      PROC0100 IF NOT EQUAL
.
          MATCH     SP70,TCINDC1
          GOTO      PROC0150 IF EQUAL
.
          MATCH     "0",TCINDC1
          GOTO      PROC0150 IF EQUAL
.
          GOTO      PROC0100 
.
PROC0150  MOVE      ACODE,DIM3
.
PROC0200  MATCH     SP70,DIM3
          GOTO      PROC8000 IF EQUAL
.
          PACK      KEY23,SP70
          CALL      RSPRHR1
PROC0500  CALL      RKPRHR1
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          MATCH     SP70,PRHRTACC
          GOTO      PROC0500 IF NOT EQUAL
.
          DISPLAY   *P13:24,*V2LON,DPRHRUNQ,PRHRPRAC,PRHRDOCT,PRHRPIND
.
          MOVE      DIM3,PRHRTACC
.
          CALL      UPPRHR1                      * update record
.
          ADD       ONE,COUNTER          
          GOTO      PROC0500                     * get next record
.
PROC8000  DISPLAY   *P1:22,*EF,"blank or zero not found in indicator 1 and ":
                    "'R' not found in indicator 2 of any codes in ":
                    "category 'ta'.":
                    *P1:24,"Fixit not executed.  ";
          CALL      HITENTER 
          GOTO      PROC9999
.
PROC9000  DISPLAY   *P1:22,*EF,*V2LON,COUNTER,*HOFF," records updated in ":
                    "prihreff":
                    *P1:24,"Fixit completed.  ";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PRIHREIO/INC

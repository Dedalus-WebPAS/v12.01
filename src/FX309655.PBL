.*****************************************************************************
.* System    :   Patient                                                     *
.* Program   :   FX309566                                                    *
.* Desc      :   Fixit to update the ICU Hours for Queensland                *
.*****************************************************************************
.* Date      :   17/12/2014                                                  *
.* Author    :   J.Tan                                                       *
.* Function  :   This program will recalculate hours in ICU for Queensland   *
.*                                                                           *
.* V10.06.01     19/03/2015  Don Nguyen  CAR 300414                          *
.*               Recompiled for CALCCODE - Added check for PTCNICUT          *
.* V10.05.03     23/12/2014  J Parkinson CAR 309655                          *
.*               Fixed length of reccount                                    *
.* V10.05.02     22/12/2014  J Parkinson CAR 309655                          *
.*               Fixed up incorrect GOTO                                     *
.* V10.05.01     22/12/2014  J Parkinson CAR 309655                          *
.*               Recompiled for CALCCODE to update with zero times           *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCONFD/INC
          INC       PATICUFD/INC
          INC       PATTRNFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATDSCFD/INC
          INC       PATCODFD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECCOUNT  FORM      8
RTDAYS    FORM      4
SKEY30    DIM       30
CALDAYS   FORM      4
.
FORM5     FORM      5
FORM6P2   FORM      6.2
FORM6     FORM      6
FORM6A    FORM      6
FROMDATE  DIM       8
.
MRCNROUN  DIM       1
NBRDAYS   FORM      4
.
SAVDATE   DIM       8
SAVTIME   DIM       8
.
TODATE    DIM       8
TTRNDATE  DIM       8
.
FIRSTDAT  DIM       8
LASTDATE  DIM       8
FIRSTIME  DIM       4
LASTTIME  DIM       4
FIRSTHR   DIM       2
FIRSTMIN  DIM       2
LASTHOUR  DIM       2
LASTMIN   DIM       2
CALCTIME  FORM      6
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
HOUR1     FORM      6
HOUR2     FORM      6
MINUTE1   FORM      3
MINUTE2   FORM      3
REDIRECT  DIM       240
TOTTIME1  FORM      4
TOTTIME2  FORM      4
TMOTFRST  DIM       2    * Time out first (used by ibacgi)
TMOTNEXT  DIM       2    * Time out next  (used by ibacgi)
.
PRGID     INIT      "FX309655"
PRGNAM    INIT      "Fixit to recalculate ICU hours for Queensland"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN1000:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process batch records
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"paticuaf";
          OPEN      PATICUA1,"paticuaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,RECCOUNT                * initialise record counter
.
          PACK      KEY8,SP70
          CALL      RSPTICU1                     * position read
PROC1000  CALL      RKPTICU1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          MOVE      PTICUADM,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PROC1000
.
          MOVE      SP70,DDATE
          IF        ASTAT=3
            CALL      RDDSCH1
            BRANCH    OVRCD,PROC1000
          ENDIF
.
          DISPLAY   *P20:24,*V2LON,PTICUADM;
          PROC      CALCCODE                * Recalculate hrs of ICU/NCU/CCU
.
          ADD       ONE,RECCOUNT                 * increment record counter
          GOTO      PROC1000                     * get next record
.
PROC9000  DISPLAY   *P1:24,*EL,*V2LON,RECCOUNT,*HOFF," records checked.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CALCCODE
          INC       TIMEDIFF
          INC       CALCDAYS
          INC       RTIODAYS
.
          INC       PATICUIO/INC
          INC       PATTRNIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDSCIO/INC
          INC       PATCODIO/INC

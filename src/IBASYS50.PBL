.-----------------------------------------
.   Program   :   IBASYS50
.   Function  :   System Master Maintenance
.   Gen Date  :   25/07/91
.   Mods      :   V9.02.00 18/10/2002 Ebon Clements SRF 22512
.             :             Ported from 509
.-----------------------------------------
          INC       STDGENDF
          INC       IBAPASFD/INC
.
.=========================
.File Description Includes
.-------------------------
          INC       IBASMAFD/INC
          INC       IBAUSEFD/INC
.=========================
.Local Variable Definition
.-------------------------
.
ACCEPT    FORM      1
ITEMNO    FORM      2
.
PRGID     INIT      "IBASYS50"
PRGNAM    INIT      "System Master Maintenance"
VERSION   INIT      "V12.01.00"
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
ML0000    CALL      INIT0000
.
ML0100    CALL      SELOPTN
          COMPARE   ZERO,COPTION
          GOTO      ML9999 IF EQUAL    * exit program
.
ML0200    *         display screen A
          CALL      PROC0000
          BRANCH    EXIT,ML0100,ML0100
          LOAD      ACCEPT,COPTION,ONE,ZERO,ZERO
.
          BRANCH    COPTION,ML1000,ML2000,ML3000
.
.---- add data ----
.
ML1000    CALL      SELA0000     * modify screen A
          BRANCH    EXIT,ML0200  * don't save data
.
          CALL      WRTA0000     * post screen A data
          GOTO      ML0200       * get next entry
.
.---- modify data ----
.
ML2000    CALL      LOCK0000     * Lock Record
          BRANCH    EXIT,ML0200  * Lock Record
          CALL      DISA0000     * modify screen A
          CALL      SELA0000     * modify screen A
          BRANCH    EXIT,ML2100  * don't save data
.
          CALL      UPDA0000     * post screen A data
          CALL      UNLC0000     * Unlock Record
          GOTO      ML0200       * get next entry
ML2100    CALL      UNLC0000     * Unlock Record
          GOTO      ML0200       * get next entry
.
.---- delete data ----
.
ML3000    CALL      LOCK0000     * Lock Record
          BRANCH    EXIT,ML0200  * Lock Record
          CALL      DISA0000     * modify screen A
          CALL      DELQST       * ok to delete (y/n/c) ?
          BRANCH    CEXIT,ML3100,ML3200,ML3300
.
ML3100    CALL      DELA0000     * delete screen A data
          GOTO      ML0200       * get next entry
ML3200    CALL      UNLC0000     * Unlock Record
          GOTO      ML0200       * get next entry
ML3300    CALL      UNLC0000     * Unlock Record
          GOTO      ML0100       * get next entry
.
ML9999    CHAIN     PGM          * chain back to menu
.
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          DISPLAY   *P64:24,"Opening ibasmaa1"
          OPEN      IBASMAA1,"ibasmaaf"
.
          DISPLAY   *P72:24,"ihapassf"
          OPEN      IHAPASS1,"ihapassf"
.
          DISPLAY   *P72:24,"ibauseaf"
          OPEN      IBAUSEA1,"ibauseaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.-------------------------------------------------
. SCRA - Display Screen
.-------------------------------------------------
SCRA0000  DISPLAY   *P1:3,*EF
          DISPLAY   *P005:04,*HOFF,"System Number      :",*HOFF:
                    *P001:06,*V2LON," 1.",*HOFF:
                    *P005:06,*HOFF,"System Description :",*HOFF:
                    *P002:07,*V2LON,"2.",*HOFF:
                    *P005:07,*HOFF,"Native Description :",*HOFF:
                    *P002:08,*V2LON,"3.",*HOFF:
                    *P005:08,*HOFF,"Menu Heading       :",*HOFF:
                    *P002:09,*V2LON,"4.",*HOFF:
                    *P005:09,*HOFF,"Default Menu       :",*HOFF:
                    *P1:24;
          RETURN
.
.-------------------------------------------------
. PROC - Enter Key Fields
.        Returns : EXIT  0=continue
.                        1=nothing entered
.                        2=EXITCHAR entered
.-------------------------------------------------
PROC0000  CALL      CLRA0000
          CALL      SCRA0000
          MOVE      ZERO,CKEYTYP
          MOVE      "026",CCOL
          MOVE      "04",CVERT
          MOVE      ZERO,CKEYTYP
          COMPARE   ONE,COPTION
          GOTO      PROC1000 IF NOT EQUAL
          MOVE      ONE,CKEYTYP
.
PROC1000  CALL      KIBSMA00
          BRANCH    EXIT,PROC9999,PROC9999
.
PROC9999  MOVE      ZERO,CKEYTYP
          RETURN
.
.-------------------------------------------------
. DISA - Display Screen A
.-------------------------------------------------
DISA0000  DISPLAY   *P026:04,*V2LON,IBSMNUMB
          DISPLAY   *P026:06,*V2LON,IBSMNAME
          DISPLAY   *P026:07,*V2LON,IBSMNATV
          DISPLAY   *P026:08,*V2LON,IBSMHEAD
          DISPLAY   *P026:09,*V2LON,IBSMDMPG
          RETURN
.
.-------------------------------------------------
. CLRA - Clear Fields A
.-------------------------------------------------
CLRA0000  MOVE      SP70,IBSMNUMB
          MOVE      SP70,IBSMNAME
          MOVE      SP70,IBSMNATV
          MOVE      SP70,IBSMHEAD
          MOVE      SP70,IBSMDMPG
          RETURN
.
.-------------------------------------------------
. SELA - Select Field
.-------------------------------------------------
SELA0000  MOVE      ZERO,CCITEM
.
.---- determine option to execute ----
.
SELA0100  ADD       ONE,CCITEM
          BRANCH    ACCEPT,SELA0500
          CALL      MAINQST
.
          COMPARE   ZERO,CCITEM
          GOTO      SELA9000 IF EQUAL
.
          COMPARE   SEQ,CCITEM
          GOTO      SELA9500 IF EQUAL
.
SELA0500  BRANCH    CCITEM,SELA1100,SELA1200,SELA1300,SELA1400
.
          BRANCH    ACCEPT,SELA0900
          BEEP
.
SELA0900  MOVE      ZERO,ACCEPT
          GOTO      SELA0100
.
. Option    1
.
SELA1100  MOVE      "026",CCOL
          MOVE      "06",CVERT
          KEYIN     *PCCOL:CVERT,*V2LON,IBSMNAME:
                    *PCCOL:CVERT,*DV,*V2LON,IBSMNAME:
                    *PCCOL:CVERT
          GOTO      SELA0100
.
. Option    2
.
SELA1200  MOVE      "026",CCOL
          MOVE      "07",CVERT
          KEYIN     *PCCOL:CVERT,*V2LON,IBSMNATV:
                    *PCCOL:CVERT,*DV,*V2LON,IBSMNATV:
                    *PCCOL:CVERT
          GOTO      SELA0100
.
. Option    3
.
SELA1300  MOVE      "026",CCOL
          MOVE      "08",CVERT
          KEYIN     *PCCOL:CVERT,*V2LON,IBSMHEAD:
                    *PCCOL:CVERT,*DV,*V2LON,IBSMHEAD:
                    *PCCOL:CVERT
          GOTO      SELA0100
.
. Option    4
.
SELA1400  MOVE      "026",CCOL
          MOVE      "09",CVERT
          KEYIN     *PCCOL:CVERT,*V2LON,IBSMDMPG:
                    *PCCOL:CVERT,*DV,*V2LON,IBSMDMPG:
                    *PCCOL:CVERT
          GOTO      SELA0100
.
.---- exit points ----
.
SELA9000  MOVE      ZERO,EXIT
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT
.
SELA9999  RETURN
.
.-------------------------------------------------
. WRTA - Write to Files
.-------------------------------------------------
WRTA0000  PACK      KEY3,IBSMNUMB,SP70
          CALL      RAIBSMA1
          COMPARE   ZERO,OVRCD
          GOTO      WRTA9000 IF EQUAL
.
          CALL      WRIBSMA1
          GOTO      WRTA9999
.
WRTA9000  DISPLAY   *P1:24,*B,"Record Already Exists - "
          CALL      HITENTER
.
WRTA9999  RETURN
.
.-------------------------------------------------
. UPDA - Update to Files
.-------------------------------------------------
UPDA0000  PACK      KEY3,IBSMNUMB,SP70
          CALL      RAIBSMA1
          BRANCH    OVRCD,UPDA9000
          CALL      UPIBSMA1
.
.
          GOTO      UPDA9999
.
UPDA9000  DISPLAY   *P1:24,*B,"Record Has Been Deleted - "
          CALL      HITENTER
.
UPDA9999  RETURN
.
.-------------------------------------------------
. DELA - Delete Record
.-------------------------------------------------
DELA0000  PACK      KEY3,IBSMNUMB,SP70
          CALL      RAIBSMA1
          BRANCH    OVRCD,DELA9000
.
          CALL      DEIBSMA1
          GOTO      DELA9999
.
DELA9000  DISPLAY   *P1:24,*B,"Record Has Been Deleted - "
          CALL      HITENTER
.
DELA9999  RETURN
.
.----------------------------------------
. LOCK - Lock Record to be Updated
.----------------------------------------
LOCK0000  PACK      KEY3,IBSMNUMB,SP70
          CALL      RLIBSMA1
          BRANCH    OVRCD,LOCK9000,LOCK8000
          MOVE      ZERO,EXIT
          GOTO      LOCK9999
.
LOCK8000  KEYIN     *P1:24,*EL,"Record Locked - (":
                    *V2LON,"R",*HOFF,")etry or (":
                    *V2LON,"E",*HOFF,")xit ? ",ANS
          REP       UPPLOW,ANS
          MATCH     ANSR,ANS
          GOTO      LOCK0000 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      LOCK0000 IF NOT EQUAL
.
LOCK9000  MOVE      ONE,EXIT
.
LOCK9999  RETURN
.----------------------------------------
. UNLC - Unlock Record to be Updated
.----------------------------------------
UNLC0000  CALL      UUIBSMA1
UNLC9999  RETURN
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       IBAPASIO/INC
          INC       IBASMAIO/INC
          INC       IBAUSEIO/INC
          INC       IBASMAKY
.
          INC       STDHLPCD
          INC       STD001IO
.

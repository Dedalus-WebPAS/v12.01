.*****************************************************************************
.*    Program      : IBAOUT88   -   ECR patients with future appointments    *
.*    Function     : This program will list all future bookings for patients *
.*                   who have had an ECR attendance within the specified     *
.*                   date range.                                             *
.*                                                                           *
.*    Author       : ROD   (29/09/93)    Cheers!                             *
.*                                                                           *
.*    Modifications:                                                         *
.*        V12.00.01 04/06/2025  Ebon Clements    TSK 0961623                 *
.*                  Alphanueric visit number changes                         *
.*        V12.00.01 30/05/2025 Jacob Jackson     TSK 0955096                 *
.*                  Alphanumeric changes                                     *
.*        V10.07.01 29/10/2015  Ebon Clements     CAR 268970                 *
.*                  Change to use OUTCLIFD index 1 instead of index 2        *
.*        V10.05.01 19/12/2014  Ebon Clements     CAR 261630                 *
.*                  Removed port number from temp file name                  *  
.*        V9.02.01  31/01/2003  Tracey Nguyen SRF 22709                      *
.*                  Changed open OUTCLIA2 to use site prefix                 *
.*        V9.02.00  Ported from r5.10                                        *
.******************************************************************************
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 15/03/2000  Steve Armstrong    5.08 Mods                 *
.*                  Mods for changes to OUTCLIFD (effective date)            *
.*****************************************************************************
.*                   V5.03.01 15/10/93  ROD                                  *
.*                            Print 'Appointment' date not 'Attendance' date *
.*                   V5.03.02 09/06/94  Paul Howells    SRF440906            *
.*                            Changed for new Conercaf Key                   *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       PATCODFD/INC
.         INC       CONECRFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       OUTCLIFD/INC
          INC       PATVISFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
CMDLINE   DIM       80
CODE      DIM       2
DOR1      DIM       30
DOR2      DIM       30
ENDCON    DIM       6
FDDATE    DIM       10
FINISH    INIT      "Finish"
FNAME     DIM       8
FROMDATE  DIM       8
PRDOR     DIM       26
PROTBKHD  FORM      1
SAVDATE   DIM       8
SAVDOR    DIM       3
SAVKEY16  DIM       16
SAVKEY27  DIM       27
SAVURNO   DIM       8
START     INIT      "Start "
STRTCON   DIM       6
TDDATE    DIM       10
TODATE    DIM       8
TODAY     DIM       8
.
. Temp file
TEMPFIL1  IFILE     SQL, FIXED=128, KEY="U1-8,9-11,12-19,20-27"
TEMPFIL2  IFILE     SQL, FIXED=128, KEY="U12-19,1-8,9-11,20-27"
XXDATE    DIM       8       1
XXDOR     DIM       3       9
XXURNO    DIM       8      12
DXXOUTNO  DIM       8      20
XXNAME    DIM       22     28
XXTIME    DIM       8      50
XXCLIN    DIM       6      58
XXDCLIN   DIM       24     64
XXAPPNO   DIM       20     88
XXDDOR    DIM       20    108
.         End of record   128
XXOUTNO   DIM       8       * redefine for form field key
.
PRGID     INIT      "IBAOUT88"
PRGNAM    INIT      "ECR Patients With Future Appointments"
VERSION   INIT      "V12.01.00"
+
.****************************************************************************
.*  ML0000  :  Mainline code                                                *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML0100    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit
.
ML1000    CALL      DSCR0000                      * Display the screen 
.
          CALL      KDAT0000                      * Keyin date range
          BRANCH    EXIT,ML0100                   * nothing input
.
          CALL      KDOR0000                      * keyin district of residence
          BRANCH    EXIT,ML1000                   * EXITCHAR
.
          CALL      CONTQST                       * Ok to Continue?
          BRANCH    CEXIT,ML5000,ML1000,ML0100
.
ML5000    CALL      CRET0000                      * erase/create temp file
          CALL      EXTR0000                      * extract data into temp file
          CALL      RPRT0000                      * Print report 
          GOTO      ML0100
.
ML9999    CALL      KILL0000                      * erase temp files
          CHAIN     PGM
+
.****************************************************************************
.*  INIT0000  :  Initialisation                                             *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          PACK      CFNAME,UID,FILBOKA3
          OPEN      OUTBOKA3,CFNAME
.
          PACK      CFNAME,UID,FILBB1A1
          CALL      OPOTBB11
          PACK      CFNAME,UID,FILBB1A2
          CALL      OPOTBB12
.
          DISPLAY   *P55:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P55:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P55:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          PACK      CFNAME,UID,FILCLIA1          * opening outcliaf
          DISPLAY   *P55:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          DISPLAY   *P55:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P55:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
.         DISPLAY   *P55:24,"conecraf"
.         OPEN      CONECRA1,"conecraf"
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME
.
INIT9999  RETURN
+
.****************************************************************************
.*  MENU0000  :  Main Menu                                                  *
.****************************************************************************
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report "
.                
          DISPLAY   *P1:7,"Select Option : "
.                  
MENU1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BRANCH    MOPTION,MENU8000
.
          BEEP
          GOTO      MENU1000
.
MENU8000  MOVE      ZERO,EXIT
          GOTO      MENU9999
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.*********************************************************************
.*        DSCR0000                               Called by ML0000    *
.*        Display the screen                                         *
.*********************************************************************
DSCR0000  MOVE      ZERO,EXIT 
          DISPLAY   *P1:9,*EF
          DISPLAY   *P1:10,"Attendance Date from       : ",*P30:10,*EL:
                    *P1:11,"                  to       : ",*P30:11,*EL
          DISPLAY   *P1:13,"District of Residence from :",*P30:13,*EL:
                    *P1:14,"                        to : ",*P30:14,*EL
DSCR9999  RETURN
+
.*********************************************************************
.*        KDOR0000                               Called by ML0000    *
.*        Keyin consultant range                                     *
.*********************************************************************
KDOR0000  MOVE      ZERO,EXIT 
          MOVE      ONE,CNEWDS
          DISPLAY   *P30:13,*EL:
                    *P30:14,*EL
          MOVE      TEN3,EVERT
.
          MOVE      ZERO,CKYIMAND
          UNPACK    SP30,STRTCON,ENDCON
          PACK      DOR1,SP30,SP30
          PACK      DOR2,SP30,SP30
          MOVE      "30",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      "DA",CODE
.
          CALL      PATCODKY
          BRANCH    EXIT,KDOR1000,KDOR9000
          MOVE      ACODE,STRTCON
          DISPLAY   *P36:EVERT,TDESC
          PACK      DOR1,STRTCON,SP2,TDESC
          GOTO      KDOR5000
KDOR1000  MOVE      SP10,STRTCON
          DISPLAY   *P30:EVERT,*V2LON,START   
          MOVE      START,DOR1
          MOVE      ZERO,EXIT
.
KDOR5000  MOVE      TEN4,EVERT
          CALL      PATCODKY
          BRANCH    EXIT,KDOR6000,KDOR9000
          MOVE      ACODE,ENDCON 
          DISPLAY   *P36:EVERT,TDESC
          PACK      DOR2,ENDCON,SP2,TDESC
          GOTO      KDOR8000
KDOR6000  MOVE      "zzzzzzzzz",ENDCON 
          DISPLAY   *P30:EVERT,*V2LON,FINISH 
          MOVE      FINISH,DOR2
          MOVE      ZERO,EXIT
.
KDOR8000  MATCH     STRTCON,ENDCON
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Invalid Doctor Range. ";
            CALL      HITENTER
            GOTO      KDOR0000
          ENDIF
          GOTO      KDOR9999
.
KDOR9000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report ":
                    *P1:7,"Select Option : ",*V2LON,MOPTION
          MOVE      ONE,EXIT
KDOR9999  RETURN
+
.**************************************************************************
.*  KDAT0000  :   Keyin a date range                                      *
.**************************************************************************
KDAT0000  PACK      TODAY,CCC,CYY,CMM,CDD         * get todays date
          REP       " 0",TODAY
.
. keyin from date
.
          UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "30",CCOL
          DISPLAY   *P30:13,*EL:
                    *P30:14,*EL
          MOVE      TEN,CVERT
          MOVE      CCC,CCENT
          MOVE      ZERO,CDEFDTE
          MOVE      "00000000",FROMDATE
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT9000                * anything input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          PACK      FDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    * display var
          REP       " 0",FDDATE 
.
          MATCH     FROMDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date must not be into the future.  ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
.
. keyin to date
.
KDAT5000  UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "30",CCOL
          MOVE      TEN1,CVERT
          MOVE      CCC,CCENT
          MOVE      ZERO,CDEFDTE
          MOVE      "99999999",TODATE
.
KDAT6000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT6000
          BRANCH    OVRCD,KDAT0000                * anything input?
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          PACK      TDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    * display var
          REP       " 0",TDDATE 
.
          MATCH     TODATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date must not be into the future.  ";
            CALL      HITENTER
            GOTO      KDAT5000
          ENDIF
.
. check if dates entered are valid (range)
.
          REP       " 0",FROMDATE
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT8000 IF LESS
.
          MOVE      ZERO,EXIT                     * Valid range
          GOTO      KDAT9999
.
KDAT8000  DISPLAY   *P1:24,*EL,*B,"Invalid date range.  ";
          CALL      HITENTER
          GOTO      KDAT0000
. 
KDAT9000  MOVE      ONE,EXIT                      * nothing input
.
KDAT9999  RETURN
+
.****************************************************************************
.*  EXTR0000  :  Extract data into temp file                                *
.****************************************************************************
EXTR0000  DISPLAY   *P1:24,*EL,*P35:24,"Scanning... [        ]";
.
. extract all ECR patients who have future appointments
.
          PACK      KEY16,FROMDATE,SP20
          CALL      RDSBOKB2
EXTR1000  CALL      RDKBOKB2
          BRANCH    OVRCD,EXTR9999                * no more records
.
          DISPLAY   *P48:24,OBAOUTNO;
.
          MATCH     OBADATE,TODATE                  * date in range?
          GOTO      EXTR9999 IF LESS
.
. get U/R Number
.
          PACK      KEY8,OBAOUTNO
          CALL      RDVISA1
          BRANCH    OVRCD,EXTR1000
.
. get District of Residence
.
          PACK      KEY8,PVIURNO
          CALL      RDMAST1
          BRANCH    OVRCD,EXTR1000
.
          MATCH     STRTCON,PTMXDOFR              * in range?
          GOTO      EXTR1000 IF LESS
          MATCH     PTMXDOFR,ENDCON               * in range?
          GOTO      EXTR1000 IF LESS
.
          PACK      SAVKEY16,OBADATE,OBAOUTNO     * save position on file 
.
. now get all future attendances
.
          PACK      KEY36,PVIURNO,TODAY,SP20,SP20
          CALL      RDSBOKA3
EXTR3000  CALL      RDKBOKA3
          BRANCH    OVRCD,EXTR1000
.
          MATCH     PVIURNO,OBAURNO
          GOTO      EXTR1000 IF NOT EQUAL
.
. prepare for write to temp file
.
          MOVE      OBADATE,XXDATE
          MOVE      PTMXDOFR,XXDOR
          MOVE      OBAURNO,XXURNO
          MOVE      OBAOUTNO,XXOUTNO
          MOVE      OBATIME,XXTIME
          MOVE      OBACLIN,XXCLIN
.
          MOVE      "<Missing>",TDESC
          PACK      KEY5,ANSD,ANSA,XXDOR
          CALL      RDCODE1
          MOVE      TDESC,XXDDOR
.
          MOVE      PGNAME,PACGNAME               * format patient name
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,XXNAME
.
          PACK      KEY20,OBASITE,XXCLIN,XXDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OBASITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     XXCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          MOVE      OCADESC,XXDCLIN
          BRANCH    OVRCD,EXTR6000
.
          MATCH     SP6,OCADOCT
          IF        !@EQUAL
            MOVE      "<Missing>",XXDCLIN
            MOVE      OCADOCT,KEY6
            CALL      RDDOCT1
            IF        OVRCD=0
              MOVE      DGNAME,PACGNAME
              MOVE      DSNAME,PACSNAME
              MOVE      DTITL,PACTITLE
              MOVE      ONE,PACFRMT
              CALL      PACNAME
              MOVE      PACFNAME,XXDCLIN
            ENDIF
          ELSE
            MOVE      OCADESC,XXDCLIN
          ENDIF
.
. get ECR approval number of this future booking
.
EXTR6000  MOVE      "<Missing>",OTBBECRA
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
.
          MOVE      OTBBECRA,XXAPPNO              * new ECR approval number
.
.         restore position on bokb
.
          MOVE      SAVKEY16,KEY16
          CALL      RDBOKB2
.
. write a record
.
          PACK      KEY27,XXDATE,XXDOR,XXURNO,XXOUTNO
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
          GOTO      EXTR3000                      * try for another attendance
.
EXTR9999  RETURN
+
.****************************************************************************
.*  RPRT0000  :  Print report from temp file                                *
.****************************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      SP8,SAVDATE
          MOVE      SP3,SAVDOR
.
          CALL      HEAD132
.
          PRINT     *40,"ECR attendance Date Range : ",FDDATE,"  -  ":
                    TDDATE,*N
          PRINT     *40,"District of Residence from: ",DOR1
          PRINT     *40,"                        to: ",DOR2,*N
          MOVE      TEN,CLNO
.
          PACK      KEY27,SP30
          CALL      RSTEMP1 
RPRT0500  CALL      RKTEMP1                       * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
. check if we need a new page
.
          COMPARE   "55",CLNO
          GOTO      RPRT5000 IF LESS
.
. we need a new page
.
          CALL      UND132
          CALL      HEAD132
          PRINT     *40,"ECR attendance Date Range : ",FDDATE,"  -  ":
                    TDDATE,*N
          PRINT     *40,"District of Residence from: ",DOR1
          PRINT     *40,"                        to: ",DOR2,*N
          MOVE      TEN,CLNO
          MOVE      SP8,SAVDATE
          MOVE      SP3,SAVDOR
.
RPRT5000  
. print a line of data
.
          MATCH     XXDATE,SAVDATE               * print date?
          IF        !@EQUAL
            MATCH    SP8,SAVDATE
            IF       !@EQUAL
              CALL     UND132
            ENDIF
            UNPACK    XXDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            PRINT     *N,*1,"Appointment date: ",CPCDATE
            CALL      PTAB0000                      * print headings
            CALL      UND132
            ADD       "3",CLNO
            MOVE      SP3,SAVDOR
          ENDIF
.
          MATCH     XXDOR,SAVDOR                  * print dist. or residence?
          IF        @EQUAL
            MOVE      SP30,PRDOR
          ELSE
            PACK      PRDOR,XXDOR,SP2,XXDDOR
            MATCH     SP3,SAVDOR
            IF        !@EQUAL
              PRINT     *1,"|",*132,"|"
            ENDIF
          ENDIF
.
          MOVE      XXTIME,KEY5                   * shorten length
          PRINT     *1,"|  ",PRDOR,*30,"| ",XXURNO,*41,"| ",XXNAME:
                    *66,"| ",KEY5,*74,"| ",XXCLIN,SP2,XXDCLIN,*109,"| ",XXAPPNO:
                    *132,"|"
.
          ADD       ONE,CLNO                      * increment line counter
          MOVE      XXDATE,SAVDATE                * save date
          MOVE      XXDOR,SAVDOR                  * save district of residence
          MOVE      XXURNO,SAVURNO                * save U/R Number
.
. now print all EXTRA FUTURE bookings this patient may have
.
          MOVE      ZERO,PROTBKHD                 * print heading flag
          PACK      SAVKEY27,XXDATE,XXDOR,XXURNO,XXOUTNO    * save position
          PACK      KEY27,XXURNO,XXDATE,SP30
          CALL      RSTEMP2
          CALL      RKTEMP2                       * get this future booking
RPRT6000  CALL      RKTEMP2                       * get other future bookings
          BRANCH    OVRCD,RPRT8000                * no more other future books
.
          MATCH     SAVURNO,XXURNO                * same patient?
          GOTO      RPRT8000 IF NOT EQUAL
.
. now print the extra future booking
.
          UNPACK    XXDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      XXTIME,KEY5                   * shorten length
          IF        PROTBKHD=1
            PRINT     *1,"|",*30,"|",*55,CPCDATE:
                      *66,"| ",KEY5,*74,"| ",XXCLIN,SP2,XXDCLIN,*109,"| ":
                      XXAPPNO,*132,"|"
          ELSE
            PRINT     *1,"|",*30,"| Other Future bookings: ",*55,CPCDATE:
                      *66,"| ",KEY5,*74,"| ",XXCLIN,SP2,XXDCLIN,*109,"| ":
                      XXAPPNO,*132,"|"
            MOVE      ONE,PROTBKHD                * dont print heading again
          ENDIF
          ADD       ONE,CLNO                      * increment line counter
          GOTO      RPRT6000
.
. we have no or no more other future bookings to print
.
RPRT8000  MOVE      SAVKEY27,KEY27                * reposition on temp file
          CALL      RDTEMP1
          GOTO      RPRT0500
.
RPRT9000  CALL      UND132                        * print underlines
          PRINT     *N,*1,"*** End of report ***" 
.
RPRT9999  RETURN
+
.****************************************************************************
.*  PTAB0000  :  Print Table Heading                                        *
.****************************************************************************
PTAB0000  CALL      UND132
          PRINT     *1,"|  District of Residence",*30,"| U/R No.",*41,"| ":
                    "Patient Name",*66,"| Time",*74,"| Clinic",*109:
                    "| ECR Approval No.",*132,"|"
.
PTAB9999  RETURN
+
.*************************************************************************
.*                             KILL0000             Called by:           *
.*                   erase temporary file                                *
.*************************************************************************
KILL0000  CLOSE     TEMPFIL1
          CLOSE     TEMPFIL2
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 128 U1-8,9-11,12-19,20-27 U12-19,1-8,9-11,20-27",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL1,FNAME
          OPEN      TEMPFIL2,FNAME
.
CRET9999  RETURN
+
.*************************************************************************
.*  Temp I/O's                                                           *
.*************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY27
          READ      TEMPFIL1,KEY27;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   RESET     KEY27
          READ      TEMPFIL1,KEY27;;
          RETURN
.
RDTEMP1   RESET     KEY27
          MOVE      ZERO,OVRCD
          READ      TEMPFIL1,KEY27;XXDATE,XXDOR,XXURNO,DXXOUTNO,XXNAME,XXTIME:
                             XXCLIN,XXDCLIN,XXAPPNO,XXDDOR
          GOTO      OVERCOND IF OVER
          MOVE      DXXOUTNO,XXOUTNO
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFIL1;XXDATE,XXDOR,XXURNO,DXXOUTNO,XXNAME,XXTIME:
                             XXCLIN,XXDCLIN,XXAPPNO,XXDDOR
          GOTO      OVERCOND IF OVER
          MOVE      DXXOUTNO,XXOUTNO
          RETURN
.
WRTEMP1   RESET     KEY27
          MOVE      XXOUTNO,DXXOUTNO
          WRITE     TEMPFIL1,KEY27;XXDATE,XXDOR,XXURNO,DXXOUTNO,XXNAME,XXTIME:
                             XXCLIN,XXDCLIN,XXAPPNO,XXDDOR
          RETURN
.
RSTEMP2   RESET     KEY27
          READ      TEMPFIL2,KEY27;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMPFIL2;XXDATE,XXDOR,XXURNO,DXXOUTNO,XXNAME,XXTIME:
                             XXCLIN,XXDCLIN,XXAPPNO,XXDDOR
          GOTO      OVERCOND IF OVER
          MOVE      DXXOUTNO,XXOUTNO
          RETURN
+
.
          INC       STD001IO
          INC       PATCODKY
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
.         INC       CONECRIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATCODIO/INC
          INC       OUTCLIIO/INC
          INC       PATVISIO/INC
          INC       WEBERRIO/INC
.

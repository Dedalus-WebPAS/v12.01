.******************************************************************************
.* System         : Patient                                                   *
.* Program        : IBAINP85.PBL                                              *
.* Name           : NSW Cancer Register Extract                               *
.******************************************************************************
.* Date           : 04/04/2008                                                *
.* Author         : Mike Laming                                               *
.* Function       : This program extracts data in the format required for     *
.*                  New South Wales Cancer Register (Used by Private Hosp)    *
.*                                                                            *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
.* V12.01.02 27/01/2026  PJ Le Febour  TSK 0960253a                           *
.*            TEMPCSVF CXTCANFL amend FIXED=2747 due to increase XURNUMBR to 8* 
.* V12.01.01 22/01/2026  PJ Le Febour  TSK 0960253                            *
.*            Correction of XURNUMBR length from 7 to 8                       *
.* V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                            *
.*            Added alpanumeric visit number generation -CLRCRD00,GCAN5000    *
.* V11.04.02 04/06/2024  Thanh T        TSK 0944479                           *
.*           Changed NDIS0000 to extract based on cancer coding date range    *
.*           replacing discharge date range                                   *
.* V11.04.01 21/11/2023  Thanh T        TSK 0939233                           *
.*           Initialize PTCDFCPR field with 280 spaces instead of 90 spaces   *
.* V11.00.01 18/06/2020  Ebon Clements  TSK 0965532                           *
.*           Exclude patient from the extract if the admission criteria       *
.*           PMVXCADM Cat VB - indicator 5 = X                                *
.* V10.14.01 16/07/2019  Nicole Torrisi TSK 0877983                           *
.*           Removed leading spaces from XDIAGLOC when single digit           *
.* V10.13.02 29/01/2019  Nicole Torrisi TSK 0869449                           *
.*           Changed length of XDIAGLOC from 1 to 2                           *
.*           Added new values to State of Residence at Diagnosis (XDIAGLOC)   *
.* V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                            *
.*           Deleted temp file (EXTCANFL & CXTCANFL) after processing.        *
.*           Corrected code to only set Extract Format (EFRMTYPE) in XFRM0000.*
.* V10.12.04 06/06/2018  Ebon Clements  TSK 0857979                           *
.*           Fixed GP address - XGPDOCAD                                      *
.*           Increased GP and Treating Dr name to 250 chars XDOCNAMR XTRDOCNM *
.*           Fixed GP registration number -  XGPNAME                          *
.* V10.12.03 06/06/2018  Ebon Clements  TSK 0857979                           *
.*           Fixed reporting of the principal diagnosis - XTRGPRID            *
.* V10.12.02 05/06/2018  Ebon Clements  TSK 0857979                           *
.*           Fixed other and no treatment flag population - XOTHRFLG XNOTRFLG *
.* V10.12.01 08/03/2018  J.Tan          TSK 0842287                           *
.*           Mod to use Category Bd for 'Best Basis for Diagnosis'            *
.* V10.11.04 26/09/2017  Richa Phogat   TSK 0845455                           *
.*           Added Quotes in DREX0000 while generating CSV extract file       *
.* V10.11.03 20/09/2017  Ken Bell       TSK 0845465                           *
.*           Remove date check extracting post 2010 Registration No XTRDOCRG  *
.* V10.11.02 31/07/2017  Ebon Clements  TSK 0842287                           *
.*           Remove date check extracting post 2010 Registration No XTRDOCRG  *
.* V10.11.02 31/07/2017  Ebon Clements  TSK 0842287                           *
.*           Use HDP if PTCDSTAG and PTCDBSDG if populated.                   *
.*           Report 01019999 if date of first diagnosis is blank              *
.*           Report Y for surgery, systemic therapy and readiotherapy flags   *
.* V10.11.01 18/07/2017  Davin                                     TSK 0840363*
.*           Set extract format as 2nd parameter to ibainp85.us1 (1=txt/2=csv)*
.******************************************************************************
.* V10.10.01 11/05/2017  Thanh T.                                  TSK 0837484*
.*           Fixed the commas within the address error                        *
.* V10.09.01 04/01/2017  J.Tan                                     TSK 0828585*
.*           Changed XPATHLAB, XBASDIAG, XGPNAME and XTRDOCRG                 *
.* V10.08.02 13/05/2016  J.Tan                                     TSK 0315587*
.*           Added 'Extract Format' option                                    *
.* V10.08.01 12/04/2016  Steve Armstrong                           TSK 0813958*
.*           Added missing PATCRDFD variables to CLRCRD00                     *
.******************************************************************************
.* V10.07.01  13/10/2015 Peter Vela     CAR 316932                            *
.*            Added date of death validation to XSEPSTAT                      *
.******************************************************************************
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved TFILENAM to INIT000                                       *
.******************************************************************************
.* V10.03.02  28/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.03.01 18/11/2011  Mike Laming     CAR 240184                    *
.*                  Changes to IBAPOSTF Post Code table - added State to Keys *
.******************************************************************************
.*        V10.02.03 13/07/2011  Steve Armstrong   CAR 240722                  *
.*                  Further mods to cater for second given name as part of    *
.*                  PATGSRFD keys                                             *
.*        V10.02.02 22/06/2011  Steve Armstrong   CAR 240722                  *
.*                  Mods to cater for changes to PATGSRFD:                    *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.            *
.*                       - PTGSGNM2 added.                                    *
.*                       - all indexes changed in length.                     *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.******************************************************************************
.*        V10.00.01  Mike Laming   CAR 208570                                 *
.*                   Recompiled for changes to PATCRDFD.                      *
.******************************************************************************
.*        V9.12.03  03/03/2010  Jill Parkinson   CAR 204945                   *
.*                  Added Hospital name to report                             *
.*        V9.12.02  01/02/2010  Jill Parkinson   CAR 204945                   *
.*                  Added check for blank pthscfcd                            *
.*        V9.12.01  23/02/2010  Jill Parkinson   CAR 204945                   *
.*                  Added check for multi hospital                            *
.******************************************************************************
.*        V9.11.02  08/12/2008  Ian Farnell      CAR 181754                   *
.*                  added check for Other facility at GCAN5050                *
.*                  changed XHOSPNO to read PTCNESTB in lieu CHCSCOD          *
.*                  08/12/2008  Ian Farnell      CAR 174121                   *
.*                  Fix local doctor and Suburb (Alias seem OK)               *
.*                                                                            *
.*        V9.11.01  27/10/2008  Sandra Barcham   CAR 181754                   *
.*                  Changing hospital from CFILENO to CHCSCOD                 *
.******************************************************************************
.*        V9.10.03  30/09/2008  Davin            CAR 174653                   *
.*                  Move '.txt' file to extracts directory (mvext000)         *
.*                  Populate fromdate & todate in report                      *
.*        V9.10.02  19/06/2008  Mike Laming      CAR 165606                   *
.*                  Correct "Spread" to use PTCDCELT (not PTCDSTAG)           *
.*        V9.10.01  29/04/2008  Mike Laming      CAR 165606                   *
.*                  Mods to Header record & MVEXT000 commented out            *
.******************************************************************************
.*        V9.09.00  03/04/2008  Mike Laming      CAR 165606                   *
.*                  New program - cloned from IBAINP85 V6.16 (NSW Extract)    *
.******************************************************************************
.
.         FD's required
.
          INC       STD001FD/PBL
          INC       IBAPOSFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       MRTCONFD/INC
          INC       PATCALAG/INC            * for calcage
          INC       PATCANFD/INC            * patient codes file
          INC       PATCODFD/INC            * patient codes file
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATCRDFD/INC            * Cancer Register Detail File
          INC       PATCRGFD/INC            * Cancer Register Header File 
          INC       PMSCDGFD/INC            * Cancer Reason for Clinical Diagn.
          INC       PATDO1FD/INC            * Patient Doctors File
          INC       PATDSCFD/INC            * Discharge File
          INC       PATECDFD/INC            * Patient episode disease file
          INC       PATECOFD/INC            * Patient episode Procedure file
          INC       PATGSRFD/INC            * Patient name file           
          INC       PATICDFD/INC            * ICD-9 Codes
          INC       PATMA1FD/INC            * patient master file
          INC       PATHSPFD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSPX2FD/INC
          INC       PATMI1FD/INC            * admission file
          INC       PATVISFD/INC            * Visit File
          INC       PMIVARS/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
Z15       INIT      "zzzzzzzzzzzzzzz"      * how it feels removing port numbers
.
. ------- program variables -------
.
CATBD     INIT      "BD"
CATC      INIT      "C "
CATD      INIT      "D "
CATDD     INIT      "DD"
CATM      INIT      "M "
CATHI     INIT      "HI"                                               
CATLB     INIT      "LB"                                               
CATVA     INIT      "VA"
CATLY     INIT      "LY"
CATTI     INIT      "TI"                                              
CATP1     INIT      "P1"
CATP2     INIT      "P2"
CATP3     INIT      "P3"
CATP4     INIT      "P4"
CATP5     INIT      "P5"
CATP6     INIT      "P6"
CATSS     INIT      "sS"
CATU1     INIT      "U1"
CATU2     INIT      "U2"
CATU3     INIT      "U3"
CATU4     INIT      "U4"
CATU5     INIT      "U5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
CATU8     INIT      "U8"
CATU9     INIT      "U9"
CATV1     INIT      "V1"
CATV2     INIT      "V2"
CODED     INIT      "Coding"
DISCHD    INIT      "Discharge"
DQTE      INIT      "#""
CODENM    INIT      "NM"
QUOTE     INIT      "#""
DELIM     INIT      "#",#""
EFRMTYPE  FORM      1
ERRFLAG   FORM      1
VALERRFL  FORM      1
UPDATFLG  FORM      1
.
QCREXTN   INIT      ".NCR"
.
JAN       INIT      "JANUARY"
FEB       INIT      "FEBRUARY"
MAR       INIT      "MARCH"
APR       INIT      "APRIL"
MAY       INIT      "MAY"
JUN       INIT      "JUNE"
JUL       INIT      "JULY"
AUG       INIT      "AUGUST"
SEP       INIT      "SEPTEMBER"
OCT       INIT      "OCTOBER"
NOV       INIT      "NOVEMBER"
DEC       INIT      "DECEMBER"
.
ADD       DIM       2
ADMDATE   DIM       10
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
BASDIAGN  DIM       2
BJDAY     FORM      4
CONAME    DIM       35
CHG       FORM      1
CJDAY     FORM      4
CMDLINE   DIM       80
CODE      DIM       2
.
DIAGCODE  FORM      2                                                 
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8
DIM8A     DIM       8
DIM9      DIM       9 
DIM20     DIM       20
DIM25     DIM       25
DIM45     DIM       45
DIM50     DIM       50
DOB       DIM       10
ENDDATE   DIM       8 
FNAMEA    DIM       8
FNAMEB    DIM       8
FORM3     FORM      3
FORM12    FORM      12
FROMDATE  DIM       10
..ICDRDDTE  DIM       8
HOSPCODE  DIM       3
IBCNMHOS  FORM      1
KEY30A    DIM       30
KEY26A    DIM       30
LINENO    FORM      2
MODE      FORM      1
MODE1     DIM       1
MONTH3    DIM       3
MULTCCNT  FORM      2
PREVADMN  DIM       8
REPET     FORM      2
SAVURNO   DIM       8
SECNDFLG  FORM      1
STRTDATE  DIM       8 
STRTFLAG  FORM      1
TARGETEL  FORM      1                                                 
TODATE    DIM       10
THEDATE   DIM       10
VALDFLAG  FORM      1
.
PATHDESC  DIM       50
DIAGDESC  DIM       50
.
CANCOUNT  FORM      5        Number of Cancer Records
.
ADMNOARR  DIM       8[9999]
ADMNOCNT  FORM      4       
FOUNDFLG  FORM      1
.
.  Header Details (HDR) File *************************************************
.
. Field   Type    Size        Description
. -----   ----    ----        ----------------
XRECTYH   DIM       1         Record Type "H" Header
XHOSPNO   DIM       4         Facility Code/Hospital No.
XNOOFCAN  DIM       5         Number of Cancer Records
XRUNDATE  DIM       8         Production Date (yyyymmdd)
. 
.  Header for CSV format
.
. Field   Type    Size        Description
. -----   ----    ----        ----------------
.XHOSPNO   DIM       4         Facility Code/Hospital No.
.STRTDATE  DIM       8         Start date
.ENDDATE   DIM       8         End date
.XRUNDATE  DIM       8         Creation Date (ddmmyyyy)
XRUNTIME   DIM       4         Creation time (hhmm)
FCOUNTER   FORM      11        Number of Cancer Records
.NTYPEID   DIM       1         Report type O-Outpatient I-Inpatient
.
.  Cancer Details (CAN) File
.
TEMPCANF  IFILE     SQL, FIXED=596, KEY="U1-5"
XCANNUMB  DIM       5
.
EXTCANFL  FILE      ASCII, FIXED=595     * Extract File 
.
. Field   Type    Size        Description
. -----   ----    ----        ----------------
XRECTYT   DIM       1    1    Must be "T"
.XHOSPNO   DIM      4    2    Facility code (as for header record)    (PTCNESTB)
XPSNAME   DIM       24   6    Surname                                 (psname)
XPGFNAME  DIM       12   30   First given name                        (pgname)
XPGSNAME  DIM       12   42   Second given name                       (pgname)
XAL1SNAM  DIM       24   54   Alias 1: Surname (Max. 3 Aliases)
XAL1FNAM  DIM       12   78   Alias 1: First given name
XAL1GNAM  DIM       12   90   Alias 1: Second given name
XAL2SNAM  DIM       24   102  Alias 2: Surname
XAL2FNAM  DIM       12   126  Alias 2: First given name
XAL2GNAM  DIM       12   138  Alias 2: Second given name
XAL3SNAM  DIM       24   150  Alias 3: Surname
XAL3FNAM  DIM       12   174  Alias 3: First given name
XAL3GNAM  DIM       12   186  Alias 3: Second given name
XSEX      DIM       1    198  Sex
XINDIGST  DIM       1    199  Indig. Status    see IBAPAT98.IBA Scr4.7 for UDF
.                             1=Abor. 2=TorresSt.Island 3=Both 4=Neither
.                             8=Declined 9=Not Stated
XURNUMBR  DIM       8    200  Medical record number.                 (dpurno)
XDOB      DIM       8    208  Date of birth. (yyyymmdd)              (bdate)
XFLATNO   DIM       5    216  Flat number
XSTRTNO   DIM       4    221  Street number
XSTRTNAM  DIM       24   225  Street name                            (padd1)
XSTRTTYP  DIM       2    249  AR = Arcade
XLOCATN   DIM       24   251  Town/Suburb                   (padd2) or (psubrb)
XPOSTCOD  DIM       4    275  Postcode                          patmastf.ppost
XCOUNTRY  DIM       4    279  Country of birth.                 patmastf.pcont
XMEDICAR  DIM       10   283  Medicare number                   patmastf.pmedi
XDOCNAM   DIM       24   293  Name of doctor in charge of case  patmissf.adocta
XGPNAME   DIM       24   317  Name of GP (or referring doctor)
XGPADDR   DIM       40   341  Address of GP (or referring doctor)
XADMDATE  DIM       8    381  Date of admission (yyyymmdd)    (adate or pvidate)
XSEPDATE  DIM       8    389  Date of separation (yyyymmdd)          (ddate)
XSEPSTAT  DIM       2    397  For PUBLIC notifiers-             (dstat or ddest)
.                             For PRIVATE notifiers - Alive=01 Dead=07
XMULTPRI  DIM       1    399  Multiple Primaries - 1=Yes 2=No     (MULTCCNT)
XDIAGDTE  DIM       6    400  Date of first diagnosis (yyyymm)
.                                                          (ptcddgpr & ptrgddte)
XDIAGLOC  DIM       2    406  Resid.State when Diagnosed 1=NSW 2=Vic
XLATERY   DIM       1    407  Cancer of paired organs:            (ptcdlatr)
.                             1=Left 2=Right 3=N/A 9=Unknown
XLABNAM   DIM       25   408  Name of Lab for Pathology            (ptcdbdd1-9)
XBASDIAG  DIM       1    433  Best basis for diagnosis at this admission:
.                                                                  (ptcdbdc1-9)
.                             1=Histology 2=Cytology 3=Other
XDRGSPRD  DIM       1    434  Degree of spread of cancer           (ptcdstag)
.                             1=Local 2= Invasive 3=Reg.Nodes etc
XREASADM  DIM       1    435  Was this admission for Cancer?
.                                                     (dptecode & dptetype='P')
.                             1 = Yes 2 = No
XTRGPRIM  DIM       8    436  Valid topography code                (ptrgprim)
XTRGMORP  DIM       8    444  Valid morphology code                (ptrghist)
XTRGCOD1  DIM       8    452  Valid diagnosis code(s)
.                                                    (dptecode & dptetype!='P')
XTRGCOD2  DIM       8    460  Valid diagnosis code(s)
XTRGCOD3  DIM       8    468  Valid diagnosis code(s)
XTRGCOD4  DIM       8    476  Valid diagnosis code(s)
XTRGCOD5  DIM       8    484  Valid diagnosis code(s)
XTRGCOD6  DIM       8    492  Valid diagnosis code(s)
XTRGCOD7  DIM       8    500  Valid diagnosis code(s)
XTRGCOD8  DIM       8    508  Valid diagnosis code(s)
XPRMDIAG  DIM       8    516  Principal diagnosis this admission.
.                                 patecdaf.dptecode where patecdaf.ptetype!='P'
XPRMPROC  DIM       8    524  Valid Procedure code
.                                  patecoaf.dptecode where patecoaf.ptocnt='1'.
XSPARE    DIM       60   532  Unused OR histology text
.
.         End of Record  592
.
..TEMPCSVF  IFILE     SQL, FIXED=2747, KEY="U1-11"
TEMPCSVF  IFILE     SQL, FIXED=2747, KEY="U1-11"
.
. Details record for CSV format
.
..CXTCANFL  FILE      ASCII, FIXED=2747    * Comma delimited Extract File 
CXTCANFL  FILE      ASCII, FIXED=2747    * Comma delimited Extract File 
.
XCOUNTER  DIM       11   1    Counter
.XHOSPNO  DIM       4    12   Facility code (as for header record)    (PTCNESTB)
.XMEDICAR DIM       10   16   Medicare number                   patmastf.pmedi
.XURNUMBR DIM       8    26   Medical record number.                 (dpurno)
.XPSNAME  DIM       24   34   Surname                                 (psname)
XFRSNAME  DIM       40   58   First given name                        (pgname)
XGIVNAME  DIM       40   98   Second given name                       (pgname)
XAS1SNAM  DIM       40   138  Alias 1: Surname (Max. 3 Aliases)
XAS1FNAM  DIM       40   178  Alias 1: First given name
XAS1GNAM  DIM       40   218  Alias 1: Second given name
XAS2SNAM  DIM       40   258  Alias 2: Surname
XAS2FNAM  DIM       40   298  Alias 2: First given name
XAS2GNAM  DIM       40   338  Alias 2: Second given name
XAS3SNAM  DIM       40   378  Alias 3: Surname
XAS3FNAM  DIM       40   418  Alias 3: First given name
XAS3GNAM  DIM       40   458  Alias 3: Second given name
.XSEX     DIM       1    498  Sex
.XDOB     DIM       8    499  Date of birth. (ddmmyyyy)              (bdate)
.XCOUNTRY DIM       4    507  Country of birth.                 patmastf.pcont
.XINDIGST DIM       1    511  Indig. Status    see IBAPAT98.IBA Scr4.7 for UDF
.                             1=Abor. 2=TorresSt.Island 3=Both 4=Neither
.                             8=Declined 9=Not Stated
XAFLATNO  DIM       14   512  Flat number
XURASNUM  DIM       6    526  Street number
XURASNAM  DIM       20   532  Street name                            (padd1)
XURARTYP  DIM       20   552  Street type
XURARSUB  DIM       40   572  Town/Suburb                   (padd2) or (psubrb)
.XPOSTCOD DIM       4    612  URA Postcode          patmastf.ppost
XURASTAT  DIM       2    616  URA State
.XGPNAME  DIM       24   618  Registration number of GP
XDOCNAMR  DIM       250  642  GP Doctor's name
XGPDOCAD  DIM       512  892  GP Doctor's address
XTRDOCRG  DIM       20  1404  Treating Doctor Registration name
XTRDOCNM  DIM       250 1424  Treating Doctor's name
XTRDOCAD  DIM       512 1674  Treating Doctor's address
.XADMDATE DIM       8   2186  Date of admission (ddmmyyyy)
.XSEPDATE DIM       8   2194  Date of separation (ddmmyyyy)
.XSEPSTAT DIM       2   2202  Status of Separation 1-Disc.7-Died
.XDIAGLOC DIM       2   2204  Resid.State when Diagnosed 1=NSW 2=Vic..
XPATHLAB  DIM       100 2206  Name of Pathology Laboratory
XDIAGDAT  DIM       8   2306  Date of first diagnosis (yyyymm)
.XTRGPRIM DIM       8   2314  Primary Site of Cancer code
.XTRGMORP DIM       8   2322  Valid morphology code
.XLATERY  DIM       1   2330  Cancer of paired organs:            (ptcdlatr)
.                             1=Left 2=Right 3=N/A 9=Unknown
.XBASDIAG DIM       1   2331  Best basis for diagnosis at this admission:-9)
.XDRGSPRD DIM       1   2332  Degree of spread of cancer           (ptcdstag)
.XREASADM DIM       1   2333  Reason for Admission
XTRGPRID  DIM       8   2334  Principal Diagnosis
.XTRGCOD1 DIM       8   2342  Additional Diagnosis 1
.XTRGCOD2 DIM       8   2350  Additional Diagnosis 2
.XTRGCOD3 DIM       8   2358  Additional Diagnosis 3
.XTRGCOD4 DIM       8   2366  Additional Diagnosis 4
.XTRGCOD5 DIM       8   2374  Additional Diagnosis 5
.XTRGCOD6 DIM       8   2382  Additional Diagnosis 6
.XTRGCOD7 DIM       8   2390  Additional Diagnosis 7
.XTRGCOD8 DIM       8   2398  Additional Diagnosis 8
.XPRMPROC DIM       8   2406  Valid Procedure 1
XADPROC1  DIM       8   2414  Additional Procedure 2
XADPROC2  DIM       8   2422  Additional Procedure 3
XADPROC3  DIM       8   2430  Additional Procedure 4
XADPROC4  DIM       8   2438  Additional Procedure 5
XADPROC5  DIM       8   2446  Additional Procedure 6
XADPROC6  DIM       8   2454  Additional Procedure 7
XADPROC7  DIM       8   2462  Additional Procedure 8
XADPROC8  DIM       8   2470  Additional Procedure 9
XTSTAGEN  DIM       2   2478  TNM State Edition Number - ptcdcscm
XTSTAGEC  DIM       50  2480  T Stage                  - ptcdcsgt
XNSTAGEC  DIM       50  2530  N Stage                  - ptcdcsgn
XMSTAGEC  DIM       50  2580  M Stage                  - ptcdcsgm
XTSTAGEG  DIM       50  2630  TNM Stage Group          - ptcdcssg
XTSTAGEB  DIM       1   2680  TNM Staging group basis  - ptcdbsdg
XOSTAGES  DIM       2   2681  Other Staging Scheme     - ptcdstag
XOSTAGEG  DIM       14  2683  Other Staging Grouping   - ptcdcssg
XOSTAGEB  DIM       1   2697  Other Staging Basis      - ptcdbsdg
XSURGFLG  DIM       1   2698  Surgery flag             - ptcdtsur
XSYSTFLG  DIM       1   2699  Systemic Therapy flag    - ptcdtchm
XSYSTID1  DIM       15  2700  <next release>
XSYSTID2  DIM       15  2715  <next release>
XRADIFLG  DIM       1   2730  Radiotherapy flag        - ptcdtrad
XRADIEID  DIM       15  2731  <next release>
XOTHRFLG  DIM       1   2746  Other flag               - ptcdtbio
XNOTRFLG  DIM       1   2747  No Treatment flag        - ptcdtnon
.EOR                    2747
.
HOMESTTE  DIM       3         State code  
.
.
EXTCANTM  DIM       8                       * Temp Extract File Name
EXTCANFN  DIM       40                      * Extract File Name
.TABDELM   EQU       0x09                   * Tab Delimiter
.
.
TEMPAFIL  IFILE SQL, FIXED=38, KEY="U1-8"
.
. NAME    TYPE   LENGTH   PHYSICAL  START LOC    DESCRIPTION
. ----    ----   ------   --------  ---------    --------------
TEMPADMN  DIM       8       8          1
TEMPSPAR  DIM       30      30         8  
.EOR
.
. ------- program constants -------
SRCHNSW   INIT      "NSW"
SRCHVIC   INIT      "VIC"
SRCHQLD   INIT      "QLD"
SRCHSA    INIT      "SA "
SRCHWA    INIT      "WA "
SRCHTAS   INIT      "TAS"
SRCHNT    INIT      "NT "
SRCHACT   INIT      "ACT"
SRCHOS    INIT      "OS "
SRCHUN    INIT      "UN "
SRCHNST   INIT      "NST"
INP85A    INIT      "inp85a"
INP85B    INIT      "inp85b"
.
.
PRGID     INIT      "IBAINP85"
PRGNAM    INIT      "NSW Cancer Register Extract"
VERSION   INIT      "V12.01.02"
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000                * initilisation
.
ML0500    CALL      OPTN0000                * Get Report Option 
          BRANCH    OPTION,ML1000,ML2000
.
          GOTO      ML9999
.
ML1000    CALL      DTRNG000                * get date range
          BRANCH    EXIT,ML0500
          CALL      CHKMH000
          BRANCH    EXIT,ML0500,ML0500      * check multi hospital
          CALL      XFRM0000                * Keyin Extract format type
          CALL      CRTM0000                * create temp. files
.
          IF        EFRMTYPE=2
            MOVE      ONE,VALDFLAG
            CALL      NDIS0000              * extract by Discharge date
            BRANCH    ERRFLAG,ML0500        * errors found
          ENDIF
.
          MOVE      ZERO,VALDFLAG
          CALL      NDIS0000                * extract by Discharge date
          BRANCH    EXIT,ML0500             * =1 if no extract         
.
          CALL      GCAN0000                * create cancer extract
.
          CALL      CREX0000                * create Extract file from Temp.B
          CALL      MVEXT000                * Move Extract to rename       
          GOTO      ML0500
.
ML2000    CALL      DUMDTE00                * Set Dates for file names
          BRANCH    EXIT,ML0500
          CALL      CHKMH000
          BRANCH    EXIT,ML0500,ML0500      * check multi hospital
          CALL      CRTM0000                * create temp. files
.
          MOVE      ZERO,VALDFLAG
          CALL      NUNS0000                * extract un-sent registrations
          BRANCH    EXIT,ML0500             * =1 if no extract         
          CALL      GCAN0000                * create cancer extract
.
          CALL      CREX0000                * create Extract file from Temp.B
          CALL      MVEXT000                * Move Extract to rename       
          GOTO      ML0500
.
ML9999    MOVE      ZERO,EXIT
          CALL      CLTM0000                * remove temp files
          CHAIN     PGM
.
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
.
          DISPLAY   *P58:24,"ibapostf"
          OPEN      IBAPOST1,"ibapostf"
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P58:24,"patcrdaf"
          OPEN      PATCRDA1,"patcrdaf"
          OPEN      PATCRDA3,"patcrdaf"
          OPEN      PATCRDA4,"patcrdaf"
.
          DISPLAY   *P58:24,"patcrgaf"
          OPEN      PATCRGA1,"patcrgaf"
.
          DISPLAY   *P58:24,"pmscdgaf"
          OPEN      PMSCDGA1,"pmscdgaf"
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"          * files to open
.
          DISPLAY   *P58:24,"patecdaf"
          OPEN      PATECDA1,"patecdaf"
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P58:24,"patecoaf"
          OPEN      PATECOA1,"patecoaf"
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PATMI1A4,"patmi1af"
.
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P58:24,"patcanaf"
          OPEN      PATCANA1,"patcanaf"
.
.....     DISPLAY   *P58:24,"patdadaf"
.....     OPEN      PATDADA1,"patdadaf"
.
          DISPLAY   *P58:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P58:24,"paticddf"
          CALL      OPICD1
.
          DISPLAY   *P58:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P58:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"pmshcgaf";
          OPEN      PMSHCGA1,"pmshcgaf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEA
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEB
.
          READ      CONTROLF,TWO;*4,CONAME
          READ      CONTROLF,EIGHTY8;*242,PNSWRACE
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D
.....     READ      CONTROLF,NINTY7;*151,MRCNTDAD     
.....     READ      CONTROLF,TEN3;*213,CHCSCOD
.....     READ      CONTROLF,TEN;*54,CFILENO
.....     READ      CONTROLF,TEN;*235,CMDISP
          READ      CONTROLF,TEN9;*75,CCNOTES
.....     READ      CONTROLF,TWENTY;*91,PTCNHSEM
.....     READ      CONTROLF,FIFTY9;*132,CALRDESC
          READ      CONTROLF,TWENTY;*177,PTNSWHDP
          READ      CONTROLF,SEVENTY9;*83,PTCNESTB
.....     READ      CONTROLF,TWENTY1;*216,PTCNDTRF
          READ      CONTROLF,TWENTY1;*210,PTCNHOSP
          READ      CONTROLF,TWENTY1;*221,CNSWLOC
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.         
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          DISPLAY   *P58:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"     * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,ICDRDDTE
.                                           * open ICD10 file
          MATCH     PTCNI10D,ICDRDDTE
          IF        @LESS
            OPEN      PATICDD1,"paticddf"
          ELSE
            OPEN      PATI10D1,"pati10df"
          ENDIF
.
.                   Set Printer Stuff 
.
          CLOCK     TIME,CTIMEIS
          MOVE      DISCHD,DIM9
          STRIP     DIM9
          MOVE      ONE,CNOUNDLN
.
INIT9999  RETURN
+
.
.*********************************************************************
.         OPTN0000 Get Main Program Option   Called By ML0000
.                  Display and get U/R or Adno option.
.                  Returns : OPTION  1 = Register Details
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Cancer Extract by Date Range":
                    *P1:6,*V2LON," 2",*HOFF," = Cancer Extract for unregist":
                    "ered cancers" 
OPTN1000  KEYIN     *P1:8," Select Option : ",*V2LON,*P18:8,OPTION;
.
          BRANCH    OPTION,OPTN9999,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
.
+
.**************************************************************************
.                                   DTRNG000
.                    Routine to get the from and to dates.
.**************************************************************************
DTRNG000  MOVE      ZERO,EXIT
          DISPLAY   *P1:4,*EF,*P2:4,"From Date : ":
                    *P2:6,"To Date : ";
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
DTRNG100  MOVE      TEN4,CCOL
          MOVE      FOUR,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,DTRNG100
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE
          PACK      FROMDATE,CPCDATE
          REP       " 0",FROMDATE
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
DTRNG200  MOVE      TEN4,CCOL
          MOVE      SIX,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,DTRNG200
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE
          PACK      TODATE,CPCDATE
          REP       " 0",TODATE
.
.                                           * set up extract file name 
          UNPACK    STRTDATE,DIM4,KEY4
          PACK      EXTCANTM,KEY4,CMON,CDAY,SP10
          REP       " 0",EXTCANTM
.
          DISPLAY   *P1:23,"This Extract will have a file name of '":
                    *V2LON,EXTCANTM,".txt'",*HOFF
.
          CALL      CONTQST
          BRANCH    CEXIT,DTRNG999,DTRNG000,DTRNG900                   
.
DTRNG900  MOVE      ONE,EXIT
.
DTRNG999  RETURN
+
.****************************************************************************
.*                                  DUMDTE00
.*                  set date range for "un-registered cancers"
.****************************************************************************
DUMDTE00  
.         MOVE      PTCNESTB,DIM5
.
.                                           * set up extract file name 
          PACK      EXTCANTM,CMM,CDD,CMM,CDD
          REP       " 0",EXTCANTM
.
          DISPLAY   *P1:23,"This Extract will have a file name of '":
                    *V2LON,EXTCANTM,".txt'",*HOFF
.
DUMDT800  MOVE      ZERO,EXIT
          CALL      CONTQST
          BRANCH    CEXIT,DUMDT999,DUMDT900,DUMDT900
          BEEP
          GOTO      DUMDT800
.
DUMDT900  MOVE      ONE,EXIT
.
DUMDT999  DISPLAY   *P1:23,*EF
          RETURN
+
.**************************************************************************
.                                 NCOD0000
.          Loop thru Admission file for Cancer Reg. by Coding Dates
.**************************************************************************
NCOD0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,CANCOUNT
          MOVE      SP30,TEMPSPAR
          MOVE      SP8,PREVADMN
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSMISS4
NCOD1000  CALL      RDKMISS4
          BRANCH    OVRCD,NCOD8000
.
          MATCH     ACODDTE,ENDDATE     * In Date range
          GOTO      NCOD8000 IF LESS
.
          PACK      KEY8,AADMNO,SP10    
          MATCH     PREVADMN,KEY8
          GOTO      NCOD1000 IF EQUAL
          MOVE      KEY8,PREVADMN
.
          CALL      RDPTCRG1                * Get cancer registation record
          BRANCH    OVRCD,NCOD1000
.
.                                           * found a wanted Cancer Reg.
          MOVE      AADMNO,TEMPADMN
          MOVE      TEMPADMN,KEY8
          CALL      WRTEMP1
          ADD       ONE,CANCOUNT
          GOTO      NCOD1000
.
NCOD8000  COMPARE   ZERO,CANCOUNT
          IF        @EQUAL
            DISPLAY   *P1:24,"No Cancer Registrations to be sent - ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
.
NCOD9999  RETURN
.
.**************************************************************************
. Cancer Reg. by creation and Update dates
.**************************************************************************
NDIS0000  MOVE      ZERO,EXIT
          MOVE      ZERO,ERRFLAG
          MOVE      "99",CLNO
.
          MOVE      ZERO,CANCOUNT
          MOVE      SP30,TEMPSPAR
          MOVE      SP8,PREVADMN
.
          MOVE      ZERO,UPDATFLG           
          CALL      CLRADM00
.
NDIS0500  IF        UPDATFLG = ZERO  
            PACK      KEY41,STRTDATE,SP70    
            CALL      RSPTCRD3
          ELSE
            PACK      KEY41,STRTDATE,SP70
            CALL      RSPTCRD4
          ENDIF
.
NDIS1000  IF        UPDATFLG = ZERO  
            CALL      RKPTCRD3
            BRANCH    OVRCD,NDIS7000
            MATCH     PTCDDTRC,ENDDATE           * In date range
            GOTO      NDIS7000 IF LESS
          ELSE
            CALL      RKPTCRD4
            BRANCH    OVRCD,NDIS8000
            MATCH     PTCDDTUP,ENDDATE           * In date range
            GOTO      NDIS8000 IF LESS
          ENDIF
.
          PACK      KEY8,DPTCDDAD,SP70
          CALL      RDDSCH1
          BRANCH    OVRCD,NDIS1000  
.
          PACK      KEY8,DADMNO,SP70
          MATCH     PREVADMN,KEY8
          GOTO      NDIS1000 IF EQUAL
          MOVE      KEY8,PREVADMN
.
          CALL      RDPTCRG1
          BRANCH    OVRCD,NDIS1000
.
          PACK      KEY8,DADMNO,SP10
          CALL      RDMISS1                 * read admission details
          BRANCH    OVRCD,NDIS1000
          CALL      RDVISA1
          BRANCH    OVRCD,NDIS1000
.
          PACK      KEY8,AURNO,SP10
          CALL      RDMAST1                 * read PMI Details
          BRANCH    OVRCD,NDIS1000
.
          CALL      CLPMSPX2
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
.
          PACK      KEY8,DADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,NDIS1000
          IF        IBCNMHOS=1
            MATCH     HOSPCODE,PMVXMHOS
            GOTO      NDIS1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PMVXCADM           * Admission Criteria
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSB,PMVXCADM,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MATCH     ANSX,TCINDC5        * Exclude from extract
              GOTO      NDIS1000 IF EQUAL
            ENDIF
          ENDIF
.
          COMPARE   TWO,EFRMTYPE
          GOTO      NDIS4000 IF NOT EQUAL   * Not CSV format
.
          COMPARE   ONE,VALDFLAG
          GOTO      NDIS4000 IF NOT EQUAL   * Process
.
          CALL      CHKADM00                * check admission validation array
          IF        FOUNDFLG = 0 
            MOVE      ZERO,VALERRFL
            CALL      VPRM0000              * validate Primary disease date
            CALL      VALD0000              * validate other fields
            IF        VALERRFL = 1 
              CALL      ADDADM00
            ENDIF
          ENDIF 
.
          GOTO      NDIS1000
.                                           * found a wanted Cancer Reg.
NDIS4000  MOVE      DADMNO,TEMPADMN
          MOVE      TEMPADMN,KEY8
          CALL      RDTEMP1
          BRANCH    OVRCD,NDIS4500
.
          GOTO      NDIS1000                * already existed
.
NDIS4500  CALL      WRTEMP1
          ADD       ONE,CANCOUNT
          GOTO      NDIS1000
.
NDIS7000  MOVE      ONE,UPDATFLG
          GOTO      NDIS0500
.
NDIS8000  COMPARE   TWO,EFRMTYPE
          GOTO      NDIS8200 IF NOT EQUAL   * Not CSV format
.
          IF        ERRFLAG=1
            DISPLAY   *P1:23,"Error found. ";
            CALL      HITENTER
            MOVE      ONE,EXIT
            GOTO      NDIS9999
          ENDIF
          BRANCH    VALDFLAG,NDIS9999
.
NDIS8200  COMPARE   ZERO,CANCOUNT
          IF        @EQUAL
            DISPLAY   *P1:24,"No Cancer Registrations to be sent - ";
            CALL      HITENTER
            MOVE      ONE,EXIT
            GOTO      NDIS9999
          ENDIF
.
NDIS9999  RETURN
.
.---------------------------------------------------------------------------
. Clear admission validation array
.---------------------------------------------------------------------------
CLRADM00  MOVE      ZERO,ADMNOCNT
          MOVE      ZERO,F4
          WHILE     F4 < 9999 
            ADD       ONE,F4
            MOVE      SP70,ADMNOARR[F4]             
          DO
.
CLRADM99  RETURN
.
.---------------------------------------------------------------------------
. Add admisson no. into admission validation array
.---------------------------------------------------------------------------
ADDADM00  ADD       ONE,ADMNOCNT
          MOVE      DDADMNO,ADMNOARR[ADMNOCNT]
.
ADDADM99  RETURN
.
.---------------------------------------------------------------------------
. Check existence of the admission no. from the addmission validation array
. Parameters: DDADMNO - admission no.
. Return:     FOUNDFLG
.             0 - admission no. not found
.             1 - admission no. found
.---------------------------------------------------------------------------
CHKADM00  MOVE      ZERO,FOUNDFLG
          MOVE      0,F4
          WHILE     F4 < ADMNOCNT
            ADD       ONE,F4
            MATCH     DDADMNO,ADMNOARR[F4]
            IF        @EQUAL 
              MOVE      ONE,FOUNDFLG
              GOTO      CHKADM99
            ENDIF
          DO
.
CHKADM99  RETURN
+
.**************************************************************************
.                               NUNS0000
.           Loop thru Cancer Reg Details for un-sent registrations
.**************************************************************************
NUNS0000  MOVE      ZERO,EXIT
          MOVE      ZERO,ERRFLAG
          MOVE      "99",CLNO
.
          MOVE      ZERO,CANCOUNT
          MOVE      SP30,TEMPSPAR
          MOVE      SP8,PREVADMN
          PACK      KEY25,SP20,SP10
          CALL      RSPTCRD1                * Get cancer register details
NUNS1000  CALL      RKPTCRD1                
          BRANCH    OVRCD,NUNS8000
.
          MATCH     SP8,PTCDDTEX            * blank Extract Date field ?
          GOTO      NUNS1000 IF NOT EQUAL
.
          MATCH     PREVADMN,DPTCDDAD       * same admission no ?
          GOTO      NUNS1000 IF EQUAL
          MOVE      DPTCDDAD,PREVADMN
.
          PACK      KEY8,DPTCDDAD,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,NUNS1000
          IF        IBCNMHOS=1
            MATCH     HOSPCODE,PMVXMHOS
            GOTO      NUNS1000 IF NOT EQUAL
          ENDIF
.                   
          MOVE      DPTCDDAD,TEMPADMN
          MOVE      TEMPADMN,KEY8
          CALL      WRTEMP1
          ADD       ONE,CANCOUNT
          GOTO      NUNS1000
.
NUNS8000  IF        CANCOUNT = 0
            DISPLAY   *P1:24,"No un-sent Cancer Registrations found - ";
            CALL      HITENTER
            MOVE      ONE,EXIT                                           
            GOTO      NUNS9999
          ENDIF
.
NUNS9999  RETURN
+
.**************************************************************************
.         Validate data for CSV Extract
.**************************************************************************
VALD0000  MATCH     PBDATE,PVIDATE
          IF        @LESS
            COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
            CALL      HEDV0000 IF LESS 
            PRINT    *1,PMVXVISN:
                     *12," Date of Birth must be less or equal to":
                        " Admission Date."
            CALL     SETERR00
          ENDIF
.
          PACK      KEY8,CCC,CYY,CMM,CDD     * Today's date
          REP       " 0",KEY8
.
          MATCH     ADATE,KEY8
          IF        @LESS
            COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
            CALL      HEDV0000 IF LESS 
            PRINT    *1,PMVXVISN:
                     *12," Admission Date must be less or equal to":
                        " Today's Date."
            CALL     SETERR00
          ENDIF
.
          IF        ASTAT=3
            MATCH     PBDATE,DDATE
            IF        @LESS
              COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
              CALL      HEDV0000 IF LESS 
              PRINT    *1,PMVXVISN:
                       *12," Date of Birth must be less or equal to":
                          " Discharge Date."
              CALL     SETERR00
            ENDIF
            MATCH     ADATE,DDATE
            IF        @LESS
              COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
              CALL      HEDV0000 IF LESS 
              PRINT    *1,PMVXVISN:
                       *12," Admission Date must be less or equal to":
                         " Discharge Date."
              CALL     SETERR00
            ENDIF
            MATCH     DDATE,KEY8
            IF        @LESS
              COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
              CALL      HEDV0000 IF LESS 
              PRINT    *1,PMVXVISN:
                       *12," Admission Date must be less or equal to":
                          " Today's Date."
              CALL     SETERR00
            ENDIF
          ENDIF
.
          MATCH     PBDATE,KEY8
          IF        @LESS
            COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
            CALL      HEDV0000 IF LESS 
            PRINT    *1,PMVXVISN:
                     *12," Date of Birth must be less or equal to":
                        " Today's Date."
            CALL     SETERR00
          ENDIF
.
VALD9999  RETURN
.
.---------------------------------------------------------------------------
. Set error flag
.---------------------------------------------------------------------------
SETERR00  MOVE     ONE,ERRFLAG
          MOVE     ONE,VALERRFL
          ADD      ONE,CLNO
.
SETERR99  RETURN
+
.**************************************************************************
.*                                 CLRCRD00
.*                   Clear Cancer Registration Details
.**************************************************************************
CLRCRD00  MOVE      ZEROVISN,PTCDDADN
          MOVE      ZERO,PTCDCNRG
          UNPACK    SP70,PTCDADTE,PTCDDDTE,PTCDTMRC,PTCDMTSP
          MOVE      ZERO,PTCDUKPR
          UNPACK    SP70,PTCDPRMM,PTCDHTYP,PTCDMET1,PTCDMET2,PTCDMET3
          UNPACK    SP70,PTCDLATR,PTCDSTAG
          UNPACK    SP70,PTCDBT01,PTCDBT02,PTCDBT03,PTCDBT04,PTCDBT05
          UNPACK    SP70,PTCDBT06,PTCDBT07,PTCDBT08,PTCDBT09,PTCDBT10
          MOVE      ZERO,PTCDRFLG
          UNPACK    SP70,PTCDOTDG,PTCDPATH,PTCDDGPR
          PACK      PTCDFCPR,SP70,SP70,SP70,SP70
          UNPACK    SP70,PTCDBO01,PTCDBO02,PTCDBO03,PTCDBO04,PTCDBO05
          UNPACK    SP70,PTCDBO06,PTCDBO07,PTCDBO08,PTCDBO09,PTCDDGOT
          MOVE      ZERO,PTCDAUTP
          MOVE      ZERO,PTCDBRLW
          MOVE      ZERO,PTCDBTSZ
          MOVE      ZERO,PTCDGLSC
          UNPACK    SP70,PTCDMLIN,PTCDRPLB,PTCDCKLV,PTCDCOMM         
          UNPACK    SP70,PTCDDTEX,PTCDDTRC,PTCDTMCR,PTCDUSCR         
          UNPACK    SP70,PTCDDTUP,PTCDTMUP,PTCDUSUP,PTCDBSDG         
.
          MOVE      SP70,PTCDCELT
          MOVE      SP70,PTCDDOCC
          MOVE      SP70,PTCDDOCN
          MOVE      SP70,PTCDNTDT
          MOVE      SP70,PTCDSNT1
          MOVE      SP70,PTCDSNT2
          MOVE      SP70,PTCDSNT3
          MOVE      SP70,PTCDBOD1
          MOVE      SP70,PTCDBOD2
          MOVE      SP70,PTCDBOD3
          MOVE      SP70,PTCDBOD4
          MOVE      SP70,PTCDBOD5
          MOVE      SP70,PTCDBOL5
          MOVE      SP70,PTCDBOD6
          MOVE      SP70,PTCDBOL6
          MOVE      SP70,PTCDBOD7
          MOVE      SP70,PTCDBOL7
          MOVE      SP70,PTCDGD01
          MOVE      SP70,PTCDGD02
          MOVE      SP70,PTCDGD03
          MOVE      SP70,PTCDGD04
          MOVE      SP70,PTCDGD05
          MOVE      SP70,PTCDGD06
          MOVE      SP70,PTCDGD07
          MOVE      SP70,PTCDGD08
          MOVE      SP70,PTCDGD09
          MOVE      SP70,PTCDPLOD
          MOVE      SP70,PTCDCOD1
          MOVE      SP70,PTCDCOD2
          MOVE      SP70,PTCDCSGT
          MOVE      SP70,PTCDCSGN
          MOVE      SP70,PTCDCSGM
          MOVE      SP70,PTCDCSSG
          MOVE      SP70,PTCDCSCM
          MOVE      SP70,PTCDTNON
          MOVE      SP70,PTCDTSUR
          MOVE      SP70,PTCDTSDT
          MOVE      SP70,PTCDTRAD
          MOVE      SP70,PTCDTRDT
          MOVE      SP70,PTCDTCHM
          MOVE      SP70,PTCDTCDT
          MOVE      SP70,PTCDTHOR
          MOVE      SP70,PTCDTHDT
          MOVE      SP70,PTCDTBIO
          MOVE      SP70,PTCDTBDT
          MOVE      SP70,PTCDRLDT
          MOVE      SP70,PTCDNDCL
          MOVE      SP70,PTCDTSAB
          MOVE      SP70,PTCDTOTH
          MOVE      SP70,PTCDTHOS
          MOVE      SP70,PTCDRST1
          MOVE      SP70,PTCDRST2
          MOVE      SP70,PTCDRST3
          MOVE      SP70,PTCDRST4
          MOVE      SP70,PTCDRST5
          MOVE      SP70,PTCDSURB
          MOVE      SP70,PTCDSTTE
          MOVE      SP70,PTCDPOST
          MOVE      SP70,PTCDMET4
          MOVE      SP70,PTCDMET5
          MOVE      SP70,PTCDMET6
          MOVE      SP70,PTCDECOG
          MOVE      SP70,PTCDRADI
          MOVE      SP70,PTCDSYST
          MOVE      SP70,PTCDSCFL
          MOVE      SP70,PTCDCSSY
          MOVE      SP70,PTCDBDIA
          MOVE      SP70,PTCDSPAR
.
CLRCRD99  RETURN
+
.**************************************************************************
.*                                 CLRCRD00
.*                   Clear Cancer Registration Details
.**************************************************************************
CLRCAN00  UNPACK    SP70,XCANNUMB,XRECTYT,XHOSPNO,XPSNAME,XPGFNAME,XPGSNAME
          UNPACK    SP70,XAL1SNAM,XAL1FNAM,XAL1GNAM
          UNPACK    SP70,XAL2SNAM,XAL2FNAM,XAL2GNAM
          UNPACK    SP70,XAL3SNAM,XAL3FNAM,XAL3GNAM
          UNPACK    SP70,XSEX,XINDIGST,XURNUMBR,XDOB,XFLATNO,XSTRTNO,XSTRTNAM
          UNPACK    SP70,XSTRTTYP,XLOCATN,XPOSTCOD,XCOUNTRY,XMEDICAR,XDOCNAM
          PACK      XDOCNAMR,SP70,SP70,SP70,SP70
          UNPACK    SP70,XGPNAME,XGPADDR
          UNPACK    SP70,XADMDATE,XSEPDATE,XSEPSTAT,XMULTPRI,XDIAGDTE
          UNPACK    SP70,XDIAGLOC,XLATERY,XLABNAM,XBASDIAG,XDRGSPRD,XREASADM
          UNPACK    SP70,XTRGPRIM,XTRGMORP,XTRGCOD1,XTRGCOD2,XTRGCOD3,XTRGCOD4
          UNPACK    SP70,XTRGPRID
          UNPACK    SP70,XTRGCOD5,XTRGCOD6,XTRGCOD7,XTRGCOD8,XPRMDIAG,XPRMPROC
          UNPACK    SP70,XSPARE
.
          BRANCH    EFRMTYPE,CLRCAN99        * fixed text
.
          UNPACK    SP70,XCOUNTER
          UNPACK    SP70,XFRSNAME
          UNPACK    SP70,XGIVNAME
          UNPACK    SP70,XAS1SNAM
          UNPACK    SP70,XAS1FNAM
          UNPACK    SP70,XAS1GNAM
          UNPACK    SP70,XAS2SNAM
          UNPACK    SP70,XAS2FNAM
          UNPACK    SP70,XAS2GNAM
          UNPACK    SP70,XAS3SNAM
          UNPACK    SP70,XAS3FNAM
          UNPACK    SP70,XAS3GNAM
.
          UNPACK    SP70,XAFLATNO,XURASNUM,XURASNAM,XURARTYP
          UNPACK    SP70,XURARSUB
          MOVE      "0",XURASTAT
          PACK      XGPDOCAD,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70
          UNPACK    SP70,XTRDOCRG
          PACK      XTRDOCNM,SP70,SP70,SP70,SP70
          PACK      XTRDOCAD,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70
          PACK      XPATHLAB,SP70,SP70
          UNPACK    SP70,XDIAGDAT
.
          UNPACK    SP70,XADPROC1,XADPROC2,XADPROC3,XADPROC4
          UNPACK    SP70,XADPROC5,XADPROC6,XADPROC7,XADPROC8
          UNPACK    SP70,XTSTAGEN
          UNPACK    SP70,XTSTAGEC
          UNPACK    SP70,XNSTAGEC
          UNPACK    SP70,XMSTAGEC
          UNPACK    SP70,XTSTAGEG
          UNPACK    SP70,XTSTAGEB,XOSTAGES,XOSTAGEG,XOSTAGEB,XSURGFLG,XSYSTFLG
          UNPACK    SP70,XSYSTID1,XSYSTID2,XRADIFLG,XRADIEID,XOTHRFLG,XNOTRFLG
.
CLRCAN99  RETURN
+
.**************************************************************************
.         Validate header 
.**************************************************************************
HEDV0000  CALL      HEDC0000
          PRINT     *1,"Visit No",*12," Error message"
          CALL      UND132
          ADD       TWO,CLNO  
HEDV9999  RETURN
+
.**************************************************************************
.*                                HEDC0000
.*                      Print a cancer details Header
.**************************************************************************
HEDC0000  CALL      HEAD132
.
          IF        IBCNMHOS=1
            PRINT     *31,"Hospital - ",PTHSNAME
          ENDIF
          PRINT     *31,"Cancer Register Details"
          IF        OPTION = 1
            PRINT     *31,"From Cancer Coding Date : ",FROMDATE:
                          "     To : ",TODATE
          ELSE
            PRINT     *31,"All un-registered or re-registered Cancers"
          ENDIF
          CALL      UND132
.
          MOVE      SIX,CLNO  
.
HEDC9999  RETURN
+
.**************************************************************************
.*                                    CPRT0000
.*                      Print the Cancer Details Report Records
.**************************************************************************
CPRT0000  MOVE      PTCRPRIM,KEY9
          CALL      GETICD10
          IF        EXIT =0
            MOVE      DDESC,DIAGDESC           * Primary site text description
          ELSE
            MOVE      SP70,DIAGDESC
          ENDIF
.
          MOVE      PTCRHIST,KEY9
          CALL      GETICD10
          IF        EXIT =0
            MOVE      DDESC,PATHDESC           * Primary site text description
          ELSE
            MOVE      SP70,PATHDESC
          ENDIF
.
          MOVE      TWO,PACFRMT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,KEY28
.
          PRINT     *1,"| U/R Number",*18,": ",PURNO,*54,"| Admission No.":
                    *76,": ",DADMNO,*132,"|"
          PRINT     *1,"| Name",*18,": ",KEY28,*54,"| Admission Date ":
                    *76,": ",ADMDATE,*132,"|"
          PRINT     *1,"| Date of Birth",*18,": ",DOB,*54,"| ",DIM9," Date":
                    *76,": ",THEDATE,*132,"|"
          PRINT     *1,"| Sex",*18,": ",PSEX,*54,"| Diagnosis":
                    *76,": ",DIAGDESC,*132,"|"
          PRINT     *1,"| ",*54,"| Pathology",*76,": ",PATHDESC,*132,"|"
.
          CALL      UND132
          ADD       SEVEN,CLNO  
          COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
          CALL      HEDC0000 IF LESS 
          CALL      HEDC0000 IF EQUAL
          MOVE      PURNO,SAVURNO
.
CPRT9999  RETURN
+
.**************************************************************************
.*                                    GCAN0000
.*                      Create Cancer Reg. extract data
.**************************************************************************
GCAN0000  MOVE      ZERO,EXIT
. 
          CALL      HEDC0000                * print report heading
.
          MOVE      ZERO,CANCOUNT
          MOVE      ZERO,FCOUNTER
          MOVE      SP8,KEY8
          CALL      RSTEMP1                 * read thru temp file
GCAN1000  CALL      RKTEMP1
          BRANCH    OVRCD,GCAN9000
.
          MOVE      TEMPADMN,DADMNO
.
.                                           * read the required files
...       UNPACK    SP20,PTDAADTS,PTDADCTS
...       PACK      KEY8,DADMNO
...       CALL      RDPTDAD1                * read transfer Destination file
.
          PACK      KEY8,DADMNO,SP70 
          CALL      RDPTCRG1                * read Cancer Reg. Header
          BRANCH    OVRCD,GCAN1000
.
          PACK      KEY8,DADMNO,SP10
          CALL      RDMISS1                 * read admission details
          BRANCH    OVRCD,GCAN1000
.
          PACK      KEY8,AURNO,SP10
          CALL      RDMAST1                 * read PMI Details
          BRANCH    OVRCD,GCAN1000
.
          CALL      CLPMSPX2
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
.
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * read discharge file
          BRANCH    OVRCD,GCAN1000
.
          UNPACK    DDATE,ACC,AYY,AMM,ADD
          PACK      THEDATE,ADD,SLASH,AMM,SLASH,ACC,AYY
.
.
GCAN2000  CALL      CLRCAN00                * initialise CAN extract fields
          PACK      XHOSPNO,PTCNESTB,SP4
          IF        IBCNMHOS=1
            MATCH     SP70,PTHSCFCD
            IF        !@EQUAL
              PACK      XHOSPNO,PTHSCFCD,SP4
            ENDIF
          ENDIF
          MOVE      "T",XRECTYT
.
          PACK      XPSNAME,PSNAME,SP20     * Surname & given names
          SCAN      " ",PGNAME
          IF        @EQUAL
            MOVE      PGNAME,KEY12
            SQUEEZE   KEY12
            PACK      XPGSNAME,KEY12,SP20
            APPEND    SP20,PGNAME
            RESET     PGNAME
          ENDIF
          PACK      XPGFNAME,PGNAME,SP20
          PACK      XFRSNAME,PMPXFNAM,SP70
          PACK      XGIVNAME,PMPXSNAM,SP70
.
          CALL      GALIS000                * get Aliases
.
          MOVE      PSEX,XSEX               * sex
          REP       "M1F2I3R3U9",XSEX
          TYPE      XSEX
          IF        !@EQUAL
            MOVE      "9",XSEX
          ENDIF
.
          MOVE      "9",XINDIGST            * Indigenous not stated 
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATU1,CATU2,CATU3,CATU4:
                                   CATU5,CATU6,CATU7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
          MOVE      SP4,THCSCOD
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Cat."??"
          MATCH     SP1,THCSCOD
          IF        !@EQUAL
            UNPACK    THCSCOD,XINDIGST      * HDP Equivalent for NSW
          ENDIF
.
GCAN2010  MOVE      PURNO,KEY8              * UR Number
          SQUEEZE   KEY8
          PACK      XURNUMBR,KEY8,SP10
.
          MOVE      SP10,DOB                * Date of Birth
          MATCH     SP70,PBDATE
          IF        !@EQUAL
            MOVE      PBDATE,XDOB
            UNPACK    PBDATE,ACC,AYY,AMM,ADD
            PACK      DOB,ADD,SLASH,AMM,SLASH,ACC,AYY    * for print report
          ENDIF
.
          CALL      GADDR000                * get Address infos
.
          MATCH     SP3,PCONT               * Country of birth
          IF        !@EQUAL
            PACK      KEY5,CATC,PCONT
            CALL      RDCODE1               * Cat."C "
            IF        OVRCD=0
              UNPACK    TCFORM6,DIM2,XCOUNTRY
              REP       " 0",XCOUNTRY
            ENDIF
          ENDIF
.
          MATCH     SP10,PMEDI              * Medicare number
          IF        !@EQUAL
            PACK      XMEDICAR,PMEDI,SP10
          ENDIF
.
          IF        EFRMTYPE=1
            MATCH     SP70,ADOCTA             * Case Doctor (Doc. in Charge)
            IF        !@EQUAL
              UNPACK    SP70,DTITL,DGNAME,DSNAME
              PACK      KEY6,ADOCTA,SP10
              CALL      RDDOCT1
              SQUEEZE   DTITL
              SQUEEZE   DGNAME
              SQUEEZE   DSNAME
              PACK      XDOCNAM,DTITL,SP1,DSNAME,COMMA,SP1,DGNAME,SP30
              PACK      XDOCNAMR,DTITL,SP1,DSNAME,COMMA,SP1,DGNAME,SP30
            ENDIF
            MATCH     SP70,ADOCTL
            IF        @EQUAL
              PACK      XGPNAME,PLOCDOC         * GP Nam
            ELSE
              PACK      XGPNAME,ADOCTL
            ENDIF
          ELSE
            MATCH     SP70,PMVXRHC1
            IF        !@EQUAL
              PACK      KEY10,PMVXRHC1,SP70
              CALL      RDPMHCP1
              IF        OVRCD=0
                CALL      SETGP000            * Setup GP Name and Address
              ENDIF
            ENDIF
.
            MATCH      SP70,ADOCTA
            IF         !@EQUAL
              PACK      KEY10,ADOCTA,SP70
              CALL      RDPMHCP1
              IF        OVRCD=0
                CALL      SETTR000            * Setup Treating Doc Name & Add.
              ENDIF
            ENDIF
.
          ENDIF
.
          MOVE      ADATE,XADMDATE          * Admission Date
          UNPACK    ADATE,ACC,AYY,AMM,ADD
          PACK      ADMDATE,ADD,SLASH,AMM,SLASH,ACC,AYY  * for print report
.
          MOVE      DDATE,XSEPDATE          * Discharge Date
.
          IF        EFRMTYPE=1
            MOVE      "01",XSEPSTAT           * default Discharge Status - Alive
          ELSE
            MOVE      "1",XSEPSTAT            * 1 for discharged
          ENDIF
          IF        PCEASE = 1
.
            MATCH     SP70,DDATE
            GOTO      GCAN2015 IF EQUAL
.
            MATCH     SP70,PDECDTE
            GOTO      GCAN2015 IF EQUAL
.
            MATCH     PDECDTE,DDATE
            GOTO      GCAN2020 IF LESS
.
GCAN2015    IF        EFRMTYPE=1
              MOVE      "07",XSEPSTAT
            ELSE
              MOVE      "7",XSEPSTAT
            ENDIF
.
          ENDIF
.
....      IF        PTCNHOSP = 1
....                                        * for Public Hospitals
....        IF        PTNSWHDP = 1
....          MATCH     SP3,DSTAT           * read discharge status
....          IF        !@EQUAL
....            PACK      KEY5,CATD,DSTAT
....            CALL      RDCODE1           * Cat."DD"
....            IF        OVRCD = 0
....              MOVE      THCSCOD,XSEPSTAT
....              REP       " 0",XSEPSTAT   * Seperation mode
....            ENDIF
....          ENDIF
....        ELSE
....          MATCH     SP3,DDEST           * read discharge destination
....          IF        !@EQUAL
....            PACK      KEY5,CATDD,DDEST
....            CALL      RDCODE1           * Cat."DD"
....            IF        OVRCD = 0
....              MOVE      THCSCOD,XSEPSTAT
....              REP       " 0",XSEPSTAT   * Seperation mode
....            ENDIF
....          ENDIF
....        ENDIF
....      ENDIF
.
GCAN2020  MOVE      PTCRMULT,FORM3
          MOVE      "2",XMULTPRI            * more than 1 Primary? default "no"
          IF        FORM3 > 1
            MOVE      "1",XMULTPRI          * "yes" - more than 1 Primary      
          ENDIF
.
GCAN2500  CALL      CPRT0000                * print registered patient
.
.
.                                           * loop thri PATECDaf for Codes
GCAN2600  MOVE      ZERO,FORM2
          PACK      KEY13,DADMNO,SP20                                 *C-240107
          CALL      RSPTECD1
GCAN2650  CALL      RKPTECD1                * read thru Episode for 1st Diag
          IF        OVRCD = 0       
            MATCH     DPTEDADM,DDADMNO
            IF        @EQUAL
              MATCH     "P",PTEDTYPE
              IF        @EQUAL
                MATCH     SP1,XREASADM      * only do this bit once **********
                IF        @EQUAL
                  MOVE      "2",XREASADM    * Cancer Admission ? - default "no"
                  PACK      KEY9,PTEDCODE,SP10
                  CALL      RDPTCAN1
                  IF        OVRCD = 0   
                    MOVE      "1",XREASADM  * "yes", Cancer Admission
                  ENDIF
                GOTO      GCAN2650 
                ENDIF
              ENDIF                         *                       **********
.
.                                           * for PTEDTYPE <> "P"
              ADD       ONE,FORM2
              IF        FORM2 < 9
                IF        FORM2 = 1
                  PACK      XTRGCOD1,PTEDCODE,SP10
                ENDIF
                IF        FORM2 = 2
                  PACK      XTRGCOD2,PTEDCODE,SP10
                ENDIF
                IF        FORM2 = 3
                  PACK      XTRGCOD3,PTEDCODE,SP10
                ENDIF
                IF        FORM2 = 4
                  PACK      XTRGCOD4,PTEDCODE,SP10
                ENDIF
                IF        FORM2 = 5
                  PACK      XTRGCOD5,PTEDCODE,SP10
                ENDIF
                IF        FORM2 = 6
                  PACK      XTRGCOD6,PTEDCODE,SP10
                ENDIF
                IF        FORM2 = 7
                  PACK      XTRGCOD7,PTEDCODE,SP10
                ENDIF
                IF        FORM2 = 8
                  PACK      XTRGCOD8,PTEDCODE,SP10
                ENDIF
                GOTO    GCAN2650
              ENDIF
            ENDIF
          ENDIF
.
GCAN3000  PACK      XPRMDIAG,PTCRPRIM,SP10
          MOVE      ZERO,FORM1
          PACK      KEY13,DADMNO,SP20                                 *C-240107
          CALL      RSPTECO1
GCAN3100  CALL      RKPTECO1                * read thru Operations
          IF        OVRCD = 0       
            MATCH     DPTEOADM,DDADMNO
            IF        @EQUAL
              IF        EFRMTYPE=1
                IF      PTEOCNT = 1    
                  PACK    XPRMPROC,PTEOCODE,SP10
                ENDIF
              ELSE
                STORE     PTEOCODE,PTEOCNT,XPRMPROC,XADPROC1,XADPROC2,XADPROC3:
                                           XADPROC4,XADPROC5,XADPROC6,XADPROC7:
                                           XADPROC8
                GOTO      GCAN3100
              ENDIF
            ENDIF
          ENDIF
. 
.                                 * read thru Cancer Reg details
.                                           * for each Cancer's details   
.
          UNPACK    SP70,XDIAGLOC,XDIAGDTE,XLATERY,XLABNAM,XBASDIAG
          UNPACK    SP70,XDRGSPRD,XTRGPRIM,XTRGMORP
          UNPACK    SP70,XTRGPRID
.
          UNPACK    SP70,XDIAGDAT
          PACK      XPATHLAB,SP70,SP70
          UNPACK    SP70,XTSTAGEB,XOSTAGES,XOSTAGEG,XOSTAGEB,XSURGFLG,XSYSTFLG
          UNPACK    SP70,XRADIFLG,XOTHRFLG,XNOTRFLG
.
          MOVE      SP70,XTSTAGEN
          MOVE      SP70,XTSTAGEC
          MOVE      SP70,XNSTAGEC
          MOVE      SP70,XMSTAGEC
          MOVE      SP70,XTSTAGEG
.
          PACK      KEY25,DADMNO,SP25
          CALL      RSPTCRD1                * Get cancer register details
GCAN5000  CALL      RKPTCRD1
          BRANCH    OVRCD,GCAN1000
.
          MATCH     DADMNO,PTCDDADN
          GOTO      GCAN1000 IF NOT EQUAL
.
          MOVE      PTCDCSCM,XTSTAGEN
          MOVE      PTCDCSGT,XTSTAGEC
          MOVE      PTCDCSGN,XNSTAGEC
          MOVE      PTCDCSGM,XMSTAGEC
          MOVE      PTCDCSSG,XTSTAGEG
.
          MATCH     SP70,XTRGPRID
          IF        @EQUAL
            PACK      XTRGPRID,PTCDPRMM,SP8    * Set principal diagnosis from 
          ENDIF                                 * from first cancer reg 
.
          MATCH     SP70,PTCDBSDG
          IF        !@EQUAL
            PACK      KEY5,CATBD,PTCDBSDG,SP70   * Use cat BD HDP if populated
            CALL      RDCODE1                    * otherwise us actual code
            IF        OVRCD<>1
              UNPACK    THCSCOD,XTSTAGEB
            ENDIF
          ENDIF
          MATCH     SP70,XTSTAGEB
          IF        @EQUAL
            MOVE      PTCDBSDG,XTSTAGEB
          ENDIF
.
          MATCH     SP70,PTCDSTAG
          IF        !@EQUAL
            PACK      KEY5,CATSS,PTCDSTAG,SP70   * Use cat sS HDP if populated
            CALL      RDCODE1                    * otherwise us actual code
            IF        OVRCD<>1
              UNPACK    THCSCOD,XOSTAGES
            ENDIF
          ENDIF
          MATCH     SP70,XOSTAGES
          IF        @EQUAL
            MOVE      PTCDSTAG,XOSTAGES
          ENDIF
.
          MOVE      PTCDCOMM,XOSTAGEG
.
          MATCH     SP70,PTCDBSDG
          IF        !@EQUAL
            PACK      KEY5,CATBD,PTCDBSDG,SP70   * Use cat BD HDP if populated
            CALL      RDCODE1                    * otherwise us actual code
            IF        OVRCD<>1
              UNPACK    THCSCOD,XOSTAGEB
            ENDIF
          ENDIF
          MATCH     SP70,XOSTAGEB
          IF        @EQUAL
            MOVE      PTCDBSDG,XOSTAGEB
          ENDIF
.
          MATCH     "1",PTCDTSUR
          IF        @EQUAL
            MOVE      ANSY,XSURGFLG
          ENDIF
.
          MATCH     "1",PTCDTCHM
          IF        @EQUAL
            MOVE      ANSY,XSYSTFLG
          ENDIF
.
          MATCH     "1",PTCDTRAD
          IF        @EQUAL
            MOVE      ANSY,XRADIFLG
          ENDIF
.
          MATCH     "1",PTCDTBIO
          IF        @EQUAL
            MOVE      ANSY,XOTHRFLG
          ENDIF
.
          MATCH     "1",PTCDTNON
          IF        @EQUAL
            MOVE      ANSY,XNOTRFLG
          ENDIF
.
          MATCH     SP7,PTCDSTTE            * State of first diagnosis
          IF        @EQUAL
            SEARCH    HOMESTTE,SRCHNSW,TEN1,FORM2
          ELSE
            STRIP     PTCDSTTE
            SEARCH    PTCDSTTE,SRCHNSW,TEN1,FORM2
            MATCH     "Ter",HOMESTTE
            IF        @EQUAL
              MOVE      "5",FORM2           * default Territories
            ENDIF
          ENDIF
          IF        FORM2 = 0
            MOVE      "1",FORM2             * Default NSW
          ENDIF
.
          IF        FORM2 = 10
            MOVE      "98",FORM2
          ENDIF
.
          IF        FORM2 = 11
            MOVE      "99",FORM2
          ENDIF
.
          MOVE      FORM2,XDIAGLOC          * Residence at Diagnosis
          SQUEEZE   XDIAGLOC
.
          MOVE      ZERO,FORM1
          MOVE      PTCDDGPR,FORM1          * Diagnosis prior to Admission?
          IF        FORM1 = 0
            UNPACK    PTCDDDTE,XDIAGDTE     * date of first diagnosis
            UNPACK    PTCDDDTE,XCENT,XYEAR,XMON,XDAY
            PACK      XDIAGDAT,XDAY,XMON,XCENT,XYEAR
            MATCH     SP70,XDIAGDAT
            IF        @EQUAL
              MOVE      "01019999",XDIAGDAT   * unknown
            ENDIF
          ELSE
            IF        FORM1 = 1
              UNPACK    PTCDDDTE,KEY4,KEY2    *
              MOVE      "99",KEY2
              PACK      XDIAGDTE,KEY4,KEY2    * set date to "yyyy99"
              UNPACK    PTCDDDTE,XCENT,XYEAR,XMON,XDAY
              PACK      XDIAGDAT,XDAY,XMON,XCENT,XYEAR
              MATCH     SP70,XDIAGDAT
              IF        @EQUAL
                MOVE      "01019999",XDIAGDAT   * unknown
              ENDIF
            ELSE
              MOVE      "999901",XDIAGDTE     * date unknown
              MOVE      "01019999",XDIAGDAT   * unknown
            ENDIF
          ENDIF
.
          MOVE      "9",XLATERY             * Unknown laterality 
          PACK      KEY5,CATLY,PTCDLATR,SP10
          CALL      RDCODE1                 * Cat."LY"
          IF        OVRCD=0
            CMATCH    SP1,THCSCOD
            IF        !@EQUAL
              UNPACK    THCSCOD,XLATERY,DIM5  * Laterality
            ENDIF
          ENDIF
.
.         This field is only on NZ and SA template (mrtweb0208003)
.
.         MATCH     SP3,PTCDRPLB            * Place Pathology done
.         IF        !@EQUAL
.           PACK      KEY5,CATLB,PTCDRPLB
.           CALL      RDCODE1               * Cat. "LB"
.           IF        OVRCD = 0
.             PACK      XLABNAM,TDESC,SP20
.             IF        EFRMTYPE=2
.               PACK      XPATHLAB,TDESC,SP20
.               MATCH     SP70,PTCDLDES
.               IF        !@EQUAL
.                 PACK      XPATHLAB,PTCDLDES,SP70
.               ENDIF
.             ENDIF
.           ENDIF
.         ENDIF
.
          PACK      XPATHLAB,PTCDOTDG,SP70  * Laboratory Path
.
GCAN5050  MOVE      SP70,XBASDIAG            * Best Basis for Diagnosis
          MOVE      "Bd",KEY2
          PACK      KEY5,KEY2,PTCDBDIA
          CALL      RDCODE1
          IF        OVRCD=0
            UNPACK    THCSCOD,XBASDIAG      * Position 1
          ENDIF
.
          MOVE      SP6,THCSCOD
....      PACK      KEY5,CATHI,PTCDSTAG     * Degree of Spread (Staging)
....      CALL      RDCODE1                 * Cat."HI"
....      UNPACK    THCSCOD,XDRGSPRD,KEY5
          SQUEEZE   PTCDCELT
          MOVE      PTCDCELT,XDRGSPRD
.
          PACK      XTRGPRIM,PTCDPRMM,SP8   * Principal Topography (Cancer code)
          PACK      XTRGMORP,PTCDHTYP,SP8   * Principal Morphology (this Cancer)
. 
          ADD       ONE,CANCOUNT
          MOVE      CANCOUNT,XCANNUMB
.
          ADD       ONE,FCOUNTER
          MOVE      FCOUNTER,XCOUNTER
          CALL      WRTCAN1
.
          PACK      PTCDDTEX,CCC,CYY,CMM,CDD,SP10   * set "Date Sent"
          REP       " 0",PTCDDTEX
          CALL      UPPTCRD1               *** remove for testing ****
.
          GOTO      GCAN5000
.
.
GCAN9000  MOVE      ZERO,EXIT
.
GCAN9999  RETURN
+
.**************************************************************************
.         Setup GP Name and address
.**************************************************************************
SETGP000  PACK      XGPNAME,PMHCMPGN,SP70 * Always using Post 2010 number
.
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          PACK      XDOCNAM,PACFNAME,SP70     * GP Name
          PACK      XDOCNAMR,PACFNAME,SP70     * GP Name
.
          PACK      KEY12,PMVXRH1G,PMVXRH1C,SP70
          CALL      RDPMHCG1
          BRANCH    OVRCD,SETGP200
.
. Use linked practice address is populated
.
          MATCH     SP70,PMHGADD1
          IF        @EQUAL
            MATCH     SP70,PMHGADD2
            GOTO      SETGP200 IF EQUAL
          ENDIF
.
          MOVE      PMHGADD1,PMHCADR1
          MOVE      PMHGADD2,PMHCADR2
          MOVE      PMHGADD3,PMHCADR3
          MOVE      PMHGADD4,PMHCADR4
          MOVE      PMHGSTAT,PMHCSTAT
          MOVE      PMHGPOST,PMHCPOST
.
SETGP200  STRIP     PMHCADR1
          STRIP     PMHCADR2
          STRIP     PMHCADR3
          STRIP     PMHCADR4
          STRIP     PMHCSTAT
          STRIP     PMHCPOST
.
          REP       ", ",PMHCADR1
          REP       ", ",PMHCADR2
          REP       ", ",PMHCADR3
          REP       ", ",PMHCADR4
.
          CLEAR     XGPDOCAD
          MOVE      PMHCADR1,XGPDOCAD
          ENDSET    XGPDOCAD
.
          APPEND    SP1,XGPDOCAD
          APPEND    PMHCADR2,XGPDOCAD
          ENDSET    XGPDOCAD
.
          APPEND    SP1,XGPDOCAD
          APPEND    PMHCADR3,XGPDOCAD
          ENDSET    XGPDOCAD
.
          APPEND    SP1,XGPDOCAD
          APPEND    PMHCADR4,XGPDOCAD
          ENDSET    XGPDOCAD
.
          APPEND    SP1,XGPDOCAD
          APPEND    PMHCSTAT,XGPDOCAD
          ENDSET    XGPDOCAD
.
          APPEND    SP1,XGPDOCAD
          APPEND    PMHCPOST,XGPDOCAD
          ENDSET    XGPDOCAD
          RESET     XGPDOCAD
.
SETGP999  RETURN
+
.**************************************************************************
.         Setup Treating doctor
.**************************************************************************
.SETTR000  PACK      XTRDOCRG,PMHCMRBN,SP70   * Registration number (Pre 2010)
.          UNPACK    PMHCSDAT,KEY4
.          REP       " 0",KEY4
.          MATCH     "2010",KEY4
.          IF        !@LESS
SETTR000  PACK      XTRDOCRG,PMHCMPGN,SP70    * Always using Post 2010 number
.          ENDIF
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          PACK      XTRDOCNM,PACFNAME,SP70     * GP Name
.
          STRIP     PMHCADR1
          STRIP     PMHCADR2
          STRIP     PMHCADR3
          STRIP     PMHCADR4
          STRIP     PMHCSTAT
          STRIP     PMHCPOST
.
          REP       ", ",PMHCADR1
          REP       ", ",PMHCADR2
          REP       ", ",PMHCADR3
          REP       ", ",PMHCADR4
.
          CLEAR     XTRDOCAD
          MOVE      PMHCADR1,XTRDOCAD
          ENDSET    XTRDOCAD
.
          APPEND    SP1,XTRDOCAD
          APPEND    PMHCADR2,XTRDOCAD
          ENDSET    XTRDOCAD
.
          APPEND    SP1,XTRDOCAD
          APPEND    PMHCADR3,XTRDOCAD
          ENDSET    XTRDOCAD
.
          APPEND    SP1,XTRDOCAD
          APPEND    PMHCADR4,XTRDOCAD
          ENDSET    XTRDOCAD
.
          APPEND    SP1,XTRDOCAD
          APPEND    PMHCSTAT,XTRDOCAD
          ENDSET    XTRDOCAD
.
          APPEND    SP1,XTRDOCAD
          APPEND    PMHCPOST,XTRDOCAD
          ENDSET    XTRDOCAD
          RESET     XTRDOCAD
.
SETTR999  RETURN
+
.**************************************************************************
.         Get Primary Disease date
.**************************************************************************
VPRM0000  PACK      DIM25,DPTCDDAD,PTCDPRMM,DPTCDCNR,SP70   * save key
.
          PACK      KEY25,PMVXVISN,SP25
          CALL      RSPTCRD1                * Get cancer register details
VPRM1000  CALL      RKPTCRD1
          BRANCH    OVRCD,VPRM9999
.
          MATCH     PMVXVISN,DPTCDDAD
          GOTO      VPRM9999 IF NOT EQUAL
.
          MATCH     SP70,PTCDPRMM
          GOTO      VPRM1000 IF EQUAL
.
          CALL      VPDT0000                  * Valide Primary disease date
.
VPRM9999  RETURN
.
.**************************************************************************
.         Validate Primary Disease date
.**************************************************************************
VPDT0000  MATCH     SP70,PTCDDDTE
          GOTO      VPDT9999 IF EQUAL
.
          MATCH     "99990101",PTCDDDTE
          GOTO      VPDT9999 IF EQUAL
.
          PACK      DIM8,CCC,CYY,CMM,CDD      * Today's date
          REP       " 0",DIM8
.
          MATCH     PTCDDDTE,DIM8
          IF        @LESS
            COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
            CALL      HEDV0000 IF LESS 
            PRINT    *1,PMVXVISN:
                     *12," Date of Primary Disease must be less or equal to":
                        " Today's Date."
            CALL     SETERR00
          ENDIF
.
          MATCH     PBDATE,PTCDDDTE
          IF        @LESS
            COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
            CALL      HEDV0000 IF LESS 
            PRINT    *1,PMVXVISN:
                     *12," Date of Birth must be less or equal to":
                        " Date of Primary Disease."
            CALL     SETERR00
          ENDIF
.
          IF        ASTAT=3
            MATCH     PTCDDDTE,DDATE
            IF        @LESS
              COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
              CALL      HEDV0000 IF LESS 
              PRINT    *1,PMVXVISN:
                       *12," Date of Primary Disease must be less or equal to":
                          " Discharge Date."
              CALL     SETERR00
            ENDIF
          ENDIF
VPDT9999  RETURN
+
.**************************************************************************
.*                                GADDR000
.*                              Get address
.**************************************************************************
GADDR000  CLEAR    XSTRTNAM
          REP       ", ",PADD1
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            PACK      XSTRTNAM,PADD1
            PACK      XURASNAM,PADD1,SP70
            SETLPTR   PADD1
          ENDIF
.
          UNPACK    SP70,KEY25,HOMESTTE
          MATCH     SP4,PPOST
          IF        !@EQUAL
            PACK      KEY56,PPOST,SP70            * use postcode to get suburb
            CALL      RSIBPOS1
            CALL      RKIBPOS1
            IF        OVRCD=0
              MATCH     PPOST,IBPOPCOD            * same postcode ?
              IF        @EQUAL
                PACK      KEY25,IBPOSUBR,SP70     * Suburb
                MOVE      IBPOSTAT,HOMESTTE       * State
              ENDIF
            ENDIF
          ENDIF
.
.         Check with parameter whether we are using address line three or
.         address line two as the Locality
.
          MOVE      PSUBRB,XLOCATN           * Using address line 3(suburb)
          PACK      XURARSUB,PSUBRB,SP70
          BRANCH    CNSWLOC OF GADDR100
          REP       ", ",PADD2
          MOVE      PADD2,XLOCATN            * Using address line 2
          PACK      XURARSUB,PADD2,SP70
.
GADDR100  MATCH     SP10,PPOST
          IF        @EQUAL
            MOVE      "0989",XPOSTCOD             * Not stated/unknown
            PACK      KEY56,XLOCATN,SP70          * Use suburb to get postcode
            CALL      RSIBPOS2
            CALL      RKIBPOS2
            IF        OVRCD=0
              MATCH     XLOCATN,IBPOSUBR          * same suburb ?
              IF        @EQUAL
                PACK      PPOST,IBPOPCOD,SP10     * Postcode
                MOVE      IBPOSTAT,HOMESTTE       * State
              ENDIF
            ENDIF
          ENDIF
.
          PACK      XPOSTCOD,PPOST,SP10     * post code
.
.         Set up state for CSV format
.
          BRANCH    EFRMTYPE,GADDR999       * Text fixed
.
          MATCH     "8888",PPOST
          GOTO      GADDR999 IF EQUAL
.
          PACK      KEY56,PPOST,SP70
          CALL      RSIBPOS1                * Position & read a postcode file
          CALL      RKIBPOS1                  record
          BRANCH    OVRCD,GADDR999
.
          MATCH     PPOST,IBPOPCOD
          GOTO      GADDR999 IF NOT EQUAL   * Different postcode
.
          MATCH     "NSW",IBPOSTAT
          IF        @EQUAL
            MOVE      "1",XURASTAT
            GOTO      GADDR999
          ENDIF
          MATCH     "VIC",IBPOSTAT
          IF        @EQUAL
            MOVE      "2",XURASTAT
            GOTO      GADDR999
          ENDIF
.
          MATCH     "QLD",IBPOSTAT
          IF        @EQUAL
            MOVE      "3",XURASTAT
            GOTO      GADDR999
          ENDIF
.
          MATCH     "SA ",IBPOSTAT
          IF        @EQUAL
            MOVE      "4",XURASTAT
            GOTO      GADDR999
          ENDIF
.
          MATCH     "WA ",IBPOSTAT
          IF        @EQUAL
            MOVE      "5",XURASTAT
            GOTO      GADDR999
          ENDIF
.
          MATCH     "TAS",IBPOSTAT
          IF        @EQUAL
            MOVE      "6",XURASTAT
            GOTO      GADDR999
          ENDIF
.
          MATCH     "NT ",IBPOSTAT
          IF        @EQUAL
            MOVE      "7",XURASTAT
            GOTO      GADDR999
          ENDIF
.
          MATCH     "ACT",IBPOSTAT
          IF        @EQUAL
            MOVE      "8",XURASTAT
            GOTO      GADDR999
          ENDIF
.
GADDR999  RETURN
+
.**************************************************************************
.*                                GALIS000
.                  Get & Write Former/Alias Names details
.**************************************************************************
GALIS000  MOVE      ZERO,FORM2
          PACK      KEY8,AURNO
.
          PACK      KEY115,KEY8,SP70,SP70
          CALL      RSPTGSR1
GALIS100  CALL      RKPTGSR1
          BRANCH    OVRCD,GALIS999
.
          MATCH     KEY8,GSRURNO            * check U/R
          GOTO      GALIS999 IF NOT EQUAL
.
          MATCH     GSRSNAM,PTMASNAM        * check surname
          GOTO      GALIS500 IF NOT EQUAL
.
          MATCH     GSRGNAM,PMPXFNAM        * check first given name
          GOTO      GALIS500 IF NOT EQUAL
.
          MATCH     PTGSGNM2,PMPXSNAM       * check second given name
          GOTO      GALIS500 IF NOT EQUAL
.
          MOVE      ZERO,GSRURNO 
          GOTO      GALIS100
.
.                                           * same UR No. found
GALIS500  ADD       ONE,FORM2
          BRANCH    FORM2,GALIS510,GALIS520,GALIS530
          GOTO      GALIS999
.
GALIS510  PACK      XAL1SNAM,GSRSNAM,SP20   * 1st alias
          PACK      XAL1FNAM,GSRGNAM,SP20
          PACK      XAL1GNAM,PTGSGNM2,SP20
.
          PACK      XAS1SNAM,GSRSNAM,SP20   * 1st ASias
          PACK      XAS1FNAM,GSRGNAM,SP20
          PACK      XAL1GNAM,PTGSGNM2,SP20
          GOTO      GALIS100
.
GALIS520  PACK      XAL2SNAM,GSRSNAM,SP20   * 2nd alias
          PACK      XAL2FNAM,GSRGNAM,SP20
          PACK      XAL2GNAM,PTGSGNM2,SP20
.
          PACK      XAS2SNAM,GSRSNAM,SP20   * 2nd alias
          PACK      XAS2FNAM,GSRGNAM,SP20
          PACK      XAS2GNAM,PTGSGNM2,SP20
          GOTO      GALIS100
.
GALIS530  PACK      XAL3SNAM,GSRSNAM,SP20   * 3rd alias
          PACK      XAL3FNAM,GSRGNAM,SP20
          PACK      XAL3GNAM,PTGSGNM2,SP20
.
          PACK      XAS3SNAM,GSRSNAM,SP20   * 3rd alias
          PACK      XAS3FNAM,GSRGNAM,SP20
          PACK      XAS3GNAM,PTGSGNM2,SP20
          GOTO      GALIS999
.
GALIS999  RETURN
+
.**************************************************************************
.*                                GETICD10
.**************************************************************************
GETICD10  MOVE      ZERO,EXIT               * ICD10 code is in KEY9
.
          PACK      DDESC,SP30,SP30
          MATCH     PTCNI10D,ICDRDDTE
          IF        @LESS
            CALL      RD10T9D1
            MOVE      OVRCD,EXIT
          ELSE
            CALL      RDPT10D1
            MOVE      OVRCD,EXIT
          ENDIF
          MOVE      DDESC,KEY36
.
GETICD99  RETURN
.
.******************************************************************************
.*                             CRTM0000                                       *
.*                 Open and create the temporary files                        *
.******************************************************************************
CRTM0000  MOVE      ZERO,EXIT
          CALL      CLTM0000                * Close and erase Tempfiles
.
.                                           * create temp. Admiss.No. table
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    " 38 U1-8",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPAFIL,FNAMEA
.                                           * now temp. Cancer Data table
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEB,CMDLINE
.
          IF        EFRMTYPE=1
            APPEND    " 596 U1-5",CMDLINE
            RESET     CMDLINE
            EXECUTE   CMDLINE,TASKID
            OPEN      TEMPCANF,FNAMEB
          ELSE
            APPEND    " 2747 U1-11",CMDLINE
            RESET     CMDLINE
            EXECUTE   CMDLINE,TASKID
            OPEN      TEMPCSVF,FNAMEB
          ENDIF
.
CRTM9999  RETURN
+
.******************************************************************************
.*                             CLTM0000                                       *
.*                 Close and erase the temporary files                        *
.******************************************************************************
CLTM0000  CLOSE     TEMPAFIL
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
CLTM1000  CLOSE     TEMPCANF
          CLOSE     TEMPCSVF
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEB,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          IF        EFRMTYPE=1
            PREP      EXTCANFL,EXTCANTM
            CLOSE     EXTCANFL,DELETE
          ELSE
            PREP      CXTCANFL,EXTCANTM
            CLOSE     CXTCANFL,DELETE
          ENDIF
CLTM9999  RETURN
+
.******************************************************************************
.*                             CREX0000                                       *
.*                 Create Extract files from temporary table                  *
.******************************************************************************
CREX0000  IF        EFRMTYPE=1
            CLOSE     EXTCANFL,DELETE
            PREP      EXTCANFL,EXTCANTM       * create extract file
          ELSE
            CLOSE     CXTCANFL,DELETE
            PREP      CXTCANFL,EXTCANTM       * create comma delimited file
          ENDIF
          CALL      GHDR0000                * write header record to extract
.
          MOVE      ZERO,FCOUNTER
          PACK      KEY6,SP10
          CALL      RSTCAN1
CREX1000  CALL      RKTCAN1
          BRANCH    OVRCD,CREX9000
.
          IF        EFRMTYPE=1
            WRITE     EXTCANFL,SEQ;*+,XRECTYT,XHOSPNO,XPSNAME,XPGFNAME,XPGSNAME:
                                  XAL1SNAM,XAL1FNAM,XAL1GNAM,XAL2SNAM,XAL2FNAM:
                                  XAL2GNAM,XAL3SNAM,XAL3FNAM,XAL3GNAM,XSEX:
                                  XINDIGST,XURNUMBR,XDOB,XFLATNO,XSTRTNO:
                                  XSTRTNAM,XSTRTTYP,XLOCATN,XPOSTCOD,XCOUNTRY:
                                  XMEDICAR,XDOCNAM,XGPNAME,XGPADDR,XADMDATE:
                                  XSEPDATE,XSEPSTAT,XMULTPRI,XDIAGDTE,XDIAGLOC:
                                  XLATERY,XLABNAM,XBASDIAG,XDRGSPRD,XREASADM:
                                  XTRGPRIM,XTRGMORP,XTRGCOD1,XTRGCOD2,XTRGCOD3:
                                  XTRGCOD4,XTRGCOD5,XTRGCOD6,XTRGCOD7,XTRGCOD8:
                                  XPRMDIAG,XPRMPROC,XSPARE
          ELSE
            CALL      SFLD0000
            WRITE     CXTCANFL,SEQ;*+,QUOTE,XCOUNTER,DELIM:
                                  XHOSPNO,DELIM,XMEDICAR,DELIM:
                                  XURNUMBR,DELIM,XPSNAME,DELIM,XFRSNAME,DELIM:
                                  XGIVNAME,DELIM,XAS1SNAM,DELIM,XAS1FNAM,DELIM:
                                  XAS1GNAM,DELIM,XAS2SNAM,DELIM,XAS2FNAM,DELIM:
                                  XAS2GNAM,DELIM,XAS3SNAM,DELIM,XAS3FNAM,DELIM:
                                  XAS3GNAM,DELIM,XSEX,DELIM,XDOB,DELIM:
                                  XCOUNTRY,DELIM,XINDIGST,DELIM,XAFLATNO,DELIM:
                                  XURASNUM,DELIM,XURASNAM,DELIM,XURARTYP,DELIM:
.
                                  XURARSUB,DELIM,XPOSTCOD,DELIM,XURASTAT,DELIM:
                                  XGPNAME,DELIM,XDOCNAMR,DELIM,XGPDOCAD,DELIM:
                                  XTRDOCRG,DELIM,XTRDOCNM,DELIM,XTRDOCAD,DELIM:
                                  XADMDATE,DELIM,XSEPDATE,DELIM,XSEPSTAT,DELIM:
                                  XDIAGLOC,DELIM,XPATHLAB,DELIM,XDIAGDAT,DELIM:
                                  XTRGPRIM,DELIM,XTRGMORP,DELIM,XLATERY,DELIM:
                                  XBASDIAG,DELIM,XDRGSPRD,DELIM,XREASADM,DELIM:
.
                                  XTRGPRID,DELIM,XTRGCOD1,DELIM,XTRGCOD2,DELIM:
                                  XTRGCOD3,DELIM,XTRGCOD4,DELIM,XTRGCOD5,DELIM:
                                  XTRGCOD6,DELIM,XTRGCOD7,DELIM,XTRGCOD8,DELIM:
                                  XPRMPROC,DELIM,XADPROC1,DELIM,XADPROC2,DELIM:
                                  XADPROC3,DELIM,XADPROC4,DELIM,XADPROC5,DELIM:
                                  XADPROC6,DELIM,XADPROC7,DELIM,XADPROC8,DELIM:
.
                                  XTSTAGEN,DELIM,XTSTAGEC,DELIM,XNSTAGEC,DELIM:
                                  XMSTAGEC,DELIM,XTSTAGEG,DELIM,XTSTAGEB,DELIM:
                                  XOSTAGES,DELIM,XOSTAGEG,DELIM,XOSTAGEB,DELIM:
                                  XSURGFLG,DELIM,XSYSTFLG,DELIM,XSYSTID1,DELIM:
                                  XSYSTID2,DELIM,XRADIFLG,DELIM,XRADIEID,DELIM:
                                  XOTHRFLG,DELIM,XNOTRFLG,QUOTE
.
          ENDIF
          GOTO      CREX1000
.
CREX9000  IF        EFRMTYPE=1
            CLOSE     EXTCANFL
          ELSE
            CLOSE     CXTCANFL
          ENDIF
.
CREX9999  RETURN
+
.**************************************************************************
.         Set fields for comma delimited file
.**************************************************************************
SFLD0000  SQUEEZE   XCOUNTER
          STRIP     XHOSPNO   * Hospital ID
          STRIP     XMEDICAR  * Medicare number
          STRIP     XURNUMBR  * Medical record number
          STRIP     XPSNAME   * Surname
          STRIP     XFRSNAME  * First given name                        (pgname)
          STRIP     XGIVNAME  * Second given name                       (pgname)
          STRIP     XAS1SNAM  * Alias 1: Surname
          STRIP     XAS1FNAM  * Alias 1: First given name
          STRIP     XAS1GNAM  * Alias 1: Second given name
          STRIP     XAS2SNAM  * Alias 2: Surname
          STRIP     XAS2FNAM  * Alias 2: First given name
          STRIP     XAS2GNAM  * Alias 2: Second given name
          STRIP     XAS3SNAM  * Alias 3: Surname
          STRIP     XAS3FNAM  * Alias 3: First given name
          STRIP     XAS3GNAM  * Alias 3: Second given name
.
          STRIP     XSEX      * Sex
          MATCH     SP70,XDOB
          IF        !@EQUAL
            UNPACK    XDOB,XCENT,XYEAR,XMON,XDAY
            PACK      XDOB,XDAY,XMON,XCENT,XYEAR
          ENDIF
          STRIP     XDOB      * Date of Birth
          STRIP     XCOUNTRY  * Country of birth
          STRIP     XINDIGST  * Indig. Status 
          STRIP     XAFLATNO  * Flat number
          STRIP     XURASNUM  * Street number
          STRIP     XURASNAM  * Street name
          STRIP     XURARTYP  * Street type
.
          STRIP     XURARSUB  * Town/Suburb
          STRIP     XPOSTCOD  * URA Postcode
          STRIP     XURASTAT  * URA State
          STRIP     XGPNAME   * GP Registration name
          STRIP     XDOCNAMR  * GP Doctor's name
          STRIP     XGPDOCAD  * GP Doctor's address
          STRIP     XTRDOCRG  * Treating Doctor Registration name
          STRIP     XTRDOCNM  * Treating Doctor's name
          STRIP     XTRDOCAD  * Treating Doctor's address
          STRIP     XDOCNAM   * GP Doctor's name
.
          UNPACK    XADMDATE,XCENT,XYEAR,XMON,XDAY
          PACK      KEY8,XDAY,XMON,XCENT,XYEAR
          MOVE      KEY8,XADMDATE
          STRIP     XADMDATE  * Date of admission
.
          UNPACK    XSEPDATE,XCENT,XYEAR,XMON,XDAY
          PACK      KEY8,XDAY,XMON,XCENT,XYEAR
          MOVE      KEY8,XSEPDATE
          STRIP     XSEPDATE  * Date of separation
.
          STRIP     XSEPSTAT  * For PUBLIC notifiers
          SQUEEZE   XDIAGLOC  * Place at time of Diagnosis
          STRIP     XPATHLAB  * Name of Pathology Laboratory
          STRIP     XDIAGDAT  * Date of first diagnosis
          STRIP     XTRGPRIM  * Primary Site of Cancer code
          STRIP     XTRGMORP  * Valid morphology code
          STRIP     XLATERY   * Cancer of paired organs
          STRIP     XBASDIAG  * Best basis for diagnosis at this admission
          STRIP     XDRGSPRD  * Degree of spread of cancer
          STRIP     XREASADM  * Reason for Admission
          STRIP     XTRGPRID  * Primary Diagnosis
          STRIP     XTRGCOD1  * Additional Diagnosis 1
          STRIP     XTRGCOD2  * Additional Diagnosis 2
          STRIP     XTRGCOD3  * Additional Diagnosis 3
          STRIP     XTRGCOD4  * Additional Diagnosis 4
          STRIP     XTRGCOD5  * Additional Diagnosis 5
          STRIP     XTRGCOD6  * Additional Diagnosis 6
          STRIP     XTRGCOD7  * Additional Diagnosis 7
          STRIP     XTRGCOD8  * Additional Diagnosis 8
.
          STRIP     XPRMPROC  * Valid Procedure code
          STRIP     XADPROC1  * Additional Procedure 1
          STRIP     XADPROC2  * Additional Procedure 2
          STRIP     XADPROC3  * Additional Procedure 3
          STRIP     XADPROC4  * Additional Procedure 4
          STRIP     XADPROC5  * Additional Procedure 5
          STRIP     XADPROC6  * Additional Procedure 6
          STRIP     XADPROC7  * Additional Procedure 7
          STRIP     XADPROC8  * Additional Procedure 8
          STRIP     XTSTAGEN  * TNM State Edition Number - ptcdcscm
          STRIP     XTSTAGEC  * T Stage                  - ptcdcsgt
          STRIP     XNSTAGEC  * N Stage                  - ptcdcsgn
          STRIP     XMSTAGEC  * M Stage                  - ptcdcsgm
          STRIP     XTSTAGEG  * TNM Stage Group          - ptcdcssg
          STRIP     XTSTAGEB  * TNM Staging group basis  - ptcdbsdg
          STRIP     XOSTAGES  * Other Staging Scheme     - ptcdstag
          STRIP     XOSTAGEG  * Other Staging Grouping   - ptcdcssg
          STRIP     XOSTAGEB  * Other Staging Basis      - ptcdbsdg
          STRIP     XSURGFLG  * Surgery flag             - ptcdtsur
          STRIP     XSYSTFLG  * Systemic Therapy flag    - ptcdtchm
          STRIP     XSYSTID1  * <next release>
          STRIP     XSYSTID2  * <next release>
          STRIP     XRADIFLG  * Radiotherapy flag        - ptcdtrad
          STRIP     XRADIEID  * <next release>
          STRIP     XOTHRFLG  * Other flag               - ptcdtbio
          STRIP     XNOTRFLG  * No Treatment flag        - ptcdtnon
.
SFLD9999  RETURN
+
.**************************************************************************
.*                                GHDR0000
.*                     Write Extract Header data
.**************************************************************************
GHDR0000  MOVE      ZERO,EXIT
.
          MOVE      "H",XRECTYH
          PACK      XHOSPNO,PTCNESTB,SP4
          IF        IBCNMHOS=1
            MATCH     SP70,PTHSCFCD
            IF        !@EQUAL
              PACK      XHOSPNO,PTHSCFCD,SP4
            ENDIF
          ENDIF
          MOVE      CANCOUNT,XNOOFCAN
          REP       " 0",XNOOFCAN
.
          CALL      IBACLOCK
          PACK      XRUNDATE,CDD,CMM,CCC,CYY
          REP       " 0",XRUNDATE
          MOVE      CTIMEIS,DIM8
          REP       ": ",DIM8
          SQUEEZE   DIM8
          MOVE      DIM8,XRUNTIME
.
.                                    * extend record from 18 to 590 (eof 591)
.                                                * write the header
          IF        EFRMTYPE=1
            WRITE     EXTCANFL,SEQ;*+,XRECTYH,XHOSPNO,XNOOFCAN,XRUNDATE:  *  18
                                  SP70,SP70,SP70,SP70,SP70,SP70,SP70:     * 490
                                  SP70,SP10,SP2                           *  82
          ELSE
.
            STRIP     XHOSPNO                         * Hospital ID
.
            IF        OPTION=1
              UNPACK    STRTDATE,XCENT,XYEAR,XMON,XDAY  * Start date
              PACK      DIM8,XDAY,XMON,XCENT,XYEAR
.
              UNPACK    ENDDATE,XCENT,XYEAR,XMON,XDAY   * End date
              PACK      DIM8A,XDAY,XMON,XCENT,XYEAR
            ENDIF
.
            MOVE      FCOUNTER,XCOUNTER    * Count of records
            SQUEEZE   XCOUNTER
.
            MOVE      "I",KEY1             * Inpatient report type
.
            WRITE     CXTCANFL,SEQ;*+,XHOSPNO,COMMA,DIM8,COMMA:
                                  DIM8A,COMMA,XRUNDATE,COMMA,XRUNTIME,COMMA:
                                  XCOUNTER,COMMA,KEY1
          ENDIF
.
GHDR9999  RETURN
+
.**************************************************************************
.*                                MVEXT000
.*                       Move to web 'extracts' directory
.**************************************************************************
MVEXT000  MOVE      ZERO,EXIT
.
          IF        EFRMTYPE = 1
            MOVE      ONE,F1
          ELSE
            MOVE      TWO,F1
          ENDIF
.
          CLEAR     CMDLINE                 * cancer details file
          APPEND    "ibainp85.us1 ",CMDLINE
          APPEND    EXTCANTM,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    F1,CMDLINE              * 1 = txt / 2 = csv
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
.
.****************************************************************************
.*                                CHKMH000             Called by: ML0000    *
.*                        check and input multi hospital                    *
.****************************************************************************
CHKMH000  IF        IBCNMHOS = 1
            DISPLAY   *P1:3,*EF,"Hospital ID: ";
            MOVE      SP70,HOSPCODE
            MOVE      TEN5,ECOL
            MOVE      THREE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
            CALL      PATHSPKY
            BRANCH    EXIT,CHKMH950,CHKMH950
            MOVE      KEY3,HOSPCODE
            DISPLAY   *P22:3,*EL,PTHSNAME,*W1;
          ENDIF
.
CHKMH900  MOVE      ZERO,EXIT
          GOTO      CHKMH999
.
CHKMH950  MOVE      ONE,EXIT
CHKMH999  RETURN
.******************************************************************************
.         Extract Format
.******************************************************************************
XFRM0000  MOVE      SP70,ANS
          DISPLAY   *P1:5,*EF,"Extract Format: ":
                    *P30:5,"(1=Text, 2=CSV)";
          KEYIN     *P18:5,ANS;
          MOVE      ZERO,EFRMTYPE
          MOVE      ANS,EFRMTYPE
          BRANCH    EFRMTYPE,XFRM1000,XFRM1000
          COMPARE   ZERO,EFRMTYPE
          GOTO      XFRM9000 IF EQUAL
          BEEP
          GOTO      XFRM0000
.
XFRM1000  IF        EFRMTYPE=1
            DISPLAY   *P20:5,*EL,"- Text";
          ELSE
            DISPLAY   *P20:5,*EL,"- CSV";
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      XFRM9999
.
XFRM9000  MOVE      ONE,EXIT
XFRM9999  RETURN
+
.******************************************************************************
.*                             I/O for Temporary files                        *
.******************************************************************************
RSTEMP1   RESET     KEY8
          READ      TEMPAFIL,KEY8;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPAFIL;TEMPADMN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMPAFIL,KEY8;TEMPADMN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMPAFIL;TEMPADMN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     TEMPAFIL,KEY8;TEMPADMN,TEMPSPAR
          RETURN
.
DETEMP1   RESET     KEY8
          DELETE    TEMPAFIL,KEY8
          RETURN
.
.******************************************************************************
.
RSTCAN1   IF        EFRMTYPE=1
            RESET     KEY6
            READ      TEMPCANF,KEY6;;
          ELSE
            RESET     KEY11
            READ      TEMPCSVF,KEY11;;
          ENDIF
          RETURN
.
RKTCAN1   MOVE      ZERO,OVRCD
          IF        EFRMTYPE=1
            READKS    TEMPCANF;XCANNUMB,XRECTYT,XHOSPNO,XPSNAME,XPGFNAME:
                                  XPGSNAME,XAL1SNAM,XAL1FNAM,XAL1GNAM,XAL2SNAM:
                                  XAL2FNAM,XAL2GNAM,XAL3SNAM,XAL3FNAM,XAL3GNAM:
                                  XSEX,XINDIGST,XURNUMBR,XDOB,XFLATNO:
                                  XSTRTNO,XSTRTNAM,XSTRTTYP,XLOCATN,XPOSTCOD:
                                  XCOUNTRY,XMEDICAR,XDOCNAM,XGPNAME,XGPADDR:
                                  XADMDATE,XSEPDATE,XSEPSTAT,XMULTPRI,XDIAGDTE:
                                  XDIAGLOC,XLATERY,XLABNAM,XBASDIAG,XDRGSPRD:
                                  XREASADM,XTRGPRIM,XTRGMORP,XTRGCOD1,XTRGCOD2:
                                  XTRGCOD3,XTRGCOD4,XTRGCOD5,XTRGCOD6,XTRGCOD7:
                                  XTRGCOD8,XPRMDIAG,XPRMPROC,XSPARE
          ELSE
            READKS    TEMPCSVF;XCOUNTER,XHOSPNO,XMEDICAR,XURNUMBR:
                                  XPSNAME,XFRSNAME,XGIVNAME,XAS1SNAM,XAS1FNAM:
                                  XAS1GNAM,XAS2SNAM,XAS2FNAM,XAS2GNAM,XAS3SNAM:
                                  XAS3FNAM,XAS3GNAM,XSEX,XDOB,XCOUNTRY,XINDIGST:
                                  XAFLATNO,XURASNUM,XURASNAM,XURARTYP,XURARSUB:
                                  XPOSTCOD,XURASTAT,XGPNAME,XDOCNAMR,XGPDOCAD:
                                  XTRDOCRG,XTRDOCNM,XTRDOCAD,XADMDATE,XSEPDATE:
                                  XSEPSTAT,XDIAGLOC,XPATHLAB,XDIAGDAT,XTRGPRIM:
                                  XTRGMORP,XLATERY,XBASDIAG,XDRGSPRD,XREASADM:
                                  XTRGPRID,XTRGCOD1,XTRGCOD2,XTRGCOD3,XTRGCOD4:
                                  XTRGCOD5,XTRGCOD6,XTRGCOD7,XTRGCOD8,XPRMPROC:
                                  XADPROC1,XADPROC2,XADPROC3,XADPROC4,XADPROC5:
                                  XADPROC6,XADPROC7,XADPROC8,XTSTAGEN,XTSTAGEC:
                                  XNSTAGEC,XMSTAGEC,XTSTAGEG,XTSTAGEB,XOSTAGES:
                                  XOSTAGEG,XOSTAGEB,XSURGFLG,XSYSTFLG,XSYSTID1:
                                  XSYSTID2,XRADIFLG,XRADIEID,XOTHRFLG,XNOTRFLG
          ENDIF
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTCAN1   IF        EFRMTYPE=1
            WRITE     TEMPCANF,KEY6;XCANNUMB,XRECTYT,XHOSPNO,XPSNAME,XPGFNAME:
                                  XPGSNAME,XAL1SNAM,XAL1FNAM,XAL1GNAM,XAL2SNAM:
                                  XAL2FNAM,XAL2GNAM,XAL3SNAM,XAL3FNAM,XAL3GNAM:
                                  XSEX,XINDIGST,XURNUMBR,XDOB,XFLATNO:
                                  XSTRTNO,XSTRTNAM,XSTRTTYP,XLOCATN,XPOSTCOD:
                                  XCOUNTRY,XMEDICAR,XDOCNAM,XGPNAME,XGPADDR:
                                  XADMDATE,XSEPDATE,XSEPSTAT,XMULTPRI,XDIAGDTE:
                                  XDIAGLOC,XLATERY,XLABNAM,XBASDIAG,XDRGSPRD:
                                  XREASADM,XTRGPRIM,XTRGMORP,XTRGCOD1,XTRGCOD2:
                                  XTRGCOD3,XTRGCOD4,XTRGCOD5,XTRGCOD6,XTRGCOD7:
                                  XTRGCOD8,XPRMDIAG,XPRMPROC,XSPARE
          ELSE
            WRITE     TEMPCSVF,KEY11;XCOUNTER,XHOSPNO,XMEDICAR,XURNUMBR:
                                  XPSNAME,XFRSNAME,XGIVNAME,XAS1SNAM,XAS1FNAM:
                                  XAS1GNAM,XAS2SNAM,XAS2FNAM,XAS2GNAM,XAS3SNAM:
                                  XAS3FNAM,XAS3GNAM,XSEX,XDOB,XCOUNTRY,XINDIGST:
                                  XAFLATNO,XURASNUM,XURASNAM,XURARTYP,XURARSUB:
                                  XPOSTCOD,XURASTAT,XGPNAME,XDOCNAMR,XGPDOCAD:
                                  XTRDOCRG,XTRDOCNM,XTRDOCAD,XADMDATE,XSEPDATE:
                                  XSEPSTAT,XDIAGLOC,XPATHLAB,XDIAGDAT,XTRGPRIM:
                                  XTRGMORP,XLATERY,XBASDIAG,XDRGSPRD,XREASADM:
                                  XTRGPRID,XTRGCOD1,XTRGCOD2,XTRGCOD3,XTRGCOD4:
                                  XTRGCOD5,XTRGCOD6,XTRGCOD7,XTRGCOD8,XPRMPROC:
                                  XADPROC1,XADPROC2,XADPROC3,XADPROC4,XADPROC5:
                                  XADPROC6,XADPROC7,XADPROC8,XTSTAGEN,XTSTAGEC:
                                  XNSTAGEC,XMSTAGEC,XTSTAGEG,XTSTAGEB,XOSTAGES:
                                  XOSTAGEG,XOSTAGEB,XSURGFLG,XSYSTFLG,XSYSTID1:
                                  XSYSTID2,XRADIFLG,XRADIEID,XOTHRFLG,XNOTRFLG
          ENDIF
          RETURN
+
.******************************************************************************
.
.         I/O includes needed
.
          INC       STD001IO/PBL
.
          INC       CLPATMAS
          INC       CLPMSPX2
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBAPOSIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCANIO/INC            * Cancer Register Detail File
          INC       PATCRDIO/INC            * Cancer Register Detail File
          INC       PATCRGIO/INC            * Cancer Register Header File
          INC       PMSCDGIO/INC            * Cancer Reason for Clinical Diagn.
          INC       PATCODIO/INC            * patient codes file
...       INC       PATDADIO/INC            * Transfer Destination file
          INC       PATDO1IO/INC            * Patient Doctors File
          INC       PATDSCIO/INC            * Discharge File
          INC       PATECDIO/INC            * Patient episode disease file
          INC       PATECOIO/INC            * Patient episode operation file
          INC       PATGSRIO/INC            * Patient name file           
          INC       PATICDIO/INC            * ICD-9 File
          INC       PATMA1IO/INC            * patient master file
          INC       PATHSPIO/INC
          INC       PMSHCPIO/INC
          INC       PMSHCGIO/INC
          INC       PMSPX2IO/INC
          INC       PATMI1IO/INC            * admission file
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

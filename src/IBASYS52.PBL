.***************************************************************************
.* System    :   System Security                                           *
.* Program   :   IBASYS52                                                  *
.* Desc      :   System Security Report                                    *
.***************************************************************************
.* Date      :   27/08/91                                                  *
.* Author    :   Steve O'Gorman                                            *
.* Mods.     :                                                             *
.*            VC.01.01  16/12/96 Paul Howells   SRF 642397                 *
.*            Don't Print "No of Records" if exit program without printing *
.*            anything                                                     *
.***************************************************************************
.
          INC       STDGENDF
.
.
          INC       IBAPASFD/INC
          INC       IBASMAFD/INC
          INC       IBAUSEFD/INC
.
.***************************************************************************
.*                               Constants                                 *
.***************************************************************************
.
DASH78    INIT      "---------------------------------------":
                    "---------------------------------------"
.
PIPE      INIT      "|"
.
.
.***************************************************************************
.*                               Variables                                 *
.***************************************************************************
.
DIM30     DIM       30
.
ENDDCODE  DIM       3
.
ITEMNO    FORM      2
.
LINENUMB  FORM      2
.
NOOFRECS  FORM      3
.
STRTCODE  DIM       3
.
.
PRGID     INIT      "IBASYS52"
PRGNAM    INIT      "System Security Report"
VERSION   INIT      "V12.01.00"
.
.***************************************************************************
.*                       Mainline Controlling Logic                        *
.***************************************************************************
.
ML0000    CALL      INIT0000                * Initialise
.
ML0100    CALL      OPTN0000                * Main Option Screen
          BRANCH    EXIT,ML9999
.
ML0200    CALL      CLR0000                 * Clear Variables Needed
          CALL      SCRN0000                * Main Keyin Screen
          CALL      INPT0000                * Input Data
          BRANCH    EXIT,ML0100
.
          CALL      SEL0000                 * Select Item, Post, Cancel
          BRANCH    EXIT,ML0100
.
ML1000    CALL      PROC0000                * Process Records
.
          PRINT     *N,*N:
                    *N,"Number of Records = ",NOOFRECS;
.
ML9999    CHAIN     PGM
          STOP
.
.********************************************************************** 
.*                              INIT0000                              *
.*                           Initialise Routine                       *
.********************************************************************** 
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"Opening"
.
          DISPLAY   *P72:24,"ibacodef"
          OPEN      IBACODE1,"ibacodef"
.
          DISPLAY   *P72:24,"ibasmaaf"
          OPEN      IBASMAA1,"ibasmaaf"
.
          DISPLAY   *P72:24,"ibauseaf"
          OPEN      IBAUSEA1,"ibauseaf"
          OPEN      IBAUSEA2,"ibauseaf"
.
          DISPLAY   *P72:24,"ihapassf"
          OPEN      IHAPASS1,"ihapassf"
          OPEN      IHAPASS2,"ihapassf"
.
          RETURN
.
.********************************************************************** 
.*                              CLR0000                               *
.*                      Clear Variables Routine                       *
.********************************************************************** 
.
CLR0000   MOVE      ZERO,NOOFRECS
          MOVE      ZERO,LINENUMB
.
          RETURN
.
.********************************************************************** 
.*                              OPTN0000                              *
.*                         Main Option Screen                         *
.********************************************************************** 
.
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = System Security Report":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*V2LON,*EL,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    OPTION,OPTN8000
.
          BEEP
          GOTO      OPTN1000
.
OPTN8000  MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
.
.********************************************************************** 
.*                              SCRN0000                              *
.*                            Main  Screen                            *
.********************************************************************** 
.
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 1",*HOFF," = Start Security Code : ":
                    *P1:5,*V2LON," 2",*HOFF," = End Security Code   : ";
.
          RETURN
.
.********************************************************************** 
.*                            INPT0000                                *
.*                        Input All Fields                            *
.********************************************************************** 
.
INPT0000  CALL      STRT0000                * Keyin Start Code
          BRANCH    EXIT,INPT9000           * Exit Input Mode
.
INPT1000  CALL      FNSH0000                * Keyin End Code
          BRANCH    EXIT,INPT0000           * Go Back to Keyin Report Seq.
.
          MOVE      FALSE,EXIT              * Entered Ok.
          GOTO      INPT9999
.
INPT9000  MOVE      TRUE,EXIT
.
INPT9999  RETURN
.
.********************************************************************** 
.*                            STRT0000                                *
.*                        Keyin Start Code                            *
.********************************************************************** 
.
STRT0000  MOVE      ZERO,CKEYTYP
          MOVE      "28",CCOL
          MOVE      "04",CVERT
          MOVE      ZERO,HTOP
          DISPLAY   *PCCOL:CVERT,*EL;
          CALL      KIBSMA00                * Question Mark for System
          BRANCH    EXIT,STRT9000
.
          MOVE      IBSMNUMB,STRTCODE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,STRTCODE,*HOFF:
                    *P35:CVERT,IBSMNATV;
.
          MATCH     SP3,ENDDCODE
          GOTO      STRT8000 IF EQUAL
.
          MATCH     STRTCODE,ENDDCODE
          GOTO      STRT8000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Start System Must be Less than ":
                                  "End System - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      STRT0000
.
STRT8000  MOVE      FALSE,EXIT
          GOTO      STRT9999
.
STRT9000  MOVE      TRUE,EXIT
.
STRT9999  RETURN
.
.********************************************************************** 
.*                            FNSH0000                                *
.*                         Keyin End Code                             *
.********************************************************************** 
.
FNSH0000  MOVE      ZERO,CKEYTYP
          MOVE      "28",CCOL
          MOVE      "05",CVERT
          MOVE      ZERO,HTOP
          DISPLAY   *PCCOL:CVERT,*EL;
          CALL      KIBSMA00                * Question Mark for System
          BRANCH    EXIT,FNSH9000
.
          MOVE      IBSMNUMB,ENDDCODE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,ENDDCODE,*HOFF:
                    *P35:CVERT,IBSMNATV;
.
          MATCH     STRTCODE,ENDDCODE
          GOTO      FNSH8000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"End System must be Greater ":
                                  "than Start System - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      FNSH0000
.
FNSH8000  MOVE      FALSE,EXIT
          GOTO      FNSH9999
.
FNSH9000  MOVE      TRUE,EXIT
.
FNSH9999  RETURN
+
.**************************************************************************
.*                                   SEL0000                              *
.*                         select field to modify or post                 *
.**************************************************************************
.
SEL0000    MOVE      FALSE,EXIT
           CALL      MAINQST
           COMPARE   ZERO,CCITEM                * ok to post
           GOTO      SEL9999 IF EQUAL
           COMPARE   "-1",CCITEM                * cancel option
           GOTO      SEL9000 IF EQUAL
           BRANCH    CCITEM,SEL1000,SEL2000
.
           BEEP
           GOTO      SEL0000
.
SEL1000    CALL      STRT0000               * Keyin Start Code
           GOTO      SEL0000
.
SEL2000    CALL      FNSH0000               * Keyin End Code
           GOTO      SEL0000
.
SEL9000    MOVE      TRUE,EXIT
.
SEL9999  RETURN
.
.********************************************************************** 
.*                            PROC0000                                *
.*                     Print Data From Temp File                      *
.********************************************************************** 
.
PROC0000  DISPLAY   *P1:24,*EL:
                    *P20:24,"Printing : ";
.
          MOVE      ZERO,CPAGENO
          CALL      PAGE0000                * New Page
.
          PACK      KEY3,STRTCODE,SP30
          CALL      RDIBSMA1
          GOTO      PROC1500
.
PROC1000  CALL      RKIBSMA1
          BRANCH    OVRCD,PROC9999
.
PROC1500  MATCH     IBSMNUMB,ENDDCODE
          GOTO      PROC9999 IF LESS
.
          DISPLAY   *P31:24,*EL,*V2LON,IBSMNUMB;
.
          CALL      HEAD0000                * Display Heading
.
          PACK      KEY7,IBSMNUMB,SP30
          CALL      RSIBUSE2
.
PROC2000  CALL      RKIBUSE2
          BRANCH    OVRCD,PROC2999
.
          MATCH     IBSMNUMB,IBUSSYST
          GOTO      PROC2999 IF NOT EQUAL
.
          PACK      KEY4,IBUSCODE
          CALL      RDPASS1
          BRANCH    OVRCD,PROC2000
.
          PACK      KEY5,SECDEPT,SP5
          CALL      RDICOD1
          BRANCH    OVRCD,PROC2000
.
          CALL      LINE0000
.
          COMPARE   FIFTY5,LINENUMB
          GOTO      PROC2000 IF LESS
.
          CALL      TAIL0000
          CALL      PAGE0000
          CALL      HEAD0000
          GOTO      PROC2000
.
PROC2999  CALL      TAIL0000
.
          COMPARE   FIFTY,LINENUMB
          GOTO      PROC1000 IF LESS
.
          CALL      PAGE0000
          GOTO      PROC1000
.
PROC9999  RETURN
.
.********************************************************************** 
.*                            LINE0000                                *
.*                 Print a Summary Line for Record                    *
.********************************************************************** 
.
LINE0000  PRINT     *N,*1,PIPE,SP1,SECODE:
                       *8,PIPE,SP1,SECUSER:
                      *46,PIPE,SP1,IBUSLEVL:
                      *52,PIPE,SP1,SECDEPT,SP1,PRXDESC:
                      *80,PIPE;
.
          ADD       ONE,NOOFRECS
          ADD       ONE,LINENUMB
.
          RETURN
.
.********************************************************************** 
.*                            PAGE0000                                *
.*                       Print a Page Break                           *
.********************************************************************** 
.
PAGE0000  CLOCK     TIME,CTIMEIS
.
          MOVE      ONE,CNOUNDLN
          CALL      HEAD80
.
          PRINT     *20,"Systems From ",STRTCODE," to ",ENDDCODE
.
          MOVE      FIVE,LINENUMB
.
          RETURN
.
.********************************************************************** 
.*                            HEAD0000                                *
.*                     Print the Page Heading                         *
.********************************************************************** 
.
HEAD0000  ADD       FOUR,LINENUMB
.
          PRINT     *N,*1,"System Code : ",IBSMNUMB,SP2,IBSMNATV:
                    *N,*1,ASK,DASH78,ASK:
                    *N,*1,PIPE," Code":
                       *8,PIPE," User Name":
                      *46,PIPE," Sec":
                      *52,PIPE," Department":
                      *80,PIPE:
                    *N,*1,ASK,DASH78,ASK;
.
HEAD9999  RETURN
.
.********************************************************************** 
.*                            TAIL0000                                *
.*                       Print the Page Tail                          *
.********************************************************************** 
.
TAIL0000  PRINT     *N,*1,PIPE:
                       *8,PIPE:
                      *46,PIPE:
                      *52,PIPE:
                      *80,PIPE:
                    *N,*1,ASK,DASH78,ASK
.
          ADD       THREE,LINENUMB
.
          RETURN
.
.********************************************************************** 
.*                        I/O Routines                                *
.********************************************************************** 
.
          INC       IBAPASIO/INC
.
          INC       IBASMAIO/INC
          INC       IBAUSEIO/INC
.
          INC       IBAPASDS
          INC       IBASMAKY
.
          INC       STDHLPCD
          INC       STD001IO

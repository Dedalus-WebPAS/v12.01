.*****************************************************************************
.*    Program      : FX310703                                                *
.*    Description  : Fix it for STV Public patient Misc.charge pending       *
.*                                                                           *
.*    Author       : J.Tan                                                   *
.*    Date         : 06/05/2015                                              *
.*    Modifications:                                                         *
.*                  V10.06.03 03/06/2015 J.Tan        CAR 310703             *
.*                            Mods to keyin a claim code                     *
.*                  V10.06.02 28/05/2015 J.Tan        CAR 310703             *
.*                            Changed to use CLAIM CODE = PUB                *
.*                  V10.06.01 18/05/2015 J.Tan        CAR 310703             *
.*                            Added Start and End date/time processing       *
.*                  V10.06.00 06/05/2015 J.Tan        CAR 310703             *
.*                            Created fix                                    *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATCONFD/INC
          INC       PMSMTIFD/INC
          INC       PATMCHFD/INC
          INC       PATMI1FD/INC
          INC       PATCODFD/INC
          INC       PATFN1FD/INC
          INC       PMSVX1FD/INC
          INC       PATHSPFD/INC
.
CLAIMCD   DIM       3
CLAIMDS   DIM       20
.
RECNUM    FORM      8
RECUPD    FORM      8
FORM12P2  FORM      12.2
KEY21A    DIM       21
IBCNMHOS  FORM      1             * multi-campus flag
.
PRGNAM    INIT      "Fix Misc. Charge Pending"
PRGID     INIT      "FX310703"
VERSION   INIT      "V12.01.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000,MAIN1000  * proceed 
          GOTO      MAIN0100
.
MAIN1000  CALL      KCOD0000                      * Keyin claim code
          BRANCH    EXIT,MAIN0100
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN2000,MAIN0100,MAIN0100 * Yes, no, cancel
.
MAIN2000  CALL      PROC0000                     * process
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,RECUPD
          OPEN      PMSMTIA3,"pmsmtiaf"
          OPEN      PATMCHG1,"patmchgf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATHSPA1,"pathspaf"
.
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Print":
                    *P1:6,*V2LON,TWO,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * print/run
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                               KCOD0000                             *
.**********************************************************************
KCOD0000  MOVE      SP70,CLAIMCD
          MOVE      SP70,CLAIMDS
          KEYIN     *P1:19,*EL,"Keyin Claim Code : ",*P20:19,*V2LON,CLAIMCD
          ENDSET    CLAIMCD
          APPEND    SP70,CLAIMCD
          RESET     CLAIMCD
.
          MATCH     SP70,CLAIMCD
          GOTO      KCOD9000 IF EQUAL
.
          MOVE      "CL",KEY2
          PACK      KEY5,KEY2,CLAIMCD
          CALL      RDCODE1
          BRANCH    OVRCD,KCOD1000
.
          DISPLAY   *P25:19,*EL,*V2LON,TDESC
          MOVE      TDESC,CLAIMDS
          MOVE      ZERO,EXIT
          GOTO      KCOD9999
.
KCOD1000  DISPLAY   *P20:19,*EL,*V2LON,"Invalid Claim code. ";
          CALL      HITENTER
          GOTO      KCOD0000
.
KCOD9000  MOVE      ONE,EXIT
KCOD9999  RETURN
+
.**********************************************************************
.*                               PROC0000                             *
.**********************************************************************
PROC0000  DISPLAY   *P1:18,*EL,"Record No. : ";
          MOVE      ZERO,CPAGENO                  * initialise print variables
          MOVE      ZERO,CNOUNDLN
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:20,"Started  : ",CDATE,SP1,CTIMEIS
.
          CALL      HEAD0000                      * print header
.
          MOVE      "3",KEY1                      * misc.item
          PACK      KEY23,KEY1,SP70
          CALL      RSPMMTI3
PROC1000  CALL      RKPMMTI3                      * read next record
          BRANCH    OVRCD,PROC6000                * no more records
.
          MATCH     "3",PMMIRTYP
          GOTO      PROC6000 IF NOT EQUAL
.
          MATCH     " 3",PMMISYST                  * Inpatient
          GOTO      PROC1000 IF NOT EQUAL
.
          ADD       ONE,RECNUM
          IF        (RECNUM%1000) = 0
            DISPLAY   *P25:18,*V2LON,RECNUM
          ENDIF
.
          MOVE      PMMIVISN,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PROC1000
.
          MATCH     SP70,ACLAIM
          GOTO      PROC1000 IF EQUAL
.
          MATCH     CLAIMCD,ACLAIM
          GOTO      PROC1000 IF NOT EQUAL        * Not selected code
.
          MOVE      PMMIAMTP,PMMIAMTT
          ADD       PMMIRBAT,PMMIAMTT            * set up total
          COMPARE   ZERO,PMMIAMTT
          GOTO      PROC1000 IF EQUAL            * Zero amount
.
          MOVE      ZERO,FORM12P2
          CALL      GMIS0000                     * recalculate misc.
          MOVE      MPATPOR,FORM12P2
          ADD       MHFPOR,FORM12P2              * New charge
.
          ADD       ONE,RECUPD
.
          BRANCH    COPTION,PROC3000             * print only
.
.         fix up record
.
          MOVE      MPATPOR,PMMIAMTP
          MOVE      MHFPOR,PMMIRBAT
.
          MOVE      MPATPOR,PMMIAMTT
          ADD       MHFPOR,PMMIAMTT
          IF        PMMISERV<>0
            MULT      PMMISERV,PMMIAMTT
          ENDIF
          CALL      UPPMMTI3
.
          PRINT     *1,PMMIVISN,*15,KEY10,*27,CPCDATE:
                    *41,PMMIITEM,*55,PMMIAMTT,*72,FORM12P2
          GOTO      PROC1000                      * get next record
.
.         print record
.
PROC3000  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          PACK      KEY10,CPCDATE
.
          UNPACK    PMMITDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          IF        PMMISERV<>0
            DIV       PMMISERV,PMMIAMTT           * Unit price
          ENDIF
.
          PRINT     *1,PMMIVISN,*15,KEY10,*27,CPCDATE:
                    *41,PMMIITEM,*55,PMMIAMTT,*72,FORM12P2
.
          GOTO      PROC1000                      * get next record
.
PROC6000  IF        COPTION=1
            PRINT     *1,"No of Records : ",RECUPD
          ELSE
            PRINT     *1,"No of Records Updated : ",RECUPD
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:21,*EF,"No of Records read     : ",RECNUM
          DISPLAY   *P1:22,*EF,"No of Records processed: ",RECUPD
          DISPLAY   *P1:23,"Ended  : ",CDATE,SP1,CTIMEIS
.
          DISPLAY   *P1:24,*EL,"Finished processsing. ";
.
          CALL      HITENTER
.
PROC9999  RETURN
+
.******************************************************************************
.         Subroutine to get the appropriate miscellaneous charges record
.******************************************************************************
GMIS0000  MOVE      ZERO,EXIT
.                                         * convert "H/F Table" to "Table Type"
          MOVE      SP10,PTHFBCAT           * Initialise table type
          PACK      KEY14,AFUNDH,AFNDTB,SP20
          CALL      RDFUND1                 * read health fund file
.
          PACK      KEY29,ACLAIM,AFUNDH,PTHFBCAT,PMMIITEM,PMMITDAT,SP20
          CALL      PATMCHRD                * Read misc. charge file
          COMPARE   ZERO,EXIT
          GOTO      GMIS9999 IF EQUAL
.
          PACK      KEY29,ACLAIM,SP6,SP3,PMMIITEM,PMMITDAT,SP20
          CALL      PATMCHRD                * Read misc. charge file
          COMPARE   ZERO,EXIT
          GOTO      GMIS9999 IF EQUAL
.
          CALL      DCLM0000        * set default claim code - returns PTCNDCLM
          PACK      KEY29,PTCNDCLM,SP6,SP3,PMMIITEM,PMMITDAT,SP20
          CALL      PATMCHRD                * Read misc. charge file
.
GMIS9999 RETURN
+
.****************************************************************************
.*                            HEAD0000                 Called by: PROC0000  *
.*                       Print page heading                       PRIN0000  *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
          PRINT     *40,"Claim Code : ",CLAIMCD," - ",CLAIMDS
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,"Adm Number",*15,"Adm.date",*27,"Trans.Date ":
                    *41,"Misc.Charge":
                    *55,"     Old Charge",*72,"     New Charge"
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      EIGHT,CLNO                   * set line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: PROC0000  *
.*                      Draw line across page                     HEAD0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------"
.
          ADD       ONE,CLNO                     * increment line count
.
LINE9999  RETURN
+
          INC       PATMCHRD
          INC       PATDEFCL
.
          INC       PMSMTIIO/INC
          INC       PATMCHIO/INC
          INC       PATMI1IO/INC
          INC       PATCODIO/INC
          INC       PATFN1IO/INC
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC
          INC       STD001IO

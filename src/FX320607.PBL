.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   FX320607                                                    *
.* Desc      :   Fixit for patchcof                                          *
.*****************************************************************************
.* Date      :   13/08/2015                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through patchcof looking for records *
.*               where the date/time match that of pattranf and if found,    *
.*               those records are deleted.  A report will be printed at the *
.*               end of processing, listing all the deleted records.         *
.*                                                                           *
.* V12.00.01 22/05/2025 PJ Le Febour   TSK 09550096 AlphaNum VisitNum        *
.*                      amend   COMPARE  CHADMN,TADMN to MATCH CHADMN,TADMN  *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCHCFD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
LINENOTE  DIM       70
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
.
PRGID     INIT      "FX320607"
PRGNAM    INIT      "Fixit for patchcof (Cat CC)"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patchcof";
          OPEN      PATCHCO1,"patchcof"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION  1 = Run Fixit (Report Only)                         *
.*                       2 = Run Fixit                                       *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (Report Only)":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit" 
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run fixit (report only)
                            OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*              Loop through patchcof looking for records that match         *
.*          pattranf records (date & time) and if found, delete them.        *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,COUNTER                 * initialise record counter
.
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      HEAD0000                     * print report header
.
          MOVE      SP30,KEY24
PROC0500  CALL      RDSCHCO1                     * position at start of file
PROC1000  CALL      RDKCHCO1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,CHADMN,CHDATE,CHTIME;
.
          MATCH     "CC",CHCATG                  * Category CC ?
          GOTO      PROC1000 IF NOT EQUAL        * no - ignore record
.
          PACK      KEY30,CHADMN,CHDATE,CHTIME,SP70
          CALL      RDSTRAN2                     * position before adm/date/time
          CALL      RDKTRAN2                     * read next record
          BRANCH    OVRCD,PROC1000               * eof - no match found
.
          MATCH     CHADMN,TADMN                 * same admission still ?
          GOTO      PROC1000 IF NOT EQUAL        * no - get next record
.
          MATCH     CHDATE,TDATE                 * same date ?
          GOTO      PROC1000 IF NOT EQUAL        * no - get next record
.
          MATCH     CHTIME,TTIME                 * same time ?
          GOTO      PROC1000 IF NOT EQUAL        * no - get next record
.
.         We have found a matching record, so first check if acare
.         is blank on the admission record
.
          ADD       ONE,COUNTER                  * increment record counter
.
          MOVE      SP70,LINENOTE                * initialise note field
          MOVE      CHADMN,KEY8
          CALL      RDMISS1                      * admission record found ?
          BRANCH    OVRCD,PROC5000               * no
.
          MATCH     SP3,ACARE                    * adm. care type blank ?
          IF        @EQUAL
            MOVE      "Blank admission care type (patma1af.acare)",LINENOTE
          ENDIF
.
PROC5000  CALL      DISP0000                     * print record for deletion
.
.         Now delete the patchcof record
.
          IF        COPTION = 2
            PACK      KEY24,CHADMN,CHDATE,CHTIME
            CALL      DECHCO1                    * delete patchcof record
            GOTO      PROC0500                   * reposition on deleted record
          ENDIF
.
          GOTO      PROC1000                     * get next record
.
PROC9000  CALL      LINE0000
.
          IF        COPTION = 1
            PRINT     *N,COUNTER,*HOFF," records found.  "
          ELSE
            PRINT     *N,COUNTER,*HOFF," records deleted.  "
          ENDIF
          PRINT     *N,*1,"*** End of Report ***"
.
          DISPLAY   *P1:23,*EF,"Fixit completed.";
          IF        COPTION = 1
            DISPLAY   *P1:24,*V2LON,COUNTER,*HOFF," records found.  ";
          ELSE
            DISPLAY   *P1:24,*V2LON,COUNTER,*HOFF," records deleted.  ";
          ENDIF
          CALL      HITENTER
.
PROC9999  RETURN
+
.****************************************************************************
.*                            DISP0000                 Called by: PROC0000  *
.*                  Valid record so print it                                *
.****************************************************************************
.
DISP0000  COMPARE   FIFTY5,CLNO                  * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          UNPACK    CHDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,PIPE,*7,CHCODE,*15,PIPE,*22,CHADMN,*34,PIPE,*36,CPCDATE:
                    *48,PIPE,*50,CHTIME,*59,PIPE,*61,LINENOTE,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DISP9999  RETURN
+
.****************************************************************************
.*                            HEAD0000                 Called by: PROC0000  *
.*                       Print page heading                       DISP0000  *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Cat CC Code",*15,PIPE,*17,"Admission Number":
                    *34,PIPE,*36,"   Date   ",*48,PIPE,*50,"  Time  ":
                    *59,PIPE,*61,"Notes",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SIX,CLNO                     * increment line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: PROC0000  *
.*                      Draw line across page                     HEAD0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCHCIO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC

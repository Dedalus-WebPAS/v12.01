.******************************************************************************
.*  System    :  HMS                                                          *
.*  Program   :  IBAHMS30                                                     *
.*  Function  :  An Admission List for Epworth - Quote 7449                   *
.******************************************************************************
.*  Author    :  Whirlpool                                                    *
.*  Date      :  09/09/1992                                                   *
.*  Mods.     :                                                               *
.******************************************************************************
.* V12.00.01 03/06/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added include SEXDSCIO                                    *
.*        V10.12.01 08/01/2018 Richa Phogat   Task 0850234                    *
.*                  Updated logic in WTMP0000 to use 'Expected Ward' instead  *
.*                  of 'Post op ward' (from BOKRX1FD) for Medical Bookings/   *
.*                  Pre-Admissions
.*        V10.05.01 27/02/2015 Jill Parkinson CAR 313634                      *
.*                  Fixed pack of KEY8 with BKREURNO instead of FORM8 before  *
.*                  call to RDMAST1                                           *
.*        V10.04.01 12/06/2014 Steve Armstrong CAR 301639                     *
.*                  Moved call to TFILENAM into INIT0000 and added call to    *
.*                  KILL0000 on exit of program.                              *
.******************************************************************************
.*        V10.03.03 19/12/2012 Patrick Adair   CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.03.02 17/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V10.03.01 18/03/2012 Peter McMullen CAR 258587                      *
.*                  Remove references to redundant file patyears              *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.11.02  08/01/2009  Steve Armstrong  CAR 185927                   *
.*                  Removed isadd on temp file and included extra index in    *
.*                  isbuild.                                                  *
.*        V9.11.01  22/10/2008  Jill Habasque    CAR 174698                   *
.*                  Added move of BKREBOOK to TMPADMN before calling GDGC0000 *
.******************************************************************************
.*        V9.10.02  31/07/2008  Mike Laming      CAR 174698                   *
.*                  Add Unique No. to Key for read of "bokdiaaf" at GDGC0000  *
.*        V9.10.01  13/06/2008  Mike Laming      CAR 168190                   *
.*                  Change test of BKRXPOWD to allow Booking record thru -    *
.*                  change at WTMP1200                                        *
.******************************************************************************
.*        V9.09.02  29/10/2007  Steve Armstrong  CAR 152934                   *
.*                  Mods to make Hospital ID keyin non-mandatory (thus        *
.*                  allowing "All Hospitals" as a valid selection).           *
.*                  Moved Hospital keyin to last keyin.                       *
.*                  Fixed Booking Type keyin and Doctor Type keyin to use     *
.*                  standard routines.                                        *
.*        V9.09.01  13/06/2007  Steve Armstrong  CAR 142350                   *
.*                  Mods to use pre-admission ward/bed (award/abed) instead   *
.*                  of booking (expected) ward/bed if patient is pre-admitted.*
.*                  Mods to use pre-admission diagnosis/comments if the       *
.*                  corresponding booking fields are blank.                   *
.*                  Added Post-op ward (bkrxpowd) and removed Club Membership *
.*                  (pusr1) column.                                           *
.*                  Mods to use ptmixwrd/ptmiebed instead of award/abed.      *
.*        V9.09.00  13/06/2007  Steve Armstrong  CAR 142145                   *
.*                  Ported from V9.06.                                        *
.******************************************************************************
.*        V9.06.00  09/01/2007  Manjunath.N                                   *
.*                  Added multi-hospital functionality                        *
.******************************************************************************
.*        V6.12.00  17/05/2004  Mike Laming   CAR 49016                       *
.*                  July 2004 PRS/2 - changed to use sex routine SEXDSC00     *
.*        V6.07.01  28/08/2000  Steve Downing                                 *
.*                  Mods to include Indeterminate and Unknown sex             *
.*        V6.06.01  10/02/1999  Jill Habasque                                 *
.*                  Fixed DISPHEAD display                                    *
.*        V6.05.01  08/12/1998 Jill Habasque                                  *
.*                  Removed CCENTRY                                           *
.*        V6.00.01  16/10/1992  Whirlpool         SRF 118007                  *
.*                  Changed to order by doctor surname not code               *
.*        V6.00.02  30/10/1992  i chung           SRF 118115                  *
.*                  Modify to delete temp files after printing report         *
.*        V6.00.03  23/12/1992  Steve Armstrong   SRF 118820  Quote 7540      *
.*                  Added time range to options 1 and 2.                      *
.*                  Added question to print diagnosis details.                *
.*                  Added ward and bed to printout.                           *
.*        V6.00.04  13/01/1993  Graeme Williams   SRF 118820  Quote 7540      *
.*                  Fixed printing of diagnosis and comments                  *
.*        V6.00.05  14/01/1993  Graeme Williams   SRF 118820  Quote 7540      *
.*                  Fixed printing of preferred accomodation                  *
.*        V6.00.06  15/01/1993  Graeme Williams   SRF 118820  Quote 7540      *
.*                  Print claim code instead of description                   *
.*        V6.00.07  18/01/1993  Graeme Williams   SRF 118820  Quote 7540      *
.*                  Use admission details whenever possible                   *
.*        V6.00.08  25/01/93  J.Tan        SRF 120282                         *
.*                  Use the admission date and time from booking not admission*
.*        V6.00.09  04/02/1993  i chung           SRF 120375                  *
.*                  Moved Bed to be on the same line as Ward                  *
.*        V6.01.01  04/02/1994  Graeme Williams   SRF 127675                  *
.*                  Fixed getting of diagnosis if attending doctor is not the *
.*                  doctor in charge of the clinic.                           *
.*        V6.01.03  15/06/94 J.Tan      SRF 130027                            *
.*                  Fixed end time to include seconds                         *
.*        V6.01.04  15/06/94 I.Rutt     SRF 129735                            *
.*                  Modified so if report has no data, the report will show   *
.*                  date and correct day                                      * 
.*        V6.02.01  31/08/1994  Greg Horvat      SRF 127348  Quote 8088       *
.*                  Added new option 4 - Doctor Speciality Sequence, which is *
.*                  system parameter driven                                   *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCALAG/INC
.
          INC       BOKDIAFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       OPRDEAFD/INC
.
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATPR1FD/INC
          INC       PATWR1FD/INC
          INC       PATCONFD/INC
          INC       PATHSPFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.
.         TEMP FILES
.
TMPHMSA1  IFILE     SQL, FIXED=293, KEY="U1-8,37-56,57-64"
TMPHMSA2  IFILE     SQL, FIXED=293, KEY="U1-8,17-36,9-16,37-56,57-64"
TMPHMSA3  IFILE     SQL, FIXED=293, KEY="U1-8,9-16,37-56,57-64"
.
.Name     Type    Length     Phys     Start    Description
.------   ----    ------     ----     -----    ----------------------------
TMPDATE   DIM       8         8        1        Expected date            
TMPTIME   DIM       8         8        9        Expected assessment time
TMPDOCSN  DIM       20        20       17       Doctor surname
TMPSNAME  DIM       20        20       37       Surname
TMPADMNO  DIM       8         8        57       Admission / booking number
TMPADOCT  DIM       6         6        65       Attending Doctor
TMPPACCM  DIM       3         3        71       Preferred Accomodation
TMPCLAIM  DIM       3         3        74       Claim code
TMPGNAME  DIM       25        25       77       Given names
TMPTITLE  DIM       4         4        102      Title
TMPSEX    DIM       1         1        106      Sex
TMPAGE    FORM      3         3        107      Age
TMPURNO   DIM       8         8        110      U/R Number 
TMPPOWRD  DIM       3         3        118      Post-op ward
TMPDIAG   DIM       9         9        121      Diagnosis
TMPDIAG1  DIM       37        37       130      Diagnosis description one
TMPDIAG2  DIM       37        37       167      Diagnosis description two
TMPDIAG3  DIM       37        37       204      Diagnosis description three
TMPCOMM   DIM       37        37       241      Comment                  
TMPASDOC  DIM       6         6        278      Associated Doctor
TMPWARD   DIM       3         3        284      Ward
TMPBED    DIM       3         3        287      Bed
TMPDOCTP  DIM       3         3        290      Doctor Type
.
.End of Record                         293
+
.
.         WORK VARIABLES
.
BEDESC    DIM       20
BJDAY     FORM      5
BOOKEND   DIM       3
BOOKSTRT  DIM       3
BSDESC    DIM       20
.
IBCNMHOS  FORM      1
HOSCODE   DIM       3
.
CJDAY     FORM      5
CLTYDESC  DIM       3 
CLUBDESC  DIM       3 
CMDLINE   DIM       95
CODE      DIM       2
COM       DIM       1
.
DAYOFWK   DIM       9
DEDESC    DIM       20
DIAGFLG   FORM      1
DIM5      DIM       5
DIM10     DIM       10
DIM16     DIM       16
DIM30     DIM       30
DIM80     DIM       80
DOCNAME   DIM       16
DOCNAME2  DIM       16
DOCTEND   DIM       3
DOCTSTRT  DIM       3
DSDESC    DIM       20
DUEDATE   DIM       10
.
EDATED10  DIM       10
ENDDATE   DIM       8
ETIME     DIM       8
.
FILENAME  DIM       8
.
MENUOPTN  FORM      1
PACCDESC  DIM       2
PCOMFLAG  FORM      1                       * Print comments flag 
.                                             0 = No   1 = Yes
REPDATE   DIM       8
ROW       FORM      2
.
SDATE     DIM       10
SEX       DIM       8                                         * was 7  *C-49016
STIME     DIM       8
STRTDATE  DIM       8
.
TFILE     DIM       6
TIMEEND   DIM       8
TIMESTRT  DIM       8
TOTAL     FORM      5
PSAGETYP  DIM       3
PSAGECON  DIM       3
.
.         CONSTANTS
.
.
SP37      INIT      "                                     "
ZEDS      INIT      "zzzzzzzzzz"
.      
PRGID     INIT      "IBAHMS30"
PRGNAM    INIT      "EXPECTED ADMISSION LIST"     
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              ML0000                                       *
.*                             Mainline                                      *
.*****************************************************************************
ML0000    CALL      INIT0000                * initialization
          BRANCH    EXIT,ML9999             * exit
.
ML1000    CALL      MENU0000                * Handle Menu
          BRANCH    MENUOPTN,ML2000,ML2000,ML2000,ML2000
          GOTO      ML9999                  * Exit
.
ML2000    CALL      QCOM0000                * Comments ?
.
ML2500    CALL      GDAT0000                * get the required date range
          BRANCH    EXIT,ML1000
.
          BRANCH    MENUOPTN,ML2600,ML2600,ML2700,ML2600
.
ML2600    CALL      GTIM0000                * get time range if option 1 or 2
.
ML2700    CALL      GBTY0000                * get booking type
          BRANCH    EXIT,ML1000             * exitchar input
.
          BRANCH    MENUOPTN,ML2900,ML2900,ML2900,ML2800
.
ML2800    CALL      GSAD0000                * get speciality/att. doctor type
          BRANCH    EXIT,ML1000             * exitchar input
.
ML2900    CALL      GDIG0000                * see if diagnosis to be printed
.
          CALL      KHOS0000                * keyin hospital (multi-campus only)
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to continue ?
          BRANCH    CEXIT,ML3000,ML2500,ML1000
.                         Yes    No     Cancel
.
ML3000    CALL      MAKE0000                * Create the temp file
          CALL      WTMP0000                * write the data to the tempfile
          CALL      PRNT0000                * Print the report
          CALL      KILL0000                * Delete temp files after printing
          GOTO      ML1000
.
ML9999    CALL      KILL0000                * remove temp file
          CHAIN     PGM
+
.*****************************************************************************
.*                              INIT0000                                     *
.*                        Initialization section                             *
.*****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening bokdiaaf";
          OPEN      BOKDIAA1,"bokdiaaf"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A4,"bokrc1af"
.
          DISPLAY   *P64:24,"bokrx1af";
          OPEN      BOKRX1A1,"bokrx1af"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes";
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpr1af";
          OPEN      PATPR1A1,"patpr1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS = 1
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          MOVE      ONE,CNEWDS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FILENAME
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9100  MOVE      ONE,EXIT

INIT9999  RETURN
+
.*****************************************************************************
.*                               MENU0000                                    *
.*                      Handle the programs menu routine                     *
.*****************************************************************************
MENU0000  MOVE      ZERO,MENUOPTN
.
          CALL      DISPHEAD
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Time/Doctor/Surname Sequence":
                    *P1:6,*V2LON,"2",*HOFF," = Time/Surname/Sequence":
                    *P1:7,*V2LON,"3",*HOFF," = Surname Sequence":
                    *P1:8,*V2LON,"4",*HOFF," = Doctor Speciality Sequence":
                    *P1:10,"Select Option : ";
.
MENU1000  KEYIN     *P17:10,*EL,*DV,UNDLN1,*P17:10,*V2LON,MENUOPTN;
.
          BRANCH    MENUOPTN,MENU3000,MENU4000,MENU5000,MENU6000
.
          COMPARE   ZERO,MENUOPTN
          GOTO      MENU9999 IF EQUAL
.
          BEEP
          GOTO      MENU1000
.
MENU3000  DISPLAY   *P52:2,*V2LON," - Time/Doctor/Surname Seq";
          MOVE      "-Time/Doctor/Surname",CPHDROPT
          GOTO      MENU9999
.
MENU4000  DISPLAY   *P52:2,*V2LON," - Time/Surname Sequence";
          MOVE      " - Time/Surname Seq.",CPHDROPT
          GOTO      MENU9999
.
MENU5000  DISPLAY   *P52:2,*V2LON," - Surname Sequence";
          MOVE      " - Surname Sequence ",CPHDROPT
          GOTO      MENU9999
.
MENU6000  DISPLAY   *P52:2,*V2LON," - Doctor Speciality Seq";
          MOVE      " - Doctor Spec. Seq.",CPHDROPT
.
MENU9999  RETURN
+
.*****************************************************************************
.*                               QCOM0000                                    *
.*                      Ask if they want comments printed                    *
.*****************************************************************************
QCOM0000  MOVE      ZERO,PCOMFLAG
.
QCOM1000  DISPLAY   *P1:12,*EL,"Do you wish to print patient comments also ":
                          "(",*V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ?";
.
QCOM2000  KEYIN     *P52:12,*EL,*DV,UNDLN1,*P52:12,*V2LON,COM;
          REP       UPPLOW,COM
          DISPLAY   *P52:12,*EL,*V2LON,COM;
.
          MATCH     ANSN,COM
          GOTO      QCOM3000 IF NOT EQUAL
          MOVE      ZERO,PCOMFLAG
          GOTO      QCOM9999
.
QCOM3000  MATCH     ANSY,COM
          GOTO      QCOM4000 IF NOT EQUAL
          MOVE      ONE,PCOMFLAG
          GOTO      QCOM9999
.
QCOM4000  BEEP
          GOTO      QCOM2000
.
QCOM9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000       Called by:                 *
.*                For multi-hospital, get the hospital code                   *
.******************************************************************************
.
KHOS0000  COMPARE   ONE,IBCNMHOS                 * using multi-hospital ?
          GOTO      KHOS9999 IF NOT EQUAL        * no - finished
.
          DISPLAY   *P1:22,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      "22",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND                * Not Mandatory
.
          CALL      PATHSPKY                     * get code
          BRANCH    EXIT,KHOS2000:               * nothing input
                         KHOS9500                * exitchar input
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          DISPLAY   *P15:22,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS3000  DISPLAY   *P25:22,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
.
KHOS9999  RETURN
+
.*****************************************************************************
.*                               GDAT0000                                    *
.*                      Get the required date range                          *
.*****************************************************************************
GDAT0000  DISPLAY   *P1:14,"Start Date : ":
                    *P1:15,"End   Date : ";
.
          UNPACK    SP20,STRTDATE,ENDDATE
          MOVE      CCC,CCENT
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CHIGHLT
          MOVE      "14",CCOL
.
GDAT4000  MOVE      "14",CVERT
          DISPLAY   *P14:14,*EL,*P14:15,*EF;
          MOVE      ZERO,EXIT
.
          UNPACK    SP20,CDAY,CMON,CYEAR
          CALL      KEYDATE
          BRANCH    CQUEST,GDAT4000
          BRANCH    OVRCD,GDAT9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          PACK      SDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",SDATE
.
GDAT5000  MOVE      "15",CVERT
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,GDAT5000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          PACK      EDATED10,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",EDATED10
.
          MATCH     STRTDATE,ENDDATE
          GOTO      GDAT9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** End Date must be greater than or = Start Date **    ";
          CALL      HITENTER
          GOTO      GDAT5000
.
GDAT9000  MOVE      ONE,EXIT
.
GDAT9999  RETURN
+
.*****************************************************************************
.*                               GTIM0000               Called by: ML0000    *
.*                        Get the time range                                 *
.*****************************************************************************
GTIM0000  DISPLAY   *P30:14,"Start Time :":
                    *P30:15,"End   Time :";
.
GTIM1000  DISPLAY   *P43:14,*EL,*P43:15,*EL;
          MOVE      SP2,CHOUR                    * get start time
          MOVE      SP2,CMIN
          MOVE      "43",CCOL
          MOVE      TEN4,CVERT
          MOVE      ZERO,CHIGHLT
          CALL      KEYTIME
          BRANCH    CQUEST,GTIM1000
          BRANCH    OVRCD,GTIM5000
          PACK      TIMESTRT,CHOUR,COLON,CMIN,SP10
          MOVE      TIMESTRT,STIME
          GOTO      GTIM6000
.
GTIM5000  MOVE      SP8,TIMESTRT
          MOVE      "Start",STIME
          DISPLAY   *P43:14,*V2LON,"Start";
.
GTIM6000  DISPLAY   *P43:15,*EL;
          MOVE      SP2,CHOUR                    * get end time
          MOVE      SP2,CMIN
          MOVE      TEN5,CVERT
          MOVE      ZERO,CHIGHLT
          CALL      KEYTIME
          BRANCH    CQUEST,GTIM1000
          BRANCH    OVRCD,GTIM8000
          PACK      TIMEEND,CHOUR,COLON,CMIN,ZEDS    * to include seconds
          PACK      ETIME,CHOUR,COLON,CMIN,SP10
.
.         Check end time is not less than start time
.
          MATCH     TIMESTRT,TIMEEND
          GOTO      GTIM9999 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "End Time cannot be less than Start Time.    ";
          CALL      HITENTER
          GOTO      GTIM1000
.
GTIM8000  MOVE      ZEDS,TIMEEND
          MOVE      "Finish",ETIME
          DISPLAY   *P43:15,*V2LON,"Finish";
.
GTIM9999  RETURN
+
.*****************************************************************************
.*                               GBTY0000                                    *
.*                       Get the booking type range                          *
.* Returns:  EXIT  0 = valid codes entered                                   *
.*                 1 = exitchar entered                                      *
.*****************************************************************************
.
GBTY0000  DISPLAY   *P1:17,"Booking Type From : "
          MOVE      TWENTY1,ECOL
          MOVE      TEN7,EVERT
          MOVE      "BK",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
.
          CALL      PATCODKY
          BRANCH    EXIT,GBTY1000:               * nothing entered
                         GBTY9100                * exitchar entered
.
          MOVE      ACODE,BOOKSTRT
          DISPLAY   *P25:EVERT,TDESC;
          GOTO      GBTY2000
.
GBTY1000  MOVE      SP3,BOOKSTRT
          DISPLAY   *P21:EVERT,*V2LON,"Start";
.
GBTY2000  DISPLAY   *P50:17,*EL,"To : "
          MOVE      FIFTY5,ECOL
.
          CALL      PATCODKY
          BRANCH    EXIT,GBTY3000:               * nothing entered
                         GBTY9100                * exitchar entered
.
          MATCH     BOOKSTRT,ACODE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"To Type is less than From Type.  ";
            CALL      HITENTER
            GOTO      GBTY2000
          ENDIF
.
          MOVE      ACODE,BOOKEND
          DISPLAY   *P60:EVERT,TDESC;
          GOTO      GBTY9000
.
GBTY3000  MOVE      ZEDS,BOOKEND
          DISPLAY   *P55:EVERT,*V2LON,"Finish";
.
GBTY9000  MOVE      ZERO,EXIT
          GOTO      GBTY9999
.
GBTY9100  MOVE      ONE,EXIT
.
GBTY9999  RETURN
+
.*****************************************************************************
.*                               GSAD0000                                    *
.*                    Get the speciality/attending doctor                    *
.* Returns:  EXIT  0 = valid codes entered                                   *
.*                 1 = exitchar entered                                      *
.*****************************************************************************
.
GSAD0000  DISPLAY   *P1:18,"Doctor  Type From : "
          MOVE      TWENTY1,ECOL
          MOVE      TEN8,EVERT
          MOVE      "DT",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
.
          CALL      PATCODKY
          BRANCH    EXIT,GSAD1000:               * nothing entered
                         GSAD9100                * exitchar entered
.
          MOVE      ACODE,DOCTSTRT
          DISPLAY   *P25:EVERT,TDESC;
          GOTO      GSAD2000
.
GSAD1000  MOVE      SP3,DOCTSTRT
          DISPLAY   *P21:EVERT,*V2LON,"Start";
.
GSAD2000  DISPLAY   *P50:18,*EL,"To : "
          MOVE      FIFTY5,ECOL
.
          CALL      PATCODKY
          BRANCH    EXIT,GSAD3000:               * nothing entered
                         GSAD9100                * exitchar entered
.
          MATCH     DOCTSTRT,ACODE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"To Type is less than From Type.  ";
            CALL      HITENTER
            GOTO      GSAD2000
          ENDIF
.
          MOVE      ACODE,DOCTEND
          DISPLAY   *P60:EVERT,TDESC;
          GOTO      GSAD9000
.
GSAD3000  MOVE      ZEDS,DOCTEND
          DISPLAY   *P55:EVERT,*V2LON,"Finish";
.
GSAD9000  MOVE      ZERO,EXIT
          GOTO      GSAD9999
.
GSAD9100  MOVE      ONE,EXIT
.
GSAD9999  RETURN
+
.*****************************************************************************
.*                               GDIG0000              Called by: ML0000     *
.*                    see if diagnosis is to be printed                      *
.*****************************************************************************
GDIG0000  MOVE      ANSY,ANS
.
GDIG1000  KEYIN     *P1:20,*EL,"Do you wish to print diagnosis details (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*P48:20,*V2LON,*RV,ANS,*P48:20,*DV,ANS;
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
          DISPLAY   *P48:20,*V2LON,ANS;
          GOTO      GDIG3000
.
GDIG3000  MATCH     ANSY,ANS
          GOTO      GDIG9000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      GDIG9500 IF EQUAL
.
          BEEP
          GOTO      GDIG0000                     * invalid input
.
GDIG9000  MOVE      ZERO,DIAGFLG
          GOTO      GDIG9999
.
GDIG9500  MOVE      ONE,DIAGFLG
.
GDIG9999  RETURN
+
.*****************************************************************************
.*                               WTMP0000                                    *
.*                           Write to the tempfile                           *
.*****************************************************************************
WTMP0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Extracting Data .....";
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RSBKREC4
WTMP1000  CALL      RKBKREC4
          BRANCH    OVRCD,WTMP9999
.
          MATCH     BKREEDAT,ENDDATE        * test if in required range
          GOTO      WTMP9999 IF LESS
.
.         Filter Records Based on Hospital ID
.
          COMPARE   ONE,IBCNMHOS                 * using multi-hospital ?
          GOTO      WTMP1500 IF NOT EQUAL        * no - ignore check
.
          MATCH     SP10,PTHSHOSP                * all hospitals ?
          GOTO      WTMP1500 IF EQUAL            * yes - ignore check
.
          MOVE      BKREBOOK,KEY8
          CALL      RDBKRX11                     * booking ext. record found ?
          BRANCH    OVRCD,WTMP1000               * no - ignore record
.
WTMP1200  UNPACK    SP70,WWARD,WBED,WBDESC,PTWDHOSN
          MATCH     SP3,BKRXPOWD                 * post op ward blank ?
          GOTO      WTMP1300 IF EQUAL            * yes, get expected ward
.
          PACK      KEY6,BKRXPOWD,SP10
          CALL      RDWARD1                      * ward on file ?
          BRANCH    OVRCD,WTMP1300 
.
          GOTO      WTMP1400
.
WTMP1300  PACK      KEY6,BKREWARD,SP10           * expected ward from BOKRC1FD
          CALL      RDWARD1
          BRANCH    OVRCD,WTMP1000
.
WTMP1400  MATCH     PTWDHOSN,PTHSHOSP            * same hospital ?
          GOTO      WTMP1000 IF NOT EQUAL        * no - ignore record
.
.------   Test if in time range required ---------------
.
WTMP1500  IF        MENUOPTN <> 3
             MATCH     TIMESTRT,BKREATIM
             GOTO      WTMP1000 IF LESS
.
             MATCH     BKREATIM,TIMEEND
             GOTO      WTMP1000 IF LESS
          ENDIF
.
.------   Test if in the booking range required --------
.
          MATCH     BOOKSTRT,BKRETYPE
          GOTO      WTMP1000 IF LESS
.
          MATCH     BKRETYPE,BOOKEND
          GOTO      WTMP1000 IF LESS
.
.------  Get only booked or preadmitted patients ------
.
          COMPARE   TWO,BKRESTAT
          GOTO      WTMP1000 IF NOT LESS
.
.         A valid record for printing has been found
.
          CALL      CVAR0000                * clear variables
.
.         Get the booking extension record
.
          MOVE      BKREBOOK,KEY8
          CALL      RDBKRX11                * read extension record
          IF        OVRCD = 0
            MOVE      BKRXPOWD,TMPPOWRD
          ENDIF
.
          MOVE      BKREBOOK,TMPADMNO
          MOVE      BKREURNO,TMPURNO        * default booking details
          MOVE      BKREATIM,TMPTIME
          MOVE      BKRECLMT,TMPCLAIM
          MOVE      BKREADOC,TMPADOCT
          MOVE      BKRESDOC,TMPASDOC
          MOVE      SP3,TMPPACCM
          MOVE      BKREEDAT,TMPDATE
          MOVE      BKREWARD,TMPWARD
          MOVE      BKREEBED,TMPBED
.
          CALL      GDGC0000                * get diagnosis and comments
.
.         If pre-admitted, use details from admission record
.
          MOVE      BKREBOOK,DIM8VISN
          PACK      KEY8,DIM8VISN,SP70    
          CALL      RDMISS1              * get admission record
          IF        OVRCD = 0
             IF        ASTAT <> 1
               GOTO      WTMP1000
             ENDIF
             MOVE      ACARE,TMPPACCM
             MOVE      ACLAIM,TMPCLAIM
             MOVE      ADOCTA,TMPADOCT
             MOVE      ADOCTR,TMPASDOC
             MOVE      PTMIXWRD,TMPWARD
             MOVE      PTMIEBED,TMPBED
.
             MATCH     SP30,TMPDIAG1
             IF        @EQUAL
               MOVE      ADIAG1,TMPDIAG1
               MOVE      ADIAG2,TMPDIAG2
             ENDIF
.
             MATCH     SP30,TMPCOMM
             IF        @EQUAL
               MOVE      ACOMM1,TMPCOMM
             ENDIF
.
.            UNPACK    TMPDATE,CCENT,CYEAR,CMON,CDAY
.            PACK      TMPDATE,ADATE
          ENDIF
          MOVE      BKREBOOK,TMPADMNO
.
.------   Only when using Doctor Speciality Sequence (Option=4) --------
.------   Test if in the doctor range required                  --------
.
WTMP2000  IF        MENUOPTN=4
            MOVE      SP3,DRTYPE
            PACK      KEY6,TMPADOCT
            CALL      RDDOCT1               * read the doctors file
.
            MATCH     DOCTSTRT,DRTYPE
            GOTO      WTMP1000 IF LESS
.
            MATCH     DRTYPE,DOCTEND
            GOTO      WTMP1000 IF LESS
.
            MOVE      DRTYPE,TMPDOCTP
          ENDIF
.
          MATCH     SP8,BKREURNO          
          GOTO      WTMP3000 IF NOT EQUAL
.
          MOVE      BKREBOOK,DIM8VISN
          PACK      KEY8,DIM8VISN,SP70
          CALL      RDPRAM1
          BRANCH    OVRCD,WTMP1000
          GOTO      WTMP4000
.
WTMP3000  PACK      KEY8,BKREURNO,SP70
          CALL      RDMAST1                 * get master file details
          BRANCH    OVRCD,WTMP1000

WTMP4000  MOVE      PSNAME,TMPSNAME
          MOVE      PGNAME,TMPGNAME
          MOVE      PTITL,TMPTITLE
          MOVE      PSEX,TMPSEX
.
.------  Calculate the patients age ------
.
          MOVE      TMPDATE,AGEDATE
          REP       " 0",AGEDATE
          CALL      CALCAGE
          MOVE      PSAGEYRS,TMPAGE 
.
          MOVE      SP20,DSNAME
          PACK      KEY6,TMPADOCT
          CALL      RDDOCT1
.
          MOVE      DSNAME,TMPDOCSN
.
          PACK      KEY36,TMPDATE,TMPSNAME,TMPADMNO
          CALL      WRHMTMP1
          GOTO      WTMP1000

WTMP9999  RETURN
+
.*****************************************************************************
.*                             GDGC0000                                      *
.*                     Get diagnosis and comments                            *
.*****************************************************************************
GDGC0000  PACK      BKRETYPE,BKRETYPE,SP3
          MATCH     SP3,BKRETYPE
          GOTO      GDGC5000 IF EQUAL
.
          MOVE      "BK",CODE
          PACK      KEY5,CODE,BKRETYPE
          CALL      RDCODE1
          BRANCH    OVRCD,GDGC9999
.
          MATCH     "THE ",THCSCOD          * If theatre use oprdea file
          GOTO      GDGC5000 IF EQUAL
.
.------  Not theatre so use Diagnosis file   ------
.
          MOVE      "         1",DIM10                                *I-174698
          PACK      KEY18,BKREBOOK,DIM10,SP20                         *C-174698
          CALL      RDBKDIA1                     * diagnosis on file ?
          BRANCH    OVRCD,GDGC9999               * no - finished
.
...       MOVE      BKDIDES1,TMPDIAG1
...       MOVE      BKDIDES2,TMPDIAG2
.                                                                     *I-174698
          CLEAR     DIM80
          APPEND    BKDIDES1,DIM80
          ENDSET    DIM80
          APPEND    SP1,DIM80
          APPEND    BKDIDES2,DIM80
          RESET     DIM80
          UNPACK    DIM80,TMPDIAG1,TMPDIAG2
.
          MOVE      BKDICOMM,TMPCOMM
          GOTO      GDGC9999
.
.----- Get descriptions from the oprdea file ---------
.
GDGC5000  PACK      KEY31,BKREBOOK,ZERO,SP30,SP10
          CALL      RSOPDEA2
GDGC5500  CALL      RKOPDEA2
          BRANCH    OVRCD,GDGC9999
.
          MATCH     OPDAADMN,TMPADMNO
          GOTO      GDGC9999 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT
          GOTO      GDGC9999 IF EQUAL
.
          MOVE      OPDADES1,TMPDIAG1
          MOVE      OPDADES2,TMPDIAG2
          MOVE      OPDADES3,TMPDIAG3
          MOVE      OPDACOMM,TMPCOMM
.
GDGC9999  RETURN
+
.*****************************************************************************
.*                               GDAN0000                                    *
.*                      Get descriptions and names for report                *
.*****************************************************************************
GDAN0000  MOVE      SP20,CLTYDESC
          MOVE      SP20,CLUBDESC
          MOVE      SP20,PACCDESC
          MOVE      SP30,DOCNAME
          MOVE      SP30,DOCNAME2
.
.------  Get description for Claim Type ------
.
          PACK      TMPCLAIM,TMPCLAIM,SP3
          MATCH     SP3,TMPCLAIM            * Test if blank
          GOTO      GDAN2000 IF EQUAL
.
          MOVE      "CL",CODE
          PACK      KEY5,CODE,TMPCLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,GDAN2000
          MOVE      TDESC,CLTYDESC
.
.------  Get description for Preferred Accomodation ------
.
GDAN2000  PACK      TMPPACCM,TMPPACCM,SP3
          MATCH     SP3,TMPPACCM            * Test if blank
          GOTO      GDAN4000 IF EQUAL
.
          MOVE      "CC",CODE
          PACK      KEY5,CODE,TMPPACCM
          CALL      RDCODE1
          BRANCH    OVRCD,GDAN4000
          MOVE      TDESC,PACCDESC
.
.------  Format the Doctor's name --------
.
GDAN4000  PACK      KEY6,TMPADOCT
          CALL      RDDOCT1
          BRANCH    OVRCD,GDAN4500
.
          MOVE      DGNAME,ANS
          CLEAR     DOCNAME
          APPEND    DTITL,DOCNAME
          RESET     DOCNAME,3
          APPEND    SP1,DOCNAME
          APPEND    ANS,DOCNAME
          APPEND    DOT,DOCNAME
          APPEND    DSNAME,DOCNAME
          RESET     DOCNAME
          PACK      DOCNAME,DOCNAME,SP20
.
.------  Format the Associated Doctor's name --------
.
GDAN4500  PACK      KEY6,TMPASDOC 
          CALL      RDDOCT1
          BRANCH    OVRCD,GDAN5000
.
          MOVE      DGNAME,ANS
          CLEAR     DOCNAME2
          APPEND    DTITL,DOCNAME2
          RESET     DOCNAME2,3
          APPEND    SP1,DOCNAME2
          APPEND    ANS,DOCNAME2
          APPEND    DOT,DOCNAME2
          APPEND    DSNAME,DOCNAME2
          RESET     DOCNAME2
          PACK      DOCNAME2,DOCNAME2,SP20
.
GDAN5000  UNPACK    TMPDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DUEDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
.* ****************************************** Start of Changes         *I-49016
.                                           * original code for sex desc. has
.                                           * been deleted & replaced by :-
          MOVE      TMPSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,SEX
.* ******************************************   End of Changes         *I-49016
.
GDAN9999  RETURN
+
.*****************************************************************************
.*                               PRNT0000                                    *
.*                          Print the actual Report                          *
.*****************************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Generating List .....";
          MOVE      ZERO,CNOUNDLN
          MOVE      ZERO,CPAGENO
          MOVE      SP8,TMPDATE
          MOVE      ZERO,TOTAL
.
.-------  Get the starting date    -----------
.
          CALL      POSI0000
          CALL      RNXT0000
.
          IF        OVRCD=1
            MOVE      STRTDATE,TMPDATE
          ENDIF
.
.-------  format the report date   -----------
.
          UNPACK    TMPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      TMPDATE,REPDATE
.
          CALL      HEAD0000
          CALL      POSI0000
.
PRNT1000  CALL      RNXT0000
          BRANCH    OVRCD,PRNT9000
.
.------- Test if the date has changed -------
.
          MATCH     REPDATE,TMPDATE
          GOTO      PRNT2000 IF EQUAL
          MOVE      TMPDATE,REPDATE
          UNPACK    TMPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE 
          CALL      HEAD0000 
.
PRNT2000  CALL      GDAN0000                * get descriptions, names etc
          CALL      PREC0000                * print the record
          ADD       ONE,TOTAL               * increment number of patients
.
          COMPARE   "57",CLNO               * test if the page is full
          GOTO      PRNT1000 IF LESS
          CALL      HEAD0000
          GOTO      PRNT1000
.
PRNT9000  CALL      UND132
          PRINT     *N,*N,"Number of Patients = ",TOTAL:
                    *N,*N,*N,"***  End of List  ***"
          DISPLAY   *P1:24,*EL,*V2LON,*P24:24:
                    "*** List Generation Completed ***",*W2;
.
PRNT9999  RETURN
+
.****************************************************************************
.*                              HEAD0000                                    *
.*                       Print the report Heading                           *
.****************************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD132
          IF          IBCNMHOS = 1
            PRINT     *40,"Hospital : ",*60,PTHSNAME
          ENDIF
.
          PRINT     *40,"Start Date        : ",SDATE;
          IF        MENUOPTN <> 3
             PRINT     *75,"Start Time        : ",STIME;
          ENDIF
.
          PRINT     *N,*40,"End   Date        : ",EDATED10;
          IF        MENUOPTN = 3
             PRINT     *N;
          ELSE
             PRINT     *75,"End Time          : ",ETIME
          ENDIF
.
          PACK      BOOKSTRT,BOOKSTRT,SP3
          MATCH     SP3,BOOKSTRT
          GOTO      HEAD1000 IF EQUAL

          PRINT     *N,*40,"Booking Type from : ",BOOKSTRT,*65,BSDESC;
          GOTO      HEAD2000
.
HEAD1000  PRINT     *N,*40,"Booking Type from : Start";
.
HEAD2000  PACK      BOOKEND,BOOKEND,ZEDS
          MATCH     "zzz",BOOKEND
          GOTO      HEAD3000 IF EQUAL

          PRINT     "  to  ",BOOKEND,SP2,BEDESC
          GOTO      HEAD4000
.
HEAD3000  PRINT     "  to  Finish"
.
HEAD4000  COMPARE   "4",MENUOPTN
          GOTO      HEAD8000 IF NOT EQUAL
.
          PACK      DOCTSTRT,DOCTSTRT,SP3
          MATCH     SP3,DOCTSTRT
          GOTO      HEAD5000 IF EQUAL

          PRINT     *40,"Doctor Type from  : ",DOCTSTRT,*65,DSDESC;
          GOTO      HEAD6000
.
HEAD5000  PRINT     *40,"Doctor Type from  : Start";
.
HEAD6000  PACK      DOCTEND,DOCTEND,ZEDS
          MATCH     "zzz",DOCTEND
          GOTO      HEAD7000 IF EQUAL

          PRINT     "  to  ",DOCTEND,SP2,DEDESC
          GOTO      HEAD8000
.
HEAD7000  PRINT     "  to  Finish"
.
HEAD8000  UNPACK    REPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DATECONV
          LOAD      DAYOFWK,WEEKDAY,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          PRINT     *1,DAYOFWK,*13,CPCDATE
          CALL      UND132
          IF        MENUOPTN=4
            PRINT     *1,"|ADM",*7,"|EXPECTED",*16,"|POP",*20,"|PA",*23,"|CL.":
                      *27,"|SURNAME",*48,"|SEX",*52,"|U/R NO.":
                      *61,"| ATT. DOCTOR",*78,"|DOCTOR",*85,"|  DIAGNOSIS":
                      *116,"|    REMARKS",*132,"|",*N:
                      *1,"|TIME",*7,"|WARD/BED",*16,"|WRD",*20,"|",*23,"|TYP":
                      *27,"|   TITLE, GIVEN NAME",*48,"|","AGE",*52,"|BOOK NO.":
                      *61,"| ASS. DOCTOR",*78,"|",*80,"TYPE",*85,"|":
                      *116,"|" *132,"|"
            CALL      UND132
            MOVE      EIGHT,CLNO
          ELSE
            PRINT     *1,"|ADM",*7,"|EXPECTED",*16,"|POP",*20,"|PA",*23,"|CL.":
                      *27,"|SURNAME",*48,"|SEX",*52,"|U/R NO.":
                      *61,"| ATT. DOCTOR",*78,"|  DIAGNOSIS":
                      *116,"|    REMARKS",*132,"|",*N:
                      *1,"|TIME",*7,"|WARD/BED",*16,"|WRD",*20,"|",*23,"|TYP":
                      *27,"|   TITLE, GIVEN NAME",*48,"|","AGE",*52,"|BOOK NO.":
                      *61,"| ASS. DOCTOR",*78,"|",*116,"|" *132,"|"
            CALL      UND132
            MOVE      SEVEN,CLNO
          ENDIF
.
          IF        IBCNMHOS = 1
            ADD       ONE,CLNO
          ENDIF
          
HEAD9999  RETURN
+
.****************************************************************************
.*                              POSI0000                                    *
.*                       Position in the required index                     *
.****************************************************************************
POSI0000  BRANCH    MENUOPTN,POSI1000,POSI2000,POSI3000
.
.------ Print the report in Time/Doctor/Surname Sequence ------
.
POSI1000  PACK      KEY64,SP30,SP20,SP20
          CALL      RSHMTMP2
          GOTO      POSI9999
.
.------ Print the report in Time/Surname Sequence ------
.
POSI2000  PACK      KEY44,SP30,SP20
          CALL      RSHMTMP3
          GOTO      POSI9999
.
.------ Print the report in Surname Sequence ------
.
POSI3000  PACK      KEY36,SP30,SP20
          CALL      RSHMTMP1
.
POSI9999  RETURN
+
.****************************************************************************
.*                              RNXT0000                                    *
.*                   Read the next record via index                         *
.****************************************************************************
RNXT0000  BRANCH    MENUOPTN,RNXT1000,RNXT2000,RNXT3000
.
.------ Print the report in Time/Doctor/Surname Sequence ------
.
RNXT1000  CALL      RKHMTMP2
          GOTO      RNXT9999
.
.------ Print the report in Time/Surname Sequence ------
.
RNXT2000  CALL      RKHMTMP3
          GOTO      RNXT9999
.
.------ Print the report in Surname Sequence ------
.
RNXT3000  CALL      RKHMTMP1
          GOTO      RNXT9999

RNXT9999  RETURN
+
.****************************************************************************
.*                              PREC0000                                    *
.*                       Print the actual record                            *
.****************************************************************************
PREC0000  MOVE      TMPTIME,DIM5
          CLEAR     DIM16
          RESET     TMPTITLE
          PACK      DIM16,TMPTITLE,SP1,TMPGNAME
.
          PRINT     *1,"|",DIM5,*7,"|",TMPWARD;
          MATCH     SP3,TMPBED
          GOTO      PREC0100 IF EQUAL
          PRINT     *12,"/",TMPBED;
.
PREC0100  PRINT     *16,"|",TMPPOWRD,*20,"|",PACCDESC,*23,"|",CLTYDESC:
                    *27,"|",TMPSNAME,*48,"|",SP1,TMPSEX,*52,"|",TMPURNO:
                    *61,"|",DOCNAME,*78,"|";
.
          IF        MENUOPTN=4
            PRINT     *79,TMPDOCTP,*85,"|";
            BRANCH    DIAGFLG,PREC0200           * no diagnosis detail to print
            MOVE      SP30,DIM30
            MOVE      TMPDIAG1,DIM30
            PRINT     *86,DIM30;
          ELSE
            BRANCH    DIAGFLG,PREC0200           * no diagnosis detail to print
            PRINT     *79,TMPDIAG1;
          ENDIF
.
PREC0200  PRINT     *116,"|",*132,"|"
          ADD       ONE,CLNO
.
          PRINT     *1,"|",*7,"|",*16,"|",*20,"|",*23,"|",*27,"|",*32,DIM16:
                    *48,"|",TMPAGE,*52,"|",TMPADMNO,*61,"|",DOCNAME2,*78,"|";
.
          IF        MENUOPTN=4
            PRINT     *85,"|";
            BRANCH    DIAGFLG,PREC0300           * no diagnosis detail to print
            MOVE      SP30,DIM30
            MOVE      TMPDIAG2,DIM30
            PRINT     *86,DIM30;
          ELSE
            BRANCH    DIAGFLG,PREC0300           * no diagnosis detail to print
            PRINT     *79,TMPDIAG2;
          ENDIF
.
PREC0300  PRINT     *116,"|",*132,"|"
          ADD       ONE,CLNO
.
          BRANCH    DIAGFLG,PREC0500             * no diagnosis detail to print
.
          PACK      TMPDIAG3,TMPDIAG3,SP37
          MATCH     SP37,TMPDIAG3
          GOTO      PREC0500 IF EQUAL
.
          IF        MENUOPTN=4
            MOVE      SP30,DIM30
            MOVE      TMPDIAG3,DIM30
            PRINT     *1,"|",*7,"|",*16,"|",*20,"|",*23,"|",*27,"|",*48,"|":
                      *52,"|",*61,"|",*78,"|",*85,"|",DIM30,*116,"|",*132,"|"
          ELSE
            PRINT     *1,"|",*7,"|",*16,"|",*20,"|",*23,"|",*27,"|",*48,"|":
                      *52,"|",*61,"|",*78,"|",*79,TMPDIAG3,*116,"|",*132,"|"
          ENDIF
          ADD       ONE,CLNO
.  
PREC0500  BRANCH    PCOMFLAG,PREC1000       * are we printing comments
          GOTO      PREC9999
.
PREC1000  PACK      TMPCOMM,TMPCOMM,SP37
          MATCH     SP37,TMPCOMM
          GOTO      PREC9999 IF EQUAL
.
          IF        MENUOPTN=4
            MOVE      SP30,DIM30
            MOVE      TMPCOMM,DIM30
            PRINT     *1,"|",*7,"|",*16,"|",*20,"|",*23,"|",*27,"|",*48,"|":
                      *52,"|",*61,"|",*78,"|",*85,"|",DIM30,*116,"|",*132,"|"
          ELSE
            PRINT     *1,"|",*7,"|",*16,"|",*20,"|",*23,"|",*27,"|",*48,"|":
                      *52,"|",*61,"|",*78,"|",*79,TMPCOMM,*116,"|",*132,"|"
          ENDIF
          ADD       ONE,CLNO
.
PREC9999  RETURN
+
.*****************************************************************************
.*                               MAKE0000                                    *
.*                      Build the tempfile for the report                    *
.*****************************************************************************
MAKE0000  CALL      KILL0000                * erase the existing file
.
          DISPLAY   *P1:24,*EL,*V2LON:
                    "Initialising Temporary Index Files .....",*W1;
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    " 293 u1-8,37-56,57-64 u1-8,17-36,9-16,37-56,57-64",CMDLINE
          APPEND    " u1-8,9-16,37-56,57-64",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TMPHMSA1,FILENAME
          OPEN      TMPHMSA2,FILENAME
          OPEN      TMPHMSA3,FILENAME
.
MAKE9999  RETURN
+
.*****************************************************************************
.*                                KILL0000                                   *
.*                      Erase the tempfile for the report                    *
.*****************************************************************************
KILL0000  DISPLAY   *P1:24,*EL,*V2LON,"Deleting Temporary Index Files .....",*W1
          CLOSE     TMPHMSA1
          CLOSE     TMPHMSA2
          CLOSE     TMPHMSA3
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FILENAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*****************************************************************************
.
.         Clear the temp file variables
.
CVAR0000  MOVE      SP20,TMPDATE  
          MOVE      SP20,TMPTIME  
          MOVE      SP20,TMPADOCT 
          MOVE      SP20,TMPSNAME 
          MOVE      SP20,TMPADMNO 
          MOVE      SP20,TMPPACCM 
          MOVE      SP20,TMPCLAIM 
          MOVE      SP20,TMPGNAME 
          MOVE      SP20,TMPTITLE 
          MOVE      SP20,TMPSEX  
          MOVE      ZERO,TMPAGE   
          MOVE      SP20,TMPURNO  
          MOVE      SP3,TMPPOWRD 
          PACK      TMPDIAG1,SP30,SP20
          PACK      TMPDIAG2,SP30,SP20
          PACK      TMPDIAG3,SP30,SP20
          PACK      TMPCOMM,SP30,SP20
          MOVE      SP3,TMPWARD
          MOVE      SP3,TMPBED
          MOVE      SP3,TMPDOCTP
          MOVE      SP10,TMPDIAG
.
CVAR9999  RETURN
+
.*****************************************************************************
.*        I/Os for Temp File                                                 *
.*****************************************************************************
RSHMTMP1  RESET     KEY36
          READ      TMPHMSA1,KEY36;;
          RETURN
.
RPHMTMP1  MOVE      ZERO,OVRCD
          RESET     KEY36
          READKP    TMPHMSA1;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                             TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                             TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG,TMPDIAG1:
                             TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC,TMPWARD,TMPBED:
                             TMPDOCTP
          GOTO      OVERCOND IF OVER
          RETURN
.
RKHMTMP1  MOVE      ZERO,OVRCD
          RESET     KEY36
          READKS    TMPHMSA1;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                             TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                             TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG,TMPDIAG1:
                             TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC,TMPWARD,TMPBED:
                             TMPDOCTP
          GOTO      OVERCOND IF OVER
          RETURN
.
RDHMTMP1  MOVE      ZERO,OVRCD
          RESET     KEY36
          READ      TMPHMSA1,KEY36;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                                   TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                                   TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG:
                                   TMPDIAG1,TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC:
                                   TMPWARD,TMPBED,TMPDOCTP
          GOTO      OVERCOND IF OVER
          RETURN
.
RAHMTMP1  MOVE      ZERO,OVRCD
          RESET     KEY36
          READ      TMPHMSA1,KEY36;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRHMTMP1  RESET     KEY36
          WRITE     TMPHMSA1,KEY36;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                                   TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                                   TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG:
                                   TMPDIAG1,TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC:
                                   TMPWARD,TMPBED,TMPDOCTP
          RETURN
.
DEHMTMP1  RESET     KEY36  
          DELETE    TMPHMSA1,KEY36
          RETURN
.
RSHMTMP2  RESET     KEY64
          READ      TMPHMSA2,KEY64;;
          RETURN
.
RPHMTMP2  MOVE      ZERO,OVRCD
          RESET     KEY64
          READKP    TMPHMSA2;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                             TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                             TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG,TMPDIAG1:
                             TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC,TMPWARD,TMPBED:
                             TMPDOCTP
          GOTO      OVERCOND IF OVER
          RETURN
.
RKHMTMP2  MOVE      ZERO,OVRCD
          RESET     KEY64
          READKS    TMPHMSA2;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                             TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                             TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG,TMPDIAG1:
                             TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC,TMPWARD,TMPBED:
                             TMPDOCTP
          GOTO      OVERCOND IF OVER
          RETURN
.
RDHMTMP2  MOVE      ZERO,OVRCD
          RESET     KEY64
          READ      TMPHMSA2,KEY64;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                                   TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                                   TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG:
                                   TMPDIAG1,TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC:
                                   TMPWARD,TMPBED,TMPDOCTP
          GOTO      OVERCOND IF OVER
          RETURN
.
RAHMTMP2  MOVE      ZERO,OVRCD
          RESET     KEY64
          READ      TMPHMSA2,KEY64;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSHMTMP3  RESET     KEY44
          READ      TMPHMSA3,KEY44;;
          RETURN
.
RPHMTMP3  MOVE      ZERO,OVRCD
          RESET     KEY44
          READKP    TMPHMSA3;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                             TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                             TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG,TMPDIAG1:
                             TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC,TMPWARD,TMPBED:
                             TMPDOCTP
          GOTO      OVERCOND IF OVER
          RETURN
.
RKHMTMP3  MOVE      ZERO,OVRCD
          RESET     KEY44
          READKS    TMPHMSA3;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                             TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                             TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG,TMPDIAG1:
                             TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC,TMPWARD,TMPBED:
                             TMPDOCTP
          GOTO      OVERCOND IF OVER
          RETURN
.
RDHMTMP3  MOVE      ZERO,OVRCD
          RESET     KEY44
          READ      TMPHMSA3,KEY44;TMPDATE,TMPTIME,TMPDOCSN,TMPSNAME,TMPADMNO:
                                   TMPADOCT,TMPPACCM,TMPCLAIM,TMPGNAME,TMPTITLE:
                                   TMPSEX,TMPAGE,TMPURNO,TMPPOWRD,TMPDIAG:
                                   TMPDIAG1,TMPDIAG2,TMPDIAG3,TMPCOMM,TMPASDOC:
                                   TMPWARD,TMPBED,TMPDOCTP
          GOTO      OVERCOND IF OVER
          RETURN
.
RAHMTMP3  MOVE      ZERO,OVRCD
          RESET     KEY44
          READ      TMPHMSA3,KEY44;ANS
          GOTO      OVERCOND IF OVER
          RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCAGE
          INC       DATECONV
          INC       SEXDSCIO
.
          INC       PATCODKY
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       BOKDIAIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       OPRDEAIO/INC
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATPR1IO/INC
          INC       PATWR1IO/INC
          INC       PATHSPIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
+

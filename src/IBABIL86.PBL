. *****************************************************************************
. *                                                                           *
. *                       P R O G R A M   S U M M A R Y                       *
. *                       -------------   -------------                       *
. *                                                                           *
. *     PROGRAM NAME:    IBABIL86                                             *
. *                                                                           *
. *     FUNCTION:        PATIENT DAY BOOK                                     *
. *                                                                           *
. *     DATE WRITTEN:    11/12/86                                             *
. *                                                                           *
. *     AUTHOR:          MALCOLM HAYSE                                        *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *****************************************************************************
. * V12.00.01 21/05/2025 Thanh T         TSK 0955096                          *
. *           Changed for alphanumeric visit number                           *
. *****************************************************************************
. *       V11.05.01 17.12.2024  DA Sarkies       TSK 0951086                  *
. *                 Increased size of TOTDAY                                  *
. *       V10.13.01 05/12/2018  Don Nguyen       TSK 0838558                  *
. *                 Deleted temp file (PSTROL) after processing               *
. *       V10.05.01 02/02/2015  Ania P        CAR 261630                      *
. *                 Removed use of PORT for temp file naming                  *
. *       V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
. *                 Added pmsvx1af                                            *
. *       V9.02.01  30/08/2002  Jill Habasque  SRF 17573                      *
. *                 Changed to use RTIODAYS instead of NHOSPDAY               *
. *       V5.08.02  25.10.2000 J.Tan                                          *
. *                 Recompiled for WEB Report Scheduler                       *
. *       V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *                   V5.07.03  13/08/1999  Steve Downing                     *
. *                             Redundant code removed from date calculations *
. *                   V5.07.02  26/07/1999  Steve Downing                     *
. *                             Century included in To-date calculations      *
. *                             Y/N option accepts upper & lower case         *
. *                   V5.07.01  15/07/1999  Andrew Peel  SRF 633107           *
. *                             Fixed I * L on temporary indexed file         *
. *                   V5.07.B02 25/01/1999  Nicole Harrington 507 rebound     *
. *                             Mods to display and print centuries           *
. *                   V5.07.B01 13/11/1998  Jim Stathopoulos                  *
. *                             507 Changes                                   *
. *****************************************************************************
. *                      W2.01  30/12/86 M.HAYSE                              *
. *                             Run faster using ADMN4                        *
. *                      W2.02  20/05/87 S.Barcham                            *
. *                             Fix number of days                            *
. *                      W2.03  22/09/87 J.Tan                                *
. *                             Alter to patient day book                     *
. *                      W2.04  03/11/87 S.Barcham                            *
. *                             Use NHOSPDAY                                  *
. *                                                                           *
. *                      V5.00  01/05/89 S.Barcham                            *
. *                             Fix 3621 I * L                                *
. *                      V5.01  16/05/89 DIG                                  *
. *                             Changed to standard and modified print for    *
. *                             8 digit U/R's etc                             *
. *                      V5.02  27/05/89 J.Tan            SRF 100750          *
. *                             Fixed Mother Adm. to Form 8 (PATMI1FD).       *
. *                      V5.03  06/06/89 J.Tan            SRF 100809          *
. *                             Mods Locking master file (PATIOMAS).          *
. *                                                                           *
. *                   V5.01.01  06/07/89 M.Ohleiter                           *
. *                             Changes for 8 char UR and Adm No. - Rel. 5.01 *
. *                   V5.01.02  12/02/90 J.Tan            SRF 103449          *
. *                             Close file before iserase                     *
. *                   V5.01.121 26/10/90 i chung          SRF 106789          *
. *                             Added total no. of patients at end of report  *
. *                   V5.01.122 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *                                                                           *
. *                   V5.01.123 03.03.93 Neeriem "I Hate Support" Dye         *
. *                             Modify to print full address                  *
. *                   V5.01.124 04.03.93 Neeriem "I Hate Support" Dye         *
. *                             Use Keydate                                   *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       PATDO1FD/INC
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
.
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         CONSTANTS AND WORK VARIABLES
.
ADMDATE   DIM       10
ADIAG28   DIM       28
.
CALDAY    FORM      3
CALDAYS   FORM      4
CHG       FORM      1
CMDLINE   DIM       50
CUDATE    DIM       8        
.
DALERG    DIM       3
DANS      DIM       3
DAYCASE   DIM       3
DHOSP     DIM       3
DILLN     DIM       3
DIM2A     DIM       2
DIM3      DIM       3
DIM4      DIM       4
DISDATE   DIM       10
DNAME     DIM       12
.
FNAMER    DIM       8
FNAMET    DIM       8
FROMDATE  DIM       8                       * FROM DATE IN (CCYYMMDD) FORMAT
.
JDAYS     FORM      3
JYEAR     FORM      2
.
.
LNO       FORM      2
LYEAR     FORM      "366"
.
NBRDAYS   FORM      4
RTDAYS    FORM      4
NEWUR     DIM       8
.
OLDUR     DIM       8
OPCND     FORM      1
OYEAR     FORM      "365"
.
PADD120   DIM       20 
PADD220   DIM       20
PADMNO    DIM       8
PAGENO    FORM      3
PATCOUNT  FORM      7                     * added for V5.01.121 - patients count
PNAME     DIM       30
PNAME20   DIM       20
PPENNO29  DIM       29
PSTROL    FILE      ASCII, FIXED=256
.
SAVDATE   DIM       8
SAVTYPE   DIM       1
SKEY30    DIM       30
SEC       FORM      2
STATUS    FORM      1
.
TODATE    DIM       8                       * TO DATE IN (CCYYMMDD) FORMAT
TOTDAY    FORM      4
.
UNIQUE    FORM      8
VERT      FORM      2
.
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
.
XDAY1     DIM       2
XDAY2     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
.
. ----- Tempfile consists of following vars -----
.
TEMPFILE  IFILE     SQL, FIXED=324, KEY="U11-30,31-38"
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.ADMDATE        DIM       10         10             1     
.PSNAME         DIM       20         20            11
DUNIQUE         DIM        8          8            31
.OLDUR          DIM        8          8            39
.NEWUR          DIM        8          8            47
.PSAGE          FORM       3          3            55
.PSEX           DIM        1          1            58
.TWARD          DIM        3          3            59
.TBED           DIM        3          3            62
.DISDATE        DIM        10         10           65
.DIM3           DIM        3          3            75
.DNAME          DIM       12         12            78
.ADIAG29        DIM       29         29            90
.PNAME          DIM       22         22           119
.PADD120        DIM       20         20           141
.PPENNO29       DIM       29         29           161
.PADD220        DIM       20         20           190
.PSUBRB         DIM       15         15           210
.PPOST          DIM        4          4           225
.
.End of Record                                    324
. ---------------------------------------------------
.
PRGID     INIT      "IBABIL86"
PRGNAM    INIT      "Patient Day Book"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening PATVISA2";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
OPFIL     DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          PACK      CUDATE WITH CCC,CYY,CMM,CDD
          REP       " 0" IN CUDATE
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
.         MAIN MENU
.
START     MOVE      ZERO,PAGENO
          MOVE      ZERO,PATCOUNT              * initialize patients count
.
          HLINE     *G37,2,48,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Date Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Patient Surname Sequence"
.
START1    KEYIN     *P1:8,"Select Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF KDATE,KSURN
          BEEP
          GOTO      START1
.
KDATE     DISPLAY   *P48:2,*V2LON,"- Date Sequence "
          GOTO      KADATE
.
KSURN     DISPLAY   *P48:2,*V2LON,"- Surname Sequence "
          GOTO      KADATE
.
NOADMN    DISPLAY   *P1:24,*EL,*P27:24,*B,*V2LON:
                    "** No Admissions on file **",*W3;
          GOTO      START
.
KADATE    DISPLAY   *P1:4,*EF,"Keyin From date : ":
                        *P1:6,"Keyin To   date : "
.
.         KEYIN TO DATE
.
KADAT1    
.
....
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
.
          MOVE      "20",CCOL          * set up collumn
          MOVE      " 4",CVERT         * set up row
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
          COMPARE   ZERO TO IDAY
          GOTO      START IF EQUAL
.
....
.
ENDAD1    PACK      FROMDATE WITH ICENT,IYEAR,IMON,IDAY
          REP       " 0",FROMDATE
.
          MATCH     FROMDATE TO CUDATE
          GOTO      INVDTE1 IF LESS
.
          UNPACK    FROMDATE INTO XCENT1,XYEAR1,XMON1,XDAY1
.
.         DISPLAY TO DATE 
.
KADAT2    
.
....
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
.
          MOVE      "20",CCOL          * set up collumn
          MOVE      " 6",CVERT         * set up row
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
          COMPARE   ZERO TO IDAY
          GOTO      KADAT1 IF EQUAL
.
....
.
ENDAD2    PACK      TODATE WITH ICENT,IYEAR,IMON,IDAY
          REP       " 0",TODATE
.
          MATCH     TODATE TO CUDATE
          GOTO      INVDTE2 IF LESS
          MATCH     FROMDATE TO TODATE
          GOTO      INVDATG IF LESS
.
          UNPACK    TODATE INTO XCENT2,XYEAR2,XMON2,XDAY2
.
.         Add one to the TODATE. This is because initially the program did not
.         include the end date, so it looks at all the data upto but including
.         the TODATE.
.
          MOVE      IDAY,DD
          MOVE      IMON,MM
          MOVE      IYEAR,YY
          MOVE      ICENT,CC
          CALL      DMYCON
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          PACK      TODATE WITH CC,YY,MM,DD
          REP       " 0",TODATE
          GOTO      REKEY
.
INVDATG   DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be less than or":
                    " equal to To Date **",*W3,*C,*EL;
          GOTO      KADAT1
.
INVDTE1   DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be less than or":
                    " equal to today's date **",*W3,*C,*EL;
          GOTO      KADAT1
.
INVDTE2   DISPLAY   *P1:24,*B,*EL,*V2LON,"** To Date must be less than or":
                    " equal to today's date **",*W3,*C,*EL;
          GOTO      KADAT2
.
.          CHECK IF DATES ARE OK OR NOT
.
REKEY     KEYIN     *P1:24,*EF," Dates ok (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    "N" TO ANS
          GOTO      KADAT1 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      CNTCHK IF EQUAL
          BEEP
          GOTO      REKEY
.
CNTCHK    BRANCH    OPTION OF OPTION1,OPTION2
.
.         PRINT VIA ADMNS NUMBER (DATE SEQUENCE)
.
OPTION2   CALL      KILLIDX
          CALL      CREAIDX
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE TO UNIQUE
.
OPTION1   DISPLAY   *P1:24,*EL,*P15:24,"Date :",*P40:24,"Scanning Admission :";
.
          MOVE      "60",LNO
          PACK      KEY16 WITH FROMDATE,SP8
          CALL      RDSMISS3
.
NEXTA     CALL      RDKMISS3
          BRANCH    OVRCD OF SORTD
.
	  DISPLAY   *P61:24,*V2LON,AADMNO;
.
.         PATIENT MUST BE ADMITTED, DISCHARGED OR ON LEAVE
.
          COMPARE   ONE,ASTAT
          GOTO      NEXTA IF EQUAL
.
          COMPARE   FIVE,ASTAT
          GOTO      NEXTA IF EQUAL
.
          UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      WDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0" IN WDATE
.
          MATCH     TODATE TO WDATE
          GOTO      SORTD IF NOT LESS  
.
A1OK      MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NEXTA
.
          PACK      ADMDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          MOVE      SP30,DNAME
.
          MATCH     SP6,ADOCTA
          GOTO      A1OK1 IF EQUAL
.
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF A1OK1
.
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          RESET     DNAME
          REP       ", ",DNAME
          RESET     DNAME
.
A1OK1     DISPLAY   *P22:24,*V2LON,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
          MOVE      SP30,ADIAG28
          MOVE      ADIAG1,ADIAG28
.
TSTDDTE   MOVE      SP10 TO DISDATE
          COMPARE   THREE TO ASTAT
          GOTO      A11OK IF NOT EQUAL
.
          MOVE      AADMNO TO KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD TO A11OK
.
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      DISDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          MOVE      DDIAG TO ADIAG28
.
A11OK     COMPARE   TWO TO ASTAT
          GOTO      DAYS IF EQUAL
.
          CALL      RTIODAYS
          MOVE      NBRDAYS TO TOTDAY
.
DAYS      MOVE      PTITL,DIM4
          PACK      PNAME WITH DIM4,SP1,PGNAME
.
          MOVE      AWARD,TWARD
          MOVE      ABED,TBED
          MOVE      AURNO,PURNO
          MOVE      AADMNO,PADMNO
          MOVE      SP6,OLDUR
          MOVE      AURNO,NEWUR
.
          MOVE      AURNO,PVIURNO
          PACK      KEY24 WITH PVIURNO,SP20
          CALL      RDSVISA2
NXTVIS    CALL      RDKVISA2
          BRANCH    OVRCD OF ENDUR
.
          MATCH     AURNO,PVIURNO
          GOTO      ENDUR IF NOT EQUAL
.
          COMPARE   THREE,PVITYPE
          GOTO      NXTVIS IF NOT EQUAL
.
          MOVE      PVIBILL,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF ENDUR
.
.         IF FIRST ADMISSION IS CURRENT ONE THEN IS IS A NEW AT THAT TIME
.
          MATCH     AADMNO,PADMNO
          GOTO      ENDUR IF EQUAL
.
          CALL      RDKMISS1
          BRANCH    OVRCD OF ENDUR
.
.         IF WE HAVE A SECOND ADMISSION AND IT WASNT THE FIRST IS MUST BE OLD
.
          MOVE      PURNO,OLDUR
          MOVE      SP8,NEWUR
.
ENDUR     MOVE      TOTDAY,DIM3
          MOVE      PADD1,PADD120
          MOVE      PADD2,PADD220
          MOVE      SP30,PPENNO29
          MATCH     SP6,PPENNO
          GOTO      CHKDAYS IF EQUAL
.
          CLEAR     PPENNO29
          APPEND    "PENSION ## : ",PPENNO29
          APPEND    PPENNO,PPENNO29
          RESET     PPENNO29
.
CHKDAYS   MATCH     SP10,DISDATE
          GOTO      NOCURT IF NOT EQUAL
          MOVE      SP8,DIM3
.
NOCURT    BRANCH    OPTION OF SRTDATE,SRTNAME
.
.         DATA IS ALREADY SORTED ... NO NEED TO SORT
.
SRTDATE   CALL      DISPDA
          GOTO      NEXTA
.
SRTNAME   MOVE      UNIQUE,DUNIQUE
          PACK      KEY28 WITH PSNAME,DUNIQUE
          WRITE     TEMPFILE,KEY28;ADMDATE,PSNAME,DUNIQUE,OLDUR,NEWUR:
                                   PSAGE,PSEX,TWARD,TBED,DISDATE,DIM3,DNAME:
                                   ADIAG28,PNAME,PADD1,PPENNO29,PADD2:
                                   PSUBRB,PPOST,PADD4
          ADD       ONE,UNIQUE
          GOTO      NEXTA
.
NOOPEN    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*P16:24,"** Temporary Statistics file ":
                    *V2LON,FNAMET,*HOFF," not found **",*W2;
          RETURN
+
.
SORTD     BRANCH    OPTION OF ENDP   **** DATE SEQUENCE IS PRINTED ALREADY
.
          DISPLAY   *P1:24,*EL,*P29:24,"** Generating Report **",*W2;
.
          CLOSE     TEMPFILE
.
          TRAP      NOOPEN IF IO
          OPEN      TEMPFILE,FNAMET
          TRAPCLR   IO
.
          DISPLAY   *P1:24,*EL,*P23:24,"Patient Name :"
          MOVE      SP30 TO KEY28
          READ      TEMPFILE,KEY28;;
.
READLOP   READKS    TEMPFILE;ADMDATE,PSNAME,DUNIQUE,OLDUR,NEWUR,PSAGE,PSEX: 
                             TWARD,TBED,DISDATE,DIM3,DNAME:
                             ADIAG28,PNAME,PADD1,PPENNO29,PADD2:
                             PSUBRB,PPOST,PADD4
          GOTO      ENDP IF OVER
          MOVE      DUNIQUE,UNIQUE
.
          DISPLAY   *P39:24,*EL,*V2LON,PSNAME
.
          CALL      DISPDA
          GOTO      READLOP
.
.         OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,PAGENO
          GOTO      NOADMN IF EQUAL
.
.         CALL      PAGEND
.
          PRINT     *N,"  Total Number of Patients : ",PATCOUNT:
                    *N,*N,*N,"  ***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
          BRANCH    OPTION OF START
          CALL      KILLIDX
          GOTO      START
+
.         PRINT ACTUAL DATA 
.
DISPDA    COMPARE   "55",LNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"|",OLDUR,*10,"|",NEWUR,*19,"|",PSNAME:
                    *64,"|",PSAGE:
                    *68,"| ",TWARD,SLASH,TBED:
                    *77,"| ",DNAME:
                    *91,"|",DISDATE,*102,"|",ADIAG28,*132,"|":
                    *N,"|",*10,"|",*19,"|",PNAME:
                    *64,"| ",PSEX:
                    *68,"| ",DIM3:
                    *77,"| ",ADMDATE:
                    *91,"| ",*102,"|",*132,"|":
                    *N,"|",*10,"|",*19,"|",PADD1:
                    *64,"|",*68,"|",*77,"|":
                    *91,"| ",PPENNO29,*102,"|",*132,"|":
                    *N,"|",*10,"|",*19,"|",PADD2:
                    *64,"|",*68,"|",*77,"|":
                    *91,"| ",*102,"|",*132,"|":
                    *N,"|",*10,"|",*19,"|",PSUBRB:
                    *64,"|",*68,"|",*77,"|":
                    *91,"| ",*102,"|",*132,"|":
                    *N,"|",*10,"|",*19,"|",PADD4," ",PPOST:
                    *64,"|",*68,"|",*77,"|":
                    *91,"| ",*102,"|",*132,"|"
          ADD       "6",CLNO
.
          ADD       FIVE,LNO
          ADD       ONE,PATCOUNT               * increment patients count
.
.         CALL      PAGEND
.         RETURN
+
.
.         LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,LNO
          RETURN
+
.
.         HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,PAGENO
.
.         COMPARE   ONE,PAGENO
.         CALL      PAGEND IF NOT EQUAL
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
.
          BRANCH    OPTION OF PRNTA,PRNTC
.
PRNTA     MOVE     "(Date Sequence)",CPHDROPT
          GOTO      PRTCONT
.
PRNTC     MOVE      "(Surname Sequence)",CPHDROPT
.
PRTCONT   CALL      HEAD132
          PRINT     *40,"From Date : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    *65,"To Date : ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2,*N
.
          CALL      PAGEND
.
          PRINT     *1,"|  Old",*10,"|  New":
                    *19,"|Patients Name",*64,"|Age":
                    *68,"|Ward/Bed",*77,"| Att. Doctor":
                    *91,"| Discharge":
                    *102,"| Diagnosis / Pension Number",*132,"|":
                    *N,"| U/R No",*10,"| U/R No":
                    *19,"|",*64,"|Sex":
                    *68,"|Days Hos",*77,"| Admiss Date":
                    *91,"| Date",*102,"|",*132,"|"
           ADD      TWO,CLNO
.
          CALL      PAGEND
.
          MOVE      "8",LNO
          RETURN
+
.       Deleting AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
CREAIDX   PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 324 U11-30,31-38"
          WEOF      PSTROL,SEQ
.
          MOVE      ONE,STATUS
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          BRANCH    STATUS TO INDOK
.
          MOVE      ONE,OPCND
          DISPLAY   *P20:24,*B,*V2LON:
                    "** The Index File not created **",*W3;
.
INDOK     CLOSE     PSTROL,DELETE
          RETURN
+
.
ENDIT     CHAIN     PGM
          STOP
.
.==============================================================================
.
          INC       STD001IO
.
          INCLUDE   RTIODAYS
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

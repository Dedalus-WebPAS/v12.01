.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*      PROGRAM NAME:     IBAPAT58                                            *
.*                                                                            *
.*      FUNCTION:         INSURANCE CLAIMS LISTING                            *
.*                                                                            *
.*      DATE WRITTEN:     02/01/89                                            *
.*                                                                            *
.*      AUTHOR:           KARIN PEAK   (I.B.A)                                *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.*       V12.00.01 30/05/2025 Jacob Jackson  TSK 0955096                      *
.*                 Alphanumeric changes                                       *
.*       V10.10.01 11/04/2017 Ania P         CAR 260631                       *
.*                 Removed use of PORT                                        *
.*       V10.01.01 03/02/2011 Jill Parkinson CAR 233088                       *
.*                 Added pmsvx1af                                             *
.*       V9.03.01  14/08/2003  Lina Vo      CAR 41196                         *
.*                 Recompiled for big changes in PATSELFN.                    *
.*       V9.02.01  27/03/2003  Dean Cramer  CAR 36519                         *
.*                 Moved Date prompt                                          *
.*                 Fix where print fields overlap for web, also fixed where   *
.*                 admission number truncated in character for vets affairs   *
.*       V5.08.01  28/08/2000  Caleb Sharman                                  *
.*                 Changed Health Fund variables to be 8 chars                *
.*       V5.07.01  18/10/1999  Jill Habasque SRF 635201                       *
.*                 Fixed date fields, fixed index on WC tempfile              *
.*       V5.07.B02 01/02/1999  Jim Stathopoulos                               *
.*                 Fixed index problem                                        *
.*                 09/11/1998  Glenn Berry      5.07 changes                  *
.******************************************************************************
.*       V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                   *
.*                 Changed temp file name "pstidx" to standard format         *
.******************************************************************************
.*       V5.01.02  20/11/89 J.Tan                                             * 
.*                 Added other financial to PATSELFN.  SRF 102252             *
.*       V5.01.XX  11/07/90 Paul Duncan                                       *
.*                 recomp for OUTBOA & OUTBOB                                 *
.*       V5.01.121 27/11/90 Glen Hobby                                        *
.*                 Recompiled for changes to PATMX1FD                         *
.*       V5.01.122 21/05/91 Steve O'Gorman                                    *
.*                 Fixed Errors from New Comp.                                *
.*       V5.03.B1  06/06/95  Paul Howells  SRF 136735                         *
.*                 Recompiled for PATSELFN                                    *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASECFD/INC
          INC       IBAPASFD/INC
          INC       IBACONFD/INC
          INC       PATWC1FD/INC
          INC       PATWMAFD/INC
          INC       PATWVEFD/INC
          INC       PATPR1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATMA1FD/INC
          INC       PATIN1FD/INC
          INC       PATINVFD/INC
          INC       PATCODFD/INC
          INC       PATSELDT/INC
          INC       PATVISFD/INC
          INC       OUTSITFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.         Temporary index file for report
.
PATTMP1        IFILE      SQL, FIXED=383, KEY="U1-8,375-382"
PATTMP1A       IFILE      SQL, FIXED=383, KEY="U177-182,375-382"
.
.               Workers compensation
.
.  NAME         TYPE     LENGTH    PHYSICAL   START LOC.    DESCRIPTION
.  ----         ----     ------    --------   ----------    -----------
.DWCADMNO       DIM         8          8           1        Admission Number
.WCENAME        DIM        30         30           9        Employee name
.WCEADD1        DIM        35         35          39        Empl.Address line1
.WCEADD2        DIM        35         35          74        Empl.Address line2
.WCEADD3        DIM        35         35         109        Empl.Address line3
.WCEPOST        DIM         8          8         144        Postcode
.WCETELE        DIM        20         20         152        Telephone
.WCACDAT        DIM         8          8         172        Date of accident
.WCICODE        DIM         6          6         180        Ins. COMPANY CODE
.WCCLMNO        DIM        20         20         186        Ins. CLAIM NUMBER 
.AFUNDH         DIM         6          6         206        Health fund
.PSNAME         DIM        20         20         212        Patient surname
.PGNAME         DIM        25         25         232        Patient given name
.PADD1          DIM        35         35         257        Pat.Address line1
.PADD2          DIM        35         35         292        Pat.Address line2
.PSUBRB         DIM        35         35         327        Suburb
.PPOST          DIM         8          8         362        Postcode
TOTOUTS         FORM        6.2        5         370        Total outstanding
DUNIQUE         DIM         8          8         375        Unique key
.     END OF RECORD                              383
.
.          Motor Accident board
.
PATTMP2        IFILE      SQL, FIXED=316, KEY="U1-8,308-315"
PATTMP2A       IFILE      SQL, FIXED=316, KEY="U139-144,308-315"
.
.DMADMNO    DIM       8          8           1     ADMISSION NUMBER
.MREFNO     DIM      20         20           9     M.A.B. REFERENCE NUMBER
.MPOLIC     DIM      20         20          29     POLICE STAT. ACCT REPORTED TO
.MACDAT     DIM       8          8          49     DATE OF ACCIDENT (YYMMDD)
.MSNAME     DIM      30         30          57     SOLICITOR NAME
.MSTELE     DIM      12         12          87     SOLICITOR TELEPHONE
.MACLOC     DIM      40         40          99     ACCIDENT LOCATION
.AFUNDH     DIM       6          6         139     HEALTH FUND
.PSNAME     DIM      20         20         145     Patient surname
.PGNAME     DIM      25         25         165     Patient given name
.PADD1      DIM      35         35         190     Pat.Address line1
.PADD2      DIM      35         35         225     Pat.Address line2
.PSUBRB     DIM      35         35         260     Suburb
.PPOST      DIM       8          8         295     Postcode
.TOTOUTS    FORM      6.2        5         303     Total outstanding
.DUNIQUE    DIM       8          8         308     Unique key
.     END OF RECORD                        316
.
.          Veteran affairs
.
PATTMP3        IFILE      SQL, FIXED=196, KEY="U1-8,188-195"
PATTMP3A       IFILE      SQL, FIXED=196, KEY="U29-34,188-195"
.
.DVADMNO    DIM       8          8           1     ADMISSION NUMBER
.VCLMNO     DIM      20         20           9     CLAIM NUMBER
.AFUNDH     DIM       6          6          29     HEALTH FUND
.PSNAME     DIM      20         20          35     Patient surname
.PGNAME     DIM      25         25          55     Patient given name
.PADD1      DIM      35         35          80     Pat.Address line1
.PADD2      DIM      35         35         115     Pat.Address line2
.PSUBRB     DIM      35         35         150     Suburb
.PPOST      DIM       8          8         175     Postcode
.TOTOUTS    FORM      6.2        5         183     Total outstanding
.DUNIQUE    DIM       8          8         188     Unique key
.     END OF RECORD                        196
.
.     Redefined form fields for key
.
UNIQUE      FORM      8
.
.               WORK AREA
.
OPTION1         FORM    1
.
DIM7            DIM     7 
DIM16           DIM     16
DIM20           DIM     20
DIM20A          DIM     20
DIM25           DIM     25
DIM25A          DIM     25
DIM30           DIM     30
DIM30A          DIM     30
.
.
PSTROL             FILE      ASCII, FIXED=256
.
NAME            DIM     22
FNAMEA          DIM      8
FNAMEB          DIM      8
FNAMEC          DIM      8
FNAMET          DIM      8
.
OPCND           FORM     1
CMDLINE         DIM      50
SAVFUND         DIM      6
.
.      EXTRA VARIABLES
.
ADMNO            DIM     8
CODE             DIM     2
CLSFLG           FORM    1
COTHSFN          FORM    1
.
EADD3           DIM     21
ICNAME          DIM     20
TOTAL           FORM    "      .00"
.
FIVE9           FORM    "59"
FROMDATE        DIM     8
TODATE          DIM     8 
.
DATEF           DIM     8
FDATE           DIM     8 
TDATE           DIM     8 
.
XDAY1           DIM     2
XMON1           DIM     2
XYEAR1          DIM     2
XCENT1          DIM     2
.
XDAY2           DIM     2
XMON2           DIM     2
XYEAR2          DIM     2
XCENT2          DIM     2
.
CDAY1           DIM     2
CMON1           DIM     2
CYEAR1          DIM     2
CCENT1          DIM     2
.
PRGID     INIT      "IBAPAT58"
PRGNAM    INIT      "Insurance Claims Listing"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL          DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patin1af";
          OPEN      PATIN1A1,"patin1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"patwc1af";
          OPEN      PATWC1A1,"patwc1af"
.
          DISPLAY   *P64:24,"patwmabf";
          OPEN      PATWMAB1,"patwmabf"
.
          DISPLAY   *P64:24,"patwvetf";
          OPEN      PATWVET1,"patwvetf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A3,"aaede1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM       
          MOVE      TFILNAME,FNAMEA
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEB
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEC
.
+
.
.           MAIN        MENU
.
START   MOVE       ZERO,CPAGENO
        MOVE       ZERO,TOTAL
.
        DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = Workers Compensation Claims":
                  *P1:6,*V2LON,TWO,*HOFF," = Motor Accident Board Claims":
                  *P1:7,*V2LON,THREE,*HOFF," = Veteran Affairs Claims"
.
REKSTRT KEYIN     *P1:9,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
.
BRNCH   BRANCH    OPTION OF BRNCH1,BRNCH1,BRNCH1
        BEEP
        GOTO      REKSTRT
.
BRNCH1  DISPLAY   *P1:11,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:12,*V2LON,ONE,*HOFF," = By Admission No.":
                  *P1:13,*V2LON,TWO,*HOFF," = By Health Fund "
.
        KEYIN     *P1:15,"Option : ",*EL,*V2LON,OPTION1
.
        COMPARE   ZERO,OPTION1
        GOTO      REKSTRT IF EQUAL
.
        BRANCH    OPTION1 OF START1,START1
        BEEP
        GOTO      BRNCH1
.
.             KEYIN FROM DATE
.
START1  DISPLAY   *P1:18,*EF,"From Date : ":
                  *P1:20,"To   Date : "
.
        UNPACK    SP6 INTO CYEAR,CMON,CDAY
.
KIBA25  MOVE      TEN5,CCOL
        MOVE      TEN8,CVERT
.
        MOVE      "40",ECOL
        MOVE      CVERT,EVERT
        MOVE      CCC,CCENT
.
FIXMON  CALL      KEYDATE
        BRANCH    OVRCD,BRNCH1
.
        PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
        REP       " 0",FROMDATE
.
        MOVE      FROMDATE,DATEF
.
KIBA26  UNPACK    SP6 INTO CYEAR,CMON,CDAY
.
        MOVE      TEN5,CCOL
        MOVE      TWENTY,CVERT
        MOVE      "40",ECOL
        MOVE      CVERT,EVERT
.
        MOVE      CCC,CCENT
.
FIXMON2 CALL      KEYDATE
        BRANCH    OVRCD,KIBA25
.
        PACK      TODATE,CCENT,CYEAR,CMON,CDAY
        REP       " 0",TODATE
.
        MATCH     FROMDATE,TODATE
        GOTO      ERR1 IF LESS
.
SYSTYP  CALL      PATSELFN
        BRANCH    EXIT,START
.
        GOTO      REKEY
.
ERR1    DISPLAY   *P1:24,*EL,*P10:24,*B,*V2LON:
                  "** FROM Date Greater than TO Date **",*W2,*P1:24,*EL;
        GOTO      START1
.
.        CHECK IF DATES ARE OK OR NOT
.
REKEY   KEYIN     *P1:24,*EF,"Ok to Continue (":
	          *V2LON,"Y",*HOFF,*DV,SLASH:
		  *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
        REP       "nNyY",ANS
        MATCH     "N" TO ANS
        GOTO      START IF EQUAL
.
        MATCH     "Y" TO ANS
        GOTO      RDATE IF EQUAL
.
        BEEP
        GOTO      REKEY
.
RDATE   CALL       INDZD
        BRANCH     OPCND OF START1
.
        MOVE      ONE,UNIQUE
        MOVE      "60",CLNO
        DISPLAY   *P1:24,*EL,*P20:24,"Admission Number :":
                  *P60:24,"Scanning :";
.
        BRANCH    FSTSYS,AAEREP,OUTREP,INPREP
.
        CALL      ACCEMG
        CALL      OUTPAT
        CALL      INPPAT
        GOTO      REPORT
.
AAEREP  CALL      ACCEMG
        GOTO      REPORT
.
OUTREP  CALL      OUTPAT
        GOTO      REPORT
.
INPREP  CALL      INPPAT
        GOTO      REPORT
.
ACCEMG  PACK      KEY21 WITH FROMDATE,SP20
        CALL      RDSDETA3
NEXTA   CALL      RDKDETA3
        BRANCH    OVRCD OF ENDAAE
.
        MATCH     ADADATE,TODATE
        RETURN    IF LESS
.
        MOVE      DADANUMB,AADMNO
        MOVE      AADMNO,KEY8
        CALL      REDCLM
        GOTO      NEXTA
.
ENDAAE  RETURN
.
. ------- Outpatients -------
.
OUTPAT    PACK      KEY6,FSTSITE
          CALL      RDSITA1
.
          MATCH     SP6,FSTSITE
          GOTO      OUTALL IF EQUAL
.
          BRANCH    OVRCD,ENDOUT
.
          GOTO      OUTCLS
.
OUTALL    CALL      RDKSITA1
          BRANCH    OVRCD,ENDALL 
.
OUTCLS    CLOSE     OUTBB1A2
          PACK      CFNAME,OSTFILE,FILBB1A2
          CALL       OPOTBB12
.
          PACK      KEY12,FROMDATE,SP20
          CALL      RDSBOKB2
.
NEXTO     CALL      RDKBOKB2
          BRANCH    OVRCD,ENDOUT
.
          MATCH     OBADATE,TODATE
          GOTO      ENDOUT IF LESS
.
          MOVE      DADANUMB,AADMNO
          MOVE      AADMNO,KEY8
          CALL      REDCLM
          GOTO      NEXTO
.
ENDOUT    MATCH     SP6,FSTSITE
          GOTO      OUTALL IF EQUAL
.
ENDALL    RETURN
.
INPPAT  PACK      KEY16,FROMDATE,SP10
        CALL      RDSMISS3
NEXTP   CALL      RDKMISS3
        BRANCH    OVRCD OF ENDINP
. 
        MOVE      TODATE,TDATE
.
        MATCH     ADATE,TDATE
        RETURN    IF LESS
.
        COMPARE   ONE,ASTAT
        GOTO      NEXTP IF EQUAL
.
        COMPARE   FIVE,ASTAT
        GOTO      NEXTP IF EQUAL
.
        MOVE      AADMNO,KEY8
        CALL      REDCLM
        GOTO      NEXTP
.
ENDINP  RETURN
.
REDCLM  BRANCH    OPTION OF RDWCOMA,RDMABA,RDVETA
.
RDWCOMA CALL      RDWCOM1
        MOVE      WCADMNO,ADMNO
        GOTO      CHKREDA
.
RDMABA  CALL      RDWMAB1
        MOVE      MADMNO,ADMNO
        GOTO      CHKREDA
.
RDVETA  CALL      RDWVET1
        MOVE      VADMNO,ADMNO
.
CHKREDA BRANCH    OVRCD OF BRCCLM
.
        DISPLAY   *P72:24,ADMNO;
.
        MOVE      ADMNO,PVIBILL
        MOVE      PVIBILL,KEY8
        CALL      RDVISA1
.
        COMPARE   FSTSYS,PVITYPE
        RETURN    IF LESS
.
        COMPARE   PVITYPE,FEDSYS
        RETURN    IF LESS
.
        MATCH     FSTSITE,PVISITE
        RETURN    IF LESS
.
        MATCH     PVISITE,FEDSITE
        RETURN    IF LESS
.
        MATCH     SP3,FSTDEPT
        GOTO      PRCCLM IF EQUAL
.
        MOVE      PVISITE,KEY6
        CALL      RDSITA1
        BRANCH    OVRCD,BRCCLM
.
        MATCH     FSTDEPT,OSTSYST
        RETURN    IF LESS
.
        MATCH     OSTSYST,FEDDEPT
        RETURN    IF LESS
.
PRCCLM  MOVE      PVIURNO,PURNO
        MOVE      PURNO,KEY8
        CALL      RDMAST1
        BRANCH    OVRCD OF EURNO
.
.       Calculate Insurance Invoice Amount
.
GETTOTS CALL      CALCOUTS
.
        COMPARE   ZERO,TOTOUTS
        GOTO      BRCCLM IF EQUAL
.
        DISPLAY   *P40:24,*V2LON,ADMNO;
.
        ADD       TOTOUTS,TOTAL
        MOVE      UNIQUE,DUNIQUE
        CALL      WRITEMP
.
BRCCLM  RETURN
.
.
REPORT  COMPARE   ONE,UNIQUE
        GOTO      NODATA IF EQUAL
.
        MOVE      ZERO,TOTAL
        MOVE      SP6,SAVFUND
        MOVE      SP20,KEY14
        MOVE      SP20,KEY16
.
        CALL      RDSTEMP
NXTEMP  CALL      RDKTEMP
        BRANCH    OVRCD OF CHKLST
.
        BRANCH    OPTION1 OF PRTREC
.
.       Print by health fund
.
        MATCH     SAVFUND,AFUNDH
        GOTO      PRTREC IF EQUAL
.
.       A new health fund
.
        MOVE      AFUNDH,SAVFUND
        CALL      PRTOT
        MOVE      "60",CLNO
        MOVE      ZERO,TOTAL
.
PRTREC  CALL      DISPDA
        GOTO      NXTEMP
.
CHKLST  CALL      PRTOT           * print total of the last one if necessary
        GOTO      ENDREP
.
.
.
.       Error Messages
.
EURNO   COMPARE   ONE,FSTSYS
        GOTO      EURNO1 IF NOT EQUAL
        DISPLAY   *P1:22,*EL,*P10:22,*B,*V2LON:
                  "** Invalid Accident & Emergency Number found : ",PURNO," **";
        GOTO      NEXTA
.
EURNO1  COMPARE   TWO,FSTSYS
        GOTO      EURNO2 IF NOT EQUAL
        DISPLAY   *P1:22,*EL,*P10:22,*B,*V2LON:
                  "** Invalid Outpatient Number found : ",PURNO," **";
        GOTO      NEXTO
.
EURNO2  DISPLAY   *P1:22,*EL,*P10:22,*B,*V2LON:
                  "** Invalid Inpatient Number found : ",PURNO," **";
        GOTO      NEXTP
.
EPREADM DISPLAY   *P20:22,*EL,*B,*V2LON:
                  "** Pre-Admission File Error **";
        GOTO      NEXTA
+
NODATA  DISPLAY   *P20:24,*EL,*V2LON:
                  "** No Available Data **",*W2,*P1:24,*EL;
        CALL      KILLIDZ
        GOTO      START
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDREP   COMPARE  ZERO,CPAGENO
         GOTO     NODATA IF EQUAL
.
         BRANCH   OPTION OF ENDR10,ENDR10,ENDR20
.
ENDR10   PRINT    *N,*N," *** END OF REPORT ***"
         GOTO     ENDR30
.
ENDR20   PRINT    *N,*N,*21," *** END OF REPORT ***"
.
ENDR30   DISPLAY  *P20:24,*EL,*V2LON:
                  "** Report Generation Completed **",*W2,*P1:24,*EL;
         CALL     KILLIDZ
         GOTO     START
.
.
.        Over condition found..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
PRTOT    COMPARE   ZERO,TOTAL
         RETURN    IF EQUAL
.
         BRANCH    OPTION TO PRT10,PRT10,PRT20
.
PRT10    PRINT     *114,"TOTAL",*120,"! ",TOTAL,*132,"!",*N:
                   *120,"*-----------*"
         RETURN
.
PRT20    PRINT     *74,"TOTAL",*82,"!   ",TOTAL,*96,"!",*N:
                   *82,"*-------------*"
         RETURN
.
+
.
.     PRINT ACTUAL DATA 
.
DISPDA  COMPARE   "55",CLNO
        CALL      HEAD IF NOT LESS
.
        MOVE      PGNAME,ANS
        PACK      NAME WITH ANS,DOT,PSNAME
        MOVE      WCEADD1,DIM30
        MOVE      WCEADD2,DIM30A
        MOVE      PADD1,DIM25 
        MOVE      PADD2,DIM25A
        MOVE      PSUBRB,DIM16 
        MOVE      PPOST,DIM7  
.
        BRANCH    OPTION OF DISWCO,DISMAB,DISVET
.
DISWCO  UNPACK    WCACDAT,XCENT,XYEAR,XMON,XDAY
.
        MOVE      SP30,INAME
        MOVE      WCICODE,KEY6
        CALL      RDINSR1
.
        MOVE      INAME,ICNAME            ** Truncate Insurance Company name
        MOVE      WCEADD3,EADD3           ** Truncate Employer Address Line 3
.
        PRINT     *1,"!",WCADMNO,*10,"!",NAME,*36,"!":
                  XDAY,"/",XMON,"/",XCENT,XYEAR,*47,"!",WCENAME:
                  *78,"!",ICNAME,*99,"!",WCCLMNO,*120,"! ",TOTOUTS:
                  *132,"!",*N,*1,"!",*10,"!",DIM25,*36,"!",*47,"!":
                  DIM30,*78,"!",*99,"!",*120,"!",*132,"!":
                  *N,*1,"!",*10,"!",DIM25A,*36,"!",*47,"!":
                  DIM30A,*78,"!",*99,"!",*120,"!",*132,"!":
                  *N,*1,"!",*10,"!",DIM16,*28,DIM7,*36,"!",*47,"!":
                  EADD3,*70,WCEPOST,*78,"!",*99,"!",*120,"!",*132,"!"
        GOTO      DISPCNT
.
DISMAB  UNPACK    MACDAT WITH XCENT,XYEAR,XMON,XDAY
        UNPACK    MACLOC INTO DIM20,DIM20A
.
        PRINT     *1,"!",MADMNO,*10,"! ",NAME,*37,"!":
                  XDAY,"/",XMON,"/",XYEAR,*46,"!",MPOLIC:
                  *68,"!",MSNAME,*99,"!",MREFNO,*120,"! ",TOTOUTS:
                  *132,"!",*N,*1,"!",*10,"! ",DIM25,*37,"!",*46,"!":
                  *68,"!",*99,"!",*120,"!",*132,"!":
                  *N,*1,"!",*10,"! ",DIM25A,*37,"!",*46,"!":
                  DIM20:
                  *68,"!Telephone : ",MSTELE,*99,"!",*120,"!",*132,"!":
                  *N,*1,"!",*10,"! ",DIM16,*28,DIM7,*37,"!",*46,"!":
                  DIM20A:
                  *68,"!",*99,"!",*120,"!",*132,"!"
        GOTO      DISPCNT
.
DISVET  PRINT     *21,"! ",VADMNO,*32,"! ",NAME,*59,"! ",VCLMNO:
                  *82,"!   ",TOTOUTS,*96,"!":
                  *N,*21,"!",*32,"! ",DIM25,*59,"!",*82,"!",*96,"!":
                  *N,*21,"!",*32,"! ",DIM25A,*59,"!",*82,"!",*96,"!":
                  *N,*21,"!",*32,"! ",DIM16,*52,DIM7,*59,"!",*82,"!":
                  *96,"!"
.
DISPCNT CALL      PAGEND
        ADD       FIVE,CLNO
        RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    BRANCH  OPTION TO PAGEND1,PAGEND1,PAGEND2
.
PAGEND1   PRINT   "*---------------------------------------------------":
                  "--------------------------------------------------------":
                  "-----------------------*"
          ADD     ONE,CLNO
          RETURN
.
PAGEND2   PRINT   *21,"*-----------------------------------------------":
                  "---------------------------*"
          ADD     ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD     
. ADD     ONE,CPAGENO
          CLOCK   TIME,CTIMEIS
.
          CALL    HEAD132
.                 *45,"*** INSURANCE CLAIMS LISTING ***":
.
          MOVE    DATEF,FDATE
.
          BRANCH  OPTION OF PRNTWC,PRNTMA,PRNTVE
.
PRNTWC    PRINT   *45,"WORKERS COMPENSATION CLAIMS";
          BRANCH   OPTION1,PRNTWC0
          PRINT   *75,"H.F. : ",AFUNDH;
.
PRNTWC0   UNPACK  FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL    PACDATE
          PRINT   *N,*35,"Admission From Date : ",CPCDATE;
.
          UNPACK  TODATE,CCENT,CYEAR,CMON,CDAY
          CALL    PACDATE
          COMPARE THREE,FSTSYS
          GOTO    PRNTWC1 IF NOT EQUAL
          PRINT   *68,"Admission To Date : ",CPCDATE,*N
          GOTO    PRNTWC2
.
PRNTWC1   PRINT   *68,"Admission To Date : ",CPCDATE,*N
.
PRNTWC2   CALL    PRTSEL
          CALL    PAGEND
.
          PRINT   *1,"!Adm No",*10,"!Patients Name and Address":
                  *36,"!Accident",*47,"!Employer Name and Address":
                  *78,"!Insurance Company",*99,"!Claim":
                  *120,"!Outstanding",*132,"!",*N:
                  *1,"!",*10,"!",*36,"!  Date",*47,"!":
                  *78,"!",*99,"!Number",*120,"!    Amount",*132,"!"
.
          GOTO    PRTCONT
.
PRNTMA    PRINT   *45,"MOTOR ACCIDENT BOARD CLAIMS";
          BRANCH  OPTION1 OF PRNTMA0
          PRINT   *75,"H.F. : ",AFUNDH;
.
PRNTMA0   UNPACK  FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL    PACDATE
          PRINT   *N,*35,"Admission From Date : ",CPCDATE;
.
          UNPACK  TODATE,CCENT,CYEAR,CMON,CDAY
          CALL    PACDATE
          COMPARE THREE,FSTSYS
          GOTO    PRNTMA1 IF NOT EQUAL
          PRINT   *68,"Admission To Date : ",CPCDATE,*N
          GOTO    PRNTMA2
.
PRNTMA1   PRINT   *68,"Admission To Date : ",CPCDATE,*N
.
PRNTMA2   CALL    PRTSEL
          CALL    PAGEND
.
          PRINT   *1,"! Adm No",*10,"! Patients Name and Address":
                  *37,"!Accident",*46,"!Police Station /":
                  *68,"!Solicitor",*99,"!M.A.B":
                  *120,"!Outstanding",*132,"!",*N:
                  *1,"!",*10,"!",*37,"!  Date":
                  *46,"!Accident Location",*68,"!":
                  *99,"!Reference No",*120,"!    Amount",*132,"!"
.
          GOTO    PRTCONT
.
PRNTVE    PRINT   *45,"VETERAN AFFAIRS CLAIMS";
.
          BRANCH  OPTION1 OF PRNTVE0
          PRINT   *75,"H.F. : ",AFUNDH;
.
PRNTVE0   UNPACK  FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL    PACDATE
          PRINT   *N,*30,"Admission From Date : ",CPCDATE;
.
          UNPACK  TODATE,CCENT,CYEAR,CMON,CDAY
          CALL    PACDATE
          COMPARE THREE,FSTSYS
          GOTO    PRNTVE1 IF NOT EQUAL
          PRINT   *68,"Admission To Date : ",CPCDATE,*N
          GOTO    PRNTVE2
.
PRNTVE1   PRINT   *68,"Admission To Date : ",CPCDATE,*N
.
PRNTVE2   CALL    PRTSEL
          CALL    PAGEND
.
          PRINT   *21,"! Adm No",*32,"! Patients Name and Address":
                  *59,"! Reference Number":
                  *82,"! Outstanding",*96,"!",*N:
                  *21,"!",*32,"!",*59,"!":
                  *82,"!    Amount",*96,"!"
.
PRTCONT   CALL    PAGEND
.
          MOVE    "9",CLNO
          RETURN
.
.          This subroutine calculates the total INSURANCE amount for
.          the current admission number ADMNO
.
CALCOUTS   MOVE   ZERO,TOTOUTS
           PACK   KEY16 WITH ADMNO,SP8
           CALL   RDSINV3
.
CALCOUT1   CALL   RDKINV3
           BRANCH OVRCD OF CALCOUT9
.
           MATCH     ADMNO,PINVADM
           GOTO      CALCOUT9 IF NOT EQUAL
.
           ADD       PINVAMT,TOTOUTS
           ADD       PINVPATA,TOTOUTS
           ADD       PINVHFDA,TOTOUTS
           ADD       PINVOTHA,TOTOUTS
           ADD       PINVJOUR,TOTOUTS
           GOTO      CALCOUT1
.
CALCOUT9   RETURN
+
PRTSEL    COMPARE   ZERO,FSTSYS
          GOTO      AEHEAD IF NOT EQUAL
.
          PRINT     *45,"CONSOLIDATED REPORT",*N,*N;
          COMPARE   ZERO,FSTSYS
          RETURN    IF EQUAL
.
AEHEAD    COMPARE   ONE,FSTSYS
          GOTO      OPHEAD IF NOT EQUAL
.
          PRINT     *45,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          COMPARE   ONE,FSTSYS
          RETURN    IF EQUAL
.
OPHEAD    COMPARE   TWO,FSTSYS
          GOTO      IPHEAD IF NOT EQUAL
.
          PRINT     *45,"OUTPATIENT REPORT",*N;
          MATCH     SP6,FSTSITE
          GOTO      ALSITE IF NOT EQUAL
.
          PRINT     *45,"ALL SITES";
          GOTO      OPDEPT
.
ALSITE    PRINT     *45,"SITE",FSTSITE;
.
OPDEPT    MATCH     SP3,FSTDEPT
          GOTO      ALDEPT IF NOT EQUAL
.
          PRINT     *59,"ALL DEPARTMENTS",*N,*N;
          GOTO      PRTSEL9
.
ALDEPT    PRINT     *59,"DEPT",FSTDEPT,*N,*N;
          GOTO      PRTSEL9
.
IPHEAD    COMPARE   THREE,FSTSYS
          GOTO      OTHEAD IF NOT EQUAL
.
          PRINT     *45,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
OTHEAD    PRINT     *45,"OTHER FINANCIAL REPORT",*N,*N;
.
PRTSEL9   RETURN 
.
.
.       Create an indexed file based on port
.       for accumulating Admission Type totals
.
INDZD   
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
.
        CALL        KILLIDZ
        CALL        WRINDX
.
        EXECUTE     CMDLINE,TASKID
.
        MOVE        ZERO,OPCND
        MOVE        ZERO,CLSFLG
.
        TRAP        NOINDZ IF IO
        CALL        OPCLTMP         * Open tempfile
        TRAPCLR     IO
ENDC    RETURN
.
NOINDZ  MOVE        ONE,OPCND
        DISPLAY     *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    " Not Found **",*W4;
        TRAPCLR     IO
        RETURN
+
.
.       Deleting an indexed file based on port
.
KILLIDZ MOVE       ONE,CLSFLG
        LOAD       FNAMET,OPTION,FNAMEA,FNAMEB,FNAMEC
        CALL       OPCLTMP          * Close tempfile
.
        DISPLAY    *P1:24,*EL,*P20:24,*V2LON:
                   "** Deleting Indexed File **",*W;
.
        CLEAR      CMDLINE
        APPEND     "iserase ",CMDLINE
        APPEND     FNAMET,CMDLINE
        RESET      CMDLINE
        EXECUTE    CMDLINE,TASKID
        DISPLAY    *P1:3,*EF,*P1:10
        RETURN
.
WRINDX  CLEAR      CMDLINE
        APPEND     "isbuild ",CMDLINE
        APPEND     FNAMET,CMDLINE
.
        BRANCH     OPTION OF WRIDX10,WRIDX20,WRIDX30
.
WRIDX10 BRANCH     OPTION1 OF WRIDX12
        APPEND     " 383 U177-182,375-382",CMDLINE
        RESET      CMDLINE
        RETURN
.
WRIDX12 APPEND     " 383 U1-8,375-382",CMDLINE
        RESET      CMDLINE
        RETURN
.
WRIDX20 BRANCH     OPTION1 OF WRIDX22
        APPEND     " 316 U139-144,308-315",CMDLINE
        RESET      CMDLINE
        RETURN
.
WRIDX22 APPEND     " 316 U1-8,308-315",CMDLINE
        RESET      CMDLINE
        RETURN
.
WRIDX30 BRANCH     OPTION1 OF WRIDX32
        APPEND     " 196 U29-34,188-195",CMDLINE
        RESET      CMDLINE
        RETURN
.
WRIDX32 APPEND     " 196 U1-8,188-195",CMDLINE
        RESET      CMDLINE
        RETURN
.
+
.
.       Open/close tempfile
.
OPCLTMP LOAD      FNAMET,OPTION,FNAMEA,FNAMEB,FNAMEC
        BRANCH    OPTION OF OPEN10,OPEN20,OPEN30
.
.       Workers Comp.
.
OPEN10  BRANCH    OPTION1 OF OPEN12
.
        BRANCH    CLSFLG OF CLOSE10      * By health fund
        OPEN      PATTMP1A,FNAMET
        RETURN
.
CLOSE10 CLOSE     PATTMP1A
        RETURN
.
OPEN12  BRANCH    CLSFLG OF CLOSE12       * By admissions
        OPEN      PATTMP1,FNAMET
        RETURN
.
CLOSE12 CLOSE     PATTMP1
        RETURN
.
.       Motor accident
.
OPEN20  BRANCH    OPTION1 OF OPEN22
.
        BRANCH    CLSFLG OF CLOSE20      * By health fund
        OPEN      PATTMP2A,FNAMET
        RETURN
.
CLOSE20 CLOSE     PATTMP2A
        RETURN
.
OPEN22  BRANCH    CLSFLG OF CLOSE22      * By admissions
        OPEN      PATTMP2,FNAMET
        RETURN
.
CLOSE22 CLOSE     PATTMP2
        RETURN
.
.       Veteran affairs
.
OPEN30  BRANCH    OPTION1 OF OPEN32
.
        BRANCH    CLSFLG OF CLOSE30      * By health fund
        OPEN      PATTMP3A,FNAMET
        RETURN
.
CLOSE30 CLOSE     PATTMP3A
        RETURN
.
OPEN32  BRANCH    CLSFLG OF CLOSE32      * By admissions
        OPEN      PATTMP3,FNAMET
        RETURN
.
CLOSE32 CLOSE     PATTMP3
        RETURN
.
.
.       Write to tempfile
.
WRITEMP ADD       ONE,UNIQUE
        MOVE      UNIQUE,DUNIQUE
        BRANCH    OPTION OF WRIT10,WRIT20,WRIT30
.
.       Workers Comp.
.
WRIT10  PACK      DWCADMNO WITH AADMNO
        BRANCH    OPTION1 OF WRIT12
.
.       By health fund
.
        PACK      KEY14 WITH AFUNDH,DUNIQUE
        WRITE     PATTMP1A,KEY14;DWCADMNO,WCENAME,WCEADD1,WCEADD2,WCEADD3:
                            WCEPOST,WCETELE,WCACDAT,WCICODE,WCCLMNO,AFUNDH:
                            PSNAME,PGNAME,PADD1,PADD2,PSUBRB,PPOST,TOTOUTS:
                            DUNIQUE
        RETURN
.
.       By admission
.
WRIT12  PACK      KEY16 WITH DWCADMNO,DUNIQUE
        WRITE     PATTMP1,KEY16;DWCADMNO,WCENAME,WCEADD1,WCEADD2,WCEADD3:
                            WCEPOST,WCETELE,WCACDAT,WCICODE,WCCLMNO,AFUNDH:
                            PSNAME,PGNAME,PADD1,PADD2,PSUBRB,PPOST,TOTOUTS:
                            DUNIQUE
        RETURN
.
.
.       Motor accident
.
WRIT20  PACK      DMADMNO WITH AADMNO
        BRANCH    OPTION1 OF WRIT22
.
.       By health fund
.
        PACK      KEY14 WITH AFUNDH,DUNIQUE
        WRITE     PATTMP2A,KEY14;DMADMNO,MREFNO,MPOLIC,MACDAT,MSNAME,MSTELE:
                            MACLOC,AFUNDH,PSNAME,PGNAME,PADD1,PADD2:
                            PSUBRB,PPOST,TOTOUTS,DUNIQUE
        RETURN
.
WRIT22  PACK      KEY16 WITH DMADMNO,DUNIQUE
        WRITE     PATTMP2,KEY16;DMADMNO,MREFNO,MPOLIC,MACDAT,MSNAME,MSTELE:
                            MACLOC,AFUNDH,PSNAME,PGNAME,PADD1,PADD2:
                            PSUBRB,PPOST,TOTOUTS,DUNIQUE
        RETURN
.
.       Veteran affairs
.
WRIT30  PACK      DVADMNO WITH AADMNO
        BRANCH    OPTION1 OF WRIT32
.
.       By health fund
.
        PACK      KEY14 WITH AFUNDH,DUNIQUE
        WRITE     PATTMP3A,KEY14;DVADMNO,VCLMNO:
                           AFUNDH,PSNAME,PGNAME,PADD1,PADD2:
                           PSUBRB,PPOST,TOTOUTS,DUNIQUE
        RETURN
.
WRIT32  PACK      KEY16 WITH DVADMNO,DUNIQUE
        WRITE     PATTMP3,KEY16;DVADMNO,VCLMNO:
                           AFUNDH,PSNAME,PGNAME,PADD1,PADD2:
                           PSUBRB,PPOST,TOTOUTS,DUNIQUE
        RETURN
.
.
RDSTEMP BRANCH   OPTION OF RDS10,RDS20,RDS30
.
.       Workers Comp.
.
RDS10   BRANCH   OPTION1 OF RDS12
        RESET    KEY14
        READ     PATTMP1A,KEY14;;
        RETURN
.
RDS12   RESET    KEY16
        READ     PATTMP1,KEY16;;
        RETURN
.
.       Motor Accidents
.
RDS20   BRANCH   OPTION1 OF RDS22
        RESET    KEY14
        READ     PATTMP2A,KEY14;;
        RETURN
.
RDS22   RESET    KEY16
        READ     PATTMP2,KEY16;;
        RETURN
.
.       Veteran affairs
.
RDS30   BRANCH   OPTION1 OF RDS32
        RESET    KEY14
        READ     PATTMP3A,KEY14;;
        RETURN
.
RDS32   RESET    KEY16
        READ     PATTMP3,KEY16;;
        RETURN
.  
.
RDKTEMP MOVE     ZERO,OVRCD
        BRANCH   OPTION OF RDK10,RDK20,RDK30
.
RDK10   BRANCH   OPTION1 OF RDK12
        READKS   PATTMP1A;DWCADMNO,WCENAME,WCEADD1,WCEADD2,WCEADD3:
                          WCEPOST,WCETELE,WCACDAT,WCICODE,WCCLMNO,AFUNDH:
                          PSNAME,PGNAME,PADD1,PADD2,PSUBRB,PPOST,TOTOUTS:
                          DUNIQUE
        MOVE     DWCADMNO,WCADMNO
        MOVE     DUNIQUE,UNIQUE
        GOTO     ENDRDK
.
RDK12   READKS   PATTMP1;DWCADMNO,WCENAME,WCEADD1,WCEADD2,WCEADD3:
                         WCEPOST,WCETELE,WCACDAT,WCICODE,WCCLMNO,AFUNDH:
                         PSNAME,PGNAME,PADD1,PADD2,PSUBRB,PPOST,TOTOUTS:
                         DUNIQUE
        MOVE     DWCADMNO,WCADMNO
        MOVE     DUNIQUE,UNIQUE
        GOTO     ENDRDK
.
RDK20   BRANCH    OPTION1 OF RDK22
        READKS    PATTMP2A;DMADMNO,MREFNO,MPOLIC,MACDAT,MSNAME,MSTELE:
                           MACLOC,AFUNDH,PSNAME,PGNAME,PADD1,PADD2:
                           PSUBRB,PPOST,TOTOUTS,DUNIQUE
        MOVE      DMADMNO,MADMNO
        MOVE      DUNIQUE,UNIQUE
        GOTO      ENDRDK
.
RDK22   READKS    PATTMP2;DMADMNO,MREFNO,MPOLIC,MACDAT,MSNAME,MSTELE:
                          MACLOC,AFUNDH,PSNAME,PGNAME,PADD1,PADD2:
                          PSUBRB,PPOST,TOTOUTS,DUNIQUE
        MOVE      DMADMNO,MADMNO
        MOVE      DUNIQUE,UNIQUE
        GOTO      ENDRDK
.
RDK30   BRANCH    OPTION1 OF RDK32
        READKS    PATTMP3A;DVADMNO,VCLMNO:
                           AFUNDH,PSNAME,PGNAME,PADD1,PADD2:
                           PSUBRB,PPOST,TOTOUTS,DUNIQUE
        MOVE      DVADMNO,VADMNO
        MOVE      DUNIQUE,UNIQUE
        GOTO      ENDRDK
.
RDK32   READKS    PATTMP3;DVADMNO,VCLMNO:
                           AFUNDH,PSNAME,PGNAME,PADD1,PADD2:
                           PSUBRB,PPOST,TOTOUTS,DUNIQUE
        MOVE      DVADMNO,VADMNO
        MOVE      DUNIQUE,UNIQUE
.
ENDRDK  GOTO      OVERCOND IF OVER
        RETURN
.
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATSELFN
          INC       TFILENAM
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATPR1IO/INC
          INC       PATINVIO/INC
          INC       PATIN1IO/INC
          INC       PATWC1IO/INC
          INC       PATWMAIO/INC
          INC       PATWVEIO/INC
          INC       PATVISIO/INC
          INC       PATCODIO/INC
          INC       OUTSITIO/INC
          INC       OUTBB1IO/INC
          INC       AAEDE1IO/INC
          INC       IBASECIO/INC
          INC       IBAPASIO/INC
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
.
ENDIT     CHAIN     PGM
          STOP

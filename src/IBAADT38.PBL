.******************************************************************************
.* System    : Patient                                                        *
.* Program   : IBAADT38                                                       *
.* Desc      : Casemix Codes Maintainence                                     *
.******************************************************************************
.* Date      : 10/10/00                                                       *
.* Author    : Caleb Sharman                                                  *
.* Modifications :                                                            *
.*        V9.03.01  10/10/2003  Lina Vo       CAR 41778                       *
.*                  Changed program to ask for inclusion of inactive MBS      *
.*                  item codes in Load Items File.                            *
.*        V9.02.01  24/03/2003  Jill Habasque SRF 36489                       *
.*                  Added write/update to keyword file                        *
.*        V5.09.B02 24/01/2001  Jill Habasque                                 *
.*                  Added ? search for casemix codes                          *
.*                                                                            *
.******************************************************************************
+
         INC        STD001FD
         INC        STDHLPDF
         INC        STDKWSDF
.
         INC        PATCMCFD/INC
         INC        PATKCMFD/INC
         INC        PATDGWFD/INC
         INC        PATITMFD/INC
.
. ----- Program Variables -----
.
ACTVFLAG  DIM       1                       * Include inactive items in upload
DISPOPTN  DIM       14                      * Display option selected
INSOPT    DIM       1
INSRTCD   DIM       9
KYIND     FORM      1                       * indicates item selected
.
. ----- Program Constants -----
.
CHANGE    INIT      "Change           "
INSERT    INIT      "Insert           "
LOADDRG   INIT      "Load from DRG File"
LOADITEM  INIT      "Load from Item File"
SP60      INIT      "                              ":
                    "                              "
PRGID     INIT      "IBAADT38"
PRGNAM    INIT      "Casemix Codes Maintenance"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                 MAINLINE                                   *
.*                             Controlling Logic                              *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialization and open files
.
ML1000    CALL      OPTN0000                * Select options
          BRANCH    OPTION,ML2000,ML3000,ML4000,ML5000
.                          Insert   /\  DRG File  /\   
.                                 Change        Item File
          GOTO      ML9999
.
. ----- Insert -----
.
ML2000    CALL      INST0000    
          GOTO      ML1000
.
. ----- Change -----
.
ML3000    CALL      CHNG0000    
          GOTO      ML1000
.
. ----- Load DRG File -----
.
ML4000    CALL      LDRG0000    
          GOTO      ML1000
.
. ----- Load Item File -----
.
ML5000    CALL      INACTV00
          BRANCH    EXIT,ML1000
          CALL      LITM0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM                     * Chain back to program
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                              Initialization                                *
.*  The initialization routine is used to display headings and open files.    *
.*  Any other general initialization can also be performed here.              *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display heading
.
          DISPLAY   *P56:24,"Opening patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P56:24,"Opening patdhwaf";
          OPEN      PATDGWA1,"patdgwaf"
.
          DISPLAY   *P56:24,"Opening patitemn";
          OPEN      PATITEM1,"patitemn"
.
          OPEN      PATKCMA1,"patkcmaf"   * Casemix Codes Key Words
          OPEN      PATKCMA2,"patkcmaf"   * Casemix Codes Key Words
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000               Called by: ML0000   *
.*                         Get user options or exit                           *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          CALL      DISPHEAD
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Insert":
                    *P1:6,*V2LON,"2",*HOFF," = Change":
                    *P1:7,*V2LON,"3",*HOFF," = Load from DRG File":
                    *P1:8,*V2LON,"4",*HOFF," = Load from Item File ":
                    *P1:11,"Select Option : ";
.
OPTN1000  KEYIN     *P17:11,*DV,UNDLN1,*P17:11,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL       * Exit
.
          BRANCH    OPTION,OPTN9000,OPTN9000,OPTN9000,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  LOAD      DISPOPTN,OPTION,INSERT,CHANGE,LOADDRG,LOADITEM
          DISPLAY   *P53:2,*V2LON," - ",DISPOPTN,SP1;
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 DISP0000               Called by: INST0000 *
.*                           Display template                        CHNG0000 *
.******************************************************************************
DISP0000  DISPLAY   *P1:3,*EF,*P4:5,"   Casemix Code        :":
                    *P4:7,"1. Description  1      :":
                    *P4:8,"2. Description  2      :":
                    *P4:9,"3. Expected LOS        :":
                    *P4:10,"4. Active (Y/N)        :"
.
DISP9999  RETURN
+
.******************************************************************************
.*                                 INST0000               Called by: ML2000   *
.*                           Insert Casemix Code                              *
.******************************************************************************
INST0000  MOVE      ONE,INSOPT
          MOVE      ONE,FORM1
          MOVE      ZERO,KYIND
          MOVE      SP9,INSRTCD
.
          CALL      DISP0000
.
          MOVE      TWENTY9,CCOL
          MOVE      FIVE,CVERT 
          MOVE      TWO,CKEYTYP
          CALL      KPTCAA00
          MOVE      PTCACODE,INSRTCD
.
          RESET     INSRTCD
          MATCH     SP9,INSRTCD              * nothing entered ?
          GOTO      INST9999 IF EQUAL
.
          PACK      KEY9,INSRTCD             * check if code already exists
          CALL      RDPTCMC1
          BRANCH    OVRCD,INST1000
.
          DISPLAY   *P3:24,"This code already exists               ";
          CALL      HITENTER
          GOTO      INST0000
.
INST1000  BRANCH    FORM1,INST2000,INST3000,INST4000,INST5000
          GOTO      INST6000
.
INST2000  CALL      DES10000             * enter description 1
          BRANCH    KYIND,INST6000
.
INST3000  CALL      DES20000             * enter description 2
          BRANCH    KYIND,INST6000
.
INST4000  CALL      ELOS0000             * enter expected LOS
          BRANCH    KYIND,INST6000
.
INST5000  CALL      ACTV0000             * enter active
          BRANCH    KYIND,INST6000
.
INST6000  MOVE      ONE,KYIND
          MOVE      SP8,INSOPT
          DISPLAY   *P2:24,"Select Item, (",*V2LON,ANSP,*HOFF:
                    ")ost, (",*V2LON,ANSC,*HOFF,")ancel  : ";
.
          KEYIN     *P36:24,INSOPT
.
          REP       UPPLOW,INSOPT
          MATCH     ANSP,INSOPT           * post selected ?
          GOTO      INST9000 IF EQUAL
.
          MATCH     ANSC,INSOPT           * cancel selected ?
          GOTO      INST9999 IF EQUAL
.
          MOVE      INSOPT,FORM1
          IF        FORM1<1 | FORM1>4
            GOTO      INST6000
          ENDIF
.
          TYPE      INSOPT
          GOTO      INST6000 IF NOT EQUAL
          GOTO      INST1000
.
INST9000  MOVE      KEY9,PTCACODE 
          CALL      WRPTCMC1              * write record to file
          PROC      PATKCMUP
.         
INST9999  RETURN     
+
.******************************************************************************
.*                                 CHNG0000               Called by: ML3000   *
.*                           Insert Casemix Code                              *
.******************************************************************************
CHNG0000  CALL      DISP0000
          MOVE      SP9,INSRTCD
.
          MOVE      TWENTY9,CCOL
          MOVE      FIVE,CVERT
          MOVE      ZERO,CKEYTYP
          CALL      KPTCAA00
          MOVE      PTCACODE,INSRTCD
.
          RESET     INSRTCD
          MATCH     SP9,INSRTCD              * nothing entered ?
          GOTO      CHNG9999 IF EQUAL
.
          MATCH     INSRTCD,QUEST            * question mark entered ?
          GOTO      CHNG1000 IF EQUAL
.
          PACK      KEY9,INSRTCD
          CALL      RDPTCMC1
          BRANCH    OVRCD,CHNG2000
.
          GOTO      CHNG3000
.
CHNG1000  CALL      HPTCAA00                  * help screen
.
          MATCH     KEY9,SP9                 * anything selected ?             
          GOTO      CHNG0000 IF EQUAL   
.
          CALL      RDPTCMC1
          GOTO      CHNG3000
.
CHNG2000  DISPLAY   *P3:24,"This code does not exist              ";
          CALL      HITENTER
.
          GOTO      CHNG0000
.
CHNG3000  DISPLAY   *P29:5,PTCACODE,*P29:7,PTCADES1,*P29:8,PTCADES2:
                    *P29:9,PTCAELOS
.
          COMPARE   ONE,PTCAACTV
          IF        @EQUAL
            DISPLAY   *P29:10,"Yes"
          ELSE
            DISPLAY   *P29:10,"No"
          ENDIF
.
          GOTO      CHNG8500
.
CHNG4000  BRANCH    FORM1,CHNG5000,CHNG6000,CHNG7000,CHNG8000
          GOTO      CHNG8500
.
CHNG5000  CALL      DES10000             * enter description 1
          BRANCH    KYIND,CHNG8500
.
CHNG6000  CALL      DES20000             * enter description 2
          BRANCH    KYIND,CHNG8500
.
CHNG7000  CALL      ELOS0000             * enter expected LOS
          BRANCH    KYIND,CHNG8500
.
CHNG8000  CALL      ACTV0000             * enter active
          BRANCH    KYIND,CHNG8500
.
CHNG8500  MOVE      ONE,KYIND
          DISPLAY   *P2:24,"Select Item, (",*V2LON,ANSP,*HOFF:
                    ")ost, (",*V2LON,ANSC,*HOFF,")ancel  : ";
.
          RESET     INSOPT
          MOVE      SP1,INSOPT
          KEYIN     *P36:24,INSOPT
.
          REP       UPPLOW,INSOPT
          MATCH     ANSP,INSOPT           * post selected ?
          GOTO      CHNG9000 IF EQUAL
.
          MATCH     ANSC,INSOPT           * cancel selected ?
          GOTO      CHNG0000 IF EQUAL
.
          RESET     INSOPT
          MATCH     INSOPT,SP1            * nothing entered ?
          GOTO      CHNG8500 IF EQUAL
.
          MOVE      INSOPT,FORM1
          GOTO      CHNG4000
.
CHNG9000  CALL      UPPTCMC1
          PROC      PATKCMUP
          GOTO      CHNG0000
.
CHNG9999  RETURN
+
.******************************************************************************
.*                                 DES10000               Called by: INST2000 *
.*                           Input Description 1                              *
.******************************************************************************
DES10000  KEYIN     *P29:7,PTCADES1
.
DES19999  RETURN
+
.******************************************************************************
.*                                 DES20000               Called by: INST3000 *
.*                           Input Description 2                              *
.******************************************************************************
DES20000  KEYIN     *P29:8,PTCADES2
.
DES29999  RETURN
+
.******************************************************************************
.*                                 ELOS0000               Called by: INST4000 *
.*                           Input Expected length of stay                    *
.******************************************************************************
ELOS0000  KEYIN     *P29:9,PTCAELOS
.
ELOS9999  RETURN
+
.******************************************************************************
.*                                 ACTV0000               Called by: INST5000 *
.*                           Input Active (Y/N)                               *
.******************************************************************************
ACTV0000  KEYIN     *P29:10,*EL,DIM1     
          REP       UPPLOW,DIM1
.
          MATCH     ANSY,DIM1
          GOTO      ACTV1000 IF EQUAL
.
          MATCH     ANSN,DIM1     
          GOTO      ACTV2000 IF EQUAL
.
          GOTO      ACTV0000
.
ACTV1000  MOVE      ONE,PTCAACTV
          DISPLAY   *P29:10,"Yes"
          GOTO      ACTV9999
.
ACTV2000  MOVE      ZERO,PTCAACTV
          DISPLAY   *P29:10,"No"
.
ACTV9999  RETURN
+
.******************************************************************************
.*                                 LDRG0000               Called by: ML4000   *
.*                             Load from DRG File                             *
.******************************************************************************
LDRG0000  PACK      KEY4,SP4
          CALL      RSPTDGW1
LDRG1000  CALL      RKPTDGW1        * read next DRG record
          BRANCH    OVRCD,LDRG9999  * end of file ?
.
          PACK      PTCACODE,PTDWDRGC,SP10       * move DRG no. to casemix code
          UNPACK    PTDWDESC,PTCADES1,PTCADES2   * move DRG desc. to des1 + des2
          MOVE      ZERO,PTCAELOS                * move zero to expected LOS 
          MOVE      ONE,PTCAACTV                 * move yes to active
.
          DISPLAY   *P2:24,"Reading file : ",PTCACODE
.
          PACK      KEY9,PTCACODE
          CALL      RDPTCMC1
          BRANCH    OVRCD,LDRG2000               * does file already exist ?
.
          GOTO      LDRG1000                     * file does exist
.
LDRG2000  DISPLAY   *P50:24,"Writing record : ",PTCACODE
          CALL      WRPTCMC1                     * file doesnt exist
          PROC      PATKCMUP
          GOTO      LDRG1000                     * read the next record
.
LDRG9999  RETURN
+
.******************************************************************************
.*                                 INACTV00               Called by: ML0000   *
.*                         Get include inactive flag                          *
.******************************************************************************
INACTV00  MOVE      ZERO,EXIT
          MOVE      SP10,ACTVFLAG
          DISPLAY   *P1:13,*EL,"Include inactive items (Y/N)? : ";
.
INACTV10  KEYIN     *P33:13,*DV,UNDLN1,*P33:13,*V2LON,ACTVFLAG;
.
          REP       "yYnN",ACTVFLAG
          MATCH     "Y",ACTVFLAG
          GOTO      INACTV99 IF EQUAL       * Yes 
.
          MATCH     "N",ACTVFLAG
          GOTO      INACTV99 IF EQUAL       * No  
.
          MATCH     "/",ACTVFLAG
          GOTO      INACTV98 IF EQUAL       * Exit
.
          MOVE      SP10,ACTVFLAG
          BEEP
          GOTO      INACTV10
.
INACTV98  MOVE      ONE,EXIT
INACTV99  RETURN
.
.******************************************************************************
.*                                 LITM0000               Called by: ML4000   *
.*                             Load from Item File                            *
.******************************************************************************
LITM0000  PACK      KEY9,SP10
          CALL      RDSITEM1 
LITM1000  CALL      RDKITEM1        * read next item record
          BRANCH    OVRCD,LDRG9999  * end of file ?
.
          MATCH     "N",ACTVFLAG                * do not include inactive codes
          IF        @EQUAL
            MATCH     SP1,PTITACTV
            GOTO      LITM1000 IF NOT EQUAL     * skip inactive code
          ENDIF
.
          PACK      PTCACODE,ITEMNO,SP10        * move item no. to casemix code
          UNPACK    IDESC,PTCADES1,PTCADES2     * move item desc. to des1 + des2
          PACK      PTCADES1,PTCADES1,SP70
          PACK      PTCADES2,PTCADES2,SP70
          MOVE      ZERO,PTCAELOS               * move zero to expected LOS
          MOVE      ONE,PTCAACTV                * move yes to active
.
          DISPLAY   *P2:24,"Reading file : ",PTCACODE
.
          PACK      KEY9,PTCACODE
          CALL      RDPTCMC1
          BRANCH    OVRCD,LITM2000               * does file already exist ?
.
          GOTO      LITM1000                     * file does exist
.
LITM2000  DISPLAY   *P50:24,"Writing record : ",PTCACODE
          CALL      WRPTCMC1                     * file doesnt exist
          PROC      PATKCMUP
          GOTO      LITM1000                     * read the next record
.
LITM9999  RETURN
+
.
.==============================================================================
.
          INC       STD001IO
          INC       STDHLPCD
.
          INC       PATKCMPR
          INC       PATCMCIO/INC
          INC       PATKCMIO/INC
          INC       PATCMCKY                   * casemix codes keyin routine
          INC       PATDGWIO/INC
          INC       PATITMIO/INC
+
.


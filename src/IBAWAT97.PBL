. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBAWAT97                         *
. *                                                        *
. *     FUNCTION:         EMERGENCY FLAG CLEAR             *
. *                                                        *
. *     DATE WRITTEN:     24/02/88                         *
. *                                                        *
. *     AUTHOR :          MALCOLM HAYSE   (I.B.A)          *
. *                                                        *
. *     MODIFICATIONS:                                     *
. *                  V12.00.01  23/05/2025 Bella Turco     *
. *                   Alphanumeric changes                 *
. *                   V5.07.B01 12/01/1999 N Harrington    *
. *                   507 rebounds error 26 and fixed read *
. *                   on bokstaaf to include century       *
. **********************************************************
. *                       V5.01 28/03/89 Graeme Wiliams    *
. *                             Changed BOKMASFD           *
. *                             SRF 5643                   *
. *                       V5.02 05/06/89 M.Ohleiter        *
. *                             Changed BOKMASFD           *
. *                    V5.01.01 28/06/89 M.Ohleiter        *
. *                             Updated for release 5.01   *
. *                    V5.01.02 12/12/89 K.Peak            *
. *                             Recompiled for 5th index in*
. *                             BOKMASFD SRF 103377        *
. *                                                        *
. *       V5.01.03  24/01/91  Justin Coates                *
. *                 Changed to use BOKRC1FD (not BOKMASFD) *
. *                                                        *
. **********************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
.
          INC       BOKRC1FD/INC
          INC       BOKSTAFD/INC
.
.        EXTRA   VARIABLES
.
.
PPORT     FORM      2
.
CNOPORT   FORM      2
DIM6      DIM       6
.
FILETYP   DIM       6
.
INPFILE   FILE      ASCII, FIXED=256
.
SAVRECT   DIM       8
SEC       FORM      6
LASTSEC   FORM      6
.
SECTPOST  FORM      3       *** SECTOR POSTION FOR PICK FILE INDICATOR
.
CODEBK    INIT      "BK"
VERSION   INIT      "V12.01.00"
PRGID     INIT      "IBAWAT97"
PRGNAM    INIT      "Emergency Flag Clear"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,"bokstaaf";
          OPEN      BOKBSTA1,"bokstaaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
START     HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Booking Master File":
                    *P2:6,*V2LON,TWO,*HOFF," = Booking Statistics"
.
REKSTRT1  KEYIN     *P1:8,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF KMAST,KBSTA
.
          BEEP
          GOTO      REKSTRT1
.
.             MASTER BOOKING FILE
.
KMAST     DISPLAY   *P50:2,*V2LON,"- Booking Groups "
.
          MOVE      ZEROVISN,BKREBOOK
          KEYIN     *P1:4,"Booking Number : ",*EF,*V2LON,BKREBOOK
          MATCH     ZEROVISN,BKREBOOK
          GOTO      START IF EQUAL
.
          MOVE      BKREBOOK,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD OF NOMASTS
.
          MATCH     SP2,BKRELOCK
          GOTO      NOTBUSA IF EQUAL
.
          CALL      UPBKREC1
          DISPLAY   *P30:4,*V2LON,"*** CLEARED ***",*W;
          GOTO      KMAST
.
NOMASTS   DISPLAY   *P30:4,*B,*V2LON:
                    "*** Booking does not Exist ***",*W;
          GOTO      KMAST
.
NOTBUSA   DISPLAY   *P30:4,*B,*V2LON:
                    "*** Booking is not Busy, NOT Cleared ***",*W;
          GOTO      KMAST
.
.             Booking Type
.
KBSTA     DISPLAY   *P50:2,*V2LON,"- Booking Statistics "
.
          MOVE      SP5,BSTYPE
          KEYIN     *P1:4,"Booking Type   : ",*EF,*V2LON,BSTYPE
          RESET     BSTYPE
          GOTO      START IF EOS
.
          ENDSET    BSTYPE
          APPEND    SP5,BSTYPE
          RESET     BSTYPE
.
          PACK      KEY5 WITH CODEBK,BSTYPE
          CALL      RDCODE1
          BRANCH    OVRCD OF INVBSTY
.
          DISPLAY   *P30:4,*V2LON,TDESC
.
          DISPLAY   *P1:6,*EL,"Date (MM/CCYY) : ",UNDLN2,SLASH,UNDLN2
.
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      CCC,CCENT
.
          MOVE      TEN8,CCOL
          MOVE      SIX,CVERT
.
          CALL      KEYMONTH
          BRANCH    OVRCD OF KBSTA
.
          PACK      BSDATE WITH CCENT,CYEAR,CMON
          REP       " 0",BSDATE
.
          PACK      KEY9 WITH BSTYPE,BSDATE
          CALL      RDBSTA1
          BRANCH    OVRCD OF INVBSTA
.
          MATCH     SP2,BSLOCK
          GOTO      NOTBUSB IF EQUAL
.
          CALL      UUBKBST1
          DISPLAY   *P30:6,*V2LON,"*** CLEARED ***",*W;
          GOTO      KBSTA
.
INVBSTY   DISPLAY   *P30:4,*B,*V2LON:
                    "*** Booking Type does not Exist ***",*W;
          GOTO      KBSTA
.
INVBSTA   DISPLAY   *P30:4,*B,*V2LON:
                    "*** Booking Statistic does not Exist ***",*W;
          GOTO      KBSTA
.
NOTBUSB   DISPLAY   *P30:4,*B,*V2LON:
                    "*** Booking Statistic is not Busy, NOT Cleared ***",*W;
          GOTO      KBSTA
+
.
.         I/O includes needed
.
          INCLUDE   STD001IO
          INC       PATCODIO/INC
          INC       BOKRC1IO/INC
          INC       BOKSTAIO/INC
.
.
ENDIT     CHAIN     PGM
          STOP

.*******************************************************************************
.*                                                                             *
.*                      P R O G R A M   S U M M A R Y                          *
.*                      -------------   -------------                          *
.*                                                                             *
.*     PROGRAM NAME  : PATSCH88                                                *
.*                                                                             *
.*     FUNCTION      : Inpatient Discharge Fax Scheduler                       *
.*                                                                             *
.*     DATE CREATED  : 13/08/2015                                              *
.*                                                                             *
.*     MODIFICATIONS :                                                         *
.*         V10.07.01   15/02/2016  Don Nguyen    CAR 308621                    *
.*                     Moved read/write of PTCNIDFT to Sector 120              *
.*         V10.06.00   13/08/2015  Don Nguyen    CAR 308621                    *
.*                     Created new (based off EMRSCH88)                        *
.*******************************************************************************

          INC       STD001FD
          INC       EMRCONFD/INC
          INC       EMRCONTD/INC
          INC       IBALPCFD/INC       * Labels & Forms Printing Control
          INC       PATCONFD/INC
          INC       WEBSCHFD/INC
          INC       WEBSECFD/INC
.
.
KEYIPURN  DIM       8                  * Keyin UR No.
KEYIPADM  DIM       8                  * Keyin Admission No.
KEYPRNTR  DIM       6                  * Keyin Printer Code
UNIQF5    FORM      5
USERID    DIM       10
.
.
PRGID     INIT      "PATSCH88"
PRGNAM    INIT      "Inpatient Discharge Fax Scheduler"
VERSION   INIT      "V12.01.00"

.*******************************************************************************
.                   MAINLINE                                                   *
.                   Main Controlling Logic                                     *
.*******************************************************************************
ML0000    CALL      INIT0000                     * Init and open files
ML1000    CALL      OPTN0000                     * Select options
          BRANCH    OPTION,ML2000,ML2000
          GOTO      ML9999
.
ML2000    CALL      KEYA0000                     * Keyin Admission No. 
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                      * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML5000,ML9999,ML1000
.                         Yes    No     Cancel
.
ML5000    CALL      PROC0000
          BRANCH    EXIT,ML1000
.
ML9999    CHAIN     PGM
          STOP
+
.*******************************************************************************
.                   INIT0000                                  Called by ML0000 *
.                   Initialization routine                                     *
.*******************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening ibalpcaf";
          OPEN      IBALPCA1,"ibalpcaf"
.
          DISPLAY   *P64:24,"webschaf";
          OPEN      WEBSCHA1,"webschaf"
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
.         READ      CONTROLF,EIGHTY9;*241,EMCNEDFD,*244,EMCNEDFP
          READ      CONTROLF,HUND20;*175,PTCNIDFT
.
INIT9999  RETURN
.
.*******************************************************************************
.*                                OPTN0000             Called by: ML0000       *
.*                        get user options or exit                             *
.*                                                                             *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                              *
.*                        TRUE  (1)  Exit option selected                      *
.*******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Schedule Inpatient Discharge Fax"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION                  * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    OPTION,OPTN9000              * Schedule Fax
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*******************************************************************************
.                   KEYA0000                                  Called by ML2000 *
.                   Keyin Admission Number                                     *
.*******************************************************************************
KEYA0000  KEYIN     *P1:24,*EL,"Keyin Admission Numnber: ",*V2LON,KEYIPADM;
          KEYIN     *P1:24,*EL,"Keyin UR Numnber: ",*V2LON,KEYIPURN;
          KEYIN     *P1:24,*EL,"Keyin User ID: ",*V2LON,USERID;
          KEYIN     *P1:24,*EL,"Printer Code: ",*V2LON,KEYPRNTR;
.
          RJUSTIFY  KEYIPADM
          RJUSTIFY  KEYIPURN
.
KEYA9999  RETURN
.
.*******************************************************************************
.                   PROC0000                                 Called by ML50000 *
.                   Create New ibalpcaf job                                    *
.*******************************************************************************
PROC0000  MOVE      ZERO,UNIQF5             * Find next job number
.
          PACK      KEY5,Z70
          CALL      RSIBLPC1
          CALL      RPIBLPC1
          BRANCH    OVRCD,PROC1000
.
          MOVE      IBLPSCHE,UNIQF5
.
PROC1000  ADD       ONE,UNIQF5
          MOVE      UNIQF5,IBLPSCHE
.
          MOVE      SP70,IBLPPRNT
          MOVE      ONE,IBLPCOPY
          MOVE      PTCNIDFT,IBLPSTAT       * Stationery Code
.         MOVE      TWO,EMLPREP             * Report 1=Label, 2=ID Sheets
.         MOVE      SP70,EMLPLTY            * Label Type
.         MOVE      EMCNEDFP,IBLPPRNT       * Printer Code
          MATCH     SP70,KEYPRNTR
          IF        !@EQUAL
            MOVE      KEYPRNTR,IBLPPRNT
          ENDIF
          MOVE      KEYIPURN,IBLPURNO
          MOVE      KEYIPADM,IBLPVISN
.
          PACK      KEY10,USERID,SP70       * Read to get userID passcode
          CALL      RDWBSE1
          BRANCH    OVRCD,PROC8000
.
          MOVE      WBSEUID,IBLPRUID
          PACK      IBLPRDAT,CCC,CYY,CMM,CDD
          REP       " 0",IBLPRDAT
          PACK      IBLPRTIM,CTIMEIS
          REP       " 0",IBLPRTIM
.         MOVE      WBSEPCD,EMLPPSC         * Passcode
          MOVE      SP70,IBLPSPAR
. 
PROC5000  PACK      KEY5,IBLPSCHE,SP70
          CALL      RAIBLPC1
          IF          OVRCD = 1
            CALL      WRIBLPC1
          ELSE
            ADD       ONE,UNIQF5
            MOVE      UNIQF5,IBLPSCHE
            GOTO      PROC5000
          ENDIF     
.
          DISPLAY   *P1:23,*B,*EF,"Inpatient Discharge Fax Scheduled! "
          CALL      HITENTER
          GOTO      PROC9000
.
PROC8000  DISPLAY   *P1:23,*B,*EF,"Invalid UserID: ",*V2LON,USERID,*HOFF
.
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      PROC9999
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
.
.-------------------------------------------------------------------------------

          INC       STD001IO
          INC       IBALPCIO/INC
          INC       WEBSCHIO/INC
          INC       WEBSECIO/INC

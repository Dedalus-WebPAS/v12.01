.***************************************************************************  
.* System    :   THEATRE                                                   *
.* Program   :   IBAOPR11                                                  *
.* Desc      :   Operating Room Available Maintenance                      *
.***************************************************************************
.* Date      :   19/10/90                                                  *
.* Author    :   J. Ceniza                                                 *
.* Function  :   This program is used to maintain the exceptions to the    *
.*               normal operating room available times (stored on the      * 
.*               OPROPRFD file ).                                          *
.* Mods      :                                                             *
.*        V10.03.01 18/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V5.07.01  25/02/1999    Jill Habasque                            *
.*                  Added test for active/inactive theatre codes           *
.*        V5.00     18/12/1990    Justin Coates                            *
.*                  Converted to Version 5                                 *
.*        V5.01.01  21/01/1991    ROD                                      *
.*                  Converted to Version 5.01                              *
.***************************************************************************
.
.$$$$$
.  OPERATING ROOM AVAILABLE MAINTENANCE
.  ------------------------------------
.
.   - Initialization
.
.        - Display Header
.        - Open Files
.          OPROPRA1
.          OPROPRA2
.          OPRORAA1
.          CONTROLF
.
.   - Processing
.         
.         - display screen format
.         - enter theatre code ( ? available, displaying OPROPRDS records )
.         - if no input, chain back to main program
.         - for valid theatre code, input a session date
.         - if no input for session date, re-keyin theatre code
.         - for given theatre code and session date, the OPRORAFD file
.           is searched if there is a record. If so the time available
.           is displayed ( time avail. from the OPRORAFD file ). 
.         - If no record is found in the OPRORAFD file for the theatre code
.           and session date, the time avail. is taken from the OPROPRFD 
.           file by converting the session date to the day of the week
.           thus determining the time avail. for the day given a theatre code.
.
.         - Process option.
.            - If user selects 1, the default is always the time avail. from
.              the OPROPRFD file.
.            - If post is selected, there are 4 cases to consider
.
.                If there is currently no record in the OPRORAFD file, and the
.                time is the same as the appropriate time in the OPROPRFD file
.                nothing is done.
.
.                If there is currently no record in the OPRORAFD file, and the 
.                time is different to the appropriate time in the OPROPRFD file
.                write a new record to the OPRORAFD file.
.
.                If there is currently a record in the OPRORAFD file, and the
.                time is the same as the appropriate time in the OPROPRFD file
.                delete the current record in the OPRORAFD file.
.
.                If there is currently a record in the OPRORAFD file, and the 
.                time is different to the appropriate time in the OPROPRFD file
.                Update the record in the OPRORAFD file.
.
.
.  - END  
.
.$$$$$
.
.
          INC       STD001FD
          INC       OPROPRFD/INC
          INC       OPRORAFD/INC
          INC       OPRCONFD/INC
.
AVAITIME  FORM      4
DIMTIME   DIM       4
DIM30     DIM       30
DIM4      DIM       4
DOPRAVAI  DIM       4  
ORAFLAG   FORM      1
OPRFLAG   FORM      1
SESNDATE  DIM       8
THTRCODE  DIM       6
TIMEAVAI  FORM      4
.
.  -- CONSTANTS ---
.
TEN440    FORM      "1440"
.
PRGID     INIT      "IBAOPR11"
PRGNAM    DIM       50        * actual name set up in INIT
PRGNAM1   INIT      " AVAILABLE MAINTENANCE"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
ML0000    CALL      INIT0000               * initialization and open files
.
. ---- enter theatre code ----
.
ML1000    CALL      SCR0000                * display screen format
          CALL      KCOD0000               * input theatre code
          BRANCH    EXIT,ML9999            * no input or EXITCHAR inputted
ML1150    CALL      KDAT0000               * keyin session date
          BRANCH    EXIT,ML1000            * nothing inputted
          CALL      CONV0000               * calc. day of week for date entered
.                                          * and get time from OPROPRFD file
          CALL      OPRA0000               * check OPRORAFD file to see if any
.                                          * record for date and code. Returns
.                                          * ORAFLAG (1) if no record found
          CALL      DISP0000               * display time avail. (from *ORAFD if
.                                          * record found there else from *OPRFD
ML1200    CALL      SEL0000                * select field to modify
          BRANCH    EXIT,ML2000            * (C)ancel selected
          BRANCH    ORAFLAG,ML1500         * no current record in OPRORAFD file
.
. Post selected ( with current record in OPRORAFD )
.
          CALL      PST20000               * update or delete record in OPRORAFD
.                                          * depending on value of time avail.
          GOTO      ML2000
.
. Post selected ( with no current record in OPRORAFD )
.
ML1500    CALL      PST10000               * post depending on value of 
.
. Redisplay screen 
.
ML2000    CALL      SCR0000                * re-display screen 
          CALL      DKEY0000               * display code and description
          GOTO      ML1150
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.*  Any other general initialization can also be performed here.            *
.****************************************************************************
INIT0000  RESET     PRGNAM
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,FORTY;*60,OPCNAUDR,*81,OPCNODSC
.
.         remove blanks from op room desc
.
          ENDSET    OPCNODSC
.
INIT0200  MATCH     SP1,OPCNODSC
          GOTO      INIT0500 IF NOT EQUAL
.
          BUMP      OPCNODSC,-1
          GOTO      INIT0200 IF NOT EOS
.
INIT0500  LENSET    OPCNODSC
          RESET     OPCNODSC
          PACK      PRGNAM,OPCNODSC,PRGNAM1
          RESET     PRGNAM
          REP       UPPLOW,PRGNAM
          CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,"oproraaf";
          OPEN      OPRORAA1,"oproraaf"
          DISPLAY   *P56:24,"opropraf";
          OPEN      OPROPRA1,"opropraf"
          OPEN      OPROPRA2,"opropraf"
          DISPLAY   *P56:24,"controlf";
.
          MOVE      ONE,CNEWDS
          BRANCH    OPCNAUDR,INIT9999       * not using audits
          DISPLAY   *P56:24,"opraudra";
          OPEN      OPRAUDRA,"opraudra"
.
INIT9999  RETURN
.
.**************************************************************************
.*                                  SCR0000            Called by: ML0000  *
.*                         display screen format                          *
.**************************************************************************
SCR0000   DISPLAY   *P1:3,*EF:
                    *P5:4,OPCNODSC," : ":
                    *P5:6,"Session Date    :":
                    *P2:8,*V2LON,ONE,*HOFF,". Time Available  : "
          DISPLAY   *P28:8,"minutes"
.
SCR9999   RETURN
.
.**************************************************************************
.*                        CLR0000                    Called by ML0000     *
.*                                                             KCOD000    *
.*                clear file variables                                    *
.**************************************************************************
CLR0000   MOVE      SP6,OPRMROOM
          PACK      OPRMDESC,SP30,SP10
CLR9999   RETURN
.
.**************************************************************************
.*                         KCOD0000                  Called by : ML0000   *
.*               keyin the theatre code                                   *
.**************************************************************************
KCOD0000  CALL      CLR0000                    * clear variables 
          KEYIN     *P23:4,*EL,*DV,UNDLN6:       
                    *P23:4,*V2LON,OPRMROOM:
                    *P23:4,*DV,OPRMROOM
.
          CALL      CHEK0000                     * validate user input
          BRANCH    CEXIT,KCOD9000,KCOD1000,KCOD0000
.
          DISPLAY   *P31:4,OPRMDESC
          GOTO      KCOD8000                     * valid input
.
.         list valid theatre codes 
.
KCOD1000  CALL      OPROPRDS                  
.
KCOD3000  DISPLAY   *P1:24,*EL,"Code : "
          KEYIN     *P8:24,*DV,UNDLN6:
                    *P8:24,*V2LON,OPRMROOM:
                    *P8:24,*DV,OPRMROOM
.
          CALL      CHEK0000
          BRANCH    CEXIT,KCOD9000,KCOD1000,KCOD3000 
          CALL      SCR0000                      * redisplay screen
          CALL      DKEY0000               * display code and description
.
KCOD8000  MOVE      FALSE,EXIT                   * valid input
          GOTO      KCOD9999
.
KCOD9000  MOVE      TRUE,EXIT                    * no input
.
KCOD9999  RETURN
+
.***************************************************************************
.*                        DKEY0000                     Called by : KCOD0000*
.*                                                               : DISP0000*
.*               display details from the key                              *
.***************************************************************************
DKEY0000  DISPLAY   *P23:4,*V2LON,OPRMROOM,SP2,*HOFF,OPRMDESC
.
DKEY9999  RETURN
.
.***************************************************************************
.*                        DISP0000                                         *
.*              display record detail ( in this case time )                *
.*              whether from OPROPRFD or OPRORAFD (depends)                *
.***************************************************************************
DISP0000  DISPLAY   *P23:8,*V2LON,DIMTIME
DISP9999  RETURN
+
.***************************************************************************
.*                                  CHEK0000           Called by: KCOD0000 *
.*                     Validate theatre id. code field                     *
.*                                                                         *
.*   Returns  : CEXIT    =  (0)  If an operator code is entered            * 
.*                         (1)  If there is nothing or exitchar entered    *
.*                         (2)  If a question mark "?" is entered          *
.*                         (3) invalid input                               *
.***************************************************************************
CHEK0000 
          ENDSET    OPRMROOM                 * Pad the field with spaces
          APPEND    SP6,OPRMROOM
          RESET     OPRMROOM
.
.         Check if anything was input and set the exit flag appropriately
.
          MATCH     OPRMROOM,SP6             * Was anything input ?
          GOTO      CHEK9000 IF EQUAL        * No
.
.         Check for a "?". 
.
          CMATCH    QUEST,OPRMROOM           * "?" entered ?
          GOTO      CHEK8000 IF EQUAL        * Yes.
.
          CMATCH    OPRMROOM,EXITCHAR        * "\" entered ?
          GOTO      CHEK9000 IF EQUAL        * Yes.
.
CHEK7100  MOVE      OPRMROOM,KEY6
          PACK      OPRMDESC,SP30,SP10
          CALL      RDOPOPR1   
          BRANCH    OVRCD,CHEK7500                    * invalid not found
.
          COMPARE   ONE,OPRMSTAT
          IF        @EQUAL 
            DISPLAY   *P1:24,*EL,*V2LON,"Inactive Code ",*HOFF;
            CALL      HITENTER
            MOVE      THREE,CEXIT
            GOTO      CHEK9999
          ENDIF
.
CHEK7400  MOVE      ZERO,CEXIT                        * valid input
          GOTO      CHEK9999
.
CHEK7500  MOVE      THREE,CEXIT                       * invalid input
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Code.  ";
          CALL      HITENTER
          GOTO      CHEK9999

CHEK8000  MOVE      TWO,CEXIT                * A "?" input
          GOTO      CHEK9999
.
CHEK9000  MOVE      ONE,CEXIT                * "\" was input
.
CHEK9999  RETURN
+
.*****************************************************************************
.*                            KDAT0000                   Called by : ML0000  *
.*              keyin the session date                                       *
.*****************************************************************************
KDAT0000  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      "19",CCENT
          MOVE      ZERO,CCANLDTE
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          MOVE      TWENTY3,CCOL
          MOVE      SIX,CVERT
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT0000          * "?" input not allowed
          BRANCH    OVRCD,KDAT9000           * no input
          PACK      SESNDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      FALSE,EXIT
          GOTO      KDAT9999
KDAT9000  MOVE      TRUE,EXIT
KDAT9999  RETURN
+
.**************************************************************************
.*                         OPRA0000                Called by : ML0000     *
.*          for given theatre code and date, check if any record in       *
.*          OPRORAFD file.                                                *
.*          RETURNS ORAFLAG - 1  - if no record in file                   *
.*                            0 - if record found in file                 *
.**************************************************************************
OPRA0000  PACK      KEY14,SESNDATE,OPRMROOM
          CALL      RDOPORA1                    * read OPRORAFD file
          BRANCH    OVRCD,OPRA9000              * no record found
          MOVE      OPRAVAIL,TIMEAVAI
          MOVE      OPRAVAIL,DIMTIME
          MOVE      ZERO,ORAFLAG                * record found in OPRAFLAG
          GOTO      OPRA9999
.
OPRA9000  MOVE      ONE,ORAFLAG                 * no current record in 
.                                               * OPRORAFD file
OPRA9999  RETURN
.
.**************************************************************************
.*                       CONV0000                  Called by : ML0000     *
.*               convert date of the week for the entered date            *
.**************************************************************************
CONV0000  UNPACK    SESNDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,OPRFLAG           * initialize flag 
          CALL      DATECONV
          MOVE      OPRMROOM,KEY6
          CALL      RDOPOPR1               * read OPROPRFD file
          BRANCH    WEEKDAY,CONV1000,CONV2000,CONV3000,CONV4000,CONV5000:
                            CONV6000,CONV7000
.
.  ( WEEKDAY  1 - Monday,  7 - Sunday )
.
CONV1000  MOVE      OPRMMON,AVAITIME
          MOVE      AVAITIME,DIMTIME
          GOTO      CONV9999
.
CONV2000  MOVE      OPRMTUE,AVAITIME
          MOVE      AVAITIME,DIMTIME
          GOTO      CONV9999
.
CONV3000  MOVE      OPRMWED,AVAITIME
          MOVE      AVAITIME,DIMTIME
          GOTO      CONV9999
.
CONV4000  MOVE      OPRMTHU,AVAITIME
          MOVE      AVAITIME,DIMTIME
          GOTO      CONV9999
.
CONV5000  MOVE      OPRMFRI,AVAITIME
          MOVE      AVAITIME,DIMTIME
          GOTO      CONV9999
.
CONV6000  MOVE      OPRMSAT,AVAITIME
          MOVE      AVAITIME,DIMTIME
          GOTO      CONV9999
.
CONV7000  MOVE      OPRMSUN,AVAITIME
          MOVE      AVAITIME,DIMTIME
CONV9999  RETURN
.
.****************************************************************************
.*                        KTIM0000                  Called by : SEL0000     *
.*               keyin the time                                             *
.****************************************************************************
KTIM0000  KEYIN     *P23:8,*DV,*V2LON,AVAITIME:  * display default time
.                                                * from OPROPRFD file
                    *P23:8,*V2LON,*JR,DOPRAVAI:
                    *P23:8,*DV,*JR,DOPRAVAI
.
          RESET     DOPRAVAI
          GOTO      KTIM8000 IF EOS             * default accepted
.
          TYPE      DOPRAVAI                    * numeric input
          GOTO      KTIM1000 IF NOT EQUAL       * no
.
          MOVE      DOPRAVAI,TIMEAVAI           * move to form field
.
          COMPARE   ZERO,TIMEAVAI               * -ve input
          GOTO      KTIM1000 IF LESS            * yes
.
          COMPARE   TIMEAVAI,TEN440             * <= 1440
          GOTO      KTIM7000 IF NOT LESS        * yes
.
KTIM1000  BEEP
          GOTO      KTIM0000
.
KTIM7000  COMPARE   TIMEAVAI,AVAITIME              * default time changed ?
          GOTO      KTIM9000 IF NOT EQUAL          * yes
.
KTIM8000  MOVE      ZERO,OPRFLAG              * default OPROPRFD time accepted
          MOVE      AVAITIME,TIMEAVAI
          DISPLAY   *P23:8,*V2LON,TIMEAVAI
          GOTO      KTIM9999
.
KTIM9000  MOVE      ONE,OPRFLAG                    * OPROPRFD time changed
KTIM9999  RETURN
.
.****************************************************************************
.*                                   SEL0000           Called by: ML0000    *
.*                         select field to modify or post                   *
.*                                                                          *
.*    Returns:  EXIT     = FALSE  Post was selected.                        *
.*                         TRUE  Cancel was selected or EXITCHAR            *
.****************************************************************************
SEL0000   CALL      MAINQST                  * Ask for an item, post or cancel
.
          COMPARE   ZERO,CCITEM              * What was entered ?
          GOTO      SEL8000 IF EQUAL         * Post was selected
          GOTO      SEL9000 IF LESS          * Cancel was selected
.
          BRANCH    CCITEM,SEL1000
          BEEP
          GOTO      SEL0000
.
SEL1000   CALL      KTIM0000                 * keyin time
          GOTO      SEL0000
.
SEL8000   MOVE      FALSE,EXIT               * Post was selected
          GOTO      SEL9999
.
.         Cancel was selected 
.
SEL9000   MOVE      TRUE,EXIT           
.
SEL9999   RETURN
+
.**************************************************************************
.*                        PST10000                  Called by : ML0000    *
.*          there is currently no record in the OPRORAFD file.            *
.*          Post if time has been changed compared to OPROPRFD record.    *
.**************************************************************************
PST10000  BRANCH    OPRFLAG,PST11000         * new time input
.                              
.  time is the same as the appropriate time in OPROPRFD file ( do nothing )
.
          GOTO      PST19999
.
. --- time is different write new record to OPRORAFD file --
.
PST11000  PACK      KEY14,SESNDATE,OPRMROOM
          MOVE      TIMEAVAI,OPRAVAIL
          CALL      WROPORA1                * write new record to OPRORAFD 
          DISPLAY   *P50:24,*EL,*V2LON,"** Posting ** "
.
          BRANCH    OPCNAUDR,PST19999 
          CALL      IBACLOCK
          MOVE      "A",OPRAAUDR
          CALL      WRTA0000 
.
PST19999  RETURN
+
.**************************************************************************
.*                        PST20000                  Called by : ML0000    *
.*          there is currently a record in the OPRORAFD file.             *
.**************************************************************************
PST20000  DISPLAY   *P50:24,*EL,*V2LON,"** Posting ** "
          COMPARE   TIMEAVAI,AVAITIME       * compare time in OPRORAFD to
.                                           * OPROPRFD file 
          GOTO      PST27000 IF NOT EQUAL   * not the same
.
. -- time in OPROPRFD is the same as in OPRORAFD 
.
.    check whether time has been changed
.
          PACK      KEY14,SESNDATE,OPRMROOM
          CALL      DEOPORA1                * delete current record in OPRORAFD 
          BRANCH    OPCNAUDR,PST29999
          CALL      IBACLOCK
          MOVE      "D",OPRAAUDR
          CALL      WRTA0000
          GOTO      PST29999
.
PST27000  BRANCH    OPCNAUDR,PST27800
          CALL      IBACLOCK
          MOVE      "B",OPRAAUDR
          CALL      WRTA0000
.
PST27800  PACK      KEY14,SESNDATE,OPRMROOM
          CALL      RDOPORA1                * position pointer
          MOVE      TIMEAVAI,OPRAVAIL
          CALL      UPOPORA1                * update current record in OPRORAFD 
.
          BRANCH    OPCNAUDR,PST29999
          MOVE      "C",OPRAAUDR
          CALL      WRTA0000
.
PST29999  RETURN

.**************************************************************************
.*                            WRTA0000                 Called by: MANY    *
.*                          write audits                                  *
.**************************************************************************
+
WRTA0000  MOVE      CTIMEIS,OPRAAUDT
          PACK      OPRAAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OPRAAUDD
          MOVE      PORT,OPRAAUDP
          MOVE      PASSCODE,OPRAAUDO
          MOVE      ONE,OPRAAUDS
          PACK      KEY19,OPRAAUDD,OPRAAUDT,OPRAAUDP,OPRAAUDR
          CALL      AWOPORA
WRTA9999  RETURN
.
. =========================================================================
. Includes for all file I/O would be included here.
. Includes for all procedural routines would be included here.
. =========================================================================
.
          INC       STD001IO
          INC       OPROPRIO/INC
          INC       OPRORAIO/INC
          INC       OPROPRDS
          INC       DATECONV

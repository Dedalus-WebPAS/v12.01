.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*      PROGRAM NAME:     IBAPAT89                                            *
.*                                                                            *
.*      FUNCTION:         PATIENT STATUS REPORT                               *
.*                                                                            *
.*      DATE WRITTEN:     17/03/86                                            *
.*                                                                            *
.*      AUTHOR:           MALCOLM HAYSE                                       *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.******************************************************************************
.* V12.00.01 22/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*       V10.05.01  01/01/2015  Ania P        CAR 261630                      *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.03.01 27/06/2013 Ebon Clements  CAR 287511                      *
.*                  Fixed temp file layout and key for WEXTN length change    *
.*                  Change to only print 4 character ward extension           *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.04.01  08/11/2004  Lina Vo    CAR 54907                          *
.*                  Fixed Multi Hospital                                      *
.*        V9.02.01  26/09/2001  Dean Cramer                                   *
.*                  Change so headings correct when CGI for web               *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*        V5.07.03  18/10/1999  Steve Downing                                 *
.*                  Further Y2K mods                                          *
.*        V5.07.02  02/09/1999  Steve Downing                                 *
.*                  Mods to create & open temp file routine                   *
.*        V5.07.01  29/07/1999  Steve Downing                                 *
.*                  Y2K mods                                                  *
.*        V5.07.B02 28/01/1999  Jim Stathopoulos                              *
.*                 Report Modifications                                       *
.*                 09/11/1998  Glenn Berry      5.07 changes                  *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
. *                     V5.01.02  05/10/89 L. P.           SRF 102608         *
. *                               Fix key definition and file for tempfile.   *
. *                     V5.01.121 25/10/90 i chung         SRF 106789         *
. *                               Added total no. of patients at end of report*
. *                   V5.01.122 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
.*        V5.01.123 10.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Keyin hospital                                            *
.*        V5.01.124 25/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.125 20/07/1994  Rob Leonard                                   *
.*                  Fixed hospital keyin                                      * 
. *       V5.02.01  14/02/95 Paul Howells     SRF 134992                      *
. *                 Fixed start and end surname.                              *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
+
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
.
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATWR1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         IBACONFD Variable
.
IBCNMHOS  FORM      1
.
.
.         WORK AREA
.
AFIRST    DIM       12
DISFIRST  DIM       12
ALAST     DIM       12
DISLAST   DIM       12
.
CDESC     DIM       20
CHECK     DIM       2
CMATRN    FORM      1
CMDLINE   DIM       50
CMEDCAR   FORM      1           195      1 = USING MEDICARE ON IBAPAT89
CODEA     INIT      "A "
CURDATE   DIM       8
CURCCYR   FORM      4
CURDY     FORM      3
.
DCURCCYR  DIM       4
DJULCCYR  DIM       4
DIM6      DIM       6
DIM8      DIM       8
DIM18     DIM       18
DNAME12   DIM       12
DTYPE     DIM       15
.
EOYMON    DIM       2
EOYDAY    DIM       2
EOYYEAR   DIM       2
.
FNAMEI    DIM       8
FNAMER    DIM       8
FNAMEU    DIM       8
.
ERASE     INIT      "iserase "
FNAME     INIT      "pat89a"          * set up for temp file name
ISBUILD   INIT      "isbuild "
UKEY      DIM       30                * set up for record length & relevant key
WRDEXTN4  DIM       4                 * DIM 4 ward extension
.
JULCCYR   FORM      4
.
OPCND     FORM      1
.
PATCOUNT  FORM      7                     * added for V5.01.121 - patients count
PROM1     INIT      "Surname Sequence                    "
PROM2     INIT      "Surname Sequence with Attend Doctor "
PROM3     INIT      "Ward/Bed Sequence                   "
PROM4     INIT      "Ward/Bed Sequence with Attend Doctor"
PROMPT    DIM       36
PSTROL    FILE      ASCII, FIXED=256
.
SORT1     INIT      "u1-3,4-6,84-91,122-129"       * key22
SORT2     INIT      "u19-38,84-91,122-129"         * key36
SORTSTR   DIM       22
STAR2     INIT      "**"
STATUS    FORM      1
STMAST    DIM       2
.
TEMPFILE  IFILE     SQL, FIXED=130, KEY=SORTSTR 
.
UNIQUE    FORM      8
WRDBED    DIM       7
SP12      INIT      "            "
Z12       INIT      "zzzzzzzzzzzz"
START     INIT      "Start "
FINISH    INIT      "Finish"
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.AWARD          DIM        3          3             1
.ABED           DIM        3          3             4
.PURNO          DIM        8          8             7
.PTITL          DIM        4          4            15
.PSNAME         DIM       20         20            19
.PGNAME         DIM       25         25            39
.WEXTN          DIM       20         20            64
XDAADMNO        DIM        8          8            84
.WRDBED         DIM        7          7            92
.CHECK          DIM        2          2            99
.DTYPE          DIM       15         15           101
.ADOCTA         DIM        6          6           116
DUNIQUE         DIM        8          8           122
.
.End of Record                                    130
.
. ---------------------------------------------------
.
PRGID     INIT      "IBAPAT89"
PRGNAM    INIT      "Patient Status Report"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A2,"patwr1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
          READ      CONTROLF,TEN;*195,CMEDCAR
          READ      CONTROLF,TEN5;*188,CMATRN
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
.
            DISPLAY   *P1:5,*EF,"Hospital Id : ";
            MOVE      TEN5,ECOL
            MOVE      FIVE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND               * Not Mandatory
HOSKEY      CALL      PATHSPKY
            BRANCH    EXIT,HOSKEY20,ENDIT
            GOTO      HOSKEY40
.
HOSKEY20    MOVE       "All Hospitals",PTHSNAME
            MOVE       SP10,PTHSHOSP
.
HOSKEY40    DISPLAY   *P22:5,*EL,PTHSNAME,*W1;
          ENDIF
.
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
          MOVE      CDD,DD
          MOVE      CMM,MM
          MOVE      CYY,YY
          MOVE      CCC,CC
          CALL      DMYCON
          MOVE      JULDAY,CURDY
          PACK      DCURCCYR,JULCC,JULYR
          REP       " 0",DCURCCYR
          MOVE      DCURCCYR,CURCCYR
.
.         MEDICARE DISPLAY SHOULD BE OPTIONAL
.
          BRANCH    CMEDCAR OF USEMEDI
          MOVE      SP2,STAR2
USEMEDI
.
.         SELECT OPTION
.
START     MOVE      ZERO,CPAGENO
          MOVE      ZERO,PATCOUNT              * initialize patients count
          MOVE      ONE TO UNIQUE
.
          HLINE     *G37,2,51,80
          DISPLAY  *P28:2,*HOFF," Patient Status Report ": 
                   *P1:3,*EF:
                   *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:6,*V2LON,ONE,*HOFF," = ",PROM1:
                    *P1:7,*V2LON,TWO,*HOFF," = ",PROM2:
                  *P1:8,*V2LON,THREE,*HOFF," = ",PROM3:
                   *P1:9,*V2LON,FOUR,*HOFF," = ",PROM4
.
OPT1      KEYIN     *P1:11,"Select Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF SORT10,SORT10,SORT20,SORT20
          BEEP
          GOTO      OPT1
.
NOMAST    DISPLAY   *P1:24,*EL,*P34:24,*V2LON,"** No patients found **",*W2;
          GOTO      START
.
SORT10    MOVE      SORT2,SORTSTR            * surname sequence    key36
          GOTO      NAMENO
.
SORT20    MOVE      SORT1,SORTSTR            * ward/bed sequence   key22
          GOTO      NAMENO
.
.         Create an indexed file based on port for accumulating Admission Type
.         totals
.
.         Create a new temporary file
.
INDZD     MOVE      ZERO,STATUS
.
          CALL      KILLIDZ                     * remove existing file
.
          CLEAR     CMDLINE
          BRANCH    OPTION TO IDXSUR,IDXSUR
.
          MOVE      " 130 u1-3,4-6,84-91,122-129",UKEY
          GOTO      IDXNOW
.
IDXSUR    MOVE      " 130 u19-38,84-91,122-129",UKEY
.
IDXNOW    MOVE      ONE,STATUS
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,FNAMEI,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          MOVE      ZERO,OPCND
.
          OPEN      TEMPFILE,FNAMEI            * open temp index file
.
ENDC      RETURN
.
.         Deleting an indexed file based on port
.
KILLIDZ   CLOSE     TEMPFILE
.
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Deleting Temporary Index File **";
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,FNAMEI         * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
          RETURN
+
.
.         PRINT VIA SURNAME SORT KEY
.
NAMENO    LOAD      PROMPT USING OPTION OF PROM1,PROM2,PROM3,PROM4
.
          DISPLAY   *P43:2,*EL,*V2LON,"- ",PROMPT
          MOVE      SP20 TO AFIRST
          MOVE      Z12 TO ALAST
.
          KEYIN     *P1:13,*EF,"Starting Surname : ",*V2LON,AFIRST
.
          ENDSET    AFIRST
          APPEND    SP20,AFIRST
          RESET     AFIRST
.
          MOVE      AFIRST,DISFIRST
          MATCH     SP12,AFIRST
          IF        @EQUAL
            MOVE      START,DISFIRST
          ENDIF
.
          DISPLAY   *P20:13,*V2LON,*EL,DISFIRST
.
          KEYIN     *P1:15,"Ending Surname   : ",*V2LON,ALAST
.
          ENDSET    ALAST
          APPEND    SP20,ALAST
          RESET     ALAST
.
          MOVE      ALAST,DISLAST
          MATCH     SP12,ALAST
          IF        @EQUAL
            MOVE      Z12,ALAST
            MOVE      FINISH,DISLAST
          ENDIF
.
          MATCH     Z12,ALAST
          IF        @EQUAL
            MOVE      FINISH,DISLAST
          ENDIF
.
          DISPLAY   *P20:15,*V2LON,*EL,DISLAST
.
MASTA2    CALL      POSTQST
          BRANCH    CEXIT,INITC,NAMENO,START
.
          BEEP
          GOTO      MASTA2
.
.         INITIALISE FILE
.
INITC     DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialising Status File **",*P1:12;
.
          CALL      INDZD
          BRANCH    OPCND OF ENDIT
.
          MOVE      TWO,ASTAT
.
NEXTAS    PACK      KEY9 WITH ASTAT,SP10
          CALL      RDSMISS2
          MOVE      "60",CLNO
.
          DISPLAY   *P1:12,*EF,*P40:24,"Scanning Admission :";
.
NEXTALP   CALL      RDKMISS2
          BRANCH    OVRCD OF ENDW
.
          COMPARE   FIVE,ASTAT
          GOTO      ENDW IF EQUAL
.
          COMPARE   THREE,ASTAT
          GOTO      CNTALP IF NOT EQUAL
.
.         Check for two consecutive discharged admissions ( to allow for
.         index file corruption )
.
          CALL      RDKMISS2
          BRANCH    OVRCD OF ENDW
.
          COMPARE   THREE,ASTAT
          GOTO      CNTALP IF NOT EQUAL
.
.         PROCESS ON LEAVE PATIENTS
.
          MOVE      FOUR,ASTAT
          GOTO      NEXTAS
.
CNTALP    DISPLAY   *P61:24,*V2LON,AADMNO;
.
          IF        IBCNMHOS = 1
            MATCH     SP10,PTHSHOSP
            IF        !@EQUAL
              PACK      KEY6,AWARD,SP3
              CALL      RDWARD1
              BRANCH    OVRCD,NEXTALP
...              MATCH     PTWDHOSN,KEY3
              MATCH     PTWDHOSN,PTHSHOSP
              GOTO      NEXTALP IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NEXTALP
.
          MATCH     ALAST,PSNAME
          GOTO      CHKNAM2 IF EQUAL
          GOTO      NEXTALP IF NOT LESS
.
CHKNAM2   MATCH     AFIRST,PSNAME
          GOTO      CHKNAM3 IF EQUAL
          GOTO      NEXTALP IF LESS
.
CHKNAM3   PACK      WRDBED WITH AWARD,SLASH,ABED
          MATCH     SP3,AWARD
          GOTO      CONT1 IF NOT EQUAL
          MOVE      "STANDBY",WRDBED
.
CONT1     BRANCH    ASTAT OF NEXTALP,CURADM,NEXTALP,ONLEAVE
          GOTO      NEXTALP
.
ONLEAVE   MOVE      ZEROVISN,WADMNO
          PACK      KEY14 WITH AADMNO,SP6
          CALL      RDSWARD2
          CALL      RDKWARD2
          BRANCH    OVRCD OF NEXTALP
          MATCH     WADMNO,AADMNO
          GOTO      NEXTALP IF NOT EQUAL
          MOVE      "LEAVE  ",WRDBED
          MOVE      SP3,AWARD
          MOVE      SP3,ABED
.
CURADM    MOVE      SP20,DTYPE
          MATCH     SP3,ATYPE
          GOTO      DA4 IF EQUAL
.
          PACK      KEY5 WITH CODEA,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD OF DA4
          MOVE      TDESC,DTYPE
.
DA4       MOVE      SP2,CHECK
          MATCH     SP10,PMEDI
          GOTO      DA5 IF NOT EQUAL
          MOVE      STAR2,CHECK
.
          DAYSEP    ADATE,CURDATE,FORM4

          COMPARE   TWO,FORM4
          GOTO      DA5 IF NOT LESS
          MOVE      SP2,CHECK
.
DA5       MOVE      SP70,WEXTN
          PACK      KEY6 WITH AWARD,ABED
          CALL      RDWARD1
.
.
.         DIM6 DECIDES IF SURNAME OR WARD/BED SEQUENCE
.
          BRANCH    OPTION OF SURNSEQ,SURNSEQ,WARDSEQ,WARDSEQ
.
SURNSEQ   MOVE      UNIQUE,DUNIQUE
          MOVE      AADMNO,XDAADMNO
          PACK      KEY36 WITH PSNAME,XDAADMNO,DUNIQUE
          WRITE     TEMPFILE,KEY36;AWARD,ABED,PURNO,PTITL,PSNAME,PGNAME,WEXTN:
                                   XDAADMNO,WRDBED,CHECK,DTYPE,ADOCTA,DUNIQUE
          ADD       ONE,UNIQUE
          GOTO      NEXTALP
.
WARDSEQ   MOVE      AADMNO,XDAADMNO
          MOVE      UNIQUE,DUNIQUE
          PACK      KEY22 WITH AWARD,ABED,XDAADMNO,DUNIQUE
          WRITE     TEMPFILE,KEY22;AWARD,ABED,PURNO,PTITL,PSNAME,PGNAME,WEXTN:
                                   XDAADMNO,WRDBED,CHECK,DTYPE,ADOCTA,DUNIQUE
          ADD       ONE,UNIQUE
          GOTO      NEXTALP
+
.         OVER CONDITION FOUND..
.
ENDW      DISPLAY   *P10:24,*EL,"Generating :";
          READ      TEMPFILE,SP30;;
.
NEXTR     READKS    TEMPFILE;AWARD,ABED,PURNO,PTITL,PSNAME,PGNAME,WEXTN:
                             XDAADMNO,WRDBED,CHECK,DTYPE,ADOCTA,DUNIQUE
          GOTO      ENDP IF OVER
          MOVE      DUNIQUE,UNIQUE
          MOVE      XDAADMNO,AADMNO
.
          BRANCH    OPTION OF DSPSUR,DSPSUR
.
          DISPLAY   *P25:24,*V2LON,WRDBED;
          GOTO      DSPIT
.
DSPSUR    DISPLAY   *P25:24,*V2LON,PSNAME;
.
DSPIT     CALL      DISPDA
          GOTO      NEXTR
.
.         OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOMAST IF EQUAL
          CALL      PAGEND
.
          PRINT     *N,"  Total Number of Patients : ",PATCOUNT:
                    *N,*N,*N,"  ***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
          CALL      KILLIDZ
          GOTO      START
.
.         PRINT ACTUAL DATA 
.
DISPDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
          ADD       ONE,PATCOUNT               * increment patients count
.
          MOVE      PGNAME,DIM18
          BRANCH    OPTION OF PDATA1,PDATA2,PDATA3,PDATA4
.
PDATA1    MOVE      WEXTN,WRDEXTN4
          PRINT     *1,"| ",PSNAME," ",DIM18," ",PTITL:
                    *48,"| ",WRDBED,*57,CHECK:
                    *59,"| ",WRDEXTN4,*65,"|",AADMNO:
                    *74,"| ",DTYPE,*92,"|",*101,"|",*108,"|",*132,"|"
          ADD       ONE,CLNO
          RETURN
.
PDATA2   
          MOVE      ADOCTA,KEY6
          MOVE      SP20,DNAME12
          CALL      RDDOCT1
          BRANCH    OVRCD OF PDAT2
.
          MOVE      DGNAME,ANS
          PACK      DNAME12 WITH ANS,DOT,DSNAME
.
PDAT2     MOVE      WEXTN,WRDEXTN4
          PRINT     *1,"| ",PSNAME," ",DIM18," ",PTITL:
                    *48,"| ",WRDBED,*57,CHECK:
                    *59,"| ",WRDEXTN4,*65,"|",AADMNO:
                    *74,"| ",DTYPE,*92,"| ",DNAME12,*108,"|",*132,"|"
          ADD       ONE,CLNO
          RETURN
.
PDATA3    MOVE      WEXTN,WRDEXTN4
          PRINT     *1,"| ",WRDBED,*10,CHECK:
                    *12,"| ",WRDEXTN4,*18,"|",AADMNO:
                    *27,"| ",PSNAME," ",DIM18," ",PTITL:
                    *74,"| ",DTYPE,*92,"|",*101,"|",*108,"|",*132,"|"
          ADD       ONE,CLNO
          RETURN
.
PDATA4    MOVE      ADOCTA,KEY6
          MOVE      SP20,DNAME12
          CALL      RDDOCT1
          BRANCH    OVRCD OF PDAT4
.
          MOVE      DGNAME,ANS
          PACK      DNAME12 WITH ANS,DOT,DSNAME
.
PDAT4     MOVE      WEXTN,WRDEXTN4
          PRINT     *1,"| ",WRDBED,*10,CHECK:
                    *12,"| ",WRDEXTN4,*18,"|",AADMNO:
                    *27,"| ",PSNAME," ",DIM18," ",PTITL:
                    *74,"| ",DTYPE,*92,"| ",DNAME12,*106,"|",*132,"|"
          ADD       ONE,CLNO
          RETURN
+
.
.         LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*----------------------------------------------":
                    "-------------------------------------":
                    "-----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.         HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
.
          COMPARE   ONE,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          CLOCK     TIME,CTIMEIS
          PRINT     *F,PRGID,*43,CNAME,*116,"DATE : ":
                    CDATE:
                    *N,VERSION:
                    *43,"***  ",PRGNAM,"  ***  - ",PROMPT:
                    *116,"TIME :   ",CTIMEIS,*N:
                    *116,"PAGE : ",*130,CPAGENO,*N:
                    *43,"Starting Surname : ",DISFIRST,*N:
                    *43,"Ending Surname   : ",DISLAST,*N
.
          IF        IBCNMHOS = 1
            PRINT     *40,"Hospital : ",PTHSNAME,*N;
            MOVE      "11",CLNO
          ELSE
            MOVE      "9",CLNO
          ENDIF
.
          CALL      PAGEND
.
          BRANCH    OPTION OF HEAD1,HEAD2,HEAD3,HEAD4
.
HEAD1     PRINT     *1,"| Patient Name",*48,"| Ward/Bed":
                    *59,"| Extn",*65,"| Admno":
                    *74,"| Classification",*92,"| Baby":
                    *101,"| PKU",*108,"|",*116,"Comments",*132,"|"
          GOTO      ENDHEAD
.
HEAD2     PRINT     *1,"| Patient Name",*48,"| Ward/Bed":
                    *59,"| Extn",*65,"| Admno":
                    *74,"| Classification":
                    *92,"| Attend DR.",*108,"|",*116,"Comments",*132,"|"
          GOTO      ENDHEAD
.
HEAD3     PRINT     *1,"| Ward/Bed",*12,"| Extn":
                    *18,"| Admno",*27,"| Patient Name":
                    *74,"| Classification",*92,"| Baby":
                    *101,"| PKU",*108,"|",*116,"Comments",*132,"|"
          GOTO      ENDHEAD
.
HEAD4     PRINT     *1,"| Ward/Bed",*12,"| Extn":
                    *18,"| Admno",*27,"| Patient Name":
                    *74,"| Classification":
                    *92,"| Attend Doct",*106,"|",*115,"Comments",*132,"|"
.
ENDHEAD   CALL      PAGEND
.
          RETURN
+
.
ENDIT     CHAIN      PGM
          STOP
.
.==============================================================================
.
          INC       STD001IO
          INC       PATHSPKY
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATWR1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

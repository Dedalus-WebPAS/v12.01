.******************************************************************************
.* System    :   Patient Management System                                    *
.* Program   :   FXNHIRES                                                     *
.* Desc      :   Update PTYPE on patma1af if blank from nhimasaf residency    *
.******************************************************************************
.* Date      :   20/10/2005                                                   *
.* Author    :   Jill Habasque                                                *
.* Mods      :                                                                *
.******************************************************************************
+
          INC       STD001FD
.
          INC       NHIMASFD/INC            * NHI Master file
          INC       PATMA1FD/INC            * Master file
          INC       PATCODFD/INC            * Codes file
.
. LOCAL VARIABLES
. ---------------
CHARNUM   FORM      2
CODE      DIM       2
COUNT     FORM      6                       * Record counter
.
DIM20     DIM       20
.
FOUNDCHR  FORM      1                       * found character flag
.                                              0 = valid character not found
.                                              1 = valid character found
.
LENVAR    FORM      2
.
PTCNANMA  FORM      1
.
RESIDENT  DIM       3
NONRESID  DIM       3
UPDFLAG   FORM      1                       * update flag
.                                               0 = update record
.                                               1 = write new record
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
PRGNAM    INIT      "Convert PMI Data"
PRGID     INIT      "FXTAUNHI"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
.
ML0000    CALL      INIT0000                * Initialise variables & open files
.
          CALL      KEYR0000                * Key in the resident status
          BRANCH    EXIT,ML0000,ML0000
.
          CALL      OPTN0000                * Ask proceed question
          BRANCH    EXIT,ML9999
.
          CALL      PMST0000                * Process the master file
.
ML9999    CHAIN     PGM                     * Chain back to appropriate menu
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
.
INIT0000  CALL      DISPHEAD                     * Display screen header
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"            
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"            
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"            
.
          DISPLAY   *P64:24,"nhimasaf";
          OPEN      NHIMASA1,"nhimasaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                            Ask Proceed Question                            *
.******************************************************************************
.
OPTN0000  DISPLAY   *P1:24,*EL,*HOFF,"Ok to Proceed (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ";
.
OPTN1000  KEYIN     *P23:24,*V2LON,ANS
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KEYR0000              Called by: ML0000   *
.*                       Keyin resident/non resident values                   *
.******************************************************************************
.
KEYR0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:12,"Code for Resident "
          MOVE      "30",ECOL
          MOVE      "12",EVERT
          PACK      CODE,ANST,SP70
          MOVE      ONE,CKYIMAND
          CALL      PATCODKY                * Keyin the ID type
          BRANCH    EXIT,KEYR9999,KEYR9999
.
          MOVE      ACODE,RESIDENT
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,RESIDENT:
                    *PECOL:EVERT,*HOFF,TDESC;
.
          DISPLAY   *P1:13,"Code for Non-resident "
          MOVE      "30",ECOL
          MOVE      "13",EVERT
          PACK      CODE,ANST,SP70
          MOVE      ONE,CKYIMAND
          CALL      PATCODKY                * Keyin the ID type
          BRANCH    EXIT,KEYR9999,KEYR9999
.         
          MOVE      ACODE,NONRESID
          MOVE      TDESC,TDESC
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,NONRESID:         
                    *PECOL:EVERT,*HOFF,TDESC;
.
.
KEYR9999  RETURN
+
.******************************************************************************
.*                                  PMST0000              Called by: ML0000   *
.*                           Process The Master File                          *
.******************************************************************************
.
PMST0000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:22,*EL,"Started  ",CTIMEIS;
.
          DISPLAY   *P1:23,"Processing: ",*P50:23,"Record: ";
          MOVE      ZERO,COUNT                   * initialise count
          MOVE      ZERO,CNOUNDLN                * initialise print variables
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,CLNO
          CALL      IBACLOCK
.
.         Loop through the patma1af file
.
          MOVE      SP8,KEY8
          CALL      RDSMAST1                     * position at start of file
PMST1000  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,PMST9000               * eof - finished
.
          COMPARE   ZERO,PSTAT                   * valid U/R ?
          GOTO      PMST1000 IF NOT EQUAL        * n0  -ignore
.
          ADD       ONE,COUNT                    * increment count
          IF        (COUNT%1000)=0|COUNT=1
            DISPLAY   *P13:23,*V2LON,PURNO:
                      *P58:23,COUNT;
          ENDIF
.
          MATCH     SP70,PTYPE
          GOTO      PMST1000 IF NOT EQUAL
.
          MOVE      PURNO,DIM20               * load nhi number
          SQUEEZE   DIM20                        * remove spaces from nat. no.
          MOVELPTR  DIM20,LENVAR
.
          IF        LENVAR < 1 | LENVAR > 7
            GOTO      PMST1000                   * invalid NHI number
          ENDIF
.
          BRANCH    LENVAR,PMST1010:
                           PMST1020:
                           PMST1030:
                           PMST1040:
                           PMST1050:
                           PMST1060:
                           PMST1070
.
PMST1010  PACK      NHMANMPI,SP6,DIM20
          GOTO      PMST2000
.
PMST1020  PACK      NHMANMPI,SP5,DIM20
          GOTO      PMST2000
.
PMST1030  PACK      NHMANMPI,SP4,DIM20
          GOTO      PMST2000
.
PMST1040  PACK      NHMANMPI,SP3,DIM20
          GOTO      PMST2000
.
PMST1050  PACK      NHMANMPI,SP2,DIM20
          GOTO      PMST2000
.
PMST1060  PACK      NHMANMPI,SP1,DIM20
          GOTO      PMST2000
.
PMST1070  MOVE      DIM20,NHMANMPI
PMST2000  PACK      KEY7,NHMANMPI,SP70
          CALL      RDNHMAS1
          BRANCH    OVRCD,PMST1000
          MATCH     ANSY,NHMARES
          IF        @EQUAL
            MOVE      RESIDENT,PTYPE
          ENDIF
.
          MATCH     ANSN,NHMARES
          IF        @EQUAL
            MOVE      NONRESID,PTYPE
          ENDIF
          CALL      UPMAST1
.
          GOTO      PMST1000                     * get next record
.
PMST9000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:24,*EL,"Fix completed.  ",CTIMEIS;
          CALL      HITENTER
.
PMST9999  RETURN
+
.*****************************************************************************
.         I/O Includes                                                       *
.*****************************************************************************
.
          INC       STD001IO
          INC       PATCODKY
.
          INC       NHIMASIO/INC            * NHI master file
          INC       PATMA1IO/INC            * Master file
          INC       PATCODIO/INC            * Codes file
+

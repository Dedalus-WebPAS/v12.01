.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT91                                                 *
.* Description    :  Upload the Target Length of Stay to the Casemix Funding  *
.*                   Table                                                    *
.******************************************************************************
.* Date           :  15/03/2017                                               *
.* Author         :  Tracey Nguyen                                            *
.* Function       :  Upload the Target Length of Stay to the Casemix Funding  *
.*                   Table                                                    *
.* Modifications  :							      *
.*                                                                            *
.* V10.10.01  15/03/2017  Tracey Nguyen  TSK 0315207                          *
.*            Created program                                                 *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCONFD/INC
          INC       PATCODFD/INC            * Codes file
          INC       PATCMXFD/INC            * Casemix Funding
          INC       PMSCIDFD/INC            * Contract ID file
          INC       PATCMCFD/INC            * Casemix code file
          INC       PATCFAFD/INC            * Claim code file
          INC       PATFN1FD/INC            * Health fund file

.
.         Casemix Funding  Upload File
.
CMXFNTXT  FILE       HL7, FIXED=1000
.
.Name     Type     Size     Description
.----     ----     ----     -----------
XCCNID    DIM         6     Contract ID
XCCLMN    DIM         3     Claim code (Cat CL)
XCHFND    DIM         6     Health Fund
XCCASM    DIM         9     DRG/MBS Code
XCTLOS    DIM         4     Target Length of Stay
.

. ----- Program Variables -----
.
ADDCOUNT  FORM      8
BATCHNO   FORM      8
CRETURN   INIT      015                * carriage return character
CMDLINE   DIM       100
CODEJ     INIT      "J "
DIM4      DIM       4
ERRCOUNT  FORM      8
ERRFLAG   FORM      1
ERRORMSG  DIM       50
FORM8     FORM      8
FNAMEI    DIM       150
FNAMEB    DIM       8
FGSTFLAG  FORM      1
HEADSEC   FORM      5
IBCNMHOS  FORM      1
JTYPE     DIM        1
LINECNTR  FORM      8
USERID    DIM       10
PATHNAME  DIM       100
PNAME     DIM       25
SEC1      FORM      "00001"
TIMEIS    DIM       8
UPDCOUNT  FORM      8
ZERO8     INIT      "0000    "
.
XFLD0001  DIM       60
XFLD0002  DIM       60
XFLD0003  DIM       60
XFLD0004  DIM       60
XFLD0005  DIM       60
.
.
PRGID     INIT      "IBAADT91"
PRGNAM    INIT      "Upload Casemix Funding for TLOS"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000              * Initailize variables & open files
          CALL      KOPT0000              * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          CALL      KASC0000              * Keyin ascii file
          BRANCH    EXIT,ML9900
.
          CALL      KUSR0000              * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000              * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      PRHD0000              * Print header
.
          CALL      CTOT0000              * Clear total vars
.
          CALL      VALD0000              * Are all input records correct?
          BRANCH    EXIT,ML2000           * No, go print report
.
          CALL      POST0000              * Yes, Update/Write to patcmxaf table
.
ML2000    CALL      PTOT0000              * Print report totals
.
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patcmxaf"               * Casemix Funding
          OPEN      PATCMXA2,"patcmxaf"
.
          DISPLAY   *P64:24,"patcfaaf"               * Claim Code
          OPEN      PATCFAA1,"patcfaaf"
.
          DISPLAY   *P64:24,"patfn1af"               * Health Fund
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATFN1A3,"patfn1af"

          DISPLAY   *P64:24,"patcmcaf"               * Casemix Code
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P64:24,"pmscidaf"               * Contract Id
          OPEN      PMSCIDA1,"pmscidaf"
.
          MOVE      ZERO,ERRFLAG
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Upload Casemix Funding for TLOS                 *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload Casemix Funding":
                    *P1:10,"Select Option : ";
.
KOPT1000  KEYIN     *P17:10,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"Casemix Funding Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CMXFNTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
.
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P1:16,*EL,"Keyin Username: ",*P26:16,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P1:17,*EL,"Keyin Pathname: ",*P26:17,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  VALD0000                                  *
.* Validate all records from input file                                       *
.******************************************************************************
VALD0000  MOVE      ZERO,LINECNTR
          MOVE      ZERO,EXIT
.
          OPEN      CMXFNTXT,FNAMEI         * Open input file
VALD1000  READ      CMXFNTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005
          GOTO      VALD9000 IF OVER        * Read input file until EOF
.
          ADD       ONE,LINECNTR            * Input file line counter for error
.
          CALL      VREC0000                * Validate input record
          GOTO      VALD1000                * Read next input record
.
VALD9000  CLOSE     CMXFNTXT                * Close input file
.
VALD9999  RETURN
+
+
.******************************************************************************
.*                                  POST0000                                  *
.* Update all validated input records                                         *
.******************************************************************************
POST0000  OPEN      CMXFNTXT,FNAMEI         * Open input file
POST1000  READ      CMXFNTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005
          GOTO      POST9000 IF OVER        * Read input file until EOF
.
          PACK      XCCNID,XFLD0001,SP70    * Contract Id
          PACK      XCCLMN,XFLD0002,SP70    * Claim code
          PACK      XCHFND,XFLD0003,SP70    * Health Fund
          PACK      XCCASM,XFLD0004,SP70    * Casemix Code
.
          MOVE      ZERO,FORM4              * Target LOS
          PACK      DIM4,XFLD0005,SP70      * Convert to numeric	
          SQUEEZE   DIM4
          MOVE      DIM4,FORM4
          MOVE      FORM4,XCTLOS
.
          CALL      UCMX0000                * Update/Add Casemix Funding record
.
          GOTO      POST1000                * Read next input record
.
POST9000  CLOSE     CMXFNTXT                * Close input file
.
POST9999  RETURN
+
.----------------------------------------------------------------------
. Update/Add Casemix Funding record for Target Length of Stay 
.----------------------------------------------------------------------
UCMX0000  CALL      CLRCMX00                * Clear patcmxaf fields
.
          MOVE      XCCNID,PTCMCNID         * Contract Id
          MOVE      XCCLMN,PTCMCLMN         * Claim code
          MOVE      XCHFND,PTCMHFND         * Health Fund
          MOVE      XCCASM,PTCMCASM         * Casemix code
.
          PACK      KEY24,PTCMCNID,PTCMCLMN,PTCMHFND,PTCMCASM,SP70
          CALL      RDPTCMX2                * Read patcmxaf
          IF        OVRCD=0
            MOVE      XCTLOS,PTCMTLOS       * Record already exists  
            CALL      UPPTCMX2              * Update existing patcmxaf record
            ADD       ONE,UPDCOUNT          * Increment updated rec counter
          ELSE
            MOVE      XCTLOS,PTCMTLOS       * Record does not exist
            CALL      WRPTCMX2              * Write new record to patcmxaf 
            ADD       ONE,ADDCOUNT          * Increment added rec counter
          ENDIF
.
UCMX9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Date               : ",CDATE:
                    *N,"Time               : ",CTIMEIS:
                    *N,"User ID            : ",USERID:
                    *N,"Casemix Funding Load file : ",PATHNAME
.
          CALL      UND132
.
          PRINT     *1,"| Line",*12,"| Error ",*70,"| Record Detail":
                    *132,"|"
.
          CALL      UND132
.
PRHD9999  RETURN
+
.******************************************************************************
.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaadt91.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
.         Clear PATCMXFD variables               
.******************************************************************************
CLRCMX00  MOVE      SP70,PTCMCLMN
          MOVE      SP70,PTCMHFND
          MOVE      SP70,PTCMCASM
          MOVE      ZERO,PTCMXLOS
          MOVE      ZERO,PTCMDIFG
          MOVE      SP70,PTCMCNID
          MOVE      SP70,PTCMECEQ
          MOVE      SP70,PTCMECTS
          MOVE      SP70,PTCMTRPT
          MOVE      ZERO,PTCMTLOS
          MOVE      SP70,PTCMEHCP
          MOVE      SP70,PTCMSPAR
.
CLRCMX99  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,UPDCOUNT
          MOVE      ZERO,ADDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
. Print total line
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
          PRINT     *1,"| Total Updated : ",UPDCOUNT,*132,"|"
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
+
.******************************************************************************
. Validate input record fields
.******************************************************************************
VREC0000  PACK      XCCNID,XFLD0001,SP70               * Contract Id
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            CLEAR     ERRORMSG
            APPEND    "Contract Id is mandatory - cannot be spaces  ",ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ELSE
            PACK      KEY6,XFLD0001,SP70
            CALL      RDPMCID1
            IF        OVRCD=1
              CLEAR     ERRORMSG
              APPEND    "Invalid Contract Id: ",ERRORMSG
              APPEND    XCCNID,ERRORMSG
              APPEND    SP70,ERRORMSG
              RESET     ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF 
          ENDIF 
.
VREC2000  PACK      XCCLMN,XFLD0002,SP70               * Claim code
          MATCH     SP70,XFLD0002
          IF        @EQUAL
            CLEAR     ERRORMSG
            APPEND    "Claim Code is mandatory - cannot be spaces  ",ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ELSE
            PACK      XCCLMN,XFLD0002,SP70
            PACK      KEY5,ANSC,ANSL,XCCLMN,SP70
            CALL      RDCODE1
            IF        OVRCD = 1
              CLEAR     ERRORMSG
              APPEND    "Invalid Claim Code: ",ERRORMSG
              APPEND    XCCLMN,ERRORMSG
              APPEND    SP70,ERRORMSG
              RESET     ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
              GOTO      VREC3000
            ENDIF
            MOVE      XCCLMN,KEY3
            CALL      RDPTCFA1
            IF        OVRCD = 1
              CLEAR     ERRORMSG
              APPEND    "Invalid Claim Code: ",ERRORMSG
              APPEND    XCCLMN,ERRORMSG
              APPEND    SP70,ERRORMSG
              RESET     ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
VREC3000  PACK      XCHFND,XFLD0003,SP70               * Health Fund
          MATCH     SP70,XCHFND
          IF        !@EQUAL
            PACK      KEY6,XFLD0003,SP70
            PACK      KEY14,KEY6,ZERO8,SP70
            CALL      RDFUND1
            IF        OVRCD=1
              CLEAR     ERRORMSG
              APPEND    "Invalid Health Fund: ",ERRORMSG
              APPEND    KEY6,ERRORMSG
              APPEND    SP70,ERRORMSG
              RESET     ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
VREC4000  PACK      XCCASM,XFLD0004,SP70               * Casemix Code
          MATCH     SP70,XCCASM
          IF        @EQUAL
            CLEAR     ERRORMSG
            APPEND    "Casemix code is mandatory - cannot be spaces ",ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ELSE
            PACK      KEY9,XFLD0004,SP70
            CALL      RDPTCMC1 
            IF        OVRCD=1
              CLEAR     ERRORMSG
              APPEND    "Invalid Casemix Code: ",ERRORMSG
              APPEND    KEY9,ERRORMSG
              APPEND    SP70,ERRORMSG
              RESET     ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
VREC5000  MOVE      ZERO,FORM4                         
          PACK      DIM4,XFLD0005,SP70                 * Convert to numeric
          MATCH     SP4,DIM4
          GOTO      VREC9999 IF EQUAL
.
          SQUEEZE   DIM4
          TYPE      DIM4                               * If not numeric         
          IF        !@EQUAL
            CLEAR     ERRORMSG
            APPEND    "Invalid TLOS value must be numeric: ",ERRORMSG
            APPEND    DIM4,ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
            GOTO      VREC9999
          ENDIF     
. 
          MOVE      DIM4,FORM4                         * If numeric & negative
          IF        FORM4 < 0
            CLEAR     ERRORMSG
            APPEND    "Invalid TLOS value cannot be negative: ",ERRORMSG
            APPEND    FORM4,ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ENDIF
.
VREC9999  RETURN
+
.******************************************************************************
. Print error line
.******************************************************************************
PERR0000  ADD       ONE,ERRCOUNT                   * Total error count
.
          PACK      XCCNID,XFLD0001,SP70           * Contract Id
          PACK      XCCLMN,XFLD0002,SP70           * Claim code
          PACK      XCHFND,XFLD0003,SP70           * Health Fund
          PACK      XCCASM,XFLD0004,SP70           * Casemix Code
          PACK      XCTLOS,XFLD0005,SP70           * Target Length of Stay

          PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*70,"|":
                    XCCNID,*78," | ",XCCLMN,*86," | ",XCHFND,*98," | ":
                    XCCASM,*114," |",XCTLOS,*132,"|"
.
PERR9999  RETURN
+
.******************************************************************************
          INC       STD001IO
.
          INC       PATCODIO/INC            * Codes file
          INC       PATCMCIO/INC            * Casemix code file
          INC       PATFN1IO/INC            * Health fund file
          INC       PMSCIDIO/INC            * Contract ID file
          INC       PATCMXIO/INC            * Casemix funding
          INC       PATCFAIO/INC            * Claim code file

.

.******************************************************************************
.*    Program      : IBAMRT58                                                 *
.*    Description  : Cancer Codes Master Listing                              *
.*                                                                            *
.*    Author       : ROD - The Non Standards Man !!!       (12/10/92)         *
.*                                                                            *
.*    Modifications:                                                          *
.*       V11.05.01  20/03/2025  Davin          TSK 0956210                    *
.*                  Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*       V11.02.01  31/03/2022  Davin          TSK 0917793                    *
.*                  Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*       V10.10.01  17/03/2017  Davin          TSK 0833093     HDP 2017/2018  *
.*                  Add ICD10 Ed.10 - Go-live Date PTCNGDVX (Sect.110 Pos.85) *
.*       V10.06.01  17/03/2015  Davin         CAR 311669      HDP 2015/16     *
.*                  Add PTCNGDV9 for ICD10 Ed.9 Go-live Date (Sect.110 Pos.77)*
.*       V10.03.01  23/04/2013  Davin         CAR 284420      HDP 2013/14     *
.*                  Add PTCNGDV8 for ICD10 Ed.8 Go-live Date (Sect.110 Pos.69)*
.*       V10.00.01  07/04/2010  Mike Laming   CAR 219246      HDP 2010        *
.*                  Add PTCNGDV7 for ICD10 Ed.7 Go-live Date (Sect.110 Pos.61)*
.*        V9.09.01  20/03/2008  Mike Laming   CAR 163918     HDP 2008         *
.*                  Add PTCNGDV6 for ICD10 Ed.6 Go-live                       *
.*        V9.06.01  15/05/2006  Mike Laming   CAR 105244      2006 HDP        *
.*                  ADD PTCNGDV5 for ICD10 Ed.5 Go-live                       *
.*        V9.03.02  23/06/2004  Henry Son        CAR 49037                    *
.*                  ICD10 Ed.4 changes - add PTCNGDV4.                        *
.*        V9.03.01  05/12/2000  Sylvek Litewka  CAR 20222                     *
.*                  Modified procedure GDAT0000 to use common IO when reading *
.*                  ICD Disease file.                                         *
.* -------------------------------------------------------------------------- *
.*        V5.08.01  27/06/2000  Greg Horvat                                   *
.*                  Recompiled for PATICDKY - Validating ICD10 version 2 codes*
.*        V5.08.B02 07/03/2000  Jill Habasque                                 *
.*                  Removed NEWDITEM definition                               *
.*        V5.08.B01 06/03/2000  Jill Habasque                                 *
.*                  Recompiled for STDKWSDF & STDKWSCD                        *
.*        V5.07.02  06/11/1999  Greg Horvat                                   *
.*                  Recompiled for PATICDKY                                   *
.*        V5.07.01  08/03/1999  Steve Armstrong                               *
.*                  Mods for ICD10 keyword search                             *
.*            V5.07.B01 27/01/1999  Davin                                     *
.*                      Read PTCNI10D for correct disease code validation     *
.******************************************************************************
.*            V5.01.02  17/05/1993  Robert Sumsion                           *
.*                      Converted vars and includes to mrt in standard form. *
.*            V5.01.03  25/05/1994  Sandra Barcham                           *
.*                      Fix for global recompile                             *
.*****************************************************************************
.  
          INC       STD001FD 
          INC       PATCANFD/INC 
          INC       PATICDFD/INC 
          INC       PATDICVR/INC
.
DESC45    DIM       45
DIM70     DIM       70
ENDCODE   DIM       9
PENDESC   DIM       60
LATYN     DIM       3
PENDCODE  DIM       9
PSTDCODE  DIM       9
SAVKEY9   DIM       9
STDCODE   DIM       9
PSTDESC   DIM       60
PTCNI10D  DIM       8
PTCNGDV2  DIM       8
PTCNGDV3  DIM       8
PTCNGDV4  DIM       8                       * Ed4. golive date         *I-49037
PTCNGDV5  DIM       8                       * Ed5 golive date         *I-105244
PTCNGDV6  DIM       8                       * Ed.6 Go-live date       *I-163918
PTCNGDV7  DIM       8                       * Ed.7 Go-live date       *I-219246
PTCNGDV8  DIM       8                       * Ed.8 Go-live date       *I-284420
PTCNGDV9  DIM       8                       * Ed.9 Go-live date       *I-311669
PTCNGDVX  DIM       8                       * Ed.10 Go-live date      *I-0833093
PTCNGDX1  DIM       8                       * Go live date for ICD10 Ed.11
PTCNGDX2  DIM       8                       * Go live date for ICD10 Ed.12
PTCNGDX3  DIM       8                       * Go live date for ICD10 Ed.13
.
PRGID     INIT      "IBAMRT58"
PRGNAM    INIT      "Cancer Codes Master Listing"
VERSION   INIT      "V12.01.00"
+
.****************************************************************************
.*  ML0000  :  Mainline code                                                *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit
.
ML2000    CALL      KCAN0000                      * Keyin Cancer code range
          BRANCH    EXIT,ML1000                   * EXITCHAR
.
          CALL      CONTQST                       * Ok to Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.
ML5000    CALL      RPRT0000                      * Print report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.*  INIT0000  :  Initialisation                                             *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcanaf"
          OPEN      PATCANA1,"patcanaf"
.
          DISPLAY   *P64:24,"paticddf"
          CALL      OPICD1             * Open ICD Disease files
.
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D     * icd10 go-live date
.
          MOVE      SP9,DSICDDTE
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.****************************************************************************
.*  MENU0000  :  Main Menu                                                  *
.****************************************************************************
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Cancer Codes Master Listing":
                    *P1:7,"Select Option : "
.
MENU1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BRANCH    MOPTION,MENU8000
.
          BEEP
          GOTO      MENU1000
.
MENU8000  MOVE      ZERO,EXIT
          GOTO      MENU9999
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.****************************************************************************
.*  KCAN0000  :  Keyin range of Cancer codes                                *
.****************************************************************************
KCAN0000  DISPLAY   *P1:9,*EF,"Start Cancer Code : ":
                    *P1:10,"End   Cancer Code : "
.
          MOVE      "21",ECOL                     * set up parameters
          MOVE      "9",EVERT
          MOVE      SP9,CKYIDEF9
          MOVE      ZERO,CKYIMAND
.
          CALL      PATICDKY
          BRANCH    EXIT,KCAN2000,KCAN9000        * nothing, EXITCHAR input
.
          MOVE      DPCODE,STDCODE                * start ICD9 code
          MOVE      DPCODE,PSTDCODE               * start ICD9 code (print)
          MOVE      DDESC,PSTDESC                 * desc for print
          MOVE      DDESC,DESC45
          DISPLAY   *P32:9,DESC45                 * display Description
          GOTO      KCAN5000
.
. nothing input for start doctor code
.
KCAN2000  MOVE      SP6,STDCODE                   * start doctor code
          MOVE      "Start",PSTDCODE              * print var
          PACK      PSTDESC,SP30,SP30             * desc for print
          DISPLAY   *P21:9,*V2LON,"Start"
.
. Keyin Ending Doctor code
.
KCAN5000  MOVE      "21",ECOL                     * set up parameters
          MOVE      "10",EVERT
          MOVE      SP9,CKYIDEF9
          MOVE      ZERO,CKYIMAND
.
          CALL      PATICDKY
          BRANCH    EXIT,KCAN7000,KCAN9000        * nothing, EXITCHAR input
.
          MOVE      DPCODE,ENDCODE                * end doctor type
.
          MATCH     STDCODE,ENDCODE               * valid range?
          GOTO      KCAN6000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      KCAN0000
.
KCAN6000  MOVE      DPCODE,ENDCODE                * end doctor code (print)
          MOVE      DPCODE,PENDCODE               * code for print
          MOVE      DDESC,DESC45
          DISPLAY   *P32:10,DESC45                * display Doctor name
          MOVE      DDESC,PENDESC
          MOVE      ZERO,EXIT
          GOTO      KCAN9999
.
. nothing input for end doctor code
.
KCAN7000  MOVE      "zzzzzz",ENDCODE              * end doctor code
          MOVE      "Finish",PENDCODE             * print var
          PACK      PENDESC,SP30,SP30
          DISPLAY   *P21:10,*V2LON,"Finish"
          MOVE      ZERO,EXIT
          GOTO      KCAN9999
.
KCAN9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KCAN9999  RETURN
+
.****************************************************************************
.*  RPRT0000  :  Print report from temp file                                *
.****************************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO                     * force new page
.
          PACK      KEY9,STDCODE
          CALL      RDPTCAN1
          BRANCH    OVRCD,RPRT0400
          GOTO      RPRT0700
.
RPRT0400  CALL      RSPTCAN1
RPRT0500  CALL      RKPTCAN1                      * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
          MATCH     PTCCCODE,ENDCODE              * in range?
          GOTO      RPRT9000 IF LESS
.
. check if we need a new page
.
RPRT0700  COMPARE   "55",CLNO
          GOTO      RPRT5000 IF LESS
.
. we need a new page
.
          CALL      HEAD132
          PRINT     *40,"Cancer Code From : ",PSTDCODE,SP3,PSTDESC:
                    *N,*40,"Cancer Code To   : ",PENDCODE,SP3,PENDESC,*N
          CALL      PTAB0000                      * print headings
          MOVE      TEN,CLNO
.
RPRT5000  CALL      GDAT0000                      * get data for printing
.
. print a line of data
.
          PRINT     *1,"| ",PTCCCODE,*15,"| ",DDESC,*91,"|",*96,LATYN,*132,"|"
.
          ADD       ONE,CLNO                      * increment line counter
          GOTO      RPRT0500
.
RPRT9000  CALL      UND132                        * print underlines
          PRINT     *N,*1,"*** End of report ***" 
.
RPRT9999  RETURN
+
.****************************************************************************
.*  GDAT0000  :  Get data for printing                                      *
.****************************************************************************
GDAT0000  PACK      DDESC,SP30,SP30,SP10          * get description
          PACK      KEY9,PTCCCODE           * Read ICD Disease record
          CALL      RDPTICD1                * ICD10 as current date used
          IF        OVRCD=1
            CALL      RD10T9D1              * Try ICD9
          ENDIF
.
          IF        PTCCLATR=1
            MOVE      "Yes",LATYN
          ELSE
            MOVE      "No ",LATYN
          ENDIF
.
GDAT9999  RETURN
+
.****************************************************************************
.*  PTAB0000  :  Print Table Heading                                        *
.****************************************************************************
PTAB0000  CALL      UND132
          PRINT     *1,"| Cancer Code | Description",*91,"| ":
                    "Laterality Keyin Required",*132,"|"
          CALL      UND132
.
PTAB9999  RETURN
+
.
          INC       STD001IO
          INC       PATICDIO/INC
          INC       PATCANIO/INC
          INC       PATDICIO/INC
          INC       PATOICIO/INC
          INC       PATICDKY
          INC       ICDHLPSC

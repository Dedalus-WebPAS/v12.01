.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   FX323341                                                  *
.* Desc      :   Fix NZ ACC Claim Status                                   *
.***************************************************************************
.* Date      :   24/08/2016                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will update the ACC claim status. For all    *
.*               records in acclodaf it will update the claim status to    *
.*               completed in opdwx1af.pmwxcsta                            *
.* Modifications  :                                                        *
.* V10.08.00 24/08/2016  Ebon Clements TSK 0323341                         *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ACCLODFD/INC
          INC       PATWC1FD/INC
          INC       PMSWX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECNUM    FORM      8
.
PRGID     INIT      "FX323341"
PRGNAM    INIT      "Fix NZ ACC Claim Status"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      ACCLODA1,"acclodaf"
          OPEN      PATWC1A3,"patwc1af"
          OPEN      PMSWX1A1,"pmswx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  DISPLAY   *P1:15,*EL,"Processing ACC Number : ";
.
          PACK      KEY20,SP70
          CALL      RSACLOD1
PROC1000  CALL      RKACLOD1                       * Loop ACC lodged claims
          BRANCH    OVRCD,PROC9000
.
          DISPLAY   *P25:15,*EL,*V2LON,ACLOACCN
.
          PACK      KEY44,ACLOACCN,SP70
          CALL      RDSWCOM3
PROC2000  CALL      RDKWCOM3                       * Read workers compensation
          BRANCH    OVRCD,PROC1000                 * reacords by claim number
.
          MATCH     ACLOACCN,WCCLMNO               * Same ACC Number?
          GOTO      PROC1000 IF NOT EQUAL

          PACK      KEY8,WCADMNO,SP70              * Read Extn by visit 
          CALL      RDPMWX11
          BRANCH    OVRCD,PROC2000
.
          MATCH     "2",PMWXCSTA
          IF        !@EQUAL
            MOVE      "2",PMWXCSTA                 * Set status to completed
            CALL      UPPMWX11     
          ENDIF
.
          GOTO      PROC2000
.
PROC9000  DISPLAY   *P1:20,*EF,"ACC Claim Status Update Complete.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ACCLODIO/INC
          INC       PATWC1IO/INC
          INC       PMSWX1IO/INC

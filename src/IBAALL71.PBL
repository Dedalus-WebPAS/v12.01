.***************************************************************************
.* System    :   Allied Health                                             *
.* Program   :   IBAALL71                                                  *
.* Desc      :   Transfer allied health referrals                          *
.***************************************************************************
.* Date      :   19/10/2005                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will loop the allied health referral file    *
.*           :   and change the HCP and Department                         *
.* Mods      :                                                             *
.***************************************************************************
.* V11.02.01 07/02/2022  Thanh T       TSK 0896905                         *
.*           Recompiled for ALLDEPFD changes                               *
.* V10.14.01 15/03/2019  Thanh T.          TSK 0869558                     *
.*           Added UPOART00. When the appointment request in transfer      *
.*           referral control parameter is turned on, if the department    *
.*           required (outartaf.otaradep) matches the department from      * 
.*           field, it will be updated with the department to field        *
.***************************************************************************
.* V10.06.01 12/03/2015  Steve Armstrong   CAR 314108                      *
.*           Removed definition of PTCGDATE as PATCGPFD is no longer in    *
.*           use                                                           *
.***************************************************************************
.* V10.04.01 04/07/2014  Ebon Clements     CAR 299337                      *
.*           Fixed I * C on ALLREFA1                                       *
.***************************************************************************
.* V10.03.02 30/06/2013  Davin             CAR 283202                      *
.*           Recompiled for changes to hl7 messaging                       *
.* V10.03.01 30/06/2013  Davin             CAR 279183                      *
.*          Recompiled for changes to hl7 messaging (allquefd)             *
.*               V10.01.01 09/11/2010  Mike Laming       CAR 232086        *
.*                         Added ALLDEPFD/IO and code to change Hospital   *
.*                         ode inline with Dept Code at PROC2500           *
.*               V10.00.01 02/02/2010  Steve Armstrong   CAR 135505        *
.*                         Added DGCLII13 & DGCLICEU triggers              *
.*                         Also added HL7TRGID & HL7INCLD setting for all  *
.*                         broadcast messages                              *
.***************************************************************************
.*               V9.09.03  04/01/2008  J.Tan          CAR 135498           *
.*                         Mods for NZ MHINC Extract                       *
.*               V9.09.02 19/12/2007 Ebon Clements CAR 157606              *
.*                         Changed status description of Open to Active    *
.*               V9.09.01  08/10/2007 Ebon Clements CAR 140426             *
.*                         Added encounters and non U/R activity           *
.*               V9.05.B01 20/02/2006 Ebon Clements CAR 76341              *
.*                         Created referral file audits                    *
.*               V9.04.00 19/10/2005 Ebon Clements CAR 76341               *
.*                         Created program                                 *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       ALLACTFD/INC
          INC       ALLAEFFD/INC
          INC       ALLCONFD/INC
          INC       ALLENCFD/INC
          INC       ALLERCFD/INC
          INC       ALLQUEFD/INC
          INC       ALLREFFD/INC
          INC       ALLDEPFD/INC                                      *I-232086
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       IBACONFD/INC
          INC       NHIMASFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATMA1FD/INC
          INC       PATNIDFD/INC
          INC       PMSHCPFD/INC
          INC       PMSPX2FD/INC
          INC       PMSQPTFD/INC
          INC       WEBSECFD/INC
          INC       OUTARTFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CISMFLAG  FORM      1
CODE      DIM       2
DIM35     DIM       35
DISPDATE  DIM       11
DISPINCL  DIM       100
DISPSTAT  DIM       9
DISPTYPE  DIM       8
DOCCODE   DIM       10
FROMDEPT  DIM       3
FROMDDES  DIM       20
FROMHCPC  DIM       10
FROMHCPD  DIM       40
IBAMFLAG  FORM      1
MESSAGID  DIM       20
NMPNUMB   DIM       20
RECDTYPE  FORM      1
SAVKEY22  DIM       22
SAVKEY29  DIM       29
SAVKEY42  DIM       42
TODEPART  DIM       3
TODEPDES  DIM       20
TOHCPCOD  DIM       10
TOHCPDES  DIM       40
INCLUDEW  FORM      1
INCLUDEA  FORM      1
INCLUDEE  FORM      1
INCLUDEI  FORM      1
INCLUDEC  FORM      1
INCLUDEX  FORM      1
INCLUDER  FORM      1
INCLUDEU  FORM      1
RECCOUNT  FORM      8
UPDATEFL  FORM      1
.
PRGID     INIT      "IBAALL71"
PRGNAM    INIT      "Transfer Patient Referrals"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
          GOTO      ML9999
.
ML1000    CALL      DEPF0000               * get department from
          BRANCH    EXIT,ML9999
.
ML1500    CALL      HCPF0000               * get hcp from
          BRANCH    EXIT,ML9999
.
ML2000    CALL      DEPT0000               * get department to
          BRANCH    EXIT,ML9999
.
ML2500    CALL      HCPT0000               * get hcp to
          BRANCH    EXIT,ML9999
.
ML3000    CALL      INCW0000               * Include waiting referrals   
          BRANCH    EXIT,ML9999
.
ML3500    CALL      INCA0000               * Include active referrals
          BRANCH    EXIT,ML9999
.
ML4000    CALL      INCI0000               * Include inactive referrals
          BRANCH    EXIT,ML9999
.
ML4500    CALL      INCC0000               * Include closed referrals
          BRANCH    EXIT,ML9999
.
ML5000    CALL      INCX0000               * Include cancelled referrals
          BRANCH    EXIT,ML9999
.
ML5500    CALL      INCR0000               * Include rejected referrals
          BRANCH    EXIT,ML9999
.
ML6000    CALL      INCE0000               * Include encounters
          BRANCH    EXIT,ML9999
.
ML6500    CALL      INCU0000               * Include non U/R activity
          BRANCH    EXIT,ML9999
.
ML7500    CALL      VALD0000               * print report and or update
          BRANCH    EXIT,ML9999
.
ML8000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML9000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML9000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P60:24,"opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P68:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P68:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P68:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P68:24,"allrefaf"
          OPEN      ALLREFA1,"allrefaf"
          OPEN      ALLREFA4,"allrefaf"
.
          DISPLAY   *P68:24,"alldepaf"
          OPEN      ALLDEPA1,"alldepaf"
.
          DISPLAY   *P68:24,"allactaf"
          OPEN      ALLACTA1,"allactaf"
.
          DISPLAY   *P68:24,"allencaf"
          OPEN      ALLENCA2,"allencaf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P68:24,"outartaf"
          OPEN      OUTARTA1,"outartaf"
.
          DISPLAY   *P68:24,"pmshcpaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHCPA2,"pmshcpaf"
.
          READ      CONTROLF,HUND06;*27,ALCNRAUD,*28,ALCNEAUD
          READ      CONTROLF,HUND21;*126,ALCNUPAR

          MATCH     "1",ALCNRAUD            
          IF        @EQUAL
            OPEN      ALLAUDRE,"allaudre"
          ENDIF
.
          MATCH     "1",ALCNEAUD
          IF        @EQUAL
            OPEN      ALLAUDEN,"allauden"
          ENDIF
.
          CALL      IBACLOCK
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Transfer Referrals"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                DEPF0000             Called by: ML1000    *
.* Keyin department from                                                    *
.****************************************************************************
DEPF0000  DISPLAY   *P1:9,"Department From : ",SP30
          MOVE      SP70,FROMDEPT
          MOVE      SP70,FROMDDES
          MOVE      "18",ECOL
          MOVE      "9",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,ANSC,ANSG
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,DEPF9100,DEPF9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P25:EVERT,TDESC
          MOVE      ACODE,FROMDEPT
          MOVE      TDESC,FROMDDES
.
DEPF9000  MOVE      ZERO,EXIT
          GOTO      DEPF9999
.
DEPF9100  MOVE      ONE,EXIT
          GOTO      DEPF9999
.
DEPF9999  RETURN
+
.****************************************************************************
.*                                HCPF0000             Called by: ML1500    *
.* Keyin hcp from                                                           *
.****************************************************************************
HCPF0000  DISPLAY   *P1:10,"From HCP        : "
          MOVE      SP70,PMHCSNAM
          MOVE      SP70,PMHCGNAM
          MOVE      SP10,PMHCTITL
          MOVE      SP70,FROMHCPC 
          MOVE      SP70,FROMHCPD 
          MOVE      "18",ECOL
          MOVE      "10",EVERT
          MOVE      ONE,CKYIMAND
          CALL      PMSHCPKY                * Keyin the referring doctor
          BRANCH    EXIT,HCPF9100,HCPF9100
.                        Spaces   Exitchar
.
          MOVE      DOCCODE,FROMHCPC
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME                 * Format patients name
          MOVE      PACFNAME,FROMHCPD
.
          DISPLAY   *P30:10,FROMHCPD
          MOVE      ZERO,EXIT
          GOTO      HCPF9999
.
HCPF9100  MOVE      ONE,EXIT
          GOTO      HCPF9999
.
HCPF9999  RETURN
+
.****************************************************************************
.*                                DEPT0000             Called by: ML1000    *
.* Keyin department from                                                    *
.****************************************************************************
DEPT0000  DISPLAY   *P1:11,"Department To   : ",SP30
          MOVE      SP70,TODEPART
          MOVE      SP70,TODEPDES
          MOVE      "18",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,ANSC,ANSG
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,DEPT9100,DEPT9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P25:EVERT,TDESC
          MOVE      ACODE,TODEPART
          MOVE      TDESC,TODEPDES
.
DEPT9000  MOVE      ZERO,EXIT
          GOTO      DEPT9999
.
DEPT9100  MOVE      ONE,EXIT
          GOTO      DEPT9999
.
DEPT9999  RETURN
+
.****************************************************************************
.*                                HCPT0000             Called by: ML2500    *
.* Keyin hcp to                                                             *
.****************************************************************************
HCPT0000  DISPLAY   *P1:12,"To HCP          : "
          MOVE      SP70,PMHCSNAM
          MOVE      SP70,PMHCGNAM
          MOVE      SP10,PMHCTITL
          MOVE      SP70,TOHCPCOD
          MOVE      SP70,TOHCPDES
          MOVE      "18",ECOL
          MOVE      "12",EVERT
          MOVE      ONE,CKYIMAND
          CALL      PMSHCPKY                * Keyin the referring doctor
          BRANCH    EXIT,HCPT9100,HCPT9100
.                        Spaces   Exitchar
.
          MOVE      DOCCODE,TOHCPCOD
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME                 * Format patients name
          MOVE      PACFNAME,TOHCPDES
.
          DISPLAY   *P30:12,TOHCPDES
          MOVE      ZERO,EXIT
          GOTO      HCPT9999
.
HCPT9100  MOVE      ONE,EXIT
          GOTO      HCPF9999
.
HCPT9999  RETURN
+
.****************************************************************************
.*                                INCW0000             Called by: ML3000    *
.* Include waiting referrals                                                *
.****************************************************************************
INCW0000  MOVE      ZERO,INCLUDEW
          MOVE      ZERO,EXIT
.
INCW1000  DISPLAY   *P1:13,"Include Waiting Patients : "
          KEYIN     *P32:13,*V2LON,*RV,DIM1:
                    *P32:13,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:13,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEW
          MATCH     "Y",DIM1
          GOTO      INCW9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEW
          MATCH     "N",DIM1
          GOTO      INCW9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCW9000 IF EQUAL
.
          BEEP
          GOTO      INCW1000
.
INCW9000  MOVE      ONE,EXIT
          GOTO      INCW9999
.
INCW9999  RETURN
.
+
.****************************************************************************
.*                                INCA0000             Called by: ML3500    *
.* Include active referrals                                                 *
.****************************************************************************
INCA0000  MOVE      ZERO,INCLUDEA
          MOVE      ZERO,EXIT
.
INCA1000  DISPLAY   *P1:14,"Include Active Patients : "
          KEYIN     *P32:14,*V2LON,*RV,DIM1:
                    *P32:14,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:14,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEA
          MATCH     "Y",DIM1
          GOTO      INCA9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEA
          MATCH     "N",DIM1
          GOTO      INCA9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCA9000 IF EQUAL
.
          BEEP
          GOTO      INCA1000
.
INCA9000  MOVE      ONE,EXIT
          GOTO      INCA9999
.
INCA9999  RETURN
+
.****************************************************************************
.*                                INCI0000             Called by: ML4000    *
.* Include inactive referrals                                                *
.****************************************************************************
INCI0000  MOVE      ZERO,INCLUDEI
          MOVE      ZERO,EXIT
.
INCI1000  DISPLAY   *P1:15,"Include Inactive Patients : "
          KEYIN     *P32:15,*V2LON,*RV,DIM1:
                    *P32:15,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:15,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEI
          MATCH     "Y",DIM1
          GOTO      INCI9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEI
          MATCH     "N",DIM1
          GOTO      INCI9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCI9000 IF EQUAL
.
          BEEP
          GOTO      INCI1000
.
INCI9000  MOVE      ONE,EXIT
          GOTO      INCI9999
.
INCI9999  RETURN
+
.****************************************************************************
.*                                INCC0000             Called by: ML4500    *
.* Include closed referrals                                                 *
.****************************************************************************
INCC0000  MOVE      ZERO,INCLUDEC
          MOVE      ZERO,EXIT
.
INCC1000  DISPLAY   *P1:16,"Include Closed Patients : "
          KEYIN     *P32:16,*V2LON,*RV,DIM1:
                    *P32:16,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:16,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEC
          MATCH     "Y",DIM1
          GOTO      INCC9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEC
          MATCH     "N",DIM1
          GOTO      INCC9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCC9000 IF EQUAL
.
          BEEP
          GOTO      INCC1000
.
INCC9000  MOVE      ONE,EXIT
          GOTO      INCC9999
.
INCC9999  RETURN
+
.****************************************************************************
.*                                INCX0000             Called by: ML5000    *
.* Include cancelled referrals                                              *
.****************************************************************************
INCX0000  MOVE      ZERO,INCLUDEX
          MOVE      ZERO,EXIT
.
INCX1000  DISPLAY   *P1:17,"Include Cancelled Patients : "
          KEYIN     *P32:17,*V2LON,*RV,DIM1:
                    *P32:17,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:17,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEX
          MATCH     "Y",DIM1
          GOTO      INCX9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEX
          MATCH     "N",DIM1
          GOTO      INCX9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCX9000 IF EQUAL
.
          BEEP
          GOTO      INCX1000
.
INCX9000  MOVE      ONE,EXIT
          GOTO      INCX9999
.
INCX9999  RETURN
+
.****************************************************************************
.*                                INCR0000             Called by: ML5500    *
.* Include rejected referrals                                               *
.****************************************************************************
INCR0000  MOVE      ZERO,INCLUDER
          MOVE      ZERO,EXIT
.
INCR1000  DISPLAY   *P1:18,"Include Rejected Patients : "
          KEYIN     *P32:18,*V2LON,*RV,DIM1:
                    *P32:18,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:18,*V2LON,DIM1
.
          MOVE      ONE,INCLUDER
          MATCH     "Y",DIM1
          GOTO      INCR9999 IF EQUAL
.
          MOVE      ZERO,INCLUDER
          MATCH     "N",DIM1
          GOTO      INCR9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCR9000 IF EQUAL
.
          BEEP
          GOTO      INCR1000
.
INCR9000  MOVE      ONE,EXIT
          GOTO      INCR9999
.
INCR9999  RETURN
+
.****************************************************************************
.*                                VALD0000             Called by: ML0000    *
.* Print validation report only or print report and update records          *
.****************************************************************************
VALD0000  MOVE      ZERO,UPDATEFL                  * No to update records
          MOVE      ZERO,EXIT
.
VALD1000  DISPLAY   *P1:21,"Update Records : "
          KEYIN     *P18:21,*V2LON,*RV,DIM1:
                    *P18:21,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P18:21,*V2LON,DIM1
          MOVE      ONE,UPDATEFL
          MATCH     "Y",DIM1
          GOTO      VALD9999 IF EQUAL
.
          MOVE      ZERO,UPDATEFL
          MATCH     "N",DIM1
          GOTO      VALD9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     VALD9000 IF EQUAL
.
          BEEP
          GOTO      VALD1000
.
VALD9000  MOVE      ONE,EXIT
          GOTO      VALD9999
.
VALD9999  RETURN
+
.****************************************************************************
.*                                INCE0000             Called by: ML6000    *
.* Include Encounters                                                       *
.****************************************************************************
INCE0000  MOVE      ZERO,INCLUDEE
          MOVE      ZERO,EXIT
.
INCE1000  DISPLAY   *P1:19,"Include Encounters: "
          KEYIN     *P32:19,*V2LON,*RV,DIM1:
                    *P32:19,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:19,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEE
          MATCH     "Y",DIM1
          GOTO      INCE9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEE
          MATCH     "N",DIM1
          GOTO      INCE9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCE9000 IF EQUAL
.
          BEEP
          GOTO      INCE1000
.
INCE9000  MOVE      ONE,EXIT
          GOTO      INCE9999
.
INCE9999  RETURN
+
.****************************************************************************
.*                                INCU0000             Called by: ML6500    *
.* Include non U/R activity                                                 *
.****************************************************************************
INCU0000  MOVE      ZERO,INCLUDEU
          MOVE      ZERO,EXIT
.
INCU1000  DISPLAY   *P1:20,"Include Non U/R Activity : "
          KEYIN     *P32:20,*V2LON,*RV,DIM1:
                    *P32:20,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:20,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEU
          MATCH     "Y",DIM1
          GOTO      INCU9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEU
          MATCH     "N",DIM1
          GOTO      INCU9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCU9000 IF EQUAL
.
          BEEP
          GOTO      INCU1000
.
INCU9000  MOVE      ONE,EXIT
          GOTO      INCU9999
.
INCU9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  CALL      HEAD0000 
          MOVE      ZERO,RECCOUNT
          MOVE      ZERO,CPAGENO
.
          PACK      KEY22,FROMDEPT,SP70
          CALL      RSALREF4
PROC1000  CALL      RKALREF4
          BRANCH    OVRCD,PROC9000
.
          MATCH     ALREDEPT,FROMDEPT       * Check department
          GOTO      PROC9000 IF NOT EQUAL     
.
          MATCH     ALREHCP,FROMHCPC        * Check HCP
          GOTO      PROC1000 IF NOT EQUAL     
.
          IF        INCLUDEW=1
            MATCH     "0",ALRESTAT          * Waiting
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDEA=1
            MATCH     "1",ALRESTAT          * Active
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDEC=1
            MATCH     "2",ALRESTAT          * Closed
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDEI=1
            MATCH     "3",ALRESTAT          * Inactive
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDEX=1
            MATCH     "4",ALRESTAT          * Cancelled
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDER=1
            MATCH     "5",ALRESTAT          * Rejected
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          GOTO      PROC1000
.
PROC2000  MOVE      ONE,RECDTYPE            * Referral
          CALL      PREF0000                * Print U/R Line details
.
          IF        UPDATEFL=1
            PACK      SAVKEY22,ALREDEPT,ALRESTAT,ALREHCP,ALREVISN,SP70
.
            MOVE      TWO,AUDTTYPE          * before history record
            CALL      WAALRE00
.
            CALL      ARMHI000              * Set to 'Unsent' - MHINC extract
            MOVE      TODEPART,ALREDEPT
            MOVE      TOHCPCOD,ALREHCP 
.                                           * change Hosp.(if reqd)   *I-232086
PROC2500    MOVE      SP20,ALDEHOSP
            PACK      KEY3,TODEPART,SP10
            CALL      RDALDEP1
            MATCH     SP3,ALDEHOSP
            IF        !@EQUAL
              MOVE      ALDEHOSP,ALREHOSN
            ENDIF                                              * end  *I-232086
.
            CALL      UPALREF4              * Update Referral
.
            MATCH     "1",ALCNUPAR
            IF        @EQUAL
              CALL      UPOART00            * update appointment request
            ENDIF  
.
            CALL      PMIGTNID              * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      ONE,HL7TRGID
            MOVE      SP8,HL7INCLD
            CALL      DGCLII13              * broadcast Update Ref. (REF^I13)
.
            MOVE      THREE,AUDTTYPE        * after history record
            CALL      WAALRE00
.
            MOVE      SAVKEY22,KEY22        * Reposition 
            CALL      RSALREF4
          ENDIF
.
          GOTO      PROC1000
.
PROC9000  CALL      ENCT0000                * Process Encounters
          CALL      NONU0000                * Process Non U/R Activity
.
          CALL      TAIL0000                * Print End of Report
          GOTO      PROC9999
.
PROC9999  RETURN
.
.-------------------------------------------------------------------------------
. to update department required (otaradep) with department to when it matches
. the department from field       
.-------------------------------------------------------------------------------
UPOART00  PACK      KEY32,ALREURNO,ALREVISN,Z70
          CALL      RSOTART1                * positional read
UPOART10  CALL      RPOTART1                * read a record
          BRANCH    OVRCD,UPOART99
.
          MATCH     ALREURNO,OTARURNO       * same ur number
          GOTO      UPOART99 IF NOT EQUAL
.
          MATCH     ALREVISN,OTARREFN       * if the referral/visit matches
          GOTO      UPOART99 IF NOT EQUAL
.
          MATCH     "0",OTARSTAT
          GOTO      UPOART10 IF NOT EQUAL
.
          MATCH     FROMDEPT,OTARADEP
          GOTO      UPOART10 IF NOT EQUAL
.
UPOART80  MOVE      TODEPART,OTARADEP
          CALL      UPOTART1
.
          GOTO      UPOART10           
.
UPOART99  RETURN
+
.******************************************************************************
.         MHINC - MH Department and Primary Ref.
.******************************************************************************
ARMHI000  PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,ARMHI999
.
          MATCH     "M",TCINDC4
          GOTO      ARMHI999 IF NOT EQUAL      * Not a Mental Health department
.
          MATCH     "1",ALREUYN4
          GOTO      ARMHI999 IF NOT EQUAL      * Not Primary Referral
.
          MOVE      "0",ALREMOHR               * Unsent
.
ARMHI999  RETURN
+
.************************************************************************
.*                        HEAD0000                                      *
.*                 Produce printout heading                             *
.************************************************************************
HEAD0000  MOVE      ZERO,CLNO
          CALL      HEAD132
.
          PRINT     *25,"Department From : ",FROMDDES:
                        *80,"Department To   : ",TODEPDES
          PRINT     *25,"HCP From : ",FROMHCPD:
                        *80,"HCP To  : ",TOHCPDES
.
          CLEAR     DISPINCL
.
          IF        INCLUDEW=1
            APPEND    "Waiting ",DISPINCL
          ENDIF
.
          IF        INCLUDEA=1
            APPEND    "Active ",DISPINCL
          ENDIF
.
          IF        INCLUDEI=1
            APPEND    "Inactive ",DISPINCL
          ENDIF
.
          IF        INCLUDEC=1
            APPEND    "Closed ",DISPINCL
          ENDIF
.
          IF        INCLUDEX=1
            APPEND    "Cancelled ",DISPINCL
          ENDIF
.
          IF        INCLUDER=1
            APPEND    "Rejected ",DISPINCL
          ENDIF
.
          IF        INCLUDEE=1
            APPEND    "EOC ",DISPINCL
          ENDIF
.
          IF        INCLUDEU=1
            APPEND    "Non U/R ",DISPINCL
          ENDIF
.
          IF        UPDATEFL=1
            PRINT     *25,"Update Records : ",DYES
          ELSE
            PRINT     *25,"Update Records : ",DNO
          ENDIF
.
          PRINT       *25,"Include Status : ",DISPINCL
.
          CALL      UND132
.
          PRINT     *1,"| U/R No",*12,"| Patient Name",*50,"| Referral ":
                    *61,"| Status ",*73,"| Date",*86,"| Record Type",*132,"|"
.
          CALL      UND132
          ADD       EIGHT,CLNO
.
HEAD9999  RETURN
.
+
.**************************************************************************
.*                               NONU0000              Called by: PROC9000*
.* Process non U/R related activity                                       *
.**************************************************************************
NONU0000  COMPARE   ONE,INCLUDEU
          GOTO      NONU9999 IF NOT EQUAL
. 
          PACK      KEY29,FROMDEPT,SP70
          CALL      RSALACT1
NONU1000  CALL      RKALACT1
          BRANCH    OVRCD,NONU9999
.         
          MATCH     ALACDEPT,FROMDEPT       * Check department
          GOTO      NONU9999 IF NOT EQUAL
.
          MATCH     ALACDOCT,FROMHCPC       * Check HCP
          GOTO      NONU1000 IF NOT EQUAL
.
NONU2000  MOVE      TWO,RECDTYPE            * Non U/R
          CALL      PREF0000                * Print U/R Line details
.
          IF        UPDATEFL=1
            PACK      SAVKEY29,ALACDEPT,ALACCODT,ALACCOTM,ALACDOCT,SP70
.
            MOVE      TODEPART,ALACDEPT
            MOVE      TOHCPCOD,ALACDOCT
            PACK      KEY29,ALACDEPT,ALACCODT,ALACCOTM,ALACDOCT,SP70
            CALL      RAALACT1
            IF        OVRCD=1
              MOVE      SAVKEY29,KEY29
              CALL      RDALACT1
              IF        OVRCD=0
                MOVE      TODEPART,ALACDEPT
                MOVE      TOHCPCOD,ALACDOCT
                CALL      UPALACT1            * Update Referral
              ENDIF
            ENDIF
.
            MOVE      SAVKEY29,KEY29        * Reposition
            CALL      RSALACT1
          ENDIF
.
          GOTO      NONU1000
.
NONU9999  RETURN
+
.**************************************************************************
.*                               ENCT0000              Called by: PROC9000*
.* Process encounters                                                     *
.**************************************************************************
ENCT0000  COMPARE   ONE,INCLUDEE
          GOTO      ENCT9999 IF NOT EQUAL
.
          PACK      KEY42,FROMHCPC,SP70
          CALL      RSALENC2
ENCT1000  CALL      RKALENC2
          BRANCH    OVRCD,ENCT9999
.
          MATCH     ALENHCP,FROMHCPC        * Check HCP
          GOTO      ENCT9999 IF NOT EQUAL
.
          PACK      KEY8,ALENVISN,SP70
          CALL      RDALREF1                * Read referral record
          BRANCH    OVRCD,ENCT1000
.
ENCT2000  MOVE      THREE,RECDTYPE          * Encounter
          CALL      PREF0000                * Print U/R Line details
.
          IF        UPDATEFL=1
            PACK      SAVKEY42,ALENHCP,ALENSDAT,ALENSTIM,ALENVISN,ALENENCT,SP70
.
            MOVE      TWO,AUDTTYPE          * before history record
            CALL      WAALEN00
.
            MOVE      TOHCPCOD,ALENHCP
            CALL      UPALENC2              * Update Encounter
.
            MOVE      THREE,AUDTTYPE        * after history record
            CALL      WAALEN00
.
            MOVE      ALREVISN,KEY8
          OPEN      ALLREFA4,"allrefaf"
            CALL      RDALREF1              * get referral details for dgate
            IF        OVRCD = 0
              CALL      PMIGTNID            * get national id for dgate write
              MOVE      NMPNUMB,PTNINMPI
              MOVE      TWO,HL7TRGID
              MOVE      SP8,HL7INCLD
              PROC      DGCLICEU            * broadcast Update Ref. (ADT^A03)
            ENDIF
.
            MOVE      SAVKEY42,KEY42        * Reposition
            CALL      RSALENC2
          ENDIF
.
          GOTO      ENCT1000
.
ENCT9999  RETURN
.
.****************************************************************************
.*        Print UR and Name                                                 *
.****************************************************************************
PREF0000  ADD       ONE,RECCOUNT
.
          COMPARE   TWO,RECDTYPE            * Non U/R
          IF        @EQUAL
            UNPACK    ALACCODT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,DISPDATE
            MOVE      SP70,DIM35
            MOVE      SP70,DISPSTAT
            MOVE      SP70,ALREURNO
            MOVE      SP70,ALREVISN
            GOTO      PREF1500
          ENDIF
.
          COMPARE   THREE,RECDTYPE          * Encounter 
          IF        @EQUAL
            UNPACK    ALENSDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,DISPDATE
            MOVE      SP70,DIM35
            MOVE      SP70,DISPSTAT
          ENDIF
.
          PACK      KEY8,ALREURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "Invalid U/R Number",DIM35
            GOTO      PREF1000
          ENDIF
.
          PACK      KEY8,ALREURNO,SP70
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
            GOTO      PREF1000
          ENDIF
.
          MOVE      PTITL,PACTITLE
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Pack Full Name
          MOVE      PACFNAME,DIM35
.
PREF1000  MATCH     "0",ALRESTAT
          IF         @EQUAL
           MOVE       "Waiting",DISPSTAT
          ENDIF
.
          MATCH     "1",ALRESTAT
          IF         @EQUAL
           MOVE       "Active",DISPSTAT
          ENDIF
.
          MATCH     "2",ALRESTAT
          IF         @EQUAL
           MOVE       "Closed",DISPSTAT
          ENDIF
.
          MATCH     "3",ALRESTAT
          IF         @EQUAL
           MOVE       "Inactive",DISPSTAT
          ENDIF
.
          MATCH     "4",ALRESTAT
          IF         @EQUAL
           MOVE       "Cancelled",DISPSTAT
          ENDIF
.
          MATCH     "5",ALRESTAT
          IF         @EQUAL
           MOVE       "Rejected",DISPSTAT
          ENDIF
.
          UNPACK    ALRERDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISPDATE
.
PREF1500  COMPARE   "60",CLNO
          GOTO      PREF2000 IF LESS
          CALL      HEAD0000                 * Print  header
.
PREF2000  MOVE      SP70,DISPTYPE
          IF        RECDTYPE=1
            MOVE      "Referral",DISPTYPE
          ENDIF
.
          IF        RECDTYPE=2
            MOVE      "Non U/R",DISPTYPE
          ENDIF
.
          IF        RECDTYPE=3
            MOVE      "EOC",DISPTYPE
          ENDIF
.
          PRINT     *1,"| ",ALREURNO,*12,"| ",DIM35:
                    *50,"| ",ALREVISN,*61,"| ",DISPSTAT:
                    *73,"| ",DISPDATE,*86,"| ",DISPTYPE,*132,"|"
.
          ADD       ONE,CLNO
          GOTO      PREF9999
.
PREF9999  RETURN
.
.****************************************************************************
.*        Trailer Print Routine                                             *
.****************************************************************************
TAIL0000  COMPARE   "60",CLNO
          GOTO      TAIL1000 IF LESS
          CALL      HEAD0000                 * Print header
.
TAIL1000  CALL      UND132
          PRINT     *1,"Records Selected ",RECCOUNT
          CALL      UND132
          PRINT     *N,"*** End of Report ***"
.
TAIL9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       BRHLCOMN
          INC       CLPMSPX2
          INC       DGCLII13
          INC       DGCLICEU
          INC       PATCODKY
          INC       PMIGTNID
          INC       PMSHCPKY
.
          INC       ALLACTIO/INC
          INC       ALLQUEIO/INC
          INC       ALLREFIO/INC
          INC       ALLDEPIO/INC                                      *I-232086
          INC       ALLENCIO/INC
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       NHIMASIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATNIDIO/INC
          INC       PMSHCPIO/INC
          INC       PMSPX2IO/INC
          INC       PMSQPTIO/INC
          INC       WEBSECIO/INC
          INC       OUTARTIO/INC

.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:    IBABIL32                                              *
.*                                                                            *
.*     FUNCTION:        TAKE UP OUTSTANDING DEBTS UPLOAD FROM A TXT FILE      *
.*                                                                            *
.*     DATE WRITTEN:    08/08/2007                                            *
.*                                                                            *
.*     AUTHOR:          J.Tan                                                 *
.*                                                                            *
.*     NOTE        :    For ORACLE database you will need to build Claim and  *
.*                      Health Fund/Table mapping (dat/idx file)              *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*       V12.00.01 14/05/2025  Ebon Clements   TSK 0955096                    *
.*                 Added alpanumeric visit number validation - CMAX0000       *
.*                 amend STADMNO FORM 8 to DIM 8                              *
.*                 CMAX0000 amend COMPARE to MATCH STADMNO                    *
.*       V11.05.02 05/03/2025  J.Tan   TSK 0955356                            *
.*                 Added SKEY40 for PATCALFN                                  *
.*       V11.05.01 21/02/2025  J.Tan   TSK 0955356                            *
.*                 Added FININVN,FINADMN,FINURNO                              *
.*       V11.01.01 18/02/2021  J.Tan          TSK 0888639                     *
.*                 Added PTDTTMNO - Theatre Team no                           *
.*       V11.00.02 15/09/2020  J.Tan          TSK 0897071                     *
.*                 Mod for NZ Invoice range                                   *
.*       V11.00.01 24/02/2020  J.Tan          TSK 0838262                     *
.*                 Added PATITMRD                                             *
.*       V10.13.02 19/09/2018  J.Tan        TSK 0863727                       *
.*                 Added Restrictive Override code                            *
.*       V10.13.01 21/08/2017  J.Tan        TSK 0859742                       *
.*                 Added ECLIPSE new fields from aaedtr                       *
.*       V10.07.03 09/03/2016  Peter Vela     CAR 0815191                     *
.*                 Fixed pack of KEY22 in PROC6020                            *
.*       V10.07.02 01/03/2016  Peter Vela     CAR 0814118                     *
.*                 Added validation for spaces in VLDF4000                    *
.*       V10.07.01 21/12/2015  J.Tan          CAR 0303942                     *
.*                 Mods to setup Payment code                                 *
.*       V10.05.03 14/01/2015  Jill Parkinson CAR 300400                      *
.*                 Mods to not include error records in the total, and to     *
.*                 print details of valid records when running validation only*
.*       V10.05.02 05/08/2014  Peter Vela   CAR 302164                        *
.*                 Added ATDTRBRS - Rebate Reason CAT Rr                      *
.*       V10.05.01 16/07/2014  J.Tan        CAR 300400                        *
.*                 Added option to validate only                              *
.*       V10.04.01 20/01/2014  J.Tan        CAR 249828                        *
.*                 Added PTDTPMBS - Patient MBS Team and Counter              *
.*       V10.03.01 31/10/2013 Sandra Barcham  CAR 233084                      *
.*                 Fill in FINHOSP for patfinsf                               *
.*       V10.01.02 13/01/2011  Mike Laming    CAR 233084                      *
.*                 Modified for new CLPATINV (Added PTINCNID) in ZEROINV      *
.*       V10.01.01 20/12/2010  Mike Laming   CAR 233046                       *
.*                 Mods to Misc.Charges PATMCHFD - Key change, logic changes  *
.*       V10.00.01 29/07/2010  Jill Parkinson CAR 227360                      *
.*                 Mods to display current admission number                   *
.*       V9.12.03  28/07/2009  J.Tan         CAR 202119                       *
.*                 Added Emergency system                                     *
.*       V9.12.02  17/06/2009  Jill Habasque CAR 197395                       *
.*                 Mods to ask if leading zero's should be removed            * 
.*       V9.12.01  22/05/2009 WeeLee Koo CAR 197276                           *
.*                 Added FMUR0000 to strip leading zeros of UR No             * 
.*       V9.11.02  13/10/2008 J.Tan      CAR 173574                           *
.*                 Mods to read claim mapping text file not index file        *
.*                 Mods to read Health Fund  mapping text file                *
.*       V9.10.02  10/10/2008 J.Tan      CAR 173574                           *
.*                 Mods to read text file not index file                      *
.*       V9.10.01  23/05/2008 J.Tan      CAR 169149                           *
.*                 Added Date and Time invoice created/updated                *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
.
          INC       NZPFINFD/INC
          INC       NZPFIGFD/INC
          INC       NZPRFNFD/INC 
. 
          INC       PATCOMM/INC
          INC       PATCONFD/INC
.
          INC       AAEDE1FD/INC
          INC       AAEDTRFD/INC
          INC       EMRVISFD/INC
.
          INC       OUTPREFD/INC
.
          INC       PATCODFD/INC
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       PATDRGFD/INC
          INC       PATDTRFD/INC
          INC       PATFINFD/INC
          INC       PATFIGFD/INC
          INC       PATFN1FD/INC
          INC       PATGSRFD/INC
          INC       PATHSPFD/INC
          INC       PATINVFD/INC
          INC       PATIRNFD/INC
          INC       NZPIRNFD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
          INC       PATMCHFD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATRFNFD/INC
          INC       PATRFGFD/INC
          INC       PATVISFD/INC
          INC       BOKRC1FD/INC
          INC       IBACONFD/INC
.
.      EXTRA VARIABLES
.
ADEXISTS  FORM      1
.
BJDAY     FORM      3
CJDAY     FORM      3
CHG       FORM      1
CLCODE    INIT      "CL"
CURRDATE  DIM       8
CMDLINE   DIM       60
COUNTER   FORM      1
ERRDESC   DIM       60
ERRARRAY  DIM       60[9]
ERRFLAG   FORM      1
ERRCOUNT  FORM      1
.
FORM8     FORM      8
FORM9P2   FORM      9.2
FORM12P2  FORM      12.2
FINCODE1  DIM       13            4     FINANCE CODE
FINTYPE1  DIM       1             3     FINANCIAL TYPE
.
FINCODE2  DIM       13            4     FINANCE CODE
FINTYPE2  DIM       1             3     FINANCIAL TYPE
FGSTFLAG  FORM      1
NMPNUMB   DIM       20
.
SKEY40    DIM       40
STOFYEAR  DIM       8
FIELD     FORM      1
TDATE1    DIM       8
.
USERID    DIM       10
.
LASTDTE   DIM       8
.
FNDFLAG   FORM      1
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
MAPFLAG   FORM      1
MISCODE   DIM       9
MISCTTL   DIM       30
MISCODE1  DIM       9
MISCTTL1  DIM       30
MISCODE2  DIM       9
MISCTTL2  DIM       30
.
MAXDATE   DIM       8
CLAIM     DIM       3            * vars for PATMCHKY
HFUND     DIM       6
HFTAB     DIM       8
DIM15     DIM       15
CODEFI    INIT      "FI"
DIM20     DIM       20
DIM30     DIM       30
DIM8      DIM       8
DIM9      DIM       9 
FILEPATH  DIM       60
SAVEPATH  DIM       60
SCNDGNAM  DIM       40
CLMMPATH  DIM       60
HFNDPATH  DIM       60
KEY21A    DIM       21
KEY26A    DIM       26
FNAMET    DIM       8
FNAMEW    DIM       8
PATSYST   FORM      1
REMOVEZ   FORM      1
XALTNUMB  DIM       20      1399   Alternate ID                      (pmsaidaf)
. 
RDSCOUNT  FORM      8
ADDCOUNT  FORM      8
IURCOUNT  FORM      8
AHGCOUNT  FORM      8
IHGCOUNT  FORM      8
INVCOUNT  FORM      8
STACOUNT  FORM      8
SYSCOUNT  FORM      8
FNDCOUNT  FORM      8
CLMCOUNT  FORM      8
EODPAMNT  FORM      12.2
EODHAMNT  FORM      12.2
IODPAMNT  FORM      12.2
IODHAMNT  FORM      12.2
.
SAVCHG    DIM       9
SEC       FORM      2
SP12      INIT      "            "
SP14      INIT      "              "
SP16      INIT      "                "
SP36      INIT      "                                    "
SP40      INIT      "                                        "
SP50      INIT      "                             ":
                    "                     "
.
STADMNO   DIM       8
.
VALDFLAG  FORM      1             * Validation check flag
.                                     0 = writing data to database
.                                     1 = only validating import data file
.
OUTDFILE  FILE      ASCII, FIXED=256
.
.Name     Type   Length   Start   Description
.----     ----   ------   -----   -----------
TMPADMN   DIM        8        1   Admission Number
.TMPURNO   DIM        8        9   U/R Number
TMPINVN   DIM        8       17   Invoice Number
TMPINVD   DIM        8       25   Invoice Date
TMPINVA1  FORM      12.2     33   Patient Invoice Amount
TMPINVA2  FORM      12.2     48   Rebate Invoice Amount
TMPCLMV   DIM        3       63   Claim Code
TMPHFND   DIM        6       66   Health Fund
TMPHTAB   DIM        8       72   Health Fund table
TMPSYST   DIM        1       80   SYSTEM (1=AAE 2=OUTPAT 3=INPAT)
.EOR                81
.
TMPCLMAF  FILE      ASCII, FIXED=256
MAPCLMA1  IFILE     SQL, FIXED=7, KEY="UC1-3"
.
.Name     Type   Length   Start   Description
.----     ----   ------   -----   -----------
TMPOLDCL  DIM        3        1   Old Claim code
TMPNEWCL  DIM        3        4   New Claim code
.EOR                          7
.
TMPHFNAF  FILE      ASCII, FIXED=256
MAPHFNA1  IFILE     SQL, FIXED=29, KEY="UC1-6,7-14"
.
.Name     Type   Length   Start   Description
.----     ----   ------   -----   -----------
TMPOLDHF  DIM        6        1   Old Health fund 
TMPOLDTB  DIM        8        7   Old Health fund table
TMPNEWHF  DIM        6       15   New Health fund 
TMPNEWTB  DIM        8       21   New Health fund table
.EOR                         29
.
TMPINVA   FORM      12.2
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
PRGID     INIT      "IBABIL32"
PRGNAM    INIT      "Take Up Outstanding Debts"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KHOS0000                    * Keyin hospital id
.
          CALL      SRTF0000
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN1000  CALL      KITM0000                      * keyin Misc.items
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN2000,MAIN1000,MAIN9999 * Yes, no, cancel
.
MAIN2000  CALL      DSCR0000
.
          CALL      PROC0000                      * processing
.
MAIN9999  CHAIN     PGM
          STOP
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"patfinsf";
          OPEN      PATFINS1,"patfinsf"
          OPEN      NZPFINA1,"nzpfinaf"
.
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATINVR2,"patinvrf"
          OPEN      PATINVR3,"patinvrf"
          OPEN      PATINVR4,"patinvrf"
.
          DISPLAY   *P64:24,"patirnge";
          OPEN      PATIRNG1,"patirnge"
          OPEN      NZPIRNA1,"nzpirnaf"
.
          DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmchgf";
          OPEN      PATMCHG1,"patmchgf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      AAEDE1A1,"aaede1af"
          OPEN      AAEDTRE1,"aaedtref"
          OPEN      EMRVISA1,"emrvisaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN;*1,STADMNO
          READ      CONTROLF,TEN9;*212,CFEETYP,*216,CINVRIP
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,SEVENTY9;*68,PTCNMFEE,PTCNJFEE
          READ      CONTROLF,EIGHTY8;*94,CLACDATE
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          MATCH     SP3,PTCNDCLM
          IF        @EQUAL
            MOVE      "0  ",PTCNDCLM
          ENDIF
.
          UNPACK    CLACDATE WITH XCENT,XYEAR,XMON,XDAY
          PACK      MAXDATE WITH XCENT,XYEAR,XMON,XDAY
.
.         Now work out the start of the current financial year
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          IF        EXIT = 1
            DISPLAY   *P1:24,*EL,*B,"Current Date not on Period File. ";
            CALL      HITENTER
            GOTO      INIT9000
          ENDIF
.
          MOVE      "01",DRGNUM              * the first period of year
          PACK      KEY6,DRGYR,DRGNUM
          CALL      RDDRGA1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Start of Year not on Period File. ";
            CALL      HITENTER
            GOTO      INIT9000
          ENDIF
.
          MOVE      DRGFDTE,STOFYEAR         * start of this financial year
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS             * start time for input
          MOVE      "99",CLNO
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      "hos04deb",FNAMET
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9000  MOVE      ONE,EXIT
INIT9999  RETURN
+
.********************************************************************
.*                  ASKQ0000                                        *
.* Returns: VALDFLAG  - validation flag                             *
.*                       0 = normal mode                            *
.*                       1 = validation only mode                   *
.********************************************************************
ASKQ0000  MOVE      ANSY,ANS
          KEYIN     *P1:17,*EF,"Validation only (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P25:17,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P25:17,*V2LON,DYES,*HOFF:
                      *P40:17,"(No data will be uploaded)"
            MOVE      ONE,VALDFLAG               * yes
            GOTO      ASKQ9999
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P25:17,*V2LON,DNO,*HOFF:
                      *P40:17,"(Data will be uploaded)"
            MOVE      ZERO,VALDFLAG              * yes
            GOTO      ASKQ9999
          ENDIF
          GOTO      ASKQ0000                     * invalid input

ASKQ9999  RETURN
+
.********************************************************************
.*                          KITM0000                                *
.*  Keyin filename and Misc.debtor code                             *
.********************************************************************
KITM0000  DISPLAY   *P1:6,"Enter Miscellaneous Debt      :":
                    *P1:7,"Enter Miscellaneous H.F. Debt :";
.
          MOVE      FOUR,CVERT
          MOVE      "6",EVERT
          MOVE      "33",ECOL
          MOVE      ONE,CNEWDS
          MOVE      PTCNDCLM,CLAIM
          MOVE      SP6,HFUND
          MOVE      SP6,HCODE
          MOVE      SP10,HFTAB 
          CALL      PATMSCKY
          BRANCH    EXIT,KITM9000,KITM9000
          DISPLAY   *P50:6,MDESC
          MOVE      "A",FINTYPE1
          PACK      FINCODE1,ANSM,MMSCGRP,MCHARGE,SP20
          MOVE      MCHARGE,MISCODE1
          MOVE      MDESC,MISCTTL1
.
          MOVE      FIVE,CVERT
          MOVE      "33",ECOL
          MOVE      "7",EVERT 
          CALL      PATMSCKY
          BRANCH    EXIT,KITM9000,KITM9000
          DISPLAY   *P50:7,MDESC
.
          MOVE      "A",FINTYPE2
          PACK      FINCODE2,ANSM,MMSCGRP,MCHARGE,SP20
          MOVE      MCHARGE,MISCODE2
          MOVE      MDESC,MISCTTL2
.
.
          CALL      ASKQ0000                    * Ask questions/preferences
.
          MOVE      ZERO,EXIT
          GOTO      KITM9100
.
KITM9000  MOVE      ONE,EXIT
          GOTO      KITM9999
.
KITM9100  MOVE      ZERO,REMOVEZ
          KEYIN     *P1:22,*EL,"Do you want to remove leading zero's from UR":
                    " (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ONE,REMOVEZ
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ZERO,REMOVEZ
          ENDIF
.
KITM9999  RETURN
+
.********************************************************************
.*                          DSCR0000                                *
.*  Processing screen display                                       *
.********************************************************************
DSCR0000  DISPLAY   *P1:4,*EF
          IF        VALDFLAG = 1
            DISPLAY   *P1:6,"Validation mode  (No data will be uploaded)"
          ELSE
            DISPLAY   *P1:6,"Normal mode  (Data will be uploaded)"
          ENDIF
          DISPLAY   *P1:23,*EF,"Upload will commence."
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P1:13,*EF,"Processing ",*V2LON,FILEPATH,*HOFF:
                    *P1:15,"Started    : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:17,"U/R Number : ":
                    *P1:18,"Surname    : ":
                    *P1:19,"Records    : ":
                    *P1:24,"Processing ",*V2LON,FNAMET,*HOFF;
.
          MOVE      ZERO,RDSCOUNT         * initialise records read
          MOVE      ZERO,ADDCOUNT         * initialise added records
          MOVE      ZERO,IURCOUNT         * initialise invalid U/R no count
          MOVE      ZERO,AHGCOUNT         * initialise Adm.no too high count
          MOVE      ZERO,IHGCOUNT         * initialise Invoice no too high count
.
          MOVE      ZERO,INVCOUNT         * initialise existing inv.record count
          MOVE      ZERO,STACOUNT         * initialise Pat.Status record count
          MOVE      ZERO,SYSCOUNT         * initialise Pat.System record count
          MOVE      ZERO,FNDCOUNT         * initialise invalid fund record count
          MOVE      ZERO,CLMCOUNT         * initialise inv.claim code rec.count
          MOVE      ZERO,EODPAMNT         * initialise ODP amount (Emergency)
          MOVE      ZERO,EODHAMNT         * initialise ODH amount (Emergency)
          MOVE      ZERO,IODPAMNT         * initialise ODP amount (Inpatient)
          MOVE      ZERO,IODHAMNT         * initialise ODH amount (Inpatient)
.
DSCR9999  RETURN
+
.********************************************************************
.*                          PROC0000                                *
.*  Procesing outstanding debts from the outstanding debtors file   *
.********************************************************************
PROC0000  MOVE      ZERO,ERRFLAG
          OPEN      OUTDFILE,FILEPATH
.
PROC1000  CALL      RDINFILE
          BRANCH    OVRCD,PROC9000
.
          MOVE      ZERO,ERRFLAG
          MOVE      ZERO,TMPINVA
          MOVE      TMPINVA1,TMPINVA
          ADD       TMPINVA2,TMPINVA
.
          ADD       ONE,RDSCOUNT                 * increment records read
.
          MOVE      ZERO,IDAY
          MOVE      ZERO,IMON
          MOVE      ZERO,IYEAR
          MOVE      ZERO,ICENT
          MOVE      ZERO,ADEXISTS
.
          MOVE      ZERO,ERRCOUNT
          MOVE      ONE,F1
          WHILE     F1<=8
            MOVE      SP70,ERRARRAY[F1]
            ADD       ONE,F1
          DO
.
          CALL      CMAP0000
          CALL      FMUR0000                * format U/R number
.
          MOVE      TMPURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD=1
            UNPACK      SP70,PGNAME,PSNAME
            CLEAR       ERRDESC
            APPEND      "Invalid U/R No.: ",ERRDESC
            APPEND      TMPURNO,ERRDESC
            APPEND      SP70,ERRDESC
            RESET       ERRDESC
            CALL        LERR0000
            ADD         ONE,IURCOUNT
          ENDIF
.
          IF        (RDSCOUNT%1000)=0 | RDSCOUNT = 1
            DISPLAY   *P14:17,*V2LON,TMPURNO:
                      *P14:18,PSNAME:
                      *P14:19,RDSCOUNT;
          ENDIF
.
          MOVE      TMPADMN,KEY8
          CALL      GDET0000                 * Get patient details
.
          CALL      VLDF0000                 * validation fields
.
.         if Error found, print data with error message
.         if No Error found, print data only for Normal mode (not validating)
.
          BRANCH    ERRFLAG,PROC8200         * Found an error
.
          BRANCH    VALDFLAG,PROC8200        * Validation only - don't load
.
          MOVE      TMPURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD=1
            UNPACK    SP70,PGNAME,PSNAME
          ELSE
            MOVE      TMPHFND,PFUNDH
            MOVE      TMPHTAB,PFNDTB
            CALL      UPMAST1
          ENDIF
.
          CALL      FIXADMN
          CALL      FIXDTRA
          CALL      FIXINVR                 * set up Invoice
          CALL      FIXVISA
          CALL      FIXVX1A
.
.         Check for the visit file
.
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1
          COMPARE   ZERO,OVRCD
          GOTO      PROC5000 IF EQUAL
.
.         Write a new record for visit file
.
          CALL      WRVISA1
.
PROC5000  MOVE      PVIBILL,KEY8
          CALL      RAPMVX11
          COMPARE   ZERO,OVRCD
          GOTO      PROC5200 IF EQUAL
.
.         Write a new record for visit extension file
.
          CALL      WRPMVX11
.
PROC5200  COMPARE   ONE,PATSYST
          GOTO      PROC5800 IF NOT EQUAL     * Not A&E
.
          MOVE      PVIBILL,KEY8
          CALL      RAEMVIS1
          COMPARE   ZERO,OVRCD
          GOTO      PROC5800 IF EQUAL
.
.         Write a new record for Emergency visit file
.
          CALL      WREMVIS1
.
.         Write the invoice record
.
PROC5800  MOVE      TMPINVN,KEY8
          CALL      RDINV1
          IF        OVRCD=1
            PACK      PTINCDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTINCDAT             * date created
            CLOCK     TIME,PTINCTIM             * time created
            MOVE      PASSCODE,PTINCUID         * user id
            CALL      WRINV1
          ENDIF
.
PROC5900  PROC      FLAGDEBT                * set outstanding debt indicator
.
.         Write the DTRAN record
.
          COMPARE   ZERO,TMPINVA1
          GOTO      PROC6000 IF EQUAL
.
PROC5920  MOVE      TMPADMN,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      TRVISA1                 * set next transaction number
.
          IF        PATSYST=1
            MOVE      PVITRAN,ATTRANS
            MOVE      TMPINVA1,ATPATAMT
            MOVE      TMPINVA1,ATPATPOR
            MOVE      MISCODE1,ATITEMNO
            MOVE      MISCTTL1,ATDDESC
            PACK      KEY22,TMPADMN,ATINVNO,ATTRANS,SP70
            CALL      RDADTRE1
            COMPARE   ZERO,OVRCD
            GOTO      PROC5920 IF EQUAL
            CALL      WRDTRE1
          ELSE
            IF        PATSYST=3
              MOVE      PVITRAN,TTRANSN1
              MOVE      TMPINVA1,TPATAMTT
              MOVE      TMPINVA1,TPATAMTP
              MOVE      MISCODE1,TITEMNO
              MOVE      MISCTTL1,TDDESC
              PACK      KEY22 WITH TMPADMN,TREF,TTRANSN1,SP70
              CALL      RDADTRN1
              COMPARE   ZERO,OVRCD
              GOTO      PROC5920 IF EQUAL
              CALL      WRDTRN1
            ENDIF
          ENDIF
.
PROC6000  COMPARE   ZERO,TMPINVA2
          GOTO      PROC6200 IF EQUAL
.
PROC6020  MOVE      TMPADMN,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      TRVISA1                 * set next transaction number
.
          IF        PATSYST=1
            MOVE      PVITRAN,ATTRANS
            MOVE      TMPINVA2,ATPATAMT
            MOVE      TMPINVA2,ATPATPOR
            MOVE      MISCODE2,ATITEMNO
            MOVE      MISCTTL2,ATDDESC
            PACK      KEY22,TMPADMN,ATINVNO,ATTRANS,SP70
            CALL      RDADTRE1
            COMPARE   ZERO,OVRCD
            GOTO      PROC6020 IF EQUAL
            CALL      WRDTRE1
          ELSE
            IF        PATSYST=3
              MOVE      PVITRAN,TTRANSN1
              MOVE      TMPINVA2,TPATAMTT
              MOVE      TMPINVA2,TPATAMTP
              MOVE      MISCODE2,TITEMNO
              MOVE      MISCTTL2,TDDESC
              PACK      KEY22 WITH TMPADMN,TREF,TTRANSN1,SP70
              CALL      RDADTRN1
              COMPARE   ZERO,OVRCD
              GOTO      PROC6020 IF EQUAL
              CALL      WRDTRN1
            ENDIF
          ENDIF
.
.         Write Admission file
.
PROC6200  BRANCH    PATSYST,PROC7000
.
          MOVE      TTRANSN1,ATRANNO
          ADD       ONE,ATRANNO
.
          PACK      ACODDTE,CCC,CYY,CMM,CDD  * set coding date to current date
          REP       " 0",ACODDTE
.
          MOVE      TMPHFND,AFUNDH
          MOVE      TMPHTAB,AFNDTB
          MOVE      TMPADMN,KEY8
          BRANCH    ADEXISTS OF PROC6800
          CALL      WRMISS1
          GOTO      PROC7000
.
PROC6800  CALL      UPMISS1
          CALL      UUPTMIS1
.
PROC7000  MOVE      PINVNO,FININVN
          MOVE      PINVADM,FINADMN
          MOVE      PINVUR,FINURNO
.
          COMPARE   ZERO,TMPINVA1
          GOTO      PROC7800 IF EQUAL
.
          MATCH     TMPINVD,STOFYEAR
          IF        @LESS
            PACK      FINDATE,TMPINVD
          ELSE
            PACK      FINDATE,STOFYEAR
          ENDIF
          REP       " 0",FINDATE
.
          MOVE      TMPINVA1,FINAMT
          MOVE      FINTYPE1,FINTYPE
          MOVE      FINCODE1,FINCODE
          MOVE      PTHSHOSP,FINHOSP
          IF        PTCNMFEE=1
            UNPACK    FINCODE1,DIM1,MMSCGRP,MCHARGE,DIM15
.                              Misc. Charge, Group Code, Item Code
            MATCH     MMSCGRP,SP3
            IF        @EQUAL
              PACK      FINCODE,ANSM,ACLAIM,MCHARGE,SP20
.                               Misc. Charge, Claim Code, Item Code
            ELSE
              MOVE      "-",DIM1
              PACK      FINCODE,ANSM,ACLAIM,DIM1,MMSCGRP,SP20
.                               Misc. Charge, Claim Code, Minus, Group Code
            ENDIF
          ENDIF
          MOVE        PATSYST,FINSYS
          MOVE        SP6,FINSITE
          MOVE        ZERO,FGSTFLAG
          CALL        PATCALF
.
PROC7800  COMPARE     ZERO,TMPINVA2
          GOTO        PROC8000 IF EQUAL
.
          MATCH       TMPINVD,STOFYEAR
          IF          @LESS
            PACK        FINDATE,TMPINVD
          ELSE
            PACK        FINDATE,STOFYEAR
          ENDIF
          REP         " 0",FINDATE
.
          MOVE        TMPINVA2,FINAMT
          MOVE        FINTYPE2,FINTYPE
          MOVE        FINCODE2,FINCODE
          MOVE        PTHSHOSP,FINHOSP
          IF          PTCNMFEE=1
            UNPACK      FINCODE2,DIM1,MMSCGRP,MCHARGE,DIM15
.                                Misc. Charge, Group Code, Item Code
            MATCH       MMSCGRP,SP3
            IF          @EQUAL
              PACK        FINCODE,ANSM,ACLAIM,MCHARGE,SP20
.                                 Misc. Charge, Claim Code, Item Code
            ELSE
              MOVE        "-",DIM1
              PACK        FINCODE,ANSM,ACLAIM,DIM1,MMSCGRP,SP20
.                                 Misc. Charge, Claim Code, Minus, Group Code
            ENDIF
          ENDIF
          MOVE        PATSYST,FINSYS
          MOVE        SP6,FINSITE
          MOVE        ZERO,FGSTFLAG
          CALL        PATCALF
.
PROC8000  ADD         ONE,ADDCOUNT
.
PROC8200  CALL        PRTD000
          GOTO        PROC1000
.
PROC9000  CALL        PRTT0000
PROC9999  RETURN
+
.******************************************************************************
.         Fields Validation
.******************************************************************************
VLDF0000  CALL      CMAX0000                 * Check for Max Inv./Adm.No
.
.         check with Claim mapping file
.
          PACK      KEY3,TMPCLMV
          IF        MAPFLAG=0
            CALL      RDTMCLA1
            IF        OVRCD=0
              MOVE      TMPNEWCL,KEY3
            ENDIF
          ENDIF
.
          PACK       KEY5,ANSC,ANSL,KEY3,SP70
          CALL       RDCODE1
          COMPARE    ZERO,OVRCD
          GOTO       VLDF4000 IF EQUAL     * found a valid mapping claim
.
          CLEAR      ERRDESC
          APPEND     "Invalid Claim: ",ERRDESC
          APPEND     KEY3,ERRDESC
          APPEND     SP70,ERRDESC
          RESET      ERRDESC
          CALL       LERR0000
          ADD        ONE,CLMCOUNT
.
VLDF4000  PACK       KEY14,TMPHFND,TMPHTAB,SP70
          IF         FNDFLAG=0
            CALL       RDTMHFA1
            IF         OVRCD=0
              PACK       KEY14,TMPNEWHF,TMPNEWTB,SP70
            ENDIF
          ENDIF
.
          MATCH       SP70,KEY14
          GOTO        VLDF9999 IF EQUAL
          GOTO        VLDF9999 IF EOS
.
          CALL        RDFUND1
          IF          OVRCD=1
            CLEAR       ERRDESC
            APPEND      "Invalid Health Fund: ",ERRDESC
            APPEND      KEY14,ERRDESC
            APPEND      SP70,ERRDESC
            RESET       ERRDESC
            CALL        LERR0000
            ADD         ONE,FNDCOUNT
          ENDIF
.
VLDF9999  RETURN
+
.********************************************************************
.         CMAX0000                                                  *
.         Check for Maximum Invoice number and Admission number     *
.********************************************************************
CMAX0000  READ      CONTROLF,HUND30;*75,PTCNUANV      * Using AN Visit Numbers
.
          MATCH     "1",PTCNUANV
          IF        @EQUAL
            READ      CONTROLF,HUND30;*76,PTCNNXTV
            MATCH     TMPADMN,PTCNNXTV
            GOTO      CMAX1000 IF LESS
          ELSE
            MOVE      ZEROVISN,DIM8
            MOVE      TMPADMN,DIM8
            MATCH     STADMNO,DIM8  
            GOTO      CMAX1000 IF LESS
          ENDIF
.
          CLEAR     ERRDESC
          APPEND    "Admission Number Too High: ",ERRDESC
          APPEND    TMPADMN,ERRDESC
          APPEND    SP70,ERRDESC
          RESET     ERRDESC
          CALL      LERR0000
          ADD       ONE,AHGCOUNT
.
.         Get the next invoice number in range
.
CMAX1000  MOVE      ZERO,FORM8
          MOVE      TMPINVN,FORM8
          COMPARE   IRNEXT,FORM8
          GOTO      CMAX2000 IF LESS
.
          CLEAR     ERRDESC
          APPEND    "Invoice Number Too High: ",ERRDESC
          APPEND    TMPINVN,ERRDESC
          APPEND    SP70,ERRDESC
          RESET     ERRDESC
          CALL      LERR0000
          ADD       ONE,IHGCOUNT
.
CMAX2000  MOVE      TMPINVN,KEY8
          CALL      RDINV1
          IF        OVRCD=0
            UNPACK      PINVDTE,CCENT,CYEAR,CMON,CDAY
            CALL        PACDATE
            CLEAR       ERRDESC
            APPEND      "Invoice No.",ERRDESC
            APPEND      PINVNO,ERRDESC
            APPEND      " already exists ",ERRDESC
            APPEND      "with Inv.date:",ERRDESC
            APPEND      CPCDATE,ERRDESC
            APPEND      SP70,ERRDESC
            RESET       ERRDESC
            CALL        LERR0000
            ADD         ONE,INVCOUNT
          ENDIF
.
CMAX9999  RETURN
+
.******************************************************************************
.         Get patient details
.******************************************************************************
GDET0000  MOVE      ZERO,PATSYST
          MOVE      TMPSYST,PATSYST
          BRANCH    PATSYST,GDET1000,GDET9200,GDET3000
          GOTO      GDET9200
.
.         A&E
.
GDET1000  CALL      RDDETA1
          BRANCH    OVRCD,GDET8000
.
          MOVE      ONE,ADEXISTS
          GOTO      GDET8000
.
.         Inpatient
.
GDET3000  CALL      RDMISS1
          BRANCH    OVRCD,GDET8000
.
          MOVE      ONE,ADEXISTS
          BRANCH    ASTAT OF GDET9000,GDET8000,GDET8000,GDET8000
          GOTO      GDET9000
.
GDET8000  MOVE      ZERO,EXIT
          GOTO      GDET9999
.
GDET9000  CLEAR     ERRDESC
          APPEND    "Invalid Admission Status: ",ERRDESC
          APPEND    ASTAT,ERRDESC
          APPEND    SP70,ERRDESC
          RESET     ERRDESC
          CALL      LERR0000
          ADD       ONE,STACOUNT
          GOTO      GDET9999
.
GDET9200  CLEAR     ERRDESC
          APPEND    "Invalid Patient System: ",ERRDESC
          APPEND    PATSYST,ERRDESC
          APPEND    SP70,ERRDESC
          RESET     ERRDESC
          CALL      LERR0000
          ADD       ONE,SYSCOUNT
GDET9999  RETURN
+
.******************************************************************************
.*                           FMUR0000                                         *
.*      Format the U/R by removing leading zeroes and right justifying        *
.******************************************************************************
FMUR0000  MATCH     SP70,TMPURNO
          GOTO      FMUR0100 IF NOT EQUAL
.
          SQUEEZE   TMPURNO
          PACK      PURNO,TMPURNO
          RJUSTIFY  PURNO
          MOVE      PURNO,TMPURNO
.
FMUR0100  MOVE      ZERO,COUNTER
          WHILE     COUNTER < 7
            ADD       ONE,COUNTER
            CMOVE     TMPURNO,ANS
            MATCH     SP1,ANS
            IF        !@EQUAL
              MATCH     "0",ANS
              IF        @EQUAL
                IF        REMOVEZ=1
                  CMOVE     SP1,TMPURNO
                ELSE  
                  GOTO    FMUR0500
                ENDIF 
              ELSE
                GOTO    FMUR0500
              ENDIF
            ENDIF
            BUMP      TMPURNO
          DO

FMUR0500  RESET     TMPURNO
          PACK      DIM8,TMPURNO,SP8
.
FMUR9999  RETURN
+
.********************************************************************
.         Checking claim mapping
.********************************************************************
CMAP0000 BRANCH       MAPFLAG,CMAP2000
         PACK         KEY3,TMPCLMV
         CALL         RDTMCLA1
         BRANCH       OVRCD,CMAP2000
         MOVE         TMPNEWCL,TMPCLMV
.
CMAP2000 BRANCH       FNDFLAG,CMAP9999
         PACK         KEY14,TMPHFND,TMPHTAB,SP70
         CALL         RDTMHFA1
         BRANCH       OVRCD,CMAP9999
         MOVE         TMPNEWHF,TMPHFND
         MOVE         TMPNEWTB,TMPHTAB
.
CMAP9999 RETURN
+
.********************************************************************
.        Set up Admission record for writing
.********************************************************************
FIXADMN  BRANCH     PATSYST,FIXAD10,FIXAD99,FIXAD30
         GOTO       FIXAD99
.
.        A&E
.
FIXAD10  BRANCH     ADEXISTS,FIXAD99
         MOVE       TMPADMN,KEY8
         MOVE       TMPURNO,ADAURNO
         MOVE       TMPADMN,ADANUMB
         MOVE       "00000000",ADADATE
         MOVE       "00:00   ",ADATIME
         MOVE       TMPHFND,AEDAFUND
         MOVE       TMPHTAB,AEDATBLE
         MOVE       TMPCLMV,ADACOMP
         UNPACK     SP70,ADACLASS,ADAINSUR,ADASRC,ADASIT,ADAMODE
         UNPACK     SP70,ADAFILL,ADADOCT,ADALOCK
         MOVE       "00:00   ",ADASEEN
         MOVE       ZERO,ADAADMIT
         MOVE       ZERO,ADADURN
         UNPACK     SP70,ADAEMPL,ADAPOST
         MOVE       SP70,ADAADD1
         MOVE       SP70,ADAADD2
         MOVE       SP70,ADASUBR
         MOVE       SP70,ADASUBR
         UNPACK     SP70,ADATELE,ADACONT
         UNPACK     SP70,ADAUSR1,ADAUSR2,ADAUSR3,ADAUSR4,ADAUSR5
         MOVE       SP70,ADADIAG
         MOVE       ZERO,ADAFIL2
         UNPACK     SP70,ADAWAIT,ADASDTE,ADAPREV
         MOVE       ZERO,AEDAPVIS
         UNPACK     SP70,AEDARFGP,AEDAGPPC,AEDATIAS,AEDADTAT,AEDATESI,AEDAEMPL
         UNPACK     SP70,AEDARTAS,AEDAINIT,AEDAPLIN,AEDADIAS,AEDARNEN,AEDACONS
         UNPACK     SP70,AEDAGPPT,AEDAPRTM,AEDARDOC,ADAPRTY,AEDAOPER
         MOVE       SP70,AEDASPAR
         MOVE       ZERO,AEDANPNA
         CALL       WRDETA1
         GOTO       FIXAD99
.
.        Inpatient
.
FIXAD30  BRANCH     ADEXISTS OF FIXAD32
         MOVE       TMPURNO,AURNO
         MOVE       TMPADMN,AADMNO
         MOVE       "00000000",ADATE
         MOVE       "00:00   ",ATIME
         MOVE       ZERO,AWARD
         MOVE       ZERO,ABED
         MOVE       SP6,ADOCTR
         MOVE       SP30,ARDRNAM
         MOVE       SP6,ADOCTA
         MOVE       SP3,ASRCE
         MOVE       SP3,ATYPE
         MOVE       SP3,ACLSS
         MOVE       SP3,ACARE
         MOVE       TMPHFND,AFUNDH
         MOVE       TMPHTAB,AFNDTB
         MOVE       SP10,AFNDMM
         MOVE       ZERO,AELIG
         MOVE       ZERO,AVISIT
         MOVE       ZERO,AALERG
         MOVE       ZERO,ADISC
         MOVE       ZERO,AALLOW
         MOVE       SP50,ADOCTL
         MOVE       ZERO,APURGE
         MOVE       THREE,ASTAT
         MOVE       ZERO,AILLN
         MOVE       TMPCLMV,ACLAIM
         MOVE       ZERO,ASTAY
         MOVE       ONE,ATRANNO
         MOVE       ZERO,AINVPRT
         MOVE       SP8,ALACDTE
         MOVE       SP6,ADIET
         MOVE       SP6,ADOCTT
         MOVE       SP30,PTMSSPAR
         MOVE       ZERO,APORT
         MOVE       ZERO,ADSCHI
         MOVE       ANSN,PTMICMXP
         UNPACK     SP70,PTMIPCMX,PTMIGSTA,PTMIEBED,PTMIDMOV
.
FIXAD32  MOVE       ATRANNO,TTRANSN1
         ADD        ONE,ATRANNO
         ADD        ONE,AINVPRT
.
         MOVE       ALACDTE,TDATE1
.
         MATCH      MAXDATE,TDATE1
         RETURN     IF NOT LESS
         MOVE       MAXDATE,ALACDTE
.
FIXAD99  RETURN
.
.********************************************************************
.        Set up Invoice record for writing
.********************************************************************
FIXINVR  CALL       CLPATINV                * clear Invoice record

         MOVE       TMPINVN,PINVNO
         MOVE       TMPINVD,PINVDTE
         MOVE       PSNAME,PINALPHA
.
         MOVE       TMPADMN,PINVADM
         MOVE       TMPURNO,PINVUR
         MOVE       TMPINVA,PINVAMT
         MOVE       TMPINVA2,PINVREB       * H.F Invoice amount
         MOVE       TMPCLMV,PINVCLM
         MOVE       PATSYST,PINVSYS
         MOVE       TMPHFND,PTINHFND
.
         RETURN
.
.********************************************************************
.        Set up Debtors Transaction Record for writing
.********************************************************************
FIXDTRA  BRANCH     PATSYST,FIXDT10,FIXDT99,FIXDT30
         GOTO       FIXDT99
.
.        A&E
.
FIXDT10  MOVE       TMPADMN,ATNUMB
         MOVE       TMPINVA,ATPATAMT
         MOVE       TMPINVA,ATPATPOR
         MOVE       ZERO,ATREBPOR
         MOVE       TMPINVN,ATINVNO
         MOVE       TMPINVD,ATDDATE
         UNPACK     SP70,ATITEMNO,ATRECEPT,ATMISGRP,ATDEPTYP,ATBATCHN
         MOVE       SP70,ATDDESC
         MOVE       ZERO,ATPAYTYP
         MOVE       ONE,ATINVPRT
         MOVE       "1",ATRECTYP
         MOVE       "DB",ATTYPE
         MOVE       ONE,ATNINVS
         MOVE       ONE,ATSERVS
         UNPACK     SP70,ATDTEFFD
         MOVE       ZERO,ATSPARE1
         MOVE       ZERO,ATSPARE2
         MOVE       ZERO,ATDTGSTA
         UNPACK     SP70,ATDTGSTC
         MOVE       "0",ATDTCRST
         MOVE       ZERO,ATDTGSTM
.
         UNPACK     SP70,ATDTBTCH,ATDTSUBN,ATDTEDAD,ATDTSVTM,ATDTITYP
         MOVE       ZERO,ATDTSCHF
         MOVE       SP70,ATDTRBRS
         MOVE       SP70,ATDTPCOD
         PACK       ATDTACOI,SP70       * After Care Override Indicator
         PACK       ATDTDSOV,SP70       * Duplicate Service Override
         PACK       ATDTSTXT,SP70       * Service Text
         PACK       ATDTLSPN,SP70       * Location Specific Practice No(LSPN)
         PACK       ATDTMPOV,SP70       * Multi Procedure Override
         PACK       ATDTNMPT,SP70       * Number of Patients Seen
         PACK       ATDTSDCD,SP70       * Self Deem Code (Cat Sd)
         PACK       ATDTTDUR,SP70       * Time Duration (Mins)
         PACK       ATDTROVR,SP70       * Restricted Override Code (Cat Ro)
         MOVE       SP70,ATSPARE
.
         GOTO       FIXDT99
.
.        Inpatient
.
FIXDT30  MOVE       TMPADMN,TADMNO
         MOVE       TMPURNO,TDCODE
         MOVE       TMPINVA,TPATAMTT
         MOVE       ZERO,TPATAMTG
         MOVE       ZERO,TPATAMTH
         MOVE       ZERO,TPATAMTI
         MOVE       ZERO,TPATAMTP
         MOVE       ZERO,TREBATE
         UNPACK     TMPINVD WITH XCENT,XYEAR,XMON,XDAY
         MOVE       XCENT,TFCENT
         MOVE       XYEAR,TFYEAR
         MOVE       XMON,TFMONTH
         MOVE       XDAY,TFDAY
         MOVE       ZERO,TTCENT
         MOVE       ZERO,TTYEAR
         MOVE       ZERO,TTMONTH
         MOVE       ZERO,TTDAY
         MOVE       "DB",TTYPE
         MOVE       "P",TTYPEDEB
         MOVE       TMPINVN,TREF
         MOVE       SP1,TPAYTYPE
         MOVE       SP8,TRECEIPT
         MOVE       ONE,TINVPRT
         MOVE       THREE,TRECTYPE
         MOVE       ZERO,TNODAYS
         MOVE       ONE,TCLAIM
         MOVE       ONE,TSERVS
.
         PACK       PTDTDES2,SP20,SP20
         MOVE       ZERO,PTDTLSPT
         MOVE       ZERO,PTDTLSRB
         MOVE       ZERO,PTDTDTYP
         MOVE       ZERO,PTDTCCU
         MOVE       ZERO,PTDTGSTA
         MOVE       ZERO,PTDTGSTM
         MOVE       ZERO,PTDTGSTL
         UNPACK     SP20,PTDTGSTC,PTDTEFFD
         MOVE       ZERO,PTDTEPSD
         MOVE       SP70,PTDTTMNO
         MOVE       SP70,PTDTPMBS
         MOVE       SP70,PTDTPCOD
FIXDT99  RETURN
.
.********************************************************************
.        Set up Patient Visitor Record for writing
.********************************************************************
FIXVISA  BRANCH     PATSYST,FIXV100,FIXV999,FIXV300
         GOTO       FIXV999
.
FIXV100  MOVE       ADAURNO,PVIURNO
         MOVE       ADADATE,PVIDATE
         REP        " 0",PVIDATE
         MOVE       ADANUMB,PVIBILL
         MOVE       ONE,PVITYPE
	 MOVE       ADADOCT,PVIDOCT
         MOVE       ZERO,PVISTAT
         MOVE       SP2,PVILOCK
         MOVE       ONE,PVITRAN
         MOVE       SP30,PVISPAR
         GOTO       FIXV999
.
FIXV300  MOVE       AURNO,PVIURNO
         MOVE       ADATE,PVIDATE
         REP        " 0",PVIDATE
         MOVE       AADMNO,PVIBILL
         MOVE       THREE,PVITYPE
         MOVE       ADOCTA,PVIDOCT
         MOVE       ONE,PVISTAT
         MOVE       SP2,PVILOCK
         MOVE       ONE,PVITRAN
         MOVE       SP30,PVISPAR
FIXV999  RETURN
.
.********************************************************************
.        Set up Visit extension record
.********************************************************************
FIXVX1A  CALL       CLPMSVX1
         MOVE       PVIBILL,PMVXVISN
         MOVE       PVIDATE,PMVXVSDT
         MOVE       PTHSHOSP,PMVXMHOS
.
         COMPARE    ONE,PATSYST
         RETURN     IF NOT EQUAL           * Not A&E
.
         CALL       CLEMRVIS
         MOVE       PVIBILL,EMVIADMN
         MOVE       "3",EMVISTAT
         MOVE       PVIURNO,EMVIURNO
         MOVE       PVIDATE,EMVIDATE
         RETURN
+
.
.********************************************************************
.        Subroutine to print data
.********************************************************************
PRTD000  COMPARE    "55",CLNO
         CALL       HEAD IF NOT LESS
.
         UNPACK     TMPINVD,CCENT,CYEAR,CMON,CDAY
         CALL       PACDATE
         MOVE       TMPINVA,FORM9P2
         IF         ERRFLAG=1
           PRINT      *1," ****** ERRROR ******"
         ENDIF
         PRINT      *1,TMPADMN,*10,TMPURNO,*19,PGNAME,*45,PSNAME:
                    *69,TMPINVN,*82,CPCDATE:
                    *95,FORM9P2,*109,TMPCLMV,*115,TMPSYST:
                    *121,TMPHFND,SLASH,TMPHTAB
         ADD        ONE,CLNO
.
         COMPARE    ZERO,TMPINVA1
         GOTO       PRTD100 IF EQUAL
         MOVE       TMPINVA1,FORM9P2
         PRINT      *45,"(",MISCODE1,")  ",MISCTTL1,*95,FORM9P2
         ADD        ONE,CLNO
.
PRTD100  COMPARE    ZERO,TMPINVA2
         GOTO       PRTD200 IF EQUAL
         MOVE       TMPINVA2,FORM9P2
         PRINT      *45,"(",MISCODE2,")  ",MISCTTL2,*95,FORM9P2
         ADD        ONE,CLNO
.
PRTD200  COMPARE    ZERO,ERRFLAG
         GOTO       PRTD800 IF EQUAL      * No error
.
         MOVE       ONE,F1
         WHILE      F1<=ERRCOUNT
           MATCH     SP70,ERRARRAY[F1]
           IF        !@EQUAL
             PRINT     *19,ERRARRAY[F1]
             ADD       ONE,CLNO            * increment page line count
           ENDIF
           ADD       ONE,F1
         DO
.
PRTD800  MOVE       TMPSYST,PATSYST
. *** don't include in total loaded if an error occurred *****
         BRANCH     ERRFLAG,PRTD999
.
         IF         PATSYST=1
           ADD        TMPINVA1,EODPAMNT    * Patient Invoice Amount (AAE)
           ADD        TMPINVA2,EODHAMNT    * Rebate Invoice Amount
         ELSE
           IF         PATSYST=3
             ADD        TMPINVA1,IODPAMNT  * Patient Invoice Amount (INP)
             ADD        TMPINVA2,IODHAMNT  * Rebate Invoice Amount
           ENDIF
         ENDIF
.
PRTD999  RETURN
+
.********************************************************************
.        Print Header 
.********************************************************************
HEAD     MOVE       ONE,CNOUNDLN
         IF         VALDFLAG=1
           MOVE       "- Error Report ",CPHDROPT
         ELSE
           MOVE       "- Upload Data",CPHDROPT
         ENDIF
         CALL       HEAD132
.
         PRINT      *N,*N,*2,"Adm ##",*11,"U/R ##",*19,"Patient Given Names":
                    *45,"Patient Surname",*68,"Invoice ##":
                    *80,"Invoice Date",*95,"Invoice Amt":
                    *109,"Claim",*115,"Sys",*121,"Health Fund":
                    *N,"--------",*10,"--------",*19,"--------------------":
                    "-----",*45,"--------------------",*68,"---------":
                    *80,"------------",*95,"-----------":
                    *109,"------------------------"
         MOVE       SIX,CLNO
         RETURN
.
.********************************************************************
.        Printing end of report
.********************************************************************
PRTT0000  COMPARE   ZERO,CPAGENO
          GOTO      PRTT1000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*V2LON,*P20:24:
                    "** Report Generation Completed **",*W;
.
PRTT1000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P14:19,*V2LON,RDSCOUNT,*HOFF:
                    *P1:21,"Finished   : ",*V2LON,CTIMEIS
.
.         Warn the user if there were any records with errors
.
          MOVE      IURCOUNT,FORM8
          ADD       AHGCOUNT,FORM8
          ADD       IHGCOUNT,FORM8
          ADD       STACOUNT,FORM8
          ADD       SYSCOUNT,FORM8
          ADD       INVCOUNT,FORM8
          ADD       CLMCOUNT,FORM8
          ADD       FNDCOUNT,FORM8
.
          IF        FORM8 >0
            DISPLAY   *P1:22,*V2LON,*BLINKON,"Warning:  Records found with ":
                      "errors.  Refer to error report."
          ENDIF
.
          PRINT     *N:
                    *1,"Total records read                    - ",RDSCOUNT,*N:
                    *1,"Records added                         - ",ADDCOUNT,*N:
                    *1,"Invalid U/R Number detected           - ",IURCOUNT,*N:
                    *1,"Admission Number too High detected    - ",AHGCOUNT,*N:
                    *1,"Invoice number too High detected      - ",IHGCOUNT,*N:
                    *1,"Invalid Admission Status detected     - ",STACOUNT,*N:
                    *1,"Invalid Patient System detected       - ",SYSCOUNT,*N:
                    *1,"Existing Invoice number detected      - ",INVCOUNT,*N:
                    *1,"Records with invalid claim code       - ",CLMCOUNT,*N:
                    *1,"Records with invalid fund detected    - ",FNDCOUNT,*N;
.
          MOVE      EODPAMNT,FORM12P2
          ADD       EODHAMNT,FORM12P2
          PRINT  *N,*1,"Total Patient Debt (ODP) AAE          - ",EODPAMNT,*N:
                    *1,"Total     H.F Debt (ODH) AAE          - ",EODHAMNT,*N:
                    *1,"Total AAE (System 1)                  - ",FORM12P2,*N;
.
          MOVE      IODPAMNT,FORM12P2
          ADD       IODHAMNT,FORM12P2
          PRINT  *N,*1,"Total Patient Debt (ODP) INPAT        - ",IODPAMNT,*N:
                    *1,"Total     H.F Debt (ODH) INPAT        - ",IODHAMNT,*N:
                    *1,"Total INPAT (System 3)                - ",FORM12P2
.
          MOVE      EODPAMNT,FORM12P2
          ADD       EODHAMNT,FORM12P2
          ADD       IODPAMNT,FORM12P2
          ADD       IODHAMNT,FORM12P2
          PRINT  *N,*1,"Grand Total                           - ",FORM12P2
.
          PRINT     *N,"**** End of Report ****";
.
          IF        VALDFLAG=0
            DISPLAY   *P1:24,*EL,*B,"Upload complete.  ";
          ENDIF
          CALL      HITENTER
.
PRTT9999  RETURN
+
.*****************************************************************************
.*                              LERR0000           Called by: VCOD0000       *
.*                   Load  an error detail line                              *
.*****************************************************************************
LERR0000  ADD       ONE,ERRCOUNT
          PACK      ERRARRAY[ERRCOUNT],ERRDESC,SP70
          MOVE      ONE,ERRFLAG
.
LERR9999  RETURN
+
.********************************************************************
.                SRTF0000                                           *
.********************************************************************
SRTF0000  IF        CFEETYP=5
            PACK      KEY6,PTHSHOSP,CINVRIP
            CALL      RDNZIRN1
            BRANCH    OVRCD OF SRTF8900
            MOVE      NZIRNEXT,IRNEXT
          ELSE
            MOVE      CINVRIP,KEY3
            CALL      RDIRNG1
            BRANCH    OVRCD OF SRTF8900
          ENDIF
.
          CALL      KPTH0000            * Get the path
          BRANCH    EXIT,SRTF9000
.
          MOVE      ZERO,FNDFLAG
          MOVE      ZERO,MAPFLAG
          TRAP      SRTF3000 IF IO
          OPEN      OUTDFILE,FILEPATH
          TRAPCLR   IO
.
.         Open Claim mapping text file
.
          TRAP      SRTF4000 IF IO
          OPEN      TMPCLMAF,CLMMPATH
          TRAPCLR   IO
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "iserase mapclmaf",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Create Claim mapping index file
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild mapclmaf",CMDLINE
          APPEND    " 7 UC1-3",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      MAPCLMA1,"mapclmaf"
SRTF1000  MOVE      SP70,KEY3
          CALL      RSTMCLA1
          CALL      RKTMCLA1
          BRANCH    OVRCD,SRTF2000
          PACK      KEY3,TMPOLDCL,SP70
          CALL      DETMCLA1
          GOTO      SRTF1000
.
.         Copy from Claim mapping text file to index file
.
SRTF2000  CALL      RDCLFILE
          BRANCH    OVRCD,SRTF5000
.
          MOVE      TMPOLDCL,KEY3
          CALL      RATMCLA1
          COMPARE   ZERO,OVRCD
          GOTO      SRTF2000 IF EQUAL
.
          CALL      WRTMCLA1
          GOTO      SRTF2000
.
SRTF3000  BEEP
          DISPLAY   *P1:23,*EL,"** O/S DEBTORS FILE ":
                    " NOT FOUND **",FILEPATH;
          CALL      HITENTER
          DISPLAY   *P1:23,*EL;
          NORETURN
          GOTO      SRTF0000
.
SRTF4000  BEEP
          DISPLAY   *P1:23,*EL,"** WARNING NO CLAIM CODE MAPPING FILE ** ":
                    CLMMPATH;
          NORETURN
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,SRTF4800,SRTF0000       * Yes, no, cancel
          GOTO      SRTF9000 
.
SRTF4800  MOVE      ONE,MAPFLAG
.
.         Open Health fund mapping text file
.
SRTF5000  TRAP      SRTF8000 IF IO
          OPEN      TMPHFNAF,HFNDPATH
          TRAPCLR   IO
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "iserase maphfnaf",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Create HF mapping index file
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild maphfnaf",CMDLINE
          APPEND    " 29 UC1-6,7-14",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      MAPHFNA1,"maphfnaf"
SRTF5200  MOVE      SP70,KEY14
          CALL      RSTMHFA1
          CALL      RKTMHFA1
          BRANCH    OVRCD,SRTF6000
          PACK      KEY14,TMPOLDHF,TMPOLDTB,SP70
          CALL      DETMHFA1
          GOTO      SRTF5200
.
.         Copy from HF mapping text file to index file
.
SRTF6000  CALL      RDHFFILE
          BRANCH    OVRCD,SRTF9999
.
          PACK      KEY14,TMPOLDHF,TMPOLDTB,SP70
          CALL      RATMHFA1
          COMPARE   ZERO,OVRCD
          GOTO      SRTF6000 IF EQUAL
.
          PACK      TMPNEWHF,TMPNEWHF,SP70
          PACK      TMPNEWTB,TMPNEWTB,SP70
          CALL      WRTMHFA1
          GOTO      SRTF6000
.
SRTF8000  BEEP
          DISPLAY   *P1:23,*EL,"** WARNING NO HEALTH FUND MAPPING FILE ** ":
                    HFNDPATH;
          NORETURN
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,SRTF8800,SRTF0000       * Yes, no, cancel
          GOTO      SRTF9000
.
SRTF8800  MOVE      ONE,FNDFLAG
          MOVE      ZERO,EXIT
          GOTO      SRTF9999
.
SRTF8900  DISPLAY   *P1:24,*EL,*B:
                    "Inpatient Invoice range not setup. ";
          CALL      HITENTER
.
SRTF9000  MOVE      ONE,EXIT
SRTF9999  RETURN
+
.**********************************************************************
.         Get File and path name
.**********************************************************************
KPTH0000  MOVE      SP70,FILEPATH
          DISPLAY   *P1:5,"Enter Path Name               :";
          KEYIN     *P33:5,*EL,FILEPATH
          RESET     FILEPATH
          GOTO      KPTH9000 IF EOS
.
          ENDSET    FILEPATH
.
          CMATCH    "/",FILEPATH
          GOTO      KPTH2200 IF NOT EQUAL
.
          LENSET    FILEPATH
          RESET     FILEPATH
.
          PACK      FILEPATH,FILEPATH,SP30,SP30,SP30,SP30
          STRIP     FILEPATH
          MOVE      FILEPATH,SAVEPATH
          STRIP     SAVEPATH
.
          ENDSET    FILEPATH
          APPEND    FNAMET,FILEPATH
          APPEND    ".txt",FILEPATH
          RESET     FILEPATH
.
          CLEAR     CLMMPATH
          APPEND    SAVEPATH,CLMMPATH
          APPEND    "tmpclmaf.txt",CLMMPATH
          APPEND    SP70,CLMMPATH
          RESET     CLMMPATH
.
          CLEAR     HFNDPATH
          APPEND    SAVEPATH,HFNDPATH
          APPEND    "tmphfnaf.txt",HFNDPATH
          APPEND    SP70,HFNDPATH
          RESET     HFNDPATH
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH2200  DISPLAY   *P1:24,*EL,*B,"Path must end with a slash (/)";
          CALL      HITENTER
          GOTO      KPTH0000
.
KPTH9000  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.********************************************************************
GETSVAR
CLPATMAS  RETURN
.
.********************************************************************
.         Enter selected Hospital code
.********************************************************************
KHOS0000  DISPLAY   *P1:4,*EF;
.
          COMPARE   FIVE,CFEETYP
          GOTO      KHOS1000 IF EQUAL       * NZ
          COMPARE   ONE,IBCNMHOS
          GOTO      KHOS3000 IF NOT EQUAL
.
KHOS1000  IF        CFEETYP=5
            DISPLAY   *P1:4,"Hospital Id                   :";
          ELSE
            IF        IBCNMHOS=1
              DISPLAY   *P1:4,"Hospital Id                   :";
            ENDIF
          ENDIF
.
          MOVE      "4",EVERT
          MOVE      "33",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS3000,KHOS3000
.
KHOS2000  DISPLAY   *P33:4,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS3000  MOVE      SP20,PTHSNAME
          MOVE      SP10,PTHSHOSP
.
KHOS9999  RETURN
+
.********************************************************************
.*                          I/O for tempfile                        *
.********************************************************************
RDINFILE  MOVE      ZERO,OVRCD
          READ      OUTDFILE,SEQ;TMPADMN,TMPURNO,TMPINVN,TMPINVD:
                                 TMPINVA1,TMPINVA2,TMPCLMV,TMPHFND,TMPHTAB:
                                 TMPSYST
          GOTO      OVERCOND IF OVER
          RETURN
.
RDCLFILE  MOVE      ZERO,OVRCD
          READ      TMPCLMAF,SEQ;TMPOLDCL,TMPNEWCL
          GOTO      OVERCOND IF OVER
          RETURN
.
RDHFFILE  MOVE      ZERO,OVRCD
          READ      TMPHFNAF,SEQ;TMPOLDHF,TMPOLDTB,TMPNEWHF,TMPNEWTB
          GOTO      OVERCOND IF OVER
          RETURN
.
.
RDTMCLA1  MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      MAPCLMA1,KEY3;TMPOLDCL,TMPNEWCL
          GOTO      OVERCOND IF OVER
          RETURN
.
RATMCLA1  MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      MAPCLMA1,KEY3;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMCLA1  RESET     KEY3
          READ      MAPCLMA1,KEY3;;
          RETURN
.
RKTMCLA1  MOVE      ZERO,OVRCD
          READKS    MAPCLMA1;TMPOLDCL,TMPNEWCL
          GOTO      OVERCOND IF OVER
          RETURN
.
DETMCLA1  RESET     KEY3
          DELETE    MAPCLMA1,KEY3
          RETURN
.
WRTMCLA1  RESET     KEY3
          WRITE     MAPCLMA1,KEY3;TMPOLDCL,TMPNEWCL
          RETURN
.

RDTMHFA1  MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      MAPHFNA1,KEY14;TMPOLDHF,TMPOLDTB,TMPNEWHF,TMPNEWTB
          GOTO      OVERCOND IF OVER
          RETURN
.
RATMHFA1  MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      MAPHFNA1,KEY14;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMHFA1  RESET     KEY14
          READ      MAPHFNA1,KEY14;;
          RETURN
.
RKTMHFA1  MOVE      ZERO,OVRCD
          READKS    MAPHFNA1;TMPOLDHF,TMPOLDTB,TMPNEWHF,TMPNEWTB
          GOTO      OVERCOND IF OVER
          RETURN
.
DETMHFA1  RESET     KEY14
          DELETE    MAPHFNA1,KEY14
          RETURN
.
WRTMHFA1  RESET     KEY14
          WRITE     MAPHFNA1,KEY14;TMPOLDHF,TMPOLDTB,TMPNEWHF,TMPNEWTB
          RETURN
.
.==============================================================================
.
          INC       STD001IO
          INC       CALCAGE
          INC       PATCALFN
          INC       PATCOMN1
          INC       PATCOMN3
          INC       PATSNDX
          INC       PATSNX2
          INC       PATMSCKY
          INC       CLPMSVX1
          INC       CLPATINV
          INC       PMSOSDTM                * Outstanding Debt alert routine
          INC       PATHSPKY
          INC       CLEMRVIS
          INC       PATMCHRD                * Miscellaneous Charges read routine
          INC       PATITMRD
.
          INC       AAEDE1IO/INC
          INC       AAEDTRIO/INC
          INC       EMRVISIO/INC
.
          INC       OUTPREIO/INC
.
          INC       PATDRGIO/INC
          INC       PATCODIO/INC
          INC       PATDTRIO/INC
          INC       PATFINIO/INC
          INC       PATFIGIO/INC
          INC       PATFN1IO/INC
          INC       PATGSRIO/INC
          INC       PATHSPIO/INC
          INC       PATINVIO/INC
          INC       PATIRNIO/INC
          INC       NZPIRNIO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       PATMCHIO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATRFNIO/INC
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       BOKRC1IO/INC
          INC       NZCOMPIO/INC
.
          INC       NZPFINIO/INC
          INC       NZPFIGIO/INC
          INC       NZPRFNIO/INC
          INC       PATRFGIO/INC

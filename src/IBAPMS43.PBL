.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   IBAPMS43                                                  *
.* Desc      :   Merged U/R Report                                         *
.***************************************************************************
.* Date      :   10/10/2007                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will print a report of merge U/R numbers     *
.*           :   for a date range                                          *
.* Mods      :                                                             *
.* V10.00.01 09/04/2010 Steve Armstrong   CAR 219045                       *
.*           Recompiled for changes PATMRGFD.                              *
.***************************************************************************
.* V9.11.01  08/04/2009  Jill Habasque     CAR 194133                      *
.*           Changed KEY24 to KEY25 for patmrgaf read                      *
.***************************************************************************
.* V9.09 01  24/01/2007  Ebon Clements          CAR 151940                 *
.*           Added print of date record merged                             *
.* V9.09 00  10/10/2007  Ebon Clements          CAR 151940                 *
.*           Created report                                                *
.***************************************************************************
.
          INC       STD001FD
.
          INC       PATMA1FD/INC
          INC       PATMRGFD/INC
+
.
. LOCAL VARIABLES
. ---------------
.
ADDRESS   DIM       49
CURFNAME  DIM       51
CURPURNO  DIM       8
CURPBDAT  DIM       10
CURRPSEX  DIM       1
CURRADDR  DIM       49
CURRMEDI  DIM       10
DESCSTAT  DIM       20
FROMDATE  DIM       8
MERGSTAT  FORM      1  
OLDFNAME  DIM       51
OLDPURNO  DIM       8
OLDPADDR  DIM       49
OLDPBDAT  DIM       10
OLDPPSEX  DIM       1
OLDPMEDI  DIM       10
MERGDATE  DIM       10
TODATE    DIM       8
.
PRGID     INIT      "IBAPMS43"
PRGNAM    INIT      "Merged U/R Report"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                           MAINLINE                                        *
.*                  Controlling routine for program code                     *
.*****************************************************************************
.
ML0000    CALL      INIT0000                     * Initialization
.
ML1000    CALL      SCRN0000                     * get program option
          BRANCH    EXIT,ML9999                  * exit selected
.
ML2000    CALL      SDAT0000                     * get start report date
          BRANCH    EXIT,ML1000                  * nothing entered
.
          CALL      EDAT0000                     * get end report date
          BRANCH    EXIT,ML2000                  * nothing entered
.
          CALL      STAT0000                     * get merge status 
          BRANCH    EXIT,ML2000                  * nothing entered
.
          CALL      CONTQST                      * Ok. to Continue ?
          BRANCH    CEXIT,ML5000:                * yes
                          ML2000:                * no
                          ML1000                 * cancel
.
ML5000    CALL      PROC0000                     * Process Report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*****************************************************************************
.*                           INIT0000                  Called by: ML0000     *
.*             Initialise variables and open files                           *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P72:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P72:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P72:24,"patmrgaf"
          OPEN      PATMRGG3,"patmrgaf"
.
          CALL      IBACLOCK
.
INIT9999  RETURN
+
.*****************************************************************************
.*                            SCRN0000                 Called by: ML0000     *
.*                       Main Option Screen                                  *
.* Returns: EXIT  0 = run report                                             *
.*                1 = exit selected                                          *
.*****************************************************************************
.
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Run Merged U/R Report"
.
SCRN1000  KEYIN     *P1:7,*EL,"Select option : ":
                    *P17:7,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION                  * exit selected ?
          GOTO      SCRN9500 IF EQUAL            * yes
.
          BRANCH    OPTION,SCRN9000              * run report
.
          BEEP                                   * invalid input
          GOTO      SCRN1000
.
SCRN9000  MOVE      ZERO,EXIT
          GOTO      SCRN9999
.
SCRN9500  MOVE      ONE,EXIT
.
SCRN9999  RETURN
+
.*****************************************************************************
.*                            SDAT0000                 Called by: ML0000     *
.*                    Keyin Start Date for the Report                        *
.* Returns: FROMDATE = start date (ccyymmdd)                                 *
.*          EXIT   0 = valid date entered                                    *
.*          EXIT   1 = nothing entered                                       *
.*****************************************************************************
.
SDAT0000  DISPLAY   *P1:9,*EF,"From Date :";
.
          UNPACK    SP30,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      NINE,CVERT
          MOVE      TEN3,CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          CALL      KEYDATE                      * keyin date
          BRANCH    OVRCD,SDAT9500               * nothing entered
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          MOVE      ZERO,EXIT
          GOTO      SDAT9999
.
SDAT9500  MOVE      ONE,EXIT
.
SDAT9999  RETURN
+
.*****************************************************************************
.*                            EDAT0000                 Called by: ML0000     *
.*                    Keyin End Date for the Report                          *
.* Returns: TODATE = end date (ccyymmdd)                                     *
.*          EXIT   0 = valid date entered                                    *
.*          EXIT   1 = nothing entered                                       *
.*****************************************************************************
.
EDAT0000  DISPLAY   *P1:10,*EF,"To Date   : ";
.
          UNPACK    SP30,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      TEN,CVERT
          MOVE      TEN3,CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          CALL      KEYDATE                      * keyin date
          BRANCH    OVRCD,EDAT9500               * nothing entered
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
.         Make sure date range is valid
.
          MATCH     FROMDATE,TODATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"To Date must not be less than ":
                      "From Date.  ";
            CALL      HITENTER
            GOTO      EDAT0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      EDAT9999
.
EDAT9500  MOVE      ONE,EXIT
.
EDAT9999  RETURN
+
.*****************************************************************************
.*                            STAT0000                 Called by: ML0000     *
.* Keyin merged status filter                                                *
.*****************************************************************************
STAT0000  MOVE      ZERO,MERGSTAT
          DISPLAY   *P1:11,*EF,"Merge Status : "; 
          KEYIN     *P16:11,*V2LON,MERGSTAT
.
          BRANCH    MERGSTAT,STAT9000,STAT9000,STAT9000,STAT9000    
          BEEP                                   * invalid input
          GOTO      STAT9500
.
STAT9000  IF        MERGSTAT=1
            MOVE      "Merge Requested",DESCSTAT
          ENDIF
.
          IF        MERGSTAT=2
            MOVE      "Merge Fully Accepted",DESCSTAT
          ENDIF
.
          IF        MERGSTAT=3
            MOVE      "Merge Processed",DESCSTAT
          ENDIF
.
          IF        MERGSTAT=4
            MOVE      "Merge Rejected",DESCSTAT
          ENDIF
.
          DISPLAY   *P18:11,*EF,DESCSTAT;
.
          MOVE      ZERO,EXIT
          GOTO      STAT9999
.
STAT9500  MOVE      ONE,EXIT
          MOVE      SP70,DESCSTAT
.
STAT9999  RETURN
+
.*****************************************************************************
.*                            PROC0000                 Called by: ML0000     *
.*                          Extract Data                                     *
.*****************************************************************************
PROC0000  MOVE      ZERO,CPAGENO                 * initialise print variables
          CALL      HEAD0000                     * print header
.
          PACK      KEY25,FROMDATE,SP20
          CALL      RSPTMRG3                     * position on record type
PROC1000  CALL      RKPTMRG3                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          MATCH     PTMRRDTE,TODATE              * end date
          GOTO      PROC9999 IF LESS
.
          COMPARE   MERGSTAT,PTMRSTAT
          GOTO      PROC1000 IF NOT EQUAL
.
          CALL      PRNT0000                     * print merge details
.
          GOTO      PROC1000                     * get next record
.
PROC9000  PRINT     *N,*N,"***  End of Report  ***";
.
PROC9999  RETURN
+
.*****************************************************************************
.*                            HEAD0000                 Called by: EXTR0000   *
.*                         Report Heading                         MTCH0000   *
.*****************************************************************************
HEAD0000  MOVE      ZERO,CLNO
          CALL      HEAD132
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *40,"Request Date From: ",CPCDATE;
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *75,"To: ",CPCDATE,*100,"Status: ",DESCSTAT
.
          CALL      UND132
.         
          PRINT     *1,"|     Current Patient Details ":
                    *66,"|     Old Patient Details",*132,"|"
.
          CALL      UND132
.
          PRINT     *1,"| U/R No.    Name":
                   *66,"| U/R No.    Name",*132,"|"
.
          PRINT     *1,"| Date of Birth Sex Address":
                   *66,"| Date of Birth Sex Address",*132,"|"
.
          PRINT     *1,"| Medicare Number":
                   *66,"| Medicare Number",*132,"|"
.
          PRINT     *1,"| Date of Merge":
                   *66,"| Date of Merge",*132,"|"
.
          CALL      UND132
          ADD       SIX,CLNO
.
HEAD9999  RETURN
+
.*****************************************************************************
.*                            PRNT0000                 Called by: PROC0000   *
.*  Print merged patient details                                             *
.*****************************************************************************
PRNT0000  MOVE     SP70,CURPURNO
          MOVE     SP70,OLDPURNO
          MOVE     SP70,CURFNAME
          MOVE     SP70,OLDFNAME
          MOVE     SP70,CURPBDAT
          MOVE     SP70,OLDPBDAT
          MOVE     SP70,CURRPSEX
          MOVE     SP70,OLDPPSEX
          MOVE     SP70,CURRADDR
          MOVE     SP70,OLDPADDR
          MOVE     SP70,CURRMEDI
          MOVE     SP70,OLDPMEDI
.
          MOVE     PTMROLUR,OLDPURNO
          PACK     KEY8,PTMROLUR,SP70
          CALL     RDMAST1
          BRANCH   OVRCD,PRNT1000          
.
          MOVE     PSNAME,PACSNAME
          MOVE     PGNAME,PACGNAME
          MOVE     PTITL,PACTITLE
          MOVE     ONE,PACFRMT
          CALL     PACNAME
          MOVE     PACFNAME,OLDFNAME
.
          UNPACK   PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL     PACDATE
          MOVE     CPCDATE,OLDPBDAT
.
          MOVE     PSEX,OLDPPSEX
.
          CALL     ADDR0000
          MOVE     ADDRESS,OLDPADDR
.
          MOVE     PMEDI,OLDPMEDI
.
PRNT1000  MOVE     PTMRNWUR,CURPURNO
          PACK     KEY8,PTMRNWUR,SP70
          CALL     RDMAST1
          BRANCH   OVRCD,PRNT2000
.         
          MOVE     PSNAME,PACSNAME
          MOVE     PGNAME,PACGNAME
          MOVE     PTITL,PACTITLE
          MOVE     ONE,PACFRMT
          CALL     PACNAME
          MOVE     PACFNAME,CURFNAME
.
          UNPACK   PTMRRDTE,CCENT,CYEAR,CMON,CDAY
          CALL     PACDATE
          MOVE     CPCDATE,MERGDATE
.
          UNPACK   PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL     PACDATE
          MOVE     CPCDATE,CURPBDAT
.
          MOVE     PSEX,CURRPSEX
.
          CALL     ADDR0000
          MOVE     ADDRESS,CURRADDR
.
          MOVE     PMEDI,CURRMEDI
.
PRNT2000  PRINT     *1,"| ",CURPURNO,*14,CURFNAME:
                   *66,"| ",OLDPURNO,*79,OLDFNAME,*132,"|"
.
          PRINT     *1,"| ",CURPBDAT,*14,CURRPSEX,*16,CURRADDR:
                   *66,"| ",OLDPBDAT,*79,OLDPPSEX,*81,OLDPADDR,*132,"|"
.
          PRINT     *1,"| ",CURRMEDI:
                   *66,"| ",OLDPMEDI,*132,"|"
.
          PRINT     *1,"| ",MERGDATE:
                   *66,"| ",MERGDATE,*132,"|"
.
          CALL      UND132
          ADD       FIVE,CLNO
.
          COMPARE   "66",CLNO
          GOTO      PRNT9999 IF LESS
          CALL      HEAD0000   
.
PRNT9999  RETURN
+
.*****************************************************************************
.*                             ADDR0000                Called by: MTCH0000   *
.*                   Format Patients Address                                 *
.* Requires: PADD1 - patient address line 1                                  *
.*           PADD2 - patient address line 2                                  *
.*           PADD3 - patient address line 3                                  *
.*           PADD4 - patient address line 4                                  *
.*           PPOST - patient postcode                                        *
.* Returns: ADDRESS - formatted patient address                              *
.*****************************************************************************
.
ADDR0000  PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
.
ADDR9999  RETURN
+
.*****************************************************************************
.*        I/O Includes                                                       *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       PATMA1IO/INC
          INC       PATMRGIO/INC

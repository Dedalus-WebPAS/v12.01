*************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:    IBAPAT14                                              *
.*                                                                            *
.*     FUNCTION:        MAINTENANCE OF ITEM FILE                              *
.*                                                                            *
.*     DATE WRITTEN:    15/06/1985                                            *
.*                                                                            *
.*     AUTHOR:          MALCOLM HAYSE                                         *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V11.02.01  02/02/2022  Alina Bhari    TSK 0903886                   *
.*                   Recompiled for PATIKIPR -  Made users able to be search  *
.*                                              by long description
.*        V11.00.01  02/03/2020  J.Tan          TSK 0838262                   *
.*                   Added Effective from and to date to MBS Item file        *
.*                   Removed Old and New patitemn file                        *
.*                   Mod to Load MBS item using Current date of Eff.Fromdate  *
.*        V10.08.02  11/10/2016  J.Tan        TSK 0321309                     *
.*                   Added option to upload items from XML file               *
.*        V10.08.01  03/08/2016  J.Tan        TSK 0813090                     *
.*                   Added 'Theatre Average Calculateion Band (Class No 17)   *
.*        V9.12.02   21/09/2009  J.Tan        CAR 204390                      *
.*                   Mods to old item file                                    *
.*        V9.12.01   26/05/2009  J.Tan        CAR 194525                      *
.*                   Fixed creating old and new items file                    *
.*        V9.04.01   11/01/2005  Davin        CAR 55773                       *
.*                   Changed MBS upload (option 20) to use newitemn not       *
.*                   patitemn                                                 *
.*        V9.03.04   16/04/2003  Lina Vo      CAR 48917                       *
.*                   Removed locking of Web MBS item Maintenance flag         *
.*                   (PTCNWITM).  PATWEB88 server now does the locking.       *
.*        V9.03.03   15/04/2003  Lina Vo      CAR 48917                       *
.*                   Changed Load Ascii file to allow path and file name.     *
.*        V9.03.02   25/06/2003  Lina Vo      CAR 36485                       *
.*                   Added check for PTCNWITM when PGM="IBARSH"(called by web)*
.*                   and use PTCNWITM for program locking.                    *
.*        V9.03.01   05/05/2003  Dean Cramer  CAR 38200                       *
.*                   Open file ROLLFILE for clearing newitemn file            *
.*                   Include 2nd key for newitemn - make command line field   *
.*                   long enough 
.*        V9.02.03   23/08/2002  Dean Cramer  SRF 21185                       *
.*                   Made so does start before read next then delete at       *
.*                   INDEX100                                                 *
.*        V9.02.02   27/02/2001  Mike Laming  SRF 14622                       *
.*                   Add routine to strip leading zeros from input text       *
.*                   file Item Number.                                        *
.*                   Add code to write the last text data                     *
.*                   Remove all references to TEMPFILE2 (doesn't work anyway) *
.*        V9.02.01   24/10/2001  J.Tan                                        *
.*                   Mods to write to item using qiamt                        *
.*        V5.08.04   20/11/2000  Jill Habasque                                *
.*                   Fixed newfile to use index 1 only                        *
.*        V5.08.03   24/10/2000  Ebon Clements                                *
.*                   Added upload of MBS items to patitemn and vmoitemn       *
.*        V5.08.02   06/07/2000  J Habasque SRF 638989                        *
.*                   Added PATIT1M1 - oracle mods                             *
.*        V5.08.B07  19/04/2000  J Habasque                                   *
.*                   Fixed GST code keyin                                     *
.*        V5.08.B06  21/03/2000  J.Tan                                        *
.*                   Fixed GST                                                *
.*        V5.08.B05  08/03/2000  Steve Downing                                *
.*                   Fixed read position for IBCNUGST                         *
.*        V5.08.B04  08/03/2000  Steve Downing                                *
.*                   Fixed keyin error for misc items & sugg. class.          *
.*        V5.08.B03  04/02/2000  J Habasque                                   *
.*                   Mods to use ibagstaf                                     *
.*        V5.08.B02  28/01/2000  J Habasque                                   *
.*                   GST mods - PTITGSTA & PTITGSTC                           *
.*        V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                   Fixed global recompile bugs                              *
.******************************************************************************
.*     V5.07.B02  19/11/1998  Nicole Harrington                               *
.*                Added century to read on PATIAUD and other Y2K fixes        * 
.******************************************************************************
.*     V5.06.00   25/09/1998 Katie Nelson                                     *
.*                Added the procedure PATIKIUP which updates the DRG          *
.*                Index Table                                                 *
.******************************************************************************
.*     V5.03.01   05/10/1995     MATT                                         *
.*                Changed definition of PATIAUD to SFILE                      *
.*     V5.04.01   08/07/1997     ROD                                          *
.*                pack key to read item file, set up start/end range vars     *
.*                                                                            *
.*************************************************************************
.*                                                                            *
.*     MODIFICATIONS:   V5.01.02  22/05/90 J.Tan           SRF 104901         *
.*                                Added 8 new theatre classifications         *
.*                      V5.01.03  12/09/90 i chung         SRF 103699         *
.*                                Added Options 8 and 17 for duplication of   *
.*                                Theatre Classification banding              *
.*                      V5.01.04  02/11/90 J.Tan                              *
.*                                Fixed percentage increase amount            *
.*                      V5.01.05  23/05/91 i chung         SRF 108642         *
.*                                Allowed Start to Finish for MBS Item No.    *
.*                                range                                       *
.*                      V5.01.06  26/07/91 J.Tan       SRF 109555             *
.*                                Allowed to allocate theatre class. up to 16 *
.*                      V5.01.07  16/02/94 Matt Surridge                      *
.*                                Added MAINQST, DELQST, PATCODKY and upgraded*
.*                                generaly to meet standards.                 *
.*                      V5.01.08  18/03/94 Ian Rutt                           *
.*                                Added single user program locking and keyin *
.*                                for item code                               *
.*                                                                            *
.******************************************************************************
.
          INC       STDGENDF
.
          INC       STDKWSDF
          INC       IBAGSTFD/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PATITMFD/INC
          INC       PATIKIFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.        EXTRA   VARIABLES
.
ACTV      DIM       1
DESC      DIM       30
ACTION    DIM       1
ADD       DIM       2
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
ADDREC    FORM      6
.
.CAUDI     FORM      1
CHG       FORM      1
CMDLINE   DIM       200
COL       FORM      3
CODE      DIM       2
CODEA     INIT      "A "
CODEFI    INIT      "FI"
CODETB    INIT      "TB"
.
VFLDFLAG  FORM      1
DCLSS     DIM       20
DIM1A     DIM       1              * added for V5.01.03 - required by PATITMDS
DIM16     DIM       16
DIM20     DIM       20             * added for V5.01.03 - required by PATITMDS
DMGRP     DIM       20
DATSTRNG  DIM       4000
.
EMPFLAG   FORM      1              * 1=create new file empty
ENDREC    FORM      1
ERASE     INIT      "iserase "
.
FIELD     FORM      2
FNAMER    DIM       8
FNAMEI    DIM      80
XMLFLNAM  DIM      80
FORM5     FORM      5                                                  *I-90202
FRTHCLSS  FORM      2              * added for V5.01.03 - From Theatre Class.
.
GST0      INIT      "GST Free   "
GST1      INIT      "GST Payable"
GST2      INIT      "Either     "
.
IBCNUGST  FORM      1
ISBUILD   INIT      "isbuild "
ITEME     DIM       9
ITEMEDSC  DIM       37             * added for V5.01.03 - From Item Number desc.
ITEMS     DIM       9
ITEMSDSC  DIM       37             * added for V5.01.03 - To   Item Number desc.
.
LASTCHAR  DIM       1
.
MODE      DIM       1
MODE1     DIM       1
MSGFLG    FORM      1
.
OPERCODE  DIM       4
.
 IFGE     $$VER,7
PATIAUD   SFILE    ASCII, FIXED=256
 XIF
 IFEQ     $$VER,6
PATIAUD   FILE     ASCII, FIXED=256
 XIF
PERCENT   FORM      3.4
.
RANGE     FORM      1
RECNUM    FORM      8
RECCNT    FORM      3
.
SEC       FORM      2
SEC1      FORM      "00001"
SELCOUNT  FORM      2
SP40      INIT      "                                        "
SP100     INIT      "                                                  ":
                    "                                                  "
.
TAGSTRNG  DIM       300
TEMPFILA  DIM       8
TMPSTRNG  DIM       4000
TEMPFILE  DIM       8
...TEMPFILE2 DIM       8                                                D-90202
THEANUM   FORM      2
THEATRE   DIM       3              * added for V5.01.03 - work Theatre Class
TIMEIS    DIM       8
TMSG      DIM       3
TOTHCLSS  FORM      2              * added for V5.01.03 - To Theatre Class.
TXDATE    DIM       8
TYPEFLAG  FORM      1             * information type flag
.                                    0 = start tag value
.                                    1 = end tag value
.                                    2 = data value
.
UKEY      INIT      " 58 U1-9"
UPDY      DIM       1
UPDREC    FORM      6
.
VERT      FORM      2
WORK      FORM      8.4
.
XMLFLAG   FORM      1
X1MBSN    DIM       9
X1MBSO    DIM       9
XPOST     FORM      4
.

FLATFILE  FILE      ASCII,FIXED=4000             * XML Item file
MBSFILE   FILE       ASCII, FIXED=256
TEMP1     IFILE SQL, FIXED=58, KEY="U1-9"
...TEMP2     IFILE SQL, FIXED=318, KEY="U1-9"                           D-90202

.TEMP1
.Name     Type     Size     Start
.----     ----     ----     -----
TXITEM    DIM      9        1                      * Item Number
TXDESC    DIM      40       10                     * Description
TXAMT     FORM    12.2      50                     * MBS Amount
.EOR                        58
.
.TEMP2
.Name     Type     Size     Start
.----     ----     ----     -----
.TXITEM    DIM      9        1                      * Item Number
TXDESC1   DIM      50       10                     * Description 1
TXDESC2   DIM      50       60                     * Description 2
TXDESC3   DIM      50       110                    * Description 3
TXDESC4   DIM      50       160                    * Description 4
TXDESC5   DIM      50       210                    * Description 5
TXDESC6   DIM      50       260                    * Description 6
.TXAMT     DIM      12.2     310                    * Amount
.EOR                        318


TFREC     DIM      2                               * Start of line type
TFITEM    DIM      5                               * Ittem number
TFFILL    DIM      32                              * Record filler
TFFILL2   DIM      10                              * Record filler
TFLINE    DIM      120                             * The rest of the line
TFTYP     DIM      1                               * Record type D or N
TFAMT     DIM      8                               * Amount
TFDESC    DIM      50                              * Amount
.
. PROGRAM CONSTANTS
. -----------------
ENDSTRNG  INIT      ">"
STARTSTR  INIT      "<"
PIPE      INIT      "|"
SEDSTRNG  INIT      "sed 's/<\/[^>]*>/&\n/g'"
.
PRGID     INIT      "IBAPAT14"
PRGNAM    INIT      "MBS/Item File Maintenance"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
        CALL      DISPHEAD
        CALL      IBACLOCK
.
        DISPLAY   *P56:24,"Opening patitemn";
        OPEN      PATITEM1,"patitemn"
        OPEN      PATITEM2,"patitemn"
.
        DISPLAY   *P64:24,"patcodes";
        OPEN      PATCODE1,"patcodes"
.
        DISPLAY   *P64:24,"patikiaf";
        OPEN      PATIKIA1,"patikiaf"
.
        DISPLAY   *P64:24,"ibagstaf";
        OPEN      IBAGSTA1,"ibagstaf"
        OPEN      IBAGSTA2,"ibagstaf"
.
        DISPLAY   *P64:24,"controlf";
        OPEN      CONTROLF,"controlf"
.
.       Check if program already in use
.
        MOVE      ZERO,EXIT
        READ      CONTROLF,SEVENTY9;*21,PTCNITMM
        READ      CONTROLF,ZERO;*85,IBCNUGST
        READ      CONTROLF,HUND05;*68,PTCNMBSC
.
.       check if this program is run by IBARSH (web program)  
.
        MATCH     "IBARSH",PGM
        GOTO      INIT10 IF EQUAL
.
        PACK      DIM2,ZERO,ZERO
        MATCH     DIM2,PTCNITMM
        GOTO      INIT10 IF EQUAL
.
        MATCH     PORT,PTCNITMM
        GOTO      INIT10 IF EQUAL
.
        CALL      PRGLOCK
        GOTO      ENDIT2
.         
INIT10  MOVE      PORT,PTCNITMM
        WRITAB    CONTROLF,SEVENTY9;*21,PTCNITMM
.
        CALL      TFILENAM                     * get new tempfile name
        MOVE      TFILNAME,TEMPFILA
.
        CALL      TFILENAM                     * get new tempfile name
        MOVE      TFILNAME,FNAMER
.
        CALL      TFILENAM                     * get new tempfile name
        MOVE      TFILNAME,TEMPFILE
.
        READ      CONTROLF,TEN;*209,CAUDI
.
        BRANCH    CAUDI OF START
.
        DISPLAY   *P64:24,"patiaud";
        OPEN      PATIAUD,"patiaud"
.
.       Close PATITEM1 because it may be openned on the new file
.
START   HLINE     *G37,2,53,80
        DISPLAY   *P1:3,*EF:
                 *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                  *P40:5,*V2LON,"19",*HOFF," = Regenerating MBS Keyword Index";
          IF        PTCNMBSC = 1
           DISPLAY   *P40:6,*V2LON,"20",*HOFF," = Load MBS items from ASCII file";
          ENDIF
           DISPLAY   *P40:7,*V2LON,"21",*HOFF," = Load MBS items from XML file";
.
START0    KEYIN     *P1:24,*EL,"Select Option : __",*P17:24,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    MOVE      ZERO,MSGFLG
          COMPARE   "19",OPTION
          GOTO      INDEX IF EQUAL
          COMPARE   "21",OPTION
          GOTO      LOAD0000 IF EQUAL
.
          IF        PTCNMBSC = 1
            COMPARE   "20",OPTION
            GOTO      LOAD0000 IF EQUAL
          ENDIF
.
          BEEP
          GOTO      START0
+
.
.         Regenerating MBS Keyword Index
.
INDEX     DISPLAY   *P1:24,*EL,"Recalibrating Key Word Index ..."
          MOVE      ZERO,RECNUM
.
INDEX100  PACK      KEY24,SP70
          CALL      RSPTIK1
          CALL      RKPTIK1
          BRANCH    OVRCD,INDEX200
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY       *P40:24,*V2LON,RECNUM
          ENDIF
          PACK      KEY24,PTIKITM,PTIKKWD
          CALL      DEPTIK1
          GOTO      INDEX100
.
INDEX200  DISPLAY   *P1:24,*EL,"Creating Key Word Index ..."
          OPEN      PATITEM1,"patitemn"
          MOVE      ZERO,RECNUM
          PACK      KEY17,SP70
          CALL      RDSITEM1
INDEX300  CALL      RDKITEM1
          BRANCH    OVRCD,START
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY       *P40:24,*V2LON,RECNUM
          ENDIF
          PROC      PATIKIUP
          GOTO      INDEX300
+
.**************************************************************************
.*  LOAD0000  :  Upload ASCII file into patitemn                          *
.**************************************************************************
LOAD0000  DISPLAY   *P1:4,*EF;
          MOVE      ZERO,XMLFLAG
          IF        OPTION=21
            MOVE      ONE,XMLFLAG                * Load Items from XML file
            CALL      DISPLMBS
            CALL      GXML0000                     * Enter XML file name
          ELSE
            CALL      DISPLMBS
            CALL      KASCI000                     * Enter ASCII file name
          ENDIF
	  BRANCH    EXIT,LOAD9999
.
          OPEN      PATITEM1,"patitemn"
          CALL      CREA0000                     * Create temp file
          MOVE      ZERO,ENDREC
          CALL      PRHD0000                     * Print Header
.
          BRANCH    XMLFLAG,LOAD5000
.kkkk
.
LOAD1000  READ      MBSFILE,SEQ;TFREC,TFLINE     * Read ASCII file line by line
          GOTO      LOAD3000 IF OVER                                   *C-90202
.
          MATCH      "10",TFREC                    * Item record
          IF         @EQUAL
            COMPARE   ONE,ENDREC
            IF        @EQUAL
              PACK      KEY9,TXITEM,SP70
              CALL      WRTEMP1
              DISPLAY   *P1:24,"Processing Item : ",TXITEM
              MOVE      ZERO,ENDREC
            ENDIF
            UNPACK    TFLINE,TFITEM
.                                                * clean up the Gov Item No
            TYPE      TFITEM                                           *I-90202
            GOTO      LOAD2000 IF NOT EQUAL      * Not numeric         *I-90202
            MOVE      TFITEM,FORM5               * Strip leading zeros *I-90202
            MOVE      FORM5,TFITEM                                     *I-90202
            LJUSTIFY  TFITEM                     * Left justify        *I-90202
.
LOAD2000    PACK      TXITEM,TFITEM,SP70                               *C-90202
            GOTO      LOAD10000
          ENDIF
.
         MATCH     "20",TFREC                 * Fee Details record
          IF        @EQUAL
            UNPACK    TFLINE,TFFILL2,TFAMT
            MOVE      TFAMT,TXAMT
            GOTO      LOAD1000
          ENDIF
.
          MATCH     "30",TFREC                 * Derived fee record
          IF        @EQUAL
            MOVE      ZERO,TXAMT
            GOTO      LOAD1000
          ENDIF
.
          MATCH     "40",TFREC                  * Anaesthetic record
          IF        @EQUAL
            GOTO      LOAD1000
          ENDIF
.
         MATCH    "50",TFREC                   * Description record
          IF       @EQUAL
            COMPARE   ONE,ENDREC
            GOTO      LOAD1000 IF EQUAL
            UNPACK    TFLINE,TFFILL2,TFDESC
            PACK      TXDESC,TFDESC,SP70
            MOVE      ONE,ENDREC
            GOTO      LOAD1000
          ENDIF
.
          GOTO      LOAD1000
.
.                                                * write last record   *I-90202
LOAD3000  COMPARE   ONE,ENDREC                                         *I-90202
          IF        @EQUAL
            PACK      KEY9,TXITEM,SP70                                 *I-90202
            CALL      WRTEMP1                                          *I-90202
          ENDIF                                                        *I-90202
          GOTO      LOAD8000
.
.         XML file
.
LOAD5000  CALL      OPEN0000               * open text file
          BRANCH    EXIT,LOAD9800          * unable to open .txt file
.
          MOVE      SP70,TXITEM
          MOVE      ZERO,TXAMT
          MOVE      ZERO,TXDESC
          READ      FLATFILE,SEQ;TMPSTRNG        * Read XML file
          IF        @OVER
            PRINT     *1,"XML File is empty "
            GOTO      LOAD9800
          ENDIF
          CALL      LXML0000                     *  Load items from XML file
.                                                * Write text data to Item file
LOAD8000  MOVE      ZERO,ADDREC 
          MOVE      ZERO,UPDREC
.
          DISPLAY   *P1:24,*EL
          PACK      KEY9,SP30
          CALL      RSTEMP1                      * Position at start of file
LOAD8500  CALL      RKTEMP1                      * Read first record
          BRANCH    OVRCD,LOAD9500
          DISPLAY   *P1:24,"Updating Item : ",TXITEM
          PACK      KEY9,TXITEM,SP30                                   *C-90202
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8                    * Current date
          PACK      KEY17,KEY9,KEY8
          CALL      RDITEM1
          IF        OVRCD = 1
            MOVE      TXITEM,ITEMNO
            MOVE      TXDESC,IDESC
            MOVE      TXAMT,QIAMT
            MOVE      ZERO,PTITGSTA
            MOVE      TWO,PTITEXCL
            UNPACK    SP70,IMISGRP,IALPHA,ICLSS,IBAND1,IBAND2,IBAND3,IBAND4
            UNPACK    SP70,IBAND5,IBAND6,IBAND7,IBAND8,IBAND9,IBAND10,IBAND11
            UNPACK    SP70,IBAND12,IBAND13,IBAND14,IBAND15,PTITACTV,PTITLINK,
            UNPACK    SP70,PTITIWRN,PTITGSTC,IBAND16
            MOVE      SP70,PTITTACB
            MOVE      KEY8,PTITEFDT            * Effective fromdate
            PACK      KEY9,TXITEM,SP30
            PACK      KEY17,KEY9,PTITEFDT,SP70
            CALL      WRITEM1
            ADD       ONE,ADDREC 
            PRINT     *N,*1,TXITEM,*12,TXDESC,*55,TXAMT;
          ELSE
            MOVE      TXAMT,QIAMT
            CALL      UPITEM1
            ADD       ONE,UPDREC
          ENDIF
          GOTO      LOAD8500
.
LOAD9500  PRINT     *N,*N,*1,"No of record added   : ",ADDREC:
                    *N,*1,"No of record updated : ",UPDREC,*N:
                    *N,"*** END OF PATIENT ITEMS ***"
.
LOAD9800  CLOSE     MBSFILE
          CALL      KILT0000
          PRINT     *N,*1,"*** END OF REPORT ***";
LOAD9999  GOTO      START
+
.*****************************************************************************
.*                               PRHD0000                                    *
.*            Print heading                                                  *
.*****************************************************************************
PRHD0000  CALL      HEAD80
          CALL      UND80
          PRINT     *N,*1,"Item No",*12,"Description",*55,"Schedule Fee"
          CALL      UND80
.
PRHD9999  RETURN
+
.*****************************************************************************
.*                               OPEN0000              Called by: MAIN0000   *
.*            Open the copied ".txt" file prior to processing                *
.*****************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FLATFILE,TEMPFILA            * open text file
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Converted file not found.  ";
            CALL      HITENTER
            PRINT     *1,"Converted file not found"
            GOTO      OPEN9100
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9100  MOVE      ONE,EXIT
.
OPEN9999  RETURN
+
.**************************************************************************
.         Load items from XML file
.**************************************************************************
LXML0000  MATCH     STARTSTR,TMPSTRNG            * start of tag ?
          GOTO      LXML5000 IF EQUAL            * yes
.
          MATCH     ENDSTRNG,TMPSTRNG            * end of tag ?
          GOTO      LXML6000 IF EQUAL            * yes
.
          MOVE      TMPSTRNG,ANS
          IF        TYPEFLAG = 2
            APPEND    ANS,DATSTRNG               * data string
          ELSE
            APPEND    ANS,TAGSTRNG               * tag string
          ENDIF
          CALL      GNXT0000                     * get next character
          BRANCH    EXIT,LXML9999                * end of file
          GOTO      LXML0000
.
.         We have a "<" character, so check if this is a start or end tag
.
LXML5000  PACK      TAGSTRNG,SP100,SP100,SP100   * initialise tag string
          CLEAR     TAGSTRNG
.
          CALL      GNXT0000                     * get next character
          BRANCH    EXIT,LXML9999                * end of file
.
          MOVE      ZERO,TYPEFLAG                * set flag for start tag
          MATCH     SLASH,TMPSTRNG               * start tag ?
          GOTO      LXML0000 IF NOT EQUAL        * yes
.
          RESET     DATSTRNG
          MOVE      ONE,TYPEFLAG                 * no - set flag for end tag
          CALL      GNXT0000                     * get next character
          BRANCH    EXIT,LXML9999                * end of file
          GOTO      LXML0000
.
.         We have a ">" character, so check if we need to
.         do anything with the tag text string we just loaded.
.
LXML6000  RESET     TAGSTRNG
.
.         Check if the tag was an open and close tag, in which case
.         we don't need to do anything
.
          MATCH     SLASH,LASTCHAR
          IF        @EQUAL
            MOVE      ZERO,TYPEFLAG
            GOTO      LXML9100
          ENDIF
.
          BRANCH    TYPEFLAG,LXML7000:           * end tag
                             LXML9100            * data string
.
          GOTO      LXML9000                     * get next character
.
LXML7000  MATCH     "ItemNum",TAGSTRNG
          IF        @EQUAL
            MOVE      DATSTRNG,TXITEM
          ENDIF
.
          MATCH     "ScheduleFee",TAGSTRNG
          IF        @EQUAL
            SQUEEZE   DATSTRNG
            MOVE      DATSTRNG,TXAMT
          ENDIF
          MATCH     "Description",TAGSTRNG
          IF        @EQUAL
            MOVE      DATSTRNG,TXDESC
          ENDIF
.
          MATCH     "Data",TAGSTRNG
          GOTO      LXML9100 IF NOT EQUAL
.
          PACK      KEY9,TXITEM,SP70
          CALL      WRTEMP1
.
          MOVE      SP70,TXITEM
          MOVE      ZERO,TXAMT
          MOVE      ZERO,TXDESC
.
LXML9000  MOVE      TWO,TYPEFLAG
.
          PACK      DATSTRNG,SP100,SP100,SP100,SP100,SP100:
                             SP100,SP100,SP100,SP100,SP100:
                             SP100,SP100,SP100,SP100,SP100:
                             SP100,SP100,SP100,SP100,SP100:
                             SP100,SP100,SP100,SP100,SP100:
                             SP100,SP100,SP100,SP100,SP100:
                             SP100,SP100,SP100,SP100,SP100:
                             SP100,SP100,SP100,SP100,SP100
          CLEAR     DATSTRNG
.
LXML9100  CALL      GNXT0000                     * get next character
          BRANCH    EXIT,LXML9999                * end of file
          GOTO      LXML0000
.
LXML9999  RETURN
+
.*****************************************************************************
.*                          GNXT0000               Called by: LXML0000       *
.*          Get the next character in the array string                       *
.* Returns : EXIT  0 = next character read successfully                      *
.*                 1 = end of file found                                     *
.*****************************************************************************
.
GNXT0000  MOVE      TMPSTRNG,LASTCHAR            * save the last character
          BUMP      TMPSTRNG                     * move to next character
          GOTO      GNXT9000 IF NOT EOS          * not end of record
.
          READ      FLATFILE,SEQ;TMPSTRNG        * read next record
          GOTO      GNXT9100 IF OVER             * eof - finished
.
GNXT9000  MOVE      ZERO,EXIT
          GOTO      GNXT9999
.
GNXT9100  MOVE      ONE,EXIT
.
GNXT9999  RETURN
+
.*****************************************************************************
DISPLMBS  IF        XMLFLAG=1
            DISPLAY   *P53:2,*V2LON,"- Upload MBS from XML file ";
          ENDIF
            DISPLAY   *P53:2,*V2LON,"- Upload MBS from ASCII file ";
          RETURN
+
.*****************************************************************************
.*                          GXML0000               Called by: MAIN0000       *
.*                       Keyin the response file name                        *
.* Returns:  XMLFLNAM - XML File name                                          *
.*           EXIT  0 = valid filename input                                  *
.*                 1 = nothing input                                         *
.*****************************************************************************
.
GXML0000  KEYIN     *P1:10,*EF,"Keyin Load Path & File Name : ":
                    *P34:10,*V2LON,XMLFLNAM
.
          PACK      XMLFLNAM,XMLFLNAM,SP30
          MATCH     SP30,XMLFLNAM
          IF        @EQUAL
            PRINT     *1,"Filename not entered"
            GOTO      GXML9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FLATFILE,XMLFLNAM
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"File not found.  ";
            CALL      HITENTER
            GOTO      GXML0000
          ENDIF
          CLOSE     FLATFILE
.
          STRIP     XMLFLNAM
.
          CALL      CFIL0000               * copy file
          BRANCH    EXIT,GXML9100          * unable to copy
.
          MOVE      ZERO,EXIT
          GOTO      GXML9999
.
GXML9100  MOVE      ONE,EXIT
.
GXML9999  RETURN
+
.*****************************************************************************
.*                          CFIL0000               Called by: MAIN0000       *
.*          Copy the XML file to a ".txt" file so TBL can open it            *
.* Requires: XMLFLNAM - DHS VINAH XML Response File name                     *
.* Returns : TEMPFILA - temporary ".txt" filename                            *
.*           EXIT  0 = file successfully copied                              *
.*                 1 = unable to copy file                                   *
.*****************************************************************************
.
.>>>>>>   Need to change from port based tempfile
.
CFIL0000  MOVE      "cat ",CMDLINE
          ENDSET    CMDLINE
          APPEND    XMLFLNAM,CMDLINE
          APPEND    PIPE,CMDLINE
          APPEND    SEDSTRNG,CMDLINE
          APPEND    " > ",CMDLINE
          APPEND    TEMPFILA,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MATCH     "0       ",TASKID
          IF        @EQUAL
            MOVE      ZERO,EXIT
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Unable to convert XML file.  ";
            CALL      HITENTER
            PRINT     *1,"Unable to convert XML file"
            MOVE      ONE,EXIT
          ENDIF
.
CFIL9999  RETURN
+
.******************************************************************************
.*                                 KASCI000                                   *
.*                            Keyin ASCI file                                 *
.* Returns: EXIT  0 = no file name entered                                    *
.*                1 = valid filename entered                                  *
.******************************************************************************
KASCI000  KEYIN     *P1:10,*EF,"Keyin Load Path & File Name : ",*V2LON,FNAMEI
.
          DISPLAY   *P1:24,*EL,"** The Effective from date of Item will be ":
                               "set to Current date.";
.
          PACK      FNAMEI,FNAMEI,SP20,SP20
.
          MATCH     SP70,FNAMEI                  * anything entered ?
          GOTO      KASCI950 IF EQUAL            * no
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      MBSFILE,FNAMEI               * open file
          TRAPCLR   IO
.
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"File not found.  ";
            CALL      HITENTER
            GOTO      KASCI0000
          ENDIF
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      KASCI999
KASCI950  MOVE      ONE,EXIT
.
KASCI999  RETURN
+
.****************************************************************************
.*                              KILT0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILT0000  CLOSE     TEMP1                        * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE      * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
...       CLOSE     TEMP2                                               D-90202
...       CLEAR     CMDLINE                                             D-90202
...       PACK      CMDLINE,ERASE,TEMPFILE2     * set file erase comd.  D-90202
...       EXECUTE   CMDLINE,TASKID               * erase temp file      D-90202
.
KILT9999 RETURN
+
.****************************************************************************
.*                              CREA0000               Called by:           *
.*               Create a temp file                                         *
.****************************************************************************
CREA0000    CALL      KILT0000
            CLEAR     CMDLINE
            PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
            EXECUTE   CMDLINE,TASKID               * create temporary index file
            OPEN      TEMP1,TEMPFILE               * open temp index file.
.
CREA9999    RETURN
+
.
.***************************************************************************
. INDT0000   : initialize date
. Called by  : KKEY0000,KEFD0000
.***************************************************************************
INDT0000  MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
INDT9999  RETURN
+
.
.***************************************************************************
.*                              ENDIT                                      *
.***************************************************************************
+
.
ENDIT     PACK      PTCNITMM,ZERO,ZERO
          WRITAB    CONTROLF,SEVENTY9;*21,PTCNITMM
.
          MATCH     "IBARSH",PGM
          IF        @EQUAL
            MOVE      SP10,PTCNWITM
            WRITAB    CONTROLF,HUND10;*51,PTCNWITM
          ENDIF
.
ENDIT2    CHAIN     PGM
          STOP
.
+
.******************************************************************************
.                   TEMP FILE I/O                                             *
.******************************************************************************
RSTEMP1   READ      TEMP1,KEY9;;
          GOTO      OVERCOND IF OVER
          RETURN
RKTEMP1   RESET     KEY9
          MOVE      ZERO,OVRCD
          READKS    TEMP1;TXITEM,TXDESC,TXAMT
          GOTO      OVERCOND IF OVER
          RETURN
WRTEMP1   MOVE      ZERO,OVRCD
          WRITE     TEMP1,KEY9;TXITEM,TXDESC,TXAMT
          GOTO      OVERCOND IF OVER
          RETURN
.
.==============================================================================
.
          INC       STDGENCD
.
          INC       TFILENAM
.
          INC       STDKWSCD
          INC       IBAGSTKY
          INC       IBAGKIKY
          INC       IBAGSTIO/INC
          INC       PATCODKY
          INC       PATITMKY
          INC       PATIKIPR
          INC       PATITMDS
          INC       PATITMRD
.
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
          INC       PATCODIO/INC
          INC       PATITMIO/INC
          INC       PATADIIO/INC
          INC       PATIKIIO/INC

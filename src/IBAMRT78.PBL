. ***************************************************************************
. *              P R O G R A M  S U M M A R Y                               *
. *              -------------  -------------                               *
. *                                                                         *
. *     PROGRAM NAME:     IBAMRT78                                          *
. *                                                                         *
. *     FUNCTION:         Medical Record coding Report                      *
. *                                                                         *
. *     DATE WRITTEN:     01/05/2003                                        *
. *                                                                         *
. *     AUTHOR:           Lina Vo       (I.B.A)    CAR38005                 *
. *                                                                         *
. *     MODIFICATIONS:                                                      *
. ***************************************************************************
. *       V11.05.01 06/03/2025  Thanh T          TSK 0949724                *
. *                 Recompiled for MRTEXTFD changes                         *
. ***************************************************************************
. *       V10.02.01 28/03/2011  Mike Laming   CAR 240107                    *
. *                 Mods to PATECDaf & PATECOaf variables & Keys - file     *
. *                 change                                                  *
. *       V10.01.01 10/02/2011  Jill Parkinson CAR 233088                   *
. *                 Added pmsvx1af                                          *
. *       V9.07.01  11/09/2006  Peter Vela      CAR 118227                  *
. *                 Recompiled for MRTEXTFD MRTEXTIO                        *
. *       V9.03.01  18/12/2003  Sylvek Litewka  CAR 20222                   *
. *                 Modified procedure GTDIAG00 to obtain the Diagnosis     *
. *                 description from patecdaf.PTEDEDSC instead of reading   *
. *                 ICD Diagnosis/Disease file.                             *
. *       V9.02.00  01/05/2003 Created                                      *
. *                                                                         *
.****************************************************************************
+
          INC       STD001FD
.
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Control file
          INC       PATMA1FD/INC            * Master file
          INC       PATMI1FD/INC            * Admission file
          INC       PMSVX1FD/INC            * Admission file
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
.
          INC       MRTEPSFD/INC
          INC       MRTEXTFD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATICDFD/INC
          INC       PATICOFD/INC
.
. ----- Program Variables -----
.
CDYSDAYS  FORM      6                       * CALCDAYS no of days
CDYSFDTE  DIM       8                       * CALCDAYS from date (ccyymmdd)
CDYSTDTE  DIM       8                       * CALCDAYS to date (ccyymmdd)
CDYSYEAR  FORM      2                       * CALCDAYS year
CURDTE    DIM       8                       * Current date (ccyymmdd)
CURFLAG   FORM      1
.
DELETED   DIM       3
DIAGNSIS  DIM       50                      * Diagnosis Code / Description
DISPDATE  DIM       11                      * Display Discharge Date
DSPSTDTE  DIM       10                      * Display start date (dd/mm/ccyy)
.
EXTRCODE  DIM       4
EXTRDESC  DIM       35
.
FORMTNAM  DIM       50
FORM3     FORM      3
.
REPTITLE  DIM       50
MTHNAM3   DIM       3
.
PSAGETYP  DIM       3
PSAGECON  DIM       3
.
TOTRDCNT  FORM      4                       * Total record counter
.
. ----- Program Constants -----
.
JAN       INIT      "January"
FEB       INIT      "February"
MAR       INIT      "March"
APR       INIT      "April"
MAY       INIT      "May"
JUN       INIT      "June"
JUL       INIT      "July"
AUG       INIT      "August"
SEP       INIT      "September"
OCT       INIT      "October"
NOV       INIT      "November"
DEC       INIT      "December"
.
EXTRACT   INIT      " Extract"
HYPHIN    INIT      " - "
.
PRGID     INIT      "IBAMRT78"
PRGNAM    INIT      "Medical Record coding Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      CODE0000                * Keyin the extract code
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML1000,ML1000
          GOTO      ML1000
.
ML4000    CALL      PROC0000                * Process the files
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000             Called by: ML0000   *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpr1af";
          OPEN      PATPR1A1,"patpr1af"
.
          DISPLAY   *P64:24,"mrtepsaf";
          OPEN      MRTEPSA1,"mrtepsaf"
          DISPLAY   *P64:24,"mrtextaf";
          OPEN      MRTEXTA1,"mrtextaf"
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA1,"patecdaf"
          DISPLAY   *P64:24,"paticddf";
          OPEN      PATICDD1,"paticddf"
          DISPLAY   *P64:24,"pati10df";
          OPEN      PATI10D1,"pati10df"
          DISPLAY   *P64:24,"paticdof";
          OPEN      PATICDO1,"paticdof"
          DISPLAY   *P64:24,"pati10of";
          OPEN      PATI10O1,"pati10of"
.
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D
.
          MOVE      ONE,CNEWDS              * ? keyin on display
          MOVE      ONE,CNOUNDLN            * Dont print report header
          CLOCK     TIME,CTIMEIS
          PACK      CURDTE,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE 
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          MOVE      ZERO,CPAGENO            * Reset the page no
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Extract Code":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  CODE0000              Called by: ML0000   *
.*                            Keyin The Extract Code                          *
.******************************************************************************
CODE0000  MOVE      "17",CCOL
          KEYIN     *P1:9,"Extract Code  : ",*EF:
                    *DV,EXTRCODE:
                    *PCCOL:9,*RV,EXTRCODE;
.
CODE9000  MOVE      ZERO,EXIT
          GOTO      CODE9999
.
CODE9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Process The Files                             *
.******************************************************************************
PROC0000  MOVE      ZERO,TOTRDCNT           * Reset the total record counter
          MOVE      ZERO,CPAGENO            * Reset the page counter
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Printing : ";
.
. ----- Loop thru the extract file -----
.
          PACK      KEY4,EXTRCODE,SP10
          CALL      RDMREX1
          BRANCH    OVRCD,PROC9999
.
          STRIP     EXTRCODE
          STRIP     MREXDESC
          PACK      REPTITLE,EXTRCODE,HYPHIN,MREXDESC,EXTRACT,SP70
          RESET     EXTRCODE
          RESET     MREXDESC
.
          CALL      HEAD0000                * Print report header
          CALL      SHED0000                * Print sub report header
.
          PACK      KEY12,MREXEID,SP70
          CALL      RSMREP1
PROC1000  CALL      RKMREP1
          BRANCH    OVRCD,PROC8000
.
          MATCH     MREXEID,MREPEID
          GOTO      PROC8000 IF NOT EQUAL
.
PROC2000  CALL      PATROW00                * Get patient details
          CALL      PRNT0000                * Print a report line
.
          GOTO      PROC1000
.
PROC8000  CALL      UND132                  * Print underline
          PRINT     *1,"*** End of Report ***"
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
+
.------------------------------------------------------------
. Get Patient
.------------------------------------------------------------
PATROW00  PACK      KEY8,MREPVIS,SP70
          CALL      RDMISS1
          IF        OVRCD=1
            MOVE      "Invalid Admission",FORMTNAM
          ENDIF
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "Invalid U/R",FORMTNAM
          ELSE
            STRIP     PSNAME
            PACK      FORMTNAM,PSNAME,COMMA,PGNAME,SP70
          ENDIF
.
          PACK      KEY50,SP70
          PACK      KEY50,MREPVIS,HYPHIN,FORMTNAM
.
          PACK      KEY8,MREPVIS,SP70
          CALL      RDDSCH1
          IF        OVRCD=1
            MOVE      SP70,DISPDATE
.
            MATCH     "1",MREXDIND          * check for dis pats only set to yes
            GOTO      PATROW99 IF EQUAL     * pat is not discharge find next pat
          ELSE
            UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          CALL      GTDIAG00   * Get Primary Diagnosis Code for this Patient
.
          MATCH     "1",MREPDEL
          IF        @EQUAL
            MOVE     "Yes",DELETED
          ELSE
            MOVE     "No",DELETED
          ENDIF
.
PATROW99  RETURN
.-----------------------------------------------------------------------------
.   Read Patient Episode Disease File with Admission No, Episode No=1 &
.   Episode Counter=1 for Primary Diagnosis!
.   RETURN VAR: DIAGNSIS - Diagnosis Code / Description
.-----------------------------------------------------------------------------
GTDIAG00  MOVE      ONE,FORM2    * Use as Episode=1 & Unique Counter=1
          MOVE      ONE,FORM3    * use for Unique Counter=1           *I-240107
          PACK      KEY13,MREPVIS,FORM2,FORM3,SP70                    *C-240107
          CALL      RDPTECD1
          IF        OVRCD=1
            MOVE      SP70,DIAGNSIS         * Set Description to blank!
            GOTO      GTDIAG99              * No Diagnosis Record Found
          ENDIF
.
GTDIAG30  STRIP     PTEDCODE
          PACK      DIAGNSIS,PTEDCODE,HYPHIN,PTEDDESC
.
GTDIAG99  RETURN
.******************************************************************************
.*                                  HEAD0000              Called by: PROC0000 *
.*                             Print Report Header        PRNT0000 & TAIL0000 *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print report header
          PRINT     *40,"Extract ID: ",REPTITLE 
          ADD       "1",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PROC0000 *
.*                             Print A Report Line                            *
.******************************************************************************
PRNT0000  IF        CLNO>=58
            CALL      UND132                * Print underline
            CALL      HEAD0000              * Print report header
            CALL      SHED0000              * Print report sub header
          ENDIF
.
          PRINT     *1,"|",AURNO,*10,"|",KEY50,*61,"|",DIAGNSIS:
                    *112,"|",DISPDATE,*124,"|  ",DELETED:
                    *132,"|"
.
          ADD       ONE,CLNO
          ADD       "1",TOTRDCNT            * Increment the total record counter
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
.*                                  SHED0000              Called by: PRNT0000 *
.*                           Print Report Sub Header                          *
.******************************************************************************
SHED0000  CALL      UND132                  * Print underline
            PRINT     *1,"|  U/R ",*10,"|",*61,"|":
                      *112,"|Discharged",*124,"|":
                      *132,"|",*N:
                      *1,"| Number",*10,"| Patient":
                      *61,"| Principle Diagnosis Description":
                      *112,"|   Date  ",*124,"|Deleted":
                      *132,"|"
          CALL      UND132                  * Print underline
          ADD       "4",CLNO
.
SHED9000  MOVE      ZERO,EXIT
SHED9999  RETURN
+
.******************************************************************************
.*                                  TAIL0000              Called by: PRNT0000 *
.*                              Print Report Tail                             *
.******************************************************************************
TAIL0000  CALL      UND132                  * Print underline
          ADD       "1",CLNO
.
          IF        CLNO>=51
            CALL      HEAD0000              * Print report header
          ENDIF
.
TAIL9000  MOVE      ZERO,EXIT
TAIL9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS                * Calculate no of days
.
          INC       PATCODIO/INC            * Codes file
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC            * Admission file
          INC       PMSVX1IO/INC            * Admission file
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       MRTEPSIO/INC
          INC       MRTEXTIO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATICDIO/INC
          INC       PATICOIO/INC
.
          INC       CLPMSPX2
+
.


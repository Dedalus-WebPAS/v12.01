. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBABIL27                                            *
. *                                                                           *
. *     FUNCTION:         JOURNALS REPORT                                     *
. *                                                                           *
. *     DATE WRITTEN:     21/09/87                                            *
. *                                                                           *
. *     AUTHOR:           GRAEME WILLIAMS (I.B.A)                             *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *****************************************************************************
. *  This program also has a Medical services counterpart                     *
. *****************************************************************************
. *       V10.12.01  09/01/2018  J.Tan          TSK 0848398                   *
. *                  Increased the size of Amount column                      *
. *       V10.06.01  23/06/2015  Ania P         CAR 314469                    *
. *                  Increased the size of the Description column             *
. *       V10.04.01  16/01/2014  Peter Vela     CAR 295999                    *
. *                  Moved read to PMSVX1 in CNTI                             *
. *       V10.03.02  14/11/2013  J.Tan          CAR 285294                    *
. *                  Added option to create Export file                       *
. *       V10.03.01  18/05/2012  J.Tan          CAR 253244                    *
. *                  Mods to set JOURED to '~~' for 'Finish'                  *
. *       V9.12.01   03/09/2009  Sandra Barcham CAR 186131                    *
. *                  Fix print of next category at the end                    *
. *       V9.08.02   30/03/2007  Mike Lmaing    CAR 126377                    *
. *                  Change at ENDTOT2S to print GRAND TOTAL on same page SSS *
. *       V9.08.01   09/02/2007  J.Tan         CAR 120001                     *
. *                  Mods for NZ Private billing                              *
. *       V9.04.02   12/08/2005  Jeni Tan       CAR 62750                     *
. *                  Removed redefined variables                              *
. *       V9.04.01   27/08/2003  Jeni Tan       CAR 52835                     *
. *                  Mods for Multi-Hospitals                                 *
. *****************************************************************************
. *       V9.03.02   13/08/2003  Lina Vo        CAR 41196                     *
. *                  Recompiled for big change in PATSELFN.                   *
. *       V9.03.01   04/06/2003  Dean Cramer    CAR 38200                     *
. *                  Fixed print overlaps for web                             *
. *                  Fixed heading in Outpatient options                      *
. *       V5.08.01   29/08/2000  Caleb Sharman                                *
. *                  Changed Health Fund variables to be 8 chars              *
. *       V5.08.B06  18/04/2000  Jill Habasque                                *
. *                  Mods to printing of journal total                        *
. *       V5.08.B05  29/03/2000  Steve Downing                                *
. *                  Fixed "?" scan for journal codes                         *
. *       V5.08.B04  24/03/2000  Jill Habasque                                *
. *                  Mods to printing of gst amount                           *
. *       V5.08.B03  08/03/2000  Steve Downing                                *
. *                  Fixed read position for IBCNUGST                         *
. *       V5.08.B02  28/02/2000  Steve Downing                                *
. *                  Mods to display GST journals on a separate page          *
. *       V5.08.B01  07/01/2000  Steve Armstrong                              *
. *                  Mods to use PATSTRYR to get start of year date           *
. *****************************************************************************
. *                   V5.07.01  31/03/1999  Jim Stathopoulos                  *
. *                             Modified default Starting and Ending Journal  *
. *                             and Starting Date                             *
. *                   V5.07.B02 22/01/1999  Nicole Harrington 507 rebound 115 *
. *                             Mods to print century on date                 *
. *                   V5.07.B01 11/11/1998  Jim Stathopoulos                  *
. *                             507 Changes                                   *
. *****************************************************************************
. *                       V4.01 22/11/88 Graeme Williams                      *
. *                             Allow for PINVSYS = 4                         *
. *                       V4.02 07/12/88 J.Tan     (I.B.A.)                   *
. *                             Mods to use Input Jrnl date                   *
. *                             SRF 005611                                    *
. *                       V4.03 31/01/89 K.Peak    (I.B.A.)                   *
. *                             Added PATSELFN                                *
. *                       V4.04 10/02/89 Graeme Willaism                      *
. *                             Make OUTDTRO site dependent                   *
. *                       V5.01 13/04/89 Graeme Williams                      *
. *                             New index added to Dtrans                     *
. *                             SRF 5645                                      *
. *                       V5.02 05/05/89 Graeme Williams                      *
. *                             Added PINVCADM to inv file                    *
. *                             SRF 4219                                      *
. *                       V5.03 11/05/89 D Di.Paola                           *
. *                             Alter report to allow "8"                     *
. *                             character U/R & Adm No.                       *
. *                             Put in STD001FD                               *
. *                       V5.04 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                       V5.05 21/06/89 J.Tan                                *
. *                             Print grand total                             *
. *                             SRF 101110                                    *
. *                       V5.06 23/06/89 J.Tan                                *
. *                             Added summary option                          *
. *                             SRF 100253                                    *
. *                       V5.07 30/06/89 DIG                                  *
. *                             Re-Compiled for changes to                    *
. *                             OUTSITFD                                      *
. *                                                                           *
. *                    V5.01.01 05/07/89  S. O'Gorman                         *
. *                             Converted for N.Z.                            *
. *                    V5.01.02 20/11/89 J.Tan                                *
. *                             Added other financial opt.                    *
. *                             in PATSELFN.    SRF 102252                    *
. *                    V5.01.03 19/04/90 D.Di.Paola                           *
. *                             fixed ok to continue ? on                     *
. *                             1st & 2nd screens SRF 520                     *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
.*        V5.01.122 12/06/92  Graeme Williams               SRF 114865        *
.*                  Changes for 9 character misc items                        *
.*                                                                            *
.*        V5.01.123 23.02.92 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Modify to use standard routine KEYDATE                    *
.*        V5.03.B1  06/06/95  Paul Howells                  SRF 136735        *
.*                  Recompiled for PATSELFN                                   *
.******************************************************************************
.
          INC       STD001FD   
.
          INC       AAEDTRFD/INC
          INC       IBACONFD/INC
          INC       IBAPASFD/INC
          INC       IBASECFD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
          INC       PATCODFD/INC
          INC       PATDRGFD/INC
          INC       PATDTRFD/INC
          INC       PATINVFD/INC
          INC       PATJRNFD/INC
          INC       PATMA1FD/INC
          INC       PMSVX1FD/INC
          INC       PATHSPFD/INC
          INC       PATSELDT/INC
.
. LOCAL VARIABLES
. ---------------
.
CCFORM    FORM      2
CSTRTYR   DIM       8                            * for PATSTRYR
CURJRNL   DIM       2
CODE      DIM       2
COTHSFN   FORM      1
.
FORM82    FORM      8.2
.
DDFORM    FORM      2
DIM3      DIM       3
DIM10     DIM       10
DIM25     DIM       25
DIM22     DIM       22 
DIM23     DIM       23
.
EXPORTFL  FORM      1         * Create Export File
GNDALJ    FORM      8.2
GNDFLG    FORM      1
GTHCSCOD  DIM       4
.
IDATEF    DIM       8
IDATET    DIM       8
.
JOURED    DIM       2
JOURST    DIM       2
JRNLDESC  DIM       20
.
MMFORM    FORM      2
.
PASS      FORM      1
PATSTCUR  DIM       4                            * for PATSTRYR
.
SAVTOTJ   FORM      8.2
SUMFLG    FORM      1
.
TOTALJ    FORM      8.2
TOTFLG    FORM      1
.
YYFORM    FORM      2
.
.
. PROGRAM CONSTANTS
. -----------------
CODEJ     INIT      "J "
FIVE9     FORM      "59"
.
.
VERSION   INIT      "V12.01.00"
PRGID     INIT      "IBABIL27"
PRGNAM    INIT      "Journals Report"
+
.        Start of Program
.
         DISPLAY    *ES;
         CALL       DISPHEAD
.
         DISPLAY    *P56:24,"Opening patma1af";
         OPEN       PATMA1A1,"patma1af"
.
         DISPLAY    *P64:24,"patmx1af";
         OPEN       PATMX1A1,"patmx1af"
.
         DISPLAY    *P64:24,"patcodes";
         OPEN       PATCODE1,"patcodes"
.
         DISPLAY    *P64:24,"patdrgaf";
         OPEN       PATDRGA2,"patdrgaf"
.
         DISPLAY    *P64:24,"patjrnlf";
         OPEN       PATJRNL1,"patjrnlf"
.
         DISPLAY    *P64:24,"patinvrf";
         OPEN       PATINVR1,"patinvrf"
.
         DISPLAY    *P64:24,"patdtraf";
         OPEN       PATDTRA1,"patdtraf"
.
         DISPLAY    *P64:24,"outsitaf";
         OPEN       OUTSITA1,"outsitaf"
.
         DISPLAY    *P64:24,"aaedtref";
         OPEN       AAEDTRE1,"aaedtref"
.
         DISPLAY    *P64:24,"pmsvx1af";
         OPEN       PMSVX1A1,"pmsvx1af"
.
OPFIL    DISPLAY    *P64:24,"controlf";
         OPEN       CONTROLF,"controlf"
.
         READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
.
.        Start Main Program
.
START    MOVE       ZERO,PASS
         MOVE       ZERO,SAVTOTJ
         MOVE       ZERO,CPAGENO
         DISPLAY    *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Journals Report"
.
REKSTRT  KEYIN      *P1:7,"Option : ",*EL,*V2LON,OPTION
.
         COMPARE    ZERO TO OPTION
         GOTO       ENDIT IF EQUAL
.
REPEAT   BRANCH     OPTION OF OPTION1
         BEEP
         GOTO       REKSTRT
.
NOJRNLS  IF         PASS=0
           ADD        ONE,PASS
           GOTO       STLOOP
         ENDIF
         DISPLAY    *P1:24,*EL,*P20:24,*V2LON:
                    "** No Patients on File **",*W3;
         GOTO       START
+
.
.        Get Date range for this report
.
OPTION1  DISPLAY    *P1:9,*EF:
                     *P1:9,"Starting Date     : ":
                    *P1:11,"Ending   Date     : ":
                    *P1:13,"Starting Journal  : ":
                    *P1:15,"Ending   Journal  : ":
                    *P1:17,"Summary only (Y/N): "
.
KSDATE   DISPLAY    *P20:9,*EL,*P20:11,*EL,*P20:13,*EL,*P20:15,*EL,*P20:17,*EF
.
         CALL       PATSTRYR                     * get start of year date
.
         MOVE       NINE,CVERT
         MOVE       "20",CCOL
         UNPACK     CSTRTYR,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,IDAY
         MOVE       XMON,IMON
         MOVE       XYEAR,IYEAR
         MOVE       XCENT,ICENT
.
         CALL       KEYINDTE
         COMPARE    ZERO,IDAY
         GOTO       START IF EQUAL
.
         PACK       IDATEF,ICENT,IYEAR,IMON,IDAY
         REP        " 0",IDATEF
         UNPACK     IDATEF,XCENT,XYEAR,XMON,XDAY
         DISPLAY    *P20:9,*V2LON,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
         MOVE       TEN1,CVERT
         MOVE       CDD,IDAY
         MOVE       CMM,IMON
         MOVE       CYY,IYEAR
         MOVE       CCC,ICENT
.
         CALL       KEYINDTE
         PACK       IDATET,ICENT,IYEAR,IMON,IDAY
         REP        " 0",IDATET
.
.        Validate date range
.
         MATCH      IDATEF,IDATET
         GOTO       KJCODE IF NOT LESS
.
         DISPLAY    *P40:CVERT,*EL,*V2LON,*B:
                    "** Starting date > Ending date **",*W2;
         GOTO       KSDATE
.
.        Get the Journal codes to be reported on
.
KJCODE   MOVE       SP2,JOURST
         KEYIN      *P20:15,*EL,*P20:13,*EL,*V2LON,JOURST
         ENDSET     JOURST
         APPEND     SP2 TO JOURST
         RESET      JOURST
.
         MATCH      QUEST,JOURST
         IF         @EQUAL
           CALL      GETSCR00
           MOVE      CODEJ,CODE
           CALL      PATCODDS
           CALL      PUTSCR00
           GOTO      KJCODE
         ENDIF
.
         MATCH      SP2,JOURST
         IF         @EQUAL
           DISPLAY    *P20:13,*EL,*V2LON,"Start"
         ELSE
           DISPLAY    *P20:13,*EL,*V2LON,JOURST
         ENDIF
.
KJCODE2  MOVE       "~~",JOURED
         KEYIN      *P20:15,*EL,*V2LON,JOURED
*         PACK       JOURED,JOURED,SP2
         RESET      JOURED,2
         RESET      JOURED
.
         MATCH      QUEST,JOURED
         IF         @EQUAL
           CALL      GETSCR00
           MOVE      CODEJ,CODE
           CALL      PATCODDS
           CALL      PUTSCR00
           GOTO      KJCODE2
         ENDIF
.
         MATCH      "~~",JOURED
         IF         @EQUAL
           DISPLAY    *P20:15,*EL,*V2LON,"Finish"
         ELSE
           DISPLAY    *P20:15,*EL,*V2LON,JOURED
.
           MATCH      JOURST,JOURED
           GOTO       KSUMM IF NOT LESS
.
           DISPLAY    *P40:15,*EL,*V2LON,*B:
                      "** Ending Journal < Starting Date **",*W2;
           GOTO       KJCODE
         ENDIF
.
.        Summary only
.
KSUMM    MOVE       ZERO,SUMFLG
         MOVE       SP3,DIM3
         KEYIN      *P20:17,*EL,*V2LON,ANS
         REP        UPPLOW,ANS
         CMATCH     "Y",ANS
         GOTO       KSUM10 IF EQUAL
.
         CMATCH     "N",ANS
         GOTO       KSUM20 IF EQUAL
         BEEP
         GOTO       KSUMM
.
KSUM10   MOVE       ONE,SUMFLG
         GOTO       KSUM30
.
KSUM20   MOVE       TWO,SUMFLG
.
KSUM30   LOAD       DIM3 USING SUMFLG OF DYES,DNO
         DISPLAY    *P20:17,*V2LON,DIM3
.
.
.        Check that this is alright
.
SFIELD   CALL       CONTQST                      * Ok to continue ?
         BRANCH     CEXIT,SFLD1:                 * yes
                          REPEAT:                * no
                          START                  * cancel
.
SFLD1    CALL       PATSELFN
         BRANCH     EXIT,START
.
         IF         IBCNMHOS=1
           CALL       KHOSID                     * Hospital ID
           BRANCH     EXIT,START
         ENDIF
.
         CALL      CRTX0000                 * Create Export File for Excel
.
         CALL       CONTQST                      * Ok to continue ?
         BRANCH     CEXIT,SFLD2:                 * yes
                          SFLD1:                 * no
                          START                  * cancel
.
SFLD2    IF        EXPORTFL=1
           CALL      EHED0000               * Print the heading
         ENDIF
.
.        Main Loop - For each journal in the codes file,
.                    loop over JRNLS File, and index into the DTRAN file
.
STLOOP   MOVE       SP3,JCODE
         MOVE       SP70,JRNLDESC
         MOVE       ZERO,GNDALJ
         MOVE       ZERO,GNDFLG
         MOVE       "99",CLNO
.
         DISPLAY    *P1:24,*EL,*P20:24,"Admission No :";
.
.        Check if the starting journal type actually exists
.
         PACK       KEY5,CODEJ,JOURST,SP2
         CALL       RDCODE1
         COMPARE    ZERO,OVRCD
         GOTO       CHKJST IF EQUAL
.
         CALL       RDSCODE1
NXTCODE  CALL       RDKCODE1
         BRANCH     OVRCD OF ENDP
.
         MATCH      TCODE,CODEJ
         GOTO       ENDP IF NOT EQUAL
.
CHKJST   MATCH      SP3,ACODE
         GOTO       NXTCODE IF EQUAL
.
.        Start new Journal Type
.
NEWJCD   COMPARE    ONE,SUMFLG
         GOTO       NEWJ10 IF EQUAL
.
         MOVE       "99",CLNO          * Force a new page for each type
.
NEWJ10   MOVE       ZERO,TOTALJ        * Totals for each type only
.
         IF         IBCNUGST=2 | IBCNUGST=3
           COMPARE    ZERO,PASS
           IF         @EQUAL
             MATCH    "G   ",THCSCOD       * Skip GST journals for first pass
             GOTO     NXTCODE IF EQUAL
           ELSE
             MATCH    "G   ",THCSCOD       * Put GST journals on end of report
             GOTO     NXTCODE IF NOT EQUAL * Skip others
             MOVE     THCSCOD,GTHCSCOD     * Hold on to code
           ENDIF
         ENDIF
.
NEWJ20   MOVE       ACODE,CURJRNL
         MOVE       TDESC,JRNLDESC
         PACK       KEY24 WITH CURJRNL,IDATEF,SP9
         CALL       RDSJRNL1
NEXTJ    CALL       RDKJRNL1
         BRANCH     OVRCD OF ENDP
.
         MATCH      JCODE,JOURED
         GOTO       ENDP IF LESS
.
         MATCH      JDATE,IDATET
         GOTO       NEWJ IF LESS
.
         MATCH      JCODE,CURJRNL
         GOTO       RDDTR IF EQUAL
.
.        New Code - if this is not the first code, print totals
.
NEWJ     BRANCH     EXPORTFL,NXTCODE
.
         COMPARE    "99",CLNO
         GOTO       NXTCODE IF EQUAL
.
         BRANCH     SUMFLG OF NEW10
.
         CALL       PAGEND
         PRINT      *1,"!",*67,"TOTAL ",*76,TOTALJ,*132,"!"
         MOVE       ZERO,TOTALJ
.
         MATCH      "G   ",GTHCSCOD       * Is it a GST journal
         GOTO       NXTCODE IF NOT EQUAL  *No, go to next record
.
         MOVE       ONE,TOTFLG  * Yes, set flag for total already printed. This
         GOTO       NXTCODE     * is a bandaid to stop total being printed
.                               * twice for GST journals
NEW10    COMPARE    "48",CLNO
         CALL       HEAD IF NOT LESS
.
         COMPARE    ZERO,TOTALJ
         GOTO       NXTCODE IF EQUAL
.
         MOVE       TDESC,DIM22
         PRINT      *1,"! ",ACODE,"  ",DIM22,*30,"!  ",TOTALJ,*50,"!"
         MOVE       ZERO,TOTALJ
         ADD        ONE,CLNO
         GOTO       NXTCODE
.
.        Read in Invoice/Dtran records, and master file record
.
RDDTR    MOVE       JINVN,KEY8
         CALL       RDINV1
         BRANCH     OVRCD OF INVRERR
.
         DISPLAY    *P70:24,*EL,PINVNO;
.
         COMPARE    FSTSYS,PINVSYS
         GOTO       NEXTJ IF LESS
.
         COMPARE    PINVSYS,FEDSYS
         GOTO       NEXTJ IF LESS
.
         MATCH      FSTSITE,PINVSITE
         GOTO       NEXTJ IF LESS
.
         MATCH      PINVSITE,FEDSITE
         GOTO       NEXTJ IF LESS
.
         MATCH      SP6,PINVSITE
         GOTO       CNTI IF EQUAL
.
         MOVE       PINVSITE,KEY6
         CALL       RDSITA1
         BRANCH     OVRCD,NEXTJ
.
         MATCH      SP3,FSTDEPT
         GOTO       CNTI IF EQUAL
.
         MATCH      FSTDEPT,OSTSYST
         GOTO       NEXTJ IF LESS
.
         MATCH      OSTSYST,FEDDEPT
         GOTO       NEXTJ IF LESS
.
CNTI     IF          IBCNMHOS=1
.          MATCH       SP10,PTHSHOSP
.          IF          !@EQUAL
.            PACK        KEY8,PINVADM,SP8
.            CALL        RDPMVX11
.            BRANCH      OVRCD,NEXTJ
.            MATCH       PMVXMHOS,PTHSHOSP
.            GOTO        NEXTJ IF NOT EQUAL
.          ENDIF
.
           PACK        KEY8,PINVADM,SP8
           CALL        RDPMVX11
           BRANCH      OVRCD,NEXTJ
.
           MATCH       SP10,PTHSHOSP
           IF          !@EQUAL
             MATCH       PMVXMHOS,PTHSHOSP
             GOTO        NEXTJ IF NOT EQUAL
           ENDIF
.
         ENDIF
.
         PACK       KEY22 WITH JADMN,JINVN,JTRNS
.
         CALL       READDTR
         BRANCH     OVRCD OF DTRNERR
.
         MATCH      TTYPE,JCODE
         GOTO       DTRNERR IF NOT EQUAL
.
         MOVE       PINVUR,KEY8
         CALL       RDMAST1
         BRANCH     OVRCD OF MASTERR
.
         UNPACK     JDATE WITH XCENT,XYEAR,XMON,XDAY
         DISPLAY    *P35:24,*V2LON,JADMN,SP2,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
         CALL       DISPDA
         IF         IBCNUGST=2 | IBCNUGST=3
           ADD        PTDTGSTM,TOTALJ
           ADD        PTDTGSTM,GNDALJ
           ADD        PTDTGSTM,SAVTOTJ
         ENDIF
         ADD        TPATAMTT,TOTALJ
         ADD        TPATAMTT,GNDALJ
         ADD        TPATAMTT,SAVTOTJ
.
         GOTO       NEXTJ
.
DTRNERR  DISPLAY    *P1:15,*EL,*V2LON,*B:
                    "** DTRAN Inconsistency Error for Admission ":
                    JADMN," **",*W;
         GOTO       NEXTJ
.
INVRERR  DISPLAY    *P1:15,*EL,*V2LON,*B:
                    "** Invoice Inconsistency Error for Admission ":
                    JADMN," **",*W;
         GOTO       NEXTJ
.
MASTERR  DISPLAY    *P1:17,*EL,*V2LON,*B:
                    "** Master File Record Missing for U/R ":
                    PINVUR,", Admission ",JADMN," **",*W;
         GOTO       NEXTJ
+
.
KHOSID  DISPLAY    *P1:15,*EL,"Hospital Id : ";
        MOVE       TEN5,ECOL
        MOVE       "15",EVERT
        MOVE       SP3,CKYIDEF3
        MOVE       ZERO,CKYIMAND           * Not Mandatory
        OPEN       PATHSPA1,"pathspaf"
.
        CALL       PATHSPKY
        BRANCH     EXIT,KHOS10,KHOS20
        MOVE       ZERO,EXIT
        GOTO       KHOS99  
.
KHOS10  MOVE       "All Hospitals",PTHSNAME
        MOVE       ZERO,EXIT
        GOTO       KHOS99
.
KHOS20  MOVE       ONE,EXIT
KHOS99  RETURN
+
.        End of report - Print totals and finish
.
ENDP     BRANCH     EXPORTFL,END30
.
         COMPARE    ZERO,CPAGENO
         GOTO       NOJRNLS IF EQUAL
.
         BRANCH     SUMFLG OF END10
.
         COMPARE    ONE,TOTFLG        * Has total already been printed?
         GOTO       END05 IF EQUAL    * Yes, Don't re-print it
.
         COMPARE    ZERO,TOTALJ
         GOTO       END05 IF EQUAL
         CALL       PAGEND
.
         PRINT      *1,"!",*67,"TOTAL ",*76,TOTALJ,*132,"!"
         MOVE       ZERO,TOTALJ
         MOVE       ZERO,TOTFLG
.
END05    MOVE       ONE,GNDFLG
.
         CALL       HEAD
         MOVE       ONE,TOTFLG        * Prevent total being re-written
         BRANCH     PASS,ENDTOT1,ENDTOT2
.
         PRINT      "!",*59,"JOURNAL TOTAL ",*76,GNDALJ,*132,"!"
         GOTO       END30
ENDTOT1  PRINT      "!",*55,"GST JOURNAL TOTAL ",*76,GNDALJ,*132,"!"
         GOTO       END30
ENDTOT2  PRINT      "!",*64,"GRAND TOTAL ",*76,SAVTOTJ,*132,"!"
         GOTO       END30
.
END10    COMPARE    ZERO,TOTALJ
         GOTO       END20 IF EQUAL
.
         COMPARE    ZERO,PASS
         GOTO       END15 IF EQUAL
.
         CALL       HEAD  * Print heading for GST journals and Grand total
.
END15    MOVE       TDESC,DIM22
         PRINT      *1,"! ",ACODE,"  ",DIM22,*30,"!  ",TOTALJ,*50,"!"
         MOVE       ZERO,TOTALJ
.
END20    BRANCH     PASS,ENDTOT1S,ENDTOT2S
.
         CALL       PAGEND
         PRINT      *1,"! ",*10,"JOURNAL TOTAL ",*30,"!  ",GNDALJ,*50,"!"
         GOTO       END30
.
ENDTOT1S CALL       PAGEND
         PRINT      *1,"! ",*6,"GST JOURNAL TOTAL ",*30,"!  ",GNDALJ,*50,"!"
         GOTO       END30
.
...TOT2S CALL       HEAD
ENDTOT2S CALL       PAGEND
         PRINT      *1,"! ",*10,"GRAND TOTAL ",*30,"!  ",SAVTOTJ,*50,"!"
.
END30    MOVE       SP4,GTHCSCOD
.
         COMPARE    THREE,IBCNUGST
         GOTO       END32 IF EQUAL       * NZ Priv GST
         COMPARE    TWO,IBCNUGST         * Using Australian GST?
         GOTO       END40 IF NOT EQUAL   * No, finish report
.
END32    ADD        ONE,PASS            * Yes, repeat to do report for GST
         BRANCH     PASS,STLOOP,ENDP    * journals, then do grand total for all
         GOTO       END40
.
END40    BRANCH     EXPORTFL,START
         DISPLAY    *P1:24,*EL,*P40:24,*V2LON:
                    "** Report Generation Completed **",*W;
.
         CALL       PAGEND
         PRINT      *N,"  ***  END OF REPORT  ***"
.
         GOTO       START
+
.
.        Print a line of data
.
DISPDA   COMPARE    "57",CLNO
         CALL       HEAD IF NOT LESS
.
         MATCH      "R" TO THCSCOD
         GOTO       DISPDA1 IF NOT EQUAL
.
         MOVE       TREBATE TO TPATAMTT
.
DISPDA1  MOVE       TDDESC,DIM25
         MOVE       PGNAME,DIM23
         REP        " 0",JDATE
         UNPACK     JDATE WITH XCENT,XYEAR,XMON,XDAY
.
         BRANCH     EXPORTFL,DISPDA4
.
         COMPARE    ONE,SUMFLG
         RETURN     IF EQUAL
.
         MOVE       PTDTGSTM,FORM82
         PRINT      *1,"!",*2,PSNAME,SP1,DIM23:
                    *46,"!",*47,PINVUR:
                    *55,"!",*56,JADMN:
                    *64,"!",*65,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
         IF         IBCNUGST=2 | IBCNUGST=3
           IF         PASS=1
             PRINT     *75,"!",*76,FORM82;
           ELSE
             MOVE     TPATAMTT,FORM82
             PRINT    *75,"!",*76,FORM82;
           ENDIF
         ELSE
             MOVE     TPATAMTT,FORM82
             PRINT    *75,"!",*76,FORM82;
         ENDIF
         PRINT      *87,"!",*88,DIM25:
                   *113,"!",*114,PINVNO:
                   *123,"!",*124,TBATCHN,*132,"!"
.
         ADD        ONE,CLNO
         GOTO       DISPDA9
.
DISPDA4   PACK      DIM10,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,SP70
.
          PRINT     "<tr><td>",JADMN,"</td>":
                    "<td>",PINVUR,"</td>":
                    "<td>",PSNAME,"</td>":
                    "<td>",DIM23,"</td>":
                    "<td>",DIM10,"</td>":
                    "<td>",TTYPE,"</td>"
.
          MOVE      PTDTGSTM,FORM82
          IF        IBCNUGST=2 | IBCNUGST=3
            IF        PASS=1
              PRINT    "<td>",FORM82,"</td>"
            ELSE
              MOVE    TPATAMTT,FORM82
              PRINT    "<td>",FORM82,"</td>"
            ENDIF
          ELSE
            MOVE    TPATAMTT,FORM82
            PRINT    "<td>",FORM82,"</td>"
          ENDIF
.
          PRINT     "<td>&nbsp;",JRNLDESC,"</td>":
                    "<td>",DIM25,"</td>":
                    "<td>",PINVNO,"</td>":
                    "<td>",TBATCHN,"</td>"
.
          IF         IBCNMHOS=1
           PRINT       "<td>",PMVXMHOS,"</td>"
          ENDIF
.
          PRINT    "</tr>"
.
DISPDA9  RETURN
.
.
.        Subroutine to read in the appropriate Dtran record
.
READDTR  BRANCH     PINVSYS OF AAEDTR,OUTDTR,RDDTRN1,OUTDTR,OVERCOND
         GOTO       OVERCOND
.
.        A + E Dtran
.
AAEDTR   CALL       RDDTRE1
         BRANCH     OVRCD OF OVERCOND
.
         MOVE       ATTYPE,TTYPE
         MOVE       ATPATAMT,TPATAMTT
         MOVE       ATREBPOR,TREBATE
         MOVE       ATDDESC,TDDESC
         MOVE       ATBATCHN,TBATCHN
.
         UNPACK     ATDDATE,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,TFDAY
         MOVE       XMON,TFMONTH
         MOVE       XYEAR,TFYEAR
         MOVE       XCENT,TFCENT
         RETURN
.
.        Outpatient Dtran
.
OUTDTR   CLOSE      OUTDTRO1
         PACK       CFNAME,OSTFILE,FILDTRO1
         OPEN       OUTDTRO1,CFNAME
.
         CALL       RDDTRO1
         BRANCH     OVRCD OF OVERCOND
.
         MOVE       OTTYPE,TTYPE
         MOVE       OTPATAMT,TPATAMTT
         MOVE       OTREBPOR,TREBATE
         MOVE       OTDDESC,TDDESC
         MOVE       OTBATCHN,TBATCHN
.
         UNPACK     OTDDATE,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,TFDAY
         MOVE       XMON,TFMONTH
         MOVE       XYEAR,TFYEAR
         MOVE       XCENT,TFCENT
         RETURN
+
.
.        Print the end of section line
.
PAGEND   BRANCH     EXPORTFL,PAGE99
.
         ADD        ONE,CLNO
         BRANCH     SUMFLG OF PAGE10
         PRINT      "*-----------------------------------------":
                    "-----------------------------------------":
                    "------------------------------------------------*"
         RETURN
.
PAGE10   PRINT      "*------------------------------------------------*"
PAGE99   RETURN
+
.*******************************************************************************
.        Print Page Heading
.
HEAD     BRANCH     EXPORTFL,HEAD99
.
         COMPARE    ZERO,CPAGENO
         CALL       PAGEND IF NOT EQUAL
.
         CLOCK      TIME,CTIMEIS
.
         UNPACK     IDATEF WITH XCENT,XYEAR,XMON,XDAY
         UNPACK     IDATEF WITH CCENT,CYEAR,CMON,CDAY
         CALL       PACDATE
         MOVE       CPCDATE,DIM10
         MOVE       XDAY,IDAY
         MOVE       XMON,IMON
         MOVE       XYEAR,IYEAR
         MOVE       XCENT,ICENT
.
         UNPACK     IDATET WITH XCENT,XYEAR,XMON,XDAY
         UNPACK     IDATET WITH CCENT,CYEAR,CMON,CDAY
         CALL       PACDATE
         MOVE       XDAY,DDFORM
         MOVE       XMON,MMFORM
         MOVE       XYEAR,YYFORM
         MOVE       XCENT,CCFORM
.
         MOVE       ONE,CNOUNDLN
         CALL       IBACLOCK
         CALL       HEAD132
.
         IF        IBCNMHOS =1
           PRINT     *40,"Hospital : ",PTHSHOSP,"    ",PTHSNAME
         ENDIF
.
         PRINT      *N,*40,"FROM ",DIM10:
                    " TO ",CPCDATE:
                    *N,*N;
.
         BRANCH     SUMFLG OF HEAD10
         BRANCH     GNDFLG OF HEAD10
.
         PRINT      *40,"FOR JOURNAL TYPE ":
                    ACODE," - ",TDESC,*N
.
HEAD10   CALL       PRTSEL
         CALL       PAGEND
.
         BRANCH     SUMFLG OF HEAD20
.
         PRINT      *1,"!",*2,"Patients Name":
                    *46,"!",*47,"U/R No":
                    *55,"!",*57,"Adm No":
                    *64,"!",*66,"Date of ":
                    *75,"!",*76,"   Amount":
                    *87,"!",*88,"Description":
                   *113,"!",*114," Invoice":
                   *123,"!",*124,"Batch",*132,"!",*N:
                    *1,"!",*46,"!",*55,"!",*64,"!",*66,"Journal ":
                    *75,"!",*87,"!",*113,"!",*114,"  Number",*123,"!":
                    *124,"Number",*132,"!"
         IF        IBCNMHOS =1
           MOVE       TEN,CLNO
         ELSE
           MOVE       TEN1,CLNO
         ENDIF
         GOTO       HEAD30
.
HEAD20   PRINT      *1,"! Journal Type",*30,"!        Total",*50,"!"
         IF        IBCNMHOS =1
           MOVE       TEN1,CLNO
         ELSE
           MOVE       TEN,CLNO
         ENDIF
.
HEAD30   CALL       PAGEND
HEAD99   RETURN
+
.*******************************************************************************
.         Routine to print heading for Export File
.*******************************************************************************
EHED0000  PRINT     "<table border=1><tr><td width=20%>Admission No</td>":
                    "<td>U/R No.</td>":
                    "<td>Patient Surname</td>":
                    "<td>Patient Given Name</td>":
                    "<td>Date of Journal</td>":
                    "<td>Journal Type Code</td>"
.
         IF         IBCNUGST=2 | IBCNUGST=3
           IF         PASS=1
             PRINT     "<td>GST Amount</td>"
           ELSE
             PRINT     "<td>Journal Amount</td>"
           ENDIF
          ELSE
            PRINT     "<td>Journal Amount</td>"
          ENDIF
.
          PRINT     "<td>Journal Description</td>":
                    "<td>Journal Alternative Desc.</td>":
                    "<td>Invoice No.</td>":
                    "<td>Batch No.</td>"
.
          IF         IBCNMHOS=1
           PRINT    "<td>Hospital ID</td>"
          ENDIF
.
          PRINT    "</tr>"
EHED9999  RETURN
+
.*******************************************************************************
.         Subroutine to key in a date
.
KEYINDTE  IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
.
          RETURN
+
PRTSEL    COMPARE   ZERO,FSTSYS
          GOTO      AEHEAD IF NOT EQUAL
.
          PRINT     *40,"CONSOLIDATED REPORT",*N,*N;
          COMPARE   ZERO,FSTSYS
          RETURN    IF EQUAL
.
AEHEAD    COMPARE   ONE,FSTSYS
          GOTO      OPHEAD IF NOT EQUAL
.
          PRINT     *40,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          COMPARE   ONE,FSTSYS
          RETURN    IF EQUAL
.
OPHEAD    COMPARE   TWO,FSTSYS
          GOTO      IPHEAD IF NOT EQUAL
.
          PRINT     *40,"OUTPATIENT REPORT",*N;
          MATCH     SP6,FSTSITE
          GOTO      ALSITE IF NOT EQUAL
.
          PRINT     *40,"ALL SITES";
          GOTO      OPDEPT
.
ALSITE    PRINT     *40,"SITE ",FSTSITE;
.
OPDEPT    MATCH     SP3,FSTDEPT
          GOTO      ALDEPT IF NOT EQUAL
.
          PRINT     *54,"ALL DEPARTMENTS",*N,*N;
          GOTO      PRTSEL9
.
ALDEPT    PRINT     *54,"DEPARTMENT ",FSTDEPT,*N,*N;
          GOTO      PRTSEL9
.
IPHEAD    COMPARE   THREE,FSTSYS
          GOTO      OTHEAD IF NOT EQUAL
.
          PRINT     *40,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
OTHEAD    PRINT     *40,"OTHER FINANCIAL REPORT",*N,*N;
.
PRTSEL9   RETURN 
+
.****************************************************************************
.*                                CRTX0000                                  *
.* Create export file for excel                                             *
.****************************************************************************
CRTX0000  MOVE      ZERO,EXIT
          MOVE      ZERO,EXPORTFL                  * No to create export file
          DISPLAY   *P1:23,"Create Export file for Excel : "
          KEYIN     *P32:23,*V2LON,*RV,DIM1:
                    *P32:23,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:23,*V2LON,DIM1
          MOVE      ONE,EXPORTFL
          MATCH     "Y",DIM1
          GOTO      CRTX9999 IF EQUAL
          MOVE      ZERO,EXPORTFL
.
CRTX9999  RETURN
+
         INC        STD001IO
.
         INC        PATHSPKY
         INC        PATCODKY
         INC        PATSELFN
         INC        PATSTRYR
.
         INC        AAEDTRIO/INC
         INC        IBAPASIO/INC
         INC        IBASECIO/INC
         INC        OUTDTRIO/INC
         INC        OUTSITIO/INC
         INC        PATCODIO/INC
         INC        PATDRGIO/INC
         INC        PATDTRIO/INC
         INC        PATINVIO/INC
         INC        PATJRNIO/INC
         INC        PATMA1IO/INC
         INC        PMSVX1IO/INC
         INC        PATHSPIO/INC
.
ENDIT    CHAIN      PGM
         STOP

.
.**********************************************************************
.* System   : PATIENT BILLING                                         *
.* Program  : IBAPMS62           (N.Z. Only)                          *
.* Desc     : Management Summary - Adms / Disch by Ward / Patients    *
.**********************************************************************
.* Date     :  22/11/89                                               *
.* Author   :  K.Peak                                                 *
.* Mods.    :                                                         *
.*        V12.00.01 30/05/2025 Nikitha B       TSK 0955096            *
.*                  Alphanumeric changes                              *
.*        V10.03.01 02/01/2013 Patrick Adair  CAR 261630              *
.*                       Removed port number from temp file name      *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088              *
.*                  Added pmsvx1af                                    *
.*        V9.04.01  22/12/2004  Jill Habasque CAR 55358               *
.*                  Copied from 5.12                                  *
.*        V5.09.B01 19/04/2001  Dean Cramer                           *
.*                  Changed so admission parameter columns do not     *
.*                  truncate field until greater than 5 digits        *
.*        V5.08.01  28/08/2000  Caleb Sharman                         *
.*                  Changed Health Fund variables to be 8 chars       *  
.*            V5.07.B03 02/03/1999 Jim Stathopoulos                   *
.*                      Report Modifications                          *
.*            V5.07.B02 27/01/1999 Jim Stathopoulos                   *
.*                      Report Modifications                          *
.*                  09/11/1998  Glenn Berry      5.07 changes         *
.******************************************************************************
.*             V5.01.03 05/12/89 K.Peak                               *
.*                      Fixed generation of report  SRF 103270        *
.*             V5.01.04 01/02/90  E.Phan  SRF 103785                  *
.*                      Fixed paging error, printing error & extract  *
.*                      -ion routines for beginning pats & on leave   *
.*                      Got rid of Home Leave column                  *
.*             V5.01.05 20/03/90 J.Clark  SRF104363                   *
.*                      Changed Death Count to read discharge status  *
.*                      instead of pcease in patmast                  *
.*             V5.01.121 27/11/90 Glen Hobby                          *
.*                       Recompiled for changes to                    *
.*                       PATMX1FD                                     *
.*             V5.01.122 11/02/91 DIG                                 *
.*                       Modified so that an admission will default   *
.*                       to category A/A if indicator 1 is not set up *
.*                       on the patient codes file. In this way at    *
.*                       least the total admissions figure should be  *
.*                       correct.                                     *
.**********************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATMA1FD/INC     * master file

          INC       PATMI1FD/INC     * admission file
          INC       PMSVX1FD/INC     * admission file
          INC       PATDSCFD/INC     * discharges file
          INC       PATCODFD/INC     * codes file
          INC       PATTRNFD/INC     * transfer file
          INC       PATWR1FD/INC     * ward/bed file
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.**********************************************************************
.*                         VARIABLES                                  *
.**********************************************************************
.
AAADMS    FORM      5        * number of a/a admissions
ACADMS    FORM      5        * number of a/c admissions
ADMFLG    FORM      1        * admission routine flag
ADMNO     DIM       8        * admission number
ADMS      FORM      5        * number of a/a admissions
.
BEGPAT    FORM      4        * number of patients at beginning
BGPAT     FORM      4        * number of patients at beginning
.
CDMS      FORM      5        * number of a/c admissions
CMDLINE   DIM       50
CODECC    INIT      "CC"
CODED     INIT      "D "
CODEU5    INIT      "U5"
.
DAYCSE    FORM      4        * number of daycases
DCHGS     FORM      4        * number of discharges
DCSE      FORM      4        * number of daycases
DEATHS    FORM      4        * number of deaths
DEPDESC   DIM       20       * department description
DISCHS    FORM      4        * number of discharges
DTHS      FORM      4        * number of deaths
.
eDPAT     FORM      4        * number of patients at the end
ENDDATE   DIM       8        * keyin todate
ENDPAT    FORM      4        * number of patients at the end
EPAT      FORM      4        * number of patients at the end
.
GAAADM    FORM      5        * number of a/a admissions
GACADM    FORM      5        * number of a/c admissions
GBGPAT    FORM      5        * number of pts at the beginning
GDAYCSE   FORM      5        * number of daycases
GDEATHS   FORM      5        * number of deaths
GDSCHS    FORM      5        * number of discharges
GENDPAT   FORM      5        * number of ending patients
GHOMLVE   FORM      5        * number of home leave patients
GNETCHG   FORM      5        * number of net change
GTOTADM   FORM      5        * number of total admissions
GTOTIN    FORM      6        * number of totals in
GTOTOUT   FORM      5        * number of totals out
GWLADM    FORM      5        * number of w/l admissions
GWTRFIN   FORM      5        * number of ward transfers in
GWTRFOT   FORM      5        * number of ward transfers out
.
HLVE      FORM      4        * number of home leave patients
HOMLVE    FORM      4        * number of home leave patients
.
.
NCHG      FORM      4        * number of net changes
NETCHG    FORM      4        * number of net changes
.
PFDATE    DIM       10       * print from date
PRTFLG    FORM      1        * print routine flag
PTDATE    DIM       10       * print to date
.
SAAADM    FORM      5        * number of a/a admissions
SACADM    FORM      5        * number of a/c admissions
SAVTWRD   DIM       3        * saved tran ward code
SBGPAT    FORM      5        * number of pts at the beginning
SDAYCSE   FORM      5        * number of daycases
SDEATHS   FORM      5        * number of deaths
SDSCHS    FORM      5        * number of discharges
SENDPAT   FORM      5        * number of ending patients
SHOMLVE   FORM      5        * number of home leave patients
SNETCHG   FORM      5        * number of net changes
STOTADM   FORM      5        * number of total admissions
STOTIN    FORM      6        * number of totals in
STOTOUT   FORM      5        * number of totals out
STRDATE   DIM       8        * keyin fromdate
SVUSR5    DIM       3        * saved user define field u5
SWARD     DIM       3        * saved tran ward code
SWLADM    FORM      5        * number of w/l admissions
SWTRFIN   FORM      5        * number of ward transfers in
SWTRFOT   FORM      5        * number of ward transfers out
.
TFIN      FORM      4        * number of ward transferred in
TFOUT     FORM      4        * number of ward transferred out
TOTADMS   FORM      5        * number of admissions
TOTIN     FORM      5        * number of ward transfers in
TOTOUT    FORM      5        * number of ward transfers out
.
UPDWRD    DIM       3        * Ward to update (used in TRN0000)
.
WLADMS    FORM      5        * number of w/l admissions
WDMS      FORM      5        * number of w/l admissions
WSTAT     FORM      1        * current astat value
WDATE     DIM       8        * date
WTRFIN    FORM      4        * number of ward transferred in
WTRFOUT   FORM      4        * number of ward transferred out
WRDDESC   DIM      20        * ward description
.
. ------- Temporary File -------
.
SUMTMP    DIM       8
SUMTMPXX  IFILE SQL, FIXED=47, KEY="u1-3,4-6"
.
.Name     Type    Length  Physical  Start
.----     ----    ------  --------  -----
.AUSR5    DIM       3        3        1
.AWARD    DIM       3        3        4
.DAYCSE   FORM      4        3        7
.BEGPAT   FORM      4        3        10
.AAADMS   FORM      4        3        13
.ACADMS   FORM      4        3        16
.WLADMS   FORM      4        3        19
.WTRFIN   FORM      4        3        22
.DISCHS   FORM      4        3        25
.WTRFOUT  FORM      4        3        28
.DEATHS   FORM      4        3        41
.HOMLVE   FORM      4        3        44
. End of record                       47
.
PRGID     INIT      "IBAPMS62"
PRGNAM    INIT      "Management Summary - Adms / Disch By Ward / Patients"
VERSION   INIT      "V12.01.00"
.
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
.
ML0000    CALL      INIT0000        * initialisation
.
ML1000    CALL      CLR0000         * clear variables
.
ML1500    CALL      SCR0000         * select option
          BRANCH    EXIT,ML9000
.
          BRANCH    OPTION,ML2000
.
ML2000    CALL      DSC0000         * display screen
          CALL      DAT0000         * keyin dates
          BRANCH    EXIT,ML1500
          CALL      CHK0000         * ok to continue
          BRANCH    EXIT,ML2000
.
          CALL      CREA0000        * create temporary file
.
. ------- Current Admissions -------
.
          CALL      CADM0000        * display current admission screen
          MOVE      TWO,WSTAT
          CALL      ADM0000         * scan admission file for current adms
          BRANCH    EXIT,ML9000
.
. ------- Discharges -------
.
          CALL      DADM0000        * display discharges screen
          MOVE      THREE,WSTAT
          CALL      ADM0000         * scan admission file for discharged adms
          BRANCH    EXIT,ML9000
.
. ------- Onleave Patients -------
.
          CALL      OADM0000        * display onleave admission screen
          MOVE      FOUR,WSTAT
          CALL      ADM0000         * scan admission file for o/leave adms
          BRANCH    EXIT,ML9000
.
          CALL      DSCH0000        * scan discharge file
          BRANCH    EXIT,ML9000
.
          CALL      CLR0000         * clear variables
          CALL      PRT0000         * print data
          CALL      END0000         * end of report
          GOTO      ML1000
.
. ------- The End -------
.
ML9000    CALL      KILL0000        * remove temporary file
          CHAIN     PGM
.
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialisation                                *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          RETURN
.
.**********************************************************************
.*                         CLR0000                                    *
.*                     Clear Variables                                *
.**********************************************************************
.
CLR0000   MOVE      ZERO,CPAGENO     * page counter
          MOVE      "80",CLNO        * line counter
          MOVE      ZERO,BEGPAT     * number of patients at the beginning
          MOVE      ZERO,AAADMS     * number of a/a admissions
          MOVE      ZERO,ACADMS     * number of a/c admissions
          MOVE      ZERO,WLADMS     * number of w/l admissions
          MOVE      ZERO,TOTADMS    * number of total admissions
          MOVE      ZERO,WTRFIN     * number of ward transfers in
          MOVE      ZERO,TOTIN      * number of total ward transfers in
          MOVE      ZERO,DISCHS     * number of discharges
          MOVE      ZERO,WTRFOUT    * number of ward transfers out
          MOVE      ZERO,DEATHS     * number of deaths
          MOVE      ZERO,TOTOUT     * number of total ward transfers out
          MOVE      ZERO,NETCHG     * number of net changes
          MOVE      ZERO,ENDPAT     * number of patients at the end
          MOVE      ZERO,HOMLVE     * number of home leave patients
.
          MOVE      ZERO,GBGPAT     * number of g/total beg. patients
          MOVE      ZERO,GAAADM     * number of g/total a/a admissions
          MOVE      ZERO,GACADM     * number of g/total a/c admissions
          MOVE      ZERO,GWLADM     * number of g/total w/l admissions
          MOVE      ZERO,GTOTADM    * number of g/total total admissions
          MOVE      ZERO,GWTRFIN    * number of g/total ward transfers in
          MOVE      ZERO,GTOTIN     * number of g/total total trfs. in
          MOVE      ZERO,GDSCHS     * number of g/total discharges
          MOVE      ZERO,GWTRFOT    * number of g/total ward transfers out
          MOVE      ZERO,GDEATHS    * number of g/total deaths
          MOVE      ZERO,GTOTOUT    * number of g/total total ward transfers out
          MOVE      ZERO,GNETCHG    * number of g/total net changes
          MOVE      ZERO,GENDPAT    * number of g/total patients at the end
          MOVE      ZERO,GHOMLVE    * number of g/total home leave patients
.
          MOVE      ZERO,SBGPAT     * number of s/total beg. patients
          MOVE      ZERO,SAAADM     * number of s/total a/a admissions
          MOVE      ZERO,SACADM     * number of s/total a/c admissions
          MOVE      ZERO,SWLADM     * number of s/total w/l admissions
          MOVE      ZERO,STOTADM    * number of s/total total admissions
          MOVE      ZERO,SWTRFIN    * number of s/total ward transfers in
          MOVE      ZERO,STOTIN     * number of s/total total trfs. in
          MOVE      ZERO,SDSCHS     * number of s/total discharges
          MOVE      ZERO,SWTRFOT    * number of s/total ward transfers out
          MOVE      ZERO,SDEATHS    * number of s/total deaths
          MOVE      ZERO,STOTOUT    * number of s/total total ward transfers out
          MOVE      ZERO,SNETCHG    * number of s/total net changes
          MOVE      ZERO,SENDPAT    * number of s/total patients at the end
          MOVE      ZERO,SHOMLVE    * number of s/total home leave patients
.
          RETURN
.
.**********************************************************************
.*                         SCR0000                                    *
.*                      Main Option Screen                            *
.**********************************************************************
.
SCR0000   MOVE      FALSE,EXIT
          HLINE     *G37,2,66,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Generate Report"
.
SCR1000   DISPLAY   *P1:7,*EL,"Option : "
          KEYIN     *P10:7,*V2LON,OPTION
.
          BRANCH    OPTION,SCR9999
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
          BEEP
          GOTO      SCR1000
.
SCR9000   MOVE      TRUE,EXIT
.
SCR9999   RETURN
.
.**********************************************************************
.*                         DSC0000                                    *
.*                      Display Screen                                *
.**********************************************************************
.
DSC0000   DISPLAY   *P1:11,*EL,"From Date : ",*P1:13,*EL,"To   Date : "
          RETURN
.
.**********************************************************************
.*                         DAT0000                                    *
.*                       Keyin Dates                                  *
.**********************************************************************
.
.         keyin from date
.
DAT0000   MOVE      FALSE,EXIT
          UNPACK    SP6,CYEAR,CMON,CDAY  * set up defaults
          MOVE      TEN3,CCOL            * column for keyin
          MOVE      TEN1,CVERT           * row for keyin
          MOVE      THIRTY,ECOL            * column for error
          MOVE      TEN1,EVERT           * row for error
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      ONE,CDEFDTE          * 0 = check all fields for default
.                                        * 1 = check day only for default
          CALL      KEYDATE
          BRANCH    OVRCD,DAT9000        * no date keyed in
.
          PACK      STRDATE,CCENT,CYEAR,CMON,CDAY
          PACK      PFDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",STRDATE
.
.         keyin to date
.
DAT1000   UNPACK    SP6,CYEAR,CMON,CDAY  * set up defaults
          MOVE      TEN3,CCOL            * column for keyin
          MOVE      TEN3,CVERT           * row for keyin
          MOVE      THIRTY,ECOL          * column for error
          MOVE      TEN3,EVERT           * row for error
.                                        * 1 = check day only for default
          CALL      KEYDATE
          BRANCH    OVRCD,DAT0000
.
DAT3000   PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          PACK      PTDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",ENDDATE
.
          MATCH     STRDATE,ENDDATE
          GOTO      DAT9999 IF NOT LESS
.
          DISPLAY   *P40:13,*V2LON,*B:
                    "** Ending Date > Starting Date ** ";
          CALL      HITENTER
          DISPLAY   *P40:13,*EL
          GOTO      DAT1000
.
DAT9000   MOVE      TRUE,EXIT
DAT9999   RETURN
.
.**********************************************************************
.*                         CHK0000                                    *
.*                   Check if ok to continue                          *
.**********************************************************************
.
CHK0000   MOVE      FALSE,EXIT
          MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"Ok to continue (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          CMATCH    ANSN,ANS
          GOTO      CHK1000 IF EQUAL
          CMATCH    ANSY,ANS
          RETURN    IF EQUAL
          BEEP
          GOTO      CHK0000
.
CHK1000   DISPLAY   *P29:11,*EL,*P29:13,*EL,*P29:14,*EL,*P1:16,*EF
          MOVE      TRUE,EXIT
          RETURN
.
.**********************************************************************
.*                         CADM0000                                   *
.*                  Display Current Admission Screen                  *
.**********************************************************************
.
CADM0000  DISPLAY   *P1:24,*EL,*P20:24,"Current Admission No   :"
          RETURN
.
.**********************************************************************
.*                         DADM0000                                   *
.*                  Display Discharges Screen                         *
.**********************************************************************
.
DADM0000  DISPLAY   *P1:24,*EL,*P20:24,"Discharge Admission No :"
          RETURN
.
.**********************************************************************
.*                         OADM0000                                   *
.*                  Display Onleave Admission Screen                  *
.**********************************************************************
.
OADM0000  DISPLAY   *P1:24,*EL,*P20:24,"Onleave Admission No   :"
          RETURN
.
.**********************************************************************
.*                         ADM0000                                    *
.*                   Read Admission File                              *
.**********************************************************************
.
ADM0000   PACK      KEY9,WSTAT,SP10
          CALL      RDSMISS2
ADM1000   CALL      RDKMISS2
          BRANCH    OVRCD,ADM9999
.
          COMPARE   WSTAT,ASTAT              * Same status ?
          GOTO      ADM9999 IF NOT EQUAL     * No - get next status
.
          MOVE      ADATE,WDATE
          REP       " 0",WDATE
          MATCH     WDATE,ENDDATE            * Admission date after end date ?
          GOTO      ADM1000 IF LESS          * Yes - ignore record
.
          DISPLAY   *P45:24,AADMNO
.
          MOVE      AADMNO,ADMNO
          CALL      TRN0000
          GOTO      ADM1000
.
ADM9999   RETURN
.
.**********************************************************************
.*                         DSC0000                                    *
.*                      Read Discharge File                           *
.**********************************************************************
.
.         Note that this routine has been modified to process only
.         day cases, as discharges during the period required has
.         already been processed by ADM0000.
.
DSCH0000  DISPLAY   *P1:24,*EL,*P20:24,"Processing Day Cases : "
.
          PACK      KEY16,STRDATE,SP8
          CALL      RDSDSCH2
DSCH1000  CALL      RDKDSCH2
          BRANCH    OVRCD,DSCH9999           * No more records - finished
.
          MATCH     DDATE,ENDDATE            * Discharged past end period ?
          GOTO      DSCH9999 IF LESS         * Yes - finished
.
          DISPLAY   *P43:24,DADMNO
.
          MOVE      SP3,AUSR5
          PACK      KEY8,DADMNO              * Use the discharged adm no
          CALL      RDMISS1                  * Read the admission file
          BRANCH    OVRCD,DSCH1000           * No record - ignore
.
          MATCH     ADATE,DDATE              * Admitted & discharged same day ?
          GOTO      DSCH1000 IF NOT EQUAL    * No - ignore record
.
          PACK      KEY30,AADMNO,ADATE,SP30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,DSCH1000           * No record - ignore
          MATCH     AADMNO,TADMN
          GOTO      DSCH1000 IF NOT EQUAL    * Not same adm - ignore
          MATCH     ADATE,TDATE
          GOTO      DSCH1000 IF NOT EQUAL    * Not same date - ignore
          CMATCH    ANSA,TMOVE
          GOTO      DSCH1000 IF NOT EQUAL    * Not admission - ignore
.
          CALL      TRN9000                  * Clear variables
          MOVE      ONE,DCSE                 * One more day case
          MOVE      TWARD,UPDWRD             * Admitted ward is current ward
.
.         Note that AUSR5 already read from patmi1af
.
          CALL      TRN8000                  * Update
.
          GOTO      DSCH1000
.
DSCH9999  RETURN
.
.**********************************************************************
.*                                                                    *
.*        TRN0000 - Process Transfer Records                          *
.*                                                                    *
.**********************************************************************
.
.         Clear/Initialise variables for this admission
.
TRN0000   MOVE      SP6,SWARD,TWARD          * Clear save and ward variables
          CALL      TRN9000                  * Clear variables
.
.         Read transfer records for this admission
.
          PACK      KEY30,ADMNO,STRDATE,SP30 * Use starting date as point of ref
          CALL      RDSTRAN2                 * Position just before strting date
.
. ******* Phase 1 - Check if this is a beginning patient *******
.
          CALL      RDPTRAN2                 * Any records beforehand ?
          BRANCH    OVRCD,TRN0500            * No - read next transfer record
          MATCH     TADMN,ADMNO              * Same admission ?
          GOTO      TRN0500 IF NOT EQUAL     * No - read next transfer record
          CMATCH    ANSD,TMOVE               * Discharged before starting date ?
          GOTO      TRN0500 IF EQUAL         * Yes - not a beginning patient
          MOVE      ONE,BGPAT                * Otherwise patient was in hospital
.
          MOVE      TWARD,UPDWRD             * Ward to update is current ward
          CALL      TRN8000                  * Post figures for current ward
          GOTO      TRN1000                  * Start Phase 2
.
.         No valid record, so clear in case RDP read other admission recs
.
TRN0500   MOVE      SP3,TWARD
.
. ******* Phase 2 - Read through all records for this adm in this period *******
.
TRN1000   MOVE      TWARD,SWARD              * Save prev ward
          CALL      RDKTRAN2                 * Read next transfer record
          BRANCH    OVRCD,TRN9999            * No more records - finished
          MATCH     TADMN,ADMNO              * Same admission ?
          GOTO      TRN9999 IF NOT EQUAL     * No - finished
          REP       " 0",TDATE               * Make sure no spaces
          MATCH     TDATE,ENDDATE            * Is this record past end period ?
          GOTO      TRN9999 IF LESS          * Yes - finished for this admission
.
          CALL      TRN9000                  * Clear variables
.
          CMATCH    ANSA,TMOVE               * Admission record ?
          GOTO      TRN2000 IF EQUAL
          CMATCH    ANSD,TMOVE               * Discharge record ?
          GOTO      TRN3000 IF EQUAL
          CMATCH    ANSC,TMOVE               * Change record ?
          GOTO      TRN4000 IF EQUAL
          CMATCH    ANSL,TMOVE               * On Leave record ?
          GOTO      TRN5000 IF EQUAL
          CMATCH    ANSR,TMOVE               * Return from Leave record ?
          GOTO      TRN6000 IF EQUAL
          GOTO      TRN1000                  * No indicator - ignore record
.
.-------------------------------------------------------------------------------
.         Admission Record
.-------------------------------------------------------------------------------
.
TRN2000   MOVE      SP1,TCINDC1              * Clear indicator
          MOVE      ONE,FORM1                * Set default for FORM1
          PACK      KEY5,CODECC,ACARE        * Pack key
          CALL      RDCODE1                  * Read codes file
          MOVE      TCINDC1,FORM1            * Convert DIM1 to FORM1
          STORE     ONE,FORM1,ADMS,CDMS,WDMS * Arranged, Acute, Waiting List
.
          MOVE      TWARD,UPDWRD             * Ward to update is current ward
          CALL      TRN8000                  * Post figures for current ward
          GOTO      TRN1000                  * Read next record
.
.-------------------------------------------------------------------------------
.         Discharge Record
.-------------------------------------------------------------------------------
TRN3000   PACK      KEY8,DTADMN              * Admission No from transfer file
          CALL      RDDSCH1                  * Read Discharge File
          BRANCH    OVRCD,TRN3999            * No record - ignore
.
.       Check if patient is dead or not
.
          PACK      KEY5 WITH CODED,DSTAT
          CALL      RDCODE1
          BRANCH    OVRCD OF TRN3100
.
          CMATCH    "D",TCINDC1
          GOTO      TRN3100 IF NOT EQUAL     * No - just discharged
          MOVE      ONE,DTHS                 * Otherwise one more death
          GOTO      TRN3200                  * Update temp file
.
TRN3100   MOVE      ONE,DCHGS                * One more discharged
.
TRN3200   MOVE      TWARD,UPDWRD             * Ward to update is current ward
          CALL      TRN8000                  * Post figures for current ward
.
TRN3999   GOTO      TRN1000                  * Read next record
.
.-------------------------------------------------------------------------------
.         Change Record
.-------------------------------------------------------------------------------
.
TRN4000   MATCH     SP3,SWARD                * Is there a record for prev ward ?
          GOTO      TRN4999 IF EQUAL         * No - don't worry about figures
          MATCH     TWARD,SWARD              * Same ward ?
          GOTO      TRN4999 IF EQUAL         * Yes - ignore record
.
          MOVE      TWARD,UPDWRD             * Ward to update is current ward
          MOVE      ONE,TFIN                 * Transfer in
          CALL      TRN8000                  * Post figures for current ward
.
          MOVE      SWARD,UPDWRD             * Ward to update is previous ward
          MOVE      ONE,TFOUT                * Transfer out
          CALL      TRN8000                  * Post figures for previous ward
.
TRN4999   GOTO      TRN1000                  * Read next record
.
.-------------------------------------------------------------------------------
.         On Leave Record
.-------------------------------------------------------------------------------
.
TRN5000   MOVE      ONE,HLVE                 * One more on leave
          MOVE      TWARD,UPDWRD             * Ward to update is current ward
          CALL      TRN8000                  * Post figures for current ward
          GOTO      TRN1000                  * Read next record
.
.-------------------------------------------------------------------------------
.         Return from Leave Record
.-------------------------------------------------------------------------------
.
TRN6000   MATCH     SP3,SWARD                * Is there a record for prev ward ?
          GOTO      TRN6999 IF EQUAL         * No - don't worry about figures
          MATCH     TWARD,SWARD              * Same ward ?
          GOTO      TRN6100 IF EQUAL         * Yes - take 1 off home leave
.
. ------- Go on leave from 1 ward, return to another -------
.
          MOVE      TWARD,UPDWRD             * Ward to update is current ward
          MOVE      ONE,TFIN                 * Transfer in
          CALL      TRN8000                  * Post figures for current ward
.
          MOVE      SWARD,UPDWRD             * Ward to update is previous ward
          MOVE      SEQ,HLVE                 * One less home leave
          MOVE      ONE,TFOUT                * Transfer out
          CALL      TRN8000                  * Post figures for previous ward
          GOTO      TRN6999                  * Finished
.
. ------- Go on leave & return to same ward -------
.
TRN6100   MOVE      TWARD,UPDWRD             * Ward to update is current ward
          MOVE      SEQ,HLVE                 * One less home leave
          CALL      TRN8000                  * Post figures for current ward
.
TRN6999   GOTO      TRN1000                  * Read next record
.
.-------------------------------------------------------------------------------
.         Post figures to temp file for this transfer record
.-------------------------------------------------------------------------------
.
TRN8000   PACK      KEY6,AUSR5,UPDWRD        * For the current dept & ward
          CALL      RDTMP1                   * Check if there is a record
          BRANCH    OVRCD,TRN8100            * No - write a new record
.
          ADD       BGPAT,BEGPAT             * Beginning patients
          ADD       ADMS,AAADMS              * Arranged patients
          ADD       CDMS,ACADMS              * Acute patients
          ADD       WDMS,WLADMS              * Waiting list patients
          ADD       DCHGS,DISCHS             * Discharges
          ADD       HLVE,HOMLVE              * Home Leave patients
          ADD       DCSE,DAYCSE              * Day cases
          ADD       DTHS,DEATHS              * Deaths
          ADD       TFIN,WTRFIN              * Transfers in
          ADD       TFOUT,WTRFOUT            * Transfers out
          CALL      UPTMP1                   * Update file with changes
          GOTO      TRN9000                  * Clear variables
.
TRN8100   MOVE      UPDWRD,TWARD             * Load ward variable
          MOVE      BGPAT,BEGPAT             * Beginning patients
          MOVE      ADMS,AAADMS              * Arranged patients
          MOVE      CDMS,ACADMS              * Acute patients
          MOVE      WDMS,WLADMS              * Waiting list patients
          MOVE      DCHGS,DISCHS             * Discharges
          MOVE      HLVE,HOMLVE              * Home Leave patients
          MOVE      DCSE,DAYCSE              * Day cases
          MOVE      DTHS,DEATHS              * Deaths
          MOVE      TFIN,WTRFIN              * Transfers in
          MOVE      TFOUT,WTRFOUT            * Transfers out
          CALL      WRTMP1                   * Write a new record
.
. ------- Clear variables -------
.
TRN9000   MOVE      ZERO,BGPAT
          MOVE      ZERO,ADMS
          MOVE      ZERO,CDMS
          MOVE      ZERO,WDMS
          MOVE      ZERO,DCHGS
          MOVE      ZERO,HLVE
          MOVE      ZERO,DCSE
          MOVE      ZERO,DTHS
          MOVE      ZERO,TFIN
          MOVE      ZERO,TFOUT
          RETURN
.
. ------- No more transfer records for this admission -------
.
TRN9999   RETURN
.
.
.**********************************************************************
.*                         END0000                                    *
.*                      End of Report                                 *
.**********************************************************************
.
END0000   COMPARE   ZERO,CPAGENO
          GOTO      END1000 IF NOT EQUAL
.
.         No data selected
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** No Record Selected ** ";
          CALL      HITENTER
          CALL      HEAD0000
          PRINT     *N,*N,"***  END OF REPORT  ***"
          GOTO      END9999
.
END1000   COMPARE   "56",CLNO
          GOTO      END1200 IF LESS
          CALL      HEAD0000
          MOVE      ZERO,PRTFLG
.
          PRINT     *N,*1,DEPDESC,*N,*1,"--------------------"
          ADD       TWO,CLNO
.
END1200   PRINT     *N,*1,"Sub - Total ":
                    *23,SDAYCSE,*32,SBGPAT,*40,SAAADM,*46,SACADM,*52,SWLADM:
                    *59,STOTADM,*66,SWTRFIN,*73,STOTIN,*81,SDSCHS,*89,SWTRFOT:
                    *97,SDEATHS,*104,STOTOUT,*112,SNETCHG,*120,SENDPAT
          ADD       TWO,CLNO
.
          COMPARE   "58",CLNO
          GOTO      END1300 IF LESS
          CALL      HEAD0000
.
END1300   PRINT     *N,*N,*1,"Hospital Totals":
                    *23,GDAYCSE,*32,GBGPAT,*40,GAAADM,*46,GACADM,*52,GWLADM:
                    *59,GTOTADM,*66,GWTRFIN,*73,GTOTIN,*81,GDSCHS,*89,GWTRFOT:
                    *97,GDEATHS,*104,GTOTOUT,*112,GNETCHG,*120,GENDPAT
          ADD       THREE,CLNO
.
          CALL      PGE0000
.
          PRINT     *N,*1,"Note : Admission will default to A/A if ":
                          "Indicator 1 for Admission Category is not set up":
                          " on the Patient Codes File":
                    *N,*N,"***  END OF REPORT  ***"
          DISPLAY   *P1:23,*EL,*P1:24,*EL,*P15:24,*V2LON:
                    "** Report Generation Completed ** ";
          CALL      HITENTER
.
END9999   DISPLAY   *P1:14,*EF
          RETURN
.
.**********************************************************************
.*                         PRT0000                                    *
.*                        Print Data                                  *
.**********************************************************************
.
PRT0000   MOVE      "80",CLNO           * Make this high to print heading
          MOVE      "///",SVUSR5        * Initialise save var for department
.
          PACK      KEY6,SP6            * Start reading from beginning
          CALL      RDSTMP1             * Positioning read
PRT1000   CALL      RDKTMP1             * Read next record
          BRANCH    OVRCD,PRT9999       * No more records - exit
.
          MATCH     SVUSR5,AUSR5        * Same department ?
          GOTO      PRT3000 IF EQUAL    * Yes - just print ward details
          MATCH     "///",SVUSR5        * 1st department ?
          GOTO      PRT2000 IF EQUAL    * Yes - do not print subtotals
.
          CALL      PRTX2000            * Print subtotals for last department
.
PRT2000   MOVE      AUSR5,SVUSR5        * New department
          CALL      PRTX1000            * Print new department
.
. ------- Calculate ward totals -------
.
PRT3000   MOVE      AAADMS,TOTADMS      * Calculate total admissions
          ADD       ACADMS,TOTADMS
          ADD       WLADMS,TOTADMS
.
          MOVE      TOTADMS,TOTIN       * Total ingoing patients
          ADD       WTRFIN,TOTIN
.
          MOVE      DISCHS,TOTOUT       * Total outgoing patients
          ADD       WTRFOUT,TOTOUT
          ADD       DEATHS,TOTOUT
.
          MOVE      TOTIN,NETCHG        * Net change of patient movements
          SUB       TOTOUT,NETCHG
.
          MOVE      BEGPAT,ENDPAT       * Total patients in ward at period end
          ADD       TOTIN,ENDPAT
          SUB       TOTOUT,ENDPAT
.
. ------- Work out ward description -------
.
          MOVE      "* UNKNOWN WARD *    ",WBDESC * Default desc. for errors
          PACK      KEY6,AWARD,SP3      * Key is the ward code
          CALL      RDWARD1             * Read the ward record
          MOVE      WBDESC,WRDDESC      * Description
.
          COMPARE   "60",CLNO           * Past end of page ?
          GOTO      PRT3700 IF LESS     * No - just print record
          CALL      HEAD0000            * Otherwise print report header
.
. ------- Print ward record -------
.
PRT3700   MOVE      WRDDESC,DIM19
          PRINT     *1,AWARD,SP1,DIM19:
                    *24,DAYCSE,*33,BEGPAT,*41,AAADMS,*47,ACADMS,*53,WLADMS:
                    *59,TOTADMS,*67,WTRFIN,*74,TOTIN,*82,DISCHS,*90,WTRFOUT:
                    *98,DEATHS,*104,TOTOUT,*113,NETCHG,*121,ENDPAT
          ADD       ONE,CLNO            * Printed two lines
.
          CALL      PRTX5000            * Add figures to subtotal
          CALL      PRTX6000            * Add figures to grand total
          GOTO      PRT1000             * Read next ward record
.
. ------- Finished -------
.
PRT9999   RETURN
.
. ------- Print Department -------
.
PRTX1000  MOVE      "*UNKNOWN DEPARTMENT*",TDESC * Default desc in case of error
          MATCH     SP3,AUSR5           * If code is blank
          GOTO      PRTX1100 IF EQUAL   * then error, not category desc
          PACK      KEY5,CODEU5,AUSR5
          CALL      RDCODE1
PRTX1100  MOVE      TDESC,DEPDESC
.
          COMPARE   "60",CLNO
          GOTO      PRTX1200 IF LESS
          CALL      HEAD0000                 * Print report header
.
PRTX1200  PRINT     *N,*1,"Department : ",AUSR5,SP1,DEPDESC:
                    *N,*1,"-------------------------------------"
          ADD       THREE,CLNO
          RETURN
.
. ------- Print Sub-Total -------
.
PRTX2000  PRINT     *N,*1,"Sub-Total ",*23,SDAYCSE,*32,SBGPAT,*40,SAAADM:
                    *46,SACADM,*52,SWLADM,*59,STOTADM,*66,SWTRFIN,*73,STOTIN:
                    *81,SDSCHS,*89,SWTRFOT,*97,SDEATHS,*104,STOTOUT:
                    *112,SNETCHG,*120,SENDPAT
          ADD       TWO,CLNO
.
.         Clear subtotals in preparation for next lot
.
          MOVE      ZERO,SDAYCSE
          MOVE      ZERO,SBGPAT
          MOVE      ZERO,SAAADM
          MOVE      ZERO,SACADM
          MOVE      ZERO,SWLADM
          MOVE      ZERO,STOTADM
          MOVE      ZERO,SWTRFIN
          MOVE      ZERO,STOTIN
          MOVE      ZERO,SDSCHS
          MOVE      ZERO,SWTRFOT
          MOVE      ZERO,SDEATHS
          MOVE      ZERO,STOTOUT
          MOVE      ZERO,SNETCHG
          MOVE      ZERO,SENDPAT
          MOVE      ZERO,SHOMLVE
          RETURN
.
. ------- Add to Sub-Totals -------
.
PRTX5000  ADD       DAYCSE,SDAYCSE
          ADD       BEGPAT,SBGPAT
          ADD       AAADMS,SAAADM
          ADD       ACADMS,SACADM
          ADD       WLADMS,SWLADM
          ADD       TOTADMS,STOTADM
          ADD       WTRFIN,SWTRFIN
          ADD       TOTIN,STOTIN
          ADD       DISCHS,SDSCHS
          ADD       WTRFOUT,SWTRFOT
          ADD       DEATHS,SDEATHS
          ADD       TOTOUT,STOTOUT
          ADD       NETCHG,SNETCHG
          ADD       ENDPAT,SENDPAT
          ADD       HOMLVE,SHOMLVE
          RETURN
.
. ------- Add to Grand Totals -------
.
PRTX6000  ADD       DAYCSE,GDAYCSE
          ADD       BEGPAT,GBGPAT
          ADD       AAADMS,GAAADM
          ADD       ACADMS,GACADM
          ADD       WLADMS,GWLADM
          ADD       TOTADMS,GTOTADM
          ADD       WTRFIN,GWTRFIN
          ADD       TOTIN,GTOTIN
          ADD       DISCHS,GDSCHS
          ADD       WTRFOUT,GWTRFOT
          ADD       DEATHS,GDEATHS
          ADD       TOTOUT,GTOTOUT
          ADD       NETCHG,GNETCHG
          ADD       ENDPAT,GENDPAT
          ADD       HOMLVE,GHOMLVE
          RETURN
.
.
.**********************************************************************
.*                         HEAD0000                                   *
.*                      Print Headings                                *
.**********************************************************************
.
HEAD0000  CLOCK     TIME,CTIMEIS
          ADD       ONE,CPAGENO
          PRINT     *F,PRGID,*45,CNAME,*112,"DATE : ",CDATE:
                    *N,*1,VERSION,*45,PRGNAM,*112,"TIME :   ",CTIMEIS:
                    *N,*112,"PAGE : ",*126,CPAGENO:
                    *N,*45,"From : ",PFDATE,SP3,"To : ",PTDATE,*N
.
          CALL      PGE0000
.
          PRINT     *3,"Ward",*24,"Day",*30,"Beginning":
                    *41,"- A D M I S S I O N S -":
                    *66,"Ward",*74,"Total",*81,"Disch-":
                    *89,"Ward",*97,"Deaths",*104,"Total":
                    *113,"Net",*120,"Ending":
                    *N,*1,"Code/Type",*23,"Cases",*31,"Patients":
                    *41,"A/A",*47,"A/C",*53,"W/L",*59,"Total":
                    *66,"Tfrs-In",*75,"In",*81,"arges",*88,"Tfrs-Out":
                    *105,"Out",*112,"Change",*119,"Patients"
.
          CALL      PGE0000
          MOVE      NINE,CLNO
HEAD9999  RETURN
.
.**********************************************************************
.*                         PGE0000                                    *
.*                        Print Line                                  *
.**********************************************************************
.
PGE0000   PRINT     *1,"--------------------------------------------":
                       "--------------------------------------------":
                       "--------------------------------------"
          ADD       ONE,CLNO
          RETURN
.
.**********************************************************************
.*                         CREA0000                                   *
.*                  Create Temporary File                             *
.**********************************************************************
.
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,SUMTMP
.
          CALL      KILL0000
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    SUMTMP,CMDLINE
          APPEND    " 47 u1-3,4-6 ",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      SUMTMPXX,SUMTMP
          RETURN
.
.**********************************************************************
.*                         KILL0000                                   *
.*                  Remove Temporary File                             *
.**********************************************************************
.
KILL0000  CLOSE     SUMTMPXX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    SUMTMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
.**********************************************************************
.*              I/O For Temporary File                                *
.**********************************************************************
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      SUMTMPXX,KEY6;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    SUMTMPXX;AUSR5,AWARD,DAYCSE,BEGPAT,AAADMS,ACADMS,WLADMS:
                             WTRFIN,DISCHS,WTRFOUT,DEATHS,HOMLVE
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      SUMTMPXX,KEY6;AUSR5,AWARD,DAYCSE,BEGPAT,AAADMS,ACADMS:
                                  WLADMS,WTRFIN,DISCHS,WTRFOUT,DEATHS,HOMLVE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMP1    MOVE      ZERO,OVRCD
          RESET     KEY6
          WRITE     SUMTMPXX,KEY6;KEY6,DAYCSE,BEGPAT,AAADMS,ACADMS:
                                  WLADMS,WTRFIN,DISCHS,WTRFOUT,DEATHS,HOMLVE
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    UPDATE    SUMTMPXX;AUSR5,AWARD,DAYCSE,BEGPAT,AAADMS,ACADMS:
                             WLADMS,WTRFIN,DISCHS,WTRFOUT,DEATHS,HOMLVE
          RETURN
.
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       TFILENAM                     * Generate Temp File Name
          INC       PATCODIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATMA1IO/INC
          INC       PATDSCIO/INC
          INC       STD001IO
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
.

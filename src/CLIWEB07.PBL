.-------------------------------------------------------------------------------
. Program       : CLIWEB07 
.
. Function      : Table Maintenance HL7/Results
.
. Modifications : 
.
.-------------------------------------------------------------------------------
. V11.03.01 29/11/2022  Bella Turco    TSK 0921957
.           Added PTCNNRTD & NOMORE00 to H7COD000 routine                       
. V11.02.02 29/07/2022  Bella Turco    TSK 0921957
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V11.02.01 09/02/2022  Thanh T        TSK 905641 
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V10.11.02 27/11/2017  Thanh T        Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.01 28/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
.-------------------------------------------------------------------------------
. V10.10.01 07.04.2017  B.G.Ackland    TSK 0835482 
.           FHIR Location output for Laboratory Codes
.-------------------------------------------------------------------------------
.                 V10.08.01 15/08/2016  Jill Parkinson  TSK 0314333
.                           Changed to use key4 instead of key3 for startnum to
.                           allow for over 999 codes
.                 V10.04.02 07/04/2014  Peter Vela      CAR 286258
.                           Recompiled for PATMASTM
.                 V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.                           Replace META Tag Redirect with Javascript in
.                           Common RDIREC00 Routine
.-------------------------------------------------------------------------------
.                 V10.03.01 17.12.2012  B.G.Ackland CAR 275970  
.                           Add and Delete option for resctaaf records
.                 V9.08.02  29/03/2007  Jill Habasque CAR 122473
.                           Added output of cat dg for table 0074 
.                 V9.08.01  31/01/2007  Peter Vela     CAR 127930
.                           Recompiled for MRTMASFD
.                 V9.05.01  09/03/2006  Ebon Clements CAR 78533
.                           Mods for PATCODTM - Hospital specific category codes
.                 V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.                           Mods for REDIRECT length change.
.                           Recompiled for IBAWEBDF
.                 V9.02.03  07/02/2003 Tonii CAR 35992
.                           Recompiled for PATMISTM - inactive codes in patfundf
.                 V9.02.02  09/10/2002 Peter Vela               SRF 17693
.                           Recompiled for PATMISTM - Admitting Point
.                 V9.02.01  03/12/2001 Katie Nelson/Harvinder   SRF 648628(nz)
.                           Recompiled for NHI tags added to PATMASTM
.                 V9.01.02  16.08.2001 J.Tan
.                           Recompiled for PATMASTM
.                 V9.01.01  13.08.2001 J.Tan
.                           Recompiled for PATMASTM
.                 V5.09.02 12/01/2001 Bronko Sosic  
.                          Recompiled for PATMASTM / PATMISTM
.                 V5.08.04 15.11.2000 B.G.Ackland   
.                          Added Result Codes by Lab rescta1f
.                 V5.08.03 28/09/2000 Bronko Sosic  
.                          Recompiled for PATMASTM / PATMISTM
.                 V5.08.02 16/08/2000  Caleb Sharman
.                          Changed Health Fund variables to be 8 chars
.                 V5.08.01 07/06/2000 Bronko Sosic
.                          Recompiled for PATMASTM
.----------------------------------------------------------------------
.                 V5.07.06 23.03.2000  Bronko Sosic
.                          Added startkey to option 4 for GOTO Button
.                 V5.07.05 09.02.2000  Pat Dirito 
.                          Cleared out Report 1 & 2 no longer exist as
.                          CLIWEB01 now looks after Patient Alerts
.                          Added instances of ShowDetail now has Report no extn
.                          02/02/2000 Steven Watkins
.                          Recompiled for PATMASTM / PATMISTM
.                 V5.07.04 12/01/2000  Bronko Sosic
.                          Added REP " 0" after pack with CYY
.                 V5.07.03 20/12/1999  Katie Nelson     
.                          Recompiled for IBAWEBDF/IBAWEBCD 
.                 V5.07.02 03/11/1999  Katie Nelson     
.                          Recompiled for WEBDATCD/DAYOFWEK 
.                 V5.07.01 19/09/1999  Nicole Harrington
.                          Changed to use HTML classes
.                 V5.07.00 07.09.1999 Pat Dirito
.                          Added option 3 & 4 HL7 Code Maitenance               
.-------------------------------------------------------------------------------
. Modifications : V5.06.00 02.02.1999 Pat Dirito 
.                          Created Program
.                    
.-------------------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       WEBDATDF
.
          INC       PATDHEAD/INC
.
          INC       HL7CODFD/INC     * HL7 Codes Table
          INC       HL7TABFD/INC     * HL7 Table Details 
          INC       OUTSITFD/INC
          INC       PATALRFD/INC
          INC       PATDO1FD/INC
          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATPR1FD/INC
          INC       PATMA1FD/INC
          INC       PATCALAG/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       RESCTAFD/INC   * Clinical Results - Laboratory Master file
          INC       RESLABFD/INC   * Clinical Results - Laboratory Master file
          INC       RESLNKFD/INC
          INC       RESPENFD/INC   * Clinical Results - PIT Email Notification 
          INC       PATFN1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
.
.  CGI Variables
.
NEXTPAGE  FORM      1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
STARTKEY  DIM       10       * Starting Key
.
RSCTAKEY  DIM       32
CODESKEY  DIM       8 
DIM3      DIM       3
RSCTA001  DIM       50     Description                        
RSCTA002  DIM       2      Security Level                     
.
.
HLTAB001  DIM       4       * Table ID
HLTAB002  DIM       40      * Description
.
HLCOD001  DIM       10      * Code
HLCOD002  DIM       60      * Description
HLCOD003  DIM       2       * IBA Category
HLCOD004  DIM       3       * IBA Code
HLCOD005  DIM       7       * Web Background Color  
.
RESLA001  DIM       3       * Code
RESLA002  DIM       40      * Description
RESLA003  FORM      4       * Last Run Number
RESLA004  DIM       50      * E-Mail Address for Lab
RESLA005  DIM       50      * Hospital E-Mail Address for Errors
RESLA006  FORM      8       * HL7 Message ID
RESLA007  DIM       10      * HL7 Sending Application 
.
RESPN001  DIM       32      * Notification Key
RESPN002  DIM       50      * E-Mail Address
RESPN003  FORM      1       * All Results 0=No 1=Yes
RESPN004  FORM      1       * Abnormal Results 0=No 1=Yes
RESPN005  FORM      1       * Urgent Result 0=No 1=Yes
.
. Variable Declarations  
.
DOCTCODE  DIM       6
FORM8     FORM      8
TABLID    DIM       4         * Table ID
TABDES    DIM       40        * Table Description    
CATDG     INIT      "dg"
.
SP127     DIM       127
.
.---------------------------------------------------------------------------
PRGID     INIT      "CLIWEB07"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Table Maintenance HL7/Results"
.---------------------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500:
                             MAIN1600
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100 
          GOTO      MAIN1000
.
MAIN1200  CALL      RESCTA00        * Result Codes Security & Description
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      HL7TAB00        * HL7 Table Details    
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
. 
MAIN1400  CALL      HL7COD00        * HL7 Codes Table 
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
. 
MAIN1500  CALL      RESLAB00        * Result Lab Codes
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
. 
MAIN1600  CALL      RESPEN00        * E-Mail Triggers Table Maintenance
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
. 
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages 
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);": 
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.-------------------------------------------------------------------------------
. Open Files and Initialize Variables
.-------------------------------------------------------------------------------
INIT0000  CALL      INTMAS00
          OPEN      HL7CODA1,"hl7codaf"   * HL7 Codes Table
          OPEN      HL7TABA1,"hl7tabaf"   * HL7 Table Details 
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      WEBSECA1,"websecaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      RESCTAA1,"resctaaf"
          OPEN      RESLABA1,"reslabaf"
          OPEN      RESPENA1,"respenaf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
.-------------------------------------------------------------------------------
. Clear Parameters
.-------------------------------------------------------------------------------
CLRPAR00  
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,ADMISSNO
          MOVE      SP70,URNUMBER 
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
.
          MOVE      SP70,CODESKEY  
          MOVE      SP70,RSCTAKEY  
          MOVE      SP70,RSCTA001  
          MOVE      SP70,RSCTA002  
.
          MOVE      SP70,HLTAB001  
          MOVE      SP70,HLTAB002  
.
          MOVE      SP70,HLCOD001  
          MOVE      SP70,HLCOD002  
          MOVE      SP70,HLCOD003  
          MOVE      SP70,HLCOD004  
          MOVE      SP70,HLCOD005 
. 
          MOVE      SP70,RESLA001 
          MOVE      SP70,RESLA002 
          MOVE      ZERO,RESLA003 
          MOVE      SP70,RESLA004 
          MOVE      SP70,RESLA005 
          MOVE      ZERO,RESLA006 
          MOVE      SP70,RESLA007 
.
          MOVE      SP70,RESPN001  
          MOVE      SP70,RESPN002  
          MOVE      ZERO,RESPN003  
          MOVE      ZERO,RESPN004 
          MOVE      ZERO,RESPN005 
.
          PACK      SP127,SP70,SP70
          MOVE      SP127,KEYWORDS
.
CLRPAR99  RETURN
.-------------------------------------------------------------------------------
. Set Parameters
.-------------------------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "rsctakey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,RSCTAKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "rscta001=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,RSCTA001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "rscta002=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,RSCTA002
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "codeskey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CODESKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.   
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF  
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
. 
          MATCH     "keywords=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEYWORDS
            GOTO      SETPAR99
          ENDIF
. 
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
. 
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "hltab",QRYNAME
          IF        @EQUAL
            CALL      STTAB000
            GOTO      SETPAR99 
          ENDIF
.
          MATCH     "hlcod",QRYNAME
          IF        @EQUAL
            CALL      STCOD000
            GOTO      SETPAR99 
          ENDIF
.
          MATCH     "resla",QRYNAME
          IF        @EQUAL
            CALL      STRES000
            GOTO      SETPAR99 
          ENDIF
.
          MATCH     "respn",QRYNAME
          IF        @EQUAL
            CALL      STRPN000
            GOTO      SETPAR99 
          ENDIF
.
SETPAR99  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 3 - HL7 Table Details        
.-------------------------------------------------------------------------------
STTAB000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STTAB001,STTAB002
.
          GOTO      STTAB999
.
STTAB001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,HLTAB001
          PACK      HLTAB001,HLTAB001,SP70
          GOTO      STTAB999
.
STTAB002  MOVE      QRYDATA,HLTAB002
          GOTO      STTAB999
.
STTAB999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 4 - HL7 Code Table 
.-------------------------------------------------------------------------------
STCOD000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STCOD001,STCOD002,STCOD003,STCOD004,STCOD005
.
          GOTO      STCOD999
.
STCOD001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,HLCOD001
          PACK      HLCOD001,HLCOD001,SP70
          GOTO      STCOD999
.
STCOD002  MOVE      QRYDATA,HLCOD002
          GOTO      STCOD999
.
STCOD003  MOVE      QRYDATA,HLCOD003
          GOTO      STCOD999
.
STCOD004  MOVE      QRYDATA,HLCOD004
          GOTO      STCOD999
.
STCOD005  MOVE      QRYDATA,HLCOD005
          GOTO      STCOD999
.
STCOD999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 5 - Result Lab Codes
.-------------------------------------------------------------------------------
STRES000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STRES001,STRES002,STRES003,STRES004,STRES005,STRES006:
                       STRES007
.
          GOTO      STRES999
.
STRES001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,RESLA001
          PACK      RESLA001,RESLA001,SP70
          GOTO      STRES999
.
STRES002  MOVE      QRYDATA,RESLA002
          GOTO      STRES999
.
STRES003  MOVE      QRYDATA,RESLA003
          GOTO      STRES999
.
STRES004  MOVE      QRYDATA,RESLA004
          GOTO      STRES999
.
STRES005  MOVE      QRYDATA,RESLA005
          GOTO      STRES999
.
STRES006  MOVE      QRYDATA,RESLA006
          GOTO      STRES999
.
STRES007  MOVE      QRYDATA,RESLA007
          GOTO      STRES999
.
STRES999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 6 - Result E-Mail Notification
.-------------------------------------------------------------------------------
STRPN000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STRPN001,STRPN002,STRPN003,STRPN004,STRPN005
.
          GOTO      STRPN999
.
STRPN001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,RESPN001
          PACK      RESPN001,RESPN001,SP70
          GOTO      STRPN999
.
STRPN002  MOVE      QRYDATA,RESPN002
          GOTO      STRPN999
.
STRPN003  MOVE      QRYDATA,RESPN003
          GOTO      STRPN999
.
STRPN004  MOVE      QRYDATA,RESPN004
          GOTO      STRPN999
.
STRPN005  MOVE      QRYDATA,RESPN005
          GOTO      STRPN999
.
STRPN999  RETURN
.------------------------------------------------------------
. HL7 Table Details: Display,Add,Update,Delete
.------------------------------------------------------------
HL7TAB00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      HL7TAB40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      HL7TAB10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      HL7TAB20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      HL7TAB30 IF EQUAL
.
          GOTO      HL7TAB93
.
.         ************ ADD FORMACTION ***********
.
HL7TAB10  CALL      CHKTAB00      * Checks mandatory fields
          BRANCH    EXIT,HL7TAB99
          RESET     HLTAB001
          PACK      KEY4,HLTAB001,SP70
          CALL      RDHLTA1
          COMPARE   ZERO,OVRCD
          GOTO      HL7TAB92 IF EQUAL
          CALL      TABSAV00      * Moves input variables to file variables
          PACK      KEY4,HLTAB001,SP70
          CALL      WRHLTA1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      HL7TAB99
.
.         ************ UPDATE FORMACTION **********
.
HL7TAB20  PACK      KEY4,HLTAB001,SP70   
          CALL      RDHLTA1
          BRANCH    OVRCD,HL7TAB91
          CALL      CHKTAB00      * Checks mandatory fields
          BRANCH    EXIT,HL7TAB99
          CALL      RLHLTA1       * Readlock IO
          PACK      KEY4,HLTAB001,SP70  
          CALL      TABSAV00      * Moves input variables to file variables
          CALL      UPHLTA1       * Writes away the data
          CALL      UUHLTA1       * Unlocks IO
          MOVE      ZERO,EXIT
          GOTO      HL7TAB99
.
.         ************ DELETE FORMACTION **********
.
HL7TAB30  PACK      KEY4,HLTAB001,SP70   
          CALL      RDHLTA1
          BRANCH    OVRCD,HL7TAB91
          PACK      KEY14,HLTAB001,SP70 
          CALL      RSHLCO1
HL7TAB31  CALL      RKHLCO1
          BRANCH    OVRCD,HL7TAB32
          MATCH     HLTAB001,HLCOTID
          GOTO      HL7TAB32 IF NOT EQUAL
          PACK      KEY14,HLCOTID,HLCOCOD
          CALL      DEHLCO1       * Does Cascading delete on HL7 Codes
          GOTO      HL7TAB31
.
HL7TAB32  PACK      KEY4,HLTAB001,SP70   * Delete Category
          CALL      DEHLTA1
          MOVE      ZERO,EXIT
          GOTO      HL7TAB99
.
.         ************ DISPLAY FORMACTION **********
.
HL7TAB40  PACK      KEY4,HLTAB001,SP70
          CALL      RDHLTA1
          IF        OVRCD=1
            CALL      CLRTAB00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "HL7 Table Detail Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,HL7TAB99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      HL7TAB99
.
HL7TAB91  MOVE      "HL7 Table does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HL7TAB99
.
HL7TAB92  MOVE      "HL7 Table record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HL7TAB99
.
HL7TAB93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HL7TAB99
.
HL7TAB99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRTAB00  MOVE      SP70,HLTATID   * Table ID
          MOVE      SP70,HLTADES   * Description
          MOVE      SP70,HLTASPA   * Spare
.
CLRTAB99  RETURN
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
TABSAV00  MOVE      HLTAB001,KEY4
          SQUEEZE   KEY4
          MOVE      KEY4,HLTATID       * Table ID
          MOVE      HLTAB002,HLTADES   * Description
          MOVE      SP70,HLTASPA       * Spare
.
TABSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKTAB00  RESET     HLTAB001
          MATCH     SP70,HLTAB001
          GOTO      CHKTAB90 IF EQUAL
.
          RESET     HLTAB002
          MATCH     SP70,HLTAB002
          GOTO      CHKTAB91 IF EQUAL
          GOTO      CHKTAB98
.
CHKTAB90  MOVE      "Table ID cannot be blank!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKTAB99
.
CHKTAB91  MOVE      "Table Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKTAB99
.
CHKTAB98  MOVE      ZERO,EXIT
CHKTAB99  RETURN
.------------------------------------------------------------
. HL7 Cope Table : Display,Add,Update,Delete
.------------------------------------------------------------
HL7COD00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      HL7COD40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      HL7COD10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      HL7COD20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      HL7COD30 IF EQUAL
.
          GOTO      HL7COD93
.
HL7COD10  CALL      CHKHCD00      * Checks mandatory fields
          BRANCH    EXIT,HL7COD99
          RESET     HLTAB001
          PACK      KEY14,HLTAB001,HLCOD001,SP70
          CALL      RDHLCO1
          COMPARE   ZERO,OVRCD
          GOTO      HL7COD92 IF EQUAL
          CALL      CODSAV00      * Moves input variables to file variables
          PACK      KEY14,HLTAB001,HLCOD001,SP70
          CALL      WRHLCO1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      HL7COD99
.
HL7COD20  PACK      KEY14,HLTAB001,HLCOD001,SP70   * Update Formaction
          CALL      RDHLCO1
          BRANCH    OVRCD,HL7COD91
          CALL      CHKHCD00      * Checks mandatory fields
          BRANCH    EXIT,HL7COD99
          CALL      RLHLCO1       * Readlock IO
          CALL      CODSAV00      * Moves input variables to file variables
          CALL      UPHLCO1       * Writes away the data
          CALL      UUHLCO1       * Unlock IO
          MOVE      ZERO,EXIT
          GOTO      HL7COD99
.
HL7COD30  PACK      KEY14,HLTAB001,HLCOD001,SP70   * Delete Category
          CALL      DEHLCO1
          MOVE      ZERO,EXIT
          GOTO      HL7COD99
.
HL7COD40  PACK      KEY4,HLTAB001,SP70
          CALL      RDHLTA1
          BRANCH    OVRCD,HL7COD94  
          MOVE      HLTATID,TABLID
          MOVE      HLTADES,TABDES
.
          PACK      KEY14,HLTAB001,HLCOD001,SP70
          CALL      RDHLCO1
          IF        OVRCD=1
            CALL      CLRCOD00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "HL7 Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,HL7COD99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      HL7COD99
.
HL7COD91  MOVE      "HL7 Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HL7COD99
.
HL7COD92  MOVE      "HL7 Code record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HL7COD99
.
HL7COD93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HL7COD99
.
HL7COD94  MOVE      "Invalid HL7 Table!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HL7COD99
.
HL7COD99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRCOD00  MOVE      SP70,HLCOTID   * Table ID
          MOVE      SP70,HLCOCOD   * Code
          MOVE      SP70,HLCODES   * Description
          MOVE      SP70,HLCOCAT   * IBA Category
          MOVE      SP70,HLCOICD   * IBA Code
          MOVE      SP70,HLCOBGC   * Web Background Color
          MOVE      SP70,HLCOSPA   * Spare
.
CLRCOD99  RETURN
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
CODSAV00  MOVE      HLTAB001,KEY4
          SQUEEZE   KEY4
          MOVE      KEY4,HLCOTID       * Table ID
.
          MOVE      HLCOD001,KEY10
          SQUEEZE   KEY10
          MOVE      KEY10,HLCOCOD      * Code
.
          MOVE      HLCOD002,HLCODES   * Description
          MOVE      HLCOD003,HLCOCAT   * IBA Category
          MOVE      HLCOD004,HLCOICD   * IBA Code
          MOVE      HLCOD005,HLCOBGC   * Web Background Color
          MOVE      SP70,HLCOSPA       * Spare
.
CODSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKHCD00  RESET     HLCOD001
          MATCH     SP70,HLCOD001
          GOTO      CHKHCD90 IF EQUAL
.
          RESET     HLCOD002
          MATCH     SP70,HLCOD002
          GOTO      CHKHCD91 IF EQUAL
          GOTO      CHKHCD98
.
CHKHCD90  MOVE      "Code cannot be blank!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKHCD99
.
CHKHCD91  MOVE      "Code Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKHCD99
.
CHKHCD98  MOVE      ZERO,EXIT
CHKHCD99  RETURN
.------------------------------------------------------------
. Result Lab Codes: Display,Add,Update,Delete
.------------------------------------------------------------
RESLAB00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      RESLAB40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      RESLAB10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      RESLAB20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      RESLAB30 IF EQUAL
.
          GOTO      RESLAB93
.
.         ************ ADD FORMACTION ***********
.
RESLAB10  CALL      CHKRES00      * Checks mandatory fields
          BRANCH    EXIT,RESLAB99
          RESET     RESLA001
          PACK      KEY3,RESLA001,SP70
          CALL      RDRELB1
          COMPARE   ZERO,OVRCD
          GOTO      RESLAB92 IF EQUAL
          CALL      RESSAV00      * Moves input variables to file variables
          PACK      KEY3,RESLA001,SP70
          CALL      WRRELB1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      RESLAB99
.
.         ************ UPDATE FORMACTION **********
.
RESLAB20  PACK      KEY3,RESLA001,SP70   
          CALL      RDRELB1
          BRANCH    OVRCD,RESLAB91
          CALL      CHKRES00      * Checks mandatory fields
          BRANCH    EXIT,RESLAB99
          CALL      RLRELB1       * Readlock IO
          PACK      KEY3,RESLA001,SP70  
          CALL      RESSAV00      * Moves input variables to file variables
          CALL      UPRELB1       * Writes away the data
          CALL      UURELB1       * Unlocks IO
          MOVE      ZERO,EXIT
          GOTO      RESLAB99
.
.         ************ DELETE FORMACTION **********
.
RESLAB30  PACK      KEY3,RESLA001,SP70   
          CALL      RDRELB1
          BRANCH    OVRCD,RESLAB91
          CALL      DERELB1
          MOVE      ZERO,EXIT
          GOTO      RESLAB99
.
.         ************ DISPLAY FORMACTION **********
.
RESLAB40  PACK      KEY3,RESLA001,SP70
          CALL      RDRELB1
          IF        OVRCD=1
            BRANCH    FHIRTYPE,RESLAB91
            CALL      CLRRES00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Result Lab Codes Detail Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,RESLAB99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      RESLAB99
.
RESLAB91  MOVE      "Lab Code record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESLAB99
.
RESLAB92  MOVE      "Lab Code record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESLAB99
.
RESLAB93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESLAB99
.
RESLAB99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRRES00  MOVE      SP70,RELBLCD   * Code
          MOVE      SP70,RELBDES   * Description
          MOVE      ZERO,RELBLRN   * Last Run Number
          MOVE      SP70,RELBEML   * E-Mail Address for Lab
          MOVE      SP70,RELBHEM   * Hospital E-Mail Address for Errors
          MOVE      ZERO,RELBMID   * HL7 Message ID
          MOVE      SP70,RELBSAP   * HL7 Sending Application
          MOVE      SP70,RELBSPA   * Spare
.
CLRRES99  RETURN
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
RESSAV00  MOVE      RESLA001,RELBLCD   * Code
          MOVE      RESLA002,RELBDES   * Description
          MOVE      RESLA003,RELBLRN   * Last Run Number
          MOVE      RESLA004,RELBEML   * E-Mail Address for Lab
          MOVE      RESLA005,RELBHEM   * Hospital E-Mail Address for Errors
          MOVE      RESLA006,RELBMID   * HL7 Message ID
          MOVE      RESLA007,RELBSAP   * HL7 Sending Application
          MOVE      SP70,RELBSPA       * Spare
.
RESSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKRES00  RESET     RESLA001
          MATCH     SP70,RESLA001
          GOTO      CHKRES90 IF EQUAL
.
          RESET     RESLA002
          MATCH     SP70,RESLA002
          GOTO      CHKRES91 IF EQUAL
          GOTO      CHKRES98
.
CHKRES90  MOVE      "Lab Code cannot be blank!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKRES99
.
CHKRES91  MOVE      "Lab Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKRES99
.
CHKRES98  MOVE      ZERO,EXIT
CHKRES99  RETURN
.------------------------------------------------------------
. Result E-Mail Notification: Display,Add,Update,Delete
.------------------------------------------------------------
RESPEN00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      RESPEN40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      RESPEN10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      RESPEN20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      RESPEN30 IF EQUAL
.
          GOTO      RESPEN93
.
.         ************ ADD FORMACTION ***********
.
RESPEN10  CALL      CHKPEN00      * Checks mandatory fields
          BRANCH    EXIT,RESPEN99
          RESET     RESPN001
          PACK      KEY32,RESPN001,SP70
          CALL      RDREPE1
          COMPARE   ZERO,OVRCD
          GOTO      RESPEN92 IF EQUAL
          CALL      PENSAV00      * Moves input variables to file variables
          PACK      KEY32,RESPN001,SP70
          CALL      WRREPE1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      RESPEN99
.
.         ************ UPDATE FORMACTION **********
.
RESPEN20  PACK      KEY32,RESPN001,SP70   
          CALL      RDREPE1
          BRANCH    OVRCD,RESPEN91
          CALL      CHKPEN00      * Checks mandatory fields
          BRANCH    EXIT,RESPEN99
          CALL      RLREPE1       * Readlock IO
          PACK      KEY32,RESPN001,SP70  
          CALL      PENSAV00      * Moves input variables to file variables
          CALL      UPREPE1       * Writes away the data
          CALL      UUREPE1       * Unlocks IO
          MOVE      ZERO,EXIT
          GOTO      RESPEN99
.
.         ************ DELETE FORMACTION **********
.
RESPEN30  PACK      KEY32,RESPN001,SP70   
          CALL      RDREPE1
          BRANCH    OVRCD,RESPEN91
          CALL      DEREPE1
          MOVE      ZERO,EXIT
          GOTO      RESPEN99
.
.         ************ DISPLAY FORMACTION **********
.
RESPEN40  PACK      KEY32,RESPN001,SP70
          CALL      RDREPE1
          IF        OVRCD=1
            CALL      CLRPEN00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "E-Mail Triggers Detail Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,RESPEN99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      RESPEN99
.
RESPEN91  MOVE      "E-Mail Trigger record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESPEN99
.
RESPEN92  MOVE      "E-Mail Trigger record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESPEN99
.
RESPEN93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESPEN99
.
RESPEN99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRPEN00  MOVE      SP70,REPEKEY   * Notification Key
          MOVE      SP70,REPEEML   * E-Mail Address
          MOVE      ZERO,REPEALL   * All Results 0=No 1=Yes
          MOVE      ZERO,REPEABN   * Abnormal Results 0=No 1=Yes
          MOVE      ZERO,REPEURG   * Urgent Result 0=No 1=Yes           
          MOVE      SP70,REPESPA   * Spare
.
CLRPEN99  RETURN
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
PENSAV00  MOVE      RESPN001,REPEKEY   * Notification Key
          MOVE      RESPN002,REPEEML   * E-Mail Address
.
          COMPARE   ZERO,RESPN003
          IF        @EQUAL
            MOVE      ZERO,REPEALL
          ELSE
            MOVE      RESPN003,REPEALL   * All Results 0=No 1=Yes
          ENDIF
.
          COMPARE   ZERO,RESPN004
          IF        @EQUAL
            MOVE      ZERO,REPEABN
          ELSE
            MOVE      RESPN004,REPEABN   * Abnormal Results 0=No 1=Yes
          ENDIF
.
          COMPARE   ZERO,RESPN005
          IF        @EQUAL
            MOVE      ZERO,REPEURG
          ELSE
            MOVE      RESPN005,REPEURG   * Urgent Result 0=No 1=Yes           
          ENDIF
.
          MOVE      SP70,REPESPA       * Spare
.
PENSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKPEN00  RESET     RESPN001
          MATCH     SP70,RESPN001
          GOTO      CHKPEN90 IF EQUAL
.
          RESET     RESPN002
          MATCH     SP70,RESPN002
          GOTO      CHKPEN91 IF EQUAL
          GOTO      CHKPEN98
.
CHKPEN90  MOVE      "Notification Key cannot be blank!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKPEN99
.
CHKPEN91  MOVE      "E-Mail Address is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKPEN99
.
CHKPEN98  MOVE      ZERO,EXIT
CHKPEN99  RETURN
.-------------------------------------------------------------------------------
. Process Fields called from WEBBOD00 
.-------------------------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40,PROFLD50:
                             PROFLD60
          GOTO      PROFLD99
.
PROFLD10  
          GOTO      PROFLD99
.
PROFLD20  CALL      RCTA0000
          GOTO      PROFLD99
.
.      HL7 Table Details 
.
PROFLD30  CALL      H7TB0000       * HL7 Table Details
          GOTO      PROFLD99
.
.      HL7 Code Table 
.
PROFLD40  CALL      H7CD0000       * HL7 Code Table 
          GOTO      PROFLD99
.
.      Result Lab Codes
.
PROFLD50  CALL      RESL0000       * Result Lab Codes
          GOTO      PROFLD99
.
.      Result E-Mail Notification
.
PROFLD60  CALL      RESP0000       * Result E-Mail Notification
          GOTO      PROFLD99
.
.
PROFLD99  RETURN
.-----------------------------------------------------------
. Option 3 - HL7 Table Details 
.-----------------------------------------------------------
H7TB0000  BRANCH    FLDITMNO,H7TB0100,H7TB0200,H7TB0300,H7TB0400,H7TB0500
          GOTO      H7TB9999
.
H7TB0100  WRITE     HTMLFILE;HLTATID;
          GOTO      H7TB9999
.
H7TB0200  WRITE     HTMLFILE;HLTADES;
          GOTO      H7TB9999
.
H7TB0300  CALL      H7DET000     * Table of HL7 Table Details 
          GOTO      H7TB9999
.
H7TB0400  CALL      NEXTHL00     * Link to Next Set HL7 Table/Codes
          GOTO      H7TB9999
.
H7TB0500  CALL      PREVHL00     * Link to Prev Set HL7 Table/Codes
          GOTO      H7TB9999
.
H7TB9999  RETURN
.-----------------------------------------------------------
. Option 4 - HL7 Code Table 
.-----------------------------------------------------------
H7CD0000  BRANCH    FLDITMNO,H7CD0100,H7CD0200,H7CD0300,H7CD0400,H7CD0500:
                             H7CD0600,H7CD0700,H7CD0800,H7CD0900,H7CD1000
          GOTO      H7CD9999
.
H7CD0100  WRITE     HTMLFILE;TABLID;   * Table ID
          GOTO      H7CD9999
.
H7CD0200  WRITE     HTMLFILE;HLCOCOD;  * Code 
          GOTO      H7CD9999
.
H7CD0300  MATCH     SP70,HLCODES
          GOTO      H7CD9999 IF EQUAL
          WRITE     HTMLFILE;HLCODES;  * Code Description
          GOTO      H7CD9999
.
H7CD0400  MATCH     SP70,HLCOCAT
          GOTO      H7CD9999 IF EQUAL
          WRITE     HTMLFILE;HLCOCAT;  * IBA Category
          GOTO      H7CD9999
.
H7CD0500  MATCH     "0074",TABLID
          IF        @EQUAL
            PACK      CATEGORY,CATDG,SP70
            PACK      CODE,HLCOICD,SP70
            CALL      CODSEL00
            GOTO      H7CD9999
          ENDIF
.
          MATCH     SP70,HLCOICD
          GOTO      H7CD9999 IF EQUAL
          WRITE     HTMLFILE;HLCOICD;  * IBA Code
          GOTO      H7CD9999
.
H7CD0600  MATCH     SP70,HLCOBGC
          IF        @EQUAL
            WRITE     HTMLFILE;"#FFFF00";  * Web Background Color
            GOTO      H7CD9999
          ENDIF   
          MOVE      HLCOBGC,KEY7
          REP       "## ",KEY7
          SQUEEZE   KEY7
          MOVE      KEY7,KEY6 
          WRITE     HTMLFILE;KEY6;         * Web Background Color
          GOTO      H7CD9999
.
H7CD0700  CALL      H7COD000     * Table of HL7 Table Details
          GOTO      H7CD9999
.
H7CD0800  CALL      NEXTHL00     * Link to Next Set HL7 Table/Codes
          GOTO      H7CD9999
.
H7CD0900  CALL      PREVHL00     * Link to Prev Set HL7 Table/Codes
          GOTO      H7CD9999
.
H7CD1000  WRITE     HTMLFILE;TABDES;   * Table Description
          GOTO      H7CD9999
.
H7CD9999  RETURN
.-----------------------------------------------------------
. Option 5 - Result Lab Codes
.-----------------------------------------------------------
RESL0000  BRANCH    FLDITMNO,RESL0100,RESL0200,RESL0300,RESL0400,RESL0500:
                             RESL0600,RESL0700,RESL0800,RESL0900,RESL1000:
                             RESL1100
          GOTO      RESL9999
.
RESL0100  WRITE     HTMLFILE;RELBLCD;  * Code     
          GOTO      RESL9999
.
RESL0200  WRITE     HTMLFILE;RELBDES;  * Description
          GOTO      RESL9999
.
RESL0300  COMPARE   ZERO,RELBLRN
          GOTO      RESL9999 IF EQUAL
          WRITE     HTMLFILE;RELBLRN;  * Last Run Number
          GOTO      RESL9999
.
RESL0400  MATCH     SP70,RELBEML
          GOTO      RESL9999 IF EQUAL
          WRITE     HTMLFILE;RELBEML;  * E-Mail Address for Lab
          GOTO      RESL9999
.
RESL0500  MATCH     SP70,RELBHEM
          GOTO      RESL9999 IF EQUAL
          WRITE     HTMLFILE;RELBHEM;  * Hosp E-Mail Address for Errors 
          GOTO      RESL9999
.
RESL0600  COMPARE   ZERO,RELBMID
          GOTO      RESL9999 IF EQUAL
          WRITE     HTMLFILE;RELBMID;  * HL7 Message ID       
          GOTO      RESL9999
.
RESL0700  MATCH     SP70,RELBSAP
          GOTO      RESL9999 IF EQUAL
          WRITE     HTMLFILE;RELBSAP;  * HL7 Sending Application
          GOTO      RESL9999
.
RESL0800  CALL      RESTB000     * Table of Result Lab Codes
          GOTO      RESL9999
.
RESL0900  CALL      NEXTRS00     * Link to Next Set of Lab Codes
          GOTO      RESL9999
.
RESL1000  CALL      PREVRS00     * Link to Prev Set  of Lab Codes
          GOTO      RESL9999
.
RESL1100  CALL      RESLOC00     * Link to Prev Set  of Lab Codes
          GOTO      RESL9999
.
RESL9999  RETURN
+
.
. FHIR Location Resource
.
RESLOC00  STRIP     RELBDES
          CLEAR     KEY16
          APPEND    "Laboratory-",KEY16
          APPEND    RELBLCD,KEY16
          APPEND    SP70,KEY16
          RESET     KEY16
          STRIP     KEY16
          STRIP     WBTPFIL  *  Template
          WRITE     HTMLFILE;*+," { #"resourceType#": #"Location#",":
                    "   #"id#": #"",KEY16,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}":
                    " ],":
                    " #"identifier#": {":
                    "   #"value#": #"",KEY16,"#"":
                    " },";
          WRITE     HTMLFILE;*+," #"name#": #"",RELBDES,"#"":
                    " } "
          RETURN
+
.-----------------------------------------------------------
. Option 6 - Result E-Mail Notification
.-----------------------------------------------------------
RESP0000  BRANCH    FLDITMNO,RESP0100,RESP0200,RESP0300,RESP0400,RESP0500:
                             RESP0600,RESP0700,RESP0800
          GOTO      RESP9999
.
RESP0100  WRITE     HTMLFILE;REPEKEY;  * Notification Key
          GOTO      RESP9999
.
RESP0200  WRITE     HTMLFILE;REPEEML;  * E-Mail Address
          GOTO      RESP9999
.
RESP0300  COMPARE   ZERO,REPEALL
          GOTO      RESP9999 IF EQUAL
          WRITE     HTMLFILE;REPEALL;  * All Results     
          GOTO      RESP9999
.
RESP0400  COMPARE   ZERO,REPEABN
          GOTO      RESP9999 IF EQUAL
          WRITE     HTMLFILE;REPEABN;  * Abnormal Results       
          GOTO      RESP9999
.
RESP0500  COMPARE   ZERO,REPEURG
          GOTO      RESP9999 IF EQUAL
          WRITE     HTMLFILE;REPEURG;  * Urgent Result 0=No 1=Yes       
          GOTO      RESP9999
.
RESP0600  CALL      RPNTB000     * Table of Result Lab Codes
          GOTO      RESP9999
.
RESP0700  CALL      NEXTRS00     * Link to Next Set of Lab Codes
          GOTO      RESP9999
.
RESP0800  CALL      PREVRS00     * Link to Prev Set  of Lab Codes
          GOTO      RESP9999
.
RESP9999  RETURN
.-----------------------------------------------------------
. Link to Next Group Lab Codes           
.-----------------------------------------------------------
NEXTRS00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F4
          MOVE      F4,KEY4
          REP       " +",KEY4
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"cliweb07.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY4:
                                 "&norecord=",KEY2;
.
NEXTRS99  RETURN
.-----------------------------------------------------------
. Link to Prev Group Lab Codes           
.-----------------------------------------------------------
PREVRS00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F4
          IF        F4<0
            MOVE      ZERO,F4
          ENDIF
          MOVE      F4,KEY4
          REP       " +",KEY4
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"cliweb07.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY4:
                                 "&norecord=",KEY2;
.
PREVRS99  RETURN
.-----------------------------------------------------------
. Link to Next Group of HL7 Table/Codes 
.-----------------------------------------------------------
NEXTHL00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F4
          MOVE      F4,KEY4
          REP       " +",KEY4
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
          REP       " +",STARTKEY
          REP       " +",TABLID
.
          WRITE     HTMLFILE;"cliweb07.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY4:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&hltab001=",TABLID;
.
          REP       "+ ",STARTKEY
          REP       "+ ",TABLID
.
NEXTHL99  RETURN
.-----------------------------------------------------------
. Link to Prev Group of HL7 Table/Codes
.-----------------------------------------------------------
PREVHL00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F4
          IF        F4<0
            MOVE      ZERO,F4
          ENDIF
          MOVE      F4,KEY4
          REP       " +",KEY4
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
          REP       " +",STARTKEY
          REP       " +",TABLID
.
          WRITE     HTMLFILE;"cliweb07.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY4:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&hltab001=",TABLID;
.
          REP       "+ ",STARTKEY
          REP       "+ ",TABLID
.
PREVHL99  RETURN
.------------------------------------------------------------
. Table of HL7 Table Details
.------------------------------------------------------------
H7DET000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      HLHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY4,STARTKEY,SP70
          CALL      RSHLTA1
          IF        OVRCD=0
            CALL      RPHLTA1
          ENDIF
H7DET100  CALL      RKHLTA1
          BRANCH    OVRCD,H7DET900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      H7DET100
          ENDIF
.
          CALL      H7ROW000      * Display HL7 Table Details Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      H7DET100 IF NOT EQUAL
          GOTO      H7DET999
.
H7DET900  CALL      NOMORE00      * Display No More Records Message
.
H7DET999  RETURN
.------------------------------------------------------------
. HL7 Table Details (TABLE HEADING)
.------------------------------------------------------------
HLHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "<b>HL7 Table ID</b></td>":
                               "<td class=HeadingCell width=40%>":
                               "<b>Description</b></td>":
                             "</tr>"
.
HLHED999  RETURN
.------------------------------------------------------------
. HL7 Code Table (TABLE ROW)
.------------------------------------------------------------
H7ROW000  MATCH     SP70,HLTADES 
          IF        @EQUAL
            MOVE      "&nbsp;",TABDES
          ELSE
            MOVE      HLTADES,TABDES
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&hltab001=",HLTATID,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             HLTATID,"</td>":
                             "<td>",TABDES,"</td>":
                             "</tr>"
.
H7ROW999 RETURN
.------------------------------------------------------------
. Table of HL7 Codes
.------------------------------------------------------------
H7COD000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      DLHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY14,HLTAB001,STARTKEY,SP70
          CALL      RSHLCO1
          IF        OVRCD=0
            CALL      RPHLCO1
          ENDIF
H7COD100  CALL      RKHLCO1
          BRANCH    OVRCD,H7COD900
.
          MATCH     HLTAB001,HLCOTID
          GOTO      H7COD100 IF NOT EQUAL 
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      H7COD100
          ENDIF
.
          CALL      CODRW000      * Display HL7 Table Details Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      H7COD100 IF NOT EQUAL
          GOTO      H7COD999
.
H7COD900  CALL      NOMORE00      * Display No More Records Message
.
H7COD999  RETURN
.------------------------------------------------------------
. HL7 Table Details (TABLE HEADING)
.------------------------------------------------------------
DLHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "<b>HL7 Code</b></td>":
                               "<td class=HeadingCell width=40%>":
                               "<b>Description</b></td>";
          MATCH     "0074",TABLID
          IF        @EQUAL
            WRITE     HTMLFILE;"<td class=HeadingCell width=40%>":
                               "<b>Diagnostic Type Group</b></td>";
          ENDIF
.*******                             "<td class=HeadingCell width=40%>":
.*******                             "<b>IBA Category/Code</b></td>":
          WRITE     HTMLFILE;"</tr>"
.
DLHED999  RETURN
.------------------------------------------------------------
. HL7 Code Table (TABLE ROW)
.------------------------------------------------------------
CODRW000  MATCH     SP70,HLCODES 
          IF        @EQUAL
            MOVE      "&nbsp;",TABDES
          ELSE
            MOVE      HLCODES,TABDES
          ENDIF
.
.          MATCH     SP70,HLCOCAT         * IBA Category
.          IF        @EQUAL 
.            MOVE      "&nbsp;",KEY2  
.          ELSE
.            MOVE      HLCOCAT,KEY2
.            MOVE      "-",KEY1
.          ENDIF
.
.          MATCH     SP70,HLCOICD        * IBA Code 
.          IF        @EQUAL 
.            MOVE      "&nbsp;",KEY3  
.          ELSE
.            MOVE      HLCOICD,KEY3
.          ENDIF
.                                             * Packed with Category & Code
.          PACK      KEY8,KEY2,KEY1,KEY3      * format = XX - XXX 
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail04":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&hltab001=",TABLID:
                             "&hlcod001=",HLCOCOD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             HLCOCOD,"</td>":
                             "<td>",TABDES,"</td>";
          MATCH     "0074",TABLID
          IF        @EQUAL
            MATCH     SP70,HLCOICD
            IF        !@EQUAL
              PACK      KEY5,CATDG,HLCOICD,SP70
              CALL      RDCODE1
              WRITE     HTMLFILE;"<td>",TDESC,"</td>";
            ELSE
              WRITE     HTMLFILE;"<td>&nbsp;</td>";
            ENDIF
          ENDIF
. * Category/Code column       "<td>",KEY8,"</td>": *****************
          WRITE     HTMLFILE;"</tr>"
.
CODRW999 RETURN
.------------------------------------------------------------
. Table of Result Lab Codes
.------------------------------------------------------------
RESTB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      RSHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY3,STARTKEY,SP70
          CALL      RSRELB1
          IF        OVRCD=0
            CALL      RPRELB1
          ENDIF
RESTB100  CALL      RKRELB1
          BRANCH    OVRCD,RESTB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      RESTB100
          ENDIF
.
          CALL      RESRW000      * Display Lab Codes Table Details Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      RESTB100 IF NOT EQUAL
          GOTO      RESTB999
.
RESTB900  CALL      NOMORE00      * Display No More Records Message
.
RESTB999  RETURN
.------------------------------------------------------------
. Result Lab Codes Table Details (TABLE HEADING)
.------------------------------------------------------------
RSHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "<b>Code</b></td>":
                               "<td class=HeadingCell width=40%>":
                               "<b>Description</b></td>":
                             "</tr>"
.
RSHED999  RETURN
.------------------------------------------------------------
. Lab Codes Table (TABLE ROW)
.------------------------------------------------------------
RESRW000  MATCH     SP70,RELBDES 
          IF        @EQUAL
            MOVE      "&nbsp;",TABDES
          ELSE
            MOVE      RELBDES,TABDES
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail05":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&resla001=",RELBLCD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             RELBLCD,"</td>":
                             "<td>",TABDES,"</td>":
                             "</tr>"
.
RESRW999 RETURN
.------------------------------------------------------------
. Table of Result E-Mail Triggers  
.------------------------------------------------------------
RPNTB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      RPHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY32,STARTKEY,SP70
          CALL      RSREPE1
          IF        OVRCD=0
            CALL      RPREPE1
          ENDIF
RPNTB100  CALL      RKREPE1
          BRANCH    OVRCD,RPNTB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      RPNTB100
          ENDIF
.
          CALL      RPNRW000      * Display E-Mail Triggers Table Details Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      RPNTB100 IF NOT EQUAL
          GOTO      RPNTB999
.
RPNTB900  CALL      NOMORE00      * Display No More Records Message
.
RPNTB999  RETURN
.------------------------------------------------------------
. Result E-Mail Triggers Table Details (TABLE HEADING)
.------------------------------------------------------------
RPHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "<b>E-Mail Trigger</b></td>":
                               "<td class=HeadingCell width=40%>":
                               "<b>E-Mail Address</b></td>":
                             "</tr>"
.
RPHED999  RETURN
.------------------------------------------------------------
. Email Triggers Table (TABLE ROW)
.------------------------------------------------------------
RPNRW000  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail06":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&respn001=",REPEKEY,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             REPEKEY,"</td>":
                             "<td>",REPEEML,"</td>":
                             "</tr>"
.
RPNRW999 RETURN
.------------------------------------------------------------
. Result Code Table Details: Display,Add,Update,Delete
.------------------------------------------------------------
RESCTA00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      RESCTA40 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      RESCTA20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Update formaction
          GOTO      RESCTA30 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Update formaction
          GOTO      RESCTA10 IF EQUAL
          GOTO      RESCTA92
.
RESCTA10  PACK      KEY32,RSCTAKEY,SP70
          CALL      RDRECT1
          COMPARE   ZERO,OVRCD
          GOTO      RESCTA93 IF EQUAL
          UNPACK    KEY32,RECTLAB,RECTSEG,RECTFLD,RECTSYS,RECTCOD
          MOVE      RSCTA001,RECTDES
          SQUEEZE   RSCTA002
          MOVE      ZERO,F2
          MOVE      RSCTA002,F2
          MOVE      F2,RECTSLV
          CALL      WRRECT1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      RESCTA99
.
RESCTA20  PACK      KEY32,RSCTAKEY,SP70
          CALL      RDRECT1
          BRANCH    OVRCD,RESCTA91
          MOVE      RSCTA001,RECTDES
          SQUEEZE   RSCTA002
          MOVE      ZERO,F2
          MOVE      RSCTA002,F2
          MOVE      F2,RECTSLV
          CALL      UPRECT1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      RESCTA99
.
RESCTA30  PACK      KEY32,RSCTAKEY,SP70
          CALL      RDRECT1
          BRANCH    OVRCD,RESCTA91
          CALL      DERECT1       * Delete
          MOVE      ZERO,EXIT
          GOTO      RESCTA99
.
RESCTA40  PACK      KEY32,RSCTAKEY,SP70
          CALL      RDRECT1
          IF        OVRCD=1
            CALL      CLRCTA00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Result Code Table Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,RESCTA99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      RESCTA99
.
RESCTA91  MOVE      "Invalid Result Key",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HL7TAB99
.
RESCTA92  MOVE      "Invalid Update Type",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESCTA99
.
RESCTA93  MOVE      "Record Already Exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESCTA99
.
RESCTA99  RETURN
.
CLRCTA00  MOVE      SP70,RECTLAB
          MOVE      SP70,RECTSEG
          MOVE      SP70,RECTFLD
          MOVE      SP70,RECTSYS
          MOVE      SP70,RECTCOD
          MOVE      SP70,RECTDES 
          MOVE      SP70,RECTSLV   
          MOVE      SP70,RECTSPA  
          RETURN
.
.-----------------------------------------------------------
. Option 2 - Result Codes
.-----------------------------------------------------------
RCTA0000  BRANCH    FLDITMNO,RCTA0100,RCTA0200,RCTA0300,RCTA0400,RCTA0500:
                             RCTA0600,RCTA0700,RCTA0800,RCTA0900,RCTA1000:
                             RCTA1100,RCTA1200
          GOTO      RCTA9999
.
RCTA0100  PACK      RSCTAKEY,RECTLAB,RECTSEG,RECTFLD,RECTSYS,RECTCOD,SP70
          MATCH     SP70,RSCTAKEY
          GOTO      RCTA9999 IF EQUAL
          WRITE     HTMLFILE;RSCTAKEY; * Key
          GOTO      RCTA9999
RCTA0200  MATCH     SP70,RECTLAB
          GOTO      RCTA9999 IF EQUAL
          WRITE     HTMLFILE;RECTLAB;  * Lab
          GOTO      RCTA9999
RCTA0300  MATCH     SP70,RECTSEG
          GOTO      RCTA9999 IF EQUAL
          WRITE     HTMLFILE;RECTSEG;  * Segment
          GOTO      RCTA9999
RCTA0400  MATCH     SP70,RECTFLD
          GOTO      RCTA9999 IF EQUAL
          WRITE     HTMLFILE;RECTFLD;  * Field
          GOTO      RCTA9999
RCTA0500  MATCH     SP70,RECTSYS
          GOTO      RCTA9999 IF EQUAL
          WRITE     HTMLFILE;RECTSYS;  * Coding System
          GOTO      RCTA9999
RCTA0600  MATCH     SP70,RECTCOD
          GOTO      RCTA9999 IF EQUAL
          WRITE     HTMLFILE;RECTCOD;  * Code
          GOTO      RCTA9999
RCTA0700  MATCH     SP70,RECTDES
          GOTO      RCTA9999 IF EQUAL
          WRITE     HTMLFILE;RECTDES;  * Code
          GOTO      RCTA9999
RCTA0800  MATCH     SP70,RECTSLV
          GOTO      RCTA9999 IF EQUAL
          WRITE     HTMLFILE;RECTSLV;  * Code
          GOTO      RCTA9999
.
RCTA0900  CALL      CTATB000     * Table of Result Codes
          GOTO      RCTA9999
.
RCTA1000  CALL      NEXTCT00     * Link to Next Codes
          GOTO      RCTA9999
.
RCTA1100  CALL      PREVCT00     * Link to Prev Codes
          GOTO      RCTA9999
.
RCTA1200  WRITE     HTMLFILE;CODESKEY;  * Coding System
          GOTO      RCTA9999
.
RCTA9999  RETURN
.------------------------------------------------------------
. Table of Result Lab Codes
.------------------------------------------------------------
CTATB000  WRITE     HTMLFILE;"<tr>":
                    "<td class=HeadingCell width=20%>Code</td>":
                    "<td class=HeadingCell>Description</td>":
                    "<td class=HeadingCell>Security Level</td></tr>"
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY32,CODESKEY,STARTKEY,SP70
          CALL      RSRECT1
          IF        OVRCD=0
            CALL      RPRECT1
          ENDIF
CTATB100  CALL      RKRECT1
          BRANCH    OVRCD,CTATB900
          PACK      KEY32,RECTLAB,RECTSEG,RECTFLD,RECTSYS,RECTCOD
          MATCH     CODESKEY,KEY32
          GOTO      CTATB900 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      CTATB100
          ENDIF
.
          PACK      KEY32,RECTLAB,RECTSEG,RECTFLD,RECTSYS,RECTCOD
          WRITE     HTMLFILE;"<tr><td>":
                    "<img src=../images/MaintenanceIcon.gif class=Icon":
                    " align=absmiddle hspace=4 ":
                    " onclick='EditResultCode(#"",KEY32,"#")'>":
                    RECTCOD,"</td>":
                    "<td>",RECTDES,"</td>":
                    "<td>",RECTSLV,"&nbsp;</td></tr>"
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CTATB100 IF NOT EQUAL
          GOTO      CTATB999
.
CTATB900  CALL      NOMORE00    * Display No More Records Message
.
CTATB999  RETURN
.-----------------------------------------------------------
. Link to Next Group 
.-----------------------------------------------------------
NEXTCT00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F4
          MOVE      F4,KEY4
          REP       " +",KEY4
          MOVE      REPORTNO,F2
          MOVE      F2,DIM2
          MOVE      TEMPLATE,F3
          MOVE      F3,DIM3
          REP       " 0",DIM2
          REP       " 0",DIM3
          REP       " +",CODESKEY
          WRITE     HTMLFILE;"cliweb07.pbl":
                                 "?reportno=",DIM2:
                                 "&template=",DIM3:
                                 "&codeskey=",CODESKEY:
                                 "&startnum=",KEY4:
                                 "&norecord=",KEY2;
          REP       "+ ",CODESKEY
NEXTCT99  RETURN
.-----------------------------------------------------------
. Link to Prev Group Lab Codes           
.-----------------------------------------------------------
PREVCT00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F4
          IF        F4<0
            MOVE      ZERO,F4
          ENDIF
          MOVE      F4,KEY4
          REP       " +",KEY4
          MOVE      REPORTNO,F2
          MOVE      F2,DIM2
          MOVE      TEMPLATE,F3
          MOVE      F3,DIM3
          REP       " 0",DIM2
          REP       " 0",DIM3
          REP       " +",CODESKEY
          WRITE     HTMLFILE;"cliweb07.pbl":
                                 "?reportno=",DIM2:
                                 "&template=",DIM3:
                                 "&codeskey=",CODESKEY:
                                 "&startnum=",KEY4:
                                 "&norecord=",KEY2;
          REP       "+ ",CODESKEY
PREVCT99  RETURN
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
+
.
.
.
          INC       IBAWEBCD
.
          INC       HL7CODIO/INC     * HL7 Codes Table
          INC       HL7TABIO/INC     * HL7 Table Details 
          INC       PATVISIO/INC 
          INC       PATPR1IO/INC
          INC       PATDO1IO/INC
          INC       PATMI1IO/INC
          INC       PATALRIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATWR1IO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       OUTSITIO/INC
          INC       RESCTAIO/INC
          INC       RESLNKIO/INC
          INC       RESLABIO/INC
          INC       RESPENIO/INC 
          INC       PATFN1IO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
.
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       PATMASTM
          INC       PATDOCTM
          INC       DAYOFWEK
          INC       CALCAGE
          INC       NAMSTRCD
          INC       WEBDATCD
.

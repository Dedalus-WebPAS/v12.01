.*******************************************************************************
.* System         : Inpatient                                                  *
.* Program        : IBAHMS78.PBL                                               *
.* Name           : AN-SNAP Therapy End Date Report                            *
.*******************************************************************************
.* Date           : 27/11/2012                                                 *
.* Author         : Patrick Adair                                              *
.* Function       : AN-SNAP Therapy End Date Report - yeah!                    *
.* Modifications  :                                                            *
.*---------------------------------------------------------------------------- *
.* V10.06.02  11/08/2015 Jill Parkinson CAR 316454                             *
.*                       Mods checking Therapy End date against Disc.date      *
.* V10.06.01  17/07/2015 J.Tan                                      CAR 316454 *
.*                       Mods checking Therapy Start date against Disc.date    *
.* V10.04.01  16/12/2013 J.Tan                                      CAR 291504 *
.*                       Mods to check blank PMUGENDD when HF is blank         *
.* V10.03.02  22/02/2013 Patrick Adair                              CAR 275093 *
.*                       Removed rehab plan date checks as per 10.01           *
.* V10.03.01  27/11/2012 Patrick Adair                              CAR 275093 *
.*                       Created Report                                        *
.*******************************************************************************
.
          INC       STD001FD                     * Standard Include
.
          INC       IBACONFD/INC                 * Control File
          INC       PATCODFD/INC                 * Category Codes
          INC       PATCONFD/INC                 * Control File
          INC       PATDSCFD/INC                 * Patient Discharge File
          INC       PATFN1FD/INC                 * Health Fund File
          INC       PATHSPFD/INC                 * Hospital File
          INC       PATMA1FD/INC                 * Patient Master File
          INC       PATMI1FD/INC                 * Patient Admission File
          INC       PMSUPGFD/INC                 * Program Details File
          INC       PMSVX1FD/INC                 * Patient Visit Ext File
          INC       PMSPITFD/INC                 * Program Interruptions
.
.-------------------------------------------------------------------------------
.                           Variables
.-------------------------------------------------------------------------------
ALLWRDS   DIM       1                            * 1 = All wards
CARECATG  DIM       2                            * Category for Care Type
CMDLINE   DIM       50                           * Command Line
DATEFROM  DIM       8                            * Input From Date
DATETO    DIM       8                            * Input To Date
DAYCNT    FORM      2                            * Number of Days Difference
HOSPAPPR  DIM       10                           * Hospital Approval Number
HOSPCODE  DIM       3                            * Hospital Code
HOSPNAME  DIM       40                           * Hospital Name
INCLDAYC  DIM       1                            * Include Day Cases
.                                                  0 = No
.                                                  1 = Yes
PRNTATYP  DIM       20                           * Admission Type Description
PRNTADTE  DIM       8                            * Admiss Date dd/mm/yy 
PRNTDDTE  DIM       8                            * Disch Date dd/mm/yy  
TODAY     DIM       8                            * Today's Date
.-------------------------------------------------------------------------------
.                            Constants
.-------------------------------------------------------------------------------
CATA      INIT      "A "
CATCC     INIT      "CC"
.
PRGID     INIT      "IBAHMS78"
PRGNAM    INIT      "Therapy End Date"
VERSION   INIT      "V12.01.00"
.
. ******************************************************************************
. *                                  MAIN0000                                  *
. *                                                                            *
. * Process Flow Control                                                       *
. ******************************************************************************
.
MAIN0000  CALL      INIT0000                     * Initialise - begin descent
.
MAIN1000  IF        IBCNMHOS=1
            DISPLAY   *P25:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            CALL      GHOS0000                   * Get Multi Hospital Id
            BRANCH    EXIT,MAIN9999
          ELSE
            DISPLAY   *P1:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            PACK      HOSPAPPR,CAPPRVNO,SP70     * Hospital Approval Number
            PACK      HOSPNAME,CONAME,SP70       * Hospital Name
          ENDIF
.
          CALL      GSDT0000                     * Keyin the start date
          BRANCH    EXIT,MAIN9999                * Exit if nothing input
.
          CALL      GEDT0000                     * Keyin the ending date
          BRANCH    EXIT,MAIN1000                * Exit if nothing input
.
          CALL      GDAY0000                     * Keyin Include day cases
          BRANCH    EXIT,MAIN1000                * Nothing input
.
          CALL      GCON0000                     * OK to continue
          BRANCH    EXIT,MAIN1000                * restart if no selected
.
          CALL      EXTR0000                     * Therapy End Date Report
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
          CALL      UND80
          PRINT     *N,*N,"*** End of Report ***"
.
          GOTO      MAIN0000                     * Next run
.
MAIN9999  CHAIN     PGM                          * Return from the nine circles
.
. ******************************************************************************
. *                                 INIT0000              Called by : MAIN0000 *
. *                                                                            *
. * Initialisation procedure                                                   *
. ******************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pmsupgaf";
          OPEN      PMSUPGA1,"pmsupgaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.                                                * 1 = Multi Hospital
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.                                                * Hospital Approval Number
          READ      CONTROLF,TEN;*79,CAPPRVNO
.                                                * Hospital Name
          READ      CONTROLF,TWO;*4,CONAME
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          DISPLAY   *P1:6,*EL,"Hospital Id : ";
          DISPLAY   *P1:7,"From Date  : ":
                    *P1:8,"To   Date  : ":
                    *P1:9,"Include Day Cases Y/N : ";
.
          MOVE      ZERO,INCLDAYC                * Include Day Cases Flag
          MOVE      SEVENTY,CLNO                 * Init Line Count
          MOVE      ZERO,CPAGENO                 * Init Page Count
          CLOCK     TIME,CTIMEIS                 * Get the starting time
.
INIT9999  RETURN
.
.*******************************************************************************
.*                                  GHOS0000               Called by: MAIN0000 *
.*                                                                             *
.* Get Hospital Id                                                             *
.*******************************************************************************
GHOS0000  MOVE      ZERO,EXIT
          DISPLAY   *P25:6,*EL:
                    *P25:7,*EL:
                    *P25:8,*EL:
                    *P25:10,*EF;
.
          MOVE      SP70,HOSPCODE
          MOVE      SP70,PTHSHOSP
          MOVE      TWENTY5,ECOL                 * Column of key in
          MOVE      SIX,EVERT                    * Row of key in
          MOVE      SP3,CKYIDEF3                 * Default value
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,GHOS98000,GHOS98000
.
          PACK      HOSPCODE,KEY3,SP70           * Hospital Code
          PACK      HOSPAPPR,PTHSAPPR,SP70       * Hospital Approval Number
          PACK      HOSPNAME,PTHSNAME,SP70       * Hospital Name
.
          DISPLAY   *P25:6,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      GHOS9999
.
GHOS98000  MOVE     ONE,EXIT
.
GHOS9999  RETURN
.
. ******************************************************************************
. *                                  GSDT0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the start  date of the report                                          *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GSDT0000  MOVE      ZERO,EXIT
.
          MOVE      SEVEN,CVERT                  * Line for keyin of from date
          MOVE      TWENTY5,CCOL                 * Col for keyin of from date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          UNPACK    SP6,CDAY,CMON,CYEAR          * Initialise date
          MOVE      SP8,DATEFROM
          MOVE      CCC,CCENT                    * Initialise century
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GSDT9000
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATEFROM
          MATCH     DATEFROM,TODAY
          GOTO      GSDT9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GSDT0000
.
GSDT9000  MOVE      ONE,EXIT
.
GSDT9999  RETURN
.
. ******************************************************************************
. *                                  GEDT0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the end date of the report                                             *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GEDT0000  MOVE      ZERO,EXIT
.
          MOVE      EIGHT,CVERT                  * Line for keyin of to date
          MOVE      TWENTY5,CCOL                 * Col for keyin of to date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          MOVE      SP8,DATETO
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GEDT9000
.
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATETO
          MATCH     DATETO,TODAY
          GOTO      GEDT2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEDT0000
.
GEDT2000  MATCH     DATEFROM,DATETO
          GOTO      GEDT9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"To Date May Not be Less than From ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEDT0000
.
GEDT9000  MOVE      ONE,EXIT
.
GEDT9999  RETURN
.
. ******************************************************************************
. *                                  GDAY0000             Called by : MAIN0000 *
. *                                                                            *
. * Include/Exclude Day cases from report?                                     *
. ******************************************************************************
.
GDAY0000  MOVE      ZERO,EXIT
.
GDAY1000  KEYIN     *P25:9,"_",*P25:9,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          IF        @EQUAL
            MOVE      ONE,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          MATCH     ANS,ANSN
          IF        @EQUAL
            MOVE      ZERO,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          BEEP
          GOTO      GDAY1000
.
GDAY9999  RETURN
.
. ******************************************************************************
. *                                  GCON0000             Called by : MAIN0000 *
. *                                                                            *
. * Ok. to continue (Y/N)                                                      *
. ******************************************************************************
.
GCON0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ";
.
GCON1000  KEYIN     *P24:24,"_",*P24:24,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          GOTO      GCON9999 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      GCON9000 IF EQUAL
.
          BEEP
          GOTO      GCON1000
.
GCON9000  MOVE      ONE,EXIT
.
GCON9999  RETURN
.
. ******************************************************************************
. *                                  EXTR0000             Called by : MAIN0000 *
. *                                                                            *
. * Report all patients with no therapy end date                               *
. ******************************************************************************
.
EXTR0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,*P10:24,"Adm. No :":
                    *P58:24,"Scanning:";
.
          READ      CONTROLF,SEVENTY9;*80,PTCNQLDB
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
          IF        (PTCNHDPS=4 & PTCNQLDB=6)
            MOVE      CATA,CARECATG
          ELSE
            MOVE      CATCC,CARECATG
          ENDIF
.
          PACK      KEY16,DATEFROM               * Read thru Discharge File
          CALL      RDSDSCH2
EXTR1000  CALL      RDKDSCH2
          BRANCH    OVRCD,EXTR9999               * EOF reached
.
          MATCH     DDATE,DATETO
          GOTO      EXTR9999 IF LESS             * EOR reached
.
          PACK      KEY8,DDADMNO,SP70            * Get the Admission Record
          CALL      RDMISS1
          BRANCH    OVRCD,EXTR1000
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,HOSPCODE          * check visit for hospital
            GOTO      EXTR1000 IF NOT EQUAL
          ENDIF
.
          MATCH     "A ",CARECATG                * Read relevant category type
          IF        @EQUAL
            PACK      KEY5,CARECATG,ATYPE,SP70
          ELSE
            PACK      KEY5,CARECATG,ACARE,SP70
          ENDIF
.
          CALL      RDCODE1
          BRANCH    OVRCD,EXTR1000
.
          MATCH     "A ",CARECATG                * Check if Rehab Visit
          IF        @EQUAL
            MATCH   "9",TCINDC8
            GOTO    EXTR2000 IF EQUAL
          ELSE
            MATCH   "2",TCINDC6
            GOTO    EXTR2000 IF EQUAL
          ENDIF
.
          GOTO      EXTR1000                     * Get Next Discharge Record
.
EXTR2000  MATCH     "1",INCLDAYC                 * Include Day Visits
          GOTO      EXTR3000 IF EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      EXTR1000 IF EQUAL            * Exclude Day Visits
.
EXTR3000  DISPLAY   *P20:24,*V2LON,AURNO," ",DAADMNO;
.
          PACK      KEY31,DURNO,SP70             * Read thru Program Details
          CALL      RSPMUPG1
EXTR4000  CALL      RKPMUPG1
          BRANCH    OVRCD,EXTR1000               * EOF Reached
.
          MATCH     DURNO,PMUGURNO
          GOTO      EXTR1000 IF NOT EQUAL        * EOR Reached
.
          MATCH     PMUGATYP,ATYPE               * Check Admiss Type matches 
          GOTO      EXTR4000 IF NOT EQUAL
.
          CALL      CLPATMAS
          PACK      KEY8,PMUGURNO,SP70           * Get Patient Master Record
          CALL      RDMAST1
          BRANCH    OVRCD,EXTR4000
.
          MATCH     SP70,PMUGCLAM                * Check Claim Code matches  
          IF        !@EQUAL
            MATCH     PMUGCLAM,ACLAIM
            GOTO      EXTR4000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PMUGFUND                * Check Health Fund matches
          GOTO      EXTR6000 IF EQUAL
.
          MATCH     PMUGFUND,AFUNDH              * Check Adm Health Fund matches
          GOTO      EXTR4000 IF NOT EQUAL

          PACK      KEY14,AFUNDH,AFNDTB,SP10 
          CALL      RDFUND1                     
          BRANCH    OVRCD,EXTR4000
.
          MATCH     PMUGTABT,PTHFBCAT            * Check Health Fund Table Type 
          GOTO      EXTR4000 IF NOT EQUAL
.
.         Check if Discharge date after the Therapy start date
.
EXTR6000  MATCH     PMUGSDAT,DDATE
          GOTO      EXTR4000 IF LESS
.
          MATCH     SP70,PMUGENDD
          IF        !@EQUAL
            MATCH     DDATE,PMUGENDD
            GOTO      EXTR4000 IF LESS
          ENDIF
.
          MATCH     SP70,PMUGENDD                * Check Therapy End Date
          GOTO      EXTR1000 IF NOT EQUAL
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY  * re-format dates for printing
          PACK      PRNTADTE,CDAY,SLASH,CMON,SLASH,CYEAR
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          PACK      PRNTDDTE,CDAY,SLASH,CMON,SLASH,CYEAR
.                                                * Get Admiss Type Desc
          PACK      KEY5,CATA,PMUGATYP,SP70
          CALL      RDCODE1
          IF        OVRCD = 1
            PACK      PRNTATYP,SP70
          ELSE   
            PACK      PRNTATYP,TDESC,SP70
          ENDIF
.
          CALL      PRNT0000                     * Print Record
.   
          GOTO      EXTR1000                     * Get Next Discharge Record
.
EXTR9999  RETURN
.
. ******************************************************************************
. *                                  PRNT0000              Called by: MAIN0000 *
. *                                                                            *
. * Print list of patients without Admission FIM Details                       *
. ******************************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,*P10:24,"Printing: ";
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          DISPLAY   *P20:24,*V2LON,DURNO,DDADMNO;
.
PRNT7000  PRINT     *1,"|",*3,DDADMNO:
                    *12,"|",*14,PSNAME:
                    *34,"|",*36,PRNTATYP:
                    *57,"|",*59,PRNTADTE:
                    *68,"|",*70,PRNTDDTE:
                    *80,"|"
          ADD       ONE,CLNO
.
PRNT9999  RETURN
.
. ******************************************************************************
. *                                  HEAD0000              Called by: PRNT0000 *
. *                                                                            *
. * Print Report Heading                                                       *
. ******************************************************************************
.
HEAD0000  CALL      HEAD80                      * Standard heading
.
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *N,*17,"Hospital : ",HOSPNAME
.
          PRINT     *N,*15,"Date Range : ",CPCDATE," to ";
.
          UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     CPCDATE,*N
.                                                * Print Total head page 1 only
          CALL      UND80
          PRINT     *1,"|":
                    *22,"Therapy End Date":
                    *80,"|"
          CALL      UND80
.
          PRINT     *1,"| Admis No ":
                    *12,"| Patient Name":
                    *34,"| Admission Type":
                    *57,"| Adm Date":
                    *68,"| Dsc Date":
                    *80,"|"
          CALL      UND80
          ADD       FIVE,CLNO
.
HEAD9999  RETURN
.
.*******************************************************************************
.
          INC       STD001IO                     * System Standard Routines
.
          INC       CLPATHSP                     * Clear Hospital Table variables
          INC       CLPATDSC                     * Clear Disharge Record
          INC       CLPATMAS                     * Clear Patient Master Record
          INC       PATHSPKY                     * Keyin of a Hospital Id/code
.
          INC       PATCODIO/INC                 * Category Codes
          INC       PATDSCIO/INC                 * Patient Discharge File
          INC       PATFN1IO/INC                 * Health Fund File
          INC       PATHSPIO/INC                 * Hospital File
          INC       PATMA1IO/INC                 * Patient Master File
          INC       PATMI1IO/INC                 * Patient Admission File
          INC       PMSUPGIO/INC                 * Program Details File
          INC       PMSVX1IO/INC                 * Patient Visit Ext File
          INC       PMSPITIO/INC                 * Program Interruptions

.******************************************************************************
.*  Program       : FXDO1SCR                                                  *
.*  Function      : This program converts the screen painter file entries     *
.*                  from the old Doctor field numbers to the new Doctor field *
.*                  numbers                                                   *
.*  Author        : Steve Armstrong                                           *
.*  Modifications :                                                           *
.*                                                                            *
.******************************************************************************
.
.         Data File Includes.
.
          INC       STD001FD
.
          INC       SCRMASFD/INC
          INC       SCRPSTFD/INC
          INC       SCRSLTFD/INC
.
.         Program Variables
.
OLDNUM    FORM      5
NEWNUM    FORM      5
.
.         Program Constants
.
DOCFILE   INIT      "PATDOCFD"
NEWDOC    INIT      "PATDO1FD"
.
PRGID     INIT      "FXDO1SCR"
PRGNAM    INIT      "Convert Doctor Screen Painter Fields"
VERSION   INIT      "V12.01.00"
.
.******************************************************************************
.*        ML0000 - Program Logic Control                                      *
.******************************************************************************
.
ML0000    CALL      INIT0000                   * Initalise program.
.
ML1000    CALL      OPTN0000                   * Get option.
          COMPARE   ZERO,OPTION
          GOTO      ML9999 IF EQUAL
.
          CALL      CKMS0000                   * Check SCRMAS file.
          GOTO      ML1000
.
ML9999    CHAIN     PGM                        * Exit.
+
.******************************************************************************
.*        INIT0000 - Program initialisation                                   *
.******************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P54:24,"Opening scrmasaf";
          OPEN      SCRMASA1,"scrmasaf"           * Open screen master file.
.
          DISPLAY   *P62:24,"scrpstaf";
          OPEN      SCRPSTA1,"scrpstaf"           * Open field position file.
.
          DISPLAY   *P62:24,"scrsltaf";
          OPEN      SCRSLTA1,"scrsltaf"           * Open field selection file.
.
INIT9999  RETURN
+
.******************************************************************************
.*        OPTN0000 - Get program option.                                      *
.******************************************************************************
.
OPTN0000  DISPLAY    *P1:4,*EF,*P2:4,*V2LON,"0",*HOFF,". Exit.":
                     *P2:5,*V2LON,"1",*HOFF,". Run Conversion.":
                     *P2:7,"Select Option : ";
OPTN1000  KEYIN      *P18:7,*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9999 IF EQUAL
.
          BRANCH     OPTION,OPTN9999
.
          BEEP
          GOTO       OPTN1000
.
OPTN9999  RETURN
+
.******************************************************************************
.*        CKMS0000 - Check master screen file for screens using old Doctor    *
.*                   field numbers.                                           *
.******************************************************************************
.
CKMS0000  DISPLAY   *P1:24,*EL,*P20:24,*V2LON,"Program : ",*P30:24,"Screen : ":
                    *P55:24,"Item : "; 
.
          PACK      KEY10,SP10
          CALL      RSSCMA1
CKMS1000  CALL      RKSCMA1
          BRANCH    OVRCD,CKMS9999
.
          MATCH     DOCFILE,SCMAFPR                * Check if using old FD.
          GOTO      CKMS1000 IF NOT EQUAL
.
          DISPLAY   *P30:24,SCMAPID,*P50:24,SCMASID;
.
          CALL      UPPS0000                       * Update the position file.
          CALL      UPSL0000                       * Update the selection file.
          MOVE      NEWDOC,SCMAFPR                 * Update the FD used.
          CALL      UPSCMA1
.
          GOTO      CKMS1000
.
CKMS9999  RETURN
+
.******************************************************************************
.*        UPPS0000 - Update the screen positions file.                        *
.******************************************************************************
.
UPPS0000  PACK      KEY15,SCMAPID,SCMASID,SP20
          CALL      RSSCPS1
UPPS1000  CALL      RKSCPS1
          BRANCH    OVRCD,UPPS9999
.
          MATCH     SCPSPID,SCMAPID               * Check program ID.
          GOTO      UPPS9999 IF NOT EQUAL
.
          MATCH     SCPSSID,SCMASID               * Check screen ID.
          GOTO      UPPS9999 IF NOT EQUAL
.
          DISPLAY   *P62:24,SCPSITM
.
          MOVE      SCPSITM,OLDNUM
          CALL      GETN0000
          COMPARE   ZERO,NEWNUM                   * Check if number has changed
          GOTO      UPPS1000 IF EQUAL
.
          MOVE      NEWNUM,SCPSITM                * Update Item Number
          CALL      UPSCPS1
          GOTO      UPPS1000 
.
UPPS9999  RETURN
+
.******************************************************************************
.*        UPSL0000 - Update the screen selections file.                       *
.******************************************************************************
.
UPSL0000  PACK      KEY13,SCMAPID,SCMASID,SP20
          CALL      RSSCSL1
UPSL1000  CALL      RKSCSL1
          BRANCH    OVRCD,UPSL9999
.
          MATCH     SCSLPID,SCMAPID               * Check program ID.
          GOTO      UPSL9999 IF NOT EQUAL
.
          MATCH     SCSLSID,SCMASID               * Check screen ID.
          GOTO      UPSL9999 IF NOT EQUAL
.
          DISPLAY   *P62:24,SCSLITM
.
          MOVE      SCSLITM,OLDNUM
          CALL      GETN0000
          COMPARE   ZERO,NEWNUM                    * Check if number has changed
          GOTO      UPSL1000 IF EQUAL
.
          MOVE      NEWNUM,SCSLITM                * Update Item Number
          CALL      UPSCSL1
          GOTO      UPSL1000 
.
UPSL9999  RETURN
+
.******************************************************************************
.*        GETN0000 - Get the new Item number.                                 *
.******************************************************************************
.
GETN0000  MOVE      ZERO,NEWNUM
.
          COMPARE   TEN,OLDNUM
          GOTO      GETN9999 IF LESS
.
          IF        OLDNUM > NINE & OLDNUM < TWENTY1
            ASSIGN    (OLDNUM+ONE),NEWNUM
            GOTO      GETN9999
          ENDIF
.
          IF        OLDNUM > TWENTY
            ASSIGN    (OLDNUM+TWO),NEWNUM
            GOTO      GETN9999
          ENDIF
.
GETN9999  RETURN
+
.****************************************************************************
.*        I/O Includes                                                      *
.****************************************************************************
.
          INC       STD001IO
.
          INC       SCRMASIO/INC
          INC       SCRPSTIO/INC
          INC       SCRSLTIO/INC

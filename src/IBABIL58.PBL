.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBABIL58                                             *
.*                                                                            *
.*     FUNCTION:         BED FEE MASTER LISTING                               *
.*                                                                            *
.*     DATE WRITTEN:     01/02/1988                                           *
.*                                                                            *
.*     AUTHOR:           JENI TAN                                             *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V10.03.01 15/11/2012  Mike Laming   CAR 274278                      *
.*                  Add code to pad EDHFTYP input field in KEYHFT1 routine    *
.*        V10.02.01 28/03/2011 Peter Vela CAR 233034                          *
.*                  Added Contract ID displays                                *
.*        V10.01.03 23/02/2011 Peter Vela CAR 233034                          *
.*                  Removed new file functionality                            *
.*        V10.01.02 22/01/2011 Peter Vela CAR 233034                          *
.*                  Added functionality for Johns Hopkins Bed Fee Master      *
.*                  Listing                                                   *
.*        V10.01.01 30/11/10 J.Tan    CAR 233034                              *
.*                  Removed BFHDP - not used anymore                          *
.*        V9.12.01  21/09/09 J.Tan    CAR 204390                              *
.*                  Mods to old bed fees file                                 *
.*        V9.04.01  03/08/05 J.Tan    CAR 62750                               *
.*                  Removed redefined amount variables                        *
.*        V9.03.01  06/05/2003  Dean Cramer   CAR 38200                       *
.*                  Fix headings for screen option ranges                     *
.*        V5.08.03  08/12/2000  Caleb Sharman                                 *
.*                  Mods to use keyin routines for claim codes, health funds  *
.*                  & fund tables                                             *
.*        V5.08.02  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               * 
.*        V5.08.01  06/06/2000  Jill Habasque                                 *
.*                  Changed from key10 to key14 for the health fund file      *
.*                      W2.01     20/04/88 K.Peak                             *
.*                                Added extra bed types                       *
.*                      W2.02     03/05/88 K.Peak                             *
.*                                Use TCFORM6 instead of BFBTYPE              *
.*                      W4.03     19/01/89 K.Peak          SRF 5596           *
.*                                Five periods & daycase                      *
.*                      W4.04     17/02/89 K.Peak          SRF 3417           *
.*                                Total rate on report                        *
.*                      V5.01.01  07/07/89 K.Peak                             *
.*                                Changed Adm & U/R to 8 characters           *
.*                      V5.01.02  26/09/89 Graeme Williams SRF 102442         *
.*                                Fix H/F range option                        *
.*                      V5.01.03  29/03/90 J.Tan           SRF 104047         *
.*                                Mods to display H/F Table & Adm Type        *
.*                      V5.01.04  23/05/90 J.Tan           SRF 104901         *
.*                                Extend patfn1af file                        *
.*                      V5.01.14  28/06/90 Glen Hobby                         *
.*                                Added HDP Equivalent                        *
.*                                16/07/90 D.Di.Paola      SRF 105665         *
.*                                Added seperate page option for each H/F     *
.*                      V5.01.15  08/03/91 i chung         SRF 107866         *
.*                                Added Health Fund Table range parameters    *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       JHSBF1FD/INC
          INC       PATBFEFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PMSCIDFD/INC
          INC       PATFN1FD/INC
+
.
.  	WORK AREA
.
CODE      DIM       2
CODECL    INIT      "CL"
CODEA     INIT      "A "
CODEBT    INIT      "BT"
.
DIM10     DIM       10
DANS1     DIM       16
DANS2     DIM       16
DANS3     DIM       16
DANS4     DIM       17
DANS5     DIM       10 
DANS6     DIM       13
.
DEFRB1    INIT      "PRIVATE 1 "
DEFRB2    INIT      "SHARED  1 "
DEFRB3    INIT      "PRIVATE 2 "
DEFRB4    INIT      "SHARED  2 "
DEFRB5    INIT      "PRIVATE 3 "
DEFRB6    INIT      "SHARED  3 "
DEFRB7    INIT      "PRIVATE 4 "
DEFRB8    INIT      "SHARED  4 "
.
.
.
.       EXTRA VARIABLES
.
BFTOTAL   FORM      6.2   
CDESC     DIM       20
EDCLM     DIM       3
EDHF      DIM       6
EDHFTAB   DIM       8
EDHFTYP   DIM       3
EDCLMDS   DIM       8
EDHLFDS   DIM       8
EDTABDS   DIM       8
ENDCONT   DIM       6
FORM62    FORM      6.2
FRCONT    DIM       6
MHFUND    DIM       6
PAGEFLG   FORM      1
SAVFUND   DIM       6
STCLM     DIM       3
STHF      DIM       6
STHFTAB   DIM       8
STHFTYP   DIM       3
STITEM    DIM       2
STCLMDS   DIM       8 
STHLFDS   DIM       8
STTABDS   DIM       8
STRCONT   DIM       6
TOCONT    DIM       6
.
Z26       INIT      "ZZZZZZZZZZZZZZZZZZZZZZZZZZ"
.
PRGID     INIT      "IBABIL58"
PRGNAM    INIT      "Bed Fee Master Listing"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN9;*212,CFEETYP
.
          IF        CFEETYP=9
            DISPLAY   *P64:24,"jhsbfeaf";
            OPEN      JHSBFEA1,"jhsbfeaf"
          ELSE
            DISPLAY   *P64:24,"patbfeef";
            OPEN      PATBFEE1,"patbfeef"
          ENDIF
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pmscidaf";
          OPEN      PMSCIDA1,"pmscidaf"
.
.         START OF PROGRAM
.
.          Select Option
.
START     MOVE      ZERO,CPAGENO
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Code Sequence";
.
          IF        CFEETYP=9
            DISPLAY   *P1:6,*V2LON,TWO,*HOFF," = Code Sequence (New File)";
          ENDIF 
.
          DISPLAY   *P1:8,"Select Option :";
.
REKEY     KEYIN     *P17:8,*EL,"_",*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          IF        CFEETYP=9
            BRANCH    OPTION OF KEYCLM,KEYCLM
          ELSE
            BRANCH    OPTION OF KEYCLM
          ENDIF
          BEEP
          GOTO      REKEY
.
.         Keyin Claim Code range
.
KEYCLM    MOVE      SP3,STCLM
          DISPLAY   *P1:11,*EF,"Enter Starting Claim Code  : "
.
          PACK      TDESC,SP20,SP20,SP20,SP20
          MOVE      TEN1,EVERT
          MOVE      THIRTY,ECOL
          MOVE      ZERO,CKYIMAND
          MOVE      CODECL,CODE
          CALL      PATCODKY
          MOVE      ACODE,STCLM
          ENDSET    STCLM
          APPEND    SP3,STCLM
          RESET     STCLM
.
          MATCH     SP6,STCLM
          IF        @EQUAL
            MOVE      "Start",STCLMDS
          ELSE
            MOVE      STCLM,STCLMDS
          ENDIF
          DISPLAY   *P30:11,*EL,*V2LON,STCLMDS;
.
KEYCLM1   MOVE      SP3,EDCLM
          DISPLAY   *P1:12,*EL,"Enter Ending   Claim Code  : "
.
          PACK      TDESC,SP20,SP20,SP20,SP20
          MOVE      TEN2,EVERT
          MOVE      THIRTY,ECOL
          MOVE      ZERO,CKYIMAND
          MOVE      CODECL,CODE
          CALL      PATCODKY
          MOVE      ACODE,EDCLM
          MATCH     SP3,EDCLM
          GOTO      CLMCHK IF NOT EQUAL
.
          MOVE      "zzz",EDCLM
.
CLMCHK    ENDSET    EDCLM
          APPEND    SP3,EDCLM
          RESET     EDCLM
.
          MATCH     "zzz",EDCLM
          IF        @EQUAL
            MOVE      "Finish",EDCLMDS
          ELSE
            MOVE      EDCLM,EDCLMDS
          ENDIF
.
          DISPLAY   *P30:12,*EL,*V2LON,EDCLMDS
.
          MATCH     STCLM,EDCLM
          GOTO      KEYHF IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Ending Claim Code must be >= Starting Claim Code.  ";
          CALL      HITENTER
          GOTO      KEYCLM1
.
.         Keyin Health Fund range
.
KEYHF     MOVE      SP6,STHF
          DISPLAY   *P1:14,*EL,"Enter Starting Health Fund : "
.
          PACK      HFNAME,SP20,SP20,SP20,SP20
          MOVE      TEN4,EVERT
          MOVE      THIRTY,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATFNDKY
          MOVE      HCODE,STHF
          ENDSET    STHF
          APPEND    SP6,STHF
          RESET     STHF
.
          MATCH     SP6,STHF
          IF        @EQUAL
            MOVE      "Start",STHLFDS
          ELSE
            MOVE      STHF,STHLFDS
          ENDIF
.
          DISPLAY   *P30:14,*EL,*V2LON,STHLFDS
.
KEYHF1    MOVE      SP6,EDHF
          DISPLAY   *P1:15,*EL,"Enter Ending   Health Fund : "
.
          PACK      HFNAME,SP20,SP20,SP20,SP20
          MOVE      TEN5,EVERT
          MOVE      THIRTY,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATFNDKY
          MOVE      HCODE,EDHF
          MATCH     SP6,EDHF
          GOTO      HFCHK IF NOT EQUAL
.
          MOVE      "zzzzzz",EDHF
.
HFCHK     ENDSET    EDHF
          APPEND    SP6,EDHF
          RESET     EDHF
.
          MATCH     "zzzzzz",EDHF
          IF        @EQUAL
            MOVE      "Finish",EDHLFDS
          ELSE 
            MOVE      EDHF,EDHLFDS
          ENDIF
.
          DISPLAY   *P30:15,*EL,*V2LON,EDHLFDS
.
          MATCH     STHF,EDHF
          GOTO      KEYHFT IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Ending Health Fund must be >= Starting Health Fund.  ";
          CALL      HITENTER
          GOTO      KEYHF1
.
.         Keyin Health Fund Table range
.
KEYHFT    MATCH     STHF,EDHF
          IF   !@EQUAL         
            MOVE      "Start",STTABDS
            MOVE      "Finish",EDTABDS
            GOTO      PAGEOPT 
          ENDIF
.
          IF        CFEETYP=9
.
            MOVE      SP8,STHFTAB
            DISPLAY   *P1:17,*EL,"Enter Starting Fund Table  : "
.
            PACK      HFNAME,SP20,SP20,SP20,SP20
            MOVE      TEN7,EVERT
            MOVE      THIRTY,ECOL
            MOVE      ZERO,CKYIMAND
            MOVE      STHF,MHFUND
            CALL      PATTBLKY
            MOVE      HFTABL,STHFTAB
            ENDSET    STHFTAB
            APPEND    SP8,STHFTAB
            RESET     STHFTAB
.
            MATCH     SP8,STHFTAB
            IF        @EQUAL
              MOVE      "Start",STTABDS
            ELSE
              MOVE      STHFTAB,STTABDS
            ENDIF
.
          ELSE
.
            MOVE      SP8,STHFTYP
            KEYIN     *P1:17,*EL,"Enter Starting Fund Table Type : ",STHFTYP
.
            ENDSET    STHFTYP
            APPEND    SP8,STHFTYP
            RESET     STHFTYP
.
            MATCH     SP8,STHFTYP
            IF        @EQUAL
              MOVE      "Start",STTABDS
            ELSE
              MOVE      STHFTYP,STTABDS
            ENDIF
.
          ENDIF
.
          DISPLAY   *P34:17,*EL,*V2LON,STTABDS
.
KEYHFT1   IF        CFEETYP=9
.
            MOVE      SP8,EDHFTAB;
            DISPLAY   *P1:18,*EL,"Enter Ending   Fund Table  : "
.
            PACK      HFNAME,SP20,SP20,SP20,SP20
            MOVE      TEN8,EVERT
            MOVE      THIRTY,ECOL
            MOVE      ZERO,CKYIMAND
            MOVE      STHF,MHFUND
            CALL      PATTBLKY
            MOVE      HFTABL,EDHFTAB
            MATCH     EDHFTAB,SP8
            GOTO      HFTCHK IF NOT EQUAL
.
            MOVE      "zzzzzzzz",EDHFTAB
.
          ELSE
.
            MOVE      SP8,EDHFTYP;
            KEYIN     *P1:18,*EL,"Enter Ending Fund Table Type : ",EDHFTYP
.
            ENDSET    EDHFTYP                                         *I-274278
            APPEND    SP8,EDHFTYP                                     *I-274278
            RESET     EDHFTYP                                         *I-274278
.
            MATCH     EDHFTYP,SP8
            GOTO      HFTCHK IF NOT EQUAL
.
            MOVE      "zzzzzzzz",EDHFTYP
.
          ENDIF
.
HFTCHK    IF        CFEETYP=9
.
            ENDSET    EDHFTAB
            APPEND    SP8,EDHFTAB
            RESET     EDHFTAB
.
            MATCH     "zzzzzzzz",EDHFTAB
            IF        @EQUAL
              MOVE      "Finish",EDTABDS
            ELSE
              MOVE      EDHFTAB,EDTABDS
            ENDIF
.
            DISPLAY   *P34:18,*EL,*V2LON,EDTABDS
.
            MATCH     STHFTAB,EDHFTAB
            GOTO      PAGEOPT IF NOT LESS
.
            DISPLAY   *P1:24,*EL,*B,*V2LON:
                      "Ending Fund Table must be >= Starting Fund Table.  ";
.
          ELSE
.
            ENDSET    EDHFTYP
            APPEND    SP8,EDHFTYP
            RESET     EDHFTYP
.
            MATCH     "zzzzzzzz",EDHFTYP
            IF        @EQUAL
              MOVE      "Finish",EDTABDS
            ELSE
              MOVE      EDHFTYP,EDTABDS
            ENDIF
.
            DISPLAY   *P34:18,*EL,*V2LON,EDTABDS
.
            MATCH     STHFTYP,EDHFTYP
            GOTO      PAGEOPT IF NOT LESS
.
            DISPLAY   *P1:24,*EL,*B,*V2LON:
                      "Ending Fund Table must be >= Starting Fund Table.  ";
.
          ENDIF
.
          CALL      HITENTER
          GOTO      KEYHFT1
.
.------- ask for seperate papge for each health fund ----
.
PAGEOPT   MOVE      FALSE,PAGEFLG
          MATCH     STHF,EDHF               * if same health fund, then ignore
          GOTO      PAGEOPT5 IF EQUAL          * separate page question....
.
PAGEOPT1  MOVE      SP1,ANS
          DISPLAY   *P1:20,*EL,"Separate page for each Health Fund (Y/N) ? ";
.
PAGEOPT2  KEYIN     *P46:20,*EL,"_",*P46:20,*V2LON,ANS;
          REP       UPPLOW,ANS
          RESET     ANS
          GOTO      PAGEOPT3 IF NOT EOS
          DISPLAY   *P46:20,*V2LON,"N";
          GOTO      PAGEOPT5
.
PAGEOPT3  MATCH     ANSY,ANS
          GOTO      PAGEOPT4 IF EQUAL
          MATCH     ANSN,ANS
          GOTO      PAGEOPT5 IF EQUAL
          BEEP 
          GOTO      PAGEOPT2
.
PAGEOPT4  MOVE      TRUE,PAGEFLG
.
PAGEOPT5  IF        CFEETYP<>9
            CALL      KEYN0000
          ENDIF
.
.       Check if Ok to continue
.
CHKOK     MOVE      SP1,ANS
          DISPLAY   *P1:24,*EL,"Ok to continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,"/",*V2LON,"C",*HOFF,") ? ";
.
CHKOK1    KEYIN     *P26:24,*EL,"_",*P26:24,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     "Y" TO ANS
          GOTO      CHKOPT IF EQUAL
.
          MATCH     "N" TO ANS
          GOTO      KEYCLM IF EQUAL
.
          MATCH     "C" TO ANS
          GOTO      START IF EQUAL
          BEEP
          GOTO      CHKOK1
.
.
CHKOPT    IF        CFEETYP=9
            BRANCH    OPTION OF NORCOD,NEWCOD
          ELSE
            BRANCH    OPTION OF NORCOD
          ENDIF
.
NOCODE    DISPLAY   *P1:24,*EL,*V2LON,*B,*P30:24:
                    "** No data on file **",*W3,*P1:24,*EL;
          GOTO      START
.
NORCOD    IF        CFEETYP=9
            OPEN      JHSBFEA1,"jhsbfeaf"
          ELSE
            OPEN      PATBFEE1,"patbfeef"
          ENDIF
          MOVE      SP10,DIM10
          GOTO      CLCODE
.
NEWCOD    TRAP      NONEW IF IO
          OPEN      JHSBF1A1,"newbfeaf"
          TRAPCLR   IO
          MOVE      "- New File",DIM10
.
.         PRINT VIA CLAIM CODE 
.
CLCODE    MOVE      SP6,SAVFUND
.
          IF        CFEETYP=9
.
            PACK      KEY26 WITH STCLM,STHF,SP70
            IF        OPTION=1
              CALL      RSJHBFE1
            ELSE
              CALL      RSJHB1A1
            ENDIF
.
          ELSE
.
            PACK      KEY27 WITH STCLM,STHF,SP30
            CALL      RDSBFEE1       * Current file
.
          ENDIF
.
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P29:24,*V2LON,"** Generating Report **",*W2;
.
          DISPLAY   *P1:24,*EL,"Claim Code:",*P25:24,"H.F.:":
                    *P42:24,"Table:",*P59:24,"Adm.Type:";
.
NEXTA     IF        OPTION=1
            IF        CFEETYP=9
              CALL      RKJHBFE1
            ELSE
              CALL      RDKBFEE1       * Current file
            ENDIF
          ELSE
            CALL      RKJHB1A1
          ENDIF
          BRANCH    OVRCD OF ENDP 
.
          MATCH     BFCLM,EDCLM
          GOTO      ENDP IF LESS
.
          MATCH     STHF,BFHFUND
          GOTO      SKP10 IF LESS
.
          MATCH     BFHFUND,EDHF
          GOTO      SKP20 IF LESS
          GOTO      NXT10
.
SKP10     IF        CFEETYP=9
.
            PACK      KEY26,BFCLM,STHF,SP70
            IF        OPTION=1
              CALL      RSJHBFE1
            ELSE
              CALL      RSJHB1A1
            ENDIF
.
          ELSE
.
            PACK      KEY27,BFCLM,STHF,SP30
            CALL      RDSBFEE1
.
          ENDIF
.
          GOTO      NEXTA
.
SKP20     IF        CFEETYP=9
.
            PACK      KEY26,BFCLM,Z70
            IF        OPTION=1
              CALL      RSJHBFE1
            ELSE
              CALL      RSJHB1A1
            ENDIF
.
          ELSE
.
            PACK      KEY27,BFCLM,Z70
            CALL      RDSBFEE1
.
          ENDIF
.
          GOTO      NEXTA
.
NXT10     IF        CFEETYP=9
.
            MATCH     STHFTAB,BFHFTAB
            GOTO      SKP30 IF LESS
.
            MATCH     BFHFTAB,EDHFTAB
            GOTO      SKP20 IF LESS
.
          ELSE
.
            MATCH     STHFTYP,PTBFHTYP
            GOTO      SKP30 IF LESS
.
            MATCH     PTBFHTYP,EDHFTYP
            GOTO      SKP20 IF LESS
          ENDIF
.
          GOTO      NXT20
.
SKP30     IF        CFEETYP=9
.
            PACK      KEY26,BFCLM,BFHFUND,STHFTAB,SP70
            IF        OPTION=1
              CALL      RSJHBFE1
            ELSE
              CALL      RSJHB1A1
            ENDIF
.
          ELSE
.
            PACK      KEY27,BFCLM,BFHFUND,STHFTYP,SP30
            CALL      RDSBFEE1
.
          ENDIF
.
          GOTO      NEXTA
.
NXT20     IF        CFEETYP<>9
            MATCH     SP70,STRCONT
            IF        !@EQUAL & !@EOS
              MATCH     STRCONT,PTBFCNID
              GOTO      NEXTA IF LESS          
            ENDIF
.
            MATCH     SP70,ENDCONT
            IF        !@EQUAL & !@EOS
              MATCH     PTBFCNID,ENDCONT
              GOTO      NEXTA IF LESS
            ENDIF
          ENDIF
.
          IF        CFEETYP=9
.
            DISPLAY   *P13:24,*V2LON,BFCLM,*P31:24,BFHFUND:
                      *P49:24,BFHFTAB,*P69:24,BFATYPE;
.
          ELSE
.
            DISPLAY   *P13:24,*V2LON,BFCLM,*P31:24,BFHFUND:
                      *P49:24,PTBFHTYP,*P69:24,BFATYPE;
.
          ENDIF
.
          COMPARE   ONE,PAGEFLG                 * pageflag = "1", Yes
          GOTO      NXT30 IF NOT EQUAL
          MATCH     BFHFUND,SAVFUND             * check for same fund
          CALL      HEAD IF NOT EQUAL
NXT30     CALL      DISPDA
          MOVE      BFHFUND,SAVFUND             * get new fund
          GOTO      NEXTA
.
NONEW     DISPLAY   *P1:24,*EL,*V2LON,*B,"New File does not exist.  ";
          CALL      HITENTER
          GOTO      START
.
.         OVER Condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOCODE IF EQUAL
.
          CALL      PAGEND
          PRINT     *N,*N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      START
+
.
.         PRINT ACTUAL DATA 
.
DISPDA    COMPARE   "56",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      SP20,DANS1
          MOVE      SP20,DANS2
          MOVE      SP20,DANS3
          MOVE      SP20,DANS4
          MOVE      SP10,DANS5
          MOVE      SP20,DANS6
.
          PACK      KEY5 WITH CODECL,BFCLM
          CALL      RDCODE1
          MOVE      TDESC,DANS1
.
          PACK      KEY14 WITH BFHFUND,ZERO,ZERO,ZERO,ZERO,SP10
          CALL      RDFUND1
          BRANCH    OVRCD OF PRT10
          MOVE      HFNAME,DANS2
.
PRT10     PACK      KEY5 WITH CODEBT,BFBTYPE
          CALL      RDCODE1
          MOVE      TDESC,DANS5
.
          MOVE      BFDESC,DANS6
.
          MOVE      BFPAT,BFTOTAL
          ADD       BFHF,BFTOTAL
.
          IF        CFEETYP=9
.
            PRINT     *1,"|",DANS1,*18,"|",DANS2,*35,"|",BFHFTAB:
                    *44,"| ",BFATYPE,*49,"|",BFBTYPE,*54,DANS5,*64,"|",DANS6;
.
          ELSE
.
            PRINT     *1,"|",DANS1,*18,"|",DANS2,*35,"|",PTBFHTYP:
                      *44,"| ",BFATYPE,*49,"|",BFBTYPE,*54,DANS5,*64,"|",DANS6;
          ENDIF
.
          MOVE      BFPAT,FORM62
          PRINT     *89,"| ",BFENDDY,*95,"|$",FORM62;
.
          MOVE      BFHF,FORM62
          PRINT     *106,"|$",FORM62:
                    *117,"|$",BFTOTAL,*128,"|",BFNINV,*132,"|":
                    *N,*1,"|",PTBFCNID:
                    *N,*1,"|------------------------------------":
                    "----------------------------------------------":
                    "------------------------------------------------|"
.
          MOVE      ZERO,BFTOTAL
          ADD       THREE,CLNO
          RETURN
+
.
.         Last Line on the page
.
PAGEND    PRINT     *1,"*------------------------------------":
                    "----------------------------------------------":
                    "------------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.         Headings for Output
.
HEAD      ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          COMPARE   ONE,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          PRINT     *F,*1,PRGID,*39,CNAME,*118,"DATE : ",CDATE:
                    *N,*1,VERSION,*39,"*** ",PRGNAM," ***":
                    "   (Claim Code Sequence)   ",DIM10,*118,"TIME : ",CTIMEIS:
                    *N,*118,"PAGE : ",*130,CPAGENO:
                    *N,*39,"Claim Code  Range : ",STCLMDS,"    to ",EDCLMDS:
                    *N,*39,"Health Fund Range : ",STHLFDS,"    to ",EDHLFDS:
                    *N,*39,"Fund Table  Range : ",STTABDS,"    to ",EDTABDS:
                    *N,*39,"Contract ID Range : ",FRCONT,"      to ",TOCONT:
                    *N,*N,*1,"*------------------------------------":
                    "----------------------------------------------":
                    "------------------------------------------------*":
                    *N,*1,"| Claim Code",*18,"|",*35,"|",*44,"| Adm",*49,"|",*64,"|";
.
          PRINT     *89,"|Last",*95,"| Patient",*106,"| Rebate",*117,"|":
                    *128,"|No.",*132,"|":
                    *N,*1,"|Contract ID",*18,"| Health Fund",*35,"|Table":
                    *44,"|Type",*49,"| Bed Type",*64,"| Description";
.
          PRINT     *89,"| Day",*95,"| Portion",*106,"| Portion":
                    *117,"|  Total",*128,"|Inv",*132,"|":
                    *N,*1,"*------------------------------------":
                    "----------------------------------------------":
                    "------------------------------------------------*"
.
          MOVE      TEN2,CLNO
          RETURN
+
ENDIT    CHAIN      PGM
         STOP
.
.
.**************************************************************************
.                               KEYN0000                                  *
.  Keyin selection details for contract Id                                *
.**************************************************************************
KEYN0000  UNPACK    SP70,FRCONT,TOCONT,STRCONT,ENDCONT

          DISPLAY   *P1:22,*EF:
                    *P5:22,"Contract Id From : ":
                    *P5:23,"Contract Id To   : ";
.
. ------  keyin the contract Id
.
          KEYIN     *P24:22,FRCONT
KEYN1500  PACK      FRCONT,FRCONT,SP70
          MOVE      FRCONT,STRCONT
.
          MATCH     SP70,FRCONT
          GOTO      KEYN1000 IF EQUAL
.
          PACK      KEY6,FRCONT,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,KEYN9000
.
          DISPLAY   *P31:22,PMCIDESC
          GOTO      KEYN5000
.
KEYN1000  DISPLAY   *P31:22,*V2LON,"Start",*HOFF;
          MOVE      "Start",FRCONT
          MOVE      ZERO,EXIT
.
KEYN5000  KEYIN     *P24:23,TOCONT
          PACK      TOCONT,TOCONT,SP70
          MOVE      TOCONT,ENDCONT
.
          MATCH     SP70,TOCONT
          GOTO      KEYN6000 IF EQUAL
.
          PACK      KEY6,TOCONT,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,KEYN9000
.
          DISPLAY   *P31:23,PMCIDESC
          GOTO      KEYN7000
.
KEYN6000  DISPLAY   *P31:23,*V2LON,"End",*HOFF;
          MOVE      "End  ",TOCONT
          MOVE      ZERO,EXIT
          GOTO      KEYN9999
.
KEYN7000  PACK      STRCONT,STRCONT,SP10
          PACK      ENDCONT,ENDCONT,SP10
          MATCH     STRCONT,ENDCONT
          GOTO      KEYN9999 IF NOT LESS
.
          DISPLAY   *P1:24,"To contract must be greater than from contract. ";
          CALL      HITENTER
          GOTO      KEYN5000
.
KEYN9000  MOVE      ONE,EXIT
.
KEYN9999  RETURN

+
.==============================================================================
.
         INCLUDE    STD001IO
.
         INC       PATCODKY
         INC       PATFNDKY
         INC       PATTBLKY
         INC       PATBFEIO/INC
         INC       PATCODIO/INC
         INC       PMSCIDIO/INC
         INC       PATFN1IO/INC
         INC       JHSBF1IO/INC

.----------------------------------------------------------------------
. Program       : APSWEB01
.
. Function      : Creditor/Supplier Maintenance 
.
. Modifications : 
.----------------------------------------------------------------------
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
. V10.03.01 16/07/2013  Jill Parkinson CAR 287923
.           Added include of PATCONFD
.----------------------------------------------------------------------
. V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
.----------------------------------------------------------------------
. V9.04.02  16.02.2005 Lina Vo        CAR 56406
.           Fixed Supplier keyword search to only pick up correct suppliers
. V9.04.01  07.02.2005 Lina Vo        CAR 56406
.           Move "&nbsp;" to APMASCON if blank
. V9.03.01  08.10.2003 Ebon Clements  CAR 43994
.           Recompiled for APSMASTM
.----------------------------------------------------------------------
. V9.02.02  22.11.2001 Ebon Clements
.           Corrected read of APSMASFD in search
. V9.02.01  21.09.2001 Ebon Clements
.           Added all APSMASFD fields to report 1
. V9.00.00  14.05.2001 HPS
.           Created Program
.----------------------------------------------------------------------
.
          INC       IBAWEBDF
.
          INC       PATCONFD/INC
          INC       APSMASTD/INC            * CGI variables
          INC       APSMASFD/INC            * Supplier master details
          INC       APSCKIFD/INC            * Creditors Keywords Index
          INC       APSMXRFD/INC            * C/S Cross Reference Table
          INC       APSSNTFD/INC            * Accounts Payable Supplier Notes
          INC       APSCNTFD/INC            * Accounts Payable Creditors Notes
          INC       APSIMSFD/INC            * Accounts Payable Transaction hed.
          INC       APSPPDFD/INC            * Accounts Payable Term Codes
          INC       APSCTSFD/INC            * Accounts Payable Creditor Types
          INC       FMSCODFD/INC            * Finance Category Codes
.
.----------------------------------------------------------------------
. CGI Parameter
.----------------------------------------------------------------------
NEXTPAGE  DIM       1                       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1                       * Update Type
STARTKEY  DIM       10                      * Starting Key
SAVKEY20  DIM       20                      * Key for keyword index
SAVKEY5   DIM       5                       * Key for keyword index 
LNK2PRT   FORM      1

.---------------------------------------------------------------------
PRGID     INIT      "APSWEB01"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Creditor/Supplier Maintenance Server"
.---------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      APSMA000        * Coding Summary Maintenance
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.--------------------------------------------------------
.  Process Fields in Body of HTML Template
.--------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12  
          GOTO      PROFLD99
.
PROFLD11  CALL      APSM0000                * APSMASTM include
          GOTO      PROFLD99
.
PROFLD12  CALL      SKWS0000                * Creditor code maintenance
          GOTO      PROFLD99
.
PROFLD99  RETURN
.---------------------------------------------------------
.  this will cater to reportno=1 and fldsetno=2
.---------------------------------------------------------
SKWS0000  BRANCH    FLDITMNO,SKWS0100,SKWS0200,SKWS0300,SKWS0400:
                             SKWS0500,SKWS0600
          GOTO      SKWS9999
.
SKWS0100  MOVE      TWO,LNK2PRT             * Link to Window 
          CALL      SAPCKA00                * Display Search Results
          GOTO      SKWS9999
.
SKWS0200  CALL      NEXTLINK                * Show the next set of results
          GOTO      SKWS9999
.
SKWS0300  CALL      PREVLINK                * Show the Prev set of results
          GOTO      SKWS9999
.
SKWS0400  CALL      KEYW0000                * Display the keyword processed
          GOTO      SKWS9999
.
SKWS0500  MOVE      ZERO,LNK2PRT            * Link to alternate Program
          CALL      SAPCKA00                * Display Search Results 
          GOTO      SKWS9999 
.
SKWS0600  MOVE      ONE,LNK2PRT             * Link to Parent
          CALL      SAPCKA00                * Display Search Results
          GOTO      SKWS9999 
.
SKWS9999  RETURN
.---------------------------------------------------------
.  Search Result - Display Search Results
.---------------------------------------------------------
SAPCKA00  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell>Supplier</td>":
                               "<td class=HeadingCell>Address</td>":
                               "<td class=HeadingCell>Phone Number</td>":
                               "<td class=HeadingCell>Contact</td>":
                             "</tr>"
.
          CALL      GETWRD00            * This routine Splits the KEYWORDS field
.                                           * into KEYWRD01 to KEYWRD10
.                                           * get the prgname,reportno & templte
          IF        LNK2PRT<>1
            UNPACK    DIM127,D1,LINKPRG,DIM127
            UNPACK    DIM127,D1,LINKREP,DIM127
            UNPACK    DIM127,D1,LINKTEM,DIM127
          ENDIF
.
          MATCH     SP70,KEYWORDS
          GOTO      SAPCKA99 IF EQUAL
.
          IF        NORECORD=0
            MOVE      "10",NORECORD         * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.  
          STRIP     KEYWRD01
          PACK      KEY20,KEYWRD01,SP70
          CALL      RSAPCK2
SAPCKA10  CALL      RKAPCK2
          BRANCH    OVRCD,SAPCKA90
.
          CALL      SAPCKB00
          BRANCH    OVRCD,SAPCKA10,SAPCKA90
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SAPCKA10
          ENDIF
.
          PACK      KEY5,APCKCRD,SP70
          CALL      RDAPMA1
          BRANCH    OVRCD,SAPCKA10
.
          REP       " +",APMACRD
.
          MATCH     SP70,APMASCON
          IF        @EQUAL
            MOVE      "&nbsp;",APMASCON
          ENDIF
.
          BRANCH    LNK2PRT,SAPCKA20,SAPCKA30
.
.         if LNK2PRT =1
          WRITE     HTMLFILE;"<tr valigh=top>":
                              "<td nowrap><a href=",LINKPRG,".pbl?":
                              "&reportno=",LINKREP:
                              "&template=",LINKTEM:
                              "&apmas001=",APMACRD:
                              "');#">":
                              APMASN1,"<br>":
                              APMASN2,"<br>(",APMACRD,")</a></td>":
                              "<td>",APMASA1,"<BR>",APMASA2,"<br>":
                              APMASA3,"<br>",APMACPC,"</td>":
                              "<td>",APMASTEL,"<br>",APMASFAX,"</td>":
                              "<td>",APMASCON,"</td></tr>"
          REP       "+ ",APMACRD
.
          GOTO      SAPCKA80   
. 
.                if LNK2PRT =1
SAPCKA20  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td nowrap><a href=#"":
                              "javascript:updateParent('",APMACRD:
                              "','",APMASN1,"')#">": 
                              APMASN1,"<br>":
                              APMASN2,"<br>(",APMACRD,")</a></td>":
                              "<td>",APMASA1,"<BR>",APMASA2,"<br>":
                              APMASA3,"<br>",APMACPC,"</td>":
                              "<td>",APMASTEL,"<br>",APMASFAX,"</td>":
                              "<td>",APMASCON,"</td></tr>"
          REP       "+ ",APMACRD
.  
          GOTO      SAPCKA80
. 
.                if LNK2PRT =2
SAPCKA30  CALL      DSHR0000                * Display table records
          GOTO      SAPCKA80 
.
SAPCKA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SAPCKA10 IF NOT EQUAL
.
SAPCKA90   
.
SAPCKA99  RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
SAPCKB00  MATCH     KEYWRD01,APCKKWD
          GOTO      SAPCKB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY20,APCKKWD,APCKCRD
            CALL      SAPCKC00
            BRANCH    EXIT,SAPCKB91
            MOVE      SAVKEY20,KEY20
            CALL      RDAPCK2
          ENDIF
.
SAPCKB90  MOVE      ZERO,OVRCD
          GOTO      SAPCKB99
.
SAPCKB91  MOVE      ONE,OVRCD
          GOTO      SAPCKB99
.
SAPCKB92  MOVE      TWO,OVRCD
          GOTO      SAPCKB99
.
SAPCKB99  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
SAPCKC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY5,APCKCRD,SP70
.
SAPCKC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SAPCKC99 IF EQUAL       * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY20,APCKCRD,KEYWRDXX
          CALL      RDAPCK1                 * Check for Exact Match
          COMPARE   ZERO,OVRCD              * Ok so Continue Loop
          GOTO      SAPCKC10 IF EQUAL
.
          CALL      RKAPCK1                 * Not Exact so Read Next Record
          BRANCH    OVRCD,SAPCKC90          * No Match So Exit No Match
          PACK      KEY5,APCKCRD,SP70
          MATCH     SAVKEY5,KEY5            * Match Key Fields
          GOTO      SAPCKC90 IF NOT EQUAL   * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     APCKKWD,KEYWRDXX        * Check Key Word
          GOTO      SAPCKC90 IF NOT EQUAL   * No Match So Exit
          GOTO      SAPCKC10                * Check Next Search Word
.
SAPCKC90  MOVE      ONE,EXIT
.
SAPCKC99  RETURN
.
.--------------------------------------------------------
.Display Serach Result
.--------------------------------------------------------
DSHR0000  MATCH     SP20,APMASCON
          IF        @EQUAL
            MOVE    "&nbsp",APMASCON
          ENDIF
.
          WRITE     HTMLFILE;"<tr valign=top>":
                             "<td nowrap><a href=#"javascript:ShowDetail('":
                              LINKPRG,".pbl?":
                              "&reportno=",LINKREP:
                              "&template=",LINKTEM:
                              "&apmas001=",APMACRD:
                              "');#">":
                              APMASN1,"<br>":
                              APMASN2,"<br>(",APMACRD,")</a></td>":
                              "<td>",APMASA1,"<BR>",APMASA2,"<br>":
                              APMASA3,"<br>",APMACPC,"</td>":
                              "<td>",APMASTEL,"<br>",APMASFAX,"</td>":
                              "<td>",APMASCON,"</td></tr>"
.
          REP       "+ ",APMACRD
. 
DSHR9999  RETURN
.---------------------------------------------------------
.     Key Word Display - Display the keyword processed
.---------------------------------------------------------
KEYW0000  WRITE   HTMLFILE;KEYWORDS
.
KEYW9999  RETURN
.---------------------------------------------------------
.     Displays the next set of data    
.---------------------------------------------------------
NEXTLINK  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
.
          WRITE     HTMLFILE;"apsweb01.pbl":
                             "?reportno=",REPORTNO:
                             "&template=01":
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&keywords=",KEYWORDS;
.
          RETURN
.---------------------------------------------------------
.     Displays the previous set of data
.---------------------------------------------------------
PREVLINK  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3 < 0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
.
          WRITE     HTMLFILE;"apsweb01.pbl":
                             "?reportno=",REPORTNO:
                             "&template=01":
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&keywords=",KEYWORDS;
.
          RETURN
.---------------------------------------------------------
.  Set Parameters - Read the parameter comming from HTML
.---------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.        
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "keywords=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEYWORDS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "apmas",QRYNAME
          IF        @EQUAL
            CALL      STMAS000
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.----------------------------------------------------------
.   Clear the parameter required
.----------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,TEMPLATE
          MOVE      ZERO,NORECORD
          MOVE      ZERO,STARTNUM
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,STARTKEY
          MOVE      SP70,KEYWORDS
.
          CALL      CLAPMS00
.
CLRPAR99  RETURN
.----------------------------------------------------------
.       Open the files required
.----------------------------------------------------------
INIT0000  OPEN      APSCKIA1,"apsckiaf"     * Open Cr. keyword index 1
          OPEN      APSMASA1,"apsmasaf"     *
          OPEN      APSCKIA2,"apsckiaf"     * Open Cr. Keyword Index 2
          OPEN      APSCNTA1,"apscntaf"
          OPEN      APSSNTA1,"apssntaf"
          OPEN      APSAUDMA,"apsaudma"
          OPEN      APSMXRA2,"apsmxraf"
          OPEN      APSIMSA2,"apsimsaf"
          OPEN      APSPPDA1,"apsppdaf"
          OPEN      APSCTSA1,"apsctsaf"
          OPEN      FMSCODA1,"fmscodaf"
.
INIT9999  RETURN
.----------------------------------------------------------
. this routine is called when you need to either display
. update or delete a Supplier/Creditor
.----------------------------------------------------------
APSMA000  RESET     UPDTTYPE
          MATCH     SP70,UPDTTYPE
          GOTO      APSMA400 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add Creditor/Supplier
          GOTO      APSMA100 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update Creditor/Supplier
          GOTO      APSMA200 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete Creditor/Supplier
          GOTO      APSMA300 IF EQUAL
.
.      ************ ADD CREDITOR/SUPPLIER ****************
.
APSMA100  CALL      CLRAPS00
          PACK      KEY5,APMAS001,SP70
          CALL      RDAPMA1
          IF        OVRCD=0
            GOTO      APSMA920              * Error if Supplier code exists
          ENDIF
.
APSMA110  CALL      SAVAPS00
.
          PACK      KEY5,APMAS001,SP70
          CALL      WRAPMA1                 * write to Creditor/Supplier table
          MOVE      "A",KEY1                * Added Record
          CALL      AUDT0000                * write to the Audit Table
.
.      write Creditor name to C/S Cross Reference Table
.
          MOVE      "1",APMXTYP            
          MOVE      SP70,APMXSPA            * Spare Variables
          MOVE      APMACN1,APMXREF        * Cross Reference Field 
          MOVE      APMAS001,APMXCRD
          PACK      KEY35,APMAS001,APMXREF,SP70
          CALL      WRAPMX2
.
.      write notes to Suppliers Notes Table
.
          MOVE      SP70,APSNDAT
          MOVE      SP70,APSNSPA
          MOVE      ZERO,APSNLIN           
          MOVE      APMAS001,APSNCRD
          PACK      KEY8,APMAS001,APSNLIN
          CALL      WRAPSN1
.
.      write notes to Creditors Notes Table
.
          MOVE      SP70,APCNDAT
          MOVE      SP70,APCNSPA
          MOVE      ZERO,APCNLIN           
          MOVE      APMAS001,APCNCRD
          PACK      KEY8,APMAS001,APCNLIN
          CALL      WRAPCN1
.
          MOVE      APMAS001,APMACRD
          PROC      APSCKIUP    * ADD/UPDATE/DELETE C/S Key word Search table
.
          MOVE      ZERO,EXIT
          GOTO      APSMA999
.
.      ************ UPDATE CREDITOR/SUPPLIER ****************
.
APSMA200  PACK      KEY5,APMAS001,SP70
          CALL      RDAPMA1
          BRANCH    OVRCD,APSMA930 
.
APSMA210  CALL      SAVAPS00                * get cgi parameters
.
          MOVE      "B",KEY1                * Added Record
          CALL      AUDT0000                * wirte to audit before change
          CALL      UPAPMA1                 * Update the C/S Table
          MOVE      "C",KEY1                * Added Record
          CALL      AUDT0000                * wirte to audit after change
.
          CALL      DCRT0000                * Del rec from cred/supp notes tab. 
.
.      write Creditor name to C/S Cross Reference Table
.
          MOVE      "1",APMXTYP             * C/S 
          MOVE      SP70,APMXSPA            * Spare Variables
          MOVE      APMACN1,APMXREF            * Cross Reference Field 
          MOVE      APMAS001,APMXCRD
          PACK      KEY35,APMAS001,APMXREF,SP70
          CALL      WRAPMX2
.
          CALL      DCNT0000                * del rec from creditors notes tab.
.
.     write to the Creditors notes
.
          MOVE      APMAS001,APCNCRD
          MOVE      SP70,APCNLIN
          MOVE      SP70,APCNDAT
          MOVE      SP70,APCNSPA
          PACK      KEY8,APMAS001,SP70
          CALL      WRAPCN1
.
          CALL      DSNT0000                * del rec from suppliers notes tab.
.
.     write to the Suppliers notes
.
          MOVE      APMAS001,APSNCRD
          MOVE      SP70,APSNLIN
          MOVE      SP70,APSNDAT
          MOVE      SP70,APSNSPA
          PACK      KEY8,APMAS001,SP70
          CALL      WRAPSN1
.
          MOVE      APMAS001,APMACRD
          PROC      APSCKIUP    * ADD/UPDATE/DELETE C/S Key word Search table
.
          MOVE      ZERO,EXIT
          GOTO      APSMA999
.
.      ************ DELETE CREDITOR/SUPPLIER ****************
.
APSMA300  PACK      KEY48,APMAS001,Z70
          CALL      RSAPIM2                 * positional read
          CALL      RPAPIM2                 * read a record
          BRANCH    OVRCD,APSMA310
          MATCH     APMAS001,APIMCRD
          GOTO      APSMA940 IF EQUAL
.
APSMA310  PACK      KEY5,APMAS001,SP70
          CALL      RDAPMA1
          BRANCH    OVRCD,APSMA930
          IF        APMAPOT <> 0
            GOTO      APSMA950
          ENDIF
.
          PACK      KEY5,APMAS001,SP70
          CALL      DEAPMA1                 * delete a record from apsmasaf
.
          CALL      DCRT0000                * delete all records from apsmxraf
          CALL      DCNT0000                * delete all records from apscntaf
          CALL      DSNT0000                * delete all records from apssntaf
.
          MOVE      "D",KEY1                * Deleted Record
          CALL      AUDT0000                * write a record to the audir table
.
          MOVE      APMAS001,APMACRD
          PROC      APSCKIUP    * ADD/UPDATE/DELETE C/S Key word Search table
.
          MOVE      ZERO,EXIT
          GOTO      APSMA999
.
.      ************ DISPLAY CREDITOR/SUPPLIER ****************
.
APSMA400  PACK      KEY5,APMAS001,SP20
          CALL      RDAPMA1
          IF        OVRCD=1 
           CALL      CLRAPS00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Creditor Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,APSMA999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      APSMA999
.
.      ***************** ERROR CONDITIONS *********************
.
APSMA910  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APSMA999
.
APSMA920  MOVE      "Creditor/Supplier Code Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APSMA999
.
APSMA930  MOVE      "Creditor/Supplier Code Does Not Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APSMA999
.
APSMA940  MOVE      "Transaction Exist for Creditor.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APSMA999
.
APSMA950  MOVE      "Balance at Last Purge Exists for Creditor.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APSMA999
.
APSMA999  RETURN
.------------------------------------------------------
.    Clear all the file variables
.------------------------------------------------------
CLRAPS00  MOVE      SP70,APMACRD
          MOVE      SP70,APMATYP
          MOVE      SP70,APMASKY
.
          MOVE      SP70,APMACN1
          MOVE      SP70,APMACN2
          MOVE      SP70,APMACA1
          MOVE      SP70,APMACA2
          MOVE      SP70,APMACA3
          MOVE      SP70,APMACPC
          MOVE      SP70,APMACTEL
          MOVE      SP70,APMACFAX
          MOVE      SP70,APMACON
.
          MOVE      SP70,APMASN1
          MOVE      SP70,APMASN2
          MOVE      SP70,APMASA1
          MOVE      SP70,APMASA2
          MOVE      SP70,APMASA3
          MOVE      SP70,APMASPC
          MOVE      SP70,APMASTEL
          MOVE      SP70,APMASFAX
          MOVE      SP70,APMASCON
.
          MOVE      SP70,APMASCON
          MOVE      SP70,APMABSB
          MOVE      SP70,APMABAC
          MOVE      SP70,APMAPNM
          MOVE      ZERO,APMAPPT
          MOVE      ZERO,APMAPOT
          MOVE      ZERO,APMAHOR
          MOVE      ZERO,APMAHIN
          MOVE      ZERO,APMAHCR
          MOVE      ZERO,APMAHPY
          MOVE      ZERO,APMAPMT
          MOVE      ZERO,APMAGMT
          MOVE      SP70,APMAPPD
          MOVE      SP70,APMADTM
          MOVE      SP70,APMAUR1
          MOVE      SP70,APMAUR2
          MOVE      SP70,APMAUD1
          MOVE      SP70,APMAUD2
          MOVE      SP70,APMAUC1
          MOVE      SP70,APMAUC2
          MOVE      ZERO,APMADIS
          MOVE      ZERO,APMATSE
          MOVE      ZERO,APMASPA
.
CLRAPS99  RETURN
.--------------------------------------------------------
.BEFC0000  CLOCK     FULLDATE,APMAAUDD       * Date of change/ current date
.         CLOCK     FULLTIME,APMAAUDT        * Time of change/ current time
.         CLOCK     PORT,APMAAUDP           * port number
.         PACK      KEY19,APMAAUDD,APMAAUDT,APMAAUDP,APMAAUDR
.         ASSIGN    ONE,APMAAUDS            * Not Printed
.         MOVE      USERID,APMAAUDO         * Operator ID
.         CALL      AWAPMA
.BEFC9999  RETURN
.
.AFTC0000  CLOCK     FULLDATE,APMAAUDD       * Date of change/ current date
.         CLOCK     FULLTIME,APMAAUDT        * Time of change/ current time
.         CLOCK     PORT,APMAAUDP           * port number
.         PACK      KEY19,APMAAUDD,APMAAUDT,APMAAUDP,APMAAUDR
.         ASSIGN    ONE,APMAAUDS            * Not Printed
.         MOVE      USERID,APMAAUDO         * Operator ID
.         CALL      AWAPMA
.AFTC9999  RETURN
.--------------------------------------------------------
.   delete records from C/S Cross ref table
.--------------------------------------------------------
DCRT0000  PACK      KEY35,APMAS001,Z70
          CALL      RSAPMX2                 * positional read
DCRT0100  CALL      RPAPMX2                 * read a record
          BRANCH    OVRCD,DCRT9999          * if file is over
          MATCH     APMAS001,APMXCRD        * check the C/S code
          GOTO      DCRT9999 IF NOT EQUAL   * read another record if not equal
          PACK      KEY35,APMAS001,APMXREF,SP70
          CALL      DEAPMX2                 * delete the record
          GOTO      DCRT0100                * read another record
DCRT9999  RETURN
.--------------------------------------------------------
.     delete all records from the Creditors Notes Table
.--------------------------------------------------------
DCNT0000  PACK      KEY8,APMAS001,Z70
          CALL      RSAPCN1                 * positional read
DCNT0100  CALL      RPAPCN1                 * read a record
          BRANCH    OVRCD,DCNT9999          * if the file is over
          MATCH     APMAS001,APCNCRD        * compare the credit number
          GOTO      DCNT9999 IF NOT EQUAL   * read another rec.
          PACK      KEY8,APCNCRD,APCNLIN,SP70
          CALL      DEAPCN1                 * delete the record
          GOTO      DCNT0100                * read another record
DCNT9999  RETURN
.--------------------------------------------------------
.     delete all records from the Suppliers Notes Table
.--------------------------------------------------------
DSNT0000  PACK      KEY8,APMAS001,Z70
          CALL      RSAPSN1                 * positional read
DSNT0100  CALL      RPAPSN1                 * read a record
          BRANCH    OVRCD,DSNT9999          * if the file is over
          MATCH     APMAS001,APSNCRD        * compare the credit number
          GOTO      DSNT9999 IF NOT EQUAL   * read another rec.
          PACK      KEY8,APSNCRD,APSNLIN,SP70
          CALL      DEAPSN1                 * delete the record
          GOTO      DSNT0100                * read another record
DSNT9999  RETURN
.--------------------------------------------------------
.    delete all the record from the Creditors Keyword Index
.--------------------------------------------------------
DCKI0000  PACK      KEY20,APMAS001,Z70
          CALL      RSAPCK1                 * positional read
DCKI0100  CALL      RPAPCK1                 * read a record
          BRANCH    OVRCD,DCKI9999          * if the file is over
          MATCH     APMAS001,APCKCRD        * compare the creit number
          GOTO      DCKI9999 IF NOT EQUAL   * read another record
          PACK      KEY20,APCKCRD,APCKKWD,SP20
          CALL      DEAPCK1                 * delete a record
          GOTO      DCKI0100
DCKI9999  RETURN                            
.--------------------------------------------------------
.     write a record to the audit table
.--------------------------------------------------------
AUDT0000  CLOCK     FULLDATE,APMAAUDD       * Date of change/ current date
          CLOCK     FULLTIME,APMAAUDT       * Time of change/ current time
          CLOCK     PORT,APMAAUDP           * port number
          MOVE      KEY1,APMAAUDR           * Record type
          ASSIGN    ONE,APMAAUDS            * Not Printed
          MOVE      USERID,APMAAUDO         * Operator ID
          PACK      KEY19,APMAAUDD,APMAAUDT,APMAAUDP,APMAAUDR
          CALL      AWAPMA
AUDT9999  RETURN
.----------------------------------------------------------
          INC       IBAWEBCD
          INC       FMSCODTM
          INC       APSMASTM
          INC       APSCKIPR                * file having proc APSCKIUP
          INC       APSMASIO/INC            * Creditors Master Details 
          INC       APSCKIIO/INC            * Creditors Keyword
          INC       APSMXRIO/INC            * C/S Cross Reference Table
          INC       APSSNTIO/INC            * Accounts Payable Supplier Notes
          INC       APSCNTIO/INC            * Accounts Payable Creditors Notes
          INC       APSIMSIO/INC
          INC       APSPPDIO/INC
          INC       APSCTSIO/INC
          INC       FMSCODIO/INC

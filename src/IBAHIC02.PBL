.***************************************************************************
.* System    :   HIC Claims                                                *
.* Program   :   IBAHIC02                                                  *
.* Desc      :   Print outstanding batches                                 *
.***************************************************************************
.* Date      :   20/04/2005                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will list all batches that have a status of  *
.*           :   either batched or sent according to the filters entered   *
.*                                                                         *
.* V10.11.01 21/07/2017  Ebon Clements     TSK 0268970                     *
.*           Change to use OUTCLIFD index 1 instead of index 2             *
.* V10.04.01 07/03/2014  Ania P         CAR 261630                         *
.*           Remove the use of PORT for temp file names                    *
.* V9.05.01  10/03/2006 J.Tan           CAR 96067                          *
.*           Mods maximum number of lines to print                         *
.* V9.05.B03 22/02/2005  J.Tan          CAR 96067                          *
.*           Mods to sort report by Med.Prac/Clinic id/batch date/batch no *
.* V9.05.B02 19/12/2005  J.Tan          CAR 77215                          *
.*           Mods for write off and adjustment                             *
.* V9.04.06  18/11/2005  J.Tan          CAR 77215                          *
.*           Mods to include all status of batches                         *
.* V9.04.05  21/10/2005  J.Tan          CAR 77215                          *
.*           Mods to print batches after had been actioned                 *
.* V9.04.04  10/08/2005  J.Tan          CAR 71173                          *
.*           Mods for Batch status                                         *
.* V9.04.03  24/06/2005  J.Tan          CAR 56701                          *
.*           Fixed printing batch comments                                 *
.* V9.04.02  16/06/2005  J.Tan          CAR 56701                          *
.*           Mods date sent to HIC to be the transmission date             *
.*           Mods to print batch with status after received ok from HIC    *
.* V9.04.01  11/05/2005  J.Tan          CAR 61440                          *
.*           Added new field adjustment amount to hic files                *
.* V9.04.00  20/04/2005  Ebon Clements  CAR 56701                          *
.*                       Created program                                   *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       HICBTCFD/INC
          INC       PMSHCPFD/INC
          INC       PRIPRAFD/INC
          INC       PRIDOCFD/INC
          INC       OUTSITFD/INC
          INC       OUTCLIFD/INC
          INC       PATCODFD/INC
          INC       WEBSECFD/INC
          INC       PRIALSFD/INC
          INC       PRISECFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.         Temporary File Definition
.         -------------------------
.
.         Filename : hic02axx.dat          (xx = port id)
.
HICTEMP1  IFILE SQL, FIXED=121, KEY="UC34-39,107-112,113-120,1-5"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TEMPBTCH  DIM       5      5        1     Batch Number (1 Alpha 4 Numeric)
TEMPPMCI  DIM       8      8        6     MCI (3 Alpha 5 Numeric)
TEMPPYEE  DIM       10     10       14    Payee Provider Number
TEMPPSRV  DIM       10     10       24    Service Provider Number
TEMPMPRA  DIM       6      6        34    Medical Practice
TEMPDOCT  DIM       6      6        40    Associated Doctor Code
TEMPCAMT  FORM      12.2   8        46    Total Batch Amount
TEMPAMTR  FORM      12.2   8        54    Total Amount Received
TEMPREJA  FORM      12.2   8        62    Total Amount Rejected
TEMPADJM  FORM      12.2   8        70    Total Amount Adjusted
TEMPWOFF  FORM      12.2   8        78    Total Amount 'Write Off'
TEMPTRIN  FORM      12.2   8        86    Total Amount 'Transfer to Invoice'
TEMPDATE  DIM       8      8        94    Date sent to HIC
TEMPCMNT  DIM       3      3       102    Batch Comments (Catgory IE)
TEMPSTAT  DIM       2      2       105    Status (Right justified)
TEMPCLID  DIM       6      6       107    Clinic ID
TEMPBDAT  DIM       8      8       113    Batch Date
.
.End of Record                     121
.
.         Temporary File Variables
.
EFTFLAG   DIM       1
ERASE     INIT      "iserase "
FNAME     INIT      "hic02a"          * set up for temp file name
ISBUILD   INIT      "isbuild "
TEMPFILE  DIM       8
UKEY      INIT      " 121 UC34-39,107-112,113-120,1-5" * setup rec length & key
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ALACCESS  FORM      1
CLINFROM  DIM       6
CLINICTO  DIM       6
CMDLINE   DIM       127
DISPNAME  DIM       60
DOCFNAME  DIM       50
FILTRHCP  DIM       10
FILTPROV  DIM       10
FMTSNAME  DIM       35
FMTGNAME  DIM       35
FMTTITLE  DIM       10
HEADING1  DIM       30
HEADING2  DIM       30
HEADING3  DIM       40
HEADING4  DIM       10
HEADING5  DIM       30
HEADING6  DIM       30    
NAME35    DIM       35
PRACTIFR  DIM       6
PRACTITO  DIM       6
PRINVAR1  FORM      12.2
PRINVAR2  FORM      12.2
PRINVAR3  DIM       16
PRINVAR4  DIM       11
PRINVAR5  DIM       20
SITE      DIM       6
SAVEVAR   DIM       12
TOTBILL   FORM      12.2
TOTAMNT   FORM      12.2
WEBUSRID  DIM       10
.
BSTAT00   INIT      "Batched"
BSTAT01   INIT      "Extracted"
BSTAT02   INIT      "Sent"
BSTAT03   INIT      "Acknowledge"
BSTAT04   INIT      "Nack"
BSTAT05   INIT      "Received Ok"
BSTAT06   INIT      "Received Error"
BSTAT07   INIT      "EFT Pay.Received"
BSTAT08   INIT      "Actioned"
BSTAT09   INIT      "Deposit Verified"
BSTAT10   INIT      "Closed"
.
PRGID     INIT      "IBAHIC02"
PRGNAM    INIT      "Oustanding batches report    "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      PRCF0000               * keyin practice from
          BRANCH    EXIT,ML0100
.
          CALL      PRCT0000               * keyin practice to
          BRANCH    EXIT,ML0100
.
          CALL      FCLN0000               * keyin clinic from
          BRANCH    EXIT,ML0100
.
          CALL      TCLN0000               * keyin clinic to
          BRANCH    EXIT,ML0100
.
....      CALL      KHCP0000               * keyin hcp
....      BRANCH    EXIT,ML0100
.
          CALL      KPRV0000               * keyin provider
          BRANCH    EXIT,ML0100
.
          CALL      WEBU0000               * keyin user
          BRANCH    EXIT,ML0100
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CREA0000               * Create tempfile
.
          CALL      PROC0000               * process
          CALL      PRIN0000               * Print data
          CALL      ENDP0000               * End of report
.
          CALL      KILL0000               * Delete tempfile
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          CALL      IBACLOCK
.
          CALL      TFILENAM
          PACK      TEMPFILE,TFILNAME,SP70
.
          OPEN      HICBTCA5,"hicbtcaf"       * batch file
          OPEN      PMSHCPA1,"pmshcpaf"       * HCP file
          OPEN      PRIPRAC1,"pripracf"       * private practice
          OPEN      PRIDOCT1,"pridoctf"       * practice doctors
          OPEN      OUTSITA1,"outsitaf"       * outpatient site file
          OPEN      PATCODE1,"patcodes"       * codes file
          OPEN      WEBSECA1,"websecaf"       * web user id
          OPEN      PRIALSA1,"prialsaf"       * all practice security
          OPEN      PRISECA1,"prisecaf"       * practice security
.
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,INIT9000
.
          PACK      CFNAME WITH UID,FILCLIA1        * OUTCLIFD
          OPEN      OUTCLIA1,CFNAME
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9000  DISPLAY   *P1:24,*EL,*B,"Site Code not on file. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      INIT9999
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      HICTEMP1,TEMPFILE            * open temp index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     HICTEMP1,DELETE              * close file
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,ALACCESS           * Set all access to no
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDPRALS1
          IF        OVRCD=0
            MOVE      ONE,ALACCESS
          ENDIF
.
          PACK      KEY35,SP70
          CALL      RSHCBTC5
PROC1000  CALL      RKHCBTC5
          BRANCH    OVRCD,PROC9999
.
          BRANCH    ALACCESS,PROC1200       * Access to all practices 
.
          PACK      KEY16,HCBTMPRA,WEBUSRID,SP70
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROC1000          * No access to this practice
.
PROC1200  MATCH     IDDD,HCBTSITE           * My Site
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP70,CLINFROM
          IF        !@EQUAL
            MATCH     CLINFROM,HCBTCLID
            GOTO      PROC1000 IF LESS
          ENDIF
.
          MATCH     SP70,CLINICTO
          IF        !@EQUAL
            MATCH     HCBTCLID,CLINICTO
            GOTO      PROC1000 IF LESS
          ENDIF
.
....      MATCH     SP70,FILTRHCP
....      IF        !@EQUAL
....      ENDIF
.
          MATCH     SP70,FILTPROV
          IF        !@EQUAL
            MATCH     FILTPROV,HCBTPSRV
            GOTO      PROC1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PRACTIFR
          IF        !@EQUAL
            MATCH     PRACTIFR,HCBTMPRA
            GOTO      PROC1000 IF LESS
          ENDIF
.
          MATCH     SP70,PRACTITO
          IF        !@EQUAL
            MATCH     HCBTMPRA,PRACTITO
            GOTO      PROC1000 IF LESS
          ENDIF
.
          MOVE      HCBTCLID,TEMPCLID
          MOVE      HCBTBTCH,TEMPBTCH
          MOVE      HCBTDATE,TEMPBDAT
          MOVE      HCBTPMCI,TEMPPMCI
          MOVE      HCBTPYEE,TEMPPYEE
          MOVE      HCBTPSRV,TEMPPSRV
          MOVE      HCBTMPRA,TEMPMPRA
          MOVE      SP70,TEMPDOCT              *** WORK OUT HCP 
          MOVE      HCBTCAMT,TEMPCAMT
          MOVE      HCBTAMTR,TEMPAMTR
          MOVE      HCBTREJA,TEMPREJA
          MOVE      HCBTADJM,TEMPADJM
          MOVE      HCBTWOFF,TEMPWOFF
          MOVE      HCBTTRIN,TEMPTRIN
          MOVE      HCBTTDAT,TEMPDATE
          MOVE      HCBTCMNT,TEMPCMNT
          MOVE      HCBTSTAT,TEMPSTAT
.
          PACK      KEY25,TEMPMPRA,TEMPCLID,TEMPBDAT,TEMPBTCH,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
     
          GOTO      PROC1000
.
PROC9999  RETURN
+
.**********************************************************************
.*                         PRIN0000                                   *
.*                        Print Data                                  *
.**********************************************************************
.
PRIN0000  MOVE      ZERO,TOTBILL
          MOVE      ZERO,TOTAMNT
          MOVE      SP70,SAVEVAR
          PACK      KEY25,SP70
          CALL      RSTEMP1
PRIN1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRIN9000
.
          MATCH     SP70,SAVEVAR
          GOTO      PRIN1400 IF EQUAL            * first record
.
          PACK      KEY12,TEMPMPRA,TEMPCLID
          MATCH     KEY12,SAVEVAR                * check if sort field changed
          GOTO      PRIN2000 IF EQUAL
.
          CALL      UND132
          PRINT     *18,"| TOTAL",*29,"|",TOTBILL,*46,"|",TOTAMNT,*64,"|":
                    *N,*18,"*----------",*29,"*----------------":
                    *46,"*-----------------*"
          ADD       TWO,CLNO
          MOVE      ZERO,TOTBILL
          MOVE      ZERO,TOTAMNT
          MOVE      ONE,CNOUNDLN                 * no underline
.
PRIN1400  CALL      HEAD0000
          MOVE      ZERO,CNOUNDLN
          PACK      SAVEVAR,TEMPMPRA,TEMPCLID,SP70
.
PRIN2000  MOVE      SP70,EFTFLAG
          MATCH     " 7",TEMPSTAT
          IF        @EQUAL
            MOVE      "*",EFTFLAG           * EFT pay.received still on holding
          ENDIF
.
          MOVE      TEMPCAMT,PRINVAR1            * Amount Billed
.
          MOVE      ZERO,PRINVAR2
          MOVE      TEMPCAMT,PRINVAR2            * Amount Outstanding
          SUB       TEMPAMTR,PRINVAR2            * - received
          ADD       TEMPADJM,PRINVAR2            * - adjustment
          SUB       TEMPWOFF,PRINVAR2            * - written off
.
          COMPARE   ZERO,PRINVAR2
          GOTO      PRIN1000 IF EQUAL            * no outstanding amt
.
          ADD       TEMPCAMT,TOTBILL
          ADD       PRINVAR2,TOTAMNT
.
          MOVE      ZERO,FORM2
          MOVE      TEMPSTAT,FORM2
          MOVE      BSTAT00,PRINVAR3
          LOAD      PRINVAR3,FORM2,BSTAT01,BSTAT02,BSTAT03,BSTAT04:
                                   BSTAT05,BSTAT06,BSTAT07,BSTAT08,BSTAT09:
                                   BSTAT10
.
          MATCH     SP70,TEMPDATE
          IF        !@EQUAL
            UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,PRINVAR4         * formatted date
          ELSE
            MOVE      SP70,PRINVAR4
          ENDIF
.
          MATCH     SP70,TEMPCMNT
          IF        !@EQUAL
            PACK      KEY5,ANSI,ANSE,TEMPCMNT
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,PRINVAR5
            ENDIF
          ELSE
            MOVE      SP70,PRINVAR5
          ENDIF
.
          UNPACK    TEMPBDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *1,"| ",CPCDATE,*18,"| ",TEMPBTCH:
                    *29,"|",PRINVAR1,*46,"|",PRINVAR2,EFTFLAG:
                    *64,"| ",PRINVAR3,*83,"| ",PRINVAR4:
                    *96,"|",PRINVAR5,*132,"|"
.
          ADD       ONE,CLNO
          COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          GOTO      PRIN1000              * read next record
.
PRIN9000  COMPARE   ZERO,CPAGENO
          GOTO      PRIN9999 IF EQUAL
.
          CALL      UND132
          PRINT     *18,"| TOTAL",*29,"|",TOTBILL,*46,"|",TOTAMNT,*64,"|":
                    *N,*18,"*----------",*29,"*----------------":
                    *46,"*-----------------*"
.
PRIN9999  RETURN
+
.*********************************************************************
.         print heading
.*********************************************************************
PRHD0000  COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"Medical Practice : ";
          MATCH     SP70,TEMPMPRA
          GOTO      PRHD2000 IF EQUAL
.
          PACK      PRPRDESC,SP70
          PACK      KEY6,TEMPMPRA
          CALL      RDPRPR1
          PRINT     *20,PRPRDESC;
.
PRHD2000  MATCH     SP70,TEMPCLID
          GOTO      PRHD9000 IF EQUAL
.
          MOVE      SP70,OCADESC
          PACK      KEY20,IDDD,TEMPCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                *To get Clinic ID Description
          IF        OVRCD=0
            MATCH     IDDD,OCASITE
            GOTO      PRHD4000 IF EQUAL
.
            MATCH     TEMPCLID,OCACLIN
            GOTO      PRHD4000 IF EQUAL
          ENDIF
.
          MOVE      SP70,OCADESC
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ENDIF
.
PRHD4000  PRINT     *60,"Clinic Id : ",OCADESC,"(",TEMPPSRV,")";
.
PRHD9000  PRINT     *N;
          ADD       ONE,CLNO
PRHD9999  RETURN
+
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD132
.
          COMPARE   ONE,CPAGENO
          GOTO      HEAD2000 IF NOT EQUAL
.
          PRINT     *1,"Medical Practice From : ",HEADING5," To : ",HEADING6
          PRINT     *1,"Clinic From           : ",HEADING1," To : ",HEADING2
....      PRINT     *1,"HCP : ",HEADING3,"Provider Number : ",HEADING4
          PRINT     *1,"Provider Number : ",HEADING4,*N
          ADD       THREE,CLNO
.
HEAD2000  CALL      PRHD0000
.
          CALL      UND132
          PRINT     *1,"| Batch Date",*18,"| Batch No.":
                    *29,"| Billed ",*46,"| Outstanding ":
                    *64,"| Status ",*83,"|Sent to HIC":
                    *96,"|Batch Comments",*132,"|"
.
          CALL      UND132
.
HEAD9999  RETURN
+
.**********************************************************************
.*                           ENDP0000                                 *
.*                      Print the end of the report                   *
.**********************************************************************
ENDP0000  COMPARE   ZERO,CPAGENO
          GOTO      ENDP9999 IF NOT EQUAL
.
          CALL      HEAD0000
ENDP9000  PRINT     *N,"Note: An Asterisk next to 'Outstanding amount' ":
                    "indicates there is a Payment Received in Holding File":
                    *N,*N,*50,"*** End of Report ***"
ENDP9999  RETURN
+
.******************************************************************************
.*                                   FCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code From                     *
.******************************************************************************
FCLN0000  MOVE      SP70,CLINFROM
          KEYIN     *P1:9,*EL,"Clinic Id From : ",*V2LON,CLINFROM
.
          PACK      CLINFROM,CLINFROM,SP70
          MATCH     SP70,CLINFROM
          GOTO      FCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINFROM,Z70
          CALL      RDSCLIA1
FCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,FCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINFROM     * Must be in the Same clinic
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      FCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      FCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
FCLN5000  MOVE      OCADESC,HEADING1
          DISPLAY   *P25:9,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN6000  MOVE      "Start",HEADING1
          DISPLAY   *P25:9,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
FCLN9999  RETURN
+
.******************************************************************************
.*                                   TCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code To                       *
.******************************************************************************
TCLN0000  MOVE      SP70,CLINICTO
          KEYIN     *P1:10,*EL,"Clinic Id To   : ",*V2LON,CLINICTO
.
          PACK      CLINICTO,CLINICTO,SP70
          MATCH     SP70,CLINICTO
          GOTO      TCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINICTO,Z70
          CALL      RDSCLIA1
TCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,TCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINICTO     * Must be in the Same clinic
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      TCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      TCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
TCLN5000  MOVE      OCADESC,HEADING2
          DISPLAY   *P25:10,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN6000  MOVE      "Finish",HEADING2
          DISPLAY   *P25:10,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
TCLN9999  RETURN
.
+
.******************************************************************************
.*                                   KHCP0000             Called by: ML0000   *
.*                             Keyin The HCP code                             *
.******************************************************************************
KHCP0000  MOVE      SP70,FILTRHCP
          KEYIN     *P1:11,*EL,"HCP : ",*V2LON,FILTRHCP
.
          PACK      FILTRHCP,FILTRHCP,SP70
          MATCH     SP70,FILTRHCP
          GOTO      KHCP6000 IF EQUAL
.
          PACK      KEY10,FILTRHCP,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid HCP Code",PACFNAME
          ENDIF
.
KHCP5000  MOVE      PACFNAME,HEADING3
          DISPLAY   *P18:11,*EL,*V2LON,PACFNAME;
          MOVE      ZERO,EXIT
          GOTO      KHCP9999
.
KHCP6000  MOVE      "All",HEADING3
          DISPLAY   *P18:11,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      KHCP9999
.
KHCP9500  DISPLAY   *P1:24,*EL,*B,"Invalid HCP. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
KHCP9999  RETURN
.
+
.******************************************************************************
.*                                   KPRV0000             Called by: ML0000   *
.*                             Keyin the provider number                      *
.******************************************************************************
KPRV0000  MOVE      SP70,FILTPROV
          KEYIN     *P1:12,*EL,"Provider Number : ",*V2LON,FILTPROV
.
          PACK      FILTPROV,FILTPROV,SP70
          MATCH     SP70,FILTPROV
          GOTO      KPRV6000 IF EQUAL
.
KPRV5000  MOVE      FILTPROV,HEADING4
          DISPLAY   *P29:12,*EL,*V2LON,FILTPROV;
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV6000  MOVE      "All",HEADING4
          DISPLAY   *P29:12,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV9999  RETURN
+
.******************************************************************************
.*                                   PRCF0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCF0000  MOVE      SP70,PRACTIFR
          KEYIN     *P1:13,*EL,"Medical Practice : ",*V2LON,PRACTIFR
.
          PACK      PRACTIFR,PRACTIFR,SP70
          MATCH     SP70,PRACTIFR
          GOTO      PRCF6000 IF EQUAL
.
          PACK      KEY6,PRACTIFR,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCF9500
.
PRCF5000  MOVE      PRPRDESC,HEADING5
          DISPLAY   *P27:13,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF6000  MOVE      "Start",HEADING5
          DISPLAY   *P27:13,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCF9999  RETURN
+
.******************************************************************************
.*                                   PRCT0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCT0000  MOVE      SP70,PRACTITO
          KEYIN     *P1:14,*EL,"Medical Practice : ",*V2LON,PRACTITO
.
          PACK      PRACTITO,PRACTITO,SP70
          MATCH     SP70,PRACTITO
          GOTO      PRCT6000 IF EQUAL
.
          PACK      KEY6,PRACTITO,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCT9500
.
PRCT5000  MOVE      PRPRDESC,HEADING6
          DISPLAY   *P27:14,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT6000  MOVE      "Finish",HEADING6
          DISPLAY   *P27:14,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCT9999  RETURN
.
+
.******************************************************************************
.*                                   WEBU0000             Called by: ML0000   *
.*                             Keyin The web user id                          *
.******************************************************************************
WEBU0000  MOVE      SP70,WEBUSRID
          KEYIN     *P1:15,*EL,"User Id : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,WEBU9500
.
          MOVE      ZERO,EXIT
          GOTO      WEBU9999
.
WEBU9500  DISPLAY   *P1:24,*EL,*B,"Invalid User Id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
WEBU9999  RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
.
RATEMP1   RESET     KEY25
          MOVE      ZERO,OVRCD
          READ      HICTEMP1,KEY25;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   READ      HICTEMP1,KEY25;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      HICTEMP1,KEY25;TEMPBTCH,TEMPPMCI,TEMPPYEE,TEMPPSRV:
                             TEMPMPRA,TEMPDOCT,TEMPCAMT,TEMPAMTR:
                             TEMPREJA,TEMPADJM,TEMPWOFF,TEMPTRIN,TEMPDATE:
                             TEMPCMNT,TEMPSTAT,TEMPCLID,TEMPBDAT
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    HICTEMP1;TEMPBTCH,TEMPPMCI,TEMPPYEE,TEMPPSRV:
                             TEMPMPRA,TEMPDOCT,TEMPCAMT,TEMPAMTR:
                             TEMPREJA,TEMPADJM,TEMPWOFF,TEMPTRIN,TEMPDATE:
                             TEMPCMNT,TEMPSTAT,TEMPCLID,TEMPBDAT
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    HICTEMP1;TEMPBTCH,TEMPPMCI,TEMPPYEE,TEMPPSRV:
                             TEMPMPRA,TEMPDOCT,TEMPCAMT,TEMPAMTR:
                             TEMPREJA,TEMPADJM,TEMPWOFF,TEMPTRIN,TEMPDATE:
                             TEMPCMNT,TEMPSTAT,TEMPCLID,TEMPBDAT
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     HICTEMP1,KEY25;TEMPBTCH,TEMPPMCI,TEMPPYEE,TEMPPSRV:
                             TEMPMPRA,TEMPDOCT,TEMPCAMT,TEMPAMTR:
                             TEMPREJA,TEMPADJM,TEMPWOFF,TEMPTRIN,TEMPDATE:
                             TEMPCMNT,TEMPSTAT,TEMPCLID,TEMPBDAT
          RETURN
.
UPTEMP1   UPDATE    HICTEMP1;TEMPBTCH,TEMPPMCI,TEMPPYEE,TEMPPSRV:
                             TEMPMPRA,TEMPDOCT,TEMPCAMT,TEMPAMTR:
                             TEMPREJA,TEMPADJM,TEMPWOFF,TEMPTRIN,TEMPDATE:
                             TEMPCMNT,TEMPSTAT,TEMPCLID,TEMPBDAT
          RETURN
.
DETEMP1   DELETE    HICTEMP1,KEY25
          RETURN
.
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       HICBTCIO/INC
          INC       PMSHCPIO/INC
          INC       PRIPRAIO/INC
          INC       PRIDOCIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       PATCODIO/INC
          INC       WEBSECIO/INC
          INC       PRIALSIO/INC
          INC       PRISECIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

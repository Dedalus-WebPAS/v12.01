. ****************************************************************************
. *          P R O G R A M  S U M M A R Y                                    *
. *          -------------  -------------                                    *
. *                                                                          *
. * PROGRAM NAME:     IBAOUT62                                               *
. *                                                                          *
. * FUNCTION:         PUBLIC HOLIDAY MASTER LISTING                          *
. *                                                                          *
. * DATE WRITTEN:     22/06/88                                               *
. *                                                                          *
. * AUTHOR:           N.SPIJKMAN                                             *
. *                                                                          *
. * MODIFICATIONS:                                                           *
. *                                                                          *
. * V10.07.01 14/10/2015  Ebon Clements    CAR 316629                        *
. *           Added hospital to holiday file key  - OUTPHOFD                 *
. * V9.03.01  11.09.2003  Sandra Barcham   43010                             *
. *           Remove file prefix from open of outpreaf & outphoaf            *
. ****************************************************************************
.
          INC       STD001FD
          INC       OUTPHOFD/INC
          INC       OUTCONFD/INC
.
+
.
.               Extra variables
.
PAGENO    FORM      4
.
START30   DIM       30
END30     DIM       30
.
TMPSTR    DIM       30
DIM30D    DIM       30
.
XDIM30    DIM       30
YDIM30    DIM       30
.
START8    DIM       8
END8      DIM       8
.
XDIM8     DIM       8
YDIM8     DIM       8
.
FSTREC    INIT      "Start "
LSTREC    INIT      "Finish"
.
LN55      FORM      "55"
.
FIELD     FORM      1
OUT       INIT      "out"
.
ZZ30      INIT      "ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ"
WTCNRPWL  FORM      1
.
PRGID     INIT      "IBAOUT62"
PRGNAM    INIT      "Public Holiday Master Listing"
VERSION   INIT      "V12.01.00"
+
.         Start of program
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
.
          DISPLAY   *P64:24,"outphoaf";
          OPEN      OUTPHOA1,"outphoaf"
.
          DISPLAY   *P64:24,"outphoaf";
          OPEN      OUTPHOA2,"outphoaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CLOCK     TIME,CTIMEIS
.
MENU      DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Holiday Date Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Holiday Description Sequence"
.
BEGIN     KEYIN     *P1:8,*EF,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF START,START1
.
          DISPLAY   *P35:8,*EL,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      BEGIN
.
.       Date sequence
.
START     DISPLAY   *P1:11,*EL," Date From  :":
                    *P1:13,*EL,"      To    :"
.
START0    DISPLAY   *P43:11,*EL;
          MOVE      SP8,START8
          MOVE      ZERO,OVRCD
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      TEN5,CCOL
          MOVE      TEN1,CVERT
          MOVE      ZERO,CHIGHLT
          MOVE      CCC,CCENT
          CALL      KEYDATE
          PACK      START8 WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",START8
          CALL      DSPDATE
.
          MATCH     SP2,CDAY
          GOTO      KEYNXT1 IF NOT EQUAL
          MOVE      SP8,START8
          DISPLAY   *P15:11,*V2LON,FSTREC
.
KEYNXT1   DISPLAY   *P43:13,*EL;
          MOVE      SP8,END8
          MOVE      ZERO,OVRCD
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      TEN5,CCOL
          MOVE      TEN3,CVERT
          MOVE      ZERO,CHIGHLT
          MOVE      CCC,CCENT
          CALL      KEYDATE
          PACK      END8 WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",END8
          CALL      DSPDATE
.
          MATCH     SP2,CDAY
          GOTO      CHKBOTH IF NOT EQUAL
          MOVE      SP8,END8
          DISPLAY   *P15:13,*V2LON,LSTREC
          GOTO      CONFIRM
.
CHKBOTH   MATCH     START8,SP8
          GOTO      CONFIRM IF EQUAL
.
          MATCH     START8,END8
          GOTO      CONFIRM IF NOT LESS
.
          DISPLAY   *P43:24,*B,*V2LON:
                    "** FROM Key Greater Than TO Key **",*W2,*P1:24,*EL;
          GOTO      START
.
INVCODE   DISPLAY   *P43:11,*B,*V2LON:
                    "** Date Does Not Exist **",*W2;
          GOTO      START0
.
.       Description sequence
.
START1    DISPLAY   *P1:11,*EL,"Holiday description From :":
                    *P1:13,*EL,"                    To   :"
.
          MOVE      UNDLN,START30
          DISPLAY   *P28:11,*EL,START30
          MOVE      SP30,START30
          KEYIN     *P28:11,*V2LON,START30,*P28:11,*DV,START30
.
          ENDSET    START30
          APPEND    SP30,START30
          RESET     START30
.
          MATCH     SP30,START30
          GOTO      KEYNXT2 IF NOT EQUAL
.
          DISPLAY   *P28:11,*V2LON,FSTREC
.
KEYNXT2   MOVE      UNDLN,END30
          DISPLAY   *P28:13,END30
          MOVE      SP30,END30
          KEYIN     *P28:13,*V2LON,TMPSTR
          ENDSET    TMPSTR
          APPEND    ZZ30 TO TMPSTR
          RESET     TMPSTR
.
          MATCH     ZZ30,TMPSTR
          GOTO      CHKBOT0 IF NOT EQUAL
.
          MOVE      ZZ30 TO END30
          DISPLAY   *P28:13,*V2LON,*EL,LSTREC
          GOTO      CONFIRM
.
CHKBOT0   MOVE      TMPSTR,END30
          DISPLAY   *P28:13,*V2LON,END30
.
CHKBOT1   MATCH     START30,SP30
          GOTO      CONFIRM IF EQUAL
.
          MATCH     START30,END30
          GOTO      CONFIRM IF NOT LESS
.
          DISPLAY   *P43:24,*B,*V2LON:
                    "** FROM Key Greater Than TO Key **",*W2,*P1:24,*EL;
          GOTO      START1
.
.
CONFIRM   KEYIN     *P1:24,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          MATCH     "N",ANS
          GOTO      BEGIN IF EQUAL
.
          MATCH     "Y",ANS
          GOTO      OKCONT IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.
.
OKCONT    DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "*** Generating ***",*W2;
.
          CALL      HEADSET
.
          MOVE      ZERO,PAGENO
.
          BRANCH    OPTION OF CSEQ3,DSEQ3
.
CSEQ3     DISPLAY   *P1:24,*EL,*P20:24,"Printing Holiday Date :"
.
          PACK      KEY11,START8,SP70
          CALL      RDPHOA1
          BRANCH    OVRCD TO LOOPX
          GOTO      LOOPX1
.
LOOPX     CALL      RDKPHOA1
          BRANCH    OVRCD OF ENDP
.
LOOPX1    MATCH     SP8,END8
          GOTO      NEXT1 IF EQUAL
.
          MATCH     OPHDATE,END8
          GOTO      ENDP IF LESS
.
NEXT1     DISPLAY   *P46:24,*V2LON,OPHDATE;
.
          CALL      DISPDA
          GOTO      LOOPX
.
.
DSEQ3     DISPLAY   *P1:24,*EL,"Printing Holiday Description :"
.
          PACK      KEY41 WITH START30,SP15
          CALL      RDSPHOA2
.
LOOPY     CALL      RDKPHOA2
          BRANCH    OVRCD OF ENDP
.
          MATCH     SP30,END30
          GOTO      NEXT12 IF EQUAL
.
          MATCH     OPHDESC,TMPSTR
          GOTO      ENDP IF LESS
.
NEXT12    MOVE      OPHDESC,DIM30D
          DISPLAY   *P35:24,*V2LON,DIM30D;
.
          CALL      DISPDA
          GOTO      LOOPY
.
DISPDA    COMPARE   ZERO,PAGENO
          CALL      PRINTH IF EQUAL
.
          COMPARE   LN55,CLNO
          CALL      PRINTH IF NOT LESS
.
          MOVE      ONE,CCOL
          UNPACK    OPHDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *CCOL,CPCDATE,*15,OPHDESC
.
          ADD       ONE,CLNO
          RETURN
.
.
ENDP      COMPARE   ZERO,PAGENO
          GOTO      NOREC IF EQUAL
.
          PRINT     *N,*N,"***  END OF REPORT  ***"
          DISPLAY   *P1:24,*EL,*P15:24,*V2LON:
                    "*** Report Generation Completed ***",*W2;
          GOTO      BEGIN
.
NOREC     DISPLAY   *P1:24,*EL,*P20:24,*V2LON,*B:
                    "*** No Holiday Date selected ***",*W2;
          GOTO      BEGIN
.
PRINTH    ADD       ONE,PAGENO
          PRINT     *F,"IBAOUT62",*36,CNAME,*89,"DATE - ",*96,CDATE,*N:
                    *1,VERSION,*36,"*** PUBLIC HOLIDAY MASTER LISTING ***":
                    *89,"TIME -",*96,CTIMEIS,*N:
                    *89,"PAGE -",*100,PAGENO,*N
.
          BRANCH    OPTION OF CSEQ1,DSEQ1
.
CSEQ1     PRINT     *36,"Holiday Date Sequence  -  From ";
          MOVE      "67",CCOL
          MATCH     XDIM8,FSTREC
          GOTO      CSEQ7 IF EQUAL
          UNPACK    XDIM8 INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *CCOL,CPCDATE
.**     CALL         PRTDATE
.
CSEQ4     PRINT     *62,"To ";
          MOVE      "67",CCOL
          MATCH     YDIM8,LSTREC
          GOTO      CSEQ6 IF EQUAL
          UNPACK    YDIM8 INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *CCOL,CPCDATE
.**     CALL         PRTDATE
.
CSEQ5     PRINT     *N;
          MOVE      "7",CLNO
          GOTO      DISPSC
.
CSEQ7     PRINT     *67,XDIM8
          GOTO      CSEQ4
.
CSEQ6     PRINT     *67,YDIM8
          GOTO      CSEQ5
.
DSEQ1     PRINT     *36,"Holiday Description Sequence",*N:
                    *39,"From :  ",XDIM30,*N:
                    *39,"To   :  ",YDIM30,*N
          MOVE      "9",CLNO
.
DISPSC    PRINT     *1,"HOLIDAY",*15,"DESCRIPTION":
                    *N,*1,"----------":
                    *15,"--------------------"
.
          RETURN
.
HEADSET   BRANCH    OPTION OF CSEQ2,DSEQ2
.
CSEQ2     MOVE      SP8,XDIM8
          MOVE      SP8,YDIM8
.
          MOVE      START8,XDIM8
          MOVE      END8,YDIM8
.
          MATCH     SP8,START8
          GOTO      CONT6 IF NOT EQUAL
          MOVE      FSTREC,XDIM8
.
CONT6     MATCH     SP8,END8
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM8
          RETURN
.
DSEQ2     MOVE      SP30,XDIM30
          MOVE      SP30,YDIM30
.
          MOVE      START30,XDIM30
          MOVE      END30,YDIM30
.
          MATCH     SP30,START30
          GOTO      CONT61 IF NOT EQUAL
          MOVE      FSTREC,XDIM30
.
CONT61    MATCH     ZZ30,END30
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM30
          RETURN
.
          INC       OUTPHOIO/INC
          INCLUDE   STD001IO
.
ENDIT     CHAIN     PGM
          STOP
.

.******************************************************************************
.* System         : Patient Billing                                           *
.* Program        : IBABIL80.PBL                                              *
.* Name           : Patient On Leave Report                                   *
.******************************************************************************
.* Date           : 24/02/1997 (Re written V5.04.01)                          *
.* Author         : Andrea Thomson                                            *
.* Function       :                                                           *
.* Modifications  :                                                           *
.*        V12.00.01 30/05/2025  Jacob Jackson  TSK 0955096                    *
.*                  Alphanumeric changes                                      *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added include SEXDSCIO                                    *
.*        V10.08.01 03/10/2016 Ebon Clements   TSK 0813912                    *
.*                  Added multi hospital functionality                        *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*       V5.08.01  28/08/2000  Tonii                                          *
.*                 Mods to accept Unknown and Indeterminate as valid patient  *
.*                 sex description                                            *
.******************************************************************************
.*       V5.07.02  20/07/1999  Caleb Sharman  SRF 633124                      *
.*                 Mods to print expected date                                *
.*       V5.07.01  22/06/1999  Caleb Sharman  SRF 632422                      *
.*                 Mods to print out report                                   *
.*       V5.07.B02 27/01/1999  Nicole Harrington 507 rebounds                 *
.*                 Mods to print century on dates                             *
.*       V5.07.B01 13/11/1998  Jim Stathopoulos                               *
.*                 507 Changes                                                *
.******************************************************************************
.*        V5.05.01 27/03/1998  Steve Armstrong   SRF 620423                   *
.*                 Mods to include current on-leave patients                  *
.******************************************************************************
+
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Control file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC            * Master file
          INC       PATMI1FD/INC            * Admission file
          INC       PMSVX1FD/INC            * Admission file
          INC       PATONLFD/INC            * On leave file
          INC       PATTRNFD/INC            * Transfer file
          INC       PATWR1FD/INC            * Ward file
.
. ----- Program Variables -----
.
CDYSDAYS  FORM      6                       * CALCDAYS no of days
CDYSFDTE  DIM       8                       * CALCDAYS from date (ccyymmdd)
CDYSTDTE  DIM       8                       * CALCDAYS to date (ccyymmdd)
CDYSYEAR  FORM      2                       * CALCDAYS year
CURDTE    DIM       8                       * Current date (ccyymmdd)
.
DSPEDDTE  DIM       10                      * Display end date (dd/mm/ccyy)
DSPERDTE  DIM       10                      * Display exp return date (dd/mm/yy)
DSPLEDTE  DIM       10                      * Display leave date (dd/mm/yy)
DSPREDTE  DIM       10                      * Display return date (dd/mm/yy)
DSPSTDTE  DIM       10                      * Display start date (dd/mm/ccyy)
.
ENDDTE    DIM       8                       * End date (ccyymmdd)
.
HOSPID    DIM       3
HOSPNAME  DIM       50
.
LVEDTE    DIM       8                       * Leave date (ccyymmdd)
LVETIME   DIM       5                       * Leave time (hh:mm)
LVETYPE   DIM       3                       * Leave type
.
NEWPGWRD  FORM      1                       * New page for each ward
.
RETDTE    DIM       8                       * Leave date (ccyymmdd)
RETTIME   DIM       5                       * Leave time (hh:mm)
.
SAVADMN   DIM       8                       * Save the admin no
SAVWARD   DIM       3                       * Save the ward
STRTDTE   DIM       8                       * Start date (ccyymmdd)
.
TOTDYCNT  FORM      4                       * Total days counter
TOTRDCNT  FORM      4                       * Total record counter
.
WRDDESC   DIM       20                      * Ward description
WRDDYCNT  FORM      4                       * Ward days counter
.
. ----- Program Constants -----
.
ADMIT     INIT      "Admitted  "
DISCH     INIT      "Discharged"
ONLVE     INIT      "On Leave  "
.
PRGID     INIT      "IBABIL80"
PRGNAM    INIT      "Patient On Leave Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,ML1000
.
ML3000    CALL      PAGE0000                * New page question
          BRANCH    EXIT,ML1000
.
          CALL      KHOS0000                * keyin hospital
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
          GOTO      ML1000
.
ML4000    CALL      PROC0000                * Process the files
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000             Called by: ML0000   *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patonlvf";
          OPEN      PATONLV1,"patonlvf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          READ      CONTROLF,TEN;*13,PTCNTLVE
          READ      CONTROLF,TWENTY1;*205,PTCNUKEY
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          MOVE      ONE,CNEWDS              * ? keyin on display
          MOVE      ONE,CNOUNDLN            * Dont print report header
          CLOCK     TIME,CTIMEIS
          PACK      CURDTE,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE 
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          MOVE      ZERO,CPAGENO            * Reset the page no
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Date Range":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      "17",CCOL
          DISPLAY   *P1:9,"Starting Date : ",*EF:
                    *P1:10,"Ending Date   : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "9",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      STRTDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE
.
          CALL      PACDATE                 * Format the start date
          MOVE      CPCDATE,DSPSTDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPSTDTE;
.
          MATCH     STRTDTE,CURDTE 
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Start date must not be in the future.  ";
            CALL      HITENTER 
            GOTO      DATE1000
          ENDIF
.
. ----- Keyin end date -----
.
DATE2000  MOVE      "10",CVERT
          UNPACK    STRTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE
.
          CALL      PACDATE                 * Format the end date
          MOVE      CPCDATE,DSPEDDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPEDDTE;
.
          MATCH     ENDDTE,CURDTE 
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must not be in the future.  ";
            CALL      HITENTER 
            GOTO      DATE2000
          ENDIF
.
          MATCH     STRTDTE,ENDDTE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must after the start date.  ";
            CALL      HITENTER 
            GOTO      DATE2000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  PAGE0000              Called by: ML0000   *
.*                              New Page Question                             *
.******************************************************************************
PAGE0000  MOVE      ZERO,NEWPGWRD
          DISPLAY   *P1:12,*EF,"Print Each Ward on a Separate Page (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ";
.
PAGE1000  MOVE      ANSN,ANS
          KEYIN     *P44:12,*V2LON,*RV,ANS;
.
          RESET     ANS
          REP       UPPLOW,ANS
.
          MATCH     SP1,ANS
          GOTO      PAGE2000 IF EQUAL       * Spaces, print new page
.
          MATCH     EXITCHAR,ANS
          GOTO      PAGE9500 IF EQUAL       * Exit char, exit
.
          MATCH     ANSN,ANS
          GOTO      PAGE2000 IF EQUAL       * Dont print new page for each ward
.
          MATCH     ANSY,ANS
          GOTO      PAGE3000 IF EQUAL       * Print new page for each ward
.
          BEEP
          GOTO      PAGE1000
.
PAGE2000  DISPLAY   *P44:12,*EL,*V2LON,"No";
          MOVE      ZERO,NEWPGWRD
          GOTO      PAGE9000
.
PAGE3000  DISPLAY   *P44:12,*EL,*V2LON,"Yes";
          MOVE      ONE,NEWPGWRD
.
PAGE9000  MOVE      ZERO,EXIT
          GOTO      PAGE9999
.
PAGE9500  MOVE      ONE,EXIT
PAGE9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Process The Files                             *
.******************************************************************************
PROC0000  MOVE      ZERO,WRDDYCNT           * Reset the ward days counter
          MOVE      ZERO,TOTDYCNT           * Reset the total days counter
          MOVE      ZERO,TOTRDCNT           * Reset the total record counter
          MOVE      SP6,SAVWARD             * Reset the save ward
          MOVE      ZERO,CPAGENO            * Reset the page counter
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Printing : ";
          CALL      HEAD0000                * Print report header
.
. ----- Loop thru the ward file -----
.
          PACK      KEY6,SP10
          CALL      RDSWARD1                * Position on & read next ward file
PROC1000  CALL      RDKWARD1                  record
          BRANCH    OVRCD,PROC8000
.
          MATCH     SP3,WBED
          GOTO      PROC5000 IF EQUAL       * Blank bed, set up description
.
. ----- Ward/bed found, check if any on leave patients were in this -----
. ----- ward/bed within the date range -----
.
PROC2000  PACK      KEY30,WWARD,WBED,STRTDTE,SP30
          CALL      RDSTRAN1                * Position on & read next transfer
PROC3000  CALL      RDKTRAN1                  file record
          BRANCH    OVRCD,PROC1000
.
          DISPLAY   *P12:24,*V2LON,TWARD,SP2,TBED,SP2,TADMN;
.
          MATCH     WWARD,TWARD
          GOTO      PROC1000 IF NOT EQUAL   * Diff ward
.
          MATCH     WBED,TBED
          GOTO      PROC1000 IF NOT EQUAL   * Diff bed
.
          MATCH     TDATE,ENDDTE 
          GOTO      PROC1000 IF LESS        * End date < transfer date
.
          MATCH     ANSL,TMOVE
          GOTO      PROC3000 IF NOT EQUAL   * Not a leave record
.
          CALL      CSTL0000                * Check if on short term leave
          BRANCH    EXIT,PROC3000
.
. ----- Leave record found, now get the return record -----
.
          MOVE      TDATE,LVEDTE            * Leave date
          MOVE      TTIME,LVETIME           * Leave time
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          CALL      RDTRAN2                 * Read a transfer file record
          BRANCH    OVRCD,PROC3000
.
          MOVE      TADMN,SAVADMN           * Save the admin no
PROC4000  CALL      RDKTRAN2                * Read next transfer file record
          BRANCH    OVRCD,PROC4100
.
          MATCH     SAVADMN,TADMN
          GOTO      PROC4100 IF NOT EQUAL   * Diff admin no
.
          MATCH     ANSR,TMOVE
          GOTO      PROC3000 IF NOT EQUAL   * Not a return record
.
          GOTO      PROC4500
.
.         Patient is still on leave
.
PROC4100  MOVE      SP8,TDATE
          MOVE      SP6,TTIME
.
. ----- Return record found, print data -----
.
PROC4500  MOVE      TDATE,RETDTE            * Return date
          MOVE      TTIME,RETTIME           * Return time
.
          PACK      KEY8,SAVADMN
          CALL      RDMISS1                 * Read an admin file record
          BRANCH    OVRCD,PROC3000
.
          IF         IBCNMHOS=1
            MATCH     SP70,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID          * Check hospital id
              GOTO      PROC3000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          PACK      KEY8,AURNO
          CALL      RDMAST1                 * Read a master file record
          BRANCH    OVRCD,PROC3000
.
          CALL      PRNT0000                * Print a report line
          GOTO      PROC3000
.
PROC5000  MOVE      WBDESC,WRDDESC          * Ward description
          BRANCH    WINPUT,PROC2000         * Ward only input ward
          GOTO      PROC1000
.
PROC8000  CALL      UND132                  * Print underline
          PRINT     *55,"| Ward  ",WRDDYCNT,*68,"|",*N:
                    *55,"*------------*",*N:
                    *55,"| Grand ",TOTDYCNT,*68,"|",*N:
                    *55,"*------------*",*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,TOTRDCNT,*HOFF," on leave patient(s) ":
                    "found.  ";
          CALL      HITENTER
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PROC0000 *
.*                             Print Report Header        PRNT0000 & TAIL0000 *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print report header
          PRINT     *40,"Start Date : ",DSPSTDTE,*N:
                    *40,"End Date   : ",DSPEDDTE
          ADD       "2",CLNO
.
          IF       IBCNMHOS =1
            PRINT    *40,"Hospital   : ",HOSPID,"    ",HOSPNAME,*N
            ADD      TWO,CLNO
          ENDIF
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  CSTL0000              Called by: PROC0000 *
.*                        Check If On Short Term Leave                        *
.******************************************************************************
CSTL0000  MOVE      SP3,LVETYPE             * Clear the leave type
          COMPARE   ONE,PTCNTLVE
          GOTO      CSTL9000 IF NOT EQUAL   * Not using type of leave
.
          BRANCH    PTCNUKEY,CSTL1000,CSTL1000,CSTL1000
.
          MATCH     SP3,PTTRLTYP
          GOTO      CSTL9500 IF EQUAL       * No on leave type
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,CSTL9500
.
          MATCH     SP1,TCINDC1
          GOTO      CSTL9500 IF NOT EQUAL   * Not short term leave
.
          MOVE      ACODE,LVETYPE           * Leave type
          GOTO      CSTL9000
.
CSTL1000  MATCH     SP3,PTTRLTYP
          GOTO      CSTL9000 IF EQUAL       * No on leave type
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,CSTL9000
.
          MOVE      ACODE,LVETYPE           * Leave type
.
CSTL9000  MOVE      ZERO,EXIT
          GOTO      CSTL9999
.
CSTL9500  MOVE      ONE,EXIT
CSTL9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PROC0000 *
.*                             Print A Report Line                            *
.******************************************************************************
PRNT0000  MATCH     SAVWARD,WWARD
          GOTO      PRNT1000 IF EQUAL       * Same ward, continue
.
          MOVE      WWARD,SAVWARD           * Save the ward
          IF        TOTRDCNT=0
            CALL      SHED0000              * Print report sub header
          ELSE
            CALL      TAIL0000              * Print tail
            CALL      SHED0000              * Print report sub header
          ENDIF
.
PRNT1000  IF        CLNO>=58
            CALL      UND132                * Print underline
            CALL      HEAD0000              * Print report header
            CALL      SHED0000              * Print report sub header
          ENDIF
.
          PACK      KEY23,SP30
          MOVE      PGNAME,ANS
          PACK      KEY23,ANS,DOT,PSNAME,SP30 * Patient name
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,KEY7
.* ******************************************   End of Changes         *C-49016
.
          MOVE      ZERO,CDYSDAYS
          MOVE      LVEDTE,CDYSFDTE
          MATCH     SP8,RETDTE
          IF        @EQUAL
            MOVE      CURDTE,CDYSTDTE       * If not returned used current date
          ELSE 
            MOVE      RETDTE,CDYSTDTE
          ENDIF
          CALL      CALCDAYS                * Calculate days on leave
          SUB       ONE,CDYSDAYS
.
          PACK      KEY10,SP10
          LOAD      KEY10,ASTAT,SP10,ADMIT,DISCH,ONLVE,SP10
.
          UNPACK    LVEDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the leave date
          MOVE      CPCDATE,DSPLEDTE         * Leave date
.
          PACK      DSPERDTE,SP10           * Clear the expected return date
          IF        ASTAT=4
            PACK      KEY14,WWARD,WBED,AADMNO
            CALL      RDONLV1               * Read an on leave file record
            IF        OVRCD<>1
              UNPACK    OERDATE,CCENT,CYEAR,CMON,CDAY
              CALL      PACDATE             * Format the expected return date
              MOVE      CPCDATE,DSPERDTE     * Expected return date
            ENDIF
          ENDIF
.
          UNPACK    RETDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the return date
          MOVE      CPCDATE,DSPREDTE         * Return date
.
          IF        PTCNTLVE=1
            PRINT     *1,"|",AURNO,*10,"| ",KEY23,*35,"| ",KEY7:
                      *44,"|  ",WWARD,SLASH,WBED,*55,"|     ",CDYSDAYS:
                      *68,"| ",KEY10,*81,"|",DSPLEDTE," ",LVETIME:
                      *98,"|",DSPERDTE,*109,"|",DSPREDTE," ",RETTIME:
                      *126,"| ",LVETYPE,*132,"|"
          ELSE
            PRINT     *1,"|",AURNO,*10,"| ",KEY23,*35,"| ",KEY7:
                      *44,"|  ",WWARD,SLASH,WBED,*55,"|     ",CDYSDAYS:
                      *68,"|   ",KEY10,*85,"|",DSPLEDTE," ",LVETIME:
                      *103,"|",DSPERDTE,*114,"|",DSPREDTE," ",RETTIME:
                      *132,"|"
          ENDIF
.
          ADD       ONE,CLNO
          ADD       CDYSDAYS,WRDDYCNT       * Increment the ward days counter
          ADD       CDYSDAYS,TOTDYCNT       * Increment the total days counter
          ADD       "1",TOTRDCNT            * Increment the total record counter
          DISPLAY   *P46:24,*V2LON,WWARD,SP2,WBED,SP2,TADMN;
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
.*                                  SHED0000              Called by: PRNT0000 *
.*                           Print Report Sub Header                          *
.******************************************************************************
SHED0000  PRINT     *40,"Ward       : ",WWARD,SP2,WRDDESC,*N
          CALL      UND132                  * Print underline
          IF        PTCNTLVE=1
            PRINT     *1,"|  U/R ",*10,"|",*35,"|",*44,"|",*55,"|":
                      *68,"|  Current",*81,"|    On Leave ",*98,"| Expected":
                      *109,"|  Actual Return",*126,"| T/L",*132,"|",*N:
                      *1,"| Number",*10,"| Patient's Name",*35,"|  Sex":
                      *44,"| Ward/Bed",*55,"| No of Days":
                      *68,"|   Status",*81,"|   Date   Time ":
                      *98,"|  Return",*109,"|   Date   Time",*126,"|",*132,"|"
          ELSE
            PRINT     *1,"|  U/R ",*10,"|",*35,"|",*44,"|",*55,"|",*68,"|":
                      *85,"|    On Leave ",*103,"| Expected":
                      *114,"|  Actual Return",*132,"|",*N:
                      *1,"| Number",*10,"| Patient's Name",*35,"|  Sex":
                      *44,"| Ward/Bed",*55,"| No of Days":
                      *68,"| Current Status",*85,"|   Date   Time ":
                      *103,"|  Return",*114,"|   Date   Time",*132,"|"
          ENDIF
          CALL      UND132                  * Print underline
          ADD       "4",CLNO
.
SHED9000  MOVE      ZERO,EXIT
SHED9999  RETURN
+
.******************************************************************************
.*                                  TAIL0000              Called by: PRNT0000 *
.*                              Print Report Tail                             *
.******************************************************************************
TAIL0000  CALL      UND132                  * Print underline
          PRINT     *55,"| Ward  ",WRDDYCNT,*68,"|",*N:
                    *55,"*------------*",*N
          ADD       "3",CLNO
.
          IF        NEWPGWRD=1 | CLNO>=51
            CALL      HEAD0000              * Print report header
          ENDIF
.
          MOVE      ZERO,WRDDYCNT           * Reset the ward days counter
TAIL9000  MOVE      ZERO,EXIT
TAIL9999  RETURN
+
.         ======================================================================
.         KHOS0000: Keyin hospital ID
.         ======================================================================
KHOS0000  MOVE       ZERO,EXIT
          MOVE       SP10,HOSPID
.
          COMPARE    ONE,IBCNMHOS                * Exit if not multi hospital
          GOTO       KHOS9999 IF NOT EQUAL
.
          DISPLAY    *P1:15,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       "15",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS1000,KHOS2000
          MOVE       PTHSHOSP,HOSPID
          MOVE       PTHSNAME,HOSPNAME
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS1000  MOVE       "All Hospitals",HOSPNAME
          MOVE       SP10,PTHSHOSP
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS2000  MOVE       ONE,EXIT
KHOS9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS                * Calculate no of days
          INC       PATHSPKY
          INC       SEXDSCIO
.
          INC       PATCODIO/INC            * Codes file
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC            * Admission file
          INC       PMSVX1IO/INC            * Admission file
          INC       PATONLIO/INC            * On leave file
          INC       PATTRNIO/INC            * Transfer file
          INC       PATWR1IO/INC            * Ward file
+
.


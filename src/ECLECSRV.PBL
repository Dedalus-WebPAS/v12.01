.******************************************************************************
.* System         : Eclipse                                                   *
.* Program        : ECLECSRV.PBL                                              *
.* Name           : Eclipse Eligibility Check Server                          *
.******************************************************************************
.* Date           : 19/01/2009                                                *
.* Author         : Davin Sloan                                               *
.* Function       : Process outstanding requests for online eligibility checks*
.*                                                                            *
.* Modifications  :                                                           *
.*                 V10.14.01 22/05/2019  Peter Vela       TSK 0875081         *
.*                           Added validation for expired certificate before  *
.*                           each occurence of WRPMETR1                       *
.*                 V10.11.03 07/09/2017 Peter Vela     TSK 0841134            *
.*                           Added validation for old OECs in PROC1000        *
.*                 V10.11.02 02/08/2017 Ebon Clements  TSK 0841134            *
.*                           Fixed sleep timout waiting minutes               *
.*                 V10.11.01 02/08/2017 Ebon Clements  TSK 0841134            *
.*                           Changed timout to use parameter - PTCNOECM       *
.*                 V10.08.01 14/09/2016 Peter Vela TSK 0826056                *
.*                           Increased timeout to 10 minutes                  *
.*                 V10.06.01 17/08/2015 Peter Vela CAR 320723                 *
.*                           Added Free Format validation in RSTA1000         *
.*                 V10.01.01 23/12/2010 Peter Vela CAR 233856                 *
.*                           Check for OEC Report Requests in PROC0000        *
.*                  V9.12.01 21/07/2009 Peter Vela CAR 193566                 *
.*                           Fixed read of pmsetraf                           *
.*                           10/07/2009 Davin S    CAR 193566                 *
.*                           Changed to continuously loop over pmsoecaf       *
.*                  V9.11.01 17/04/2009 Peter Vela CAR 183976                 *
.*                           Added Multi Hospital                             *
.*                                                                            *
.******************************************************************************
.
          INC       STD002FD
.
          INC       PATCRTFD/INC
          INC       PATCONFD/INC
          INC       PMSETRFD/INC       * Eclipse Transaction Request Table
          INC       PMSOECFD/INC
          INC       PMSVX1FD/INC
          INC       PATHSPFD/INC
.
. LOCAL VARIABLES
. ---------------
WAITTIME  FORM      4
SAVKEY16  DIM       16
.
CALCTIME  FORM      6
CURRDATE  DIM       8
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
DIM8      DIM       8
CDYSYEAR  FORM      2
FIRSTDAT  DIM       8
LASTDATE  DIM       8
FIRSTIME  DIM       4
LASTTIME  DIM       4
FIRSTHR   DIM       2
FIRSTMIN  DIM       2
LASTHOUR  DIM       2
LASTMIN   DIM       2
HOUR1     FORM      6
HOUR2     FORM      6
MINUTE1   FORM      3
MINUTE2   FORM      3
TOTTIME1  FORM      4
TOTTIME2  FORM      4
.
. PROGRAM CONSTANTS
. ----------------- 
.
PRGID     INIT      "ECLECSRV"
PRGNAM    INIT      "Eclipse Eligibility Check Server"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                 MAIN0000                                   *
.*                       Controlling Logic (Mainline code)                    *
.******************************************************************************
MAIN0000  CALL      SETX0000                * Setup common variables
.         
          CALL      INIT0000                * Initialise variables & open files
.
          CALL      PROC0000                * Process eclipse eligibility table
.
MAIN9999  STOP
+
.*****************************************************************************
.*                                  INIT0000                                 *
.*                       Initialise Variable & Open Files                    *
.*****************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"pmsetraf"
          OPEN      PMSETRA1,"pmsetraf"
.
          DISPLAY   *P64:24,"pmsoecaf"
          OPEN      PMSOECA1,"pmsoecaf"
          OPEN      PMSOECA2,"pmsoecaf"
. 
          DISPLAY   *P64:24,"patcrtaf"
          OPEN      PATCRTA1,"patcrtaf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          READ      CONTROLF,HUND14;*145,PTCNCLID
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND20;*247,PTCNOECM
.
          MOVE      ZERO,WAITTIME
          SQUEEZE   PTCNOECM                * Use parameter or 2 minute default
          MOVE      PTCNOECM,WAITTIME
          IF        WAITTIME=0
            MOVE     TWO,WAITTIME
          ENDIF
          ASSIGN    (WAITTIME*60),WAITTIME 
.
.         CALL      IBACLOCK                * Get the current date
.         PACK      CURRDATE,CCC,CYY,CMM,CDD
.         REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                PROC0000                                   *
.*                   Process eclipse OEC table                               *
.*****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Processing Eclipse OEC ECF Requests...";
.
.....     Loop thru eligibility requests header table.
.....     If status is 'received', need to check if report is ready yet:
.....       - write an eligibilty request to pmsetraf if none currently
.....         'waiting' or 'processing'.
.
PROC0500  PACK      KEY16,SP1,TWO,SP70
          CALL      RSPMOEC2
PROC1000  CALL      RKPMOEC2                * loop through new ER table
          BRANCH    OVRCD,PROC9000
.
          MATCH     " 2",PMOESTAT           * ER status is 'received' ?
          GOTO      PROC9000 IF NOT EQUAL
.
          MOVE      PMOECDTE,FIRSTDAT
          MOVE      PMOECTIM,D5
          REP       ": ",D5
          SQUEEZE   D5
          PACK      FIRSTIME,D5,SP70
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD * current date
          REP       " 0",CURRDATE
.
          MOVE      CURRDATE,LASTDATE
          MOVE      CTIMEIS,D5
          REP       ": ",D5
          SQUEEZE   D5
          PACK      LASTTIME,D5,SP70
.
          CALL      TIMEDIFF
.
          IF        CALCTIME > 120
            PACK      SAVKEY16,PMOESTAT,PMOEHOSP,PMOEVISN,PMOECNTR,SP70
.
            PACK      KEY11,PMOEVISN,PMOECNTR,SP70
            CALL      RDPMOEC1
            IF        OVRCD=0
              MOVE      " 3",PMOESTAT
              CALL      UPPMOEC1
            ENDIF
.
            MOVE      SAVKEY16,KEY16
            CALL      RDPMOEC2
.
            GOTO      PROC1000
.
          ENDIF
.
          PACK      KEY51,SP1,SP1,SEVEN,SP70
          CALL      RSPMETR1
PROC2000  CALL      RKPMETR1
          BRANCH    OVRCD,PROC3000          * no request on pmsetr, so write
.
          MATCH     SP1,PMETSTAT
          GOTO      PROC3000 IF NOT EQUAL       * request already waiting
.
          MATCH     " 7",PMETTYPE
          GOTO      PROC3000 IF NOT EQUAL
.
          MATCH     PMETTRID,PMOETRID
          GOTO      PROC1000 IF EQUAL 
.
          GOTO      PROC2000
.
PROC3000  PACK      KEY51,ZERO,SP1,SEVEN,SP70
          CALL      RSPMETR1
PROC3500  CALL      RKPMETR1
          BRANCH    OVRCD,PROC4000 
.
          MATCH     "0",PMETSTAT
          GOTO      PROC4000 IF NOT EQUAL       * request already processing
.
          MATCH     " 7",PMETTYPE
          GOTO      PROC4000 IF NOT EQUAL 
.
          MATCH     PMETTRID,PMOETRID
          GOTO      PROC1000 IF EQUAL
.
          GOTO      PROC3500
.
PROC4000  PACK      KEY51,SP1,SP1,EIGHT,SP70
          CALL      RSPMETR1
PROC4500  CALL      RKPMETR1
          BRANCH    OVRCD,PROC5000          * no request on pmsetr, so write
.
          MATCH     SP1,PMETSTAT
          GOTO      PROC5000 IF NOT EQUAL       * request already waiting
.
          MATCH     " 8",PMETTYPE
          GOTO      PROC5000 IF NOT EQUAL
.
          MATCH     PMETTRID,PMOETRID
          GOTO      PROC1000 IF EQUAL
.
          GOTO      PROC4500
.
PROC5000  PACK      KEY51,ZERO,SP1,EIGHT,SP70
          CALL      RSPMETR1
PROC5500  CALL      RKPMETR1
          BRANCH    OVRCD,PROC8000
.
          MATCH     "0",PMETSTAT
          GOTO      PROC8000 IF NOT EQUAL       * request already processing
.
          MATCH     " 8",PMETTYPE
          GOTO      PROC8000 IF NOT EQUAL
.
          MATCH     PMETTRID,PMOETRID
          GOTO      PROC1000 IF EQUAL
.
          GOTO      PROC5500
.
PROC8000  CALL      RSTA0000                * retrieve status/report
.
          GOTO      PROC1000
.
PROC9000  DISPLAY   *WWAITTIME;             * wait for X min.
          GOTO      PROC0500                * check for more oec records
.
PROC9999  RETURN
+
.*****************************************************************************
.*                                RSTA0000                                   *
.*               Retrieve Eclipse Transaction Status/Report                  *
.*****************************************************************************
RSTA0000  MOVE      SP1,PMETSTAT
          MOVE      " 7",PMETTYPE
          MOVE      PMOETRID,PMETTRID
          MOVE      SP70,PMETBATN
          MOVE      PMOEVISN,PMETINVN
          MOVE      SP70,PMETUDTE
          MOVE      SP70,PMETUTIM
          MOVE      SP70,PMETSPAR
.
RSTA1000  CALL      IBACLOCK                     * get current date/time
          PACK      PMETCDTE,CCC,CYY,CMM,CDD
          REP       " 0",PMETCDTE
          MOVE      CTIMEIS,PMETCTIM
          REP       " 0",PMETCTIM
.
          MOVE      SP70,PMETLOCN
          MOVE      SP70,PMETSNID
.
          IF        IBCNMHOS=ONE
.
            PACK      KEY8,PMOEVISN,SP70
            CALL      RDPMVX11
            BRANCH    OVRCD,RSTA1500
.
            MATCH     SP70,PMVXMHOS
            GOTO      RSTA1500 IF EQUAL
            GOTO      RSTA1500 IF EOS
.
            PACK      KEY3,PMVXMHOS,SP70
            CALL      RDPTHSP1
            BRANCH    OVRCD,RSTA1500
.
            MATCH     SP70,PTHSLOCN
            GOTO      RSTA1500 IF EQUAL
            GOTO      RSTA1500 IF EOS
.
            MOVE      PTHSLOCN,PMETLOCN
.
            CALL      VLCN0000
            BRANCH    EXIT,RSTA9999
.
            GOTO      RSTA5000
.
RSTA1500    MATCH     SP70,PMOEHOSP
            GOTO      RSTA2000 IF EQUAL
            GOTO      RSTA2000 IF EOS
.
            PACK      KEY3,PMOEHOSP,SP70
            CALL      RDPTHSP1
            BRANCH    OVRCD,RSTA2000
.
            MATCH     SP70,PTHSLOCN
            GOTO      RSTA2000 IF EQUAL
            GOTO      RSTA2000 IF EOS
.
            MOVE      PTHSLOCN,PMETLOCN
.
          ELSE
.
RSTA2000    MOVE      PTCNCLID,PMETLOCN
.
          ENDIF
.
          CALL      VLCN0000
          BRANCH    EXIT,RSTA9999
.
RSTA5000  CALL      GSID0000
.
          PACK      KEY51,PMETSTAT,PMETTYPE,PMETLOCN,PMETCDTE,PMETCTIM,PMETTRID
          CALL      RDPMETR1
          IF        OVRCD = 0
            GOTO      RSTA1000
          ENDIF
.
          CALL      WRPMETR1                     * write transaction request
.
RSTA9999  RETURN
.
.*****************************************************************************
.*                                GSID0000                                   *
.*                   Get sender id from certificate table                    *
.*****************************************************************************
.
.         Get the certificate that covers today
.
GSID0000  PACK      KEY16,PMETLOCN,PMETCDTE
          CALL      RDPTCTI1                     * record found for today ?
          COMPARE   ZERO,OVRCD
          GOTO      GSID9000 IF EQUAL            * yes - use this certificate id
.
          CALL      RPPTCTI1                     * no - read previous record
          IF        OVRCD = 0
            MATCH     PMETCDTE,PTCITDTE          * within date range ?
            GOTO      GSID9000 IF NOT LESS       * yes - use this certificate id
          ENDIF
.
          MOVE      SP70,PMETSNID                * no current certificate
          GOTO      GSID9999
.
GSID9000  MOVE      PTCISNID,PMETSNID            * use certificate sender id
GSID9999  RETURN
+
.*****************************************************************************
.*                                VLCN0000                                   *
.*                   Validate Location ID Certificate Dates                  *
.*****************************************************************************
VLCN0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,PMETLOCN
          GOTO      VLCN9000 IF EQUAL
          GOTO      VLCN9000 IF EOS
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          PACK      KEY16,PMETLOCN,Z70
          CALL      RSPTCTI1
VLCN1000  CALL      RPPTCTI1
          BRANCH    OVRCD,VLCN9000
.
          MATCH     PMETLOCN,PTCILOCN
          GOTO      VLCN9000 IF NOT EQUAL
.
          MATCH     PTCIFDTE,DIM8
          GOTO      VLCN1000 IF LESS
.
          MATCH     DIM8,PTCITDTE
          GOTO      VLCN1000 IF LESS
.
          GOTO      VLCN9999
.
VLCN9000  MOVE      ONE,EXIT
.
VLCN9999  RETURN
+
.
.*****************************************************************************
.*        I/O Includes                                                       *
.*****************************************************************************
.
          INC       STD002IO
.
          INC       TIMEDIFF
          INC       CALCDAYS
.
          INC       PATCRTIO/INC       
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC
          INC       PMSETRIO/INC
          INC       PMSOECIO/INC


. ***************************************************************
. *                                                             *
. *                 P R O G R A M  S U M M A R Y                *
. *                 - - - - - - -  - - - - - - -                *
. *                                                             *
. *     Program Name   :  SHOWLINK                              *
. *                                                             *
. *     Function       :  Create systable link from DPATH       *
. *                                                             *
. *     Modifications  :  V5.01 26.05.89 B.G.Ackland (I.B.A.)   *
. *                             Remove Need For Linking systable*
. *                                                             *
. *                    V5.01.01 02.07.90 H. Crick    (I.B.A.)   *
. *                             Recompiled for v5.01            *
. *                                                             *
. *                * A T T E N T I O N *                        *
. *                ---------------------                        *
. *        This   program   is  the   property  of              *
. *        Information Builders Australia  Pty Ltd              *
. *        and must not be utilised,  ENQified  or              *
. *        copied in any manner whatsoever without              *
. *        the  express   written   permission  of              *
. *        Information Builders Australia Pty Ltd.              *
. *                                                             *
. ***************************************************************
.
          INC       STD001FD
          INC       SYSTABFD/INC
.
TABLELIN  FILE      ASCII, FIXED=256
.
LLCNT     FORM      2
ROW       FORM      2
DIM60     DIM       60
DIM3      DIM       3
PATH      DIM       25
SQLDIR    DIM       40
OPENSYS   DIM       50
LDAT      INIT      "linktab "
LDATEND   INIT      " >tablelin.txt"
COMMAND   DIM       80
.
SP17      INIT      "                 "
SP18      INIT      "                  "
SP40      INIT      "                                        "
.
PRGID     INIT      "SHOWLINK"
PRGNAM    INIT      "Display the Links on a Database"
VERSION   INIT      "V12.01.00"
.
          CALL      DISPHEAD
MAIN1000  KEYIN     *P1:5,*EF,"Enter Systable Path : ",*V2LON,SQLDIR
          ENDSET    SQLDIR
          APPEND    SP40,SQLDIR
          RESET     SQLDIR
          MATCH     SP40,SQLDIR
          GOTO      MAIN9999 IF EQUAL
          CALL      SETP0000
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      SYSTABLE,OPENSYS
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      MAIN1500 IF EQUAL
          KEYIN     *P1:24,"Could NOT Open ",*DV,OPENSYS," Tap Enter.",ANS
          GOTO      MAIN1000
MAIN1500  KEYIN     *P23:5,*EL,*V2LON,*DV,SQLDIR,*HOFF,*P65:5,"Ok (":
                    *V2LON,"y",*HOFF,"/",*V2LON,"n",*HOFF,") ? ",*V2LON,ANS
          MATCH     "n",ANS
          GOTO      MAIN1000 IF EQUAL
          MATCH     "y",ANS
          GOTO      MAIN1500 IF NOT EQUAL
.
          PACK      KEY26,SP10,SP10,SP6
          DISPLAY   *P1:7,*V2LON,*ULON,*EF,"Table             ":
                    *P20:7,"Link                                          ";
          MOVE      "7",ROW
          CALL      RSSYTB1
MAIN3000  CALL      RKSYTB1
          BRANCH    OVRCD,MAIN9000
          MOVE      SYTBNAME,DIM3
          MATCH     "sys",DIM3             * ignore all system files
          GOTO      MAIN3000 IF EQUAL
.
          MOVE      SYTBPATH,DIM60
          ADD       ONE,ROW
          DISPLAY   *P1:ROW,SYTBNAME,SP1,DIM60
          COMPARE   "23",ROW
          GOTO      MAIN3000 IF NOT EQUAL
          DISPLAY   *P1:24;
          CALL      HITENTER
          MOVE      "7",ROW
          DISPLAY   *P1:8,*EF
          GOTO      MAIN3000
.
MAIN9000  DISPLAY   *P1:24,"List Complete. ";
          CALL      HITENTER
MAIN9999  CHAIN     PGM
.--------------------------------
. set directory path
.--------------------------------
SETP0000  MOVE      "40",LLCNT           * set up acct dir path
SETP1000  RESET     SQLDIR,LLCNT
          CMATCH    SP1,SQLDIR
          GOTO      SETP9000 IF NOT EQUAL
.
          SUB       ONE FROM LLCNT
          COMPARE   ZERO,LLCNT
          GOTO      SETP1000 IF NOT EQUAL
.
          CLEAR     SQLDIR
          GOTO      SETP9999
.
SETP9000  LENSET    SQLDIR
          RESET     SQLDIR
SETP9999  CLEAR     OPENSYS
          APPEND    SQLDIR,OPENSYS
          APPEND    "systables",OPENSYS
          RESET     OPENSYS
          RETURN
.--------------------------------
. set table name
.--------------------------------
SETT0000  MOVE      "18",LLCNT           * set up acct dir path
SETT1000  RESET     SYTBNAME,LLCNT
          CMATCH    SP1,SYTBNAME
          GOTO      SETT9999 IF NOT EQUAL
.
          SUB       ONE FROM LLCNT
          COMPARE   ZERO,LLCNT
          GOTO      SETT1000 IF NOT EQUAL
.
          CLEAR     SYTBNAME
          RETURN
.
SETT9999  LENSET    SYTBNAME
          RESET     SYTBNAME
          RETURN
.
          INC       SYSTABIO/INC
          INC       STD001IO
.

.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   EXTRALRT                                                    *
.* Desc      :   Patient Alert Data Extraction Program                       *
.*****************************************************************************
.* Date      :   26/02/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will extract the patient alerts into a         *
.*               pipe delimited file in a format that will feed into         *
.*               CONVALRT.                                                   *
.* Mods:                                                                     *
.*        V10.04.00 26/02/2014  Steve Armstrong  CAR 296128                  *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATALRFD/INC
          INC       PMSALNFD/INC
.
.
ALERTEXT  FILE      ASCII,FIXED=4096
.
.         Alert upload file layout - convalrt.txt
.         (as per CONVALRT.PBL)
.
.Name     Type    Length Description
.----     ----    ------ -----------
ALRTURNO  DIM       8    U/R Number                 (ptalurno)
ALRTCATG  DIM       2    Category (H1-H9)           (ptalcatg)
ALRTCODE  DIM       3    Category-Code              (ptalcode)
ALRTDATE  DIM       8    Alert Date (ccyymmdd)      (ptaldate)
ALRTCDTE  DIM       8    Creation Date (ccyymmdd)   (ptalcdte)
ALRTLIFE  DIM       1    Life Threatening Alert     (ptallife)
.                             (not used by webPAS)
ALRTREAC  DIM       3    Reaction Code (Cat wn)     (ptalreac)
ALRTUSID  DIM       10   User Id for creation       (ptalusid)
ALRTLSEV  DIM       1    Severity Level             (ptallsev)
.                             (0-9)
ALRTRCOM  DIM       20   Reaction Comment           (ptalrcom)
ALRTRDTE  DIM       8    Review Date (ccyymmdd)     (ptalrdte)
ALRTICDC  DIM       9    ICD Code                   (ptalicdc)
ALRTDTIN  DIM       8    Date Inactive (ccyymmdd)   (ptaldtin)
ALRTRQBY  DIM       10   Requested By (HCP)         (ptalrqby)
ALRTHOSP  DIM       3    Initiating Hospital        (ptalhosp)
ALRTEDAT  DIM       8    End Date (ccyymmdd)        (ptaledat)
ALRTCTIM  DIM       8    Create Time (hh:mm:ss)     (ptalctim)
ALRTUDAT  DIM       8    Update Date (ccyymmdd)     (ptaludat)
ALRTUTIM  DIM       8    Update Time (hh:mm:ss)     (ptalutim)
ALRTUUID  DIM       10   User who Updated           (ptaluuid)
ALRTCOMM  DIM       3920 Comments (56x70)           (pmancomm)
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COMCOUNT  FORM      10
EXTCOUNT  FORM      10
RECCOUNT  FORM      10
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
PRGID     INIT      "EXTRALRT"
PRGNAM    INIT      "Patient Alerts Extraction"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * Alert extract
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      CFIL0000               * create file
          BRANCH    EXIT,MAIN0100          * file exists
.
          CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patalrtf";
          OPEN      PATALRT1,"patalrtf"
.
          DISPLAY   *P64:24,"pmsalnaf";
          OPEN      PMSALNA1,"pmsalnaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data extraction                     *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Patient Alert Data Extraction"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run extraction
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                                  CFIL0000       Called by: MAIN0000       *
.*                         Create the extract file                           *
.*****************************************************************************
.
CFIL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ALERTEXT,"convalrt"     * extract file exists ?
          TRAPCLR   IO
          BRANCH    OVRCD,CFIL5000          * no - create it
.
CFIL1000  KEYIN     *P1:23,*B,*EF,"The extract file convalrt.txt already ":
                    "exists.":
                    *P1:24,"Overwrite it (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,")?":
                    *P21:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS                * "Y" entered ?
          GOTO      CFIL2000 IF EQUAL       * yes - continue
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      CFIL9100 IF EQUAL       * no
.
          BEEP
          GOTO      CFIL1000
.
.         Delete the existing file
.
CFIL2000  CLOSE     ALERTEXT,DELETE
.
.         Create a new extract file
.
CFIL5000  PREP      ALERTEXT,"convalrt"
.
          MOVE      ZERO,EXIT
          GOTO      CFIL9999
.
CFIL9100  MOVE      ONE,EXIT
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through the Patient Alerts file and extract each record       *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          MOVE      ZERO,EXTCOUNT                * initialise extract count
          MOVE      ZERO,COMCOUNT                * initialise comment count
          DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      SP20,KEY16
          CALL      RSPTALR1                     * position at start of file
PROC0500  CALL      RKPTALR1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          ADD       ONE,RECCOUNT                 * increment record counter
.
          DISPLAY   *P13:24,*V2LON,RECCOUNT
.
          MOVE      PTALURNO,ALRTURNO
          SQUEEZE   ALRTURNO
          MOVE      PTALCATG,ALRTCATG
          SQUEEZE   ALRTCATG
          MOVE      PTALCODE,ALRTCODE
          SQUEEZE   ALRTCODE
          MOVE      PTALDATE,ALRTDATE
          SQUEEZE   ALRTDATE
          MOVE      PTALCDTE,ALRTCDTE
          SQUEEZE   ALRTCDTE
          MOVE      PTALLIFE,ALRTLIFE
          STRIP     ALRTLIFE
          MOVE      PTALREAC,ALRTREAC
          SQUEEZE   ALRTREAC
          MOVE      PTALUSID,ALRTUSID
          SQUEEZE   ALRTUSID
          MOVE      PTALLSEV,ALRTLSEV
          STRIP     ALRTLSEV
          MOVE      PTALRCOM,ALRTRCOM
          STRIP     ALRTRCOM
          MOVE      PTALRDTE,ALRTRDTE
          SQUEEZE   ALRTRDTE
          MOVE      PTALICDC,ALRTICDC
          STRIP     ALRTICDC
          MOVE      PTALDTIN,ALRTDTIN
          SQUEEZE   ALRTDTIN
          MOVE      PTALEDAT,ALRTEDAT
          SQUEEZE   ALRTEDAT
          MOVE      PTALRQBY,ALRTRQBY
          SQUEEZE   ALRTRQBY
          MOVE      PTALHOSP,ALRTHOSP
          SQUEEZE   ALRTHOSP
          MOVE      PTALCTIM,ALRTCTIM
          SQUEEZE   ALRTCTIM
          MOVE      PTALUDAT,ALRTUDAT
          SQUEEZE   ALRTUDAT
          MOVE      PTALUTIM,ALRTUTIM
          SQUEEZE   ALRTUTIM
          MOVE      PTALUUID,ALRTUUID
          SQUEEZE   ALRTUUID
.
.         Now get the associated comments for this alert
.
          CLEAR     ALRTCOMM
          PACK      KEY19,PTALURNO,PTALCATG,PTALCODE,PTALCNTR,SP20
          CALL      RSPMALN1                     * position on U/R
PROC1000  CALL      RKPMALN1                     * read next record
          BRANCH    OVRCD,PROC2000               
.
          MATCH     PTALURNO,PMANURNO            * same U/R still ?
          GOTO      PROC2000 IF NOT EQUAL        * no
.
          MATCH     PTALCATG,PMANACAT            * same category still ?
          GOTO      PROC2000 IF NOT EQUAL        * no
.
          MATCH     PTALCODE,PMANACOD            * same code still ?
          GOTO      PROC2000 IF NOT EQUAL        * no
.
          MATCH     PTALCNTR,PMANCNTR            * same counter still ?
          GOTO      PROC2000 IF NOT EQUAL        * no
.
.         We have a comment line so load it
.
          APPEND    PMANCOMM,ALRTCOMM
          ADD       ONE,COMCOUNT                 * increment comment line count
          GOTO      PROC1000                     * get next record
.
PROC2000  RESET     ALRTCOMM
          STRIP     ALRTCOMM
          WRITE     ALERTEXT,SEQ;*+,ALRTURNO,PIPE,ALRTCATG,PIPE,ALRTCODE,PIPE:
                                    ALRTDATE,PIPE,ALRTCDTE,PIPE,ALRTLIFE,PIPE:
                                    ALRTREAC,PIPE,ALRTUSID,PIPE,ALRTLSEV,PIPE:
                                    ALRTRCOM,PIPE,ALRTRDTE,PIPE,ALRTICDC,PIPE:
                                    ALRTDTIN,PIPE,ALRTRQBY,PIPE,ALRTHOSP,PIPE:
                                    ALRTEDAT,PIPE,ALRTCTIM,PIPE,ALRTUDAT,PIPE:
                                    ALRTUTIM,PIPE,ALRTUUID,PIPE,ALRTCOMM,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:22,*EF,"Extraction complete.":
                    *P1:23,*V2LON,EXTCOUNT,*HOFF," alert records extracted.  ":
                    *P1:24,*V2LON,COMCOUNT,*HOFF," comment records extracted.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATALRIO/INC
          INC       PMSALNIO/INC

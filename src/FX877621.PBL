.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   FX877621                                                  *
.* Desc      :   Fix for new fields on oprdetaf from bokrx1af              *
.***************************************************************************
.* Date      :   05/07/2019                                                *
.* Author    :   Richa Phogat                                              *
.* Function  :   Copy values from bokrx1af to oprdetaf                     *
.*               for post op ward,bed, claim type and admission type       *
.* Modifications  :                                                        *
.* V11.03.01   22/03/2023  Ebon Clements     TSK 0909393                   *
.*             Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes   *
.* V10.12.00   05/07/20189 Richa Phogat  TSK 0842287                       *
.*             Copied from FX861441.PBL                                    *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRDEAFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
BBASDIAG  DIM       3
RECCOUNT  FORM      8
TOTALCNT  FORM      8
.
PRGID     INIT      "FX877621"
PRGNAM    INIT      "Fix New fields moved to oprdetaf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process oprdetaf
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA1,"oprdetaf"
.
          DISPLAY   *P64:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,"bokrx1af"
          OPEN      BOKRX1A1,"bokrx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". List Program":
                    *P1:6,*V2LON,TWO,*HOFF,". Fix Program"
.
OPTN0500  KEYIN     *P1:10,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:10,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.**************************************************************************
PROC0000  CALL      IBACLOCK
          MOVE      "Fixit - oprdetaf copy values from bokrx1af",PRGNAM
.
          CALL      HEAD80
          MOVE      ZERO,RECCOUNT          * Number of records updated
          MOVE      ZERO,TOTALCNT          * Number of errors
.
          CALL      UND80
          PRINT     *1,"| Theatre booking",*25,"| Post Op Ward/Bed":
                    *45,"| Claim Type ",*65,"| Admission Type",*80,"|"
          CALL      UND80
.
          PACK      KEY26,SP70
          CALL      RSOPDEA1
PROC1000  CALL      RKOPDEA1               * Loop theatre file       
          BRANCH    OVRCD,PROC9000
          ADD       ONE,TOTALCNT    
.
          DISPLAY   *P20:24,"Booking.: ",OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN:
                                        OPDASTAT,OPDACASE
.
          CALL      CBDI0000          * Check if any values to update
          BRANCH    EXIT,PROC1000 
.
          PACK      OPDAPOWD,BKRXPOWD,SP70
          PACK      OPDAPOBD,BKRXPOBD,SP70
          PACK      OPDAATYP,BKRECLSS,SP70
          PACK      OPDACTYP,BKRECLMT,SP70
.
          IF        COPTION=2
            CALL      UPOPDEA1
          ENDIF
.
          ADD       ONE,RECCOUNT           * Number of errors
          PRINT     *1,"| ",OPDADATE,OPDATIME,OPDACLIN,OPDASTAT,OPDACASE:
                    *25,"| ",OPDAPOWD,OPDAPOBD,*45,"| ",OPDACTYP,*65,"| ":
                    OPDAATYP,*80,"|"
.
          GOTO      PROC1000
.
.
PROC9000  
          CALL      UND80
          PRINT     *N,*1,"Total number of records Read: ",TOTALCNT
          PRINT     *N,*1,"Total number of records Updated: ",RECCOUNT
.
          DISPLAY   *P1:22,*EL,"Total Records to Update : ":
                    *V2LON,RECCOUNT,*HOFF,SP5;
.
          DISPLAY   *P1:23;
          CALL      HITENTER
.
PROC9999  RETURN
+
.**************************************************************************
.*                               CBDI0000              Called by: PROC0000*
.*       Check if any values should be updated                           .*
.**************************************************************************
CBDI0000  MOVE      ZERO,EXIT
          MATCH     SP70,OPDAPOWD
          GOTO      CBDI9000 IF NOT EQUAL
.
          MATCH     SP70,OPDAPOBD
          GOTO      CBDI9000 IF NOT EQUAL
.
          MATCH     SP70,OPDACTYP
          GOTO      CBDI9000 IF NOT EQUAL
.
          MATCH     SP70,OPDAATYP
          GOTO      CBDI9000 IF NOT EQUAL
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,CBDI9000
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDBKRX11
          BRANCH    OVRCD,CBDI9000
.
          MATCH     SP70,BKRECLMT
          GOTO      CBDI8000 IF NOT EQUAL
.
          MATCH     SP70,BKREATYP
          GOTO      CBDI8000 IF NOT EQUAL
.
          MATCH     SP70,BKRXPOWD
          GOTO      CBDI8000 IF NOT EQUAL
.
          MATCH     SP70,BKRXPOBD
          GOTO      CBDI8000 IF NOT EQUAL
.
CBDI8000  MOVE      ZERO,EXIT              * update required        
          GOTO      CBDI9999
.
CBDI9000  MOVE      ONE,EXIT               * no update required            
CBDI9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OPRDEAIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC

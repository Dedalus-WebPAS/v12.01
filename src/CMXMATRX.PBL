.******************************************************************************
.* System         : Patient Billing                                           *
.* Include Name   : CMXMATRX.PBL                                              *
.* Author         : Jill Habasque                                             *
.* Date           : 13/11/2000                                                *
.*                                                                            * 
.* Function       : Check if a casemix patient has a valid record in the      *
.*                  casemix matrix (pmscmtaf)                                 *
.*                                                                            * 
.* Requires       : A valid admission record                                  *
.*                : A valid discharge record                                  *
.*                : MSGFLAG - 0 error message to be displayed                 *
.*                            1 No display error message flag                 *
.*                            2 display matrix screen                         *
.*                                                                            * 
.* Returns        : CMCODE   Casemix code (Only if a matrix record found)     * 
.*                  CMDRG    Casemix code (Only if a matrix record found)     * 
.*                                                                            * 
.*                  CMXFLAG  0=patient classification                         *
.*                           1=charge using casemix                           * 
.*                           2=users to fix matrix code                       *
.*                                                                            * 
.*                  EXIT     0=matrix record found                            *
.*                           1=no matrix record found, check on patcmxaf      *
.*                                                                            * 
.* Modifications  :                                                           *
.*        V12.01.01 20/11/2025  J.Tan         TSK 0946593                     *
.*                  Mod Casepayment Items to sort using per-diem theatre fee  *
.*        V12.00.01 14/05/2025  Ebon Clements TSK 0955096                     *
.*                  Alphanueric visit number changes                          *
.*        V11.04.04 30/07/2024  J.Tan         TSK 0948860                     *
.*                  Added <td> for C/P Incomplete with Provisional CasemixCode*
.*        V11.04.03 03/07/2024  J.Tan         TSK 0942678                     *
.*                  Mod to display 'No matching Contract found'               *
.*        V11.04.02 13/03/2024 J.Tan           TSK 0910994                    *
.*                  Added Att.Doctor, Concession card type and Disc.UDF       *
.*        V11.04.01 06/03/2024 J.Tan           TSK 0943130                    *
.*                  Mod to set CEFFDATE(Effective date) for Matrix screen     *
.*        V11.03.01 15/09/2023 Alina Bhari     TSK 0936086                    *
.*                  Wrap table row with bold text - MTRX1900                  *
.*        V11.01.02 27/09/2021 J.Tan           TSK 0901515                    *
.*                  Added for Primary ICD procedures, 4th 5th 6th CMBS        *
.*        V11.01.01 01/07/2021  J.Tan          TSK 0908588                    *
.*                  Exclude Unqualified days for Min/Max days cut off-MTRX1720*
.*        V11.00.05 10/11/2020  J.Tan          TSK 0899562                    *
.*                  Fixed setting date for PATITMRD (GETM2000)                *
.*        V11.00.04 12/10/2020  J.Tan          TSK 0897475                    *
.*                  Fixed displaying Discharge destination description        *
.*        V11.00.03 29/09/2020  J.Tan          TSK 0896829                    *
.*                  Mod to use National(PTPGNDRG)instead of State (PTPGSDRG)  *
.*        V11.00.02 21/09/2020  J.Tan          TSK 0895330                    *
.*                  Mod to reposition read on the first counter Matrix table  *
.*        V11.00.01 06/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.14.01 28/06/2019  J.Tan          TSK 0857392                    *
.*                  Mod for ABF invoice                                       *
.*        V10.12.02 26/04/2018  J.Tan         TSK 0825826                     *
.*                  Added message for Contract ID with no rule (DIM6A)        *
.*        V10.12.01 01/09/2017  J.Tan         TSK 0837479                     *
.*                  Mod checking Consumption type                             *
.*        V10.08.02 21/09/2016  J.Tan         TSK 0826370                     *
.*                  Changed MCHGARR and MCHGAMT array to FORM3                *
.*        V10.08.01 26/08/2016  Don Nguyen     TSK 0312570                    *
.*                  Added check for PTCNDGED=1; call GTEDRG00 if set          *
.*        V10.07.01 20/07/2015  J.Tan          CAR 321345                     *
.*                  Changed MTRX1720 - to include ADYHOSP                     *
.*        V10.06.01 15/07/2015  J.Tan          CAR 319342                     *
.*                  Changed Discharge UDF to Care Class                       *
.*        V10.05.01 19/02/2015  J.Tan          CAR 311352                     *
.*                  Mods to check Discharge program (PRGID) for PATDISCH      *
.*        V10.04.01 19/06/2014  J.Tan          CAR 302452                     *
.*                  Increased array variables for MCHGARR and MCHGAMT         *
.*        V10.03.08 09/12/2013 J.Tan           CAR 294609                     *
.*                  Use System default Grouper vers.if Claim/HF version blank *
.*        V10.03.07 07/08/2013 J.Tan           CAR 288416                     *
.*                  Fixed CMCODE for Discharge Billing statement              *
.*        V10.03.06 05/08/2013  J.Tan          CAR 288060                     *
.*                  Fixed sorting Primary,Secondary and Tertiary CMBS         *
.*        V10.03.05 12/10/2012  J.Tan          CAR 274193                     *
.*                  Fixed checking for HF to search for Default               *
.*        V10.03.04 09/10/2012  J.Tan          CAR 274193                     *
.*                  Mods to get Primary MBS prior displaying Matrix screen    *
.*        V10.03.03 20/09/2012  J.Tan          CAR 271474                     *
.*                  Mods to used index 5 of pmscmtaf file                     *
.*        V10.03.02 28/02/2012  J.Tan          CAR 260975                     *
.*                  No Matrix found to disp.Casemix-use Prov.for Disc/Current *
.*        V10.03.01 24/02/2012  J.Tan          CAR 260758                     *
.*                  Mods to check for Min and Max days Stay                   *
.*        V10.02.04 17/06/2011  Jill Parkinson CAR 241548                     *
.*                  Mods to clear PTHFBCAT                                    *
.*        V10.02.03 10/06/2011  Jill Parkinson CAR 241548                     *
.*                  Mods to clear HFNAME if not on health fund file           *
.*        V10.02.02 19/04/2011  J.Tan         CAR 233935                      *
.*                  Fixed checking for Exclude Tertiary MBS item              *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys              *
.*        V10.01.02 02/12/2010  Mike Laming  CAR 233046                       *
.*                  Mods for PATMCHFD using Table Type (replacing H/F Table)  *
.*                  13/01/2011  J.Tan        CAR 233048                       *
.*                  Mods PATTFEFD Theatre fees to use table type              *
.*                  17/01/2011  J.Tan        CAR 233034                       *
.*                  Mods PATBFEFD Theatre fees to use table type              *
.*        V10.01.01 30/11/2010 J.Tan         CAR 233082                       *
.*                  Use index 5 of Matrix file to search for subsequent rules *
.*        V10.00.06 17/08/2010 J.Tan         CAR 227873                       *
.*                  Fixed getting the grouper version for blank health fund   *
.*        V10.00.05 11/08/2010 J.Tan         CAR 227873                       *
.*                  Fixed printing invoice to search for next rule after      *
.*                  finding an expired Contract                               *
.*        V10.00.04 19/07/2010 J.Tan         CAR 225200                       *
.*                  Mods to replace E and X of MBS item codes with blank for  *
.*                  checking the range                                        *
.*        V10.00.03 16/07/2010 J.Tan         CAR 226322                       *
.*                  Mods to search for next rule if Contract is not valid     *
.*        V10.00.02 29/06/2010 J.Tan         CAR 224869                       *
.*                  Fixed printing Disc.Billing for Casepayment not discharged*
.*        V10.00.01 30/04/2010 J.Tan         CAR 220887                       *
.*                  Mods checking for Active/Inactive of Misc.charge          *
.*        V9.12.05  02/02/2010  Peter Vela CAR 215234                         *
.*                  Changed F1 to F4 in CRNG0000                              *
.*        V9.12.04  27/01/2010  J.Tan    CAR 214877                           *
.*                  Mods checking for Range of MBS item numbers               *
.*        V9.12.03  03/08/2009  J.Tan                                         *
.*                  Mods to display C/P using Prov. if Matrix not found       *
.*        V9.12.02  24/07/2009  J.Tan  CAR 202168                             *
.*                  Mods to update PTMICMXP to No if matrix not found ONLY    *
.*                  when printing invoice                                     *
.*                  23/07/2009  J.Tan  CAR 201132                             *
.*                  Mods checking for zero CMBS amount for Secondary/Tertiary *
.*        V9.12.01  29/05/2009  J.Tan  CAR 196010                             *
.*                  Mods to Principal ICD10 (Secondary and Tertiary)          *
.*        V9.11.02  28/01/2009  J.Tan  CAR 188348                             *
.*                  Mods Print invoice to update PTMICMXP to 'No' if matrix   *
.*                  not found                                                 *
.*        V9.11.01  07/01/2009  J.Tan  CAR 186328                             *
.*                  Mods for Not Coded Disc.patient and No matrix found to use*
.*                  Prov.item to disp.Casepayment invoice                     *
.*        V9.09.02  19/02/2008  J.Tan                                         *
.*                  Fixed seaching matrix combination for daycase patients    *
.*        V9.09.01  14/06/2007  Mike Laming   CAR 119158                      *
.*                  Changes to fix RDMCHG1 for Misc.Charges Effective Date    *
.*        V9.08.02  14/12/2006  J.Tan         CAR 125685                      *
.*                  Mods to use Claim code grouper version if no hf           *
.*        V9.08.01  04/10/2006  J.Tan                                         *
.*                  Mods checking for Record type - overnight/daycase/either  *
.*        V9.05.B01 12/12/2005  J.Tan         CAR 59391                       *
.*                  Mods to ignore credit noted item for Primary MBS          *
.*        V9.04.03  05/09/2005  J.Tan         CAR 74860                       *
.*                  Mods for availability of Matrix button                    *
.*        V9.04.02  31/08/2005  J.Tan         CAR 74153                       *
.*                  Fixed checking for exclusive                              *
.*        V9.04.01  20/04/2005  Jill Habasque CAR 60087                       *
.*                  Changed to use index 4 for pmscmtaf                       *
.*        V9.03.08  20/07/2004  J.Tan    CAR 50220                            *
.*                  If matrix not found when printing invoice, then revert to *
.*                  patcat without error message                              *
.*        V9.03.07  16/07/2004  J.Tan    CAR 51688                            *
.*                  Fixed I*C on HTMLFILE file                                *
.*        V9.03.06  21/05/2004  Pat Dirito       CAR 50035                    *
.*                  Removed setting of PTDTEPSD to ZERO as is now Mech Vent   *
.*                  variable                                                  *
.*                  24/05/2004 J.Tan    CAR 49644                             *
.*                  Mods to check MSGFLAG - if error messages to be displayed *
.*        V9.03.05  06/05/2004  J.Tan                                         *
.*                  Fixed to use second file of pmscmtaf file                 *
.*        V9.03.04  19/04/2004  J.Tan                                         *
.*                  Mods for new fields added in pmscmtaf file                *
.*        V9.03.03  30/10/2003  J.Tan  CAR 44172                              *
.*                  Mods for pmsmtiaf - Misc.Theatre item file                *
.*        V9.03.02  19/09/2003  Lina Vo  CAR 40497                            *
.*                  Changed index and read of patmchgf to include effective   *
.*                  date.                                                     *
.*        V9.03.01  31/07/2003  J.Tan  CAR 40786                              *
.*                  Mods for WEB Billing                                      *
.*        V9.02.12  25/11/2002  J.Tan                                         *
.*                  Mods to check icd10 disease code instead of operation code*
.*        V9.02.11  16/10/2002  J.Tan                                         *
.*                  Mods to check for blank health fund                       *
.*        V9.02.10  09/10/2002  J.Tan                                         *
.*                  If no provisional code, don't updating rates for perdiem  *
.*                  If no matrix and casemix code is blank,revert to patcat   *
.*        V9.02.08  05/07/2002 J.Tan  SRF 19425                               *
.*                  Mods to use health fund grouper version for disc.drg      *
.*        V9.02.07  26/06/2002 J.Tan                                          *
.*                  Mods to use provisional drg for non-discharge             *
.*        V9.02.06  14/06/2002  J.Tan                                         *
.*                  Mods for casepayment                                      *
.*        V9.02.06  14/06/2002  J.Tan                                         *
.*                  Mods for casepayment                                      *
.*        V9.02.05  03/06/2002  J.Tan                                         *
.*                  Fixed reading correct position for controlf               *
.*        V9.02.04  22/04/2002  J.Tan                                         *
.*                  Ported from r6.09                                         *
.*        V6.09.08  19/03/2002  J.Tan  SRF = 16282                            *
.*                  Fixed casepayment theatre fees                            *
.*        V6.09.07  27/02/2002  J.Tan                                         *
.*                  Mods to default casemix code to provisional if no hf      *
.*        V6.09.06  21/02/2002  J.Tan                                         *
.*                  Fixed problems for Epworth and Mayne                      *
.*        V6.09.05  08/02/2002  Greg Horvat                                   *
.*                  Changed to revert to perdiem charging if matrix is on & no*
.*                  matrix combination found                                  *
.*        V6.09.04  25/01/2002  Greg Horvat                                   *
.*                  Pretty much rewrote how the matrix/casemix/perdiem billing*
.*                  was initially calculated cause invoices couldnt be printed*
.*                  & stuff was all wrong                                     *
.*        V6.09.03  13/11/2001  J.Tan                                         *
.*                  Mods to read controlf file at the start for Disch. UDF    *
.*                  Mods to check for system parameter of health fund matrix  *
.*        V6.09.02  06/10/2001  J.Tan                                         *
.*                  Mods to recalculate theatre charges for patient class.    *
.*        V6.09.01  Jill Habasque                                             *
.*                  Mods to check default matrix record if PTHFUMTD =1        *
.*        V6.09.00  Caleb Sharman  SRF 10160                                  *
.*                  Mods to update the DTR file when the operator decides to  *
.*                  change the billing to perdiem rates                       *
.*                                                                            *
.******************************************************************************
+
CMXMATRX  BRANCH    ABFFLAG,MTRX9999         * ABF invoice
.
          OPEN      CONTROLF,"controlf"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PMSCCDA1,"pmsccdaf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND05;*71,PTCNCMAU,PTCNCMDU,*99,PTCNNWCM
          READ      CONTROLF,HUND13;*50,OPCNCONS
          READ      CONTROLF,HUND24;*121,PTCNDGED
.
          MATCH     "1",PTCNDGED
          IF        @EQUAL
            OPEN      PMSEDGA1,"pmsedgaf"     * Effective DRG
            OPEN      PMSEDGA2,"pmsedgaf"     * Effective DRG
          ENDIF
.
          PACK      KEY8,AADMNO
          CALL      RDMISS1              * Re read an admin file record
.
          MOVE      ZERO,MATRXFLG        * Matrix flag = 1 (matrix found)
          MOVE      SP70,CMCODE
          MOVE      SP70,CMDRG
          MOVE      SP70,CONTRCID
          MOVE      SP70,HFNAME
          MOVE      SP70,PTHFBCAT
          MOVE      SP70,PTHFGRPV
          MOVE      SP70,DIM6A           * Contract ID with no rule
          UNPACK    SP70,AUDFCODE,DSCFCODE
.
. ----- Check if using the matrix -----
.
          BRANCH    PTCNNWCM,MTRX0200       * Using matrix
.
          PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
          CALL      RDFUND1
          IF        OVRCD=1
            MOVE      SP70,HFNAME
            MOVE      SP70,PTHFBCAT
          ENDIF
          BRANCH    OVRCD,MTRX0020
.
          BRANCH    PTHFUCMX,MTRX0200       * Using matrix
.
.         Check claim code
.
MTRX0020  OPEN      PATCFAA1,"patcfaaf"
          MOVE      ACLAIM,KEY3
          CALL      RDPTCFA1
          IF        OVRCD=0
            MATCH     "3",PTCFCEFR            * Not in use?
            IF        !@EQUAL
              MATCH     "1",PTCFMCPS
              GOTO      MTRX0200 IF EQUAL     * Using matrix
            ENDIF
          ENDIF
.
          MOVE      ONE,CMXFLAG             * Check if a casemix patient
          MOVE      ZERO,PCFLAG             * Default to casemix
          MOVE      PTMIPCMX,CMCODE         * default to provisional code
.
.         if provisional casemix code is blank, revert to patcat
.
          MATCH     SP70,CMCODE
          IF        @EQUAL
            MOVE      ZERO,CMXFLAG            * Revert to perdiem
            MOVE      ONE,PCFLAG              * Revert to perdiem
          ENDIF
          GOTO      MTRX9600
.
MTRX0100  MOVE      PTMIPCMX,CMCODE         * default to provisional code
          MOVE      PTMIPCMX,CMDRG
          MOVE      ONE,CMXFLAG             * Check if a casemix patient
          MOVE      ZERO,PCFLAG             * Default to casemix
          MATCH     "Y",PTMICMXP
          IF        @EQUAL
            COMPARE   TWO,MSGFLAG
            GOTO      MTRX8000 IF EQUAL       * Display matrix screen
          ENDIF
          GOTO      MTRX9500
.
MTRX0200  MATCH     "PATWEB96",PRGID
          GOTO      MTRX0500 IF EQUAL       * Discharging
.
          COMPARE   "3",ASTAT
          GOTO      MTRX0500 IF EQUAL       * Patient discharged
.
          BRANCH    PROGFLAG,MTRX0100,MTRX0300,MTRX0400,MTRX0100:
                             MTRX0400,MTRX0400
.                            Calc Inv,Disp Inv,Prnt Inv,Calc Fee Inv
.
MTRX0300  
          MATCH     SP9,PTMIPCMX
          GOTO      MTRX0100 IF NOT EQUAL
.
          BRANCH    MSGFLAG,MTRX0100,MTRX0100
          COMPARE   ONE,WEBFLAG
          GOTO      MTRX0100 IF NOT EQUAL
.
.         WRITE     HTMLFILE;"<tr><td colspan=12><table width=100% border=0>":
.                            "<tr><td>Casepayment patient not discharged.":
.                            "</td></tr></table></td></tr>";
          WRITE     HTMLFILE;"<tr><b>Casepayment patient not discharged.":
                             "</b></tr>";
.
.         GOTO      MTRX0100
.
MTRX0400  MOVE      TWO,CMXFLAG             * Exit invoicing program
          BRANCH    MSGFLAG,MTRX9000,MTRX9000
          COMPARE   ONE,WEBFLAG
          GOTO      MTRX9000 IF NOT EQUAL
.
.           WRITE     HTMLFILE;"<tr><td>Cannot print invoice. Casepayment ":
.                            "Patient not discharged. </td></tr>";
          GOTO      MTRX9000
.
. ----- Look for a matrix combination on the matrix file -----
.
MTRX0500  MOVE      ZERO,DEFLAG
          MOVE      ZERO,DEFINDIC
          MOVE      AFUNDH,PRIMFUND
          MOVE      SP70,SAVGCOD
          MOVE      SP70,DIM6A           * Contract ID with no rule
          OPEN      PMSCMTA5,"pmscmtaf"
          OPEN      PATCMTA1,"patcmtaf"
.
          MATCH     SP10,AFUNDH
          GOTO      MTRX0510 IF EQUAL        * Blank hf
.
. ------ get the health fund table type ----
.
          PACK      KEY14,AFUNDH,ZERO8
          CALL      RDFUND1
          IF        OVRCD=1
            MOVE      SP70,HFNAME
            MOVE      SP70,PTHFBCAT
          ENDIF
          BRANCH    OVRCD,MTRX7900
.
          MOVE      PTHFUMTD,DEFINDIC
.
          PACK      KEY14,AFUNDH,AFNDTB
          CALL      RDFUND1
          IF        OVRCD=1
            MOVE      SP70,HFNAME
            MOVE      SP70,PTHFBCAT
          ENDIF
          BRANCH    OVRCD,MTRX7900
          MOVE      PTHFBCAT,SAVGCOD
.
MTRX0510  CALL      GETI0000                 * Get primary disease/diagnosis
          CALL      GETP0000                 * Get primary procedure
          CALL      GETM0000
          MOVE      ZERO,DEFLAG
.
MTRX0600  CALL      GVHF0000     * Get the grouper version from health fund
.
          CALL      GCNT0000                 * Get a valid Contract ID
          BRANCH    EXIT,MTRX7920            * can't find a valid Contract
          MOVE      CONTRCID,DIM6A           * save the contract ID
.
.        Set up the daycase rate indicator
.
          UNPACK    SP70,SRECTYPE,DRECTYPE
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      "D",SRECTYPE           * Daycase
            MOVE      "D",DRECTYPE           * position key
          ELSE 
            MOVE      "O",SRECTYPE           * Overnight
            MOVE      "E",DRECTYPE           * position key
          ENDIF
.
. ----- loop through pmscmtaf to find a matching record ------
.
          MOVE      ONE,PMCCCNTR
         PACK      KEY34,CONTRCID,ACLAIM,PRIMFUND,SAVGCOD,DRECTYPE,PMCCCNTR,SP70
.
MTRX0650  CALL      RSPMCMT5
MTRX0700  CALL      RKPMCMT5
          BRANCH    OVRCD,MTRX7900
.
          MATCH     CONTRCID,PMCCCONT        * Same Contract ID?
          GOTO      MTRX7900 IF NOT EQUAL    * No
.
          MATCH     ACLAIM,PMCCCCOD
          GOTO      MTRX7900 IF NOT EQUAL
.
          MATCH     PRIMFUND,PMCCHFUN
          GOTO      MTRX7900 IF NOT EQUAL
.
          MATCH     SAVGCOD,PMCCHFTY
          GOTO      MTRX7900 IF NOT EQUAL
.
          MATCH     "D",SRECTYPE
          GOTO      MTRX0720 IF NOT EQUAL     * not daycase
.
          MATCH     "O",PMCCTYPE              * check for overnight
          GOTO      MTRX7900 IF EQUAL
.
MTRX0720  MATCH     ANSE,PMCCTYPE
          GOTO      MTRX0730 IF EQUAL
.
.         for Overnight patient, check for 'Overnight' record type
.         for Daycase patient, check for 'Either' record type
.          
          MATCH     SRECTYPE,PMCCTYPE
          GOTO      MTRX0730 IF EQUAL
.
          MOVE      ONE,PMCCCNTR
          MATCH     "O",SRECTYPE
          IF        @EQUAL
            MOVE      "O",DRECTYPE
            PACK      KEY34,CONTRCID,ACLAIM,PRIMFUND,SAVGCOD:
                            DRECTYPE,PMCCCNTR,SP70
            GOTO      MTRX0650
          ELSE
            MOVE      "E",DRECTYPE
            PACK      KEY34,CONTRCID,ACLAIM,PRIMFUND,SAVGCOD:
                            DRECTYPE,PMCCCNTR,SP70
            GOTO      MTRX0650
          ENDIF
.
MTRX0730  COMPARE   ZERO,PMCCCNTR
          GOTO      MTRX0740 IF NOT EQUAL
.
          MOVE      ONE,PMCCCNTR
          PACK      KEY34,CONTRCID,ACLAIM,PRIMFUND,SAVGCOD:
                          PMCCTYPE,PMCCCNTR,SP70
          GOTO      MTRX0650
.
. ------ discharge DRG ------
.
MTRX0740  COMPARE   ZERO,PMCCFTDG              * ignored
          GOTO      MTRX0800 IF EQUAL
.
          COMPARE   TWO,PMCCFTDG               * must be blank
          IF        @EQUAL
            MATCH     SP4,PTDSDDRG
            IF        @EQUAL
              GOTO      MTRX0800
            ELSE
              GOTO      MTRX0700 
            ENDIF
          ENDIF
.
.     Mandatory
.
          IF        PMCCFTDG=1
            MATCH     PMCCDDRG,PTDSDDRG
            GOTO      MTRX0700 IF NOT EQUAL
          ENDIF
.
.     Exclusive
.
          IF        PMCCFTDG=3
            MATCH     PMCCDDRG,PTDSDDRG
            GOTO      MTRX0700 IF EQUAL
          ENDIF
.
. ------ primary cmbs ------
.
MTRX0800  COMPARE   ZERO,PMCCFTCM              * ignored
          GOTO      MTRX0900 IF EQUAL
.
          COMPARE   TWO,PMCCFTCM               * must be blank
          IF        @EQUAL
            MATCH     SP9,PRICMBS
            IF        @EQUAL
              GOTO      MTRX0900
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
.... Mandatory
.
          IF        PMCCFTCM=1
            MOVE      PRICMBS,KEY9
            SQUEEZE   KEY9
            TYPE      KEY9
            GOTO      MTRX0820 IF NOT EQUAL
.
            MOVE      ONE,F4
            CALL      CRNG0000                 * Check range of MBS
            BRANCH    EXIT,MTRX0700
            GOTO      MTRX0840
.
MTRX0820    MATCH     PMCCPCMF,PRICMBS
            GOTO      MTRX0700 IF LESS
.
            MATCH     PRICMBS,PMCCPCMT
            GOTO      MTRX0700 IF LESS
          ENDIF
.
.... Exclusive
.
MTRX0840
          IF        PMCCFTCM=3
            MATCH     SP70,PRICMBS
            GOTO      MTRX0900 IF EQUAL
            MATCH     PMCCPCMF,PRICMBS
            GOTO      MTRX0900 IF LESS
.
            MATCH     PRICMBS,PMCCPCMT
            GOTO      MTRX0900 IF LESS
            GOTO      MTRX0700
          ENDIF
.
. ------ secondary cmbs ------
.
MTRX0900  COMPARE   ZERO,PMCCFTSC              * ignored
          GOTO      MTRX1000 IF EQUAL
.
          COMPARE   TWO,PMCCFTSC               * must be blank
          IF        @EQUAL
            MATCH     SP9,SECCMBS
            IF        @EQUAL
              GOTO      MTRX1000
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
.... Mandatory
.
          IF        PMCCFTSC=1
            MOVE      SECCMBS,KEY9
            SQUEEZE   KEY9
            TYPE      KEY9
            GOTO      MTRX0920 IF NOT EQUAL
.
            MOVE      TWO,F4
            CALL      CRNG0000                 * Check range of MBS
            BRANCH    EXIT,MTRX0700
            GOTO      MTRX0940
.
MTRX0920    MATCH     PMCCSCMF,SECCMBS
            GOTO      MTRX0700 IF LESS
.
            MATCH     SECCMBS,PMCCSCMT
            GOTO      MTRX0700 IF LESS
          ENDIF
.
.... Exclusive
.
MTRX0940
          IF        PMCCFTSC=3
            MATCH     SP70,SECCMBS
            GOTO      MTRX1000 IF EQUAL
            MATCH     PMCCSCMF,SECCMBS
            GOTO      MTRX1000 IF LESS
.
            MATCH     SECCMBS,PMCCSCMT
            GOTO      MTRX1000 IF LESS
            GOTO      MTRX0700
          ENDIF
.
. ------ tertiary cmbs ------
.
MTRX1000  COMPARE   ZERO,PMCCFTTC              * ignored
          GOTO      MTRX1050 IF EQUAL
.         
          COMPARE   TWO,PMCCFTTC               * must be blank
          IF        @EQUAL
            MATCH     SP9,TERCMBS
            IF        @EQUAL
              GOTO      MTRX1050
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
.... Mandatory
.
          IF        PMCCFTTC=1
            MOVE      TERCMBS,KEY9
            SQUEEZE   KEY9
            TYPE      KEY9
            GOTO      MTRX1020 IF NOT EQUAL
.
            MOVE      THREE,F4
            CALL      CRNG0000                 * Check range of MBS
            BRANCH    EXIT,MTRX0700
            GOTO      MTRX1040
.
MTRX1020    MATCH     PMCCTCMF,TERCMBS
            GOTO      MTRX0700 IF LESS
.
            MATCH     TERCMBS,PMCCTCMT
            GOTO      MTRX0700 IF LESS
          ENDIF
.
.... Exclusive
.
MTRX1040
          IF        PMCCFTTC=3
            MATCH     SP70,TERCMBS
            GOTO      MTRX1050 IF EQUAL
            MATCH     PMCCTCMF,TERCMBS
            GOTO      MTRX1050 IF LESS
.
            MATCH     TERCMBS,PMCCTCMT
            GOTO      MTRX1050 IF LESS
            GOTO      MTRX0700
          ENDIF
.
. ------ 4th cmbs ------
.
MTRX1050  COMPARE   ZERO,PMCCFTS4              * ignored
          GOTO      MTRX1060 IF EQUAL
.
          COMPARE   TWO,PMCCFTS4               * must be blank
          IF        @EQUAL
            MATCH     SP9,FORCMBS
            IF        @EQUAL
              GOTO      MTRX1060
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
... Mandatory
.
          IF        PMCCFTS4=1
            MOVE      FORCMBS,KEY9
            SQUEEZE   KEY9
            TYPE      KEY9
            GOTO      MTRX1052 IF NOT EQUAL
.
            MOVE      FOUR,F4
            CALL      CRNG0000                 * Check range of MBS
            BRANCH    EXIT,MTRX0700
            GOTO      MTRX1054
.
MTRX1052    MATCH     PMCCPC4F,FORCMBS
            GOTO      MTRX0700 IF LESS
.
            MATCH     FORCMBS,PMCCPC4T
            GOTO      MTRX0700 IF LESS
          ENDIF
.
.... Exclusive
.
MTRX1054
          IF        PMCCFTS4=3
            MATCH     SP70,FORCMBS
            GOTO      MTRX1060 IF EQUAL
            MATCH     PMCCPC4F,FORCMBS
            GOTO      MTRX1060 IF LESS
.
            MATCH     FORCMBS,PMCCPC4T
            GOTO      MTRX1060 IF LESS
            GOTO      MTRX0700
          ENDIF
.
. ------ 5th cmbs ------
.
MTRX1060  COMPARE   ZERO,PMCCFT5C              * ignored
          GOTO      MTRX1070 IF EQUAL
.
          COMPARE   TWO,PMCCFT5C               * must be blank
          IF        @EQUAL
            MATCH     SP9,FIFCMBS
            IF        @EQUAL
              GOTO      MTRX1070
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
.... Mandatory
.
          IF        PMCCFT5C=1
            MOVE      FIFCMBS,KEY9
            SQUEEZE   KEY9
            TYPE      KEY9
            GOTO      MTRX1062 IF NOT EQUAL
.
            MOVE      FIVE,F4
            CALL      CRNG0000                 * Check range of MBS
            BRANCH    EXIT,MTRX0700
            GOTO      MTRX1064
.
MTRX1062    MATCH     PMCCSC5F,FIFCMBS
            GOTO      MTRX0700 IF LESS
.
            MATCH     FIFCMBS,PMCCSC5T
            GOTO      MTRX0700 IF LESS
          ENDIF
.
.... Exclusive
.
MTRX1064
          IF        PMCCFT5C=3
            MATCH     SP70,FIFCMBS
            GOTO      MTRX1070 IF EQUAL
            MATCH     PMCCSC5F,FIFCMBS
            GOTO      MTRX1070 IF LESS
.
            MATCH     FIFCMBS,PMCCSC5T
            GOTO      MTRX1070 IF LESS
            GOTO      MTRX0700
          ENDIF
.

. ------ 6th cmbs ------
.
MTRX1070  COMPARE   ZERO,PMCCFT6C              * ignored
          GOTO      MTRX1100 IF EQUAL
.
          COMPARE   TWO,PMCCFT6C               * must be blank
          IF        @EQUAL
            MATCH     SP9,SIXCMBS
            IF        @EQUAL
              GOTO      MTRX1100
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
.... Mandatory
.
          IF        PMCCFT6C=1
            MOVE      SIXCMBS,KEY9
            SQUEEZE   KEY9
            TYPE      KEY9
            GOTO      MTRX1072 IF NOT EQUAL
.
            MOVE      SIX,F4
            CALL      CRNG0000                 * Check range of MBS
            BRANCH    EXIT,MTRX0700
            GOTO      MTRX1074
.
MTRX1072    MATCH     PMCCTC6F,SIXCMBS
            GOTO      MTRX0700 IF LESS
.
            MATCH     SIXCMBS,PMCCTC6T
            GOTO      MTRX0700 IF LESS
          ENDIF
.
.... Exclusive
.
MTRX1074
          IF        PMCCFT6C=3
            MATCH     SP70,SIXCMBS
            GOTO      MTRX1100 IF EQUAL
            MATCH     PMCCTC6F,SIXCMBS
            GOTO      MTRX1100 IF LESS
.
            MATCH     SIXCMBS,PMCCTC6T
            GOTO      MTRX1100 IF LESS
            GOTO      MTRX0700
          ENDIF
.
. ------ discharge status ------
.
MTRX1100  COMPARE   ZERO,PMCCFTDS              * ignored
          GOTO      MTRX1200 IF EQUAL
.
          COMPARE   TWO,PMCCFTDS               * must be blank
          IF        @EQUAL
            MATCH     SP3,DSTAT
            IF        @EQUAL
              GOTO      MTRX1200
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
          MOVE      PMCCDSTA,DIM3A
          MOVE      PMCCDST2,DIM3B
          MOVE      PMCCDST3,DIM3C
          MOVE      PMCCDST4,DIM3D
          MOVE      PMCCDST5,DIM3E
          MOVE      PMCCFTDS,PMCCTY  
          MOVE      DSTAT,PSTATCM
          CALL      CMEF0000
          BRANCH    FINDFLG,MTRX1200
          GOTO      MTRX0700

. ------ discharge destination ------
.
MTRX1200  COMPARE   ZERO,PMCCFTDD              * ignored
          GOTO      MTRX1300 IF EQUAL
.
          COMPARE   TWO,PMCCFTDD               * must be blank
          IF        @EQUAL
            MATCH     SP3,DDEST
            IF        @EQUAL
              GOTO      MTRX1300
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
          MOVE      PMCCDDES,DIM3A
          MOVE      PMCCDDE2,DIM3B
          MOVE      PMCCDDE3,DIM3C
          MOVE      PMCCDDE4,DIM3D
          MOVE      PMCCDDE5,DIM3E
          MOVE      PMCCFTDD,PMCCTY  
          MOVE      DDEST,PSTATCM
          CALL      CMEF0000
          BRANCH    FINDFLG,MTRX1300
          GOTO      MTRX0700
.
. ------ admission source ------
.
MTRX1300  COMPARE   ZERO,PMCCFTAS              * ignored
          GOTO      MTRX1400 IF EQUAL
.
          COMPARE   TWO,PMCCFTAS               * must be blank
          IF        @EQUAL
            MATCH     SP3,ASRCE
            IF        @EQUAL
              GOTO      MTRX1400
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
          MOVE      PMCCASRC,DIM3A
          MOVE      PMCCASR2,DIM3B
          MOVE      PMCCASR3,DIM3C
          MOVE      PMCCASR4,DIM3D
          MOVE      PMCCASR5,DIM3E
          MOVE      PMCCFTAS,PMCCTY  
          MOVE      ASRCE,PSTATCM
          CALL      CMEF0000
          BRANCH    FINDFLG,MTRX1400
          GOTO      MTRX0700
.
. ------ ICD10 Disease   ------
.
MTRX1400  COMPARE   ZERO,PMCCFT1C              * ignored
          GOTO      MTRX1500 IF EQUAL
.
          COMPARE   TWO,PMCCFT1C               * must be blank
          IF        @EQUAL
            MATCH     SP3,PRIICDCD
            IF        @EQUAL
              GOTO      MTRX1500
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
          COMPARE   ONE,PMCCFT1C                     *mandatory?
          GOTO      MTRX1480 IF NOT EQUAL
.
          MATCH     SP9,PRIICDCD
          GOTO      MTRX0700 IF EQUAL
.
.         Check the combination of primary, secondary and tertiary ICD10
.
          MATCH     SP70,PMCCPICD
          IF        !@EQUAL
            MATCH     PMCCPICD,PRIICDCD
            GOTO      MTRX1460 IF NOT EQUAL
            MATCH     PMCCPIC2,SECICDCD
            GOTO      MTRX1460 IF NOT EQUAL
            MATCH     SP70,PMCCPIC3
            GOTO      MTRX1500 IF EQUAL
            MATCH     PMCCPIC3,TERICDCD
            GOTO      MTRX1500 IF EQUAL
          ENDIF
.
MTRX1460  MATCH     SP9,PMCCPIC4
          IF        !@EQUAL
            MATCH     PMCCPIC4,PRIICDCD
            GOTO      MTRX1500 IF EQUAL
          ENDIF
.
          MATCH     SP9,PMCCPIC5
          IF        !@EQUAL
            MATCH     PMCCPIC5,PRIICDCD
            GOTO      MTRX1500 IF EQUAL
          ENDIF
          GOTO      MTRX0700

... exclusive (PMCCFT1C =3)
.         Check the combination of primary, secondary and tertiary ICD10
.
MTRX1480  MATCH     SP9,PMCCPICD
          GOTO      MTRX1486 IF EQUAL
.
          MATCH     PMCCPICD,PRIICDCD
          GOTO      MTRX1486 IF NOT EQUAL
          MATCH     PMCCPIC2,SECICDCD
          GOTO      MTRX1486 IF NOT EQUAL
          MATCH     SP9,PMCCPIC3
          GOTO      MTRX0700 IF EQUAL
          MATCH     PMCCPIC3,TERICDCD
          GOTO      MTRX0700 IF EQUAL
.
MTRX1486  MATCH     SP9,PMCCPIC4
          IF        !@EQUAL
            MATCH      PMCCPIC4,PRIICDCD
            GOTO       MTRX0700 IF EQUAL
          ENDIF
.
          MATCH     SP9,PMCCPIC5
          IF        !@EQUAL
            MATCH      PMCCPIC5,PRIICDCD
            GOTO       MTRX0700 IF EQUAL
          ENDIF
.
. ------ admission type ------
.
MTRX1500  COMPARE   ZERO,PMCCFTAT              * ignored
          GOTO      MTRX1600 IF EQUAL
.
          COMPARE   TWO,PMCCFTAT               * must be blank
          IF        @EQUAL
            MATCH     SP3,ATYPE
            IF        @EQUAL
              GOTO      MTRX1600
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
          MOVE      PMCCATYP,DIM3A
          MOVE      PMCCATY2,DIM3B
          MOVE      PMCCATY3,DIM3C
          MOVE      PMCCATY4,DIM3D
          MOVE      PMCCATY5,DIM3E
          MOVE      PMCCFTAT,PMCCTY  
          MOVE      ATYPE,PSTATCM
          CALL      CMEF0000
          BRANCH    FINDFLG,MTRX1600
          GOTO      MTRX0700
.
. ------ admission UDF ------
.
MTRX1600  COMPARE   ZERO,PMCCFTAU              * ignored
          GOTO      MTRX1700 IF EQUAL
.
          LOAD      AUDFCODE,PTCNCMAU,AUSR1,AUSR2,AUSR3,AUSR4,AUSR5,AUSR6:
                             AUSR7,PTMIUSR8,PTMIUSR9,PTMIUSR0
.
          COMPARE   TWO,PMCCFTAU               * must be blank
          IF        @EQUAL
            MATCH     SP3,AUDFCODE
            IF        @EQUAL
              GOTO      MTRX1700
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
          MOVE      PMCCAUDF,DIM3A
          MOVE      PMCCAUD2,DIM3B
          MOVE      PMCCAUD3,DIM3C
          MOVE      PMCCAUD4,DIM3D
          MOVE      PMCCAUD5,DIM3E
          MOVE      PMCCFTAU,PMCCTY  
          MOVE      AUDFCODE,PSTATCM
          CALL      CMEF0000
          BRANCH    FINDFLG,MTRX1700
          GOTO      MTRX0700
.
. ------ Care Class ------
.
MTRX1700  COMPARE   ZERO,PMCCFTDU              * ignored
          GOTO      MTRX1720 IF EQUAL
.
          COMPARE   TWO,PMCCFTDU               * must be blank
          IF        @EQUAL
            MATCH     SP3,ACARE
            IF        @EQUAL
              GOTO      MTRX1720
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
          MOVE      PMCCDUDF,DIM3A
          MOVE      PMCCDUD2,DIM3B
          MOVE      PMCCDUD3,DIM3C
          MOVE      PMCCDUD4,DIM3D
          MOVE      PMCCDUD5,DIM3E
          MOVE      PMCCFTDU,PMCCTY  
          MOVE      ACARE,PSTATCM
          CALL      CMEF0000
          BRANCH    FINDFLG,MTRX1720
          GOTO      MTRX0700
.
. ------ Attending Doctor ------
.
MTRX1720  COMPARE   ZERO,PMCCFTAD              * ignored
          GOTO      MTRX1740 IF EQUAL
.
          COMPARE   TWO,PMCCFTAD               * must be blank
          IF        @EQUAL
            MATCH     SP4,ADOCTA
            IF        @EQUAL
              GOTO      MTRX1740
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
.     Mandatory
.
          IF        PMCCFTAD=1
            MATCH     PMCCADOC,ADOCTA
            GOTO      MTRX0700 IF NOT EQUAL
          ENDIF
.
.     Exclusive
.
          IF        PMCCFTAD=3
            MATCH     PMCCADOC,ADOCTA
            GOTO      MTRX0700 IF EQUAL
          ENDIF
.
. ------ Concession Card Type ------
.
MTRX1740
          COMPARE   ZERO,PMCCFTCC              * ignored
          GOTO      MTRX1760 IF EQUAL
.
.         Get all concession cards
.
          MOVE      ZERO,F2
          PACK      KEY19,AURNO,SP70
          CALL      RSPMCCD1
MTRX1742  CALL      RKPMCCD1
          BRANCH    OVRCD,MTRX1746
.
          MATCH     PMCDURNO,AURNO
          GOTO      MTRX1746 IF NOT EQUAL
.
          MATCH     SP70,PMCDEXDT              * Check the expired date
          IF        !@EQUAL
            PACK      KEY8,CCC,CYY,CMM,CDD
            IF        ASTAT=3
              MOVE      DDATE,KEY8
            ENDIF
            REP       " 0",KEY8
            MATCH     KEY8,PMCDEXDT
            GOTO      MTRX1742 IF LESS         * expired
          ENDIF
.
          COMPARE   ZERO,F2
          GOTO      MTRX1744 IF EQUAL          * Not first record
.
          COMPARE   TWO,PMCCFTCC               * must be blank
          GOTO      MTRX0700 IF EQUAL
.
MTRX1744  ADD       ONE,F2                     * found a valid concession card
          MOVE      PMCCCCT1,DIM3A
          MOVE      PMCCCCT2,DIM3B
          MOVE      PMCCCCT3,DIM3C
          MOVE      PMCCCCT4,DIM3D
          MOVE      PMCCCCT5,DIM3E
          MOVE      PMCCFTCC,PMCCTY
          MOVE      PMCDCTYP,PSTATCM
          CALL      CMEF0000
          BRANCH    FINDFLG,MTRX1742           * Check next concession card
          GOTO      MTRX0700
.
MTRX1746  COMPARE   TWO,PMCCFTCC               * must be blank
          IF        @EQUAL
            COMPARE   ZERO,F2
            GOTO      MTRX1760 IF EQUAL
            GOTO      MTRX0700
          ENDIF
.
          COMPARE   ONE,PMCCFTCC               * Mandatory
          IF        @EQUAL
            COMPARE   ZERO,F2
            GOTO      MTRX0700 IF EQUAL        * No concession card
            GOTO      MTRX1760
          ENDIF
.
. ------ Discharge UDF ------
.
MTRX1760  COMPARE   ZERO,PMCCFDDU              * ignored
          GOTO      MTRX1780 IF EQUAL
.
          LOAD      DSCFCODE,PTCNCMDU,DUSD1,DUSD2,DUSD3,DUSD4,DUSD5
.
          COMPARE   TWO,PMCCFDDU               * must be blank
          IF        @EQUAL
            MATCH     SP3,DSCFCODE
            IF        @EQUAL
              GOTO      MTRX1780
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
          MOVE      PMCCUDF1,DIM3A
          MOVE      PMCCUDF2,DIM3B
          MOVE      PMCCUDF3,DIM3C
          MOVE      PMCCUDF4,DIM3D
          MOVE      PMCCUDF5,DIM3E
          MOVE      PMCCFDDU,PMCCTY
          MOVE      DSCFCODE,PSTATCM
          CALL      CMEF0000
          BRANCH    FINDFLG,MTRX1780
          GOTO      MTRX0700
.
MTRX1780  COMPARE   ZERO,PMCCMIND
          GOTO      MTRX1800 IF EQUAL          * No minimum days stay
.
          MOVE      ADATE,FROMDATE
          PACK      TODATE,CCC,CYY,CMM,CDD
          REP       " 0",TODATE
          IF        ASTAT=3
            MOVE      DDATE,TODATE
          ENDIF
          CALL      NHOSPDAY
          ADD       ADYHOSP,NBRDAYS         * include days hospitalisation
.
.         Exclude any Unqualified days
.
          MOVE      NBRDAYS,F4          * save current no of bed days
          MOVE      ZERO,FORM4
          MOVE      "U",SAVIND12
          PROC      PATDATYP            * Get LOS for Unqualified days
          IF        NBRDAYS>0
            MOVE      NBRDAYS,FORM4
          ENDIF
          MOVE      F4,NBRDAYS
          SUB       FORM4,NBRDAYS         * subtract unqualified days
.
          COMPARE   PMCCMIND,NBRDAYS
          GOTO      MTRX0700 IF LESS        * LOS is less than mininum days stay
.
          COMPARE   NBRDAYS,PMCCHBPT
          GOTO      MTRX0700 IF LESS        * LOS is greater than maximum days
.
. ------ ICD10 Procedure   ------
.
MTRX1800  COMPARE   ZERO,PMCCICPF              * ignored
          GOTO      MTRX1900 IF EQUAL
.
          COMPARE   TWO,PMCCICPF               * must be blank
          IF        @EQUAL
            MATCH     SP3,PRIICOCD
            IF        @EQUAL
              GOTO      MTRX1900
            ELSE
              GOTO      MTRX0700
            ENDIF
          ENDIF
.
          COMPARE   ONE,PMCCICPF                     *mandatory?
          GOTO      MTRX1880 IF NOT EQUAL
.
          MATCH     SP9,PRIICOCD
          GOTO      MTRX0700 IF EQUAL
.
.         Check the combination of primary, secondary and tertiary ICD procedure
.
          MATCH     SP70,PMCCICP1
          IF        !@EQUAL
            MATCH     PMCCICP1,PRIICOCD
            GOTO      MTRX1860 IF NOT EQUAL
            MATCH     PMCCICP2,SECICOCD
            GOTO      MTRX1860 IF NOT EQUAL
            MATCH     SP70,PMCCICP3
            GOTO      MTRX1900 IF EQUAL
            MATCH     PMCCICP3,TERICOCD
            GOTO      MTRX1900 IF EQUAL
          ENDIF
.
MTRX1860  MATCH     SP9,PMCCICP4
          IF        !@EQUAL
            MATCH     PMCCICP4,PRIICOCD
            GOTO      MTRX1900 IF EQUAL
          ENDIF
.
          MATCH     SP9,PMCCICP5
          IF        !@EQUAL
            MATCH     PMCCICP5,PRIICOCD
            GOTO      MTRX1900 IF EQUAL
          ENDIF
          GOTO      MTRX0700

... exclusive (PMCCICPF =3)
.         Check the combination of primary, secondary and tertiary ICD procedur
.
MTRX1880  MATCH     SP9,PMCCICP1
          GOTO      MTRX1886 IF EQUAL
.
          MATCH     PMCCICP1,PRIICOCD
          GOTO      MTRX1886 IF NOT EQUAL
          MATCH     PMCCICP2,SECICOCD
          GOTO      MTRX1886 IF NOT EQUAL
          MATCH     SP9,PMCCICP3
          GOTO      MTRX0700 IF EQUAL
          MATCH     PMCCICP3,TERICOCD
          GOTO      MTRX0700 IF EQUAL
.
MTRX1886  MATCH     SP9,PMCCICP4
          IF        !@EQUAL
            MATCH      PMCCICP4,PRIICOCD
            GOTO       MTRX0700 IF EQUAL
          ENDIF
.
          MATCH     SP9,PMCCICP5
          IF        !@EQUAL
            MATCH      PMCCICP5,PRIICOCD
            GOTO       MTRX0700 IF EQUAL
          ENDIF
.
MTRX1900  MOVE      PMCCCMCD,CMCODE
          MOVE      PMCCCMCD,CMDRG
          MOVE      ONE,CMXFLAG             * Charge using casemix
          MOVE      ZERO,PCFLAG             * Default to casemix
.
          BRANCH    MSGFLAG,MTRX9000,MTRX8000
          COMPARE   TWO,PROGFLAG
          GOTO      MTRX9000 IF NOT EQUAL
.
          COMPARE   ONE,WEBFLAG
          GOTO      MTRX9000 IF NOT EQUAL
.
          MATCH     SP10,PTMIPCMX
          IF        !@EQUAL
.           WRITE     HTMLFILE;"<tr><td colspan=12><table width=100% border=0>":
.                            "<tr><td>Provisional Casemix code:<b>":
.                            PTMIPCMX,"</b>Matrix Casemix code:<b>",CMCODE:
.                            "</b> Contract ID:<b>",CONTRCID,"</b></td></tr>":
.                            "</table></td></tr>";
            WRITE     HTMLFILE;"<tr><b>Provisional Casemix code:<b>":
                             PTMIPCMX,"</b>Matrix Casemix code:<b>",CMCODE:
                             "</b> Contract ID:<b>",CONTRCID,"</b>":
                             "</b></tr>";
          ELSE
.           WRITE     HTMLFILE;"<tr><td colspan=12><table width=100% border=0>":
.                            "<tr><td>Matrix Casemix code:<b>",CMCODE:
.                            "</b> Contract ID:<b>",CONTRCID,"</b></td></tr>":
.                            "</table></td></tr>";
            WRITE     HTMLFILE;"<tr><b>Matrix Casemix code:<b>",CMCODE:
                             "</b> Contract ID:<b>",CONTRCID,"</b>":
                             "</b></tr>";
          ENDIF
          GOTO      MTRX9000
.
. ----  using default if no match found ----
.
MTRX7900  MATCH     "1",DEFINDIC
          GOTO      MTRX7920 IF NOT EQUAL
.
. ----  already tried default record ----
.
          MATCH     SP70,AFUNDH
          GOTO      MTRX7920 IF EQUAL       * No health fund
.
          IF        DEFLAG=0
            MOVE      SP8,PRIMFUND
            MOVE      SP8,SAVGCOD
            ADD       ONE,DEFLAG
            GOTO      MTRX0600
          ENDIF
.
MTRX7920  MOVE      ONE,MATRXFLG            * Matrix not found
          MOVE      SP70,CONTRCID
.
MTRX8000  PACK      KEY8,AADMNO
          CALL      RDMISS1                 * Re read an admin file record
.
.         CALL      GETI0000
.         CALL      GETM0000
.
          CALL      DCMX0000                * Display error details
          BRANCH    EXIT,MTRX9500
.
MTRX9000  MOVE      ZERO,EXIT
          GOTO      MTRX9999
.
MTRX9500  MOVE      ONE,EXIT
          GOTO      MTRX9999
.
MTRX9600  MOVE      TWO,EXIT
MTRX9999  RETURN
+
.******************************************************************************
.         Get a valid Contract ID
.******************************************************************************
GCNT0000  PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF                * set Effective date
          MOVE      ADATE,CEFFDATE
          LOAD      CEFFDATE,CNTRCEFR,ADATE,DDATE
.
          PACK      KEY18,ACLAIM,PRIMFUND,SAVGCOD,SP70
          CALL      RSPTCMT1
GCNT1000  CALL      RKPTCMT1
          BRANCH    OVRCD,GCNT6000
.
          MATCH     ACLAIM,PTCMCCOD         * check Claim code
          GOTO      GCNT6000 IF NOT EQUAL
          MATCH     PRIMFUND,PTCMHFUN       * check HF
          GOTO      GCNT6000 IF NOT EQUAL
          MATCH     SAVGCOD,PTCMHFTY        * check HF Table type
          GOTO      GCNT6000 IF NOT EQUAL
.
          MOVE      PTCMCNTR,KEY6
          CALL      VALCONTR                * Validate Contract ID
          BRANCH    EXIT,GCNT1000
.
          MOVE      PTCMCNTR,CONTRCID
          GOTO      GCNT9000
.
.         Try default
.
GCNT6000  BRANCH    DEFLAG,GCNT8000              * done default
.
          MOVE      SP8,PRIMFUND
          MOVE      SP8,SAVGCOD
          MOVE      ONE,DEFLAG
          GOTO      GCNT0000
.
GCNT8000  MOVE      ONE,EXIT
          GOTO      GCNT9999
.
GCNT9000  MOVE      ZERO,EXIT
GCNT9999  RETURN
+
.******************************************************************************
.         Check range of MBS item with Numeric numbers only
.******************************************************************************
CRNG0000  MOVE      ZERO,F9
          MOVE      ZERO,FORM9
          MOVE      ZERO,FORM9A
.
          BRANCH    F4,CRNG1000,CRNG2000,CRNG3000:
                       CRNG4000,CRNG5000,CRNG6000
          GOTO      CRNG9999
.
CRNG1000  MOVE      PMCCPCMF,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9
          MOVE      SAVKEY9,FORM9
.
          MOVE      PMCCPCMT,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9A
          MOVE      SAVKEY9,FORM9A
          GOTO      CRNG8000
.
CRNG2000  MOVE      PMCCSCMF,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9
          MOVE      SAVKEY9,FORM9
.
          MOVE      PMCCSCMT,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9A
          MOVE      SAVKEY9,FORM9A
          GOTO      CRNG8000
.
CRNG3000  MOVE      PMCCTCMF,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9
          MOVE      SAVKEY9,FORM9
.
          MOVE      PMCCTCMT,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9A
          MOVE      SAVKEY9,FORM9A
          GOTO      CRNG8000
.
CRNG4000  MOVE      PMCCPC4F,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9
          MOVE      SAVKEY9,FORM9
.
          MOVE      PMCCPC4T,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9A
          MOVE      SAVKEY9,FORM9A
          GOTO      CRNG8000
.
CRNG5000  MOVE      PMCCSC5F,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9
          MOVE      SAVKEY9,FORM9
.
          MOVE      PMCCSC5T,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9A
          MOVE      SAVKEY9,FORM9A
          GOTO      CRNG8000
.
CRNG6000  MOVE      PMCCTC6F,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9
          MOVE      SAVKEY9,FORM9
.
          MOVE      PMCCTC6T,SAVKEY9
          REP       "E X ",SAVKEY9
          SQUEEZE   SAVKEY9
          MOVE      ZERO,FORM9A
          MOVE      SAVKEY9,FORM9A
.
CRNG8000  MOVE      KEY9,F9
          COMPARE   FORM9,F9
          GOTO      CRNG9000 IF LESS
.
          COMPARE   F9,FORM9A
          GOTO      CRNG9000 IF LESS
          MOVE      ZERO,EXIT
          GOTO      CRNG9999
.
CRNG9000  MOVE      ONE,EXIT
CRNG9999  RETURN
+
.******************************************************************************
.*         CMEF0000 - check mandatory or exclusive field type                 *
.*                                                     Called by:MTRX0000     *
.******************************************************************************
CMEF0000  COMPARE   ONE,PMCCTY                       *mandatory
          IF        @EQUAL
           MATCH      SP3,PSTATCM
           IF         @EQUAL
             GOTO       CMEF9500
           ELSE
             MATCH      DIM3A,PSTATCM
             GOTO       CMEF9000 IF EQUAL
             MATCH      DIM3B,PSTATCM
             GOTO       CMEF9000 IF EQUAL
             MATCH      DIM3C,PSTATCM
             GOTO       CMEF9000 IF EQUAL
             MATCH      DIM3D,PSTATCM
             GOTO       CMEF9000 IF EQUAL
             MATCH      DIM3E,PSTATCM
             GOTO       CMEF9000 IF EQUAL
.... CAR 46081
             MOVE       ZERO,FINDFLG
....
             GOTO       CMEF9999
           ENDIF
CMEF9000   MOVE         ONE,FINDFLG
           GOTO         CMEF9999
          ENDIF      
....
... exclusive (PMCCFTDS =3)
.
          MATCH      SP70,PSTATCM
          GOTO       CMEF9550 IF EQUAL
.
          MATCH      SP3,DIM3A
          IF         !@EQUAL
            MATCH      DIM3A,PSTATCM
            GOTO       CMEF9500 IF EQUAL
          ENDIF
.
          MATCH      SP3,DIM3B
          IF         !@EQUAL
            MATCH      DIM3B,PSTATCM
            GOTO       CMEF9500 IF EQUAL
          ENDIF
          
          MATCH      SP3,DIM3C
          IF         !@EQUAL
            MATCH      DIM3C,PSTATCM
            GOTO       CMEF9500 IF EQUAL
          ENDIF

          MATCH      SP3,DIM3D
          IF         !@EQUAL
            MATCH      DIM3D,PSTATCM
            GOTO       CMEF9500 IF EQUAL
          ENDIF
.
          MATCH      SP3,DIM3E
          IF         !@EQUAL
            MATCH      DIM3E,PSTATCM
            GOTO       CMEF9500 IF EQUAL
          ENDIF
          GOTO       CMEF9550
.
CMEF9500  MOVE    ZERO,FINDFLG
          GOTO    CMEF9999
CMEF9550  MOVE    ONE,FINDFLG
CMEF9999  RETURN
+
.******************************************************************************
.*         GETM0000 - loop thru patdtraf to find the Primary, Secondary &     *
.*                    Tertiary MBS item.  Primary will be the mbs with the    *
.*                    highest scheduled fee                                   *
.******************************************************************************
GETM0000  MOVE      SP10,PRICMBS            * initialise primary MBS code
          MOVE      SP10,SECCMBS            * initialise secondary MBS code
          MOVE      SP10,TERCMBS            * initialise tertiary MBS code
          MOVE      SP10,FORCMBS            * initialise 4th MBS code
          MOVE      SP10,FIFCMBS            * initialise 5th MBS code
          MOVE      SP10,SIXCMBS            * initialise 6th MBS code
          MOVE      ZERO,MTIFLAG
          OPEN      PATDTRA1,"patdtraf"
          OPEN      PMSMTIA2,"pmsmtiaf"
          OPEN      PATITEM1,"patitemn"
.
          MOVE      ZERO,F3
          WHILE     F3<999
            ADD       ONE,F3
            MOVE      SP70,MCHGARR[F3]     * initialise 
            MOVE      ZERO,MCHGAMT[F3]
            MOVE      ZERO,MCHGQMT[F3]
          DO
.
          MOVE      ONE,SORTDONE            * use perdiem theatre fee to sort
          IF        IBCNMHOS=1
            MOVE     SP70,PMVXMHOS
            OPEN     PMSVX1A1,"pmsvx1af"
            PACK     KEY8,AADMNO
            CALL     RDPMVX11
            IF       OVRCD=0
              OPEN      PATHSPA1,"pathspaf"
              MOVE      PMVXMHOS,KEY3
              CALL      RDPTHSP1
              IF        OVRCD=0
                MATCH     "1",PTHSTFEE
                GOTO      GETM0800 IF EQUAL  * using theatre fees file
              ENDIF
            ENDIF
          ELSE
            BRANCH    PTCNTFEE,GETM0800      * using theatre fees file
          ENDIF
          MOVE      ZERO,SORTDONE
.
GETM0800  MOVE      ZERO,F3
          PACK      KEY22,AADMNO,SP20,SP20
          CALL      RDSDTRN1
GETM1000  CALL      RDKDTRN1
          BRANCH    OVRCD,GETM5000
.
          MATCH     AADMNO,TADMNO           * compare admission number
          GOTO      GETM5000 IF NOT EQUAL
.
          COMPARE   TWO,TRECTYPE
          GOTO      GETM1000 IF NOT EQUAL   * not a theatre item
.
          MATCH     "1",PTDTCRST
          GOTO      GETM1000 IF EQUAL       * fully credited
          MATCH     "2",PTDTCRST
          GOTO      GETM1000 IF EQUAL       * Credit by item
.
          PACK      KEY8,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY,SP8
GETM2000  REP       " 0",KEY8
          MOVE      KEY8,PMMITDAT
          PACK      KEY17,TITEMNO,KEY8,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,GETM7000          * not a valid item code
.
          MOVE      ZERO,FORM12P2
          BRANCH    SORTDONE,GETM3000
          GOTO      GETM4000
.
.        use per diem theatre fee to sort
.
GETM3000  CALL      TFEE0000                 * get normal theatre charge
          BRANCH    EXIT OF GETM4000
.
          MOVE      TFPAT1,FORM12P2
          ADD       TFHF1,FORM12P2
.
GETM4000  ADD       ONE,F3
          MOVE      TITEMNO,MCHGARR[F3]
          MOVE      QIAMT,MCHGQMT[F3]
          MOVE      FORM12P2,MCHGAMT[F3]    * per diem theatre fee
          GOTO      GETM7000
.
.         Check items in pmsmtiaf file
.
GETM5000  MOVE      ONE,MTIFLAG
          MOVE      "2",PMMIRTYP
          PACK      KEY15,AADMNO,PMMIRTYP,SP20
          CALL      RSPMMTI2
GETM6000  CALL      RKPMMTI2
          BRANCH    OVRCD,GETM8000
.
          MOVE      AADMNO,DAADMNO
          MATCH     DAADMNO,PMMIVISN         * compare admission number
          GOTO      GETM8000 IF NOT EQUAL
.
          MATCH     "2",PMMIRTYP
          GOTO      GETM8000 IF NOT EQUAL   * not a theatre item
.
          MOVE      PMMIITEM,TITEMNO
          MOVE      PMMITDAT,KEY8
          GOTO      GETM2000
.
GETM7000  BRANCH    MTIFLAG,GETM6000
          GOTO      GETM1000
.
.         Sort the item from most expensive
.
GETM8000  COMPARE   ZERO,F3
          GOTO      GETM9999 IF EQUAL        * No item
.
          MOVE      F3,TARGET
.
          MOVE      ZERO,SORTDONE
          WHILE     SORTDONE=0
            MOVE      ZERO,SWAPFLAG
            MOVE      ONE,F3
            MOVE      TWO,FORM3
.
            WHILE     FORM3<=TARGET
              IF        MCHGAMT[FORM3]<MCHGAMT[F3]
                GOTO      GETM8600
              ENDIF
.
              IF        MCHGAMT[FORM3]=MCHGAMT[F3]
                IF        MCHGQMT[FORM3]<=MCHGQMT[F3]
                  GOTO      GETM8600
                ENDIF
              ENDIF
.
              MOVE      MCHGAMT[F3],FORM12P2
              MOVE      MCHGARR[F3],KEY9
              MOVE      MCHGQMT[F3],FORM12F2
.
              MOVE      MCHGAMT[FORM3],MCHGAMT[F3]
              MOVE      MCHGARR[FORM3],MCHGARR[F3]
              MOVE      MCHGQMT[FORM3],MCHGQMT[F3]
.
              MOVE      FORM12P2,MCHGAMT[FORM3]
              MOVE      KEY9,MCHGARR[FORM3]
              MOVE      FORM12F2,MCHGQMT[FORM3]
.
              MOVE      ONE,SWAPFLAG
              MOVE      F3,LASTSWAP
.
GETM8600      ADD      ONE,F3
              ADD      ONE,FORM3
            DO
.
            IF        SWAPFLAG=0
              MOVE      ONE,SORTDONE
            ELSE
              MOVE      LASTSWAP,TARGET
            ENDIF
          DO
.
          MOVE      ZERO,F3
          WHILE     F3<6
            ADD       ONE,F3
            STORE     MCHGARR[F3],F3,PRICMBS,SECCMBS,TERCMBS:
                                     FORCMBS,FIFCMBS,SIXCMBS
          DO
GETM9999  RETURN
+
.******************************************************************************
.         Subroutine to get the theatre fee amount
.******************************************************************************
TFEE0000  MOVE      ZERO,NOFEE
          OPEN      PATFN1A1,"patfn1af"
.
.         Set up the daycase rate indicator
.
          MOVE      SP1,DIM1
          MATCH     ADATE,DDATE
          GOTO      TFEE1000 IF NOT EQUAL
          MOVE      "D",DIM1
.
TFEE1000  PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF                * Get Contract Effective details
          BRANCH    EXIT,TFEE8000
.
          MOVE      PMMITDAT,CEFFDATE       * as at date
          LOAD      CEFFDATE,CNTRCEFR,ADATE,DDATE
.
.         If Contract Effective From is 'For Discharges From', Discharged date
.         is blank and TCINDC19=D, default Effective date to Current date
.
          IF        CNTRCEFR=2
            PACK      DDATE,DDATE,SP70
            MATCH     SP70,DDATE
            IF        @EQUAL
              PACK      KEY5,CODECL,ACLAIM
              CALL      RDCODE1
              IF        OVRCD=0
                MATCH     "D",TCINDC19
                IF        @EQUAL
                  PACK      CEFFDATE,CCC,CYY,CMM,CDD,SP70
                  REP       " 0",CEFFDATE
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY18 WITH ACLAIM,AFUNDH,AFNDTB,DIM1
          CALL      GETTFEES
          BRANCH    EXIT,TFEE8000
.
          COMPARE   ZERO,HFBAND
          GOTO      TFEE9999 IF EQUAL
          UNPACK    DIM5,PMMISUNQ          * Theatre banding
          UNPACK    DIM5,PTDTSUNQ          * Theatre banding
          GOTO      TFEE9999
.
TFEE8000  MOVE      ONE,NOFEE
          GOTO      TFEE9999
.
TFEE9999  RETURN
+
.******************************************************************************
.*         GETI0000 - get the first ICD Disease                               *
.******************************************************************************
GETI0000  MOVE      ZERO,FORM1
          UNPACK    SP70,PRIICDCD,SECICDCD,TERICDCD
          OPEN      PATECDA1,"patecdaf"
          PACK      KEY13,AADMNO,SP70
          CALL      RSPTECD1
GETI1000  CALL      RKPTECD1
          BRANCH    OVRCD,GETI9999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      GETI9999 IF NOT EQUAL
.
          BRANCH    FORM1,GETI2000,GETI3000
          MOVE      PTEDCODE,PRIICDCD          * Primary ICD10
          GOTO      GETI8000
.
GETI2000  MOVE      PTEDCODE,SECICDCD          * Secondary ICD10
          GOTO      GETI8000
.
GETI3000  MOVE      PTEDCODE,TERICDCD          * Tertiary ICD10
          GOTO      GETI9999
.
GETI8000  ADD       ONE,FORM1
          BRANCH    FORM1,GETI1000,GETI1000,GETI1000
          GOTO      GETI9999
.
GETI9999  RETURN
+
.******************************************************************************
.*         GETP0000 - get the first ICD Procedure                             *
.******************************************************************************
GETP0000  MOVE      ZERO,FORM1
          UNPACK    SP70,PRIICOCD,SECICOCD,TERICOCD
          OPEN      PATECOA1,"patecoaf"
          PACK      KEY13,AADMNO,SP70
          CALL      RSPTECO1
GETP1000  CALL      RKPTECO1
          BRANCH    OVRCD,GETP9999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      GETP9999 IF NOT EQUAL
.
          BRANCH    FORM1,GETP2000,GETP3000
          MOVE      PTEOCODE,PRIICOCD          * Primary ICD procedure
          GOTO      GETP8000
GETP2000  MOVE      PTEOCODE,SECICOCD          * Secondary ICD procedure
          GOTO      GETP8000
.
GETP3000  MOVE      PTEOCODE,TERICOCD          * Tertiary ICD procedure
          GOTO      GETP9999
.
GETP8000  ADD       ONE,FORM1
          BRANCH    FORM1,GETP1000,GETP1000,GETP1000
          GOTO      GETP9999
.
GETP9999  RETURN
+
.******************************************************************************
.*  DCMX0000 - display the casemix matrix fields if no valid record is found  *
.******************************************************************************
DCMX0000  COMPARE   TWO,MSGFLAG
          GOTO      DCMX0200 IF EQUAL
.
          COMPARE   ZERO,AINVPRT
          GOTO      DCMX0020 IF EQUAL       * No invoices printed
.
          MOVE      SP10,PMMIRTYP
          PACK      KEY15,AADMNO,PMMIRTYP,SP20
          CALL      RSPMMTI2
DCMX0010  CALL      RKPMMTI2
          BRANCH    OVRCD,DCMX0100
.
          MOVE      AADMNO,DAADMNO
          MATCH     DAADMNO,PMMIVISN        * compare admission number
          GOTO      DCMX0100 IF NOT EQUAL   * No outstanding items for invoice
.
          MATCH     "1",OPCNCONS            * Enable Consumption type ?
          IF        @EQUAL
            MATCH     "3",PMMIRTYP
            IF        @EQUAL
              REP       " 0",PMMICTYP
              MATCH     "0",PMMICTYP
              GOTO      DCMX0010 IF NOT EQUAL  * Item Not Used
            ENDIF
          ENDIF
.
DCMX0020  BRANCH    MSGFLAG,DCMX0100
          BRANCH    PROGFLAG,DCMX0040,DCMX0200,DCMX0100,DCMX0040
.
          COMPARE   SIX,PROGFLAG
          GOTO      DCMX0040 IF EQUAL
          GOTO      DCMX0100
.
.         Matrix is not found and if patient is discharged with No Coding
.         then use the provisional Casemix code to display C/P invoice 
.
DCMX0040  
          MATCH     SP70,PTMIPCMX       * prov.casemix code
          GOTO      DCMX0100 IF EQUAL
.         MATCH     SP70,ACODDTE        * Coded?
.         GOTO      DCMX0100 IF NOT EQUAL
.
          MOVE      PTMIPCMX,CMCODE
          MOVE      PTMIPCMX,CMDRG
          MOVE      ONE,CMXFLAG             * casemix patient
          MOVE      ZERO,PCFLAG             * Default to casemix
          MOVE      ZERO,PMCCPODY           * pre op cut off days
          MOVE      ZERO,PMCCMIND           * min stay
          MOVE      ZERO,PMCCHBPT           * max stay/high boundary point
          MOVE      ZERO,PMCCCCDY           * critical care cut off days
          MOVE      ZERO,PMCCNPDY           * no pay days
          GOTO      DCMX9000
.
DCMX0100  MOVE      ZERO,CMXFLAG            * Revert to perdiem
          MOVE      ONE,PCFLAG              * Revert to perdiem
          MOVE      ANSN,PTMICMXP
          GOTO      DCMX9000
.
. ----- Display matrix on the screen -----
.
DCMX0200  MOVE      TWO,CMXFLAG             * No match found
.
          COMPARE   THREE,PROGFLAG
          GOTO      DCMX0100 IF EQUAL
.
          COMPARE   TWO,MSGFLAG
          GOTO      DCMX1100 IF NOT EQUAL
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the admin date
          REP       " 0",CPCDATE
          PACK      KEY16,CPCDATE,SP20
.
          MOVE      SP70,CPCDATE
          MATCH     SP70,DDATE
          IF        !@EQUAL
            UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE                 * Format the admin date
            REP       " 0",CPCDATE
          ENDIF
.
          PACK      KEY14,AFUNDH,AFNDTB
          CALL      RDFUND1
          IF        OVRCD=1
            MOVE      SP70,HFNAME
            MOVE      SP70,PTHFBCAT
          ENDIF
.
          COMPARE   ONE,WEBFLAG
          GOTO      DCMX1100 IF NOT EQUAL
.
.
          WRITE     HTMLFILE;"<tr><td width=25%>Admitted On:</td>":
                             "<td width=25%><b>",KEY16,"</b></td>":
                             "<td width=25%>Disch'd On:</td>":
                             "<td width=25%><b>&nbsp;",CPCDATE:
                             "</b></td></tr>";
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          MOVE      TDESC,KEY20
.
          WRITE     HTMLFILE;"<tr><td width=25%>Claim Code:</td>":
                               "<td width=25%><b>",ACLAIM,"</b>":
                               " - <b>",KEY20,"</b></td>":
                               "<td width=25%>Discharge DRG:</td>":
                               "<td width=25%><b>&nbsp;",PTDSDDRG:
                               "</b></td></tr>":
                               "<tr><td width=25%>Health Fund:</td>":
                               "<td width=25%><b>",AFUNDH,"</b>":
                               " - <b>",HFNAME,"</b></td>";
.
          WRITE     HTMLFILE;"<td width=25%>Discharge Status:</td>";
.
          MOVE      SP20,TDESC
          PACK      DSTAT,DSTAT,SP70
          MATCH     SP70,DSTAT
          IF        !@EQUAL
            PACK      KEY5,ANSD,SP1,DSTAT
            CALL      RDCODE1
            WRITE     HTMLFILE;"<td width=25%><b>&nbsp;",DSTAT,"</b>":
                               " -<b>&nbsp;",TDESC,"</b></td></tr>";
          ELSE
            WRITE     HTMLFILE;"<td width=25%>&nbsp;</td></tr>";
          ENDIF
.
          MOVE      SP20,TDESC
          MATCH     SP70,PTHFBCAT
          IF        !@EQUAL & !@EOS
            PACK      KEY5,ANSH,ANST,PTHFBCAT
            CALL      RDCODE1
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td width=25%>Table Type:</td>":
                             "<td><b>",PTHFBCAT,"</b>":
                             " - <b>",TDESC,"</b></td>";
.
          MOVE      SP20,TDESC
          PACK      DDEST,DDEST,SP70
          MATCH     SP70,DDEST
          IF        !@EQUAL
            PACK      KEY5,ANSD,ANSD,DDEST
            CALL      RDCODE1
            WRITE     HTMLFILE;"<td width=25%>Discharge Destination:</td>":
                               "<td width=25%><b>&nbsp;",DDEST,"<b>":
                               " -<b>&nbsp;",TDESC,"</b></td></tr>";
          ELSE
            WRITE     HTMLFILE;"<td width=25%>Discharge Destination:</td>":
                               "<td width=25%>&nbsp;</td></tr>";
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td width=25%>Primary CMBS:</td>":
                             "<td width=25%><b>&nbsp;",PRICMBS,"</b></td>";
.
          WRITE     HTMLFILE;"<td width=25%>Care Class:</td>";
.
          MOVE      SP20,TDESC
          MATCH     SP70,ACARE
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSC,ACARE
            CALL      RDCODE1
            WRITE     HTMLFILE;"<td width=25%><b>&nbsp;",ACARE,"</b>":
                               " -<b>&nbsp;",TDESC,"</b></td></tr>";
          ELSE
            WRITE     HTMLFILE;"<td width=25%>&nbsp;</td></tr>";
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td width=25%>Secondary CMBS:</td>":
                             "<td width=25%><b>&nbsp;",SECCMBS,"</b></td>":
                             "<td width=25%>&nbsp;</td>":
                             "<td width=25%>&nbsp;</td></tr>":
                             "<tr><td width=25%>Tertiary CMBS:</td>":
                             "<td width=25%><b>&nbsp;",TERCMBS,"</b></td>":
                             "<td width=25%>Contract ID</td>":
                             "<td width=25%><b>&nbsp;",CONTRCID,"</b></td></tr>";
.
          UNPACK    SP70,PMCIFDAT,PMCITDAT,KEY10,CPCDATE,KEY25
          MATCH     SP70,CONTRCID
          GOTO      DCMX1000 IF EQUAL
.
          MOVE      CONTRCID,KEY6
          CALL      RDPMCID1
          BRANCH    OVRCD,DCMX1000
.
          UNPACK    PMCIFDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the admin date
          REP       " 0",CPCDATE
          PACK      KEY10,CPCDATE,SP70
          UNPACK    PMCITDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the admin date
          REP       " 0",CPCDATE
.
          MOVE      SP70,KEY25
          PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF
          BRANCH    EXIT,DCMX1000
.
          IF        CNTRCEFR=0
            MOVE      "As at the Effective Date ",KEY25
          ELSE
            IF        CNTRCEFR=1
              MOVE      "Admission From         ",KEY25
            ELSE
              IF       CNTRCEFR=2
                MOVE      "Discharge From          ",KEY25
              ENDIF
            ENDIF
          ENDIF
.
DCMX1000  WRITE     HTMLFILE;"<tr><td width=25%>4th CMBS:</td>":
                             "<td width=25%><b>&nbsp;",FORCMBS,"</b></td>":
                             "<td width=25%>Contract Effective</td>":
                             "<td width=25%><b>&nbsp;",KEY25,"</b></td></tr>":
                             "<tr><td width=25%>5th CMBS:</td>":
                             "<td width=25%><b>&nbsp;",FIFCMBS,"</b></td>":
                             "<td width=25%>Effective From Date</td>":
                             "<td width=25%><b>&nbsp;",KEY10,"</b></td></tr>";
.
.
          WRITE     HTMLFILE;"<tr><td width=25%>6th CMBS:</td>":
                             "<td width=25%><b>&nbsp;",SIXCMBS,"</b></td>":
                             "<td width=25%>Effective To Date</td>":
                             "<td width=25%><b>&nbsp;",CPCDATE,"</b></td></tr>":
                             "<tr><td width=25%>Primary ICD Disease:</td>":
                             "<td width=25%><b>&nbsp;",PRIICDCD,"</b></td>":
                             "<td width=25%>Pre-Op Cut Off Days</td>":
                             "<td width=25%>&nbsp;",PMCCPODY,"</td></tr>";
.
          MOVE      SP20,TDESC
          MATCH     SP70,ASRCE
          IF        !@EQUAL
            PACK      KEY5,ANSS,SP1,ASRCE
            CALL      RDCODE1
          ENDIF
.
          WRITE    HTMLFILE;"<tr><td width=25%>Primary ICD Procedure:</td>":
                            "<td width=25%><b>&nbsp;",PRIICOCD,"</b></td>":
                            "<td width=25%>No Pay Days</td>":
                            "<td width=25%><b>&nbsp;",PMCCNPDY,"</b></td></tr>":
                            "<tr><td width=25%>Admission Source:</td>":
                            "<td width=25%><b>&nbsp;",ASRCE,"<b>":
                            " -<b>&nbsp;",TDESC,"</b></td>":
                            "<td width=25%>Mininum Days Stay</td>":
                            "<td width=25%>&nbsp;",PMCCMIND,"</td></tr>";
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
.
          PACK      PACFNAME,SP70
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        @EQUAL
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
          ENDIF
.
          WRITE    HTMLFILE;"<tr><td width=25%>Admission Type:</td>":
                            "<td width=25%><b>&nbsp;",ATYPE,"</b>":
                            " -<b>&nbsp;",TDESC,"</b></td>":
                            "<td width=25%>Maximum Days Stay</td>":
                            "<td width=25%><b>&nbsp;",PMCCHBPT,"</b></td></tr>";
.
          UNPACK    SP70,KEY2,AUDFCODE
          LOAD      KEY2,PTCNCMAU,CODEU1,CODEU2,CODEU3,CODEU4,CODEU5,CODEU6:
                            CODEU7,CODEU8,CODEU9,CODEV1,CODEV2
          LOAD      AUDFCODE,PTCNCMAU,AUSR1,AUSR2,AUSR3,AUSR4,AUSR5,AUSR6:
                             AUSR7,PTMIUSR8,PTMIUSR9,PTMIUSR0
.
          MATCH     SP70,KEY2
          GOTO      DCMX1020 IF EQUAL
          MATCH     SP70,AUDFCODE
          GOTO      DCMX1020 IF EQUAL
.
          PACK      KEY5,KEY2,AUDFCODE
          CALL      RDCODE1
          BRANCH    OVRCD,DCMX1020
.
          WRITE     HTMLFILE;"<tr><td width=25%>Admission UDF:</td>":
                            "<td width=25%><b>&nbsp;",AUDFCODE,"</b>":
                            " -<b>&nbsp;",TDESC,"</b></td>";
          GOTO      DCMX1030
.
DCMX1020  WRITE     HTMLFILE;"<tr><td width=25%>Admission UDF:</td>":
                            "<td width=25%><b>&nbsp;</b></td>";
.
DCMX1030  WRITE     HTMLFILE;"<td width=25%>Critical Care Cut Off</td>":
                            "<td width=25%>&nbsp;",PMCCCCDY,"</td></tr>";
.
          UNPACK    SP70,KEY2,DSCFCODE
          LOAD      KEY2,PTCNCMDU,CODED1,CODED2,CODED3,CODED4,CODED5
          LOAD      DSCFCODE,PTCNCMDU,DUSD1,DUSD2,DUSD3,DUSD4,DUSD5
.
          MATCH     SP70,KEY2
          GOTO      DCMX1040 IF EQUAL
          MATCH     SP70,DSCFCODE
          GOTO      DCMX1040 IF EQUAL
.
          PACK      KEY5,KEY2,DSCFCODE
          CALL      RDCODE1
          BRANCH    OVRCD,DCMX1040
.
          WRITE     HTMLFILE;"<tr><td width=25%>Discharge UDF:</td>":
                            "<td width=25%><b>&nbsp;",DSCFCODE,"</b>":
                            " -<b>&nbsp;",TDESC,"</b></td>";
          GOTO      DCMX1050
.
DCMX1040  WRITE     HTMLFILE;"<tr><td width=25%>Discharge UDF:</td>":
                            "<td width=25%><b>&nbsp;</b></td>";
.
DCMX1050  WRITE    HTMLFILE;"<td width=25%>Attending Dr</td>":
                            "<td width=25%><b>&nbsp;",PACFNAME,"</b></td></tr>";
.
          WRITE     HTMLFILE;"<tr><td width=25%>Concession Card Type:</td>";
.
          CALL      CNCD0000             * Check for Concession cards
.
          WRITE    HTMLFILE;"<tr><td width=25%>No of Days Hospitalisation</td>":
                            "<td width=25%><b>&nbsp;",ADYHOSP,"</b></td>":
                            "<td width=25%>&nbsp;</td>":
                            "<td width=25%>&nbsp;</td></tr>";
.
           MATCH    SP70,CONTRCID
           IF       @EQUAL
             PACK     DIM6A,DIM6A,SP70
             MATCH    SP70,DIM6A
             IF       !@EQUAL
.              WRITE     HTMLFILE;"<tr><td colspan=4><b>":
.                                 "Found a Contract ID record with No Rule: ":
.                                 DIM6A,"</b></tr>";
.
               WRITE     HTMLFILE;"<tr><td colspan=4><b>":
                                  "No matching Contract found for this patient":
                                  "- ",DIM6A,"</b></tr>";
             ENDIF
           ENDIF
.
. ----- Display matrix admission for enquiry programs -----
.
DCMX1100  MATCH     SP9,CMCODE
          GOTO      DCMX9000 IF NOT EQUAL   * Casemix code found
.
.         Matrix is not found and patient is discharged 
.         then use the provisional Casemix code for displaying invoice
.
.         MATCH     "Y",PTMICMXP
.         GOTO      DCMX1105 IF EQUAL
          MATCH     SP70,PTMIPCMX       * prov.casemix code
          GOTO      DCMX1110 IF EQUAL
.         MATCH     SP70,ACODDTE        * Coded?
.         GOTO      DCMX1110 IF NOT EQUAL
.
          MOVE      PTMIPCMX,CMCODE
          MOVE      PTMIPCMX,CMDRG
          MOVE      ONE,CMXFLAG             * casemix patient
          MOVE      ZERO,PCFLAG             * Default to casemix
          MOVE      ZERO,PMCCPODY           * pre op cut off days
          MOVE      ZERO,PMCCMIND           * min stay
          MOVE      ZERO,PMCCHBPT           * max stay/high boundary point
          MOVE      ZERO,PMCCCCDY           * critical care cut off days
          MOVE      ZERO,PMCCNPDY           * no pay days
.
DCMX1105
          IF        WEBFLAG=1
            WRITE     HTMLFILE;"<tr><td colspan=4><b>Casepayment incomplete. ":
                               "  Charges will be raised as perdiem.":
                               " Provisional Casemix Code: ":
                               PTMIPCMX,"</b></td></tr>";
          ENDIF
          GOTO      DCMX9000
.
DCMX1110  IF        WEBFLAG=1
            WRITE     HTMLFILE;"<tr><td colspan=4><b>":
                             "Casepayment incomplete. Charges ":
                             "showing as perdiem. </b></td></tr>";
          ENDIF
          MOVE      ZERO,CMXFLAG            * Revert to perdiem
          MOVE      ONE,PCFLAG              * Revert to perdiem
          GOTO      DCMX9000
.
DCMX9000  MOVE      ZERO,EXIT
DCMX9999  RETURN
+
.******************************************************************************
.         Get patient's concession cards
.******************************************************************************
CNCD0000  MOVE      ZERO,F2
          PACK      KEY19,AURNO,SP70
          CALL      RSPMCCD1
CNCD1000  CALL      RKPMCCD1
          BRANCH    OVRCD,CNCD9999
.
          MATCH     PMCDURNO,AURNO
          GOTO      CNCD9999 IF NOT EQUAL
.
          MATCH     SP70,PMCDEXDT              * Check the expired date
          IF        !@EQUAL
            PACK      KEY8,CCC,CYY,CMM,CDD
            IF        ASTAT=3
              MOVE      DDATE,KEY8
            ENDIF
            REP       " 0",KEY8
            MATCH     KEY8,PMCDEXDT
            GOTO      CNCD1000 IF LESS
          ENDIF
.
          MOVE      "ct",KEY2
          PACK      KEY5,KEY2,PMCDCTYP
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP20,TDESC
          ENDIF
.
          IF        F2<>0
            WRITE     HTMLFILE;"<tr><td width=25%>&nbsp;</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td width=25%><b>",PMCDCTYP,"</b>":
                             " - <b>",TDESC,"</b></td>":
                             "<td width=25%>&nbsp;</td>":
                             "<td width=25%>&nbsp;</td></tr>";
          ADD       ONE,F2
          GOTO      CNCD1000
.
CNCD9999  RETURN
+
.******************************************************************************
.*  PCMX0000 - print the casemix matrix fields if no valid record is found    *
.******************************************************************************
PCMX0000  PRINT     *N,*N,*22,"No Casemix Matrix Match found. ":
                    *N,*N,*15,*HOFF,"UR  : ",*V2LON,AURNO,SP9,*HOFF:
                    "Admission : ",*V2LON,DAADMNO
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          PRINT     *5,*HOFF,"Claim Code",*27,": ",*V2LON,ACLAIM:
                    *35,TDESC
.
          PRINT     *5,*HOFF,"Health Fund           : ",*V2LON,AFUNDH:
                    *35,HFNAME
.
          PACK      KEY14,AFUNDH,AFNDTB
          CALL      RDFUND1
          IF        OVRCD=1
            MOVE      SP70,HFNAME
            MOVE      SP70,PTHFBCAT
          ENDIF
.
          MOVE      SP20,TDESC
          MATCH     SP70,PTHFBCAT
          IF        !@EQUAL
            PACK      KEY5,ANSH,ANST,PTHFBCAT
            CALL      RDCODE1
          ENDIF
          PRINT     *5,*HOFF,"Table Type            : ",*V2LON,PTHFBCAT:
                    *35,TDESC
.
          PRINT     *5,*HOFF,"Discharge DRG         : ",*V2LON,PTDSDDRG:
                    *N,*5,*HOFF,"Primary CMBS          : ",*V2LON,PRICMBS:
                    *N,*5,*HOFF,"Secondary CMBS        : ",*V2LON,SECCMBS:
                    *N,*5,*HOFF,"Tertiary CMBS         : ",*V2LON,TERCMBS:
                    *N,*5,*HOFF,"4th CMBS              : ",*V2LON,FORCMBS:
                    *N,*5,*HOFF,"5th CMBS              : ",*V2LON,FIFCMBS:
                    *N,*5,*HOFF,"6th CMBS              : ",*V2LON,SIXCMBS;
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSD,SP1,DSTAT
          CALL      RDCODE1
          PRINT     *5,*HOFF,"Discharge Status      : ",*V2LON,DSTAT:
                    *35,TDESC
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSD,ANSD,DDEST
          CALL      RDCODE1
          PRINT     *5,*HOFF,"Discharge Destination : ",*V2LON,DDEST:
                    *35,TDESC
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSS,SP1,ASRCE
          CALL      RDCODE1
          PRINT     *5,*HOFF,"Admission Source      : ",*V2LON,ASRCE:
                    *35,TDESC
.
          MOVE      SP20,TDESC
          PRINT     *5,*HOFF,"Primary ICD Disease   : ",*V2LON,PRIICDCD
          MOVE      SP20,TDESC
          PRINT     *5,*HOFF,"Primary ICD Procedure : ",*V2LON,PRIICOCD
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
          PRINT     *5,*HOFF,"Admission Type        : ",*V2LON,ATYPE:
                    *35,TDESC
.
          PRINT     *5,*HOFF,"Admission UDF         : ",*V2LON,AUDFCODE;
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSC,ANSC,ACARE
          CALL      RDCODE1
          PRINT     *N,*5,*HOFF,"Care Class            : ",*V2LON,ACARE:
                    *35,TDESC
.
PCMX9999  RETURN
+
.******************************************************************************
.         Subroutine to get the appropriate miscellaneous charges record
.******************************************************************************
GMISC000  MOVE      ZERO,EXIT
....      OPEN      PATMCHG1,"patmchgf"
          PACK      MISCDATE,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY,SP8      *I-119158
          REP       " 0",MISCDATE
.
          UNPACK    SP20,PTHFBCAT      * change "H/F Table" to "Table Type"
          PACK      KEY14,AFUNDH,AFNDTB,SP20
          CALL      RDFUND1            * "Table Type code" now in PTHFBCAT
          IF        OVRCD=1
            MOVE      SP70,HFNAME
            MOVE      SP70,PTHFBCAT
          ENDIF

.
          PACK      KEY29,ACLAIM,AFUNDH,PTHFBCAT,TITEMNO,MISCDATE,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT 
          GOTO      GMISC999 IF EQUAL
.
.         No record with health fund details - try a blank health fund
.
          PACK      KEY29,ACLAIM,SP6,SP3,TITEMNO,MISCDATE,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT 
          GOTO      GMISC999 IF EQUAL
.
.         No record with health fund details - last chance, try claim 0
.
          MOVE      "0  ",MCLMCOD
          PACK      KEY29,MCLMCOD,SP6,SP3,TITEMNO,MISCDATE,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
.
GMISC999 RETURN
+
.******************************************************************************
.         Get the grouper version from health fund
.******************************************************************************
GVHF0000  OPEN      PATPGRA1,"patpgraf"
.
          MATCH     "1",PTCNDGED
          GOTO      GVHF1000 IF EQUAL
.
          PACK      KEY14,PRIMFUND,ZERO8
          CALL      RDFUND1
          IF        OVRCD=1
            MOVE      SP70,HFNAME
            MOVE      SP70,PTHFBCAT
          ENDIF
.
          MATCH     SP70,PRIMFUND
          IF        @EQUAL
            PACK      KEY5,ANSC,ANSL,ACLAIM
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      PTCDGRPV,PTHFGRPV       * Use claim code grouper version
            ENDIF
          ENDIF
.
          MATCH     SP70,PTHFGRPV
          IF        @EQUAL
            READ      CONTROLF,HUND05;*158,PTCNDGPV
            MOVE      PTCNDGPV,PTHFGRPV         * Use the Default Grouper vers.
          ENDIF
.
          GOTO      GVHF2000
.
.         Get DRG Version using 'Effective DRG' table (NEW)
.
GVHF1000  CALL      GTEDRG00
          IF        EXIT=0
            MOVE      KEY3,PTHFGRPV
          ELSE
            READ      CONTROLF,HUND05;*158,PTCNDGPV
            MOVE      PTCNDGPV,PTHFGRPV         * Use the Default Grouper vers.
          ENDIF
.
GVHF2000  MOVE      ONE,PTPGEPNO
          PACK      KEY13,AADMNO,PTPGEPNO,PTHFGRPV,SP70
          CALL      RDPTPGR1
          IF        OVRCD=0
.           MOVE      PTPGSDRG,PTDSDDRG     * Use Grouper Details 'State DRG'
            MOVE      PTPGNDRG,PTDSDDRG     * Use Grouper Details 'National DRG'
          ENDIF
.
          MATCH     SP70,PRIMFUND
          IF        !@EQUAL
            PACK      KEY14,PRIMFUND,AFNDTB
            CALL      RDFUND1
            IF        OVRCD=1
              MOVE      SP70,HFNAME
              MOVE      SP70,PTHFBCAT
            ENDIF
          ENDIF
.
GVHF9999  RETURN
.

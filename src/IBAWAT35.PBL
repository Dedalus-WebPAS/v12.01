.******************************************************************************
.* System   : WAITING LIST                                                    *
.* Program  : IBAWAT35                                                        *
.* Desc     : Booking List Summary                                            *
.******************************************************************************
.* Author   : Bill Dew                                                        *
.* Date     : 31/05/1990   6 months into the new decade                       *
.* Comments :                                                                 *
.* Mods     :                                                                 *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*        V5.01.14  22/02/1993    Justin Coates  (DUDLEY)                     *
.*                  allow for status 7 and 8                                  *
.*        V5.01.15  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.$$$$$$
.    Booking List Summary
.    --------------------
.
.    Program Description : This program calculates the details to generate the
.    Booking List Summary report.
.
.    Please Note : The Print routine PBLS0000 was copied from IBAWAT34 (OUT0000)
.    Thus If any changes are made to PBLS do like wise to WAT34
.
.    END
.$$$$$$
.*******************************************************************************
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC       * patient codes file
          INC       PATDO1FD/INC       * doctor details
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC       * patient admission 
          INC       PMSVX1FD/INC       * patient admission 
          INC       TFILEVAR/INC
          INC       WATCATFD/INC       * category change file
          INC       WATCONFD/INC       * waiting list system parameters
          INC       WATPROFD/INC       * procedure file
          INC       WATTR1FD/INC       * waiting list treatments
          INC       WEBERRFD/INC
+
.******************************************************************************
.                      GLOBAL VARIABLE DECLARATIONS
.******************************************************************************
.
CANHOS    FORM      5        * cancellations by hopital
CANPAT    FORM      5        * cancellations by patients
CANOTH    FORM      4        * cancellations by other
CODE      DIM       2
CODEBC    INIT      "BC"     * cancellation category
COUNTER   FORM      3        * General purpose 3 digit counter
DIM3      DIM       3
DOCTCODE  DIM       6        * Doctor code
DOCCODE   DIM       6
DOCTNAME  DIM       40       * Doctor's name 
DOLIST    FORM      6        * Days on the waiting list
.
FILEDATE  DIM       8        * file date format for report date
FORM8     FORM      8
HOSTOT    FORM      5
HOSTTT    FORM      5
INDEX     FORM      2
LINE80    DIM       80
.
MAX       FORM      6        * Maximum number of days
MAXTOT    FORM      6
MAXTTT    FORM      6
MEANTOT   FORM      6
MEANTTT   FORM      6
MEAN      FORM      6        * Mean NUmber of days
.
OPTION2   FORM      1        * second menu option var
OTHTTT    FORM      4
OTHTOT    FORM      4
ODURGENT  FORM      4        * overdue Urgent cases exceeding Urgent days
ODSEMI    FORM      5        * overdue semi urgent cases
ODROUT    FORM      4        * overdue routine cases
ODURGTOT  FORM      4
ODSEMTOT  FORM      5
ODROUTOT  FORM      4
ODURGTTT  FORM      4
ODSEMTTT  FORM      5
ODROUTTT  FORM      4
.
PATTOT    FORM      5
PATTTT    FORM      5
PRUNIT    DIM       3
PRINT33   DIM       33
PRNTHEAD  DIM       20
PRGRP     DIM       3
PROCDESC  DIM       33
PROCTOT   FORM      6 
PRDOCT    DIM       6
PTCNDCQS  FORM      1
.
RANGE1    FORM      5        * Number patients in wait list < Range 1
RANGE2    FORM      5        * Number patients in wait list (Range 1 < Range 2)
RANGE3    FORM      5        * Number patients in wait list (Range 2 < Range 3)
RANGE4    FORM      5        * Number patients in wait list (Range 3 < Range 4)
RANGE5    FORM      5        * Number patients in wait list (Range 4 < Range 5)
RANGE6    FORM      5        * Number patients in wait list (Range 5 < Range 6)
RANGE7    FORM      5        * Number patients in wait list (Range 6 < Range 7)
RANLIM1   FORM      3
RANLIM2   FORM      3
RANLIM3   FORM      3
RANLIM4   FORM      3
RANLIM5   FORM      3
RANLIM6   FORM      4
REPTDATE  DIM       10       * Report Date
.
SCRNFLAG  FORM      1
.
TODAY     DIM       8        * current date ccyymmdd
TOTAL     FORM      6        * total for all ranges
TOTDAY    FORM      6
TOTRAN1   FORM      5
TOTRAN2   FORM      5
TOTRAN3   FORM      5
TOTRAN4   FORM      5
TOTRAN5   FORM      5
TOTRAN6   FORM      5
TOTRAN7   FORM      5
TTTRAN1   FORM      5
TTTRAN2   FORM      5
TTTRAN3   FORM      5
TTTRAN4   FORM      5
TTTRAN5   FORM      5
TTTRAN6   FORM      5
TTTRAN7   FORM      5
TPROCTOT  FORM      6                * total of the procdure totals
TTPROCTT  FORM      6                * total of the procdure totals
.
UNITCLIN  DIM       3        * Unit Clinic Code
UNITDESC  DIM       20
.
.---------------------------------< IO keys >---------------------------------
.
.    I define all keys from 2 to 40, this avoids any Pissoff such as
.    "UNDEFINED VARIABLE" during compilation.
.
.
.---------------------------< TEMPFILE SETUP VARS >---------------------------
.
.    1-6    Doctor Code
.    7-9    Unit / Clinic code
.    10-12  Procedure Group
.    13-21  Procedure Code
.
DELCOMM   DIM       30       * Dos command to delete tempfile
DOSCMND1  DIM       50       * Dos command to isbuild tempfile
FILEKEY1  INIT      "U10-12,13-21"
FILEKEY2  INIT      "U7-9,13-21"
FILEKEY3  INIT      "U1-6,13-21"
INDXINFO  DIM       30
WATTMP1   IFILE     SQL, FIXED=79, KEY=INDXINFO
TEMPSIZE  INIT      " 79 "             * Please leave a space infront of number
TEMPNAM1  DIM       8        * Physical name for tempfile
.
.--------------------------------< CONSTANTS >---------------------------------
.
MENU1     DIM       29       * it's true value is set up in init
MENU2     INIT      "Unit/Clinic Code Sequence"
MENU3     INIT      "Doctor Code Sequence"
MENU1P    INIT      "PROC Grp Code Seq"
MENU2P    INIT      "Unit/Clin Code Seq"
MENU3P    INIT      "Doctor Code Seq"
NUM37     FORM      "37"
NUM365    FORM      "365"
.
PRGID     INIT      "IBAWAT35"
PRGNAM    INIT      "Booking List Summary"
VERSION   INIT      "V12.01.00"
+
.*******************************************************************************
.                                 MAINLINE
.                   This is MAIN LINE Rock & Rollllllllllllllll
.*******************************************************************************
ML0000    CALL      INIT0000               * open files     
.
ML1000    CALL      MENU0000               * select options
          BRANCH    MOPTION,ML1111,ML2222,ML3333
          GOTO      ML9999
.
ML1111    CALL      POPT0000           * select Print options
          BRANCH    OPTION2,ML1133,ML1133,ML1133
          GOTO      ML1000
.
ML1133    CALL      GDAT0000           * Get report date
          BRANCH    EXIT,ML1111
          CALL      CONTQST
          BRANCH    CEXIT,ML4444,ML1111,ML1000
          GOTO      ML4444
ML2222
          CALL      GDAT0000           * Get report date
          BRANCH    EXIT,ML1000
          CALL      GUNT0000           * Get UNiT / clinic code
          BRANCH    EXIT,ML2222
          CALL      CONTQST
          BRANCH    CEXIT,ML4444,ML2222,ML1000
          GOTO      ML4444
ML3333
          CALL      GDAT0000           * Get report date
          BRANCH    EXIT,ML1000
          CALL      GDOC0000           * Get doctor code
          BRANCH    EXIT,ML3333
          CALL      CONTQST
          BRANCH    CEXIT,ML4444,ML3333,ML1000
ML4444
          CALL      TEMP0000           * setup tempfile
          CALL      EXTR0000           * Tranfer data to tempfiles
          BRANCH    EXIT,ML1000
          CALL      PBLS0000           * Print Booking List Summary
          GOTO      ML1000
.
ML9999    CALL      DELTEMP1
          CHAIN     PGM
+
.***************************************************************************
.*        Initialisation , opening files                                   *
.***************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening watproaf";
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA2,"watcataf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,NUM37;*140,WTCNTRG1,WTCNTRG2,WTCNTRG3:
                                        WTCNTRG4,WTCNTRG5,WTCNTRG6:
                                   *173,WTCNURDY,WTCNSUDY,WTCNRODY:
                                   *122,WTCNTPUC,*185,WTCNTPDS
.
          MOVE      ONE,CNEWDS         * set ? routines to new standards
          PACK      TODAY,CCC,CYY,CMM,CDD
          REPLACE   " 0",TODAY
.
          CLEAR     MENU1              * set up Treatment/Procedure value
          APPEND    WTCNTPDS,MENU1
          APPEND    " Group Code Sequence",MENU1
          RESET     MENU1
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,TEMPNAM1
.
INIT9999  RETURN
.**************************************************************************
.                                 TEMP0000
.    SETUP tempfile for stats and storage
.**************************************************************************
TEMP0000
          DISPLAY   *P1:24,*EL,"Pre-processing please wait."
          LOAD      INDXINFO,MOPTION,FILEKEY1,FILEKEY2,FILEKEY3
.
.---- Setup DOSCMND1 : creates the tempfile for tray items and instruments ----
.
          CLEAR     DOSCMND1
          APPEND    "isbuild ",DOSCMND1
          APPEND    TEMPNAM1,DOSCMND1  * set the name of tempfile
          APPEND    TEMPSIZE,DOSCMND1  * set the tempfile size
          APPEND    INDXINFO,DOSCMND1
          RESET     DOSCMND1
.
          CALL      DELTEMP1           * delete the temp file
.
.TEMPQQ1   DISPLAY   *P1:3,"[",DOSCMND1,"]";
.TEMPQQK1  KEYIN     ANS
.
          EXECUTE   DOSCMND1,TASKID    * temp index file for item code order
          OPEN      WATTMP1,TEMPNAM1   * open the tempfile file pointer
TEMP9999  RETURN
.*****************************************************************************
DELTEMP1
          CLOSE      WATTMP1          * close 1st and 2nd index
          CLEAR      DELCOMM
          APPEND     "iserase ",DELCOMM 
          APPEND     TEMPNAM1,DELCOMM 
          RESET      DELCOMM 
          EXECUTE    DELCOMM,TASKID
          RETURN
.**************************************************************************
.                                 MENU0000
.    Menu driver routine
.*****************************************************************************
MENU0000  HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ",MENU1:
                    *P1:6,*V2LON,"2",*HOFF," = ",MENU2:
                    *P1:7,*V2LON,"3",*HOFF," = ",MENU3
MENU1111
          KEYIN     *P1:9,*EL,"Select Option : _",*P17:9,*V2LON,MOPTION
          COMPARE   ZERO,MOPTION
          GOTO      MENU9999 IF EQUAL
          BRANCH    MOPTION,MENU9000,MENU9000,MENU9000
          BEEP
          GOTO      MENU1111
MENU9000
          LOAD      LINE80,MOPTION,MENU1,MENU2,MENU3
          DISPLAY   *P50:2,*V2LON,"- ",*+,LINE80,SP1
.
          LOAD      PRNTHEAD,MOPTION,MENU1P,MENU2P,MENU3P
          MOVE      "- ",KEY2
          PACK      CPHDROPT,KEY2,PRNTHEAD
MENU9999  RETURN
.**************************************************************************
.                                 POPT0000
.    Menu driver routine
.*****************************************************************************
POPT0000
          DISPLAY   *P1:11,*EF:
                    *P1:11,*V2LON,"1",*HOFF," = All Patients":
                    *P1:12,*V2LON,"2",*HOFF," = Staged Admissions":
                    *P1:13,*V2LON,"3",*HOFF," = Deferred Admissions"
POPT1111
          KEYIN     *P1:15,*EL,"Select Option : _",*P17:15,*V2LON,OPTION2
          COMPARE   ZERO,OPTION2
          GOTO      POPT9999 IF EQUAL
          BRANCH    OPTION2,POPT9000,POPT9000,POPT9000
          BEEP
          GOTO      POPT1111
POPT9000
POPT9999  RETURN
.*****************************************************************************
.                                 GDAT0000
.    Get Report Date
.*****************************************************************************
GDAT0000
          BRANCH    MOPTION,GDAT0222,GDAT0444,GDAT0444
GDAT0222
          MOVE      "20",CCOL
          MOVE      "17",CVERT
          DISPLAY   *P1:CVERT," Report Date     :"
          GOTO      GDAT1111
GDAT0444
          MOVE      "20",CCOL
          MOVE      "11",CVERT
          DISPLAY   *P1:CVERT,*EF," Report Date     :"
GDAT1111
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY  * set todays date default
          MOVE      CCC,CCENT
GDAT3333
          CALL      KEYDATE            * start date default todays date
          BRANCH    OVRCD,GDAT9990
.
          PACK      FILEDATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",FILEDATE
          CALL      PACDATE
          MOVE      CPCDATE,REPTDATE
.
          MATCH     FILEDATE,TODAY
          GOTO      GDAT1111 IF LESS
.
          MOVE      ZERO,EXIT
          GOTO      GDAT9999
.
GDAT9990  MOVE      ONE,EXIT
GDAT9999  RETURN
.*****************************************************************************
.                                 GUNT0000
.    get unit / clinic code
.*****************************************************************************
GUNT0000
          MOVE      ONE,SCRNFLAG       * set the redisplay flag
GUNT1000
          DISPLAY   *P1:13,*EL,"Unit/Clinic Code : ___",*P20:13;
GUNT1111  
          KEYIN     *V2LON,UNITCLIN    * get unit clinic code
          ENDSET    UNITCLIN
          APPEND    SP3,UNITCLIN
          RESET     UNITCLIN
.
          MATCH     SP3,UNITCLIN       * test for return hit
          GOTO      GUNT7000 IF EQUAL
.
          CMATCH    EXITCHAR,UNITCLIN  * test for return hit
          GOTO      GUNT7777 IF EQUAL
.
          CMATCH    "?",UNITCLIN       * test for ? routine
          GOTO      GUNT6666 IF NOT EQUAL
          PACK      CODE,ANSW,ANSU
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG      * signal requiring redisplay
GUNT4444
          DISPLAY   *P1:24,*EL,"Unit/Clinic : ___",*P15:24;
          GOTO      GUNT1111
GUNT6666
          PACK      KEY5,ANSW,ANSU,UNITCLIN,SP5
          CALL      RDCODE1
          BRANCH    OVRCD,GUNT8888
          CALL      REDP0000           * do redisplay if needed
          DISPLAY   *P20:13,*V2LON,UNITCLIN,*HOFF,SP4,TDESC
          MOVE      TDESC,UNITDESC
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      GUNT9999
GUNT7000  
          CALL      REDP0000
.
          COMPARE   ZERO,SCRNFLAG
          GOTO      GUNT0000 IF EQUAL       * re-enter as ? entered
.
          DISPLAY   *P20:13,*V2LON,*EL,"All Unit/Clinic codes"
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      GUNT9999
GUNT7777  
          DISPLAY   *P1:13,*EL
          MOVE      ONE,EXIT           * exit=1 if return hit
          GOTO      GUNT9999
GUNT8888
          DISPLAY   *P1:24,*B,*EL,"Unit/Clinic code not on file.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,GUNT1000
          GOTO      GUNT4444
GUNT9999  RETURN
.*****************************************************************************
.                                 REDP0000
.    Redisplay routine
.*****************************************************************************
REDP0000
          BRANCH    SCRNFLAG,REDP9999
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ",MENU1:
                    *P1:6,*V2LON,"2",*HOFF," = ",MENU2:
                    *P1:7,*V2LON,"3",*HOFF," = ",MENU3:
                    *P1:9,*EL,"Select Option : ",*V2LON,MOPTION
.
          BRANCH    MOPTION,REDP1111,REDP2222,REDP3333
REDP1111
          DISPLAY   *P1:11,*V2LON,"1",*HOFF," = All Patients":
                    *P1:12,*V2LON,"2",*HOFF," = Staged Admissions":
                    *P1:13,*V2LON,"3",*HOFF," = Deferred Admissions":
                    *P1:15,*EL,"Select Option : ",*V2LON,OPTION2:
                    *P1:17," Report Date     : ",*V2LON,REPTDATE
          
          GOTO      REDP9999
REDP2222
          DISPLAY   *P1:11,"Report Date      : ",*V2LON,REPTDATE
          DISPLAY   *P1:13,"Unit/Clinic Code :"
          GOTO      REDP9999
REDP3333
          DISPLAY   *P1:11,"Report Date      : ",*V2LON,REPTDATE
          DISPLAY   *P1:13,"Doctor Code      :"
          GOTO      REDP9999
REDP9999  RETURN
.*****************************************************************************
.                                 GDOC0000
.    get unit / clinic code
.*****************************************************************************
GDOC0000  DISPLAY   *P1:13,*EL," Doctor Code     : ______",*P20:13;
.
          MOVE      SP10,DOCTCODE
          MOVE      "20",ECOL
          MOVE      "13",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,GDOC7000,GDOC7777
.
          MOVE      DCODE,DOCTCODE
.
          PACK      KEY6,DOCTCODE,SP6
          CALL      DOCN0000
          DISPLAY   *P20:13,*V2LON,DOCTCODE,*HOFF,SP4,DOCTNAME
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      GDOC9999
.
GDOC7000  DISPLAY   *P20:13,*V2LON,*EL,"All Doctors"
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      GDOC9999
.
GDOC7777  DISPLAY   *P1:13,*EL         * clear the above prompt
          MOVE      ONE,EXIT           * exit=1 if return hit
          GOTO      GDOC9999
.
GDOC9999  RETURN
.*******************************************************************************
.                                 CANC0000
.    Created by Bill Dew 27/07/1990
.    Read the watcataf for cancellation codes category BC and test indicators
.    for cancellations by patient hospital or other.
.*******************************************************************************
CANC0000
          PACK      KEY29,WMURNO,WMCODE,WMCNT,SP20
          CALL      RSWTCAT2           * position fptr in watcataf
CANC2222
          CALL      RKWTCAT2
          BRANCH    OVRCD,CANC9999
.
.CANCQQ2   DISPLAY   *P1:20,*EL,"CANC>",WTCAEDAT,"|",WTCAURNO,"|",WTCAPROC:
.                    "|",WTCACATF,"|",WTCACODT,"|";
          MATCH     WTCAURNO,WMURNO    * test same patient
          GOTO      CANC9999 IF NOT EQUAL
          MATCH     WTCAPROC,WMCODE    * test same procedure code 
          GOTO      CANC9999 IF NOT EQUAL
          COMPARE   WTCAPCNT,WMCNT     * test same procedure code counter
          GOTO      CANC9999 IF NOT EQUAL
          MATCH     CODEBC,WTCACATF    * test for a cancellation code BC
          GOTO      CANC2222 IF NOT EQUAL
          MATCH     WTCAEDAT,FILEDATE  * test if greater end date range
          GOTO      CANC2222 IF LESS
.
          PACK      KEY5,CODEBC,WTCACODT
          CALL      RDCODE1            * get 2nd indicator 
          BRANCH    OVRCD,CANC2222
.
.CANCQQ3   DISPLAY   *P70:20,WTCACODT," ",TCINDC2;
.CANCQQK3  KEYIN     ANS
          MATCH     ANSH,TCINDC2       * test cancelled by hospital
          GOTO      CANC5555 IF NOT EQUAL
          ADD       ONE,CANHOS         * increment cancelled by hosp counter
          GOTO      CANC2222
CANC5555
          MATCH     ANSP,TCINDC2       * test cancelled by patient
          GOTO      CANC6666 IF NOT EQUAL
          ADD       ONE,CANPAT         * increment cancelled by patient counter
          GOTO      CANC2222
CANC6666
          ADD       ONE,CANOTH         * increment canelled by other code
          GOTO      CANC2222
CANC9999  RETURN
.************************************************************************
.*                        EXTR0000                                      *
.*            Extract values and put in tempfile                        *
.*                   called by : ML1000                                 *
.    This routine was copied from IBAWAT34 ( programmed by P. Duncan )
.    Modified by Bill Dew to suit IBAWAT35
.
.    Write to Tempfile if the following :
.    Procedure status is 1 2 3
.    If procedure status is 4 or 5 read PATMI1FD and test the Admission date
.    is greater than the report date.
.    If procedure status is 6, test the waiting list removal date is greater
.    than report date
.************************************************************************
EXTR0000  MOVE      ZERO,FORM8         * zero the record counter
          PACK      KEY27,SP30         * position at start
          CALL      RDSTREA3
.
EXTR1000  CALL      RDKTREA3
          BRANCH    OVRCD,EXTR7000
.
          MATCH     WMDATE1,FILEDATE   * Test date reached report date
          GOTO      EXTR7000 IF LESS
.
          BRANCH    WMSTAT,EXTR1199,EXTR1199,EXTR1199,EXTR1111:
                           EXTR1111,EXTR1155,EXTR1111,EXTR1155
.
. status 4,5,7
.
EXTR1111  MOVE      WMBOOK,KEY8    
          CALL      RDMISS1            * get admission date
          BRANCH    OVRCD,EXTR1133     * no admission record -data error -ignor
          PACK      KEY8,ADATE     * setup admission date in KEY8 for test
.
EXTR1122  MATCH     KEY8,FILEDATE      * admission date must be in date range
          GOTO      EXTR1000 IF LESS   * if Admission date > report date
          GOTO      EXTR1199           ** go and write record
.
EXTR1133  MOVE      WMDATE3,KEY8       * use sched adm date if adm rec missing
          GOTO      EXTR1122
.
. status 6,8
.
EXTR1155  MATCH     WMDATE4,FILEDATE   * test Removal date < report date
          GOTO      EXTR1000 IF LESS
.
EXTR1199  MOVE      SP3,WPGRP
          PACK      KEY9,WMCODE
          CALL      RDPROA1            * get procedure group code
.
          BRANCH    MOPTION,EXTR2000,EXTR3000,EXTR4000
.
.    Option 1 check for all patients(1), staged(2) or deferred(3)
.
EXTR2000  BRANCH    OPTION2,EXTR2888,EXTR2222,EXTR2666
.
EXTR2222  PACK      KEY5,ANST,ANSP,WMPTY
          CALL      RDCODE1            * get second indicator
          MATCH     ANST,TCINDC2       * test for staged priority status
          GOTO      EXTR1000 IF NOT EQUAL
          GOTO      EXTR2888
.
EXTR2666  PACK      KEY5,ANST,ANSP,WMPTY
          CALL      RDCODE1            * get second indicator
          MATCH     ANSD,TCINDC2       * test for deferred priority status
          GOTO      EXTR1000 IF NOT EQUAL
          GOTO      EXTR2888
.
EXTR2888  PACK      KEY15,WPGRP,WMCODE
          GOTO      EXTR5000
.
.    Option 2 : unit/clinic code
.
EXTR3000  PACK      KEY15,WMUNIT,WMCODE
          MATCH     SP3,UNITCLIN       * test for printing all unit codes
          GOTO      EXTR5000 IF EQUAL
          MATCH     UNITCLIN,WMUNIT    * if not - is this the right code
          GOTO      EXTR1000 IF NOT EQUAL * no - go back
          GOTO      EXTR5000
.
.    Option 3 : Doctor code
.
EXTR4000  PACK      KEY15,WMDOCTOR,WMCODE
          MATCH     SP6,DOCTCODE       * test for printing all doctor codes
          GOTO      EXTR5000 IF EQUAL
          MATCH     DOCTCODE,WMDOCTOR  * if not - is this the right code
          GOTO      EXTR1000 IF NOT EQUAL * no - go back
.
EXTR5000  ADD       ONE,FORM8          * increment the record counter
          CALL      RDTMPR1            * do a full read
          BRANCH    OVRCD,EXTR6000     * new record
          CALL      CDOL0000           * calculate days on list for patient
          CALL      UPDT0000           * update correct field 
          ADD       DOLIST,TOTDAY      * add the Days On List to total days
          CALL      MAX0000            * update the maximum
          CALL      OVER0000           * update overdue stats
          CALL      CANC0000           * calculate cancellation patients
          CALL      UPTMPR1            * update with first key
          GOTO      EXTR1000
.
EXTR6000  CALL      SETF0000           * set fields ready to write new record
          CALL      CDOL0000           * calculate days on list for patient
          CALL      UPDT0000           * update correct field
          ADD       DOLIST,TOTDAY      * add the Days On List to total days
          CALL      MAX0000            * update the maximum
          CALL      OVER0000           * update overdue stats
          CALL      CANC0000           * calculate cancellation patients
          CALL      WRTMPR1            * write with the first key
          GOTO      EXTR1000
.
EXTR7000  MOVE      ZERO,EXIT
          COMPARE   ZERO,FORM8         * test if any records saved to tempfile
          GOTO      EXTR9999 IF NOT EQUAL
          DISPLAY   *P1:24,"No patients admitted.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
EXTR9999  RETURN
+
.************************************************************************
.*                        CDOL0000                                      *
.*              Calculate number of days on list for patient            *
.*                   called by : EXTR1500                               *
.************************************************************************
CDOL0000  UNPACK    WMDATE1,XCENT,XYEAR,XMON,XDAY
          BRANCH    WMSTAT,CDOL1100,CDOL1100,CDOL1100,CDOL1400:
                           CDOL1400,CDOL1600,CDOL1400,CDOL1600
.
. ------- unscheduled,scheduled,pre-ad use enetered date -------
.
CDOL1100  UNPACK    FILEDATE,CCENT,CYEAR,CMON,CDAY
          GOTO      CDOL8000
.
. ------- for admitted/discharged/performed use admission date --------
.
CDOL1400  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY        * set admission date
          GOTO      CDOL8000
.
. ------- removed/not performed -------
.
CDOL1600  UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
          GOTO      CDOL8000
.
CDOL8000  MOVE      XYEAR,YY
          MOVE      XCENT,CC
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON                       * get julien date
          MOVE      JULDAY,COUNTER
.
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                       * get julien date
.
          MATCH     XYEAR,CYEAR                  * same year?
          GOTO      CDOL9000 IF EQUAL
.
          MOVE      ZERO,DOLIST
          MOVE      CYEAR,DOLIST
          MOVE      XYEAR,FORM2
          SUB       FORM2,DOLIST                 * no - how many years then
          MULT      NUM365,DOLIST                   * convert to days
          ADD       NUM365,DOLIST
          SUB       COUNTER,DOLIST               * sub the bit of the first year
                                                 * that they werent on the list
          ADD       JULDAY,DOLIST                * add part of the last year 
                                                 * that they were
.
          SUB       NUM365,DOLIST                  * if so sub one
          GOTO      CDOL9999
.
CDOL9000  MOVE      JULDAY,DOLIST                * same year - easy
          SUB       COUNTER,DOLIST
          GOTO      CDOL9999
.
CDOL9999  RETURN
+
.************************************************************************
.*                         UPDT0000                                     *
.*            Update the field in the range of the patient              *
.*                   called by : EXTR0000                               *
.************************************************************************
UPDT0000  COMPARE   DOLIST,WTCNTRG6              * in the range 7?
          GOTO      UPDT1000 IF NOT LESS
.
          ADD       ONE,RANGE7
          GOTO      UPDT9999
.         
UPDT1000  COMPARE   DOLIST,WTCNTRG5              * in the range 6?
          GOTO      UPDT2000 IF NOT LESS
.
          ADD       ONE,RANGE6
          GOTO      UPDT9999          
.         
UPDT2000  COMPARE   DOLIST,WTCNTRG4              * in the range 5?
          GOTO      UPDT3000 IF NOT LESS
.
          ADD       ONE,RANGE5
          GOTO      UPDT9999          
.         
UPDT3000  COMPARE   DOLIST,WTCNTRG3              * in the range 4?
          GOTO      UPDT4000 IF NOT LESS
.
          ADD       ONE,RANGE4
          GOTO      UPDT9999          
.         
UPDT4000  COMPARE   DOLIST,WTCNTRG2              * in the range 3?
          GOTO      UPDT5000 IF NOT LESS
.
          ADD       ONE,RANGE3
          GOTO      UPDT9999          
.         
UPDT5000  COMPARE   DOLIST,WTCNTRG1              * in the range 2?
          GOTO      UPDT6000 IF NOT LESS
.
          ADD       ONE,RANGE2
          GOTO      UPDT9999          
.         
UPDT6000  
          ADD       ONE,RANGE1                  * must be in range 1!!
.         
UPDT9999  RETURN
.************************************************************************
.*                         MAX0000                                      *
.*                 Update maximum if needed                             *
.*                   called by : EXTR1500                               *
.************************************************************************
MAX0000
         COMPARE    MAX,DOLIST             * is this number of days greater 
         GOTO       MAX9999 IF LESS
         MOVE       DOLIST,MAX 
MAX9999  RETURN
.************************************************************************
.                                 OVER0000
.    Calculate all patients overdue in each category Ugent Semi-urgent and
.    routine cases
.************************************************************************
OVER0000
          PACK      KEY5,ANST,ANSP,WMPTY
          CALL      RDCODE1            * get indicator for priority code
          MATCH     TCINDC1,ANSR            * for routine admission
          GOTO      OVER1000 IF NOT EQUAL
          COMPARE   DOLIST,WTCNRODY         * is it within the max time
          GOTO      OVER9999 IF NOT LESS
.
          ADD       ONE,ODROUT         * increment routine case counter
          GOTO      OVER9999
.
OVER1000
          MATCH     TCINDC1,ANSS            * for semi-urgent admiss
          GOTO      OVER2000 IF NOT EQUAL
          COMPARE   DOLIST,WTCNSUDY         * is it swithin max time
          GOTO      OVER9999 IF NOT LESS    * no - must be overdue
.
          ADD       ONE,ODSEMI              * increment semi-urgent case counter
          GOTO      OVER9999
.
OVER2000
          MATCH     TCINDC1,ANSU            * for urgent adm.
          GOTO      OVER9999 IF NOT EQUAL
          COMPARE   DOLIST,WTCNURDY         * is it within max time
          GOTO      OVER9999 IF NOT LESS    * no - must be overdue
.
          ADD       ONE,ODURGENT            * increment the urgent case counter
          GOTO      OVER9999
OVER9999  RETURN
.************************************************************************
.*                         SETF0000                                     *
.*            Set the fields for writing a new record                   *
.*                   called by : EXTR0000                               *
.************************************************************************
SETF0000  MOVE      ZERO,RANGE1
          MOVE      ZERO,RANGE2
          MOVE      ZERO,RANGE3
          MOVE      ZERO,RANGE4
          MOVE      ZERO,RANGE5
          MOVE      ZERO,RANGE6
          MOVE      ZERO,RANGE7
          MOVE      ZERO,MAX
          MOVE      ZERO,TOTDAY
          MOVE      ZERO,ODURGENT
          MOVE      ZERO,ODSEMI
          MOVE      ZERO,ODROUT
          MOVE      ZERO,CANHOS 
          MOVE      ZERO,CANPAT 
          MOVE      ZERO,CANOTH 
SETF9999  RETURN
.************************************************************************
.                                 PBLS0000
.    Print Booking List Summary
.************************************************************************
PBLS0000  MOVE      ZERO,CPAGENO      * Set up heading variables
          MOVE      ZERO,FORM8
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
.
          DISPLAY   *P1:24,*EL,"Printing Record : "
.
          UNPACK    SP30,PRGRP,PRUNIT,PRDOCT     * clear previous codes
.
PBLS1000  PACK      KEY15,SP30
          CALL      RSTMPR1
.
PBLS1500  CALL      RKTMPR1            * read data extracted into tempfile
          BRANCH    OVRCD,PBLS5000
.
          BRANCH    MOPTION,PBLS1600,PBLS1700,PBLS1800
.
PBLS1600  IF        CPAGENO=0
              CALL      HEAD0000
          ENDIF
          MATCH     WPGRP,PRGRP
          GOTO      PBLS3333 IF EQUAL
          CALL      PRST0000           * Print total for last group
          CALL      HEAD0000
          MOVE      WPGRP,PRGRP        * set up previous gropu code
          GOTO      PBLS3333
.
PBLS1700  MATCH     WMUNIT,PRUNIT 
          GOTO      PBLS3333 IF EQUAL
          CALL      PRST0000           * Print total for last group
          CALL      HEAD0000
          MOVE      WMUNIT,PRUNIT      * set up previous procedure code
          GOTO      PBLS3333
.
PBLS1800  MATCH     WMDOCTOR,PRDOCT
          GOTO      PBLS3333 IF EQUAL
          CALL      PRST0000           * Print total for last doctor
          CALL      HEAD0000
          MOVE      WMDOCTOR,PRDOCT    * set up previous doctor code
.
PBLS3333  DISPLAY   *P19:24,*V2LON,WMCODE
.
          COMPARE   "55",CLNO          * new page?
          CALL      HEAD0000 IF NOT LESS
          CALL      FRMT0000           * format data to print
          CALL      PRNT0000           * print a line of output
          GOTO      PBLS1500
.
PBLS5000  CALL      PRST0000           * print sub-totals
          CALL      END0000            * print grand totals
.
PBLS9999  RETURN
.************************************************************************
.*                        HEAD0000                                      *
.*                 Produce printout heading                             *
.************************************************************************
HEAD0000  
          CALL      HEAD132       
.HEADQQQ   DISPLAY   *P70:3,"P",CPAGENO
.
          BRANCH    MOPTION,HEAD1000,HEAD2000,HEAD3000
HEAD1000
          PRINT     *40,"Report Date  : ",REPTDATE
          PRINT     *40,WTCNTPDS," Group Code Sequence : ";
          BRANCH    OPTION2,HEAD1111,HEAD1222,HEAD1333
HEAD1111  PRINT     "All Patients",*N
          GOTO      HEAD4000
HEAD1222  PRINT     "Staged Admissions",*N
          GOTO      HEAD4000
HEAD1333  PRINT     "Deferred Admissions",*N
          GOTO      HEAD4000
HEAD2000
          PRINT     *40,"Report Date      : ",REPTDATE
          MATCH     SP3,UNITCLIN
          GOTO      HEAD2500 IF EQUAL
          PRINT     *40,"Unit/Clinic Code : ",UNITCLIN,SP4,UNITDESC,*N
          GOTO      HEAD4000
HEAD2500  PRINT     *40,"Unit/Clinic Code : All Patients"
          PACK      KEY5,ANSW,ANSU,WMUNIT
          CALL      RDCODE1
          PRINT     *N,"Unit/Clinic Code : ",WMUNIT,SP4,TDESC
          GOTO      HEAD4000
HEAD3000
          PRINT     *40,"Report Date : ",REPTDATE
          MATCH     SP6,DOCTCODE
          GOTO      HEAD3500 IF EQUAL
          PACK      KEY6,DOCTCODE,SP6
          CALL      DOCN0000
          PRINT     *40,"Doctor Code : ",DOCTCODE,SP4,DOCTNAME,*N
          GOTO      HEAD4000
HEAD3500  PRINT     *40,"Doctor Code : All Doctors"
          PACK      KEY6,WMDOCTOR,SP6
          CALL      DOCN0000
          PRINT     *N,"Doctor Code : ",WMDOCTOR,SP4,DOCTNAME
          GOTO      HEAD4000
HEAD4000
          CALL      UND132
          MOVE      WTCNTRG1,RANLIM1          * set up lower limits of ranges
          ADD       ONE,RANLIM1
          MOVE      WTCNTRG2,RANLIM2
          ADD       ONE,RANLIM2
          MOVE      WTCNTRG3,RANLIM3
          ADD       ONE,RANLIM3
          MOVE      WTCNTRG4,RANLIM4
          ADD       ONE,RANLIM4
          MOVE      WTCNTRG5,RANLIM5
          ADD       ONE,RANLIM5
          MOVE      WTCNTRG6,RANLIM6
          ADD       ONE,RANLIM6
          PRINT     "|",*36,"|",*43,"|",*59,"Distrubution",*85,"|":
                    *88,"Statistics",*99,"|",*104,"Overdue",*115,"|":
                    *119,"Cancelled",*132,"|",*N,"|",*36,"|",*43,"|",*48,ZERO:
                    *49,"|",*52,RANLIM1,*55,"|",*58,RANLIM2,*61,"|",*64:
                    RANLIM3,*67,"|",*70,RANLIM4,*73,"|",*76,RANLIM5,*79:
                    "|",*81,RANLIM6,*85,"|",*99,"|",*115,"|",*132,"|",*N:
                    "|",*3,WTCNTPDS," Description",*36,"|",*38,"Total":
                    *43,"|",*46,WTCNTRG1,*49,"|",*52,WTCNTRG2,*55,"|",*58:
                    WTCNTRG3,*61,"|",*64,WTCNTRG4,*67,"|",*70,WTCNTRG5,*73:
                    "|",*76,WTCNTRG6,*79,"|",*83,"+",*85,"|",*88,"Mean":
                    *92,"|",*96,"Max",*99,"| Urg| S-Ur| Rou|":
                    *117,"Hos",*121,"|",*123,"Pat",*127,"|":
                    *129,"Oth",*132,"|" 
          CALL      UND132
.
          ADD      NINE,CLNO
.
HEAD9999  RETURN
.************************************************************************
.*                        FRMT0000                                      *
.*          Convert data to the format to print                         *
.*                   called by : PBLS1500,PBLS2500                        *
.************************************************************************
FRMT0000   MOVE     WMCODE,PROCDESC
           SCAN     SP6,PROCDESC
           APPEND   " - Unknown ",PROCDESC
           APPEND   WTCNTPDS,PROCDESC
           MOVE     WMCODE,KEY9         
           CALL     RDPROA1              * get proc. description
           BRANCH   OVRCD,FRMT9999
           MOVE     WPDESC,PROCDESC      * move it to the print variable
.
FRMT9999   RETURN
.************************************************************************
.*                        PRNT0000                                      *
.*              Print a line of output                                  *
.*                   called by : PBLS1500,PBLS2500                        *
.************************************************************************
PRNT0000  
          MOVE      ZERO,PROCTOT
          ADD       RANGE1,PROCTOT
          ADD       RANGE2,PROCTOT
          ADD       RANGE3,PROCTOT
          ADD       RANGE4,PROCTOT
          ADD       RANGE5,PROCTOT
          ADD       RANGE6,PROCTOT
          ADD       RANGE7,PROCTOT
          DIV       PROCTOT,TOTDAY
          MOVE      TOTDAY,MEAN
          PRINT     "| ",PROCDESC,"|",PROCTOT,"|",RANGE1,"|",RANGE2,"|":
                    RANGE3,"|",RANGE4,"|",RANGE5,"|",RANGE6,"|":
                    RANGE7,"|",*86,MEAN,*92,"|",*93:
                    MAX,*99,"|",*100,ODURGENT,"|",ODSEMI,"|",ODROUT,"|":
                    *116,CANHOS,*121,"|":
                    *122,CANPAT,*127,"|",*128,CANOTH,*132,"|"
.
          ADD       ONE,CLNO                    * update line number
          ADD       RANGE1,TOTRAN1              * update all the total variables
          ADD       RANGE2,TOTRAN2
          ADD       RANGE3,TOTRAN3
          ADD       RANGE4,TOTRAN4
          ADD       RANGE5,TOTRAN5
          ADD       RANGE6,TOTRAN6
          ADD       RANGE7,TOTRAN7
          ADD       PROCTOT,TPROCTOT
          ADD       MEAN,MEANTOT
          ADD       MAX,MAXTOT
          ADD       ODURGENT,ODURGTOT
          ADD       ODSEMI,ODSEMTOT
          ADD       ODROUT,ODROUTOT
          ADD       CANHOS,HOSTOT
          ADD       CANPAT,PATTOT
          ADD       CANOTH,OTHTOT
.
PRNT9999  RETURN
.************************************************************************
.*                        PRST0000                                      *
.*           Print Sub-total for change in group,doc or unit            *
.*                   called by :  PBLS1000                               *
.************************************************************************
PRST0000
          COMPARE   ZERO,CPAGENO       * test for first page
          GOTO      PRST9999 IF EQUAL
.
          CALL      UND132
          BRANCH    MOPTION,PRST1000,PRST2000,PRST3000
PRST1000
          CLEAR     KEY33
          APPEND    "Unknown ",KEY33
          APPEND    WTCNTPDS,KEY33
          APPEND    "  Sub-Total   ",KEY33
.
          MATCH     SP3,PRGRP          * test group code is non-blank
          GOTO      PRST1500 IF EQUAL
.
          PACK      KEY5,ANSW,ANSG,PRGRP
          CALL      RDCODE1
          BRANCH    OVRCD,PRST1500
          PACK      KEY33,SP30,SP30
          CLEAR     KEY33
          MOVE      TDESC,KEY33
          SETLPTR   KEY33,33
          RESET     KEY33           
          SCAN      SP6,KEY33
          APPEND    "Sub-Total",KEY33
PRST1500  
          PRINT     "|",*3,KEY33;
          GOTO      PRST4000
.
PRST2000  MOVE      "Unknown Unit",TDESC
          PACK      KEY5,ANSW,ANSU,PRUNIT
          CALL      RDCODE1
          PRINT     "|",*3,"Total ",PRUNIT," - ",TDESC;
          GOTO      PRST4000
.
PRST3000  MOVE      "Unknown Doctor",KEY18       * Use KEY18 to load doct's name
          MOVE      PRDOCT,KEY6
          CALL      DOCN0000
          MOVE      PACFNAME,KEY18
.
PRST3500  PRINT     "|",*3,"Total ",PRDOCT," - ",KEY18;
. 
PRST4000  PRINT     *36,"|",*37,TPROCTOT,*43,"|",*44,TOTRAN1,*49,"|":
                    *50,TOTRAN2,*55,"|",*56,TOTRAN3,*61,"|",*62,TOTRAN4:
                    *67,"|",*68,TOTRAN5,*73,"|",*74,TOTRAN6,*79,"|":
                    *80,TOTRAN7,*85,"|",*86,MEANTOT,*92,"|",*93,MAXTOT:
                    *99,"|",ODURGTOT,"|",ODSEMTOT,"|",ODROUTOT,"|":
                    *116,HOSTOT,*121,"|",*122,PATTOT,*127,"|",*128,OTHTOT:
                    *132,"|"
          CALL      UND132
          ADD       THREE,CLNO
.
          ADD       TOTRAN1,TTTRAN1     * update all the total-total variables
          ADD       TOTRAN2,TTTRAN2
          ADD       TOTRAN3,TTTRAN3
          ADD       TOTRAN4,TTTRAN4
          ADD       TOTRAN5,TTTRAN5
          ADD       TOTRAN6,TTTRAN6
          ADD       TOTRAN7,TTTRAN7
          ADD       TPROCTOT,TTPROCTT
          ADD       MEANTOT,MEANTTT
          ADD       MAXTOT,MAXTTT
          ADD       ODURGTOT,ODURGTTT
          ADD       ODSEMTOT,ODSEMTTT
          ADD       ODROUTOT,ODROUTTT
          ADD       HOSTOT,HOSTTT 
          ADD       PATTOT,PATTTT
          ADD       OTHTOT,OTHTTT
.
          MOVE      ZERO,TOTRAN1              * Clear all totals
          MOVE      ZERO,TOTRAN2
          MOVE      ZERO,TOTRAN3
          MOVE      ZERO,TOTRAN4
          MOVE      ZERO,TOTRAN5
          MOVE      ZERO,TOTRAN6
          MOVE      ZERO,TOTRAN7
          MOVE      ZERO,TPROCTOT
          MOVE      ZERO,MEANTOT
          MOVE      ZERO,MAXTOT
          MOVE      ZERO,ODURGTOT
          MOVE      ZERO,ODSEMTOT
          MOVE      ZERO,ODROUTOT
          MOVE      ZERO,HOSTOT
          MOVE      ZERO,PATTOT
          MOVE      ZERO,OTHTOT
.
PRST9999  RETURN
.************************************************************************
.*                        END0000                                       *
.*             Print total and end of report                            *
.*                   called by :  PBLS4000                               *
.************************************************************************
END0000   
          PRINT     " "
          CALL      UND132
          PRINT     "|",*3,"Grand Total",*36,"|",*37,TTPROCTT,*43,"|":
                    *44,TTTRAN1,*49,"|",*50,TTTRAN2,*55,"|",*56,TTTRAN3,*61,"|":
                    *62,TTTRAN4,*67,"|",*68,TTTRAN5,*73,"|",*74,TTTRAN6,*79,"|":
                    *80,TTTRAN7,*85,"|",*86,MEANTTT,*92,"|",*93,MAXTTT,*99,"|":
                    ODURGTTT,"|",ODSEMTTT,"|",ODROUTTT,"|",*116,HOSTTT:
                    *121,"|",*122,PATTTT,*127,"|",*128,OTHTTT,*132,"|"
          CALL      UND132
          PRINT     *N,*N,"*** End of Report ***"
          MOVE      ZERO,TTTRAN1     * update all the total-total variables
          MOVE      ZERO,TTTRAN2
          MOVE      ZERO,TTTRAN3
          MOVE      ZERO,TTTRAN4
          MOVE      ZERO,TTTRAN5
          MOVE      ZERO,TTTRAN6
          MOVE      ZERO,TTTRAN7
          MOVE      ZERO,TTPROCTT
          MOVE      ZERO,MEANTTT
          MOVE      ZERO,MAXTTT
          MOVE      ZERO,ODURGTTT
          MOVE      ZERO,ODSEMTTT
          MOVE      ZERO,ODROUTTT
          MOVE      ZERO,HOSTTT 
          MOVE      ZERO,PATTTT
          MOVE      ZERO,OTHTTT
END9999   RETURN
.************************************************************************
.*                    I/O Routines for Tempfile                         *
.************************************************************************
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      WATTMP1,KEY15;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      WATTMP1,KEY15;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      WATTMP1,KEY15;WMDOCTOR,WMUNIT,WPGRP,WMCODE,RANGE1,RANGE2:
                                  RANGE3,RANGE4,RANGE5,RANGE6,RANGE7,TOTDAY,MAX:
                                  ODURGENT,ODSEMI,ODROUT,CANHOS,CANPAT,CANOTH
          GOTO      OVERCOND IF OVER
          MOVE      DWMCNT,WMCNT
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY15
          READKS    WATTMP1;WMDOCTOR,WMUNIT,WPGRP,WMCODE,RANGE1,RANGE2:
                            RANGE3,RANGE4,RANGE5,RANGE6,RANGE7,TOTDAY,MAX:
                            ODURGENT,ODSEMI,ODROUT,CANHOS,CANPAT,CANOTH
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY15
          WRITE     WATTMP1,KEY15;WMDOCTOR,WMUNIT,WPGRP,WMCODE,RANGE1,RANGE2:
                                  RANGE3,RANGE4,RANGE5,RANGE6,RANGE7,TOTDAY,MAX:
                                  ODURGENT,ODSEMI,ODROUT,CANHOS,CANPAT,CANOTH
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMPR1  
          UPDATE    WATTMP1;WMDOCTOR,WMUNIT,WPGRP,WMCODE,RANGE1,RANGE2:
                            RANGE3,RANGE4,RANGE5,RANGE6,RANGE7,TOTDAY,MAX:
                            ODURGENT,ODSEMI,ODROUT,CANHOS,CANPAT,CANOTH
          RETURN
.*****************************************************************************
.    Get code description from codes file
CODE0000
          UNPACK    KEY5,TCODE,ACODE
          MATCH     SP3,ACODE
          GOTO      CODE9000 IF EQUAL
          CALL      RDCODE1
          BRANCH    OVRCD,CODE9000
          GOTO      CODE9999
CODE9000
          MOVE      SP20,TDESC
CODE9999  RETURN
.*****************************************************************************
PRNTLINE  PRINT       *1,"*---------------------------------------------":
                         "-------------------------------------------":
                         "------------------------------------------*"
          ADD         ONE,CLNO
LINE9999  RETURN
.************************************************************************
.                                 DOCN0000
.    Setup doctors name in DOCTNAME
.*****************************************************************************
DOCN0000
          CALL      RDDOCT1
          BRANCH    OVRCD,DOCN9990     * tell user his code not on file
DOCN1111
          MOVE      TWO,PACFRMT        * title name surname
          MOVE      DSNAME,PACSNAME    * use pacdate to fix doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,DOCTNAME
          MOVE      ZERO,EXIT
          GOTO      DOCN9999
DOCN9990
          MOVE      ONE,EXIT
          MOVE      SP30,DOCTNAME
DOCN9999  RETURN
.*****************************************************************************
.
          INC       STD001IO
          INC       PATCODKY           * codes ? routine
          INC       PATDOCKY           * doctor code ? routine
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       WATCATIO/INC       * category change file
          INC       WATPROIO/INC       * procedure file
          INC       WATTR1IO/INC
          INC       WEBERRIO/INC
.............................................................................

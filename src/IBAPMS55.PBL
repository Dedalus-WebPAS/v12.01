.***************************************************************************
.* System    :    Inpatients                                               *
.* Program   :    IBAPMS55                                                 *
.* Desc      :    PMI Audit Name DOB Address Changes                       *
.***************************************************************************
.* Date      :    09/08/2021                                               *
.* Author    :    Thanh T                                                  *
.* Mods      :                                                             *
.*************************************************************************** 
.*  V11.01.02 08/11/2021  Sunny           TSK 0913361                      *
.*            Fixed spelling mistake in PRTHDR00                           *
.*  V11.01.01 27/09/2021  Thanh T          TSK 0889595                     *
.*            Added PROA0000 to print the address changed only from        *
.*            patpa1af                                                     *
.*  V11.01.00 09/08/2021  Thanh T          TSK 0889595                     *
.*            Initial Created                                              *
.***************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PMSVX1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSDAUFD/INC
          INC       PATPA1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       ADDSECTD/INC
.
HOUR      DIM       2
MIN       DIM       2
SEC       DIM       2
TODAY     DIM       8
CMDLINE   DIM       50
LINECNT   FORM      3
USERID    DIM       10
STRDATE   DIM       8
ENDDATE   DIM       8
STRDATEX  DIM       10
ENDDATEX  DIM       10
.
CDOB      DIM       10
PDOB      DIM       10
CSEXDES   DIM       30
PSEXDES   DIM       30
CTELP     DIM       19    
PTELP     DIM       19
CTELB     DIM       19
PTELB     DIM       19
CMOBIL    DIM       19
PMOBIL    DIM       19
CEMAIL    DIM       41
PEMAIL    DIM       41 
COPSMS    DIM       3
POPSMS    DIM       3
.
DIM4      DIM       4
DIM8      DIM       8
DIM10     DIM       10
DIM25     DIM       25
DIM30     DIM       30
DIM40     DIM       40
.
.------------------------------------------------------------------------------
PRGID     INIT      "IBAPMS55"
PRGNAM    INIT      "PMI Audit Name DOB Address Changes"
VERSION   INIT      "V12.01.00"
.------------------------------------------------------------------------------
. ML0000 : Mainline code
.------------------------------------------------------------------------------
.
ML0000    CALL      INIT0000                 * Initialisation
          CALL      OPTN0000                 * Keyin Option
          BRANCH    OPTION,ML1000
          GOTO      ML9999
.
ML1000    CALL      KEYD0000                 * Keyin the start/end date
          BRANCH    EXIT,ML9999              * Nothing input
.
          CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML1000
.
ML3000    CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML5000,ML1000,ML9999
.                         Yes    No     Cancel
          BEEP
          GOTO      ML3000
.
ML5000    CALL      PROC0000                   
          CALL      PROA0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM                      * Return from whence thou comest
.
.------------------------------------------------------------------------------
. INIT0000 : Initialisation routine
.------------------------------------------------------------------------------
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmsdauaf";
          OPEN      PMSDAUA3,"pmsdauaf"
.
          DISPLAY   *P64:24,"pmsdauaf";
          OPEN      PMSDAUA1,"pmsdauaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patpa1af";
          OPEN      PATPA1A1,"patpa1af"
.
          DISPLAY   *P64:24,"patpa1af";
          OPEN      PATPA1A2,"patpa1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
INIT9999  RETURN
+
.------------------------------------------------------------------------------
. Main options    
.------------------------------------------------------------------------------
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF:
                      " = PMI Audit Name, DOB and Address Changes":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*EL,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
.
          MOVE      ONE,OPTION
          BRANCH    OPTION,OPTN9999
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.------------------------------------------------------------------------------
. Keyin user id      
.------------------------------------------------------------------------------
KUSR0000  KEYIN     *P1:13,*EL,"User ID   : ",*V2LON,USERID
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
.
.******************************************************************************
.*                      Keyin ending date                                     *
.******************************************************************************
KEYD0000  DISPLAY   *P1:10,*EL,"Start Date: ":
                    *P1:11,*EL,"End   Date: "
.
          MOVE      "10",CVERT
          MOVE      "13",CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
          BRANCH    OVRCD,KEYD9500
          PACK      STRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRDATE
.
          MOVE      "11",CVERT
          MOVE      "13",CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
          BRANCH    OVRCD,KEYD9500
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
KEYD6000  MOVE      ZERO,EXIT
          MATCH     STRDATE,ENDDATE
          GOTO      KEYD9999 IF NOT LESS
.
          UNPACK    STRDATE,CCENT,CYEAR,CMON,CDAY
	  CALL      PACDATE
	  DISPLAY   *P1:24,*EL,*B,*V2LON,"End Date must be => ":
                    "Start date ",CPCDATE,".  ";
          CALL      HITENTER
          GOTO      KEYD0000
.
KEYD9500  MOVE      ONE,EXIT
KEYD9999  RETURN
.
.------------------------------------------------------------------------------
. Process the report for name and address changes
.------------------------------------------------------------------------------
PROC0000  DISPLAY   *P10:24,"Processing : ";
.
          UNPACK    STRDATE,XCENT,XYEAR,XMON,XDAY
          PACK      STRDATEX,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          REP       " 0",STRDATEX
.
          UNPACK    ENDDATE,XCENT,XYEAR,XMON,XDAY
          PACK      ENDDATEX,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          REP       " 0",ENDDATEX
.
          CALL      PRTHDR00                 * Print header
.
          MOVE      "005",KEY3
          PACK      KEY27,KEY3,STRDATE,SP70
          CALL      RSPMDAU3                 * Position at the start of file
PROC1000  CALL      RKPMDAU3                 * Get the next record
          BRANCH    OVRCD,PROC9999           * Finished processing
.
          DISPLAY   *P24:24,*V2LON,PMDEURNO;
.
          MATCH     KEY3,PMDERTYP
          GOTO      PROC9999 IF NOT EQUAL
.
          MATCH     PMDECDAT,ENDDATE
          GOTO      PROC9999 IF LESS
.
          MOVE      PMDEURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
.
          MOVE      PMDEURNO,KEY8
          CALL      RDPMPX21 
.
          CALL      PRTNAM00
          CALL      ADDCHG00
.
          GOTO      PROC1000                 * Get the next U/R
.
PROC9999  RETURN
.
.------------------------------------------------------------------------------
. Select address changes to be printed
.------------------------------------------------------------------------------
ADDCHG00  PACK      KEY24,PMDEURNO,PMDECDAT,PMDECTIM,SP70 
          CALL      RDPADD1
          IF        OVRCD = 0
            GOTO      ADDCHG80
          ENDIF
.
. Cannot find the exact match date/time, so find the row with 1 second
. difference of date/time. this is to assume that the pmsdauaf and 
. patpa1af are written within 1 second difference. The reason to check
. this is to cater for cases when the previous address are changed
. more than 1 time within a same date
.
. Try to find the row with 1 seond more
. 
ADDCHG20  UNPACK    PMDECTIM,HOUR,D1,MIN,D1,SEC
          PACK      ADATTIME,PMDECDAT,HOUR,MIN,SEC,SP70
          REP       " 0",ADATTIME
          CALL      AD1SEC00                    * add 1 sec to date/time
.
          UNPACK    ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
          PACK      DIM8,ASECHH,COLON,ASECMM,COLON,ASECSS,SP70
          REP       " 0",DIM8
.
          PACK      KEY24,PMDEURNO,ASECDATE,DIM8,SP70
          CALL      RDPADD1
          IF        OVRCD = 0
            GOTO      ADDCHG80
          ENDIF
.
. Try to find the row with 1 seond less
.
ADDCHG40  UNPACK    PMDECTIM,HOUR,D1,MIN,D1,SEC
          PACK      ADATTIME,PMDECDAT,HOUR,MIN,SEC,SP70
          REP       " 0",ADATTIME
          CALL      SU1SEC00                    * subtract 1 sec to date/time
.
          UNPACK    ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
          PACK      DIM8,ASECHH,COLON,ASECMM,COLON,ASECSS,SP70
          REP       " 0",DIM8
.
          PACK      KEY24,PMDEURNO,ASECDATE,DIM8,SP70
          CALL      RDPADD1
          IF        OVRCD = 0
            GOTO      ADDCHG80
          ENDIF
.
          GOTO      ADDCHG99
.
ADDCHG80  CALL      PRTADR00  
.
ADDCHG99  RETURN
.
.------------------------------------------------------------------------------
. Process the report for address changes
.------------------------------------------------------------------------------
PROA0000  PACK      KEY24,STRDATE,SP70
          CALL      RSPTPAD2                 * Position at the start of file
PROA1000  CALL      RKPTPAD2                 * Get the next record
          BRANCH    OVRCD,PROA9999           * Finished PROAessing
.
          DISPLAY   *P24:24,*V2LON,PAURNO;
.
          MATCH     PMDECDAT,ENDDATE
          GOTO      PROA9999 IF LESS
.
          MOVE      PAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROA1000
.
          MOVE      PAURNO,KEY8
          CALL      RDPMPX21
.
          CALL      ADDCHA00
.
          GOTO      PROA1000                 * Get the next U/R
.
PROA9999  RETURN
.
.------------------------------------------------------------------------------
. Select address changes to be printed. Only address changes not in pmsdauaf
. table will be printed. If it is in pmsdauaf table, this already included
. in PROC0000
.------------------------------------------------------------------------------
ADDCHA00  MOVE      "005",KEY3
          PACK      KEY27,PAURNO,PADATE,PATIME,KEY3,SP70
          CALL      RDPMDAU1
          IF        OVRCD = 0
            GOTO      ADDCHA99
          ENDIF
.
. Cannot find the exact match date/time, so find the row with 1 second
. difference of date/time. this is to assume that the pmsdauaf and
. patad1af are written within 1 second difference. The reason to check
. this is to cater for cases when the previous address are changed
. more than 1 time within a same date
.
. Try to find the row with 1 seond more
.
ADDCHA20  UNPACK    PATIME,HOUR,D1,MIN,D1,SEC
          PACK      ADATTIME,PADATE,HOUR,MIN,SEC,SP70
          REP       " 0",ADATTIME
          CALL      AD1SEC00                    * add 1 sec to date/time
.
          UNPACK    ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
          PACK      DIM8,ASECHH,COLON,ASECMM,COLON,ASECSS,SP70
          REP       " 0",DIM8
.
          PACK      KEY27,PAURNO,ASECDATE,DIM8,KEY3,SP70
          CALL      RDPMDAU1
          IF        OVRCD = 0
            GOTO      ADDCHA99
          ENDIF
.
. Try to find the row with 1 seond less
.
ADDCHA40  UNPACK    PATIME,HOUR,D1,MIN,D1,SEC
          PACK      ADATTIME,PMDECDAT,HOUR,MIN,SEC,SP70
          REP       " 0",ADATTIME
          CALL      SU1SEC00                    * subtract 1 sec to date/time
.
          UNPACK    ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
          PACK      DIM8,ASECHH,COLON,ASECMM,COLON,ASECSS,SP70
          REP       " 0",DIM8
.
          PACK      KEY27,PAURNO,ASECDATE,DIM8,KEY3,SP70
          CALL      RDPMDAU1
          IF        OVRCD = 0
            GOTO      ADDCHA99
          ENDIF
.
ADDCHA80  CALL      PRTADR00
.
ADDCHA99  RETURN
.
.------------------------------------------------------------------------------
. Print header details
.------------------------------------------------------------------------------
PRTHDR00  MOVE      ONE,CNOUNDLN 
          CALL      HEAD132
.
          PRINT     *40,"For Date Range:",*57,STRDATEX:
                    *68,"to",*71,ENDDATEX
          PRINT     *1," "
          PRINT     *1,"Previous Details",*67,"Current Details"      
          CALL      UND132 
.
          MOVE      6,LINECNT
.
PRIHDR99  RETURN
.
.------------------------------------------------------------------------------
. Print name changes details
.------------------------------------------------------------------------------
PRTNAM00  ADD       "9",LINECNT
          IF        LINECNT > 60
            CALL      PRTHDR00
            ADD       "9",LINECNT
          ENDIF
.
          UNPACK    PMDECDAT,XCENT,XYEAR,XMON,XDAY
          PACK      DIM10,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          REP       " 0",DIM10
.
          PRINT     *1,"Patient Number",*23,":",*25,PURNO:
                    *36,"Alteration: Name/DOB Change":
                    *69,"User ID: ",USERID:
                    *90,"Change Date/Time: ",*108,DIM10:
                    *119,PMDECTIM
.
          MOVE      PMDEFL01,DIM40
          PRINT     *1,"Surname",*23,":",*25,DIM40:
                    *67,"Surname",*90,":",*92,PSNAME
.
          MOVE      PMDEFL02,DIM40
          PRINT     *1,"First Given Name",*23,":",*25,DIM40:
                    *67,"First Given Name",*90,":",*92,PMPXFNAM
.
          MOVE      PMDEFL03,DIM40
          PRINT     *1,"Second Given Name",*23,":",*25,DIM40:
                    *67,"Second Given Name",*90,":",*92,PMPXSNAM
.
          MOVE      PMDEFL04,DIM4
          PRINT     *1,"Title",*23,":",*25,DIM4:
                    *67,"Title",*90,":",*92,PTITL
.
PRTNAM30  MOVE      SP70,PSEXDES
          STRIP     PMDEFL05
          MATCH     SP70,PMDEFL05
          IF        !@EQUAL & !@EOS    
            PACK      KEY5,ANSG,SP1,PMDEFL05,SP70
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      SP70,PTCDLDES
            ENDIF
            MOVE      PTCDLDES,PSEXDES
          ENDIF
.
          MOVE      SP70,CSEXDES
          STRIP     PSEX
          MATCH     SP70,PSEX
          IF        !@EQUAL & !@EOS
            PACK      KEY5,ANSG,SP1,PSEX,SP70
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      SP70,PTCDLDES
            ENDIF
            MOVE      PTCDLDES,CSEXDES
          ENDIF
.
          PRINT     *1,"Gender",*23,":",*25,PSEXDES:
                    *67,"Gender",*90,":",*92,CSEXDES
.
          MOVE      SP70,PDOB
          STRIP     PMDEFL06
          MATCH     SP70,PMDEFL06
          IF        !@EQUAL & !@EOS   
            UNPACK    PMDEFL06,XCENT,XYEAR,XMON,XDAY
            PACK      PDOB,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
            REP       " 0",PDOB
          ENDIF
.
          MOVE      SP70,CDOB
          STRIP     PBDATE
          MATCH     SP70,PBDATE
          IF        !@EQUAL & !@EOS
            UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
            PACK      CDOB,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
            REP       " 0",CDOB
          ENDIF
.
          PRINT     *1,"Date of Birth",*23,":",*25,PDOB:
                    *67,"Date of Birth",*90,":",*92,CDOB
.
          MOVE      PMDEFL07,DIM25
          PRINT     *1,"Preferred Given Names",*23,":",*25,DIM25:
                    *67,"Preferred Given Names",*90,":",*92,PMPXPFGN
.
          PRINT     *1,""
.
PRTNAM99  RETURN
.
.------------------------------------------------------------------------------
. Print address changes details
.------------------------------------------------------------------------------
PRTADR00  ADD       "10",LINECNT
          IF        LINECNT > 60
            CALL      PRTHDR00
            ADD       "10",LINECNT
          ENDIF
.
          UNPACK    PADATE,XCENT,XYEAR,XMON,XDAY
          PACK      DIM10,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          REP       " 0",DIM10
.
          PRINT     *1,"Patient Number",*23,":",*25,PURNO:
                    *36,"Alteration: Address Change":
                    *69,"User ID: ",USERID:
                    *90,"Change Date/Time:",*108,DIM10:
                    *119,PATIME
.
          PRINT     *1,"Address Line 1",*23,":",*25,PAADD1:
                    *67,"Address Line 1",*90,":",*92,PADD1
.
          PRINT     *1,"Address Line 2",*23,":",*25,PAADD2:
                    *67,"Address Line 2",*90,":",*92,PADD2
.
          PRINT     *1,"Suburb",*23,":",*25,PASUBR:
                    *67,"Suburb",*90,":",*92,PSUBRB
.
          PRINT     *1,"Address Line 4",*23,":",*25,PAADD4:
                    *67,"Address Line 4",*90,":",*92,PADD4
.
          PRINT     *1,"Postcode",*23,":",*25,PAPOST:
                    *67,"Postcode",*90,":",*92,PPOST
.
. Only have 19 spaces for phone no. to fix on one line 
.
          MOVE      PATELEP,PTELP
          MOVE      PATELEB,PTELB
          MOVE      PTELEP,CTELP
          MOVE      PTELEB,CTELB 
          PRINT     *1,"Home / Business Phone",*23,":",*25,PTELP:
                    *45,"/",*47,PTELB:
                    *67,"Home / Business Phone",*90,":",*92,CTELP:
                    *111,"/",*113,CTELB
.
          MOVE      PTPAMOBL,PMOBIL
          MOVE      PTMXCELL,CMOBIL
.
          MOVE      SP70,POPSMS
          MATCH     "1",PTPAOSMS
          IF        @EQUAL
            MOVE      "Yes",POPSMS
          ENDIF
.
          MOVE      SP70,COPSMS
          MATCH     "1",PTMXOSMS
          IF        @EQUAL
            MOVE      "Yes",COPSMS
          ENDIF
.                      
          PRINT     *1,"Mobile / Op-Out SMS",*23,":",*25,PMOBIL:
                    *45,"/",*47,POPSMS:
                    *67,"Mobile / Op-Out SMS",*90,":",*92,CMOBIL:
                    *111,"/",*113,COPSMS
.
. Only have 41 spaces for email address to fix on one line
.
          MOVE      PTPAEMAL,PEMAIL
          MOVE      PMPXPEML,CEMAIL 
          PRINT     *1,"Email Address",*23,":",*25,PEMAIL:
                    *67,"Email Address",*90,":",*92,CEMAIL
.
          PRINT     *1,""
.
PRTADR99  RETURN
.
          INC       STD001IO
          INC       ADDSECTM
.
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PMSVX1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSDAUIO/INC
          INC       PATPA1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

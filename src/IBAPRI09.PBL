.*****************************************************************************
.* System    :   Private Practice                                            *
.* Program   :   IBAPRI09                                                    *
.* Desc      :   Eclipse IMC Assessment Report Printing                      *
.*****************************************************************************
.* Date      :   27/07/2016                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will allow the user to report on the raw       *
.*               assessment details for a given IMC claim.                   *
.* Mods      :                                                               *
.*       V10.08.00  27/07/2016  Steve Armstrong   TSK 0294177                *
.*                  Program created.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCONFD/INC
          INC       PATPARFD/INC
          INC       PRIEADFD/INC
          INC       PRIEAHFD/INC
          INC       PRIESDFD/INC
          INC       PRIESHFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ASSDESC2  DIM       45
.
DIM7      DIM       7
DIM10A    DIM       10
DIM10B    DIM       10
DIM10C    DIM       10
DISPDTE3  DIM       10
.
FORM7P2   FORM      7.2
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
SP32      INIT      "                                "
.
.
PRGID     INIT      "IBAPMS08"
PRGNAM    INIT      "Eclipse IMC Assessment Report Printing"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
          MATCH     "1",PTCNUIMC
          GOTO      MAIN9999 IF NOT EQUAL
.
          CALL      GASS0000               * get assessment header key
          BRANCH    EXIT,MAIN9999          * nothing/exitchar entered
.
          CALL      PRIN0000               * print assessment
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000                                   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND24;*111,PTCNUIMC
.
          DISPLAY   *P56:24,*EL,"Opening ";
.
          MATCH     "1",PTCNUIMC
          IF        @EQUAL
            DISPLAY   *P64:24,"prieahaf";
            OPEN      PRIEAHA1,"prieahaf"
.
            DISPLAY   *P64:24,"prieadaf";
            OPEN      PRIEADA1,"prieadaf"
.
            DISPLAY   *P64:24,"prieshaf";
            OPEN      PRIESHA1,"prieshaf"
.
            DISPLAY   *P64:24,"priesdaf";
            OPEN      PRIESDA1,"priesdaf"
          ENDIF
.
          DISPLAY   *P64:24,"patparaf";
          OPEN      PATPARA1,"patparaf"
          OPEN      PATPARA2,"patparaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                GASS0000         Called by: MAIN0000       *
.*             Get the assessment key for the report                         *
.* Returns: EXIT    0 = valid pmseahaf record read                           *
.*                  1 = nothing entered or pmseahaf record not found         *
.*****************************************************************************
.
GASS0000  KEYIN     *P1:3,*EF:
                    *P1:4,"Assessment key: ",*V2LON,KEY32
.
          PACK      KEY32,KEY32,SP70
          MATCH     SP32,KEY32                   * anything entered ?
          GOTO      GASS9100 IF EQUAL            * no - finished        
.
.         Use KEY32 to read the assessment header record (pmseahaf)     
.
          CALL      RDPREAH1                     * header record found ?
          BRANCH    OVRCD,GASS9100               * no - finished
.
.         Now read the participant record to get the participant name
.                   
          MOVE      "Unknown Participant                     ",PTPRNAME
          MOVE      KEY32,KEY3
          CALL      RDPTPAR1
.
          MOVE      ZERO,EXIT
          GOTO      GASS9999
.
GASS9100  MOVE      ONE,EXIT
.
GASS9999  RETURN
+
.*****************************************************************************
.*                               PRIN0000                                    *
.*                  Process the records for printing                         *
.*****************************************************************************
.
PRIN0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      PRAH0000                * print assessment heading
          CALL      PRAD0000                * print assessment details
          CALL      PRSH0000                * print services heading
          CALL      PRSD0000                * print services details
.
          PRINT     *N,*1,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,"Report printed.  ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.*****************************************************************************
.*                            PRAH0000                                       *
.*                       Print page heading (pmseahaf details)               *
.*****************************************************************************
.
PRAH0000  CALL      HEAD132                      * display page header
.
          PRINT     *N,*40,"Account Reference ID: ",PRAHARID
.
          COMPARE   ONE,CPAGENO                  * first page ?
          GOTO      PRAH1000 IF NOT EQUAL        * no
.
.         For first page only, print claim header details
.
          PRINT     *N,*1,"Claim Assessment"
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Participant          : ",PRAHFBID,*30,PTPRNAME:
                    *76,PIPE,*78,"Claim ID           : ",PRAHCLID,*132,PIPE,*N:
                    *1,PIPE,*3,"Fund Status Code     : ",PRAHFSCD:
                    *76,PIPE,*78,"Fund Transaction ID: ",PRAHFTID,*132,PIPE,*N:
                    *1,PIPE,*3,"Claim Fund Assm.Code : ",PRAHCFAC:
                    *76,PIPE,*78,"Process Status Code: ",PRAHSTAT,*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
.         Print the claim assessment details
.
PRAH1000  IF        CPAGENO = 1
            PRINT     *N,*1,"Claim Assessment Details"
          ELSE
            PRINT     *N,*1,"Claim Assessment Details (contd.)"
          ENDIF
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Claim Fund",*22,PIPE,*24,"Claim Fund":
                    *132,PIPE,*N:
                    *1,PIPE,*3,"Explanation Code",*22,PIPE:
                    *24,"Explanation Text":
                    *132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          IF        CPAGENO = 1
            MOVE      TWENTY,CLNO                * initialise line count
          ELSE
            MOVE      TEN1,CLNO                  * initialise line count
          ENDIF
.
PRAH9999  RETURN
+
.*****************************************************************************
.*                               PRAD0000                                    *
.*                  Process the assessment details for printing              *
.*****************************************************************************
.
PRAD0000  PACK      KEY39,PRAHFBID,PRAHARID,PRAHCLID,PRAHRPTC,SP70
          CALL      RSPREAD1                     * position on remittance
PRAD0500  CALL      RKPREAD1                     * read next record
          BRANCH    OVRCD,PRAD9500               * eof - finished
.
          MATCH     PRAHFBID,PREDFBID            * same participant still ?
          GOTO      PRAD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PRAHARID,PREDARID            * same Account Reference ID ?
          GOTO      PRAD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PRAHCLID,PREDCLID            * same claim ID ?
          GOTO      PRAD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PRAHRPTC,PREDRPTC            * same report counter ?
          GOTO      PRAD9500 IF NOT EQUAL        * no - finished
.
.         A valid record has been found, so print it
.
          COMPARE   SIXTY,CLNO                   * page full ?
          CALL      PRAH0000 IF NOT LESS         * yes
.
          CALL      DLIN0000                     * print assessment details line
          GOTO      PRAD0500                     * get next record
.
PRAD9500  CALL      LINE0000                     * draw line across page
.
PRAD9999  RETURN
+
.*****************************************************************************
.*                            DLIN0000                                       *
.*                  Valid assessment details record so print it              *
.*****************************************************************************
.
DLIN0000  PRINT     *1,PIPE,*3,PREDCFEC,*22,PIPE,*24,PREDCFET,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DLIN9999  RETURN
+
.*****************************************************************************
.*                            LINE0000                                       *
.*                      Draw line across page                                *
.*****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
.*****************************************************************************
.*                            PRSH0000                                       *
.*                       Print page heading (pmseshaf details)               *
.*****************************************************************************
.
PRSH0000  PRINT     *N,*1,"Claimed Services Summary"
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Service",*14,PIPE,*16,"Charge",*27,PIPE:
                    *29,"Fund",*40,PIPE,*42,"Medicare",*53,PIPE:
                    *55,"Medicare",*67,PIPE,*69,"Service Fund",*132,PIPE,*N:
.
                    *1,PIPE,*3,"Date",*14,PIPE,*16,"Amount ($)",*27,PIPE:
                    *29,"Benefit",*40,PIPE,*42,"Benefit",*53,PIPE:
                    *55,"Explanation",*67,PIPE,*69,"Assessment",*132,PIPE,*N:
.
                    *1,PIPE,*14,PIPE,*27,PIPE,*29,"Amount ($)",*40,PIPE:
                    *42,"Amount ($)",*53,PIPE,*55,"Code",*67,PIPE:
                    *69,"Code",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
PRSH1000  PACK      KEY40,PRAHFBID,PRAHARID,PRAHCLID,PRAHRPTC,SP70
          CALL      RSPRESH1                     * position on claim
PRSH1500  CALL      RKPRESH1                     * read next record
          BRANCH    OVRCD,PRSH9500               * eof - finished
.
          MATCH     PRAHFBID,PRSHFBID            * same participant still ?
          GOTO      PRSH9500 IF NOT EQUAL        * no - finished
.
          MATCH     PRAHARID,PRSHARID            * same Account Reference ID ?
          GOTO      PRSH9500 IF NOT EQUAL        * no - finished
.
          MATCH     PRAHCLID,PRSHCLID            * same claim ID ?
          GOTO      PRSH9500 IF NOT EQUAL        * no - finished
.
          MATCH     PRAHRPTC,PRSHRPTC            * same report counter ?
          GOTO      PRSH9500 IF NOT EQUAL        * no - finished
.
.         A valid record has been found, so print it
.
          CALL      DSLN0000                     * print assessment details line
          GOTO      PRSH1500                     * get next record
.
PRSH9500  CALL      LINE0000                     * draw line across page
.
PRSH9999  RETURN
+
.*****************************************************************************
.*                               PRSD0000                                    *
.*                  Process the services details for printing                *
.*****************************************************************************
.
PRSD0000  PRINT     *N,*1,"Claimed Services Details"
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Item",*10,PIPE,*12,"Service Fund":
                    *29,PIPE,*31,"Service Fund",*132,PIPE,*N:
                    *1,PIPE,*3,"Number",*10,PIPE,*12,"Explanation Code":
                    *29,PIPE,*31,"Explanation Text",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
PRSD1000  PACK      KEY47,PRAHFBID,PRAHARID,PRAHCLID,PRAHRPTC,SP70
          CALL      RSPRESD1                     * position on service details
PRSD0500  CALL      RKPRESD1                     * read next record
          BRANCH    OVRCD,PRSD9500               * eof - finished
.
          MATCH     PRAHFBID,PRSDFBID            * same participant still ?
          GOTO      PRSD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PRAHARID,PRSDARID            * same Account Reference ID ?
          GOTO      PRSD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PRAHCLID,PRSDCLID            * same claim ID ?
          GOTO      PRSD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PRAHRPTC,PRSDRPTC            * same report counter ?
          GOTO      PRSD9500 IF NOT EQUAL        * no - finished
.
.         A valid record has been found, so print it
.
          CALL      DSDL0000                     * print services details line
          GOTO      PRSD0500                     * get next record
.
PRSD9500  CALL      LINE0000                     * draw line across page
.
PRSD9999  RETURN
+
.*****************************************************************************
.*                            DSLN0000                                       *
.*                  Valid claim services record so print it                  *
.*****************************************************************************
.
DSLN0000  UNPACK    PRSHCAMT,DIM7,DIM2
          PACK      DIM10A,DIM7,DOT,DIM2
          MOVE      DIM10A,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10A,SP6,ZERO,DOT,ZERO,ZERO
          ELSE
            MOVE    FORM7P2,DIM10A
          ENDIF
.
          UNPACK    PRSHFBAM,DIM7,DIM2
          PACK      DIM10B,DIM7,DOT,DIM2
          MOVE      DIM10B,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10B,SP6,ZERO,DOT,ZERO,ZERO
          ELSE
            MOVE    FORM7P2,DIM10B
          ENDIF
.
          UNPACK    PRSHMBAM,DIM7,DIM2
          PACK      DIM10C,DIM7,DOT,DIM2
          MOVE      DIM10C,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10C,SP6,ZERO,DOT,ZERO,ZERO
          ELSE
            MOVE    FORM7P2,DIM10C
          ENDIF
.
          UNPACK    PRSHSDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *1,PIPE,*3,CPCDATE,*14,PIPE,*16,DIM10A,*27,PIPE,*29,DIM10B:
                    *40,PIPE,*42,DIM10C,*53,PIPE,*55,PRSHMEXC,*67,PIPE:
                    *69,PRSHSFAC,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DSLN9999  RETURN
+
.*****************************************************************************
.*                            DSDL0000                                       *
.*            Valid claim services detail record so print it                 *
.*****************************************************************************
.
DSDL0000  PRINT     *1,PIPE,*3,PRSDITMN,*10,PIPE,*12,PRSDSFEC:
                   *29,PIPE,*31,PRSDSFET,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DSDL9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATPARIO/INC
          INC       PRIEADIO/INC
          INC       PRIEAHIO/INC
          INC       PRIESDIO/INC
          INC       PRIESHIO/INC
+

.***************************************************************************
.* System    :   HIC Claims                                                *
.* Program   :   IBAHIC09                                                  *
.* Desc      :   Print Finance Report                                      *
.***************************************************************************
.* Date      :   21/06/2005                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will list all batches that have a status     *
.*           :   of Actioned and with amount Received/payment              *
.*                                                                         *
.***************************************************************************
.* V11.02.01 09/02/2022  Thanh T       TSK 0905641                         *
.*           Recompiled as OUTMA1FD changes                                *
.* V10.11.01 25/07/2017  Ebon Clements     TSK 0268970                     *
.*           Change to use OUTCLIFD index 1 instead of index 2             *
.* V10.04.01 17/04/2014  J.Tan          CAR 261630                         *
.*                       Removed port number from temp file name           *
.*           V9.05.01    10/03/2006 J.Tan           CAR 96067              *
.*                       Mods maximum number of lines to print             *
.*           V9.05.B02   22/02/2005  J.Tan          CAR 96067              *
.*                       Fixed Clinic Id description                       *
.*           V9.05.B01   18/11/2005  J.Tan          CAR 77215              *
.*                       Mods to include all batches                       *
.*           V9.04.01    21/10/2005  J.Tan          CAR 77215              *
.*                       Mods batch status (EFT payment received prior Act)*
.*           V9.04.00    21/06/2005  J.Tan          CAR 56701              *
.*                       Created program                                   *
.***************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       HICBTCFD/INC
          INC       PMSHCPFD/INC
          INC       PRIPRAFD/INC
          INC       PRIDOCFD/INC
          INC       OUTSITFD/INC
          INC       OUTCLIFD/INC
          INC       OUTMA1FD/INC        * Session Master file
          INC       PATCODFD/INC
          INC       WEBSECFD/INC
          INC       PRIALSFD/INC
          INC       PRISECFD/INC
.
.         Temporary File Definition
.         -------------------------
.
.         Filename : hic09axx.dat          (xx = port id)
.
HICTEMP1  IFILE SQL, FIXED=84, KEY="UC76-81,1-5"
HICTEMP2  IFILE SQL, FIXED=84, KEY="UC34-39,1-5"
HICTEMP3  IFILE SQL, FIXED=84, KEY="UC24-33,1-5"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TEMPBTCH  DIM       5      5        1     Batch Number (1 Alpha 4 Numeric)
TEMPPMCI  DIM       8      8        6     MCI (3 Alpha 5 Numeric)
TEMPPYEE  DIM       10     10       14    Payee Provider Number
TEMPPSRV  DIM       10     10       24    Service Provider Number
TEMPMPRA  DIM       6      6        34    Medical Practice
TEMPCAMT  FORM      12.2   8        40    Total Batch Amount
TEMPAMTR  FORM      12.2   8        48    Total Amount Received
TEMPCOST  DIM       12     12       56    Cost Center
TEMPDATE  DIM       8      8        68    Batch Date
TEMPCLID  DIM       6      6        76    Clinic ID
TEMPSTAT  DIM       2      2        82    Batch status
.
.End of Record                      84
.
.         Temporary File Variables
.
EFTFLAG   DIM       1
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
TEMPFILE  DIM       8
UKEY      INIT      " 84 UC76-81,1-5 UC34-39,1-5 UC24-33,1-5" * set up key
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ALACCESS  FORM      1
CLINFROM  DIM       6
CLINICTO  DIM       6
CMDLINE   DIM       127
DISPNAME  DIM       60
DOCFNAME  DIM       50
FROMDATE  DIM       8
FILTRHCP  DIM       10
FILTPROV  DIM       10
FMTSNAME  DIM       35
FMTGNAME  DIM       35
FMTTITLE  DIM       10
HEADING0  DIM       30
HEADING1  DIM       30
HEADING2  DIM       30
HEADING3  DIM       40
HEADING5  DIM       30
HEADING6  DIM       30    
LOOPFLAG  FORM      1
PRACTIFR  DIM       6
PRACTITO  DIM       6
SORTFLAG  FORM      1
SAVEVAR   DIM       10
SITE      DIM       6
TODATE    DIM       8
TOTRECV   FORM      12.2
WEBUSRID  DIM       10
.
PRGID     INIT      "IBAHIC09"
PRGNAM    INIT      "Finance Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      PRCF0000               * keyin practice from
          BRANCH    EXIT,ML0100
.
          CALL      PRCT0000               * keyin practice to
          BRANCH    EXIT,ML0100
.
          CALL      FCLN0000               * keyin clinic from
          BRANCH    EXIT,ML0100
.
          CALL      TCLN0000               * keyin clinic to
          BRANCH    EXIT,ML0100
.
          CALL      KPRV0000               * keyin provider
          BRANCH    EXIT,ML0100
.
          CALL      DRNG0000               * keyin date range
.
          CALL      WEBU0000               * keyin user
          BRANCH    EXIT,ML0100
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CREA0000               * Create tempfile
.
          CALL      PROC0000               * process
          CALL      PRIN0000               * Print data
          CALL      ENDP0000               * End of report
.
          CALL      KILL0000               * Delete tempfile
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          CALL      IBACLOCK
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
          OPEN      HICBTCA2,"hicbtcaf"       * batch file
          OPEN      HICBTCA3,"hicbtcaf"       * batch file
          OPEN      HICBTCA4,"hicbtcaf"       * batch file
          OPEN      HICBTCA6,"hicbtcaf"       * batch file
          OPEN      PMSHCPA1,"pmshcpaf"       * HCP file
          OPEN      PRIPRAC1,"pripracf"       * private practice
          OPEN      PRIDOCT1,"pridoctf"       * practice doctors
          OPEN      OUTSITA1,"outsitaf"       * outpatient site file
          OPEN      PATCODE1,"patcodes"       * codes file
          OPEN      WEBSECA1,"websecaf"       * web user id
          OPEN      PRIALSA1,"prialsaf"       * all practice security
          OPEN      PRISECA1,"prisecaf"       * practice security
.
          MOVE      "out",OSTFILE
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,INIT9000
.
          PACK      CFNAME WITH OSTFILE,FILCLIA1        * OUTCLIFD
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILMA1A1
          CLOSE     OUTMA1A1
          CALL      OPOTMA11                * Open the file
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9000  DISPLAY   *P1:24,*EL,*B,"Site Code not on file. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      INIT9999
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      HICTEMP1,TEMPFILE            * open temp index file
          OPEN      HICTEMP2,TEMPFILE            * open temp index file
          OPEN      HICTEMP3,TEMPFILE            * open temp index file
.
          MOVE      ZERO,ALACCESS           * Set all access to no
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDPRALS1
          IF        OVRCD=0
            MOVE      ONE,ALACCESS
          ENDIF
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     HICTEMP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ONE,SORTFLAG         * sort by clinic id
          MATCH     SP70,CLINFROM
          GOTO      PROC1000 IF NOT EQUAL
.
          MOVE      TWO,SORTFLAG         * default to sort by medical practice
          MATCH     SP70,PRACTIFR
          GOTO      PROC3000 IF NOT EQUAL
.
          MOVE      ONE,SORTFLAG         * sort by clinic id
          MATCH     SP70,FILTPROV
          GOTO      PROC0100 IF EQUAL    * sort by clinic id if all blank
.
          MOVE      THREE,SORTFLAG       * sort by provider
          GOTO      PROC2000
.
.         By status
.******************************************************************************
PROC0100  MOVE      ZERO,LOOPFLAG
          PACK      KEY43,FROMDATE,SP70
          CALL      RSHCBTC4
PROC0200  CALL      RKHCBTC4
          BRANCH    OVRCD,PROC9999
.
          MATCH     SP70,TODATE
          IF        !@EQUAL
            MATCH     HCBTDATE,TODATE
            GOTO      PROC9999 IF LESS
          ENDIF
          GOTO      PROC5000
.
.         By Clinic id
.******************************************************************************
PROC1000  MOVE      ONE,LOOPFLAG
          PACK      KEY55,IDDD,SP70
.
          MATCH     SP70,CLINFROM
          GOTO      PROC1020 IF EQUAL
          PACK      KEY55,IDDD,CLINFROM,SP70
.
PROC1020  CALL      RSHCBTC3
PROC1100  CALL      RKHCBTC3
          BRANCH    OVRCD,PROC9999
          MATCH     IDDD,HCBTSITE	    * Same site?
          GOTO      PROC9999 IF NOT EQUAL
.
          MATCH     SP70,CLINICTO
          IF        !@EQUAL
            MATCH     HCBTCLID,CLINICTO
            GOTO      PROC9999 IF LESS
          ENDIF
.
          MATCH     SP70,CLINFROM
          IF        @EQUAL
            MATCH     CLINFROM,HCBTCLID
            GOTO      PROC1100 IF LESS
          ENDIF
.
          MATCH     SP70,FROMDATE
          IF        !@EQUAL
            MATCH     FROMDATE,HCBTDATE
            GOTO      PROC1100 IF LESS
          ENDIF
          MATCH     SP70,TODATE
          IF        !@EQUAL
            MATCH     HCBTDATE,TODATE
            GOTO      PROC1100 IF LESS
          ENDIF
.
          MATCH     SP70,FILTPROV
          IF        !@EQUAL
            MATCH     FILTPROV,HCBTPSRV
            GOTO      PROC1100 IF NOT EQUAL
          ENDIF
          GOTO      PROC5300                * check medical practice
.
.         By Provider number
.******************************************************************************
PROC2000  MOVE      TWO,LOOPFLAG
          PACK      KEY43,SP70
          CALL      RSHCBTC2
PROC2100  CALL      RKHCBTC2
          BRANCH    OVRCD,PROC9999
.
          MATCH     SP70,FILTPROV
          IF        !@EQUAL
            MATCH     FILTPROV,HCBTPSRV       * Same provider
            GOTO      PROC2100 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FROMDATE
          IF        !@EQUAL
            MATCH     FROMDATE,HCBTDATE
            GOTO      PROC2100 IF LESS
          ENDIF
          MATCH     SP70,TODATE
          IF        !@EQUAL
            MATCH     HCBTDATE,TODATE
            GOTO      PROC2100 IF LESS
          ENDIF
.
          GOTO      PROC5200                * check clinic id & practice
.
.         By Medical Practice
.******************************************************************************
PROC3000  MOVE      THREE,LOOPFLAG
          PACK      KEY39,SP70
          CALL      RSHCBTC6
PROC3100  CALL      RKHCBTC6
          BRANCH    OVRCD,PROC9999
.
          MATCH     SP70,PRACTIFR
          IF        @EQUAL
            MATCH     HCBTMPRA,PRACTIFR
            GOTO      PROC9999 IF LESS
          ENDIF
.
          MATCH     SP70,PRACTITO
          IF        !@EQUAL
            MATCH     HCBTMPRA,PRACTITO
            GOTO      PROC3100 IF LESS
          ENDIF
.
          MATCH     SP70,FROMDATE
          IF        !@EQUAL
            MATCH     FROMDATE,HCBTDATE
            GOTO      PROC3100 IF LESS
          ENDIF
          MATCH     SP70,TODATE
          IF        !@EQUAL
            MATCH     HCBTDATE,TODATE
            GOTO      PROC3100 IF LESS
          ENDIF
.
.         Check provider number
.******************************************************************************
PROC5000  MATCH     SP70,FILTPROV
          IF        !@EQUAL
            MATCH     FILTPROV,HCBTPSRV
            GOTO      PROC9000 IF NOT EQUAL
          ENDIF
.
.         Checking range of clinic id
.
PROC5200  MATCH     SP70,CLINFROM
          IF        !@EQUAL
            MATCH     CLINFROM,HCBTCLID
            GOTO      PROC9000 IF LESS
          ENDIF
.
          MATCH     SP70,CLINICTO
          IF        !@EQUAL
            MATCH     HCBTCLID,CLINICTO
            GOTO      PROC9000 IF LESS
          ENDIF
.
          COMPARE   THREE,LOOPFLAG
          GOTO      PROC6000 IF EQUAL       * done checking med prac already
.
.         Checking range of medical practice
.
PROC5300  MATCH     SP70,PRACTIFR
          IF        !@EQUAL
            MATCH     PRACTIFR,HCBTMPRA
            GOTO      PROC9000 IF LESS
          ENDIF
.
          MATCH     SP70,PRACTITO
          IF        !@EQUAL
            MATCH     HCBTMPRA,PRACTITO
            GOTO      PROC9000 IF LESS
          ENDIF
.
PROC6000  BRANCH    ALACCESS,PROC7000       * Access to all practices 
.
          PACK      KEY16,HCBTMPRA,WEBUSRID,SP70
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROC9000          * No access to this practice
.
PROC7000  MATCH     IDDD,HCBTSITE           * Same site
          GOTO      PROC9000 IF NOT EQUAL
.
          MOVE      HCBTBTCH,TEMPBTCH
          MOVE      HCBTPMCI,TEMPPMCI
          MOVE      HCBTPYEE,TEMPPYEE
          MOVE      HCBTPSRV,TEMPPSRV
          MOVE      HCBTMPRA,TEMPMPRA
          MOVE      HCBTCAMT,TEMPCAMT
          MOVE      HCBTAMTR,TEMPAMTR
          MOVE      HCBTDATE,TEMPDATE
          MOVE      HCBTCLID,TEMPCLID
          MOVE      HCBTSTAT,TEMPSTAT
          MOVE      SP70,TEMPCOST
.
          PACK      KEY18,HCBTSITE,HCBTCLID,SP70
          CALL      RDSMASA1
          CALL      RDKMASA1
          BRANCH    OVRCD,PROC8000
          MATCH     OMASITE,HCBTSITE          * Same site?
          GOTO      PROC8000 IF NOT EQUAL
          MATCH     OMACLIN,HCBTCLID          * Same clinic id?
          GOTO      PROC8000 IF NOT EQUAL
          MOVE      OTMACCTR,TEMPCOST         * Cost center
.
PROC8000  PACK      KEY11,TEMPCLID,TEMPBTCH
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
.
PROC9000  BRANCH    LOOPFLAG,PROC1100,PROC2100,PROC3100
          GOTO      PROC0200
.
PROC9999  RETURN
+
.**********************************************************************
.*                         PRIN0000                                   *
.*                        Print Data                                  *
.**********************************************************************
PRIN0000  MOVE      ZERO,CPAGENO
          MOVE      SP70,SAVEVAR
          MOVE      ZERO,TOTRECV
.
          PACK      KEY11,SP70
          PACK      KEY15,SP70
          CALL      RSTEMP1
PRIN1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRIN9000
.
          MATCH     SP70,SAVEVAR
          GOTO      PRIN1200 IF EQUAL            * first record
          CALL      CVAR0000                     * check if sort field changed
          BRANCH    EXIT,PRIN1100                * changed
.
          GOTO      PRIN1300
.
PRIN1100  CALL      UND132
          PRINT     *84,"| TOTAL",*100,"|",TOTRECV,*118,"|":
                    *N,*84,"*---------------",*100,"*-----------------*"
          ADD       TWO,CLNO
          MOVE      ZERO,TOTRECV
          MOVE      ONE,CNOUNDLN                 * no underline
.
PRIN1200  CALL      HEAD0000
          MOVE      ZERO,CNOUNDLN
          CALL      SVAR0000                     * save sort variable
.
PRIN1300  MOVE      SP70,PRPRDESC
          PACK      KEY6,TEMPMPRA,SP70
          CALL      RDPRPR1
.
          MOVE      SP70,EFTFLAG
          MATCH     " 7",TEMPSTAT
          IF        @EQUAL
            MOVE      "*",EFTFLAG           * EFT pay.received still on holding
          ENDIF
.
          UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          CALL      CLID0000              * get name of clinic id
          UNPACK    PRPRDESC,KEY25
          PRINT     *1,"| ",KEY25,*31,"| ",OCADESC:
                    *63,"| ",TEMPBTCH,*71,"| ",CPCDATE:
                    *84,"|",TEMPCAMT,*100,"|",TEMPAMTR,EFTFLAG:
                    *118,"| ",TEMPCOST,*132,"|"
          ADD       TEMPAMTR,TOTRECV
.
          ADD       ONE,CLNO
          COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          GOTO      PRIN1000              * read next record
.
PRIN9000  COMPARE   ZERO,CPAGENO
          GOTO      PRIN9999 IF NOT EQUAL
          CALL      HEAD0000
PRIN9999  RETURN
+
.*********************************************************************
.         Save sort variable
.*********************************************************************
SVAR0000  MOVE      SP70,SAVEVAR
          COMPARE   ONE,SORTFLAG
          GOTO      SVAR4000 IF EQUAL    * sort by clinic id
          COMPARE   TWO,SORTFLAG
          GOTO      SVAR6000 IF EQUAL    * sort by medical practice
.
.         Sort by service provider
.
          PACK      SAVEVAR,TEMPPSRV,SP70
          GOTO      SVAR9999
.
.         Sort by clinic id
.
SVAR4000  PACK      SAVEVAR,TEMPCLID,SP70
          GOTO      SVAR9999
.
.         Sort by medical practice
.
SVAR6000  PACK      SAVEVAR,TEMPMPRA,SP70
.
SVAR9999  RETURN
+
.*********************************************************************
.         check if sort variable change
.*********************************************************************
CVAR0000  MOVE      ZERO,EXIT
          COMPARE   ONE,SORTFLAG
          GOTO      CVAR4000 IF EQUAL    * sort by clinic id
          COMPARE   TWO,SORTFLAG
          GOTO      CVAR6000 IF EQUAL    * sort by medical practice
.
          MATCH     SAVEVAR,TEMPPSRV     * check service provider
          GOTO      CVAR9999 IF EQUAL
          GOTO      CVAR9000
.
CVAR4000  MATCH     SAVEVAR,TEMPCLID     * check clinic id
          GOTO      CVAR9999 IF EQUAL
          GOTO      CVAR9000
.
CVAR6000  MATCH     SAVEVAR,TEMPMPRA     * check medical practice
          GOTO      CVAR9999 IF EQUAL
.
CVAR9000  MOVE      ONE,EXIT
CVAR9999  RETURN

.*********************************************************************
.         Get clinic id
.*********************************************************************
CLID0000  MOVE      SP70,OCADESC
          PACK      KEY20,TEMPCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                *To get Clinic ID Description
          BRANCH    OVRCD,CLID9000
.
          MATCH     IDDD,OCASITE
          GOTO      CLID9000 IF NOT EQUAL
.
          MATCH     TEMPCLID,OCACLIN
          GOTO      CLID9000 IF NOT EQUAL
.
          MATCH     SP70,OCADESC
          GOTO      CLID9999 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,CLID9000
.
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OCADESC
          GOTO      CLID9999
.
CLID9000  MOVE      SP70,OCADESC
CLID9999  RETURN
+
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD132
.
          MOVE      SP70,CPCDATE
          MATCH     SP70,FROMDATE
          IF        !@EQUAL
            UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
          PRINT     *1,"Batch Date From       : ",CPCDATE;
.
          MOVE      SP70,CPCDATE
          MATCH     SP70,TODATE
          IF        !@EQUAL
            UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
          PRINT     "            To : ",CPCDATE
.
          PRINT     *1,"Medical Practice From : ",HEADING5," To : ",HEADING6
          PRINT     *1,"Clinic From           : ",HEADING1," To : ",HEADING2
          PRINT     *1,"Provider Number       : ",HEADING0
.
          ADD       FOUR,CLNO
.
          CALL      PRHD0000                     * print sort heading
          CALL      UND132
.
          PRINT     *1,"| Medical Practice ",*31,"| Clinic ID":
                    *63,"| Batch ",*71,"|   Batch":
                    *84,"|    Batch",*100,"|    Amount":
                    *118,"| Cost Centre",*132,"|":
                    *N,*1,"| ",*31,"|":
                    *63,"|  No.",*71,"|   Date":
                    *84,"|    Amount",*100,"|    Received":
                    *118,"|",*132,"|"
          ADD       TWO,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.*********************************************************************
.         print heading
.*********************************************************************
PRHD0000  COMPARE   ONE,SORTFLAG
          GOTO      PRHD4000 IF EQUAL    * sort by clinic id
          COMPARE   TWO,SORTFLAG
          GOTO      PRHD6000 IF EQUAL    * sort by medical practice
          GOTO      PRHD8000             * by provider no
.
PRHD4000  PACK      KEY20,IDDD,TEMPCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                *To get Clinic ID Description
          IF        OVRCD=0
            MATCH     IDDD,OCASITE
            IF        @EQUAL
              MATCH     TEMPCLID,OCACLIN
              GOTO      PRHD4200 IF EQUAL
            ENDIF
          ENDIF
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
            GOTO      PRHD4200
          ENDIF
          MOVE      SP70,OCADESC
.
PRHD4200  PRINT     *N,*1,"Clinic Id : ",OCADESC
.         PRINT     "(",TEMPPSRV,")"
          GOTO      PRHD9000
.
PRHD6000  PACK      PRPRDESC,SP70
          PACK      KEY6,TEMPMPRA
          CALL      RDPRPR1
          PRINT     *N,*1,"Medical Practice : ",PRPRDESC
          GOTO      PRHD9000
.
PRHD8000  MATCH     "All",HEADING0
          GOTO      PRHD9999 IF NOT EQUAL
          PRINT     *N,*1,"Provider No.: ",TEMPPSRV
.
PRHD9000  ADD       TWO,CLNO
PRHD9999  RETURN
+
.**********************************************************************
.*                           ENDP0000                                 *
.*                      Print the end of the report                   *
.**********************************************************************
ENDP0000  CALL      UND132                   * finish off report
          PRINT     *84,"| TOTAL",*100,"|",TOTRECV,*118,"|":
                    *N,*84,"*---------------",*100,"*-----------------*"
.
          PRINT     *N,"Note: An Asterisk next to 'Amount Received' ":
                    "indicates there is a Payment Received in Holding File":
                    *N,*N,*50,"*** End of Report ***"
.
ENDP9999  RETURN
+
.******************************************************************************
.*                                   FCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code From                     *
.******************************************************************************
FCLN0000  MOVE      SP70,CLINFROM
          KEYIN     *P1:9,*EL,"Clinic Id From : ",*V2LON,CLINFROM
.
          PACK      CLINFROM,CLINFROM,SP70
          MATCH     SP70,CLINFROM
          GOTO      FCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINFROM,Z70
          CALL      RDSCLIA1
FCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,FCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINFROM     * Must be in the Same clinic
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      FCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      FCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
FCLN5000  MOVE      OCADESC,HEADING1
          DISPLAY   *P25:9,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN6000  MOVE      "Start",HEADING1
          DISPLAY   *P25:9,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
FCLN9999  RETURN
+
.******************************************************************************
.*                                   TCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code To                       *
.******************************************************************************
TCLN0000  MOVE      SP70,CLINICTO
          KEYIN     *P1:10,*EL,"Clinic Id To   : ",*V2LON,CLINICTO
.
          PACK      CLINICTO,CLINICTO,SP70
          MATCH     SP70,CLINICTO
          GOTO      TCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINICTO,Z70
          CALL      RDSCLIA1
TCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,TCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINICTO     * Must be in the Same clinic
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      TCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      TCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
TCLN5000  MOVE      OCADESC,HEADING2
          DISPLAY   *P25:10,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN6000  MOVE      "Finish",HEADING2
          DISPLAY   *P25:10,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
TCLN9999  RETURN
.
+
.******************************************************************************
.*                                   KHCP0000             Called by: ML0000   *
.*                             Keyin The HCP code                             *
.******************************************************************************
KHCP0000  MOVE      SP70,FILTRHCP
          KEYIN     *P1:11,*EL,"HCP : ",*V2LON,FILTRHCP
.
          PACK      FILTRHCP,FILTRHCP,SP70
          MATCH     SP70,FILTRHCP
          GOTO      KHCP6000 IF EQUAL
.
          PACK      KEY10,FILTRHCP,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid HCP Code",PACFNAME
          ENDIF
.
KHCP5000  MOVE      PACFNAME,HEADING3
          DISPLAY   *P18:11,*EL,*V2LON,PACFNAME;
          MOVE      ZERO,EXIT
          GOTO      KHCP9999
.
KHCP6000  MOVE      "All",HEADING3
          DISPLAY   *P18:11,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      KHCP9999
.
KHCP9500  DISPLAY   *P1:24,*EL,*B,"Invalid HCP. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
KHCP9999  RETURN
.
+
.******************************************************************************
.*                                   KPRV0000             Called by: ML0000   *
.*                             Keyin the provider number                      *
.******************************************************************************
KPRV0000  MOVE      SP70,FILTPROV
          KEYIN     *P1:14,*EL,"Provider Number : ",*V2LON,FILTPROV
.
          PACK      FILTPROV,FILTPROV,SP70
          MATCH     SP70,FILTPROV
          GOTO      KPRV6000 IF EQUAL
.
KPRV5000  MOVE      FILTPROV,HEADING0
          DISPLAY   *P29:14,*EL,*V2LON,FILTPROV;
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV6000  MOVE      "All",HEADING0
          DISPLAY   *P29:14,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV9999  RETURN
+
.******************************************************************************
.*                                   PRCF0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCF0000  MOVE      SP70,PRACTIFR
          KEYIN     *P1:12,*EL,"Medical Practice : ",*V2LON,PRACTIFR
.
          PACK      PRACTIFR,PRACTIFR,SP70
          MATCH     SP70,PRACTIFR
          GOTO      PRCF6000 IF EQUAL
.
          PACK      KEY6,PRACTIFR,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCF9500
.
PRCF5000  MOVE      PRPRDESC,HEADING5
          DISPLAY   *P27:12,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF6000  MOVE      "Start",HEADING5
          DISPLAY   *P27:12,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCF9999  RETURN
+
.******************************************************************************
.*                                   PRCT0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCT0000  MOVE      SP70,PRACTITO
          KEYIN     *P1:13,*EL,"Medical Practice : ",*V2LON,PRACTITO
.
          PACK      PRACTITO,PRACTITO,SP70
          MATCH     SP70,PRACTITO
          GOTO      PRCT6000 IF EQUAL
.
          PACK      KEY6,PRACTITO,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCT9500
.
PRCT5000  MOVE      PRPRDESC,HEADING6
          DISPLAY   *P27:13,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT6000  MOVE      "Finish",HEADING6
          DISPLAY   *P27:13,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCT9999  RETURN
.
+
.******************************************************************************
.*                                   DRNG0000             Called by: ML0000   *
.*                             Keyin the date range                           *
.******************************************************************************
DRNG0000  DISPLAY   *P1:16,*EL,"From Date : ":
                    *P1:17,*EL,"  To Date : ";
          UNPACK    SP70,FROMDATE,TODATE
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          MOVE      TEN6,CVERT
          MOVE      TEN3,CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,DRNG2000
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
DRNG2000  MOVE      TEN7,CVERT
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,DRNG9999
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
DRNG9999  RETURN
+
.******************************************************************************
.*                                   WEBU0000             Called by: ML0000   *
.*                             Keyin The web user id                          *
.******************************************************************************
WEBU0000  MOVE      SP70,WEBUSRID
          KEYIN     *P1:18,*EL,"User Id : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,WEBU9500
.
          MOVE      ZERO,EXIT
          GOTO      WEBU9999
.
WEBU9500  DISPLAY   *P1:24,*EL,*B,"Invalid User Id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
WEBU9999  RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
.
RATEMP1   RESET     KEY11
          MOVE      ZERO,OVRCD
          READ      HICTEMP1,KEY11;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   MOVE      SP70,KEY11
          MOVE      SP70,KEY15
          IF        SORTFLAG=1
            READ      HICTEMP1,KEY11;;
          ELSE
            IF        SORTFLAG=2
              READ      HICTEMP2,KEY11;;
            ELSE
              READ      HICTEMP3,KEY15;;
            ENDIF
          ENDIF
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          IF        SORTFLAG=1
            READKS    HICTEMP1;TEMPBTCH,TEMPPMCI,TEMPPYEE,TEMPPSRV:
                               TEMPMPRA,TEMPCAMT,TEMPAMTR,TEMPCOST:
                               TEMPDATE,TEMPCLID,TEMPSTAT
          ELSE
            IF        SORTFLAG=2
              READKS    HICTEMP2;TEMPBTCH,TEMPPMCI,TEMPPYEE,TEMPPSRV:
                                 TEMPMPRA,TEMPCAMT,TEMPAMTR,TEMPCOST:
                                 TEMPDATE,TEMPCLID,TEMPSTAT
            ELSE
              READKS    HICTEMP3;TEMPBTCH,TEMPPMCI,TEMPPYEE,TEMPPSRV:
                                 TEMPMPRA,TEMPCAMT,TEMPAMTR,TEMPCOST:
                                 TEMPDATE,TEMPCLID,TEMPSTAT
            ENDIF
          ENDIF
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     HICTEMP1,KEY11;TEMPBTCH,TEMPPMCI,TEMPPYEE,TEMPPSRV:
                             TEMPMPRA,TEMPCAMT,TEMPAMTR,TEMPCOST:
                             TEMPDATE,TEMPCLID,TEMPSTAT
          RETURN
.
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       HICBTCIO/INC
          INC       PMSHCPIO/INC
          INC       PRIPRAIO/INC
          INC       PRIDOCIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       OUTMA1IO/INC        * Session Master file
          INC       PATCODIO/INC
          INC       WEBSECIO/INC
          INC       PRIALSIO/INC
          INC       PRISECIO/INC

.***************************************************************************
.* System    :   Theatre                                                   *
.* Program   :   IBAOPR83                                                  *
.* Desc      :   CLINIC MASTER LISTING                                     *
.***************************************************************************
.* Date      :   18/10/90                                                  *
.* Author    :   J. Ceniza                                                 *
.* Function  :   This program is used by the operating theatre system      *
.*               system to print the clinic master listing                 *
.***************************************************************************
.
.
.$$$$$
.
.      This program produces a master listing of the clinic codes in 
.      either code order or description order.
.
.      MAIN OPTIONS
.      ------------
.
.         0 = Exit
.
.         1 = Code sequence
.                -This option allows for the production of a list of the
.                 clinic codes in code order, either the entire list or
.                 over a range of codes.Question mark options are 
.                 available for the code range.
.
.         2 = Description sequence
.                -This option allows for the production of a list of the
.                 clinic codes in description order, either the entire list
.                 or a range of descriptions.
.
.     END
.$$$$$
.*************************************************************************
.
.
          INC       STD001FD
          INC       OPRCLIFD/INC
          INC       PATDO1FD/INC
          INC       OPRCONFD/INC
.
CODE      DIM       2
CODENUM   DIM       6
DESCRIP   DIM       30
ENDCODE   DIM       6
ENDDESC   DIM       30
ENDSPAC   DIM       30
NCODE     FORM      1 
NUMREC    FORM      4
ZENDDESC  DIM       30
ROW       FORM      2
STRTCODE  DIM       6
STRTDESC  DIM       30
STRTSPAC  DIM       30
TEMPCODE  DIM       6
.
. ------ CONSTANTS ---------
.
CODESEQ   INIT      "(Code Sequence)"
DESCSEQ   INIT      "(Desc Sequence)"
FINISH    INIT      "Finish"
NOTSPACE  INIT      "xxxx"
START     INIT      "Start"
ZZZZ      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
.
PRGID     INIT      "IBAOPR83"
PRGNAM    INIT      "Clinic Master Listing"
VERSION   INIT      "V12.01.00"
+
.***************************************************************************
.*                              ML0000                                     *
.*                      Controlling Logic (Mainline code)                  *
.***************************************************************************
ML0000    CALL      INIT0000               * initialization and open files
          BRANCH    OPCNCLSU,ML9999
.
          CALL      DOPT0000               * display options
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION:
                    ML1000:                * Code sequence
                    ML2000                 * Description sequence
.
. ---- master listing code sequence--------
.
ML1000    CALL      FSCR0000               * display screen format
ML1100    CALL      CODE0000               * input start/end codes
          BRANCH    EXIT,ML1100            * invalid range,input codes
          CALL      CONTQST                * Ask Ok to continue (Y/N/C) ?
          BRANCH    CEXIT:
                    ML1300:                * (Y)es selected
                    ML1100:                * (N)o selected 
                    ML0100                 * (C)ancel selected
ML1300    CALL      REPC0000               * (Y)es selected, print report
          GOTO      ML0100
.
. ---- master listing description sequence --------
.
ML2000    CALL      SSCR0000               * display screen format
ML2100    CALL      DESC0000               * input start/end description
          BRANCH    EXIT,ML2100            * invalid range, input codes
          CALL      CONTQST                * Ask Ok to continue (Y/N/C) ?
          BRANCH    CEXIT:
                    ML2300:                * (Y)es selected
                    ML2100:                * (N)o selected 
                    ML0100                 * (C)ancel selected
ML2300    CALL      REPD0000               * print report
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.***************************************************************************
.*                                INIT0000             Called by: ML0000   *
.*                             initialization                              *
.*  The initialization routine is used to display headings and open files. *
.*  Any other general initialization can also be performed here.           *
.***************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P50:24,"oprcliaf";
          OPEN      OPRCLIA1,"oprcliaf"
          OPEN      OPRCLIA2,"oprcliaf"
          DISPLAY   *P50:24,"patdocf";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,FORTY;*111,OPCNCLSU:
                                   *96,OPCNCDSC 
.
          MOVE      ONE,CNOUNDLN              * Stop auto. underline in HEAD80
.
INIT9999  RETURN
+
.***************************************************************************
.*                                OPTN0000             Called by: ML0000   *
.*                        get user options or exit                         *
.*                                                                         *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                 *
.*                        TRUE  (1)  Exit option selected                  *
.*              COPTION = 1  Code sequence option selected                 *
.*                        2  Description option selected                   *
.***************************************************************************
.
. ---- keyin option ----
.
OPTN0000  MOVE      ZERO,COPTION                * initialize Coption   
          DISPLAY   *P17:8,*EF                  * erase rest of screen
          KEYIN     *P17:8,*EL,*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL                     * exit
          BRANCH    COPTION,OPTN3000,OPTN3000
          BEEP                                   * invalid input
          GOTO      OPTN0000
.
OPTN3000  MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT                     * set EXIT = 1
OPTN9999  RETURN
+
.***************************************************************************
.*                        DOPT0000                 Called by : OPTN0000    *
.*                   display option                          : FSCR0000    *   
.*                                                           : SSCR0000    *
.***************************************************************************

DOPT0000  HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF:                           * select option
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Code sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Description sequence"
.
          DISPLAY   *P1:8,"Select Option : "
.
          BRANCH    COPTION,DOPT9000,DOPT9000,DOPT9000
.    
          COMPARE   ZERO,COPTION
          GOTO      DOPT9999 IF EQUAL
.
          BEEP          
          GOTO      DOPT0000
.
DOPT9000  DISPLAY   *P17:8,*V2LON,COPTION
.
DOPT9999  RETURN

.***************************************************************************
.*                          CLRC0000                 Called by : DESC0000  *
.*                clear start and end codes and description
.***************************************************************************
CLRC0000  MOVE      SP6,STRTCODE
          MOVE      SP6,ENDCODE
          MOVE      SP30,STRTDESC 
          MOVE      SP30,ENDDESC
CLRC9999  RETURN
.
.***************************************************************************
.*                          CLRW0000                     Called by DESC0000*
.*              clear display variables                                    *
.*              ie start code/endcode and their descriptions
.***************************************************************************
CLRW0000 
          MOVE      SP6,TEMPCODE
          MOVE      SP30,DESCRIP 
.
CLRW9999  RETURN
***************************************************************************
.*                          CLRK0000                    Called by DESC0000*
.*                 clear key variables
***************************************************************************
CLRK0000  MOVE      SP6,KEY6
          PACK      KEY9,SP9
          PACK      KEY36,SP30,SP10
.
CLRK9999  RETURN
.
.***************************************************************************
.*                           CODE0000                     Called by ML0000 *
.*               input the start and ending code                           *
.***************************************************************************
.
CODE0000  CALL      CLRC0000                     * clear start/end codes
.                                                * and description
          MOVE      ONE,NCODE                    * start of code variable
          MOVE      TEN,ROW                      * row for start of code
          CALL      FSCR0000                      * display start code/end code 
.                                                 * screen layoout 
CODE1500  CALL      CLRW0000                      * clear working variables
          CALL      CLRK0000                      * clear keys  
          CALL      KCOD0000                      * keyin code
          BRANCH    NCODE,CODE2000,CODE9000       * NCODE 1 - code keyed in
.                                                 *           starting code
.                                                 *       2 - ending code
.
CODE2000  MOVE      TWO,NCODE                     * end of code variable
          MOVE      TEN2,ROW                      * row for end of code
          GOTO      CODE1500

CODE9000  BRANCH    EXIT,CODE9999                * invalid range input
          MOVE      FALSE,EXIT

CODE9999  RETURN
.
.***************************************************************************
.*                           DESC0000                     Called by ML0000 *
.*               input the start and ending description                    *
.***************************************************************************
DESC0000  CALL      SSCR0000                     * display start desc/end desc 
.                                                * screen layoout 
          CALL      CLRC0000                     * clear start/end codes
.                                                * and description
          MOVE      ONE,NCODE                    * start of desc varaible
          MOVE      TEN,ROW                      * row for start description

DESC3500  CALL      CLRW0000                      * clear working variables
          CALL      CLRK0000                      * clear keys  
          CALL      KDSC0000                      * keyin description
          BRANCH    NCODE,DESC4000,DESC9000       * NCODE 1 - code keyed in
.                                                 *           starting code
.                                                 *       2 - ending code
.
DESC4000  MOVE      TWO,NCODE                     * end of desc variable 
          MOVE      TEN2,ROW                      * row for ending desc input
          GOTO      DESC3500
.
DESC9000  BRANCH    EXIT,DESC9999                 * invalid range input
.
          MOVE      FALSE,EXIT
.
DESC9999  RETURN   

.***************************************************************************
.*                     MCOD0000                      Called by : KCOD0000  *
.*        determine callin procedure ( starting code or ending code )      *
.*        and move the code and description to appropriate variables       *
.***************************************************************************
MCOD0000  BRANCH    NCODE,MCOD1000,MCOD2000     * start code or end code 
.
MCOD1000  MOVE      DESCRIP,STRTDESC            * get description for start code
          MOVE      TEMPCODE,STRTCODE           * get code as start of range 
          GOTO      MCOD9999         
        
MCOD2000  MOVE      DESCRIP,ENDDESC             * get description for end code
          MOVE      TEMPCODE,ENDCODE            * get code as end of range
.
MCOD9999  RETURN
+
.***************************************************************************
.*                                  SSCR0000           Called by: ML0000   *
.*                display screen format for description sequence           *
.***************************************************************************
SSCR0000  DISPLAY  *P1:10,*EF: 
                    *P1:10,"Starting Description : ":
                    *P3:12,"Ending Description : "
.
SSCR9999  RETURN
.***************************************************************************
.*                                  FSCR0000           Called by: ML0000   *
.*                  display screen format for code sequence                *
.***************************************************************************
FSCR0000  DISPLAY   *P1:10,*EF:
                    *P1:10,"Starting Code : ":
                    *P3:12,"Ending Code : "
.
FSCR9999  RETURN
.
.***************************************************************************
.*                         DEND0000                   Called by : KCOD0000 *
.*         display end code and description                                *
.***************************************************************************
DEND0000  DISPLAY   *P17:12,*V2LON,ENDCODE        * display end code
DEND0500  MATCH     SP6,ENDCODE                  * nothing input
          GOTO      DEND1000 IF EQUAL             * yes
.
DEND0800  DISPLAY   *P26:12,*EL,ENDDESC        * display description 
          GOTO      DEND9999
.
DEND1000  DISPLAY   *P17:12,*EL,*V2LON,ENDDESC    * display 'finish'
DEND9999  RETURN
.
.***************************************************************************
.*                         DST0000                    Called by : KCOD0000 *
.*         display start code and description                              *
.***************************************************************************
DST0000   DISPLAY   *P17:10,*V2LON,STRTCODE       * display start code
DST0500   MATCH     SP6,STRTCODE                  * nothing input
.
          GOTO      DST1000 IF EQUAL              * yes

DST0800   DISPLAY   *P26:10,*EL,STRTDESC          * display description
          GOTO      DST9999
.
DST1000   DISPLAY   *P17:10,*EL,*V2LON,STRTDESC  * display 'Start' in highlight
.
DST9999   RETURN
.
.
.***************************************************************************
.*                         DDEN0000                   Called by : KDSC0000 *
.*         display end description                              : DEN0000  *
.***************************************************************************
DDEN0000  DISPLAY   *P24:12,*EL,*V2LON,ENDDESC  * display description
DDEN9999  RETURN
.***************************************************************************
.*                         DDST0000                   Called by : KDSC0000 *
.*         display start description                            : DST00000 *
.***************************************************************************
DDST0000   DISPLAY   *P24:10,*EL,*V2LON,STRTDESC  * display description
DDST9999   RETURN
.
.***************************************************************************
.*                                  KCOD0000           Called by: CODE0000 *
.*                       Input the clin. code field                         *
.*                                                                         *
.*   Returns  : TEMPCODE  - the clin. code                                  *
.*              EXIT      - (0) FALSE - valid or nothing input             *
.*                        - (1) TRUE - invalid range                       *
.***************************************************************************
KCOD0000  KEYIN     *P17:ROW,*EL,*DV,UNDLN6:   * Keyin the security code
                    *P17:ROW,*V2LON,TEMPCODE:
                    *P17:ROW,*DV,TEMPCODE
.
.        validate code input
.
          CALL      CHKC0000             
          BRANCH    EXIT,KCOD0500,KCOD1000,KCOD0000,KCOD9900
.
.    nothing input or valid input        
.    determine if start or end code and get appropriate description
.
KCOD0500  CALL      MCOD0000         
          BRANCH    NCODE,KCOD0700,KCOD0750   * determine calling procedure
.                                             * if start code or end code
.
KCOD0700  CALL      DST0000                   * display start code and descrip 
          GOTO      KCOD9800                  * if NCODE = 1
.
KCOD0750  CALL      DEND0000                  * display end code and description
.                                             * if NCODE = 2
          GOTO      KCOD9800                
.
.    "?" inputted
.
KCOD1000  CALL      OPRCLIDS                 * Display the codes on file
          BRANCH    EXIT,KCOD0000            * No codes on file
.
.         Now ask for the security code to be entered, while leaving the
.         displayed security codes on the screen
.
KCOD2000  CALL      CLRW0000                  * Clear the record variables
.
          DISPLAY   *P1:24,*EL,"Code :"           * Keyin the security code
          KEYIN     *P8:24,*DV,UNDLN6:
                    *P8:24,*V2LON,TEMPCODE:
                    *P8:24,*DV,TEMPCODE
.
.      repeat validation of code input
.
          CALL      CHKC0000
.
          BRANCH    EXIT,KCOD7000,KCOD1000,KCOD2000
.
.   a valid code input or nothing input
.     
KCOD7000  CALL      MCOD0000               * determine if start code or end code
          CALL      DOPT0000               * display option screen 
          CALL      FSCR0000               * display the screen layout
.                                          * for code sequence
.
          BRANCH    NCODE,KCOD8000,KCOD8500   
.
KCOD8000  CALL      DST0000               * display start code and description
          GOTO      KCOD9800              * if NCODE = 1
.
KCOD8500  CALL      DST0000               * display start code and description
          CALL      DEND0000              * display end code and description
.                                         * if NCODE = 2
.
          COMPARE   FOUR,EXIT             * Invalid range
          GOTO      KCOD9900 IF EQUAL
.
KCOD9800  MOVE      FALSE,EXIT            
          GOTO      KCOD9999
.
KCOD9900  MOVE      TRUE,EXIT             * Invalid range
          DISPLAY   *P1:24,*B,"Invalid code range. ";
          CALL      HITENTER
.
KCOD9999  RETURN
+
.***************************************************************************
.*                                  CHKC0000           Called by: KCOD0000 *
.*                       Validate clinic code field                        *
.*                                                                         *
.*   Returns  : EXIT    =  (0)  If a valid code is entered                 * 
.*                         (1)  If there is nothing or exitchar entered    *
.*                         (2)  If a question mark "?" is entered          *
.*                         (3)  If invalid code entered                    *
.*                         (4)  If invalid range entered                   *
.***************************************************************************
CHKC0000  ENDSET    TEMPCODE                 * Pad the field with spaces
          APPEND    SP6,TEMPCODE 
          RESET     TEMPCODE
.
.         Check if anything was input and set the exit flag appropriately
.
          MATCH     TEMPCODE,SP6             * Was anything input ?
          GOTO      CHKC9000 IF EQUAL        * No
.
.         Check for a "?" input
.
          CMATCH    QUEST,TEMPCODE           * "?" entered ?
          GOTO      CHKC8000 IF EQUAL        * Yes.
.      
.
          CALL      VCOD0000                 * validate code with file          
          BRANCH    EXIT,CHKC6000            * invalid input

          COMPARE   TWO,NCODE                * Input is ending code ?
          GOTO      CHKC3000 IF NOT EQUAL    * No
.
.         * Code input less than/equal start code ?
.
CHKC2000  MATCH     STRTCODE,TEMPCODE
          GOTO      CHKC5000 IF LESS         * Yes , invalid range
.
CHKC3000  MOVE      ZERO,EXIT                * valid input
          GOTO      CHKC9999

CHKC5000  MOVE      FOUR,EXIT                * invalid range input
          MOVE      ZERO,OVRCD
          GOTO      CHKC9999 

.
CHKC6000  MOVE      THREE,EXIT               * invalid input
          DISPLAY   *P1:24,*B,"Invalid code input. ";
          CALL      HITENTER
          MOVE      ZERO,OVRCD               * reset OVRCD condition
          GOTO      CHKC9999        
.
.
CHKC8000  MOVE      TWO,EXIT                 * A "?" input
          GOTO      CHKC9999
.
CHKC9000  MOVE      ONE,EXIT                 * Nothing was input
          BRANCH    NCODE,CHKC9100,CHKC9200  * start or end code 
.
CHKC9100  MOVE      START,DESCRIP            * Description = "START" 
          GOTO      CHKC9999
.
CHKC9200  MOVE      FINISH,DESCRIP           * Description = "FINISH"
.
CHKC9999  RETURN
+
.***************************************************************************
.*                         VCOD0000                   Called by : CHKC0000 *
.*              see if code number input is in file                        *
.*             RETUROP  EXIT - 0  valid code number                        *
.*                            - 1  code number not in file                 *
.***************************************************************************
VCOD0000  MOVE      TEMPCODE,CODENUM          * save the code number input
          PACK      KEY6,CODENUM
          CALL      RDOPCLI1                  * see if code in file
          BRANCH    OVRCD,VCOD9000            * code  not found
          MOVE      OPCLDESC,DESCRIP
VCOD1000  MOVE      FALSE,EXIT                * code found 
          GOTO      VCOD9999
.
VCOD9000  MOVE      TRUE,EXIT                 * code not found
VCOD9999  RETURN
.
.***************************************************************************
.*                                  KDSC0000           Called by: DESC0000 *
.*                       Input the description field                       *
.*                                                                         *
.*   Returns  : DESCRIP  - the description                                 *
.*              EXIT     - (0) FALSE - if valid or nothing inputted        *
.*                       - (1) TRUE  - if invalid range entered            *
.***************************************************************************
KDSC0000  KEYIN     *P24:ROW,*EL,*DV,UNDLN30:   * Keyin the description
                    *P24:ROW,*V2LON,DESCRIP:
                    *P24:ROW,*DV,DESCRIP
.
.        check description code input
.
          CALL      CHKD0000             
          BRANCH    EXIT,KDSC0500,KDSC9900
.
.    nothing input or valid input
.   determine if start or end description inputted
.
KDSC0500  CALL      MCOD0000           
          BRANCH    NCODE,KDSC0700,KDSC0750   * determine calling procedure
.                                             * start or end description
.
KDSC0700  CALL      DDST0000                  * display start description
.                                             * if NCODE = 1
          GOTO      KDSC9800
.
KDSC0750  CALL      DDEN0000                  * display end description
.                                             * if NCODE =2
KDSC9800  MOVE      FALSE,EXIT                * valid or nothing input
          GOTO      KDSC9999              

KDSC9900  MOVE      TRUE,EXIT                 * invalid range input
.
KDSC9999  RETURN
.
+
.***************************************************************************
.*                                  CHKD0000           Called by: KDSC0000 *
.*                       Validate description                              *
.*                                                                         *
.*   Returns  : EXIT    =  (0)  If a valid description is entered          * 
.*                         (1)  If there is nothing entered                *
.*                         (2)  If invalid range entered                   *
.***************************************************************************
CHKD0000  
.
.         Check if anything was input and set the exit flag appropriately
.
          RESET     DESCRIP                  * Was anything input ?
          GOTO      CHKD9000 IF EOS          * No
.

          COMPARE   TWO,NCODE                * Input is end of code ?
          GOTO      CHKD6500 IF NOT EQUAL    * No
.
          MOVE      DESCRIP,ENDDESC
          ENDSET    DESCRIP
          APPEND    ZZZZ,DESCRIP
          RESET     DESCRIP
          MOVE      DESCRIP,ZENDDESC
          MOVE      ENDDESC,DESCRIP
.
CHKD6000  MATCH     STRTDESC,DESCRIP       * End of code less than/equal start ?
          GOTO      CHKD7000 IF LESS       * Yes, invalid range

CHKD6500  MOVE      ZERO,EXIT              * valid input
          MOVE      NOTSPACE,TEMPCODE
          GOTO      CHKD9999
.
.
CHKD7000  MOVE      TWO,EXIT               * invalid range
          DISPLAY   *P1:24,*B,"Invalid description range. ";
          CALL      HITENTER     
          GOTO      CHKD9999
.
CHKD9000  MOVE      ONE,EXIT                 * Nothing was input
          BRANCH    NCODE,CHKD9100,CHKD9200  * start or end description
.
CHKD9100  MOVE      START,DESCRIP            * Description = "Start"
          MOVE      SP6,TEMPCODE
          MOVE      ZERO,EXIT
          GOTO      CHKD9999
.
CHKD9200  MOVE      FINISH,DESCRIP           * Description = "Finish"
          MOVE      SP6,TEMPCODE
          MOVE      ZERO,EXIT
.
CHKD9999  RETURN
.
.***************************************************************************
.*                     REPC0000                    Called by : ML0000      *  
.*         print the clin. code and user name for the range inputted        *
.*         as start and end code                                           *
.***************************************************************************
REPC0000  MOVE      ZERO,CPAGENO                    * initialize page no. to 0.
          MOVE      ONE,CNOUNDLN                    * No underlines for previous
.                                                   * page
          CLOCK     TIME,CTIMEIS                    * get time report started
          MOVE      CODESEQ,CPHDROPT
          CALL      PAGE0000                        * display headings
          MOVE      ZERO,NUMREC                     * initialize no. of records
          DISPLAY   *P37:24,"Clinic Code : "
          PACK      KEY6,STRTCODE
          CALL      RDOPCLI1                     * read file with key value
          BRANCH    OVRCD,REPC1000               * if record not found,read next
          GOTO      REPC4000                     * write record
.
REPC1000  CALL      RKOPCLI1                        * read next record in file
          BRANCH    OVRCD,REPC8000                  * branch if end of file read
.
.
REPC3000  MATCH     SP6,ENDCODE                     * description is "Finish"
          GOTO      REPC4000 IF EQUAL               * Yes (so just write report)
.
          MATCH     OPCLCLIN,ENDCODE                 * Code in file within  
.                                                    * range as matched to 
.                                                    * end of code range ?
          GOTO      REPC8000 IF LESS                 * No - print end of report
.
.   code within starting and ending range
.
REPC4000  DISPLAY   *P51:24,*V2LON,OPCLCLIN;       * display code printed
.
          MOVE      SP20,DSNAME
          MOVE      SP30,DGNAME
          MOVE      SP10,DTITL
.
          MOVE      TWO,PACFRMT
.
.  get doctors name in SURNAME,TITLE GIVEN NAME format
.
          MOVE      OPCLDOCT,KEY6
          CALL      RDDOCT1
          CALL      DOCF0000
.
. -- print details
.
REPC4300  CALL      PRTC0000                      * print code and description
          ADD       ONE,CLNO                      * increment line count
          ADD       ONE,NUMREC                    * increment record count
          MOVE      ZERO,CNOUNDLN                 * print a line of underlines
.                                                 * at the end of previous page
          COMPARE   "55",CLNO                     * New page needed ?
          GOTO      REPC5000 IF NOT LESS          * Yes
          GOTO      REPC1000                        
.
REPC5000  CALL      DASH0000
          CALL      PAGE0000
          GOTO      REPC1000 
.
REPC8000  CALL      PRTE0000         * print end of report and number of records
.
REPC9999  RETURN
.
.***************************************************************************
.*                     REPD0000                    Called by : ML0000      * 
.*         print the clinic code and description for the range inputted    *
.*         as start and end description                                    *
.***************************************************************************
REPD0000  MOVE      ZERO,CPAGENO                    * initialize page no. to 0.
          MOVE      ONE,CNOUNDLN                    * No underlines for previous
.                                                   * page
          CLOCK     TIME,CTIMEIS                    * get time report started
          MOVE      DESCSEQ,CPHDROPT
          CALL      PAGE0000                        * display headings
          MOVE      ZERO,NUMREC                     * initialize no. of records
          DISPLAY   *P37:24,"Clinic Desc.: "
.
          MATCH     SP6,STRTCODE                    * Start ?
          GOTO      REPD0500 IF NOT EQUAL           * no
.
          PACK      KEY36,SP30,SP6
          GOTO      REPD0800
.
REPD0500  PACK      KEY36,STRTDESC,SP6
REPD0800  CALL      RSOPCLI2                     * position pointer
.
REPD1000  CALL      RKOPCLI2                        * read next record in file
          BRANCH    OVRCD,REPD8000                  * branch if end of file read
.
.
REPD3000  MATCH     OPCLDESC,ZENDDESC                * Desc. in file within  
.                                                    * range as matched to 
.                                                    * end of desc. range ?
          GOTO      REPD8000 IF LESS                 * No - print end of report
.
.   code within starting and ending range
.
REPD4000  DISPLAY   *P49:24,*V2LON,OPCLDESC       * display code printed
.                                                 * for user
          MOVE      SP20,DSNAME
          MOVE      SP30,DGNAME
          MOVE      SP10,DTITL
.
          MOVE      TWO,PACFRMT
.
.  get doctors name in SURNAME,TITLE GIVEN NAME format
.
          MOVE      OPCLDOCT,KEY6
          CALL      RDDOCT1
          CALL      DOCF0000
.
. -- print details
.
          CALL      PRTC0000                      * print code and description
          ADD       ONE,CLNO                      * increment line count
          ADD       ONE,NUMREC                    * increment record count
          MOVE      ZERO,CNOUNDLN                 * print a line of underlines
.                                                 * at the end of previous page
          COMPARE   "55",CLNO                     * New page needed ?
          GOTO      REPD5000 IF NOT LESS          * Yes
          GOTO      REPD1000                        
.
REPD5000  CALL      DASH0000
          CALL      PAGE0000
          GOTO      REPD1000
.
REPD8000  CALL      PRTE0000         * print end of report and number of records
.
REPD9999  RETURN
.
.***************************************************************************
.*                PRTC0000                           Called by : REPC0000  *
.*        prints the security code and the user name           : REPD0000  *
.***************************************************************************
PRTC0000  PRINT     *1,"|",SP1,OPCLCLIN,*12,"|",SP1,OPCLDESC:
                    *46,"| ",OPCLDOCT,SP2,PACFNAME,*80,"|"
.
PRTC9999  RETURN
.
.***************************************************************************
.*                       DASH0000                    Called by : REPC0000  *
.*                   prints 78 dash lines                      : REPD0000  *
.***************************************************************************
DASH0000  PRINT     *1,"*--------------------------------------------":
                    "----------------------------------*"
DASH9999  RETURN
.
.***************************************************************************
.*                PRTE0000                           Called by : REPC0000  *
.*        prints the number of records and displays "End of Report"        *
.***************************************************************************
PRTE0000  COMPARE   ZERO,NUMREC                * Number of records = 0 ?
          GOTO      PRTE1000 IF NOT EQUAL      * Yes
          GOTO      PRTE2000
PRTE1000  CALL      DASH0000                   * print dash lines
.
PRTE2000  PRINT     *N,*1,"Total number of clinics = ",SP2,NUMREC
          PRINT     *N,*1,"*** End of Report ***"
.
PRTE9999  RETURN
.
.***************************************************************************
.*                    PAGE0000                        Called by : REPC0000 *
.*        increments page number and displays all necessary heading        *
.*        ie HEAD80 and starting and ending codes and description         *
.***************************************************************************
PAGE0000  CALL      HEAD80                      * print heading ( 1st 3 lines) 
          BRANCH    COPTION,PAGE1000,PAGE2000
.
.    code sequence is selected - print heading 
.
PAGE1000  MATCH     SP6,STRTCODE              * Start ?
          GOTO      PAGE1500 IF EQUAL         * yes
 
          PRINT     *20,"Starting Code : ",STRTCODE,SP2,STRTDESC
          GOTO      PAGE1600

PAGE1500  PRINT     *20,"Starting Code : ",START
 
PAGE1600  MATCH     SP6,ENDCODE                * Finish ?
          GOTO      PAGE1900 IF NOT EQUAL      * no
.
          PRINT     *20,"Ending Code   : ",FINISH,*N
          GOTO      PAGE4000
.
PAGE1900  PRINT     *20,"Ending Code   : ",ENDCODE,SP2,ENDDESC,*N
          GOTO      PAGE4000
.
.    description sequence is selected - print heading 
.
PAGE2000  PRINT     *20,"Starting Desc : ",STRTDESC,*N:
                    *20,"Ending Desc   : ",ENDDESC,*N
.
PAGE4000  CALL      DASH0000                    * print dash lines
          PRINT     *1,"| Code ",*12,"| Description",*46,"| Doctor",*80,"|"
          CALL      DASH0000                    * print dash lines
          ADD       NINE,CLNO
PAGE9999  RETURN
+
.**************************************************************************
.*                  DOCF0000                     Called by: CHKD0000      *
.*        Format doctors name                               DESC0000      *
.**************************************************************************
.
DOCF0000  MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          CALL      PACNAME
.
DOCF9999  RETURN
.***************************************************************************
.
          INC       STD001IO
          INC       OPRCLIIO/INC
          INC       PATDO1IO/INC            * doctors file
          INC       OPRCLIDS                * clin. ? mark routine
................................................................................

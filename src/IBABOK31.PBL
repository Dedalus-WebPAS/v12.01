.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*      PROGRAM NAME:     IBABOK31                                            *
.*                                                                            *
.*      FUNCTION:         BOOKING FILE REGISTER                               *
.*                                                                            *
.*      DATE WRITTEN:     09/02/1988                                          *
.*                                                                            *
.*      AUTHOR:           Rev JOHN CLARK                                      *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.*                                                                            *
.******************************************************************************
.*        V12.00.02 30/05/2025   PJ Le Febour    TSK 0955096 AlphaNum Visitnum*
.*                  Added   RJUSTIFY on the ENDBOOK variable after KEYIN,     *
.*                          ENDBOOK used to be a form right justify on KEYIN. * 
.*                          But now a DIM needs an explicit RJUSTIFY          *
.*        V12.00.01 30/05/2025   PJ Le Febour    TSK 0955096 AlphaNum Visitnum*
.*                  Replace XBOOK   FORM 8 to XBOOKD    DIM 8                 *
.*                          ENDBOOK FORM 8 to ENDBOOK   DIM 8                 *
.*                          STBOOK  FORM 8 to STBOOK    DIM 8                 *
.*                          NINES8  FORM "99999999" to NINES8 INIT "99999999" *
.*                          COMPARE ZERO,STBOOK to MATCH ZEROVISN to STBOOK   *
.*                          COMPARE ZERO,ENDBOOK to MATCH ZEROVISN to ENDBOOK *
.*                          COMPARE STBOOK,ENDBOOK to MATCH STBOOK to ENDBOOK *
.*                          MOVE    XBOOK,DXBOOK   to MOVE  XBOOKD to DXBOOK  *
.*                          MOVE    DXBOOK,XBOOK   to MOVE  DXBOOK to XBOOKD  *
.*                          PACK   KEY8,XBOOK      to PACK  KEY8,XBOOKD       *
.*                          COMPARE BKREBOOK,ENDBOOK to MATCH BKREBOOK,ENDBOOK*
.******************************************************************************
.*        V10.04.01 07/07/2014   J.Tan           CAR 261630                   *
.*                  Removed port number from temp file name                   *
.*        V10.03.01 16/07/2012  Ebon Clements  CAR 265285                     *
.*                  Added multi hospital test to SORT0000                     *
.*        V10.01.01 03/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added PMSVX1FD                                            *
.*        V9.11.01  03/02/2009  Jill Habasque CAR 178560                      *
.*                  Added keyin of hospital ID                                *
.*        V9.04.01  27/09/2004  Jill Habasque CAR 53706                       *
.*                  Changed to print BKRXADWD                                 *
.*        V9.03.00  02/07/2003  Tonii CAR 40346 (THC63)                       *
.*                  Ported from r5.10                                         *
.******************************************************************************
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*        V5.07.02  21/12/1999  J.Tan   SRF  634119                           *
.*                  Mods to use attending doctor from admission for pre-adms. *
.*        V5.07.01  06/08/1999  Jill Habasque SRF 645912                      *
.*                  Fixed keyin of the doctor code                            *
.*                    V5.07.B01 15/01/1999  Nicole Harrington 507 rebound 082 *
.*                              Mods to print century on dates                *
.*                  22/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*                     V5.01.01  26/06/89 K.Peak                              *
.*                               Admission & U/R No. to 8 characters          *
.*                               19/07/89 M.Ohleiter      SRF 101630          *
.*                               Removed preferred accommodation              *
.*                     V5.01.02  12/12/89 K.Peak          SRF 103377          *
.*                               Recompiled for 5th index in BOKMASFD         *
.*                     V5.01.03  24/01/90 K.Peak          SRF 103419, 103420  *
.*                               Fixed print heading & Option 3 date sequence *
.*                               Fixed print heading & Option 4               *
.*                                                                            *
.*        V5.01.10  31/07/1990  Bill 'Heavy Weight Programming Champ' Dew     *
.*                  Update IBABOK31 to standards and cater for new waiting    *
.*                  list FD's and IO's.   Updating TOP DOWN code to Modular   *
.*                  code is as much fun as scrapping a kangaroo carcas from   *
.*                  your front bumper bar.                                    *
.*        V5.01.11  26/11/1990  J. Ceniza                                     *
.*                  Changes to IBABOK31 for Mater Hospital.  The first of     *
.*                  these is to print the session date for theatre bookings,  *
.*                  the second is to be able to print the register for a      *
.*                  range of booking types.                                   *
.*                  Also changed the program so that it now uses BOKRC1FD     *
.*                  which is replacing the BOKMASFD file.                     *
.*        V5.01.12  13/05/1991  H. Crick                                      *
.*                  Changed 'Sequience' to Sequence                           *
.*        V5.01.13  04/07/1991  Justin Coates                                 *
.*                  Changed temp file to be Type,Sched admiss date,Booking #  *
.*        V5.01.14  23/04/1992  D.Di.Paola      don't ____en need one         *
.*                  Fixed record length on tempfile and index.                *
.*                  just for something to do !!!!                             *
.*        V5.01.15  01/12/1992  i chung          SRF 116368                   *
.*                  Fixed inconsistent Date of Birth format                   *
.*        V5.01.16  11/01/1994  Rob Leonard  SRF 127195                       *
.*                  Fixed option 4 (Sched Adm Date Seq), so it actually works *
.*                  when using other than default date range.                 *
.*        V5.01.17  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.18  19/07/1994  Rob Leonard  SRF 129744                       *
.*                  Fixed decleration of temp file vars which was stuffing up *
.*                  the report.                                               * 
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       OPRDEAFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATPR1FD/INC
          INC       PATWR1FD/INC
+
.
.         TEMPORARY FILE DECLARATION
.
BOKTYPFL  IFILE     SQL, FIXED=20, KEY="U1-3,4-11,12-19"
.
. NAME    TYPE   LENGTH  PHYSICAL START LOC.   DESCRIPTION
. ----    ----   ------  -------- ----------   -----------
XBTYP     DIM       3        3         1       Booking Type
XSADATE   DIM       8        8         4       Scheduled Admission Date
DXBOOK    DIM       8        8        12       Booking Number
.End of Record                        20
.
.Redefined forms for keys
.------------------------
.XBOOK     FORM      8        5        *0955096
XBOOKD    DIM       8                 
.
.         GLOBAL VARIABLES
.
BADATE    DIM       10                 *** DOB stored
BEDATE    DIM       10                 *** Due Date stored
BOOKTYP1  DIM       20
BOOKTYP2  DIM       20
.
CMDLINE   DIM       50
CODE      DIM       2
.
DBABY     DIM       14
DBKADMN   DIM       8
DIM3      DIM       3
DIM222    DIM       22
DIMDATE   DIM       8
DNAME     DIM       20
DOCTNAM1  DIM       30
DOCTNAM2  DIM       30
DOCCODE   DIM       6
DPREV     DIM       3
DSTATUS   DIM       12
DSTATUS4  DIM       4
.
ENDATE    DIM       8                  *** end date stored
ENDBOK    DIM       3
.ENDBOOK   FORM      8                 *0955096
ENDBOOK   DIM       8
ENDCR     DIM       20
ENDDATE   DIM       8
ENDDOC    DIM       6
ENDNAME   DIM       20
.
FNAME     DIM       8
GNAME     DIM       20
IBCNMHOS  FORM      1
HOSCODE   DIM       3
.
.
NUMBRECS  FORM      4
OPTDES    DIM       24
PATNAME   DIM       29
.
SCRNFLAG  FORM      1
SDATE     DIM       8                  *** start date stored
SNAME     DIM       20
STARTCR   DIM       20
STBOK     DIM       3
.STBOOK    FORM      8
STBOOK    DIM       8
STDATE    DIM       8
STDOC     DIM       6
STNAME    DIM       20
TASKIDEN  DIM       8
.
.         CONSTANTS
.
CODEBK    INIT      "BK"
CODEMAT   INIT      "MAT"              *** CODES FILE CODEBK..THCSCOD="MAT"
CODETHE   INIT      "THE"              *** CODES FILE CODEBK..THCSCOD="THE"
.
FINISH    INIT      "Finish      "
.
.NINES8    FORM      "99999999"
NINES8    INIT      "99999999"
NUM55     FORM      "55"
.
OPTDES1   INIT      "Booking Number Sequence "             * screen value
OPTDES1A  INIT      "- Booking Number Seq"                 * print value
OPTDES2   INIT      "Patient Surname Sequence"             * screen value
OPTDES2A  INIT      "- Patient Surn. Seq."                 * print value
OPTDES3   INIT      "Doctor Code Sequence    "             * screen value
OPTDES3A  INIT      "- Doctor Code Seq."                   * print value
OPTDES4   INIT      "Scheduled Admission Date Sequence"    * screen value
OPTDES4A  INIT      "- Sch. Adm Date Seq."                 * print value
OPTDES5   INIT      "Booking Type Sequence"                * screen value
OPTDES5A  INIT      "- Booking Type Seq."                  * print value
.
START     INIT      "Start       "
.
THT       INIT      "Tht "
TMPFILE   INIT      "tbktyp"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBABOK31"
PRGNAM    INIT      "Booking Register"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening bokmasaf";
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A2,"bokrc1af"
          OPEN      BOKRC1A3,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A5,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN5;*188,CMATRN,CTHETR
          READ      CONTROLF,HUND05;*216,PTCNDWAW
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME
.
START
.******************************************************************************
.                      MAKE SHIFT MAIN LINE so lets ROCK
.******************************************************************************
ML0000    CALL      MENU0000
.
ML1000    CALL      CLRV0000
          BRANCH    MOPTION,ML1111,ML2222,ML3333,ML4444,ML5555
.
ML9999    CHAIN     PGM
          STOP
ML1111
          CALL      REDP1000
          CALL      GBOK0000           * get booking number range
          GOTO      ML6666
ML2222
          CALL      REDP1000
          CALL      GSUR0000           * get surname range
          GOTO      ML6666
ML3333
          CALL      REDP1000
          CALL      GDOC0000           * get doctor code range
          GOTO      ML6666
ML4444
          CALL      REDP1000
          GOTO      ML6666
.
ML5555    CALL      REDP1000
          CALL      GBTY0000           * get booking type range
.
ML6666    CALL      DRNG0000           * get admission date range
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS = 1
            OPEN      PATHSPA1,"pathspaf"
.
            DISPLAY   *P1:20,*EF,"Hospital Id : ";
            MOVE      TEN5,ECOL
            MOVE      TWENTY,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ONE,CKYIMAND               * set for mandatory
            CALL      PATHSPKY                   * get hospital
            COMPARE   TWO,EXIT                   * exit selected
            GOTO      ML1000 IF EQUAL
.
            MOVE      KEY3,HOSCODE
            DISPLAY   *P22:20,*EL,PTHSNAME;
          ENDIF
.
          CALL      CONTQST
          BRANCH    CEXIT,ML7777,ML1000,ML0000
.
ML7777    BRANCH    MOPTION,ML7888,ML7888,ML7888,ML7888,ML8888  
.
ML7888    CALL      PBRG0000           * Print Booking ReGister
          GOTO      ML0000
.
.  --  Print booking type sequence --
.
ML8888    CALL      SORT0000           * sort data into booking type sequence
.                                      * and write in temp. index file
          CALL      PRBT0000           * print by booking type order
          GOTO      ML0000
+
.******************************************************************************
.                                MENU0000 
.    Display menu options
.******************************************************************************
MENU0000  HLINE     *G37,2,48,80
          DISPLAY   *P1:3,*EF: 
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ",OPTDES1:
                    *P1:6,*V2LON,"2",*HOFF," = ",OPTDES2:
                    *P1:7,*V2LON,"3",*HOFF," = ",OPTDES3:
                    *P1:8,*V2LON,"4",*HOFF," = ",OPTDES4:
                    *P1:9,*V2LON,"5",*HOFF," = ",OPTDES5:
                    *P1:11,"Select Option : ";
MENU1111
          KEYIN     *P17:11,*EL,*DV,UNDLN1,*P17:11,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9999 IF EQUAL
          BRANCH    MOPTION,MENU2222,MENU2222,MENU2222,MENU2222,MENU2222
          BEEP
          GOTO      MENU1111
MENU2222
          LOAD      KEY35,MOPTION,OPTDES1,OPTDES2,OPTDES3,OPTDES4,OPTDES5
          LOAD      CPHDROPT,MOPTION,OPTDES1A,OPTDES2A,OPTDES3A,OPTDES4A:
                                     OPTDES5A
          DISPLAY   *P48:2,*V2LON,"- ",*+,KEY35,SP1;
.
MENU9999  RETURN
+
.******************************************************************************
CLRV0000  UNPACK    SP30,STARTCR,ENDCR
          MOVE      SP30,DOCTNAM1
          MOVE      SP30,DOCTNAM2
          MOVE      SP20,BOOKTYP1
          MOVE      SP20,BOOKTYP2
          MOVE      SP8,STDATE
          MOVE      NINES8,ENDDATE
CLRV9999  RETURN
+
.******************************************************************************
.                                 REDP0000
.******************************************************************************
REDP0000  BRANCH    SCRNFLAG,REDP9999
          MOVE      ONE,SCRNFLAG
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ",OPTDES1:
                    *P1:6,*V2LON,"2",*HOFF," = ",OPTDES2:
                    *P1:7,*V2LON,"3",*HOFF," = ",OPTDES3:
                    *P1:8,*V2LON,"4",*HOFF," = ",OPTDES4:
                    *P1:9,*V2LON,"5",*HOFF," = ",OPTDES5:
                    *P1:11,"Select Option : ",*V2LON,MOPTION;
REDP1000
          BRANCH    MOPTION,REDP1111,REDP2222,REDP3333,REDP6666,REDP5555
REDP1111
          DISPLAY   *P1:13,*EL,"From  Booking Number    :":
                    *P1:14,*EL,"To    Booking Number    :";
          GOTO      REDP6666
REDP2222
          DISPLAY   *P1:13,*EL,"From  Patient Surname   :":
                    *P1:14,*EL,"To    Patient Surname   :";
          GOTO      REDP6666
REDP3333
          DISPLAY   *P1:13,*EL,"From  Doctor Code       : ":
                    *V2LON,STARTCR,*HOFF,*P38:13,DOCTNAM1:
                    *P1:14,*EL,"To    Doctor Code       : ":
                    *V2LON,ENDCR,*HOFF,*P38:14,DOCTNAM2;
          GOTO      REDP6666
.
REDP5555  DISPLAY   *P1:13,*EL,"From  Booking Type      : ":
                    *V2LON,STARTCR,*HOFF,*P34:13,BOOKTYP1:
                    *P1:14,*EL,"To    Booking Type      : ":
                    *V2LON,ENDCR,*HOFF,*P34:14,BOOKTYP2;
REDP6666
          LOAD      CVERT,MOPTION,TEN6,TEN6,TEN6,TEN3,TEN6
          DISPLAY   *P1:CVERT,*EL,"From Scheduled Admission Date :"
          LOAD      CVERT,MOPTION,TEN7,TEN7,TEN7,TEN4,TEN7
          DISPLAY   *P1:CVERT,*EL,"To   Scheduled Admission Date :";
.
REDP9999  RETURN
+
.******************************************************************************
.                                 GBOK0000
.                            Get Booking range
.******************************************************************************
GBOK0000  DISPLAY   *P27:14,*EL,*P27:13,*EL,UNDLN8;
          KEYIN     *P27:13,*V2LON,STBOOK,*P27:13,*DV,STBOOK;
.
          MATCH     SP70,STBOOK
          IF  @EQUAL
            MOVE    ZEROVISN,STBOOK
          ENDIF
          RJUSTIFY  STBOOK
.
          MATCH     ZEROVISN,STBOOK        * test for return hit
          GOTO      GBOK2222 IF NOT EQUAL
          MOVE      START,STARTCR      * set display start desc
          GOTO      GBOK3333
GBOK2222
          MOVE      STBOOK,STARTCR     * move keyin booking number to disp desc
GBOK3333
          DISPLAY   *P27:13,*V2LON,STARTCR;
GBOK3666
          KEYIN     *P27:14,*DV,UNDLN8,*P27:14,*V2LON,ENDBOOK:
                    *P27:14,*DV,ENDBOOK;
.
          MATCH     SP70,ENDBOOK
          IF   @EQUAL
            MOVE    ZEROVISN,ENDBOOK
          ENDIF
          RJUSTIFY  ENDBOOK

          MATCH     ZEROVISN,ENDBOOK       * test return hit
          GOTO      GBOK4444 IF NOT EQUAL
          MOVE      NINES8,ENDBOOK     * move highest ascii value to end range
          MOVE      FINISH,ENDCR       * set the end range display desc
          GOTO      GBOK7777
GBOK4444
          MOVE      ENDBOOK,ENDCR      * set the keyin booking number
GBOK7777
          DISPLAY   *P27:14,*V2LON,ENDCR;
.
          MATCH     STBOOK,ENDBOOK
          GOTO      GBOK9999 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Invalid Booking Number range **    ";
          CALL      HITENTER
          GOTO      GBOK0000
GBOK9999  RETURN
+
.******************************************************************************
.                                 GSUR0000
.                             Get surname range
.******************************************************************************
GSUR0000  MOVE      UNDLN20,STNAME
          DISPLAY   *P27:14,*EL,*P27:13,*EL,STNAME;
          MOVE      SP20,STNAME
          KEYIN     *P27:13,*V2LON,STNAME;
          ENDSET    STNAME
          APPEND    SP20,STNAME
          RESET     STNAME
.
          MATCH     SP20,STNAME
          GOTO      GSUR2222 IF NOT EQUAL
          MOVE      START,STARTCR
          GOTO      GSUR3333
GSUR2222
          MOVE      STNAME,STARTCR
GSUR3333
          DISPLAY   *P27:13,*V2LON,STARTCR;
GSUR4444
          MOVE      UNDLN20,ENDNAME
          DISPLAY   *P27:14,ENDNAME;
          MOVE      SP20,ENDNAME
.
          KEYIN     *P27:14,*V2LON,ENDNAME;
          RESET     ENDNAME
          GOTO      GSUR5555 IF NOT EOS
          MOVE      FINISH,ENDCR
          MOVE      Z30,ENDNAME
          GOTO      GSUR6666
GSUR5555
          MOVE      ENDNAME,ENDCR
          LENSET    ENDNAME
          APPEND    Z30,ENDNAME
          RESET     ENDNAME
GSUR6666
          DISPLAY   *P27:14,*V2LON,ENDCR;
GSUR8888
          MATCH     STNAME,ENDNAME
          GOTO      GSUR9999 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Surname range **    ";
          CALL      HITENTER
          GOTO      GSUR0000
GSUR9999  RETURN
+
.******************************************************************************
.                                 GDOC0000
.                           Get doctor code range 
.******************************************************************************
GDOC0000  MOVE      ONE,CNEWDS
          MOVE      ONE,SCRNFLAG
.
GDOC1000  DISPLAY   *P27:14,*EL,*P27:13,*EL,UNDLN6,*P27:13;
.
GDOC1111  MOVE      "13",EVERT
          MOVE      "27",ECOL 
          CALL      KDOC0000           * keyin a single doctor code
          MOVE      KEY6,STDOC
.
          MATCH     SP6,STDOC          * test for a return hit
          GOTO      GDOC2222 IF NOT EQUAL
          MOVE      START,STARTCR      * set start desc
          MOVE      SP30,DOCTNAM1      * clear the doctor desc
.
          BRANCH    SCRNFLAG,GDOC3333
          CALL      REDP0000
          GOTO      GDOC4444
GDOC2222
          MOVE      STDOC,STARTCR
          CALL      VDOC0000           * KEY6 is keyin in KDOC0000
          BRANCH    EXIT,GDOC1000,GDOC1111
          MOVE      PACFNAME,DOCTNAM1
.
          BRANCH    SCRNFLAG,GDOC3333
          CALL      REDP0000
          GOTO      GDOC4444
GDOC3333
          DISPLAY   *P27:13,*V2LON,STARTCR,*HOFF,*P38:13,DOCTNAM1;
GDOC4444
          DISPLAY   *P27:14,UNDLN6,*P27:14;
GDOC4666
          MOVE      "14",EVERT
          MOVE      "27",ECOL 
          CALL      KDOC0000           * keyin end doctor code 
          MOVE      KEY6,ENDDOC
.
          MATCH     SP6,ENDDOC
          GOTO      GDOC5555 IF NOT EQUAL
          MOVE      FINISH,ENDCR
          MOVE      Z30,ENDDOC
          MOVE      SP30,DOCTNAM2
.
          BRANCH    SCRNFLAG,GDOC6666
          CALL      REDP0000
          GOTO      GDOC8888
GDOC5555
          MOVE      ENDDOC,ENDCR
          CALL      VDOC0000           * KEY6 carried over from KDOC0000
          BRANCH    EXIT,GDOC4444,GDOC4666
          MOVE      PACFNAME,DOCTNAM2
          BRANCH    SCRNFLAG,GDOC6666
          CALL      REDP0000
          GOTO      GDOC8888
GDOC6666
          DISPLAY   *P27:14,*V2LON,ENDCR,*HOFF,*P38:14,DOCTNAM2;
GDOC8888
          MATCH     STDOC,ENDDOC
          GOTO      GDOC9999 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Doctor range **    ";
          CALL      HITENTER
          GOTO      GDOC0000
GDOC9999  RETURN
+
.******************************************************************************
.                                 KDOC0000
.                  Keyin a doctor code and handle ? routine
.******************************************************************************
KDOC0000  MOVE      SP10,KEY6
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          MOVE      DCODE,KEY6
.
KDOC9999  RETURN
+
.******************************************************************************
.                                 VDOC0000
.             Validate doctor code
.             if invalid doctor keyed in EXIT = 1 if SCRNFLAG = 1 
.                                        EXIT = 2 if SCRNFLAG = 2
.******************************************************************************
VDOC0000  CALL      DOCN0000
          COMPARE   ZERO,EXIT
          GOTO      VDOC9999 IF EQUAL
          MOVE      TWO,EXIT
          LOAD      EXIT,SCRNFLAG,ONE
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Doctor code **    ";
          CALL      HITENTER
VDOC9999  RETURN
+
.******************************************************************************
.                                 DOCN0000
.******************************************************************************
DOCN0000  MOVE      SP30,DGNAME
          MOVE      SP30,DSNAME
          MOVE      SP6,DTITL
          PACK      PACFNAME,SP30,SP30
          CALL      RDDOCT1
          BRANCH    OVRCD,DOCN9990
.
          MOVE      DGNAME,PACGNAME            * set up pacname variables
          MOVE      DSNAME,PACSNAME 
          MOVE      DTITL,PACTITLE 
          MOVE      TWO,PACFRMT
          CALL      PACNAME                    * format name
          MOVE      ZERO,EXIT
          GOTO      DOCN9999
.
DOCN9990  MOVE      ONE,EXIT
.
DOCN9999  RETURN
+
.******************************************************************************
.                                 GBTY0000
.                           Get doctor code range 
.******************************************************************************
GBTY0000  MOVE      ONE,CNEWDS
          MOVE      ONE,SCRNFLAG
.
GBTY1000  DISPLAY   *P27:14,*EL,*P27:13,*EL,UNDLN3,*P27:13;
.
GBTY1111  CALL      KBTY0000           * keyin a single booking type
          MOVE      KEY3,STBOK
.
          MATCH     SP3,STBOK          * test for a return hit
          GOTO      GBTY2222 IF NOT EQUAL
          MOVE      START,STARTCR      * set start description
          MOVE      SP20,BOOKTYP1      * clear the booking type description
.
          BRANCH    SCRNFLAG,GBTY3333
          CALL      REDP0000
          GOTO      GBTY3333
GBTY2222
          MOVE      STBOK,STARTCR
          CALL      VBTY0000           * validate booking type
          BRANCH    EXIT,GBTY1000,GBTY1111
          MOVE      TDESC,BOOKTYP1
          BRANCH    SCRNFLAG,GBTY3333
          CALL      REDP0000
.
GBTY3333  DISPLAY   *P27:13,*V2LON,STARTCR,*HOFF,*P34:13,BOOKTYP1;
.
GBTY4444  DISPLAY   *P27:14,UNDLN3,*P27:14;
.
GBTY4666  CALL      KBTY0000           * keyin end booking type code 
          MOVE      KEY3,ENDBOK
.
          MATCH     SP3,ENDBOK
          GOTO      GBTY5555 IF NOT EQUAL
          MOVE      FINISH,ENDCR
          MOVE      Z30,ENDBOK
          MOVE      SP30,BOOKTYP2
.
          BRANCH    SCRNFLAG,GBTY6666
          CALL      REDP0000
          GOTO      GBTY6666
.
GBTY5555  MOVE      ENDBOK,ENDCR
          CALL      VBTY0000           
          BRANCH    EXIT,GBTY4444,GBTY4666
          MOVE      TDESC,BOOKTYP2
          BRANCH    SCRNFLAG,GBTY6666
          CALL      REDP0000
.
GBTY6666  DISPLAY   *P27:14,*V2LON,ENDCR,*HOFF,*P34:14,BOOKTYP2;
.
GBTY8888  MATCH     STBOK,ENDBOK
          GOTO      GBTY9999 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Booking Type range **    ";
          CALL      HITENTER
          GOTO      GBTY0000
GBTY9999  RETURN
.
.******************************************************************************
.                                 KBTY0000
.                Keyin a booking type code and handle ? routine
.******************************************************************************
KBTY0000  COMPARE   ZERO,SCRNFLAG
          GOTO      KBTY4444 IF EQUAL
.
KBTY1000  KEYIN     *V2LON,KEY3;
          ENDSET    KEY3
          APPEND    SP3,KEY3
          RESET     KEY3
.
          CMATCH    "?",KEY3
          GOTO      KBTY9999 IF NOT EQUAL
          MOVE      ZERO,SCRNFLAG
          PACK      CODE,ANSB,ANSK
          CALL      PATCODDS
.
KBTY4444  DISPLAY   *P1:24,*EL,"Booking Type Code : ___",*P21:24;
          GOTO      KBTY1000
KBTY9999  RETURN
+
.******************************************************************************
.                                 VBTY0000
.    Validate booking type code
.    if invalid booking type keyed in EXIT = 1 if SCRNFLAG = 1 
.                                     EXIT = 2 if SCRNFLAG = 2
.******************************************************************************
VBTY0000  PACK      KEY5,ANSB,ANSK,KEY3
          CALL      RDCODE1
          BRANCH    OVRCD,VBTY1000
          MOVE      ZERO,EXIT
          GOTO      VBTY9999
.
VBTY1000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Booking Type code **    ";
          CALL      HITENTER
          MOVE      TWO,EXIT
          LOAD      EXIT,SCRNFLAG,ONE
          GOTO      VBTY9999
.
VBTY9999  RETURN          
+
.*****************************************************************************
.*                     SORT0000                      Called by : ML0000      *
.*              loop through the bookings file for the requested date range  *
.*            ignoring all records whose booking type does not lie in the    *
.*            requested range.                                               *
.*****************************************************************************
SORT0000  CALL      CRET0000                * create temp. index file
          PACK      KEY16,STDATE,SP20
          CALL      RSBKREC4
.
SORT1000  CALL      RKBKREC4                * expected date sequence
          BRANCH    OVRCD,SORT9999
.
          PACK      KEY8,BKREBOOK,SP70
          CALL      RDBKRX11
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DIMDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     DIMDATE,ENDDATE
          GOTO      SORT9999 IF LESS
.
          MATCH     STBOK,BKRETYPE           * booking type < start range
          GOTO      SORT1000 IF LESS
          MATCH     BKRETYPE,ENDBOK          * booking type > end range
          GOTO      SORT1000 IF LESS
.
          COMPARE   ONE,IBCNMHOS
          GOTO      SORT2000 IF NOT EQUAL
.
          IF        PTCNDWAW=0
            PACK      KEY6,BKRXPOWD,SP70
          ELSE
            PACK      KEY6,BKREWARD,SP70
          ENDIF
.
          MATCH     SP70,KEY6
          GOTO      SORT1000 IF EQUAL
.
          CALL      RDWARD1
          BRANCH    OVRCD,SORT1000
.
          MATCH     PTWDHOSN,HOSCODE
          GOTO      SORT1000 IF NOT EQUAL
.
SORT2000  PACK      KEY19,BKRETYPE,BKREEDAT,BKREBOOK
          MOVE      BKRETYPE,XBTYP
          MOVE      BKREEDAT,XSADATE
........  MOVE      BKREBOOK,XBOOK         *0955096
          MOVE      BKREBOOK,XBOOKD
.
. -- write to temp. index file ---
.
          RESET     KEY19
........  MOVE      XBOOK,DXBOOK          *0955096
          MOVE      XBOOKD,DXBOOK
          WRITE     BOKTYPFL,KEY19;KEY19
          GOTO      SORT1000
.
SORT9999  RETURN
+
.*****************************************************************************
.*                       PRBT0000                  Called by : ML0000        *
.*           Print booking type record                                       * 
.*****************************************************************************
PRBT0000  MOVE      ZERO,NUMBRECS
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,CLNO
          CALL      HEAD0000
.
          DISPLAY   *P1:24,*EL,"Printing Booking Number :":
                    *P40:24,*EL,"Surname     :";
.
          MOVE      SP20,KEY19
          RESET     KEY19
          READ      BOKTYPFL,KEY19;;
.
PRBT1000  READKS    BOKTYPFL;XBTYP,XSADATE,DXBOOK
          GOTO      PRBT6666 IF OVER        * last record read
........  MOVE      DXBOOK,XBOOK             *0955096
          MOVE      DXBOOK,XBOOKD
.
.......   PACK      KEY8,XBOOK              *0955096
          PACK      KEY8,XBOOKD
          CALL      RDBKREC1                * read BOKRC1FD file
          CALL      RDBKRX11
          
          DISPLAY   *P27:24,*V2LON,BKREBOOK,*P57:24,BKRESNAM;
.
          COMPARE   CLNO,NUM55              * test for a full printed page
          CALL      HEAD0000 IF LESS
.
          CALL      DETT0000                * get patient details
.
          PRINT     "|",BKREBOOK,"|",BEDATE,"|",BKREURNO,"| ",PATNAME:
                    "|",BADATE,"| ",BKRETYPE," | ",DNAME," | ",DPREV:
                    "|",DSTATUS4,SP1,DBABY,*126,"| ",BKRXADWD," |"
          ADD       ONE,NUMBRECS            * increment printed record counter
          ADD       ONE,CLNO
          GOTO      PRBT1000
.
PRBT6666  COMPARE   ZERO,NUMBRECS
          GOTO      PRBT7777 IF EQUAL
.
          CALL      UND132
          PRINT     *N," Number of Bookings : ",NUMBRECS:
                    *N,*N,*N,"***  End of Register  ***"
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "*** Register Generation Completed ***";
          GOTO      PRBT9999
PRBT7777
          PRINT     *N," Number of Bookings : ",NUMBRECS:
                    *N,*N,*N,"***  End of Register  ***"
          DISPLAY   *P1:24,*EL,*V2LON:
                    "*** No Bookings selected ***";
          CALL      HITENTER
.
PRBT9999  CALL      KILL0000                * delete temp. file
          RETURN
+
.*****************************************************************************
.*                     CRET0000                      Called by :             *
.*              create temporary file                                        *
.*****************************************************************************
CRET0000  CALL      KILL0000                     * make sure file is deleted 
          CLEAR     CMDLINE                      * clear command line
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 20 u1-3,4-11,12-19",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKIDEN
          OPEN      BOKTYPFL,FNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*                           KILL0000                                      *
.*                  delete temporary index file                            *
.***************************************************************************
KILL0000  CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          CLOSE     BOKTYPFL
          EXECUTE   CMDLINE,TASKIDEN
KILL9999  RETURN
+
.******************************************************************************
.                                 DRNG0000  
.                    Get scheduled admission date range
.******************************************************************************
DRNG0000  DISPLAY   *P33:16,*EL;
          LOAD      CVERT,MOPTION,TEN6,TEN6,TEN6,TEN3,TEN6
          MOVE      "33",CCOL
          MOVE      CCC,CCENT
.
DRNG1111  UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,DRNG1111
          BRANCH    OVRCD,DRNG2222
.
          PACK      STDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STDATE
          CALL      PACDATE
          MOVE      CPDATE,SDATE
          GOTO      DRNG3333
DRNG2222
          MOVE      START,SDATE
          DISPLAY   *P33:CVERT,*V2LON,SDATE;
DRNG3333
          MOVE      "33",CCOL
          LOAD      CVERT,MOPTION,TEN7,TEN7,TEN7,TEN4,TEN7
DRNG5555
          CALL      KEYDATE
          BRANCH    CQUEST,DRNG5555
          BRANCH    OVRCD,DRNG6666
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE
          MOVE      CPDATE,ENDATE
.
          MATCH     STDATE,ENDDATE
          GOTO      DRNG9999 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Date must be greater than or equal From Date **   ";
          CALL      HITENTER
          UNPACK    STDATE,CCENT,CYEAR,CMON,CDAY
          GOTO      DRNG5555
DRNG6666
          MOVE      NINES8,ENDDATE
          MOVE      FINISH,ENDATE
          DISPLAY   *P33:CVERT,*V2LON,ENDATE;
.
DRNG9999  RETURN
+
.******************************************************************************
.*                            FPTR0000                                        *
.*          position pointer                                                  *
.*          RETURNS EXIT - (1) for MOPTION 1 if starting booking number is    *
.*                          found.                                            *
.*                       - (0) otherwise ( all for other MOPTION options ).   *
.******************************************************************************
FPTR0000  MOVE      ZERO,EXIT                * initialize OVRCD condition
          BRANCH    MOPTION,FPTR1111,FPTR2222,FPTR3333,FPTR4444
FPTR1111
          PACK      KEY8,STBOOK
          CALL      RDBKREC1
          BRANCH    OVRCD,FPTR9999            * record not found
          CALL      RDBKRX11
          MOVE      ONE,EXIT
          GOTO      FPTR9999
FPTR2222
          PACK      KEY28,STNAME,SP30
          CALL      RSBKREC2
          PACK      KEY8,BKREBOOK,SP70
          CALL      RDBKRX11
          GOTO      FPTR9999
FPTR3333
          PACK      KEY22,STDOC,SP20
          CALL      RSBKREC3
          PACK      KEY8,BKREBOOK,SP70
          CALL      RDBKRX11
          GOTO      FPTR9999
FPTR4444
          PACK      KEY16,STDATE,SP20
          CALL      RSBKREC4
          PACK      KEY8,BKREBOOK,SP70
          CALL      RDBKRX11
FPTR9999  RETURN
+
.******************************************************************************
.                                 RNRC0000
.                   Read next record and validate correct
.******************************************************************************
RNRC0000  BRANCH    MOPTION,RNRC1111,RNRC2222,RNRC3333,RNRC4444
.
RNRC1111  CALL      RKBKREC1                * booking number index
          BRANCH    OVRCD,RNRC9999
          PACK      KEY8,BKREBOOK,SP70
          CALL      RDBKRX11
.
          MATCH     BKREBOOK,ENDBOOK        * test reach end booking range
          GOTO      RNRC9990 IF LESS
.
          COMPARE   ONE,IBCNMHOS
          GOTO      RNRC1200 IF NOT EQUAL
.
          IF        PTCNDWAW=0
            PACK      KEY6,BKRXPOWD,SP70
          ELSE
            PACK      KEY6,BKREWARD,SP70
          ENDIF
.
          MATCH     SP70,KEY6
          GOTO      RNRC1111 IF EQUAL
.
          CALL      RDWARD1
          BRANCH    OVRCD,RNRC1111
.
          MATCH     PTWDHOSN,HOSCODE
          GOTO      RNRC1111 IF NOT EQUAL
.
RNRC1200  UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DIMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DIMDATE
.
          MATCH     STDATE,DIMDATE          * within start range
          GOTO      RNRC1111 IF LESS        * no, ignore record
.
          MATCH     DIMDATE,ENDDATE         * within end range
          GOTO      RNRC1111 IF LESS        * no, ignore record
          GOTO      RNRC9000
.
RNRC2222  CALL      RKBKREC2                * Surname index
          BRANCH    OVRCD,RNRC9999
.
          PACK      KEY8,BKREBOOK,SP70
          CALL      RDBKRX11
          MATCH     BKRESNAM,ENDNAME        * test reached end surname range
          GOTO      RNRC9990 IF LESS
.
          COMPARE   ONE,IBCNMHOS
          GOTO      RNRC2300 IF NOT EQUAL
.
          IF        PTCNDWAW=0
            PACK      KEY6,BKRXPOWD,SP70
          ELSE
            PACK      KEY6,BKREWARD,SP70
          ENDIF
.
          MATCH     SP70,KEY6
          GOTO      RNRC2222 IF EQUAL
.
          CALL      RDWARD1
          BRANCH    OVRCD,RNRC2222
.
          MATCH     PTWDHOSN,HOSCODE
          GOTO      RNRC2222 IF NOT EQUAL
.
RNRC2300  UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DIMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DIMDATE
.
          MATCH     STDATE,DIMDATE          * within start date range
          GOTO      RNRC2222 IF LESS        * no, ignore record
.
          MATCH     DIMDATE,ENDDATE         * within end date range
          GOTO      RNRC2222 IF LESS        * no, ignore record
          GOTO      RNRC9000
.
RNRC3333  CALL      RKBKREC3                * associated doctor sequence
          BRANCH    OVRCD,RNRC9999
          PACK      KEY8,BKREBOOK,SP70
          CALL      RDBKRX11
.
          MATCH     BKREADOC,ENDDOC         * test reach end doctor code
          GOTO      RNRC9990 IF LESS
.
          COMPARE   ONE,IBCNMHOS
          GOTO      RNRC3400 IF NOT EQUAL
.
          IF        PTCNDWAW=0
            PACK      KEY6,BKRXPOWD,SP70
          ELSE
            PACK      KEY6,BKREWARD,SP70
          ENDIF
.
          MATCH     SP70,KEY6
          GOTO      RNRC3333 IF EQUAL
.
          CALL      RDWARD1
          BRANCH    OVRCD,RNRC3333
.
          MATCH     PTWDHOSN,HOSCODE
          GOTO      RNRC3333 IF NOT EQUAL
.
RNRC3400  UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DIMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DIMDATE
.
          MATCH     STDATE,DIMDATE          * within start date range
          GOTO      RNRC3333 IF LESS        * no, ignore record 
.
          MATCH     DIMDATE,ENDDATE         * within end date range
          GOTO      RNRC3333 IF LESS        * no, ignore record 
          GOTO      RNRC9000
RNRC4444
          CALL      RKBKREC4                * expected date sequence
          BRANCH    OVRCD,RNRC9999
          PACK      KEY8,BKREBOOK,SP70
          CALL      RDBKRX11
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DIMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DIMDATE
.
          MATCH     DIMDATE,ENDDATE
          GOTO      RNRC9990 IF LESS
.
          COMPARE   ONE,IBCNMHOS
          GOTO      RNRC9000 IF NOT EQUAL
.
          IF        PTCNDWAW=0
            PACK      KEY6,BKRXPOWD,SP70
          ELSE
            PACK      KEY6,BKREWARD,SP70
          ENDIF
.
          MATCH     SP70,KEY6
          GOTO      RNRC4444 IF EQUAL
.
          CALL      RDWARD1
          BRANCH    OVRCD,RNRC4444
.
          MATCH     PTWDHOSN,HOSCODE
          GOTO      RNRC4444 IF NOT EQUAL
.
RNRC9000
          MOVE      ZERO,OVRCD
          GOTO      RNRC9999
RNRC9990
          MOVE      ONE,OVRCD
RNRC9999  RETURN
+
************************************************************************
.*                               VSDT000                               *
.*                 get basic details for a visit number                *
************************************************************************
VSDT0000  MATCH     BKREURNO,ZEROUR         * Zero U/R No. ?
          GOTO      VSDT3000 IF EQUAL       * Yes
.
. -- U/R no. <> '0'
.
          MOVE      BKREBOOK,KEY8  
          CALL      RDMISS1
          BRANCH    OVRCD,VSDT8000          * Visit No. not found in PATMI1FD
          MOVE      ADOCTA,BKREADOC
.
VSDT1060  MOVE      AURNO,KEY8              * pack key with U/R No.
          CALL      RDMAST1                 * read master file for details
          BRANCH    OVRCD,VSDT9999          * no record found 
.
          CALL      PATN0000                * get patient details
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BADATE          * set date of birth
          GOTO      VSDT9999
.
. --- Zero U/R No.
.
VSDT3000  MOVE      BKREBOOK,KEY8 
          CALL      RDPRAM1                 * read pre admissions file
          BRANCH    OVRCD,VSDT9999          * not found
          CALL      PATN0000                * get patient details
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BADATE           * set date of birth
          GOTO      VSDT9999
.
VSDT8000  MOVE      BKREBOOK,KEY8
          CALL      RDDSCH1                 * read discharge file
          BRANCH    OVRCD,VSDT9999
          MOVE      DURNO,AURNO
          GOTO      VSDT1060
VSDT9999  RETURN
+
.******************************************************************************
.                                 PBRG0000
.                          Print booking register
.******************************************************************************
PBRG0000  MOVE      ZERO,NUMBRECS
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,CLNO
          CALL      HEAD0000
.
          DISPLAY   *P1:24,*EL,"Printing Booking Number :":
                    *P40:24,*EL,"Surname     :";
.
          CALL      FPTR0000           * position fptr in desired file.
          BRANCH    EXIT,PBRG5555      * record found (needed for option 1)
.
PBRG2222  CALL      RNRC0000           * read next record
          BRANCH    OVRCD,PBRG6666
          GOTO      PBRG5555
.
.         Only for option 3 - by range of doctor code, we need to loop through
.         admission file to check for the pre-adm. attending doctor
.
PBRG3333  COMPARE   THREE,MOPTION
          GOTO      PBRG5555 IF NOT EQUAL
.
          PACK      KEY21,ONE,STDOC,SP20,SP10
          CALL      RDSMISS5
PBRG3400  CALL      RDKMISS5
          BRANCH    OVRCD,PBRG5555
          COMPARE   ONE,ASTAT          * preadmission ?
          GOTO      PBRG5555 IF NOT EQUAL
.
          MATCH     ADOCTA,ENDDOC      * reach end doctor code ?
          GOTO      PBRG5555 IF LESS
.
          MOVE      AADMNO,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,PBRG3400
          CALL      RDBKRX11
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DIMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DIMDATE
.
          MATCH     STDATE,DIMDATE     * within start date range
          GOTO      PBRG3400 IF LESS
          MATCH     DIMDATE,ENDDATE    * within end date range
          GOTO      PBRG3400 IF LESS
.
PBRG5555  DISPLAY   *P27:24,*V2LON,BKREBOOK,*P57:24,BKRESNAM;
.
          COMPARE   CLNO,NUM55         * test for a full printed page
          CALL      HEAD0000 IF LESS
.
          CALL      DETT0000           * get patient details
.
          PRINT     "|",BKREBOOK,"|",BEDATE,"|",BKREURNO,"| ",PATNAME:
                    "|",BADATE,"| ",BKRETYPE," | ",DNAME," | ",DPREV:
                    "|",DSTATUS4,SP1,DBABY,*126,"| ",BKRXADWD," |"
          ADD       ONE,NUMBRECS       * increment the printed record counter
          ADD       ONE,CLNO
          GOTO      PBRG2222
.
PBRG6666  COMPARE   ZERO,NUMBRECS
          GOTO      PBRG7777 IF EQUAL
.
          CALL      UND132
          PRINT     *N," Number of Bookings : ",NUMBRECS:
                    *N,*N,*N,"***  End of Register  ***"
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "*** Register Generation Completed ***";
          GOTO      PBRG9999
.
PBRG7777
          PRINT     *N," Number of Bookings : ",NUMBRECS:
                    *N,*N,*N,"***  End of Register  ***"
          DISPLAY   *P1:24,*EL,*V2LON:
                    "*** No Bookings selected *** ";
          CALL      HITENTER
PBRG9999  RETURN
+
.******************************************************************************
.                                 DETT0000
.                          Format patient details
.******************************************************************************
DETT0000  UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BEDATE           * set booking date
.
          MOVE      SP6,PTITL
          MOVE      SP30,PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP8,BADATE              * clear Date of Birth
          MOVE      SP30,PATNAME            * clear Patient's Name
.
          MOVE      BKREURNO,KEY8           * pack key with U/R No.
          CALL      RDMAST1                 * read Master file for details
          BRANCH    OVRCD,DETT1000          * no record found 
.
          CALL      PATN0000                * format patient's name
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BADATE           * set date of birth
.
DETT1000  CALL      VSDT0000                * check visit file for the Adm. No.
.
DETT1200  MOVE      SP30,DNAME
          PACK      PACFNAME,SP30,SP10
          MOVE      BKREADOC,KEY6
          CALL      DOCN0000
          MOVE      PACFNAME,DNAME
DETT4444
          MOVE      "Yes",DPREV             * check if previous inpatient
          CMATCH    ANSY,BKREPREV
          GOTO      DETT5555 IF EQUAL
.
          MOVE      "No",DPREV
          CMATCH    ANSN,BKREPREV
          GOTO      DETT5555 IF EQUAL
          MOVE      "Unk",DPREV
.
DETT5555  MOVE      SP20,DBABY
          PACK      KEY5,CODEBK,BKRETYPE
          CALL      RDCODE1
          BRANCH    OVRCD,DETT6666
.
          MATCH     THCSCOD,CODETHE         * THEATRE BOOKING ?
          GOTO      DETT6666 IF NOT EQUAL   * no
.
.  --- Theatre booking
.
          CALL      LDEA0000                * loop through OPRDEAFD file
          GOTO      DETT6666
.
DETT6666  BRANCH    BKRESTAT,DETT7111,DETT7222,DETT7333,DETT7444
.
.         A PRE-ADMITTED PATIENT WITH BKREPREA MEANS DEPOSIT PAID
.
          MATCH     ANSY,BKREPREA           * W2.01
          GOTO      DETT7000 IF EQUAL
          MOVE      "Booked      ",DSTATUS
          MOVE      "Book",DSTATUS4
          GOTO      DETT9999
DETT7000
          MOVE      "Deposit Paid",DSTATUS
          MOVE      "Dept",DSTATUS4
          GOTO      DETT9999
DETT7111
          MOVE      "Pre-Admitted",DSTATUS
          MOVE      "Pre ",DSTATUS4
          GOTO      DETT9999
DETT7222
          MOVE      "Cancelled   ",DSTATUS
          MOVE      "Canc",DSTATUS4
          GOTO      DETT9999
DETT7333
          MOVE      "Admitted    ",DSTATUS
          MOVE      "Admt",DSTATUS4
          GOTO      DETT9999
DETT7444
          MOVE      "Discharged  ",DSTATUS
          MOVE      "Disc",DSTATUS4
DETT9999  RETURN
+
.*****************************************************************************
.*                              LDEA0000                Called by : DETT0000 *
.*      Updates for Mater Hospital who wishes to print the session date for  *
.*      theatre bookings.                                                    *
.*      This routine loops through the 2nd index of the OPRDEAFD file using  *
.*      the 2nd index for the booking number.  If there is a record found    *
.*      for the booking number, then the program will set DBABY to           *
.*      Tht "xx/xx/xx"  where "xx/xx/xx" is the session date. Otherwise,     *
.*      the variable will be left blank.                                     *
.*****************************************************************************
LDEA0000  MOVE      BKREBOOK,DBKADMN        * move booking number to a dim field
          PACK      KEY31,BKREBOOK,SP30
          CALL      RSOPDEA2                * position pointer
.
LDEA1000  CALL      RKOPDEA2                * position pointer
          BRANCH    OVRCD,LDEA9999          * read next record 
.
          MATCH     OPDAADMN,DBKADMN        * same number
          GOTO      LDEA9999 IF NOT EQUAL   * no
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DBABY,THT,CPCDATE        * "Tht xx/xx/xx"
.
LDEA9999  RETURN
+
.****************************************************************************
.*                                 PATN0000                                 *
.*                         Concatenate patient name                         *
.****************************************************************************
PATN0000  MOVE      PGNAME,PACGNAME         * set up pacname variables
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                  * format name
          MOVE      PACFNAME,PATNAME         * move to display variable
.
PATN9999  RETURN
+
.******************************************************************************
.                                 HEAD0000
.******************************************************************************
HEAD0000  CALL      HEAD132  
.
          PRINT     *40,"Scheduled Admission Date Range : ",SDATE:
                        " To ",ENDATE,*N
          ADD       TWO,CLNO
.
          BRANCH    MOPTION,HEAD1111,HEAD2222,HEAD3333,HEAD4444,HEAD5555
HEAD1111
          PRINT     *40,"From Booking Number : ",STARTCR
          PRINT     *40,"To   Booking Number : ",ENDCR
          ADD       TWO,CLNO
          GOTO      HEAD4444
HEAD2222
          PRINT     *40,"From Patient Surname : ",STARTCR
          PRINT     *40,"To   Patient Surname : ",ENDCR
          ADD       TWO,CLNO
          GOTO      HEAD4444
HEAD3333
          PRINT     *40,"From Doctor Code : ",STARTCR,SP6,DOCTNAM1
          PRINT     *40,"To   Doctor Code : ",ENDCR,SP6,DOCTNAM2
          ADD       TWO,CLNO
          GOTO      HEAD4444
HEAD4444
          CALL      UND132
          PRINT     "|Booking | Sched.   |   U/R  |":
                    *61,"|Date of   |Book | Attending":
                    *101,"|Prev|",*126,"|",*132,"|"
          PRINT     "|Number  | Admiss.  | Number | Patient Name":
                    *61,"|Birth     |Type | Doctor Name":
                    *101,"|Inp.|Status ",*126,"|Ward |"
          CALL      UND132
          ADD       TWO,CLNO
          GOTO      HEAD9999
.
HEAD5555  PRINT     *40,"From Booking Type : ",STARTCR,SP6,BOOKTYP1
          PRINT     *40,"To   Booking Type : ",ENDCR,SP6,BOOKTYP2
          ADD       TWO,CLNO
          GOTO      HEAD4444
HEAD9999  RETURN
+
.******************************************************************************
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       PATCODKY
          INC       PATDOCKY
          INC       PATHSPKY
.
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       OPRDEAIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATPR1IO/INC
          INC       PATWR1IO/INC
+

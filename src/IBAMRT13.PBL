.******************************************************************************
.* System   : Inpatient                                                       *
.* Program  : IBAMRT13                                                        *
.* Name     : Scancare Report Extraction                                      *
.******************************************************************************
.* Date     : 13/08/2009                                                      *
.* Author   : WeeLee Koo                                                      *
.* Function : It is to extract all patients in a date range ( from and to, or *
.*            a particular day for the scheduling of daily extract). All      *
.*            patients means booked, current and discharges patients.         *
.*            The output is to be put into a flat text pipe "|" delimited file*
.*            The file is to be called 'scancare.txt'                         *
.* Mods:                                                                      *
.*        V11.00.01 11/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.01.01 10/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added pmsvx1af                                            *
.*        V10.00.02 07/05/2010 Peter Vela    CAR 220752                       *
.*                             Changed OPDATIME to OPDAEXPS                   *
.*        V10.00.01 04/05/2010 Peter Vela    CAR 220752                       *
.*                             Added functionality to assign BOOKDATE and     *
.*                             BOOKTIME values from OPDADATE and OPDATIME     *
.*        V9.12.02 10/09/2009  WeeLee Koo    CAR 201600                       *
.*                             Changed name from IBASCN13 to IBAMRT13         *
.*        V9.12.01 28/08/2009  WeeLee Koo    CAR 201600                       *
.*                             Edited HealthFund and Attending Doc(Booking)   *
.*        V9.12.00 13/08/2009  WeeLee Koo    CAR 201600                       *
.*                             Created program                                *
.******************************************************************************
.
          INC       STD001FD
.
.-----------------------------------------------------------------------------
.         File Description Includes
.-----------------------------------------------------------------------------
.
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       BOKRC1FD/INC
          INC       PATFN1FD/INC
          INC       PATDO1FD/INC
          INC       PATMMBFD/INC
          INC       OPRDEAFD/INC
          INC       PATITMFD/INC
.
. ----- Textfile Definition -----
.
FILEDESC  FILE      ASCII,FIXED=592         * File Descripter
.
.Field    Type      Length   
.------------------------------------------------------------------------------
URNUM      DIM       9              * UR Number
FNAME      DIM       26             * First Name 
LNAME      DIM       21             * Last Name
BOOKDATE   DIM       9              * Booking Date
BOOKTIME   DIM       9              * Booking Time
ADMISSNO   DIM       9              * Booking/Admission No
HFDESC     DIM       41             * HealthFund Description
HFCODE     DIM       7              * HealthFund Code
ATTENDOC   DIM       61             * Attending Doctor Name
ADPROVNO   DIM       11             * Attending Doc Provider No
MBSDESC    DIM       123            * MBS Description
MBSCODES   DIM       30             * MBS Codes
FULLDIAG   DIM       168            * Full Diagnosis
MEDICNUM   DIM       11             * Medicare No
PDOB       DIM       9              * Date of Birth
SUBURB     DIM       36             * Suburb
POSTCODE   DIM       9              * Postcode
GENDER     DIM       2              * Gender
.End of Record       592
.
.------------------------------------------------------------------------------
.         Local Variables Definition
.------------------------------------------------------------------------------
.
COUNT     FORM      1
CMDLINE   DIM       80
ENDDTE    DIM       8                       * End date (ccyymmdd)
DOCCODE   DIM       6                       * Attending Doctor
DSPEDDTE  DIM       10                      * Display end date (dd/mm/ccyy)
DSPSTDTE  DIM       10                      * Display start date (dd/mm/ccyy)
STRTDTE   DIM       8                       * Start date (ccyymmdd)
TEMPUR    DIM       8
TEMPADM   DIM       8
TOTREC    FORM      5
MBSDESC1  DIM       40
MBSDESC2  DIM       40
MBSDESC3  DIM       40
MBSCODE1  DIM       9
MBSCODE2  DIM       9
MBSCODE3  DIM       9
FULLDIAG1 DIM       55
FULLDIAG2 DIM       55
FULLDIAG3 DIM       55
.
.
.------------------------------------------------------------------------------
.         Constant Definition
.------------------------------------------------------------------------------
.
FILENAME  INIT      "scancare "
.
DELIMIT   INIT      "|"
SCOLON    INIT      ";"
SP50      INIT      "                                                   "
SP60      INIT      "                                                              "
.
PRGID     INIT      "IBAMRT13"
PRGNAM    INIT      "Extract scancare patients in a date range"
VERSION   INIT      "V12.01.00"
+
.------------------------------------------------------------------------------
.         MAIN0000 - Main Entrance of Execution                        
.------------------------------------------------------------------------------
.
MAIN0000  CALL      INIT0000            * initialise program files/variables
.
MAIN0100  CALL      OPTN0000            *  0= Exit, 1= Run Report
          BRANCH    EXIT,MAIN9999
.
MAIN1000  CALL      DATE0000            * get date range
          BRANCH    EXIT,MAIN0100
.
MAIN2000  CALL      CONTQST             * Ok to continue ?
          BRANCH    CEXIT,MAIN3000:     * yes
                          MAIN0100:     * no
                          MAIN0100      * cancel
.
MAIN3000  CALL      PROC0000            * start processing the files
          CALL      MVEXT000
.
MAIN4000  CALL      GRNT0000           * print total records
.
MAIN9999  CHAIN     PGM
+
.------------------------------------------------------------------------------
.         INIT0000 - Initialisation of program variables
.------------------------------------------------------------------------------
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"   
.  
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"   
.  
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmi1af";
          OPEN      PATMI1A3,"patmi1af"   
          OPEN      PMSVX1A1,"pmsvx1af"   
.  
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"   
.  
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"   
.  
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"   
.  
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"oprdetaf";
          OPEN      OPRDETA2,"oprdetaf"   
. 
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"   
. 
          MOVE      ONE,CNEWDS                   * ? keyin on display
          MOVE      ONE,CNOUNDLN                 * Dont print report header
.
          PREP      FILEDESC,FILENAME
.
INIT9999  RETURN
+
.
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  MOVE      ZERO,TOTREC
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Report "
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.         
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.         
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.******************************************************************************
.*                            DATE0000                                        *
.*                        Keyin The Date Range                                *
.******************************************************************************
.
.
DATE0000  MOVE      "17",CCOL
          DISPLAY   *P1:10,"Starting Date : ",*EF:
                    *P1:11,"Ending Date   : ";
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "10",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      STRTDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE
.
          CALL      PACDATE                 * Format the start date
          MOVE      CPCDATE,DSPSTDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPSTDTE;
.
.
. ----- Keyin end date -----
.
DATE2000  MOVE      "11",CVERT
          UNPACK    STRTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE
.
          CALL      PACDATE                 * Format the end date
          MOVE      CPCDATE,DSPEDDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPEDDTE;
.
          MATCH     STRTDTE,ENDDTE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must after the start date.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
.
DATE9999  RETURN
.
.******************************************************************************
.*                            PROC0000                                        *
.*                        Process the Report                                  *
.******************************************************************************
.
.     Get Booked,Current and Discharged Patients Details only
.   
.
PROC0000  DISPLAY   *P1:24,*EL,*B,"Processing....  ";
.
          PACK      KEY16,STRTDTE,SP70
          CALL      RDSMISS3                    * read admission file 
PROC1000  CALL      RDKMISS3
          BRANCH    OVRCD,PROC2000
.
          BRANCH    ASTAT,PROC2500,PROC2500,PROC2500,PROC2500
.                           Pre     Current   Disc    On leave
.                           Adm       Adm
.
          GOTO      PROC1000
.
PROC2000  PACK      KEY8,SP70
          CALL      RSBKREC1                    * read booking file
PROC2100  CALL      RKBKREC1
          BRANCH    OVRCD,PROC9999
.
          MATCH     STRTDTE,BKREADAT
          GOTO      PROC2100 IF LESS
.
          MATCH     BKREADAT,ENDDTE            * make sure admn is in date range
          GOTO      PROC2100 IF LESS
. 
          COMPARE   ZERO,BKRESTAT               * 0 = Booked
          GOTO      PROC2100 IF NOT EQUAL
.
          STRIP     BKREADAT
          PACK      BOOKDATE,BKREADAT,DELIMIT
.
          STRIP     BKREATIM
          PACK      BOOKTIME,BKREATIM,DELIMIT
.
          MOVE      DBKREBOO,TEMPADM
          MOVE      BKREURNO,TEMPUR
          MOVE      BKREADOC,DOCCODE
          MOVE      SP10,AFUNDH
          MOVE      SP10,AFNDTB
.
          CALL      WRTE0000
          GOTO      PROC2100
.
PROC2500  MATCH     ADATE,ENDDTE              * make sure admn is in date range
          GOTO      PROC1000 IF LESS
.
          STRIP     ADATE
          PACK      BOOKDATE,ADATE,DELIMIT
.
          STRIP     ATIME
          PACK      BOOKTIME,ATIME,DELIMIT
.
          MOVE      DAADMNO,TEMPADM
          MOVE      AURNO,TEMPUR
          MOVE      ADOCTA,DOCCODE
.
          CALL      WRTE0000
          GOTO      PROC1000
.
PROC9999  RETURN
.
.------------------------------------------------------------
. Move Extract to Web Directory
.------------------------------------------------------------
.
MVEXT000  CLEAR     CMDLINE
          APPEND    "ibamrt13.us1 ",CMDLINE
          APPEND    "scancare.txt ",CMDLINE
          APPEND    STRTDTE,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    ENDDTE,CMDLINE
.
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
.
.******************************************************************************
.*                            HEAD0000                                        *
.*                     Print Scancare Header                                  *
.******************************************************************************
.
HEAD0000  CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *40,"From Date : ",DSPSTDTE," To : ",DSPEDDTE
.
          CALL      UND132
.
HEAD9999  RETURN
+
.*****************************************************************************
.*                       Print Grand Totals                                  *
.*                                                                           *
.*****************************************************************************
.
GRNT0000  CALL      HEAD0000
.
          PRINT      *N,"Total number of Records : ",TOTREC:
                     *N,*N,"** END OF REPORT **"
          DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"*** Generation Completed ***",*W2;
.
GRNT9999  RETURN
.
.
.******************************************************************************
.*                            WRTE0000                                        *
.*                Format fields and write to textfile                         *
.******************************************************************************
.
WRTE0000  CALL      PMST0000         * format fields from patient master file
          CALL      HFUN0000         * format fields from health fund file
          CALL      DOCN0000         * format fields from doctor file
          CALL      MBSD0000         * format fields from MBS file
          CALL      BOKD0000         * format fields from Booking Diagnosis file
.
          SQUEEZE   TEMPADM
          PACK      ADMISSNO,TEMPADM,DELIMIT
.
          WRITE     FILEDESC,SEQ;*+,URNUM,FNAME,LNAME,BOOKDATE,BOOKTIME:
                    ADMISSNO,HFDESC,HFCODE,ATTENDOC,ADPROVNO:
                    MBSDESC,MBSCODES,FULLDIAG:
                    MEDICNUM,PDOB,SUBURB,POSTCODE,GENDER
.
          ADD       ONE,TOTREC
.
WRTE9999  RETURN
.
.
.******************************************************************************
.*                            MBSD0000                                        *
.*            Read and format fields from Operation Theatre Details           *
.******************************************************************************
.
MBSD0000  PACK      MBSCODES,SP70
          PACK      MBSDESC,SP70,SP70,SP70,SP10
          MOVE      ZERO,EXIT
          MOVE      ZERO,COUNT
.
          PACK      KEY31,TEMPADM,SP10
          CALL      RSOPDEA2
MBSD1000  CALL      RKOPDEA2
          BRANCH    OVRCD,MBSD9000
.
          MATCH     TEMPADM,OPDAADMN
          GOTO      MBSD9000 IF NOT EQUAL 
.
          MATCH     SP9,OPDAPROV
          IF        @EQUAL
            GOTO      MBSD2000
          ENDIF
.
          MOVE      OPDAPROV,MBSCODE1          
          SQUEEZE   MBSCODE1
          MOVE      MBSCODE1,MBSCODES
.          
          PACK      KEY17,OPDAPROV,OPDADATE,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,MBSD2000
. 
          MOVE      IDESC,MBSDESC1
          STRIP     MBSDESC1
          MOVE      MBSDESC1,MBSDESC
.
MBSD2000  MATCH     SP9,OPDAPRV2
          IF        @EQUAL
            GOTO      MBSD3000
          ENDIF
.
          APPEND    SCOLON,MBSCODES
          MOVE      OPDAPRV2,MBSCODE2
          SQUEEZE   MBSCODE2
          APPEND    MBSCODE2,MBSCODES
.
          PACK      KEY17,OPDAPRV2,OPDADATE,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,MBSD3000
.
          APPEND    SCOLON,MBSDESC
          MOVE      IDESC,MBSDESC2
          STRIP     MBSDESC2
          APPEND    MBSDESC2,MBSDESC
.
MBSD3000  MATCH     SP9,OPDAPRV3
          IF        @EQUAL
            GOTO      MBSD9000
          ENDIF
.
          APPEND    SCOLON,MBSCODES
          MOVE      OPDAPRV3,MBSCODE3
          SQUEEZE   MBSCODE3
          APPEND    MBSCODE3,MBSCODES
.
          PACK      KEY17,OPDAPRV3,OPDADATE,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,MBSD9000
.
          APPEND    SCOLON,MBSDESC
          MOVE      IDESC,MBSDESC3
          STRIP     MBSDESC3
          APPEND    MBSDESC3,MBSDESC
.
MBSD9000  RESET     MBSDESC
          STRIP     MBSDESC
          PACK      MBSDESC,MBSDESC,DELIMIT
          RESET     MBSCODES
          STRIP     MBSCODES
          PACK      MBSCODES,MBSCODES,DELIMIT
.
MBSD9999  RETURN
.
.******************************************************************************
.*                            BOKD0000                                        *
.*            Read and format fields from Operation Theatre Details           *
.******************************************************************************
.
BOKD0000  MOVE       ZERO,EXIT
          PACK       FULLDIAG,SP70,SP70,SP70
.
          PACK       KEY31,TEMPADM,SP10              * Booking No
          CALL       RSOPDEA2
BOKD1000  CALL       RKOPDEA2 
          BRANCH     OVRCD,BOKD9000
.
          MATCH      OPDAADMN,TEMPADM
          GOTO       BOKD9000 IF NOT EQUAL
.
          MATCH      SP70,OPDADATE
          IF         !@EQUAL & !@EOS
            STRIP      OPDADATE
            PACK       BOOKDATE,OPDADATE,DELIMIT
.           
            STRIP      OPDAEXPS
            PACK       BOOKTIME,OPDAEXPS,COLON,ZERO,ZERO,DELIMIT
          ENDIF
.
          MATCH     SP70,OPDADES1
          IF        @EQUAL
            GOTO      BOKD2000
          ENDIF
.
          MOVE      OPDADES1,FULLDIAG1
          STRIP     FULLDIAG1
          MOVE      FULLDIAG1,FULLDIAG
.
BOKD2000  MATCH     SP70,OPDADES2
          IF        @EQUAL
            GOTO      BOKD3000
          ENDIF
.
          APPEND    SP1,FULLDIAG
          MOVE      OPDADES2,FULLDIAG2
          STRIP     FULLDIAG2
          APPEND    FULLDIAG2,FULLDIAG
.
BOKD3000  MATCH     SP70,OPDADES3
          IF        @EQUAL
            GOTO      BOKD9000
          ENDIF
.
          APPEND    SP1,FULLDIAG
          MOVE      OPDADES3,FULLDIAG3
          STRIP     FULLDIAG3
          APPEND    FULLDIAG3,FULLDIAG
.
BOKD9000  RESET      FULLDIAG
          STRIP      FULLDIAG
          PACK       FULLDIAG,FULLDIAG,DELIMIT
.
BOKD9999  RETURN
.
.******************************************************************************
.*                            DOCN0000                                        *
.*                 Read and format fields from doctor file                   *
.******************************************************************************
.
DOCN0000  MOVE      ZERO,EXIT
          MOVE      SP70,ATTENDOC
          MOVE      SP10,ADPROVNO
.
          PACK      KEY6,DOCCODE,SP6
          CALL      RDDOCT1 
          BRANCH    OVRCD,DOCN9100
.
          MOVE      ONE,PACFRMT        * title name surname of doctor
          MOVE      DSNAME,PACSNAME    * use pacdate to fix doctors name
          MOVE      DGNAME,PACGNAME    * get first letter in given name
          MOVE      DTITL,PACTITLE
          CALL      PACNAME            * pack up doctors name
.
          STRIP     PACFNAME
          PACK      ATTENDOC,PACFNAME,DELIMIT
.
          STRIP     DPROV
          PACK      ADPROVNO,DPROV,DELIMIT
.
          GOTO      DOCN9999
.
DOCN9100  PACK      ATTENDOC,DELIMIT
          PACK      ADPROVNO,DELIMIT
          MOVE      ONE,EXIT
.          
DOCN9999  RETURN
.
.******************************************************************************
.*                            PMST0000                                        *
.*                 Read and format fields from patient master file            *
.******************************************************************************
.
PMST0000  MOVE      ZERO,EXIT
.
          PACK      KEY8,TEMPUR,SP70
          CALL      RDMAST1                     * read patient master file
          BRANCH    OVRCD,PMST9100
.
          MOVE      PURNO,URNUM
          SQUEEZE   URNUM
          PACK      URNUM,URNUM,DELIMIT
.
          STRIP     PGNAME
          PACK      FNAME,PGNAME,DELIMIT
.
          STRIP     PSNAME
          PACK      LNAME,PSNAME,DELIMIT
.
          STRIP     PMEDI
          PACK      MEDICNUM,PMEDI,DELIMIT
.
          STRIP     PBDATE
          PACK      PDOB,PBDATE,DELIMIT
.
          STRIP     PSUBRB
          PACK      SUBURB,PSUBRB,DELIMIT
.
          STRIP     PPOST
          PACK      POSTCODE,PPOST,DELIMIT
.
          STRIP     PSEX
          PACK      GENDER,PSEX,DELIMIT
.
          GOTO      PMST9999
.
PMST9100  MOVE      ONE,EXIT 
.
PMST9999  RETURN
.
.******************************************************************************
.*                           HFUND0000                                        *
.*                 Read and format fields from health fund master file        *
.******************************************************************************
.
HFUN0000  MOVE      ZERO,EXIT
          PACK      HFDESC,SP30,SP20
          MOVE      SP10,HFCODE
.
          PACK      KEY14,AFUNDH,AFNDTB
          CALL      RDFUND1                     * read health fund file
          BRANCH    OVRCD,HFUN9100
.
          STRIP     HFNAME
          PACK      HFDESC,HFNAME,DELIMIT
.
          STRIP     HCODE
          PACK      HFCODE,HCODE,DELIMIT
.
          GOTO      HFUN9999
.
HFUN9100  PACK      HFDESC,DELIMIT
          PACK      HFCODE,DELIMIT
          MOVE      ONE,EXIT
.
HFUN9999  RETURN
.
.-----------------------------------------------------------------------------
.         I/O's Includes
.-----------------------------------------------------------------------------
.
          INC       STD001IO
          INC       PATITMRD
.
          INC       OPRDEAIO/INC
          INC       BOKRC1IO/INC
          INC       PATDO1IO/INC
          INC       PATFN1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATMMBIO/INC
          INC       PATITMIO/INC
 

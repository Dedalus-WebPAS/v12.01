.******************************************************************************
.* System         : Waiting List                                              *
.* Program        : IBAWAT77.PBL                                              *
.* Name           : WA Additional Waiting List Extract                        *
.******************************************************************************
.* Date           : 06/01/2011                                                *
.* Author         : Ebon Clements                                             *
.* Function       : Extract waiting list patients that have had a booking,    *
.*                  a booking cancelled, an admission, a cancelled admission, *
.*                  scheduled admission date change, return to WL or removal  *
.* Modifications  :                                                           *
. *****************************************************************************
.* V11.03.02 08/05/2023  Jacob Jackson    TSK 0929614                         *
.*           Fix problem where XXXXGEND would add '0' value to report         *
.* V11.03.01 04/05/2023  Jacob Jackson    TSK 0929614                         *
.*           Change "Sex" field to be "Sex At Birth" XXXXSEAB and add new     *
.*           gender field at pos 380 XXXXGEND                                 *
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.* V10.08.01  11/11/2016 Ken Bell           TASK 0828032                      *
.*            Changed XXXXISDY to use THCSCOD instead of TCIND1               *
.* V10.03.15  25/02/2013 Ania P             CAR 281021                        *
.*            Removed unnecessary modification of constants.                  *
.* V10.03.14  01/08/2012 Ebon Clements      CAR 265399                        *
.*            Fixed Revert to W/L Indicator                                   *
.*            Populate Reason for cancellation                                *
.*            Moved revert to W/L test for XXXXPACR io WRIT0000               *
.* V10.03.13  31/07/2012 Sandra Barcham     CAR 265399                        *
.*            Fixed XXXXEVTYP for cancelled bookings                          *
.* V10.03.12  30/07/2012 Ebon Clements      CAR 265399                        *
.*            Fixed revert to WL count - PROC1600                             *
.* V10.03.11  23/07/2012 Steve Armstrong    CAR 265399                        *
.*            Fixed Cancelled Bookings                                        *
.* V10.03.10  26/06/2012 Steve Armstrong    CAR 265399                        *
.*            Fixed XXXXEVTYP                                                 *
.* V10.03.09  28/03/2012 Ebon Clements      CAR 260555                        *
.*            Fixed cat code (WD) for cancellation reason description         *
.*            on update type 08 on watchaaf - scheduled admission change      *
.* V10.03.08  22/03/2012 Ebon Clements      CAR 260555                        *
.*            Fixed reason for cancellation, admission date/time (PACS) and   *
.*            previous scheduled admission date                               *
.* V10.03.07  19/03/2012 Ebon Clements      CAR 260555                        *
.*            Fixed clinician responsible for care                            *
.*            Fixed UPTYPE08 value                                            *
.* V10.03.06  08/03/2012 Ebon Clements      CAR 260555                        *
.*            Changed to select records from watchaaf and wathisaf            *
.* V10.03.05  27/02/2012 Ebon Clements      CAR 260674                        *
.*            Fixed not ready for care days calculation - NRCDAY00            *
.* V10.03.04  24/02/2012 Ebon Clements      CAR 260674                        *
.*            Fixed medical authorisation approved XXXXMEDA                   *
.* V10.03.03  15/02/2012 Ebon Clements      CAR 244724                        *
.*            Fixed time format of XXXXATIM and XXXXATEV                      *
.* V10.03.02  08/02/2012 Jill Parkinson     CAR 259568                        *
.*            Changed to left justify and NOT zero fill XXXXCLID              *
.* V10.03.01  24/01/2011 Ebon Clements      CAR 244724                        *
.*            Added WL type to extract file                                   *
.* V10.03.00  13/01/2011 Ebon Clements      CAR 244724                        *
.*            Created extract                                                 *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATWR1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PMSWAEFD/INC
          INC       WATCATFD/INC
          INC       WATCHAFD/INC
          INC       WATHISFD/INC
          INC       WATPROFD/INC
          INC       WATSUSFD/INC
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
          INC       WEBSECFD/INC
.
. wat77tXX.txt - Where xx is the port number
.
EXTRFILE  FILE      ASCII, FIXED=380
.
.Name     Type    Length    Start  Description                    Field Number
.-------  ----    ------    -----  -----------                    ------------
XXXXEVTY  DIM     4         1      Event Type                         (1)
XXXXTOPR  DIM     2         5      TOPAS Reason for Deferral (Blank)  (2)
XXXXPACR  DIM     1         7      PAC Revert to WL Indicator         (3)
XXXXACCN  DIM     20        8      Account Number                     (4)
XXXXWLCT  DIM     1         28     Wait List Category                 (5)
XXXXLISD  DIM     8         29     Listing Date (ddmmccyy)            (6)
XXXXESTB  DIM     4         37     Establishment                      (7)
XXXXCLID  DIM     10        41     Client Identifier                  (8)
XXXXSNAM  DIM     30        51     Surname                            (9)
XXXXGNAM  DIM     30        81     First Given Name                   (10)
XXXXSGNM  DIM     30        111    Second Given Name                  (11)
XXXXRSAD  DIM     50        141    Residential Address                (12)
XXXXRSPC  DIM     6         191    Residential Address Post Code      (13)
XXXXRSSB  DIM     30        197    Residential Address Suburb         (14)
XXXXBDAT  DIM     8         227    Date of Birth (ddmmccyy)           (15)
XXXXSEAB  DIM     1         235    Sex At Birth                       (16)
XXXXISDY  DIM     1         236    Intended Same Day Stay             (17)
XXXXINSU  DIM     1         237    Insurance Status                   (18)
XXXXPAYC  DIM     2         238    Payment Classification             (19)
XXXXLSTA  DIM     1         240    listing Status                     (20)
XXXXCLNU  DIM     1         241    Clinical Urgency                   (21)
XXXXCLRC  DIM     13        242    Clinician Responsible for Care     (22)
XXXXSPEC  DIM     3         255    Specialty on WL                    (23)
XXXXPPRO  DIM     10        258    Principal Procedure Code           (24)
XXXXPDIA  DIM     10        268    Principal Diagnosis Code           (25)
XXXXPSAD  DIM     8         278    Previous Scheduled Adm Date (ddmmccyy) (26)
XXXXNSAD  DIM     8         286    New Scheduled Adm Date (ddmmccyy)      (27)
XXXXADAT  DIM     8         294    Admission Date  (ddmmccyy)             (28)
XXXXATIM  DIM     4         302    Admission Time (hhmm)              (29)
XXXXADEV  DIM     8         306    Actual Date of Event (ddmmccyy)    (30)
XXXXATEV  DIM     4         314    Actual Time of Event (hhmm)        (31)
XXXXTNRC  DIM     3         318    Time Not Ready For Care            (32)
XXXXCDAT  DIM     8         321    Census Date (ddmmccyy)             (33)
XXXXRSRC  DIM     2         329    Referral Source                    (34)
XXXXEVDL  DIM     6         331    Event Days on List                 (35)
XXXXEDU1  DIM     4         337    Event Days Urgency Cat 1           (36)
XXXXEDU2  DIM     4         341    Event Days Urgency Cat 2           (37)
XXXXEDU3  DIM     4         345    Event Days Urgency Cat 3           (38)
XXXXREVR  DIM     1         349    Reversal Recod (blank)             (39)
XXXXAIWH  DIM     1         350    AIHW Reportable/Non Reportable     (40)
XXXXMEDA  DIM     1         351    Medical Authorisation Approved     (41)
XXXXREAC  DIM     3         352    Reason For Cancellation            (42)
XXXXRECD  DIM     20        355    Reason For Cancellation Description (43)
XXXXBTYP  DIM     3         375    Bed Type                            (44)
XXXXWLTY  DIM     2         378    Wait List Type                      (45)
XXXXGEND  DIM     1         380    Gender                              (46)
.
. End of Record             380  
.
. ----- Program Variables -----
.
ADHOCEXT  FORM      1
CMDLINE   DIM       127
CALCTIME  FORM      6
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CURRPRTY  DIM       3
DIM2A     DIM       2
DIM2B     DIM       2
ENDDDATE  DIM       8
FILENAME  DIM       8
FIRSTDAT  DIM       8        (first date CCYYMMDD format)
FIRSTIME  DIM       4        (first time HHMM format)
FIRSTHR   DIM       2
FIRSTMIN  DIM       2
FORM3     FORM      3
FORM6     FORM      6
HOSPCODE  DIM       3
HOUR      DIM       2
HOUR1     FORM      6
HOUR2     FORM      6
LASTDATE  DIM       8        (last date CCYYMMDD format)
LASTTIME  DIM       4        (last time HHMM format)
LASTHOUR  DIM       2
LASTMIN   DIM       2
LISTPRTY  DIM       1
LPRDAYS   FORM      4
LSTDAYFR  DIM       8
LSTDAYTO  DIM       8
LSTDAYS   FORM      5
MIN       DIM       2
MINUTE1   FORM      3        
MINUTE2   FORM      3        
NRCDAYS   FORM      5
NRCDAYFR  DIM       8
NRCDAYTO  DIM       8
PRTODATE  DIM       10
PRFRDATE  DIM       10
PRNTHOSP  DIM       50
PRTYFDAT  DIM       8
PRTYTDAT  DIM       8
SAVEURNO  DIM       8
SAVEPCNT  DIM       2
SAVEPROC  DIM       9
SAVKEY37  DIM       37
STARTDAT  DIM       8
TOTALREC  FORM      8
UPTYPE01  INIT      "01"
UPTYPE03  INIT      "03"
UPTYPE05  INIT      "05"
UPTYPE08  INIT      "08"
UPTYPE12  INIT      "12"
UPTYPE13  INIT      "13"
UPTYPE16  INIT      "16"
ANSLI     INIT      "i"
WEBUSRID  DIM       10
.
.
. ----- Constant Variables -----
.
FNAME     INIT      "wat77t" 
.
PRGID     INIT      "IBAWAT77"
PRGNAM    INIT      "WA Additional WL Extraction"
VERSION   INIT      "V12.01.00"
.
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.****************************************************************************** 
.
ML0000    CALL      INIT0000                * Initialise variables & open files 
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000
          BRANCH    EXIT,ML9999
.
ML2000    CALL      KADH0000                * Keyin Adhoc extraction
          BRANCH    EXIT,ML1000
.
          CALL      KHOS0000                * Keyin hospital id  
          BRANCH    EXIT,ML1000
.
          CALL      KDAT0000                * Keyin the end date
          BRANCH    EXIT,ML1000
.
          CALL      KUSR0000                * Keyin web user id
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML2000,ML1000
.
ML4000    CALL      CREX0000                * Create extract text file
.
          CALL      HEAD0000                * Print Report Header
.
          CALL      CLRV0000                * Clear vars
.
          CALL      PROC0000                * Process wl change audit records
.
          CALL      EDAT0000                * Update extract run dates
.
          CALL      TOTL0000                * Print totals
.
          CALL      MVEX0000                * Move extract for web index
.
          GOTO      ML1000
.
ML9999    CHAIN     PGM
.
.******************************************************************************
.* Initialise Variable & Open Files                                           *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmswaeaf";
          OPEN      PMSWAEA1,"pmswaeaf"
.
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA1,"watcataf"
          OPEN      WATCATA2,"watcataf"
.
          DISPLAY   *P64:24,"watchaaf";
          OPEN      WATCHAA1,"watchaaf"
          OPEN      WATCHAA2,"watchaaf"
.
          DISPLAY   *P64:24,"wathisaf";
          OPEN      WATHISA3,"wathisaf"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P64:24,"wattx1af";
          OPEN      WATTX1A1,"wattx1af"
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          PACK      FILENAME,FNAME,PORT
          REP       " 0",FILENAME
.
          CALL      IBACLOCK
.
INIT9999  RETURN
.
.******************************************************************************
.* Select Option                                                              *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run":
                    *P1:8,"Select Option : ";
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
.
.******************************************************************************
.* Input date range                                                           *
.******************************************************************************
KDAT0000  DISPLAY   *P1:14,*EL,"Start Date : ":
                    *P1:15,*EL,"End   Date : ";
.
          MOVE      "14",CVERT
          MOVE      "15",CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9500
          PACK      STARTDAT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STARTDAT
.
          MOVE      ZERO,EXIT
.
.         set up input requirements for KEYDATE
.
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      "15",CCOL
          MOVE      "15",CVERT
.
.         input ending date
.
KDAT1000  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9500
.
          PACK      ENDDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDDATE
.
          MOVE      ZERO,EXIT
.
          MATCH     ENDDDATE,STARTDAT
          GOTO      KDAT9999 IF EQUAL
          GOTO      KDAT9999 IF LESS
.
          UNPACK    STARTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:24,*EL,*B,*V2LON,"End Date must be greater than ":
                    "Start date ",CPCDATE,".  ";
          CALL      HITENTER
          GOTO      KDAT0000
.
KDAT9500  MOVE      ONE,EXIT
.
KDAT9999  RETURN
.
.******************************************************************************
.* Process wl change audit records                                            *
.******************************************************************************
PROC0000  MOVE      ZERO,TOTALREC
.
          PACK      KEY37,STARTDAT,SP70
          CALL      RSWTCHA2
PROC1000  CALL      RKWTCHA2
          BRANCH    OVRCD,PROC9999
.
          MATCH     WTCHDATE,ENDDDATE          * Transaction after
          GOTO      PROC9999 IF LESS           * extract end date
.
          MATCH     WTCHUPTY,UPTYPE08          * Scheduled adm date
          GOTO      PROC2000 IF EQUAL
.
          MATCH     WTCHUPTY,UPTYPE12          * Cancelled adm
          GOTO      PROC2000 IF EQUAL
.
          MATCH     WTCHUPTY,UPTYPE16          * Cancelled booking
          GOTO      PROC2000 IF EQUAL
.
          GOTO      PROC1000
.
PROC2000  PACK      KEY19,WTCHURNO,WTCHPROC,WTCHPCNT,SP70
          CALL      RDTREA1 
          BRANCH    OVRCD,PROC1000
.
          MATCH     HOSPCODE,WTWMIHSP          * Hospital filter
          GOTO      PROC1000 IF NOT EQUAL

          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RDWTTX11
          BRANCH    OVRCD,PROC1000
.
          CALL      CLRV0000                * Clear extract fields
.
          CALL      PMIF0000                * Set PMI fields
.
          CALL      WATF0000                * Set WL fields
.
          CALL      WRIT0000                * Write to extract flat file
.
          GOTO      PROC1000
.
PROC9999  RETURN
.
.******************************************************************************
. Create extract text file           
.******************************************************************************
CREX0000  CLOSE     EXTRFILE,DELETE
.
          PREP      EXTRFILE,FILENAME
.
CREX9999  RETURN
.
.******************************************************************************
. Move Extract to Web Directory
.******************************************************************************
MVEX0000  PACK      KEY3,HOSPCODE,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            CALL      CLPATHSP
          ENDIF
.
          CLEAR     CMDLINE
          APPEND    "ibawat77.us1 '",CMDLINE
          APPEND    FILENAME,CMDLINE          * Report .txt file
.
          APPEND    "' '",CMDLINE
          APPEND    PTHSHOSP,CMDLINE          * Hospital Id
.
          APPEND    "' '",CMDLINE
.
          MATCH     SP70,PTHSHSID
          IF        @EQUAL
            PACK      KEY4,PTHSHOSP,SP70
          ELSE
            MOVE      PTHSHSID,KEY4           * Health service identifier
          ENDIF
          APPEND    KEY4,CMDLINE
.
          APPEND    "' '",CMDLINE
          APPEND    PRFRDATE,CMDLINE          * From Date
.
          APPEND    "' '",CMDLINE
          APPEND    PRTODATE,CMDLINE          * To Date
.
          APPEND    "' '",CMDLINE
          APPEND    WEBUSRID,CMDLINE          * User Id
.
          APPEND    "'",CMDLINE
.
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
MVEX9999  RETURN
.
.******************************************************************************
.* Clear the extract variables                                                *
.******************************************************************************
CLRV0000  MOVE      SP70,XXXXEVTY
          MOVE      SP70,XXXXTOPR
          MOVE      SP70,XXXXPACR
          MOVE      SP70,XXXXACCN
          MOVE      SP70,XXXXWLCT
          MOVE      SP70,XXXXLISD
          MOVE      SP70,XXXXESTB
          MOVE      SP70,XXXXCLID
          MOVE      SP70,XXXXSNAM
          MOVE      SP70,XXXXGNAM
          MOVE      SP70,XXXXSGNM
          MOVE      SP70,XXXXRSAD
          MOVE      SP70,XXXXRSPC
          MOVE      SP70,XXXXRSSB
          MOVE      SP70,XXXXBDAT
          MOVE      SP70,XXXXSEAB
          MOVE      SP70,XXXXISDY
          MOVE      SP70,XXXXINSU
          MOVE      SP70,XXXXPAYC
          MOVE      SP70,XXXXLSTA
          MOVE      SP70,XXXXCLNU
          MOVE      SP70,XXXXCLRC
          MOVE      SP70,XXXXSPEC
          MOVE      SP70,XXXXPPRO
          MOVE      SP70,XXXXPDIA
          MOVE      SP70,XXXXPSAD
          MOVE      SP70,XXXXNSAD
          MOVE      SP70,XXXXADAT
          MOVE      SP70,XXXXATIM
          MOVE      SP70,XXXXADEV
          MOVE      SP70,XXXXATEV
          MOVE      SP70,XXXXTNRC
          MOVE      SP70,XXXXCDAT
          MOVE      SP70,XXXXRSRC
          MOVE      SP70,XXXXEVDL
          MOVE      SP70,XXXXEDU1
          MOVE      SP70,XXXXEDU2
          MOVE      SP70,XXXXEDU3
          MOVE      SP70,XXXXREVR
          MOVE      SP70,XXXXAIWH
          MOVE      SP70,XXXXMEDA
          MOVE      SP70,XXXXREAC
          MOVE      SP70,XXXXRECD
          MOVE      SP70,XXXXBTYP
          MOVE      SP70,XXXXWLTY
          MOVE      SP70,XXXXGEND
.
CLRV9999  RETURN
.
.******************************************************************************
.*                                  KHOS0000                                  *
.*                      Keyin hospital ID                                     *
.******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
.
          MOVE       SP70,HOSPCODE
          MOVE       SP70,PTHSHOSP
          DISPLAY    *P1:12,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       TEN2,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ONE,CKYIMAND           * Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS9000,KHOS9000
.
          PACK       HOSPCODE,KEY3,SP70          * Hospital Code
          PACK       PRNTHOSP,PTHSNAME,SP70
.
KHOS3000  DISPLAY   *P20:12,*V2LON,PTHSNAME;
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
.
.******************************************************************************
.*  Set PMI based extract fields                                              *
.******************************************************************************
PMIF0000  PACK      KEY8,WMURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PMIF9999
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PMIF9999
.
          PACK      XXXXCLID,PURNO,SP70                              * (8)
          LJUSTIFY  XXXXCLID
.
          PACK      XXXXSNAM,PSNAME,SP70                             * (9)
          PACK      XXXXGNAM,PMPXFNAM,SP70                           * (10)
          PACK      XXXXSGNM,PMPXSNAM,SP70                           * (11)
.
          STRIP     PADD1
          PACK      XXXXRSAD,PADD1,SP1,PADD2,SP70                    * (12)
          RESET     PADD1
.
          PACK      XXXXRSPC,PPOST,SP70                              * (13)
.
          PACK      XXXXRSSB,PSUBRB,SP70                             * (14)
.
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXBDAT,XDAY,XMON,XCENT,XYEAR,SP70              * (15)
.
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          IF        DSEXNO>4
            PACK      XXXXSEAB,FOUR,SP70                             * (16)
          ELSE
            PACK      XXXXSEAB,DSEXNO,SP70                           * (16)
          ENDIF
.
          MATCH     SP70,PFUNDH
          IF        @EQUAL
            PACK      XXXXINSU,ANSN,SP70                             * (18)
          ELSE
            PACK      XXXXINSU,ANSY,SP70
          ENDIF
.
          PACK      KEY5,ANSG,ANSLI,PMPXUCC4,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      XXXXGEND,THCSCOD,SP70
          ENDIF
.
PMIF9999  RETURN
.
.******************************************************************************
.*  Set WL based extract fields                                              *
.******************************************************************************
WATF0000  MATCH     UPTYPE03,WTCHUPTY
          IF        @EQUAL
            MOVE      "RES ",XXXXEVTY                              * (1)
          ENDIF    
.
          MATCH     UPTYPE05,WTCHUPTY
          IF        @EQUAL
            MOVE      "DEF ",XXXXEVTY                              * (1)
          ENDIF    
.
          MATCH     UPTYPE08,WTCHUPTY
          IF        @EQUAL
            MOVE      "RES ",XXXXEVTY                              * (1)
          ENDIF    
.
          MATCH     UPTYPE12,WTCHUPTY
          IF        @EQUAL
            MOVE      "PAC ",XXXXEVTY                              * (1)
          ENDIF    
.
          MATCH     UPTYPE13,WTCHUPTY
          IF        @EQUAL
            MOVE      "PAC ",XXXXEVTY                              * (1)
          ENDIF    
.
          MATCH     UPTYPE16,WTCHUPTY
          IF        @EQUAL
            MOVE      "DEF ",XXXXEVTY                              * (1)
          ENDIF    
.
          MOVE      ANSN,XXXXPACR                                  * (3)
.
          MATCH     UPTYPE12,WTCHUPTY      
          GOTO      WATF2000 IF NOT EQUAL
.
          PACK      SAVKEY37,WTCHDATE,WTCHTIME,WTCHUPTY,WTCHURNO,WTCHPROC:
                            WTCHPCNT,SP70      * Save key for reposition 
.                                              * loop at PROC1000
          MOVE      WTCHURNO,SAVEURNO
          MOVE      WTCHPROC,SAVEPROC
          MOVE      WTCHPCNT,SAVEPCNT
.
          PACK      KEY35,WTCHURNO,WTCHPROC,WTCHPCNT,WTCHDATE,WTCHTIME,SP70
          CALL      RDWTCHA1
          BRANCH    OVRCD,WATF1900

          CALL      RKWTCHA1
          BRANCH    OVRCD,WATF1900
.
          MATCH     SAVEURNO,WTCHURNO            * Correct U/R
          GOTO      WATF1900 IF NOT EQUAL
.
          MATCH     SAVEPROC,WTCHPROC            * Correct procedure code
          GOTO      WATF1900 IF NOT EQUAL
.
          MATCH     SAVEPCNT,WTCHPCNT            * Correct count
          GOTO      WATF1900 IF NOT EQUAL
.
          MATCH     "16",WTCHUPTY              * Booking Cancellation
          GOTO      WATF1900 IF NOT EQUAL
.
          MOVE      ANSY,XXXXPACR              * Revert to WL        * (3)
.
WATF1900  PACK      KEY37,SAVKEY37             * Re position back on original
          CALL      RDWTCHA2                   * cancelled admission record
.                                              * for loop at PROC1000
WATF2000  PACK      XXXXACCN,WTWMECNT,SP70                           * (4)
          RJUSTIFY  XXXXACCN
          REP       " 0",XXXXACCN  
.
          MATCH     SP70,WTTXBOTY
          IF        !@EQUAL
            PACK      KEY5,ANSB,ANSK,WTTXBOTY,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXWLCT,TCINDC6,SP70                        * (5)
            ENDIF
          ENDIF
.
          UNPACK    WMDATE1,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXLISD,XDAY,XMON,XCENT,XYEAR,SP70              * (6)
.
          PACK      KEY3,WTWMIHSP,SP70           * Read hospital record
          CALL      RDPTHSP1
          IF        OVRCD<>1
            MOVE      PTHSHSID,KEY4
            PACK      XXXXESTB,KEY4,SP70                             * (7)
          ENDIF
.
          MATCH     SP70,WTTXINTD
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSI,WTTXINTD,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXISDY,THCSCOD,SP70                        * (17)
            ENDIF
          ENDIF
.
          MATCH     SP70,WTTXCTYP
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,WTTXCTYP,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXPAYC,SP1,TCINDC14,SP70                   * (19)
            ENDIF
          ENDIF
.
          MATCH     SP70,WTTXPLST
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSL,WTTXPLST,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK       XXXXLSTA,THCSCOD,SP70                       * (20)
            ENDIF
          ENDIF
.
          MATCH     SP70,WMPTY
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WMPTY,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXCLNU,THCSCOD,SP70                        * (21)
            ENDIF
          ENDIF
.
          MATCH     SP70,WMDOCTOR
          IF        !@EQUAL
            PACK      KEY6,WMDOCTOR,SP70
            CALL      RDDOCT1
            IF        OVRCD<>1
              PACK      XXXXCLRC,PTDOREGN,SP70                      * (22)
            ENDIF
          ENDIF
.
          MATCH     SP70,WMUNIT
          IF        !@EQUAL
            PACK      KEY5,ANSW,ANSU,WMUNIT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              UNPACK    THCSCOD,KEY2,XXXXWLTY
              RJUSTIFY  XXXXWLTY
              REP       " 0",XXXXWLTY                                * (45)
.
              PACK      XXXXSPEC,TWO,KEY2                            * (23)
              RJUSTIFY  XXXXSPEC
            ENDIF
          ENDIF
.
          PACK      XXXXPPRO,WMCODE,SP70                             * (24)
.
          MATCH     UPTYPE08,WTCHUPTY
          IF        @EQUAL
            UNPACK    SP70,XCENT,XYEAR,XMON,XDAY
            UNPACK    WTCHOVAL,XCENT,XYEAR,XMON,XDAY
            PACK      XXXXPSAD,XDAY,XMON,XCENT,XYEAR,SP70          * (26)
.
            UNPACK    WTCHCVAL,XCENT,XYEAR,XMON,XDAY
            PACK      XXXXNSAD,XDAY,XMON,XCENT,XYEAR,SP70          * (27)
          ENDIF
          MATCH     UPTYPE16,WTCHUPTY
          IF        @EQUAL
            UNPACK    SP70,XCENT,XYEAR,XMON,XDAY
            UNPACK    WTCHOVAL,XCENT,XYEAR,XMON,XDAY
            PACK      XXXXPSAD,XDAY,XMON,XCENT,XYEAR,SP70          * (26)
          ENDIF
.
          MATCH     UPTYPE12,WTCHUPTY
          IF        @EQUAL
            UNPACK     SP70,XCENT,XYEAR,XMON,XDAY:
                            DIM2A,DIM1,DIM2B
            UNPACK    WTCHOVAL,XCENT,XYEAR,XMON,XDAY:
                               DIM2A,DIM1,DIM2B
            PACK      XXXXADAT,XDAY,XMON,XCENT,XYEAR,SP70          * (28)
              PACK      XXXXATIM,DIM2A,DIM2B,SP70                    * (29)
          ENDIF
.
          UNPACK    WTCHDATE,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXADEV,XDAY,XMON,XCENT,XYEAR,SP70            * (30)
          UNPACK    SP70,DIM2A,DIM2B,DIM1
          UNPACK    WTCHTIME,DIM2A,DIM1,DIM2B
          PACK      XXXXATEV,DIM2A,DIM2B,SP70                      * (31)
.
          MOVE      WMDATE1,NRCDAYFR   * NRFC from date
          MOVE      ENDDDATE,NRCDAYTO  * NRFC to date
          CALL      NRCDAY00           * Calc not ready for care days
          MOVE      ZERO,FORM3
          MOVE      NRCDAYS,FORM3
          PACK      XXXXTNRC,FORM3,SP70                              * (32)
.
          CALL      IBACLOCK
          PACK      XXXXCDAT,CDD,CMM,CCC,CYY,SP70                    * (33)
          REP       " 0",XXXXCDAT
.
          MOVE      WMDATE1,LSTDAYFR   * List days from date
          MOVE      ENDDDATE,LSTDAYTO  * List days to date
          CALL      LSTDAY00           * Calc days on list
          MOVE      ZERO,FORM6
          ASSIGN    LSTDAYS-NRCDAYS,FORM6
          PACK      XXXXEVDL,FORM6,SP70                              * (35)
.
          MOVE      "1",LISTPRTY
          CALL      PRTDAY00
          PACK      XXXXEDU1,LPRDAYS,SP70                            * (36)
.
          MOVE      "2",LISTPRTY
          CALL      PRTDAY00
          PACK      XXXXEDU2,LPRDAYS,SP70                            * (37)
.
          MOVE      "3",LISTPRTY
          CALL      PRTDAY00
          PACK      XXXXEDU3,LPRDAYS,SP70                            * (38)
.
          PACK      KEY9,WMCODE,SP70
          CALL      RDPROA1
          IF        OVRCD<>1
            MOVE      ZERO,FORM6
            SQUEEZE   WPRHAT
            MOVE      WPRHAT,FORM6
            RESET     WPRHAT
            IF        FORM6>0
              PACK      XXXXAIWH,ONE,SP70                            * (40)
            ELSE
              PACK      XXXXAIWH,ZERO,SP70                           * (40)
            ENDIF
          ENDIF
.
          MATCH     "1",WTTXMAUT
          IF        @EQUAL
            PACK      XXXXMEDA,ANSY,SP70                             * (41)
          ELSE
            MATCH     "0",WTTXMAUT
            IF        @EQUAL
              PACK      XXXXMEDA,ANSN,SP70                           * (41)
            ENDIF
          ENDIF
.
          MATCH     UPTYPE08,WTCHUPTY
          IF        @EQUAL
            PACK      XXXXREAC,WTCHREAS,SP70                       * (42)
          ENDIF
.
          MATCH     UPTYPE12,WTCHUPTY
          IF        @EQUAL
            PACK      XXXXREAC,WTCHREAS,SP70                       * (42)
          ENDIF
.
          MATCH     UPTYPE13,WTCHUPTY
          IF        @EQUAL
            PACK      XXXXREAC,WTCHREAS,SP70                       * (42)
          ENDIF
.
          MATCH     UPTYPE16,WTCHUPTY
          IF        @EQUAL
            PACK      XXXXREAC,WTCHREAS,SP70                       * (42)
          ENDIF
.
          MATCH     SP70,XXXXREAC
          IF        !@EQUAL
            MATCH     UPTYPE08,WTCHUPTY
            IF        @EQUAL
              PACK       KEY5,ANSW,ANSD,XXXXREAC,SP70
            ELSE
              MATCH      UPTYPE16,WTCHUPTY
              IF         @EQUAL
                PACK       KEY5,ANSB,ANSC,XXXXREAC,SP70
              ELSE
                PACK       KEY5,ANSP,ANSC,XXXXREAC,SP70
              ENDIF
            ENDIF
            CALL       RDCODE1
            IF         OVRCD<>1
              PACK       XXXXRECD,TDESC,SP70                       * (43)
            ENDIF
          ENDIF
.
WATF3000  MATCH     SP70,WTTXWARD
          GOTO      WATF9999 IF EQUAL
.
          PACK      KEY6,WTTXWARD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,WATF9999
.
          MATCH     SP70,WEFRBT
          GOTO      WATF9999 IF EQUAL
.
          PACK      KEY5,ANSB,ANST,WEFRBT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,WATF9999
.
          PACK      XXXXBTYP,THCSCOD,SP70                            * (44)
.
WATF9999  RETURN
.
.******************************************************************************
.* Write the extract variables                                                *
.******************************************************************************
WRIT0000  WRITE     EXTRFILE,SEQ;XXXXEVTY,XXXXTOPR,XXXXPACR,XXXXACCN,XXXXWLCT:
                         XXXXLISD,XXXXESTB,XXXXCLID,XXXXSNAM,XXXXGNAM:     * 10 
                         XXXXSGNM,XXXXRSAD,XXXXRSPC,XXXXRSSB,XXXXBDAT:
                         XXXXSEAB,XXXXISDY,XXXXINSU,XXXXPAYC,XXXXLSTA:     * 20
                         XXXXCLNU,XXXXCLRC,XXXXSPEC,XXXXPPRO,XXXXPDIA:
                         XXXXPSAD,XXXXNSAD,XXXXADAT,XXXXATIM,XXXXADEV:     * 30
                         XXXXATEV,XXXXTNRC,XXXXCDAT,XXXXRSRC,XXXXEVDL:
                         XXXXEDU1,XXXXEDU2,XXXXEDU3,XXXXREVR,XXXXAIWH:     * 40
                         XXXXMEDA,XXXXREAC,XXXXRECD,XXXXBTYP,XXXXWLTY:     
                         XXXXGEND
.
          ADD       ONE,TOTALREC
.
WRIT9999  RETURN
.
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD80
.
          UNPACK    STARTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE         * formatted start date
.
          UNPACK    ENDDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted start date

          PRINT     *20,"Date From : ",PRFRDATE," to ",PRTODATE
          PRINT     *20,"Hospital : ",PRNTHOSP
.
          IF        ADHOCEXT=1
            MOVE      "Yes",KEY3
          ELSE
            MOVE      "No ",KEY3
          ENDIF
.
          PRINT     *20,"Adhoc Extraction : ",KEY3
          PRINT     *20,"User Id : ",WEBUSRID
.
          CALL      UND80
.
HEAD9999  RETURN
.
.*********************************************************************
. Print totals
.*********************************************************************
TOTL0000  PRINT     *20,"Total Records Extracted : ",TOTALREC
.
          CALL      UND80
.
TOTL9999  RETURN
.
.------------------------------------------------------------
. Calculate Days Not Ready for Care for a date range
. From date NRCDAYFR
. To date NRCDAYTO
.------------------------------------------------------------
NRCDAY00  MOVE      ZERO,NRCDAYS
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
NRCDAY10  CALL      RKWTSUS1
          BRANCH    OVRCD,NRCDAY99
.
          PACK      KEY21,WTSUURNO,WTSUCODE,WTSUCNT,SP70
          MATCH     KEY19,KEY21
          GOTO      NRCDAY99 IF NOT EQUAL
.
          PACK      KEY5,ANSV,ANSL,WTSULSTS
          CALL      RDCODE1
          BRANCH    OVRCD,NRCDAY10
.
          MATCH     "R",TCINDC1
          GOTO      NRCDAY10 IF EQUAL
.
          MATCH     WTSUFDAT,NRCDAYTO    * Suspension start after end date
          GOTO      NRCDAY10 IF LESS
.
          MATCH     NRCDAYFR,WTSUFDAT    * Suspension started before start date
          IF        @LESS
            MATCH     SP70,WTSUTDAT
            IF        !@EQUAL
              MATCH     NRCDAYFR,WTSUTDAT * Skip if suspension ended before
              GOTO      NRCDAY10 IF LESS  * start date range
            ENDIF
            MOVE      NRCDAYFR,WTSUFDAT  * Set suspension start to start date
          ENDIF
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            MOVE      NRCDAYTO,WTSUTDAT
          ELSE
            MATCH     WTSUTDAT,NRCDAYTO
            GOTO      NRCDAY40 IF NOT LESS
            MOVE      NRCDAYTO,WTSUTDAT
          ENDIF
.
NRCDAY40  DAYSEP    WTSUFDAT,WTSUTDAT,F4
          ASSIGN    F4+NRCDAYS,NRCDAYS    * Do not add extra day to NRFC days
          GOTO      NRCDAY10
.
NRCDAY99  RETURN
.
.------------------------------------------------------------
. Calculate Days on List for a date range
. From date LSTDAYFR
. To date LSTDAYTO
.------------------------------------------------------------
LSTDAY00  IF        WMSTAT>3 & WMSTAT<6
            MATCH     SP70,WMDATE3
            IF        !@EQUAL
              MATCH     LSTDAYTO,WMDATE3
              IF        @LESS
                MOVE      WMDATE3,LSTDAYTO         * admitted or discharged
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=6
            MATCH     SP70,WMDATE4
            IF        !@EQUAL
              MATCH     LSTDAYTO,WMDATE4
              IF        @LESS
                MOVE      WMDATE4,LSTDAYTO         * removed
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=4|WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDMISS1
            IF        OVRCD<>1
              MATCH     LSTDAYTO,ADATE
              IF        @LESS
                PACK      LSTDAYTO,ADATE
              ENDIF
            ENDIF
          ENDIF
.
          DAYSEP    LSTDAYFR,LSTDAYTO,LSTDAYS
.
LSTDAY99  RETURN
.
.
.------------------------------------------------------------
. Calculate ready for care days at a specific priority
. LISTPRTY - Priority  code HDP position 1
.------------------------------------------------------------
PRTDAY00  MOVE      ZERO,LPRDAYS
          MOVE      SP70,PRTYFDAT
          MOVE      SP70,PRTYTDAT
          MOVE      WMDATE1,PRTYFDAT
.
          PACK      KEY5,ANST,ANSP,SP70
          CALL      RDSCODE1
PRTDAY10  CALL      RDKCODE1                     * Get priority cat code for
          BRANCH    OVRCD,PRTDAY99               * the specific priority
.
          MATCH     "TP",TCODE
          GOTO      PRTDAY99 IF NOT EQUAL
.
          MATCH     ANSI,PTCOACTV                * Skip inactive
          GOTO      PRTDAY10 IF EQUAL
.
          MATCH     LISTPRTY,THCSCOD             * Match HDP position 1
          GOTO      PRTDAY10 IF NOT EQUAL
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANST,ANSP,SP70
          CALL      RSWTCAT2                * Position on & read code changes
PRTDAY20  CALL      RKWTCAT2                  file record
          BRANCH    OVRCD,PRTDAY80
.
          MATCH     WMURNO,WTCAURNO
          GOTO      PRTDAY80 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTCAPROC
          GOTO      PRTDAY80 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      PRTDAY80 IF NOT EQUAL   * Different procedure count
.
          MATCH     "TP",WTCACATF
          GOTO      PRTDAY80 IF NOT EQUAL   * Different category
.
          MOVE      WTCACODT,CURRPRTY
.
          MATCH     SP70,WTCACODF           * Skip first priority change
          GOTo      PRTDAY20 IF EQUAL
.
          MATCH     ACODE,WTCACODF
          IF        !@EQUAL
            MOVE      WTCAEDAT,PRTYFDAT    * Save effective date for next
            GOTO      PRTDAY20             * records to date
          ENDIF
.
          MOVE      WTCAEDAT,PRTYTDAT       * Priority to date
.
          MOVE      PRTYFDAT,NRCDAYFR       * NRFC from date
          MOVE      PRTYTDAT,NRCDAYTO       * NRFC to date
          CALL      NRCDAY00                * Calc not ready for care days
.
          MOVE      PRTYFDAT,LSTDAYFR       * List days from date
          MOVE      PRTYTDAT,LSTDAYTO       * List days to date
          DAYSEP    LSTDAYFR,LSTDAYTO,LSTDAYS
.
          MOVE      ZERO,FORM6
          ASSIGN    LSTDAYS-NRCDAYS,FORM6   * List days - NRFC days = RFC days
          ADD       FORM6,LPRDAYS
.
          MOVE      WTCAEDAT,PRTYFDAT       * Save effective date for next
          GOTO      PRTDAY20
.
PRTDAY80  MATCH     CURRPRTY,ACODE          * Not currently is specific priority
          GOTO      PRTDAY99 IF NOT EQUAL
.
          MOVE      ENDDDATE,PRTYTDAT              * Work out to date
          IF        WMSTAT>3 & WMSTAT<6
            MATCH     SP70,WMDATE3
            IF        !@EQUAL
              MATCH     PRTYTDAT,WMDATE3
              IF        @LESS
                MOVE      WMDATE3,PRTYTDAT         * admitted or discharged
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=6
            MATCH     SP70,WMDATE4
            IF        !@EQUAL
              MATCH     PRTYTDAT,WMDATE4
              IF        @LESS
                MOVE      WMDATE4,PRTYTDAT         * removed
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=4|WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDMISS1
            IF        OVRCD<>1
              MATCH     PRTYTDAT,ADATE
              IF        @LESS
                PACK      PRTYTDAT,ADATE
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      PRTYFDAT,NRCDAYFR       * NRFC from date
          MOVE      PRTYTDAT,NRCDAYTO       * NRFC to date
          CALL      NRCDAY00                * Calc not ready for care days
.
          MOVE      PRTYFDAT,LSTDAYFR   * List days from date
          MOVE      PRTYTDAT,LSTDAYTO  * List days to date
          CALL      LSTDAY00           * Calc days on list
.
          MOVE      ZERO,FORM6
          ASSIGN    LSTDAYS-NRCDAYS,FORM6   * List days - NRFC days = RFC days
          ADD       FORM6,LPRDAYS
.
PRTDAY99  RETURN
.
.**********************************************************************
.* Keyin Adhoc extraction yes or no
.**********************************************************************
KADH0000    MOVE       ZERO,ADHOCEXT
            MOVE       "N",ANS
.
            KEYIN      *P1:10,*EL,"Adhoc Extraction (Y/N) : ":
                       *P26:10,*V2LON,*RV,ANS;
            RESET      ANS
            REP        UPPLOW,ANS
.
            MATCH     "Y",ANS
            IF         @EQUAL
              MOVE       ONE,ADHOCEXT
              GOTO       KADH8000
            ENDIF
.
            MATCH     "N",ANS
            IF         @EQUAL
              MOVE       ZERO,ADHOCEXT
              GOTO       KADH8000
            ENDIF
.
            GOTO    KADH9100
.
KADH8000    MOVE    ZERO,EXIT
            GOTO    KADH9999
.
KADH9100    MOVE    ONE,EXIT
            GOTO    KADH9999
.
KADH9999    RETURN
.
.
.**********************************************************************
.* Keyin web user id
.**********************************************************************
KUSR0000  MOVE      SP70,WEBUSRID
          KEYIN     *P1:17,*EF:
                    *P1:17,"Web User Id : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
          MATCH     SP70,WEBUSRID
          GOTO      KUSR9100 IF EQUAL
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,KUSR9100
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9100  MOVE      ONE,EXIT
.
KUSR9999  RETURN
.
.**********************************************************************
.* Update extract run dates if not an adhoc extraction
.**********************************************************************
EDAT0000  BRANCH     ADHOCEXT,EDAT9999
.
          MOVE       HOSPCODE,PMWAHOSP
          MOVE       "IBAWAT77",PMWAPROG
          MOVE       STARTDAT,PMWASDAT
          MOVE       ENDDDATE,PMWAEDAT

          PACK      PMWACDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMWACDAT
          CLOCK     TIME,PMWACTIM
          MOVE      WEBUSRID,PMWAWEBC
.
          PACK      PMWASPARR,SP70,SP70
.
          PACK      KEY27,PMWAHOSP,PMWAPROG,PMWASDAT,PMWAEDAT,SP70
          CALL      RAPMWAE1
          IF        OVRCD=1
            CALL      WRPMWAE1
          ENDIF
.
EDAT9999  RETURN
.
.******************************************************************************
.
          INC       STD001IO
.
          INC       PATHSPKY
          INC       CALCDAYS
          INC       CLPATHSP
          INC       TIMEDIFF
          INC       SEXDSCIO
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATWR1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PMSWAEIO/INC
          INC       WATCATIO/INC
          INC       WATCHAIO/INC
          INC       WATHISIO/INC
          INC       WATPROIO/INC
          INC       WATSUSIO/INC
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC
          INC       WEBSECIO/INC
.

.***************************************************************************
.* System    :   WEB                                                       *
.* Program   :   EMRICDLD                                                  *
.* Desc      :   Load Emergency Diseases from txt file                     *
.***************************************************************************
.* Date      :   27/10/1999                                                *
.* Author    :   Nicole Harrington                                         *
.* Function  :   as per description                                        *
.* Modifications :                                                         *
.*                 V11.02.01 16/08/2022 Alina Bhari        TSK 0923131     *
.*                           Recompile for EMRICDPR - Emergency diagnosis  *
.*                           maintenance keyword search field by 70        *
.*                           character                                     *
.*                 V10.14.01 10/04/2019 Ebon Clements      TSK 0862664     *
.*                           Added EMCIMETH                                *
.*                 V10.06.01 22/06/2015 Ebon Clements      CAR 318401      *
.*                           Added EMCIVEMD and EMCIKEYW to load file      *
.*                 V9.02.01  28/03/2002 Bronko Sosic                       *
.*                           Recompiled for EMRICDFD                       *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       EMRICDFD/INC
          INC       EMRKEDFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
DISFILE   FILE      ASCII,FIXED=256
.
DISCODE   DIM       9
DISDESC   DIM       70
DISVEMD   DIM       9
DISKEYW   DIM       70
DISMETH   DIM       1
.
PRGID     INIT      "EMRICDLD"
PRGNAM    INIT      "Loads Emergency Diagnosis Codes from txt file"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  OPEN      DISFILE,"disease"
          OPEN      EMRICDA1,"emricdaf"
.
PROC1000  MOVE      ZERO,OVRCD
          READ      DISFILE,SEQ;DISCODE,DISDESC,DISVEMD,DISKEYW,DISMETH
          GOTO      OVERCOND IF OVER
          BRANCH    OVRCD,PROC9000
.
          MATCH     SP70,DISCODE   
          GOTO      PROC1000 IF EQUAL
.
          PACK      EMIC9COD,DISCODE,SP70
          PACK      EMICDESC,DISDESC,SP70
          PACK      EMIC10CD,DISCODE,SP70
          MOVE      ZERO,EMICACTV
          PACK      EMICVEMD,DISVEMD,SP70
          PACK      EMICKEYW,DISKEYW,SP70
          PACK      EMICMETH,DISMETH,SP70
          PACK      EMICSPAR,SP70
          PACK      KEY9,DISCODE,SP70
.
          CALL      RDEMICD1
          BRANCH    OVRCD,PROC2000
.
          PACK      EMICDESC,DISDESC,SP70
          PACK      EMICVEMD,DISVEMD,SP70
          PACK      EMICKEYW,DISKEYW,SP70
.
          CALL      UPEMICD1
          PROC      EMRICDUP                * Update Key Word Index
          GOTO      PROC1000
.
PROC2000  CALL      WREMICD1
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P3:10,*V2LON,"Finished loading diseases.",*HOFF
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       EMRICDIO/INC
          INC       EMRKEDIO/INC
          INC       STD001IO
          INC       EMRICDPR                * Update Key Word Index

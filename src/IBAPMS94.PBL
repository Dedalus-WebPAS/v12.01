.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*      PROGRAM NAME:   IBAPMS94                                              *
.*                                                                            *
.*      FUNCTION:       Medibank Private Patient Admission Extract            *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.*        V11.04.02 19/03/2024  J.Tan          Task 0919335                   *
.*                  Mod to check HF History file for HCP Equivalent           *
.*        V11.04.01 05.02.2024  DA Sarkies    TSK 0936282                     *
.*                  Increased size of spaces going into health fund variable  *
.*        V10.03.02 15/05/2012  Mike Laming   CAR 26R5283                     *
.*                  Mod to write nul records when either Preadmis & Discharge *
.*                  extracts have no data                                     *
.*        ......... 15/05/2012  Mike Laming   CAR 264941                      *
.*                  Mod to check ADATE vs EXTRDATE (instead of ASTAY)         *
.*        V10.03.01 16/03/2012  Mike Laming   CAR 252065                      *
.*                    Mod to use CAPPRVNO (Approval No.) for single Hospital  *
.*        V10.03.00 16/02/2012  Mike Laming   CAR 252065                      *
.*                    Created New.                                            *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
.
          INC       PATDADFD/INC
          INC       PATVADFD/INC
.
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PATDDHFD/INC
          INC       PATFN1FD/INC
          INC       PATHSPFD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATWR1FD/INC
          INC       PMSVX1FD/INC            * Visit extension read by Admission
          INC       PMSPX2FD/INC
          INC       PATECDFD/INC
          INC       IBAPRNFD/INC
.
+
.******************************************************************************
CMDLINE   DIM       100 
.
.
CURRDATE  DIM       8
DIM4      DIM       4
DIM5      DIM       5
DIM8      DIM       8
DIM10     DIM      10
DIM12     DIM      12
DIM14     DIM      14 
DIM15     DIM      15
DIM15A    DIM      15
DIM16     DIM      16
DIM20     DIM      20
DIM25     DIM      25
DIM30     DIM      30
DIM300    DIM     300
DIM400    DIM     400
ENDDATE   DIM       8
ERRCOUNT  FORM      7
ERROR01   DIM     100
ERROR02   DIM     100
ERROR03   DIM     100
ERROR04   DIM     100
EXTRDATE  DIM       8
EXTRTYPE  DIM       9
EXTYPE    FORM      1
FILENAMX  DIM      50
FIRSTONE  FORM      1
FORM9     FORM      9
.
HEADDES1  DIM      30
HEADDES2  DIM      40
HEALFUND  DIM       6[50]
HFUNDCNT  FORM      2
IBCNMHOS  FORM      1
INFOCNT   FORM      1
INFOLINE  DIM     100[9]
LCMDLINE  DIM     300
.
NOOFBYTE  FORM      7
NOOFMEMB  FORM      7
.
PATHNAME  DIM      60
PATHFILE  DIM     150
..PTCNPMPL  DIM      60                       * Path to MediBank extract data
PREVWARD  DIM       3
PRNTLINE  DIM     132
REPTDATE  DIM      10
.
SAVEAPPR  DIM      10
SCVERT    FORM      2
SENDIT    DIM       1
SERIOUS   FORM      1
STRTDATE  DIM       8
TMPFILE1  DIM       8
TEMPDATE  DIM       8
.
WORKDATE  DATE      8
.
DATFNAME  DIM       40                           * Previous Extract Dates
PREEXTFL  DIM       12                           * Pre-admn temp extract file
PRECTLFL  DIM       12                           * Pre-admn temp control file
DSCEXTFL  DIM       12                           * Discharge temp extract file
DSCCTLFL  DIM       12                           * Discharge temp control file
.
UNIQNUMB  FORM      3
YESTERDY  DIM       8
.
EXTFILE1  FILE      ASCII,FIXED=300
EXTCNTL1  FILE      ASCII,FIXED=256
.
. Pre-admission List
. NAME     TYPE   LENGTH   DESCRIPTION
. ----     ----   ------   -----------
XADMAPPR  DIM      10      Hospital Provider ID            (pthsappr)
XADMHNAM  DIM      40      Hospital Business Name          (pthsname)
XADMMEMB  DIM       8      Patient Membership No.          (afndmm / pfndmm)
XADMFNAM  DIM      40      Patient First Name              (pmpxfnam)
XADMSNAM  DIM      40      Patient Last Name (Surname)     (ptmasnam)
XADMPDOB  DIM       8      Date of Birth (yyyymmdd)        (pbdate)
XADMADD1  DIM      35      Address Line 1                  (padd1)
XADMADD2  DIM      35      Address Line 2                  (padd2)
XADMSURB  DIM      35      Suburb                          (psubrb)
XADMPOST  DIM       4      Post Code (numeric)             (ppost)
XADMSTTE  DIM      10      State code (VIC,NT,etc)         (padd4)
XADMADAT  DIM       8      Expected Admission Date         (adate)
.                 273
.
XCMEMBNO  DIM       7      Control record Member Count
XCBYTENO  DIM       7      Control Record Data Byte Count
.
ADMTEMP1  IFILE   SQL,FIXED=125, KEY="U1-3,4-6,7-9"   
.
. Admission List
. NAME     TYPE   LENGTH   DESCRIPTION
. ----     ----   ------   -----------
XADRAWRD  DIM       3      Patient Ward                    (award)
XADRABED  DIM       3      Patient Bed number              (abed)
XADRUNIQ  DIM       3      Unique number 
XADRNAME  DIM      60      Patient Name (Lastname,Title,Firstname)
XADRPDOB  DIM      10      Date of Birth (ddmmyyyy)        (pbdate)
XADRADAT  DIM      10      Admission Date                  (adate)
XADRMEMB  DIM       9      Patient Membership No.          (afndmm / pfndmm)
XADRWRDD  DIM      20      Ward Description                (wbdesc)
XADRSPAR  DIM       6      spare
.                 125
.
EXTFILE2  FILE      ASCII,FIXED=400
EXTCNTL2  FILE      ASCII,FIXED=256
.
. Discharge List
. NAME     TYPE   LENGTH   DESCRIPTION
. ----     ----   ------   -----------
.XADMAPPR DIM      10      Hospital Provider ID            (pthsappr)
.XADMHNAM DIM      40      Hospital Business Name          (pthsname)
.XADMMEMB DIM       8      Patient Membership No.          (afndmm / pfndmm)
.XADMFNAM DIM      40      Patient First Name              (pmpxfnam)
.XADMSNAM DIM      40      Patient Last Name (Surname)     (ptmasnam)
.XADMPDOB DIM       8      Date of Birth (yyyymmdd)        (pbdate)
.XADMADD1 DIM      35      Address Line 1                  (padd1)
.XADMADD2 DIM      35      Address Line 2                  (padd2)
.XADMSURB DIM      35      Suburb                          (psubrb)
.XADMPOST DIM       4      Post Code (numeric)             (ppost)
.XADMSTTE DIM      10      State code (VIC,NT,etc)         (padd4)
.XADMADAT DIM       8      Admission Date                  (adate)
XDSCDDAT  DIM       8      Discharge Date                  (adate)
XDSCDGCD  DIM       9      Admitting Diagnosis Code        (
XDSCDGDS  DIM      60      Admitting Diagnosis Desc.       (adiag1 / adiag2)
XDSCDGC2  DIM       9      Discharge Diagnosis Code        (ptdsddrg)
XDSCDGD2  DIM      60      Discharge Diagnosis Desc.       (ddiag / ddiag2)
.                 392
.
MBPDATE1  FILE      ASCII,FIXED=60
.
. Previous Run Date
. NAME     TYPE   LENGTH   DESCRIPTION
. ----     ----   ------   -----------
XPREDATE   DIM      8      Previous Pre-admission extract date
XADMDATE   DIM      8      Previous Admission Report date
XDSCDATE   DIM      8      Previous Discharge extract date
XSPARE     DIM     36      spare
.                  60
.
MV        INIT      "mv "
PIPE      INIT      "|"
PIP2      INIT      "| "
PIP3      INIT      " | "
SLSH      INIT      "/"
USC       INIT      "_"
PMS94     INIT      "pms94"
TBA       INIT      "TBA"
TXT       INIT      "txt"
TYPE0     INIT      "All Extract Types "
TYPE1     INIT      "Pre-admission extract"
TYPE2     INIT      "Admission Report "
TYPE3     INIT      "Discharge extract"
.                    ....*....1....*....2....*....3....*....4....*....5
UNDLN132  INIT      "-------------------------------------------------":
                    "-------------------------------------------------":
                    "--------------------------------"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAPMS94"
PRGNAM    INIT      "MEDiBANK Private Extracts"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.******************************************************************************
ML0000    MOVE      ZERO,EXIT
.
          CALL      INIT0000               * initialization and open files
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000               * select options and parameters
          BRANCH    EXIT,ML9999
.
.                                            ******** Testing Option only *****
          IF        OPTION = 2
            CALL      CONTQST              * only used for testing
            BRANCH    CEXIT,ML2000:        * yes
                            ML0100:        * no
                            ML9999         * cancel
          ENDIF                             ******** end Testing Option *****
.
ML2000    IF        SERIOUS = 1
            GOTO      ML9999
          ENDIF
.
          CALL      ACPREP00               * create required .txt & .ctl files
.
ML3000    IF        EXTYPE = 0 | EXTYPE = 1
            CALL      PREEX000             * Pre-admission extract
            BRANCH    EXIT,ML3200
            CALL      PRERN000             * rename Pre-admission files
          ENDIF
.
ML3200    IF        EXTYPE = 0 | EXTYPE = 3
            CALL      DSCEX000             * Discharge extract
            BRANCH    EXIT,ML3400
            CALL      DSCRN000             * rename Discharge files
          ENDIF
.
ML3400    IF        EXTYPE = 0 | EXTYPE = 2
            CALL      ADMRP000             * Admission Report
          ENDIF
.     
....      GOTO      ML0100                 * (does not return to Options)
.
ML9999    CHAIN     PGM                    * chain back to program
+
.******************************************************************************
.*                             INIT0000                                  *
.*                 Display screen header and open files                  *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD     * set  CURRDATE & YESTERDY
          REP       " 0",CURRDATE
          MOVE      CURRDATE,WORKDATE
          SUB       ONE,WORKDATE
          MOVE      WORKDATE,YESTERDY
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"     * Visit extension read by Admission
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA1,"patecdaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS        * Multi Hospital
          READ      CONTROLF,TEN;*79,CAPPRVNO         * Approval No (single hos)
          READ      CONTROLF,HUND18;*181,PTCNPMPL
.
          MOVE      PTCNPMPL,PATHNAME
          MOVE      "- Error report",CPHDROPT
          MOVE      SP10,REPTDATE
.
.
INIT9999  RETURN
+
.******************************************************************************
.*                            OPTN0000                                        *
.*                  Main Option Screen for Web                                *
.******************************************************************************
.
OPTN0000  MOVE      "99",CLNO 
          PACK      INFOLINE[1],SP70,SP30
          MOVE      ONE,INFOCNT
          REPEAT
           ADD        ONE,INFOCNT
           MOVE       INFOLINE[1],INFOLINE[INFOCNT]
          UNTIL     INFOCNT = 9
          MOVE      ZERO,INFOCNT
     
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Generate MediBank Extract/s":
                    *P1:6,*V2LON,"2",*HOFF," = Programmer testing only ":
                          "- Generate MediBank Extract/s":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*DV,UNDLN1,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    OPTION,OPTN2000,OPTN2000
          BEEP
          GOTO      OPTN1000
.
OPTN2000  MOVE      "Option Selection",HEADDES1
.
          IF        IBCNMHOS = 1
            DISPLAY   *P1:10,*V2LON," Hospital ID  : ":
                      *P1:11,*V2LON," Extract Type : ":
                      *P1:12,*V2LON," Extract Date : ":
                      *P1:13,*V2LON," Send Extract :       (Y/N)";
          ELSE
            DISPLAY   *P1:10,*V2LON," Extract Type : ":
                      *P1:11,*V2LON," Extract Date : ":
                      *P1:12,*V2LON," Send Extract :       (Y/N)";
          ENDIF
          IF        OPTION = 2
            DISPLAY   *P1:14,*V2LON," Health Fund Id : "
          ENDIF
.
OPTN3000  UNPACK    SP70,PTHSHOSP,PTHSAPPR,PTHSNAME
          MOVE      "10",CVERT
          IF        IBCNMHOS = 1
            CALL      KHOS0000              * Keyin hospital
            MOVE      " Hospital ID  : ",DIM16
            MOVE      PTHSNAME,DIM30
            MOVE      "  Provider Id : ",DIM15
            MATCH     SP3,PTHSHOSP          * selected "All Hospitals" ?
            IF        @EQUAL
              CALL      FINDAPPR
              MOVE      PTHSAPPR,SAVEAPPR
              MOVE      SP3,PTHSHOSP 
            ENDIF
            ADD       ONE,INFOCNT
            PACK      INFOLINE[INFOCNT],DIM16,PTHSHOSP,SP2,DIM30,SP5,DIM15:
                      PTHSAPPR,SP70
            ADD       ONE,CVERT
          ELSE
...         PACK      KEY3,SP3
...         CALL      RDPTHSP1              * read single Hospital record
            MOVE      CNAME,PTHSNAME        *
            MOVE      CAPPRVNO,PTHSAPPR     * Approval No.
          ENDIF
          MATCH     SP10,PTHSAPPR           * check Provider Id
          IF        @EQUAL
.                                           * (programmer testing)
            IF        OPTION = 2
              MOVE      "12345678",PTHSAPPR * programmer test
            ENDIF
            CLEAR     ERROR01
            APPEND    "Hospital Provider Id not available for ",ERROR01 
            APPEND    PTHSNAME,ERROR01 
            RESET     ERROR01
            MOVE      ONE,SERIOUS
          ENDIF
.
.                                           * select Extract/Report option
OPTN4000  DISPLAY   *P17:CVERT,*EL,UNDLN1;
          IF        OPTION = 2
            MOVE      "50",HLEF             * (programmer testing)
            MOVE      "10",HTOP
            MOVE      "78",HRIG
            MOVE      "17",HBOT

            CALL      GETSCR00              * Save the current screen
            BOXCLR    HLEF,HTOP,HRIG,HBOT
            BOX       16,*V2LON,HLEF,HTOP,HRIG,HBOT
.
            DISPLAY   *P51:11,*V2LON,"0 ",*HOFF,"All":
                      *P51:12,*V2LON,"1 ",*HOFF,"Preadmission extract":
                      *P51:13,*V2LON,"2 ",*HOFF,"Admission Report":
                      *P51:14,*V2LON,"3 ",*HOFF,"Discharge extract";
          ENDIF
.
OPTN4500  KEYIN     *P17:CVERT,*DV,UNDLN1,*P17:CVERT,*V2LON,ANS;
.
          MOVE      ZERO,EXTYPE             * default
          MOVE      ANS,EXTYPE
          IF        EXTYPE > 3
            GOTO      OPTN4000
          ENDIF
.
          IF        OPTION = 2
            CALL      PUTSCR00              * restore previous screen
          ENDIF
          MOVE      EXTYPE,ANS
          MOVE      TYPE0,DIM25
          LOAD      DIM25,EXTYPE,TYPE1,TYPE2,TYPE3
          DISPLAY   *P17:CVERT,*V2LON,ANS,*P25:CVERT,DIM25
          MOVE      " Extract Type : ",DIM16
          ADD       ONE,INFOCNT
          PACK      INFOLINE[INFOCNT],DIM16,ANS,SP4,DIM25,SP70
          ADD       "1",CVERT
.
.                                           * get extract date
OPTN5000  MOVE      ZERO,EXIT
          MOVE      "17",CCOL
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          PACK      EXTRDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     SP8,EXTRDATE
          IF        @EQUAL
            CLEAR     ERROR02 
            MOVE      "Extract date not entered",ERROR02 
            MOVE      ONE,SERIOUS
          ENDIF
            
          REP       " 0",EXTRDATE
          MOVE      " Extract Date : ",DIM16
          PACK      DIM25,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
          ADD       ONE,INFOCNT
          PACK      INFOLINE[INFOCNT],DIM16,DIM25,SP70
          ADD       "1",CVERT
.
.                                           * is extract/s to be zipped & sent?
OPTN6000  KEYIN     *P17:CVERT,*DV,UNDLN1,*P17:CVERT,*V2LON,ANS;
          PACK      SENDIT,ANS,SP1
          REP       "yY1Y",SENDIT
.
.
.                                           * find Code for Medibank Private Ltd
OPTN8000  MOVE      ZERO,HFUNDCNT           * number of H'funds using MPL equiv
          PACK      KEY14,SP20
          CALL      RDSFUND1
OPTN8200  CALL      RDKFUND1
          BRANCH    OVRCD,OPTN8400
.
          MATCH     "MPL ",HFHDPE           * use HDP Equivalent
          IF        !@EQUAL
            MOVE      "01",KEY2             * check HF history for HCP eq.
            PACK      KEY17,HCODE,KEY2,SP70
            CALL      PATDDHRD
.
            MATCH     "MPL ",PTHFHCPE       * otherwise us HCP Equivalent
            GOTO      OPTN8200 IF NOT EQUAL
          ENDIF
          ADD       ONE,HFUNDCNT
          MOVE      HCODE,HEALFUND[HFUNDCNT]
          GOTO      OPTN8200
.
OPTN8400  COMPARE   ZERO,HFUNDCNT
          GOTO      OPTN9900 IF NOT EQUAL
.
          CLEAR     ERROR03 
          MOVE      "Cannot find a Health Fund with HDP Equiv of HPL",ERROR03
          MOVE      ONE,SERIOUS
          GOTO      OPTN9900
.
.
OPTN9000  MOVE      ONE,EXIT
          GOTO      OPTN9999
.
OPTN9900  COMPARE   "2",EXTYPE              * don't print error page for Admiss.
          IF        @EQUAL
            COMPARE   ZERO,SERIOUS
            GOTO      OPTN9999 IF EQUAL
          ENDIF
.
.                                           * print "Info" page for extracts
          MOVE      ZERO,F2
          REPEAT
            ADD       ONE,F2
            MOVE      INFOLINE[F2],PRNTLINE
            CALL      PRNT0000
          UNTIL     F2 = INFOCNT

OPTN9902  IF        SERIOUS = 1
            CLEAR     PRNTLINE
            CALL      PRNT0000
            MATCH     SP20,ERROR01
            IF        !@EQUAL
              MOVE      ERROR01,PRNTLINE
              CALL      PRNT0000
            ENDIF
            MATCH     SP20,ERROR02
            IF        !@EQUAL
              MOVE      ERROR02,PRNTLINE
              CALL      PRNT0000
            ENDIF
            MATCH     SP20,ERROR03
            IF        !@EQUAL
              MOVE      ERROR03,PRNTLINE
              CALL      PRNT0000
            ENDIF
            
            CLEAR     PRNTLINE
            MOVE      "Medibank Extract cannot continue",PRNTLINE
            CALL      PRNT0000
            MOVE      ONE,EXIT
          ENDIF
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  FINDAPPR              Called by: ML0000   *
.*                    Find a Provider Id for Multi Hospital                   *
.******************************************************************************
FINDAPPR  MOVE      ZERO,EXIT


          PACK      KEY3,SP10
          CALL      RSPTHSP1
FIND1000  CALL      RKPTHSP1
          BRANCH    OVRCD,FIND9999
.
          MATCH     SP10,PTHSAPPR
          GOTO      FIND1000 IF EQUAL
.                                           * accept the Provider Id found
FIND9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  MOVE      ZERO,EXIT
          COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
          MOVE      CVERT,SCVERT
.
          DISPLAY   *P1:CVERT,*EL," Hospital Id  : ";
          MOVE      TEN7,ECOL
          MOVE      CVERT,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ONE,EXIT
.
KHOS3000  MOVE      SP1,KEY1                * dummy code
.
          MOVE      SCVERT,CVERT
          DISPLAY   *P25:CVERT,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.*                                  ACPREP00                                  *
.*                     Prep extract files and reset variables                 *
.******************************************************************************
ACPREP00  PACK      DATFNAME,SP30,SP30           * Previous Extract Dates
          STRIP     PATHNAME                     
.
          MOVE      "pms94dat.txt",DIM12         * read or create Dates file
          PACK      DATFNAME,PATHNAME,DIM12,SP2
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      MBPDATE1,DATFNAME
          TRAPCLR   IO
          IF        OVRCD = 1
            PREP      MBPDATE1,DATFNAME          * create a new date file
            MOVE      YESTERDY,XPREDATE
            MOVE      YESTERDY,XADMDATE
            MOVE      YESTERDY,XDSCDATE
            UNPACK    SP70,XSPARE
            WRITE     MBPDATE1,SEQ;*+,XPREDATE,XADMDATE,XDSCDATE,XSPARE
          ELSE
            READ      MBPDATE1,SEQ;XPREDATE,XADMDATE,XDSCDATE,XSPARE
          ENDIF
          CLOSE     MBPDATE1
.
. 
ACPREP10  PACK      PREEXTFL,SP20                * create Pre-admission files
          MOVE      "a",KEY1                     * set up the text file name
          PACK      PREEXTFL,PMS94,KEY1,PORT,DOT,TXT
          PREP      EXTFILE1,PREEXTFL
.         
          MOVE      "b",KEY1                     * set up the control file name
          PACK      PRECTLFL,PMS94,KEY1,PORT,DOT,TXT
          PREP      EXTCNTL1,PRECTLFL
.
.                                                * create Discharge files
          MOVE      "c",KEY1                     * set up the text file name
          PACK      DSCEXTFL,PMS94,KEY1,PORT,DOT,TXT
          PREP      EXTFILE2,DSCEXTFL
.
          MOVE      "d",KEY1                     * set up the control file name
          PACK      DSCCTLFL,PMS94,KEY1,PORT,DOT,TXT
          PREP      EXTCNTL2,DSCCTLFL
.
ACPREP99  RETURN
+
.******************************************************************************
.*                                  PREEX000              Called by: ML0000   *
.*                       Create Pre-admission Extract                         *
.******************************************************************************
PREEX000  MOVE      ZERO,EXIT
          MOVE      ZERO,XCMEMBNO           * Member count
          MOVE      ZERO,XCBYTENO           * total length count
          MOVE      ZERO,ERRCOUNT           * "MPL" records bypassed
          MOVE      PTHSAPPR,XADMAPPR
          STRIP     XADMAPPR
          MOVE      PTHSNAME,XADMHNAM
          STRIP     XADMHNAM
.
          MOVE      EXTRDATE,WORKDATE       * finalise the extract date range
....      ADD       FIVE,WORKDATE           * from five days to
....      MOVE      WORKDATE,STRTDATE
....      ADD       TWO,WORKDATE            * - seven days
....      MOVE      WORKDATE,ENDDATE
          ADD       SIX,WORKDATE            * Trudy only wants the sixth day
          MOVE      WORKDATE,STRTDATE
          MOVE      WORKDATE,ENDDATE
.
          CLEAR     PRNTLINE
          CALL      PRNT0000
          APPEND    "Pre-admission extract  - ",PRNTLINE
          APPEND    "For ",PRNTLINE
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR,SP10
          APPEND    DIM10,PRNTLINE
          RESET     PRNTLINE
          CALL      PRNT0000
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSMISS3
PREEX100  MOVE      ZERO,EXIT
          CALL      RDKMISS3
          BRANCH    OVRCD,PREEX900
.
          MATCH     "1",DASTAT              * is this a Pre-admission?
          GOTO      PREEX100 IF NOT EQUAL
.
          IF        IBCNMHOS = 1
            MATCH     SP3,PTHSHOSP
            IF        !@EQUAL
              MATCH     SP3,PMVXMHOS        * if pre-admn hospital is blank
              GOTO      PREEX120 IF EQUAL   
              MATCH     PTHSHOSP,PMVXMHOS   * check for selected hospital
              GOTO      PREEX100 IF NOT EQUAL
            ENDIF
          ENDIF
.
PREEX120  COMPARE   ZERO,ASTAY              * is expected stay 1 or more days?
          GOTO      PREEX100 IF EQUAL
.
          MATCH     STRTDATE,ADATE          * is Admission in 5 to 7 days time?
          GOTO      PREEX100 IF LESS
          MATCH     ADATE,ENDDATE
          GOTO      PREEX900 IF LESS        * finished date range
.
          CALL      CLPATMAS                * read Master file data
          CALL      CLPMSPX2
          PACK      KEY8,AURNO,SP10
          CALL      RDMAST1
          PACK      KEY8,AURNO,SP10
          CALL      RDPMPX21
.
          MATCH     SP6,AFUNDH              * check the Health Fund
          IF        @EQUAL
            MOVE      PFUNDH,AFUNDH
          ENDIF
.
          MOVE      ZERO,F2
          REPEAT
            ADD       ONE,F2
            MATCH     HEALFUND[F2],AFUNDH   * is this "Medibank Private"?
            GOTO      PREEX180 IF EQUAL
          UNTIL     F2 = HFUNDCNT
          GOTO      PREEX100
.
PREEX180  COMPARE   TWO,OPTION
          GOTO      PREEX200 IF EQUAL
.
          CALL      ADMVER00                * validate all mandatory fields
          BRANCH    EXIT,PREEX100
.
PREEX200  CALL      ADMDAT00                * copy Admn data to extract fields
.
PREEX300  PACK      DIM300,XADMAPPR,PIPE,XADMHNAM,PIPE,XADMMEMB,PIPE:
                           XADMFNAM,PIPE,XADMSNAM,PIPE,XADMPDOB,PIPE:
                           XADMADD1,PIPE,XADMADD2,PIPE,XADMSURB,PIPE:
                           XADMPOST,PIPE,XADMSTTE,PIPE,XADMADAT
          WRITE     EXTFILE1,SEQ;*+,DIM300
.
          ADD       ONE,NOOFMEMB
          MOVE      ZERO,FORM9
          MOVELPTR  DIM300,FORM9    
          ADD       FORM9,NOOFBYTE
          ADD       ONE,NOOFBYTE            * add byte for "end of record"
          GOTO      PREEX100
.

PREEX900  MOVE      ZERO,EXIT
          MATCH     ENDDATE,XPREDATE        * update Date control table
          IF        @LESS
            MOVE      EXTRDATE,XPREDATE
            OPEN      MBPDATE1,DATFNAME
            WRITE     MBPDATE1,SEQ;*+,XPREDATE,XADMDATE,XDSCDATE,XSPARE
            CLOSE     MBPDATE1
          ENDIF
.
          IF        NOOFMEMB = 0 & NOOFBYTE = 0
            PACK      DIM300,PIPE,PIPE,PIPE,PIPE,PIPE,PIPE:           *I-265283
                             PIPE,PIPE,PIPE,PIPE,PIPE                 *I-265283
            WRITE     EXTFILE1,SEQ;*+,DIM300                          *I-265283
            CLEAR     PRNTLINE
            CALL      PRNT0000
            MOVE      "End of Pre-admission extract - No data found ",PRNTLINE
            CALL      PRNT0000
....        MOVE      ONE,EXIT                                        *D-265283
....        GOTO      PREEX999                                        *D-265283
          ENDIF
.
          MOVE      NOOFMEMB,XCMEMBNO       * write counts to Extract Control Fl
          SQUEEZE   XCMEMBNO
          MOVE      NOOFBYTE,XCBYTENO
          SQUEEZE   XCBYTENO
          PACK      DIM20,XCMEMBNO,COMMA,XCBYTENO
          WRITE     EXTCNTL1,SEQ;*+,DIM20
.
          IF        ERRCOUNT > 0
            CLEAR     PRNTLINE
            CALL      PRNT0000
            APPEND    ERRCOUNT,PRNTLINE
            APPEND    "  Records bypassed - mandatory data missing",PRNTLINE
            CALL      PRNT0000
          ENDIF
.
          CLEAR     PRNTLINE
          CALL      PRNT0000
          MOVE      NOOFMEMB,XCMEMBNO
          APPEND    XCMEMBNO,PRNTLINE
          APPEND    "  Members selected    ",PRNTLINE
          APPEND    "    Data count in Bytes : ",PRNTLINE
          APPEND    XCBYTENO,PRNTLINE
          CALL      PRNT0000
.
          CLEAR     PRNTLINE
          CALL      PRNT0000
          MOVE      "End of Pre-admission extract",PRNTLINE
          CALL      PRNT0000
          CLOSE     EXTFILE1
          CLOSE     EXTCNTL1
.
PREEX999  RETURN
+         
.******************************************************************************
.*                                  PRERN000                                  *
.*                      Rename Extract files and Zipp together                *
.******************************************************************************
PRERN000  MOVE      ZERO,EXIT
          PACK      DIM15,MV,PREEXTFL,SP1    * "mv pms94axx.txt"
          MOVE      DIM15,LCMDLINE
          ENDSET    LCMDLINE
.
          STRIP     PATHNAME
          STRIP     PTHSAPPR
          MOVE      "admission.txt ",DIM14
          PACK      PATHFILE,SP1,PATHNAME,EXTRDATE,USC,PTHSAPPR,USC,DIM14
          APPEND    PATHFILE,LCMDLINE
.
          RESET     LCMDLINE
          DISPLAY   *P1:23,LCMDLINE
          EXECUTE   LCMDLINE,TASKID         * execute name change
          MATCH     "0       ",TASKID
          GOTO      PRERN900 IF NOT EQUAL
.
          PACK      DIM15,MV,PRECTLFL,SP1   * "mv pms94bxx.txt"
          MOVE      DIM15,LCMDLINE
          ENDSET    LCMDLINE
.
          MOVE      "admission.ctl ",DIM14
          PACK      PATHFILE,SP1,PATHNAME,EXTRDATE,USC,PTHSAPPR,USC,DIM14
          APPEND    PATHFILE,LCMDLINE
.
          RESET     LCMDLINE
          DISPLAY   *P1:23,LCMDLINE
          EXECUTE   LCMDLINE,TASKID         * execute name change
          MATCH     "0       ",TASKID
          GOTO      PRERN900 IF NOT EQUAL
.
          MATCH     "Y",SENDIT              * Zip & Send ?
          GOTO      PRERN999 IF NOT EQUAL
          MOVE      "admission",EXTRTYPE
          CALL      ZIPEXT00                * Zip ".txt" and ".ctl" files 
          GOTO      PRERN999
.
PRERN900  CLEAR     PRNTLINE
          APPEND    "Error renaming Pre-admission txt File - '",PRNTLINE
          APPEND    TASKID,PRNTLINE
          APPEND    "' for Command - ",PRNTLINE
          STRIP     LCMDLINE
          APPEND    LCMDLINE,PRNTLINE
          CALL      PRNT0000
.
.
PRERN999  RETURN
+         
.******************************************************************************
.*                                  ADMRP000                                  *
.*                           Print The Report Header                          *
.******************************************************************************
ADMRP000  MOVE      ZERO,EXIT
          MOVE      ZERO,UNIQNUMB
          MOVE      "99",CLNO               * set up Report Headings
          MOVE      "- Current Admissions",CPHDROPT
          MOVE      ZERO,CPAGENO
          STRIP     PTHSNAME
          UNPACK    PTHSNAME,HEADDES1
          CLEAR     HEADDES2
          APPEND    "     Report Date : ",HEADDES2
          UNPACK    EXTRDATE,CCENT,CYEAR,CMON,CDAY
          PACK      REPTDATE,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
          APPEND    REPTDATE,HEADDES2
          RESET     HEADDES2
.
          CALL      CREA0000                * create the temp file
.
          MOVE      "2",KEY1
          PACK      KEY9,KEY1,SP20
          CALL      RDSMISS2
ADMRP100  CALL      RDKMISS2
          BRANCH    OVRCD,ADMRP500
.
          MATCH     "2",DASTAT              * is this a current Admission ?
          GOTO      ADMRP500 IF NOT EQUAL
.
          IF        IBCNMHOS = 1
            MATCH     SP3,PTHSHOSP
            IF        !@EQUAL
              MATCH     SP3,PMVXMHOS        * if Visit has blank Hospital
              GOTO      ADMRP120 IF EQUAL
              MATCH     PTHSHOSP,PMVXMHOS   * check for selected hospital
              GOTO      ADMRP100 IF NOT EQUAL
            ENDIF
          ENDIF
.
ADMRP120  IF        OPTION = 2              
            COMPARE   TWENTY1,UNIQNUMB
            GOTO      ADMRP500 IF LESS      * (programmer testing-only want 20)
          ENDIF     
...       COMPARE   ZERO,ASTAY              * is expected stay 1 or more days?
...       GOTO      ADMRP100 IF EQUAL
.         
.                                           * check Admission before this Date
          MATCH     EXTRDATE,ADATE                                    *C-264941
          GOTO      ADMRP100 IF NOT LESS                              *C-264941
.
ADMRP150  CALL      CLPATMAS                * read Master file data
          CALL      CLPMSPX2
          PACK      KEY8,AURNO,SP10
          CALL      RDMAST1
          PACK      KEY8,AURNO,SP10
          CALL      RDPMPX21
.
          MATCH     SP6,AFUNDH              * check the Health Fund
          IF        @EQUAL
            MOVE      PFUNDH,AFUNDH
          ENDIF
.
          MOVE      ZERO,F2
          REPEAT
            ADD       ONE,F2
            MATCH     HEALFUND[F2],AFUNDH   * is this "Medibank Private"?
            GOTO      ADMRP200 IF EQUAL
          UNTIL     F2 = HFUNDCNT
          GOTO      ADMRP100
.
.                                           * setup Admission record
ADMRP200  PACK      XADRAWRD,AWARD,SP10
          PACK      XADRABED,ABED,SP10
          ADD       ONE,UNIQNUMB
          MOVE      UNIQNUMB,XADRUNIQ
.
          CLEAR     XADRNAME
          STRIP     PTMASNAM
          APPEND    PTMASNAM,XADRNAME
          APPEND    ", ",XADRNAME
          STRIP     PTITL
          APPEND    PTITL,XADRNAME
          APPEND    SP1,XADRNAME
          APPEND    PMPXFNAM,XADRNAME
          APPEND    SP30,XADRNAME
          RESET     XADRNAME
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          PACK      XADRPDOB,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR,SP10
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          PACK      XADRADAT,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR,SP10
          PACK      XADRMEMB,AFNDMM,SP10
.
ADMRP400  PACK      WBDESC,AWARD,SP20       * get Ward description
          PACK      KEY6,AWARD,SP10
          CALL      RDWARD1
          PACK      XADRWRDD,WBDESC,SP20
          PACK      XADRSPAR,SP20
.
          PACK      KEY9,XADRAWRD,XADRABED,XADRUNIQ,SP10
          CALL      WRTEMP1
          GOTO      ADMRP100
.
.
ADMRP500  IF        UNIQNUMB = 0
            CLEAR     PRNTLINE 
            CALL      PRNT0000
            MOVE      "End of Current Admissions - No data found ",PRNTLINE
            CALL      PRNT0000
            MOVE      ONE,EXIT
            GOTO      ADMRP950
          ENDIF
.
.
.                                           * create Admission Report
ADMRP600  PACK      KEY9,SP10               * read thru tempfile
          PACK      PREVWARD,SP20
          MOVE      ONE,FIRSTONE
          CALL      RSTEMP1
ADMRP650  CALL      RKTEMP1
          BRANCH    OVRCD,ADMRP900
.
          MATCH     PREVWARD,XADRAWRD     * only print Ward Desc. on ward change
          IF        !@EQUAL
            IF        FIRSTONE <> 1
              MOVE      UNDLN132,PRNTLINE
              CALL      PRNT0000
            ENDIF
            MOVE      ZERO,FIRSTONE
            MOVE      XADRAWRD,PREVWARD
          ELSE
            MOVE      SP20,XADRWRDD
          ENDIF

          PACK      PRNTLINE,PIP2,XADRWRDD,PIP3,XADRABED,PIP3,XADRNAME:
                             PIP3,XADRPDOB,PIP3,XADRADAT,PIP3,XADRMEMB,PIPE
ADMRP660  CALL      PRNT0000
.
          GOTO      ADMRP650
.
.
ADMRP900  PRINT     *1,UNDLN132
          CLEAR     PRNTLINE
          CALL      PRNT0000
          MOVE      "End of Current Admission Report",PRNTLINE
          CALL      PRNT0000
.
          MATCH     EXTRDATE,XADMDATE       * update Date control table
          IF        @LESS
            MOVE      EXTRDATE,XADMDATE
            OPEN      MBPDATE1,DATFNAME
            WRITE     MBPDATE1,SEQ;*+,XPREDATE,XADMDATE,XDSCDATE,XSPARE
            CLOSE     MBPDATE1
          ENDIF
.
ADMRP950  CALL      KILL0000
.
ADMRP999  RETURN
+
.******************************************************************************
.*                                  ADMDAT00                                  *
.*                      Build Admission data fields                           *
.******************************************************************************
ADMDAT00  UNPACK    AFNDMM,DIM8,DIM2
          MOVE      DIM8,XADMMEMB
          STRIP     XADMMEMB
          MOVE      PMPXFNAM,XADMFNAM
          STRIP     XADMFNAM
          MOVE      PTMASNAM,XADMSNAM
          STRIP     XADMSNAM
          MOVE      PBDATE,XADMPDOB
          STRIP     XADMPDOB
          MOVE      PADD1,XADMADD1
          STRIP     XADMADD1
          MOVE      PADD2,XADMADD2
          STRIP     XADMADD2
          MOVE      PSUBRB,XADMSURB
          STRIP     XADMSURB
          UNPACK    PPOST,DIM4,DIM5
          MOVE      DIM4,XADMPOST
          MOVE      PADD4,XADMSTTE
          STRIP     XADMSTTE
          MOVE      ADATE,XADMADAT
.
ADMDAT99  RETURN
+
.******************************************************************************
.*                                  ADMVER00                                  *
.*                      Verify Admission mandatory fields                     *
.******************************************************************************
ADMVER00  COMPARE   TWO,OPTION              * Programmer Testing ?
          GOTO      ADMVER99 IF EQUAL
.
          MOVE      ZERO,EXIT
          MATCH     SP10,PTHSAPPR           * Provider Id
          GOTO      ADMVER10 IF EQUAL
          IF        IBCNMHOS <> 1
            MATCH     SP30,PTHSNAME           * Hospital name
            GOTO      ADMVER10 IF EQUAL
          ENDIF
          MATCH     SP70,AFNDMM             * Membership No.
          IF        @EQUAL
            MATCH     SP70,PFNDMM
            GOTO      ADMVER10 IF EQUAL
            MOVE      PFNDMM,AFNDMM
          ENDIF
          MATCH     SP30,PMPXFNAM           * First name (also see PMPXHFGN ?)
          GOTO      ADMVER10 IF EQUAL
          MATCH     SP30,PTMASNAM           * Surname name (also see PMPXHFSN ?)
          GOTO      ADMVER10 IF EQUAL
          MATCH     SP8,PBDATE              * Date of Birth
          GOTO      ADMVER10 IF EQUAL
          MATCH     SP35,PADD1              * Address line 1
          IF        @EQUAL
            PACK      PADD1,TBA,SP70
          ENDIF
          MATCH     SP35,PSUBRB             * Suburb
          IF        @EQUAL
            PACK      PSUBRB,TBA,SP70
          ENDIF
          MATCH     SP8,PPOST               * Post Code
          IF        @EQUAL
            PACK      PPOST,TBA,SP70
          ENDIF
          MATCH     SP10,PADD4              * Postcode
          IF        @EQUAL
            PACK      PADD4,TBA,SP70
          ENDIF
          MATCH     SP8,ADATE               * Expected Admission date
          GOTO      ADMVER10 IF EQUAL
.
          GOTO      ADMVER99
.
ADMVER10  ADD       ONE,ERRCOUNT
          MOVE      ONE,EXIT
.
ADMVER99  RETURN
+
.******************************************************************************
.*                                  DSCEX000              Called by: ML0000   *
.*                          Create Discharge Extract                          *
.******************************************************************************
DSCEX000  MOVE      ZERO,EXIT
          MOVE      ZERO,XCMEMBNO           * Member count
          MOVE      ZERO,XCBYTENO           * total length count
          MOVE      ZERO,ERRCOUNT           * "MBP" records bypassed
          MOVE      PTHSAPPR,XADMAPPR
          STRIP     XADMAPPR
          MOVE      PTHSNAME,XADMHNAM
          STRIP     XADMHNAM
.
.                                           * Discharge data to be 3 days before
.                                           * the Extract date
          MOVE      EXTRDATE,WORKDATE       * set date range (for previous stuff
          SUB       THREE,WORKDATE
          MOVE      WORKDATE,ENDDATE 
.
          MOVE      XDSCDATE,WORKDATE
          ADD       ONE,WORKDATE
          MOVE      WORKDATE,TEMPDATE
DSCEX050  MATCH     ENDDATE,TEMPDATE
          IF        !@LESS
            MOVE      ENDDATE,TEMPDATE 
          ENDIF
          MOVE      TEMPDATE,STRTDATE
.
          CLEAR     PRNTLINE                * print Info line
          CALL      PRNT0000
          CLEAR     PRNTLINE
          APPEND    "Discharge extract     - ",PRNTLINE
          MATCH     STRTDATE,ENDDATE
          IF        !@EQUAL
            APPEND    "From ",PRNTLINE
            UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR,SP10
            APPEND    DIM10,PRNTLINE
            APPEND    "    To ",PRNTLINE
            UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR,SP10
            APPEND    DIM10,PRNTLINE
          ELSE
            APPEND    "For ",PRNTLINE
            UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR,SP10
            APPEND    DIM10,PRNTLINE
          ENDIF
          RESET     PRNTLINE
          CALL      PRNT0000
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2
DSCEX100  CALL      RDKDSCH2
          BRANCH    OVRCD,DSCEX900
.
          MATCH     DDATE,ENDDATE
          GOTO      DSCEX900 IF LESS        * finished date range
.
          PACK      KEY8,DDADMNO,SP10
          CALL      RDMISS1                 * read the Admission record
          BRANCH    OVRCD,DSCEX100
.
          MATCH     "3",DASTAT              * is this a Discharge?
          GOTO      DSCEX100 IF NOT EQUAL
.
          IF        IBCNMHOS = 1
            MATCH     SP3,PTHSHOSP
            IF        !@EQUAL
              MATCH     PTHSHOSP,PMVXMHOS   * check for selected hospital
              GOTO      DSCEX100 IF NOT EQUAL
            ENDIF
          ENDIF                             
.
.                                           * check Admission before this Date
DSCEX120  MATCH     EXTRDATE,ADATE                                    *C-264941
          GOTO      DSCEX100 IF NOT LESS                              *C-264941
.
          CALL      CLPATMAS                * read Master file data
          CALL      CLPMSPX2
          PACK      KEY8,AURNO,SP10
          CALL      RDMAST1
          BRANCH    PCEASE,DSCEX100         * bypass Deceased patient
          PACK      KEY8,AURNO,SP10
          CALL      RDPMPX21
.
          MATCH     SP6,AFUNDH              * check the Health Fund
          IF        @EQUAL
            MOVE      PFUNDH,AFUNDH
          ENDIF
.
          CALL      MPLC0000                * Get MPL HF code for Dis.date
          MOVE      ZERO,F2
          REPEAT
            ADD       ONE,F2
            MATCH     HEALFUND[F2],AFUNDH   * is this "Medibank Private"?
            GOTO      DSCEX150 IF EQUAL
          UNTIL     F2 = HFUNDCNT
          GOTO      DSCEX100
.
DSCEX150  CALL      ADMVER00                * validate all mandatory fields
          BRANCH    EXIT,DSCEX100
.
DSCEX200  CALL      ADMDAT00                * copy Admn data to extract fields
.
          MOVE      DDATE,XDSCDDAT
          STRIP     XDSCDDAT
.
          
...       MOVE      OPDAPROV,XDSCDGCD       * Provisional item # 1 ( MBS )
          MOVE      PTMIPDRG,XDSCDGCD       * Provisional DRG
          STRIP     XDSCDGCD
          MOVE      ADIAG1,XDSCDGDS
          STRIP     XDSCDGDS
.
DSCEX250  PACK      KEY12,DDADMNO,SP20
          CALL      RSPTECD1
          CALL      RKPTECD1
          BRANCH    OVRCD,DSCEX300
          MATCH     DDADMNO,DPTEDADM
          GOTO      DSCEX300 IF NOT EQUAL

          MOVE      PTEDCODE,XDSCDGC2      * first Disease code for this Admn
          STRIP     XDSCDGC2
          MOVE      PTEDDESC,XDSCDGD2
          STRIP     XDSCDGD2
.
DSCEX300  PACK      DIM400,XADMAPPR,PIPE,XADMHNAM,PIPE,XADMMEMB,PIPE:
                           XADMFNAM,PIPE,XADMSNAM,PIPE,XADMPDOB,PIPE:
                           XADMADD1,PIPE,XADMADD2,PIPE,XADMSURB,PIPE:
                           XADMPOST,PIPE,XADMSTTE,PIPE,XADMADAT,PIPE:
                           XDSCDDAT,PIPE,XDSCDGCD,PIPE,XDSCDGDS,PIPE:
                           XDSCDGC2,PIPE,XDSCDGD2
          WRITE     EXTFILE2,SEQ;*+,DIM400
.
          ADD       ONE,NOOFMEMB
          MOVE      ZERO,FORM9
          MOVELPTR  DIM400,FORM9
          ADD       FORM9,NOOFBYTE
          ADD       ONE,NOOFBYTE            * add byte for "end of record"
          GOTO      DSCEX100
.
DSCEX900  MOVE      ZERO,EXIT
          MATCH     ENDDATE,XDSCDATE        * update Date control table
          IF        @LESS & NOOFMEMB > 0
            MOVE      EXTRDATE,XDSCDATE
            OPEN      MBPDATE1,DATFNAME
            WRITE     MBPDATE1,SEQ;*+,XPREDATE,XADMDATE,XDSCDATE,XSPARE
            CLOSE     MBPDATE1
          ENDIF
.
          IF        NOOFMEMB = 0 & NOOFBYTE = 0
            PACK      DIM400,PIPE,PIPE,PIPE,PIPE,PIPE,PIPE:           *I-265283
                             PIPE,PIPE,PIPE,PIPE,PIPE,PIPE:           *I-265283
                             PIPE,PIPE,PIPE,PIPE                      *I-265283
            WRITE     EXTFILE2,SEQ;*+,DIM400                          *I-265283
            CLEAR     PRNTLINE
            CALL      PRNT0000
            MOVE      "End of Discharge extract - No data found ",PRNTLINE
            CALL      PRNT0000
....        MOVE      ONE,EXIT                                        *D-265283
....        GOTO      DSCEX999                                        *D-265283
          ENDIF
.
          MOVE      NOOFMEMB,XCMEMBNO
          SQUEEZE   XCMEMBNO
          MOVE      NOOFBYTE,XCBYTENO
          SQUEEZE   XCBYTENO
          PACK      DIM20,XCMEMBNO,COMMA,XCBYTENO
          WRITE     EXTCNTL2,SEQ;*+,DIM20
.
          IF        ERRCOUNT > 0
            CLEAR     PRNTLINE
            CALL      PRNT0000
            APPEND    ERRCOUNT,PRNTLINE
            APPEND    "  Records bypassed - mandatory data missing",PRNTLINE
            CALL      PRNT0000
          ENDIF
.
          CLEAR     PRNTLINE
          CALL      PRNT0000
          MOVE      NOOFMEMB,XCMEMBNO
          APPEND    XCMEMBNO,PRNTLINE
          APPEND    "  Members selected    ",PRNTLINE
          APPEND    "    Data count in Bytes : ",PRNTLINE
          APPEND    XCBYTENO,PRNTLINE
          CALL      PRNT0000
.
          CLEAR     PRNTLINE
          CALL      PRNT0000
          MOVE      "End of Discharge extract",PRNTLINE
          CALL      PRNT0000
          CLOSE     EXTFILE2
          CLOSE     EXTCNTL2
.
DSCEX999  RETURN
+
.******************************************************************************
.         Find HF Code for Medibank Private Ltd as at Disc.date
.******************************************************************************
MPLC0000  MOVE      ZERO,F2
          REPEAT
            ADD       ONE,F2
            MOVE      SP70,HEALFUND[F2]
          UNTIL     F2 = 50

          MOVE      ZERO,HFUNDCNT           * number of H'funds using MPL equiv
          PACK      KEY14,SP20
          CALL      RDSFUND1
MPLC1000  CALL      RDKFUND1
          BRANCH    OVRCD,MPLC9999
.
          MATCH     "MPL ",HFHDPE           * use HDP Equivalent
          IF        !@EQUAL
            MOVE      "01",KEY2             * check HF history for HCP eq.
            PACK      KEY17,HCODE,KEY2,DDATE,SP70
            CALL      PATDDHRD
.
            MATCH     "MPL ",PTHFHCPE       * otherwise us HCP Equivalent
            GOTO      MPLC1000 IF NOT EQUAL
          ENDIF
          ADD       ONE,HFUNDCNT
          MOVE      HCODE,HEALFUND[HFUNDCNT]
          GOTO      MPLC1000
.
MPLC9999  RETURN
+
.******************************************************************************
.*                                  DSCRN000                                  *
.*                      Rename Extract files and Zipp together                *
.******************************************************************************
DSCRN000  MOVE      ZERO,EXIT
          PACK      DIM15,MV,DSCEXTFL,SP1   * "mv pms94cxx.txt"
          MOVE      DIM15,LCMDLINE
          ENDSET    LCMDLINE
.
          STRIP     PATHNAME
          STRIP     PTHSAPPR
          MOVE      "discharge.txt ",DIM14
          PACK      PATHFILE,SP1,PATHNAME,EXTRDATE,USC,PTHSAPPR,USC,DIM14
          APPEND    PATHFILE,LCMDLINE
.
          RESET     LCMDLINE
          DISPLAY   *P1:23,LCMDLINE
          EXECUTE   LCMDLINE,TASKID         * execute name change
          MATCH     "0       ",TASKID
          GOTO      DSCRN900 IF NOT EQUAL
.
          PACK      DIM15,MV,DSCCTLFL,SP1   * "mv pms94dxx.txt"
          MOVE      DIM15,LCMDLINE
          ENDSET    LCMDLINE
.
          MOVE      "discharge.ctl ",DIM14
          PACK      PATHFILE,SP1,PATHNAME,EXTRDATE,USC,PTHSAPPR,USC,DIM14
          APPEND    PATHFILE,LCMDLINE
.
          RESET     LCMDLINE
          DISPLAY   *P1:23,LCMDLINE
          EXECUTE   LCMDLINE,TASKID
          MATCH     "0       ",TASKID
          GOTO      DSCRN900 IF NOT EQUAL
.
          MATCH     "Y",SENDIT              * Zip & sent ?
          GOTO      DSCRN999 IF NOT EQUAL
          MOVE      "discharge",EXTRTYPE
          CALL      ZIPEXT00                * Zip ".txt" and ".ctl" files
          GOTO      DSCRN999
.
DSCRN900  CLEAR     PRNTLINE
          APPEND    "Error renaming Discharge txt File - '",PRNTLINE
          APPEND    TASKID,PRNTLINE
          APPEND    "' for Command - ",PRNTLINE
          STRIP     LCMDLINE
          APPEND    LCMDLINE,PRNTLINE
          CALL      PRNT0000
.
DSCRN999  RETURN
+
.******************************************************************************
.*                                CREA0000                                    *
.*                          Create a tempfile                                 *
.******************************************************************************
CREA0000  CLEAR     TMPFILE1
          MOVE      "pms94a",KEY6
          PACK      TMPFILE1,KEY6,PORT,SP10
          REP       " 0",TMPFILE1
.         
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ADMTEMP1,TMPFILE1
          BRANCH    OVRCD,CREA1000
          TRAPCLR   IO
          CALL      CLER0000                * clear existing temp file
          GOTO      CREA9999                
.
CREA1000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFILE1,CMDLINE
          APPEND    " 125 u1-3,4-6,7-9",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      ADMTEMP1,TMPFILE1
.
.
CREA9999  RETURN

+
.****************************************************************************
.*                              CLER0000               Called by: MAIN0000  *
.*               Close and erase the temporary file                         *
.****************************************************************************
.
CLER0000  MOVE      SP30,KEY9 
          CALL      RSTEMP1                      * position at start of file
          CALL      RKTEMP1                      * read next record
          BRANCH    OVRCD,CLER9999               * eof - finished
.
          PACK      KEY9,XADRAWRD,XADRABED,XADRUNIQ,SP10
          CALL      DETEMP1                      * delete current record
          GOTO      CLER0000                     * get next record
.
CLER9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by: MAIN0000  *
.*               Close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     ADMTEMP1                     * close file
.         
          CLEAR     CMDLINE
          MOVE      "iserase ",KEY8
          PACK      CMDLINE,KEY8,TMPFILE1        * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.******************************************************************************
.*                                  HEAD0000                                  *
.*                           Print The Report Header                          *
.******************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          CALL      HEAD132                 * Print the report header
          PRINT     *1,HEADDES1,*36,HEADDES2,*N
          PRINT     *N;
          MATCH     SP10,REPTDATE
          GOTO      HEAD9999 IF EQUAL
.
          PRINT     *3,"WARD",*26,"BED   PATIENT NAME",*95,"BIRTH DATE":
                    *108,"ADMISSION",*121,"MEMBER No",*N,UNDLN132
.
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000                                  *
.*                                Print A Line                                *
.******************************************************************************
PRNT0000  COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS    * Print the report header
.
          RESET     PRNTLINE
          PRINT     *1,PRNTLINE
.
          ADD       ONE,CLNO
          PACK      PRNTLINE,SP70,SP70
          CLEAR     PRNTLINE
.
PRNT9999  RETURN
+
.******************************************************************************
.                                    ZIPEXT00
.                        Execute "zip" to create Zip file
.******************************************************************************
ZIPEXT00  MOVE      ZERO,EXIT
          PACK      LCMDLINE,SP70,SP70,SP70
.                                           * set up Zip File Name
ZIPEXT10  PACK      FILENAMX,EXTRDATE,USC,PTHSAPPR,USC,EXTRTYPE,SP30
          SQUEEZE   FILENAMX

          PACK      PATHFILE,PATHNAME,FILENAMX,SP70    * no suffix ".txt"
          SQUEEZE   PATHFILE
.
.
. find . -name "?yyyymmdd*.txt" | xargs zip  -jm  path_name/yyyymmdd_provider_pr
.     where "find . " means "find pathname "
.
ZIPEXT50  CLEAR     LCMDLINE   
          APPEND    "find ",LCMDLINE
          APPEND    PATHNAME,LCMDLINE
          APPEND    " -name #"",LCMDLINE
          APPEND    FILENAMX,LCMDLINE
          APPEND    ".*#" | ",LCMDLINE
          APPEND    "xargs zip -jm ",LCMDLINE
          APPEND    PATHFILE,LCMDLINE
          RESET     LCMDLINE
.
          CLEAR     PRNTLINE
          APPEND    "Creating '.zip' file -     ",PRNTLINE
          APPEND    PATHFILE,PRNTLINE
          APPEND    ".zip",PRNTLINE
          CALL      PRNT0000
          CLEAR     PRNTLINE                                 * test stuff
          CALL      PRNT0000                                 * test stuff
          MOVE      "Zip command =  ",DIM15A                 * test stuff
          PACK      PRNTLINE,DIM15A,LCMDLINE                 * test stuff
          CALL      PRNT0000                                 * test stuff
.
          EXECUTE   LCMDLINE,TASKID         * EXECUTE zip command
          DISPLAY   *P1:23,LCMDLINE,*W3
.
ZIPEXT60  MATCH     "0       ",TASKID
          IF        !@EQUAL
            CLEAR     PRNTLINE
            APPEND    "Error when zipping txt & ctl Files - ",PRNTLINE
            APPEND    TASKID,PRNTLINE
            CALL      PRNT0000
            GOTO      ZIPEXT99 
          ENDIF
.                                           * set up "send" script
ZIPEXT70  PACK      LCMDLINE,SP70,SP70,SP70
          CLEAR     LCMDLINE
          APPEND    "ibapms94.us1 ",LCMDLINE
          APPEND    FILENAMX,LCMDLINE
          APPEND    ".zip",LCMDLINE
          RESET     LCMDLINE
          EXECUTE   LCMDLINE,TASKID         * EXECUTE script 'ibapms94.us1'
.
          MATCH     "0       ",TASKID
          IF        !@EQUAL
            CLEAR     PRNTLINE
            APPEND    "Error when accessing script 'ibapms94.us1' - ",PRNTLINE
            APPEND    TASKID,PRNTLINE
            CALL      PRNT0000
          ENDIF
.
.
ZIPEXT99  RETURN
+
.******************************************************************************
.                        IO routines for Tempfiles          
.******************************************************************************
RSTEMP1   READ      ADMTEMP1,KEY9;;             
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    ADMTEMP1;XADRAWRD,XADRABED,XADRUNIQ,XADRNAME,XADRPDOB:
                             XADRADAT,XADRMEMB,XADRWRDD,XADRSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     ADMTEMP1,KEY9;XADRAWRD,XADRABED,XADRUNIQ,XADRNAME,XADRPDOB:
                             XADRADAT,XADRMEMB,XADRWRDD,XADRSPAR
          RETURN
.
DETEMP1   DELETE    ADMTEMP1,KEY9
          RETURN
+
.==============================================================================
.
          INC       STD001IO
...       INC       STDHLPCD
...       INC       STDKWSCD
.
          INC       CLPATMAS
          INC       CLPATDSC
          INC       CLPMSPX2
          INC       IBAPRINT
          INC       PATHSPKY
          INC       PATDDHRD
          INC       IBAPRNIO/INC
.
          INC       PATCODIO/INC
          INC       PATDDHIO/INC
          INC       PATFN1IO/INC
          INC       PATHSPIO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC            * Visit extension read by Admission
          INC       PATMA1IO/INC
          INC       PATWR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATECDIO/INC
+

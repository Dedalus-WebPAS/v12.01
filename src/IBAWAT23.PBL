. ********************************************************************
. * System     :    Waiting Lists                                    *
. * Program    :    IBAWAT23                                         *
. * Desc.      :    Multiple Booking Report                          *
. * Function   :    List of patients who are currently               *
. *            :    waiting for more than one treatment              *
. ********************************************************************
. * Date       :    12/10/1988                                       *
. * Author     :    N.S.                                             *
. * Comments   :                                                     *
. * Mods.      :                                                     *
.*********************************************************************
. * V11.02.01 10/02/2022  Thanh T          TSK 0889899               *
. *           Recompiled as WATTR1FD changes                         *
.*********************************************************************
. *       V10.05.01 05/01/2015  Ebon Clements     CAR 261630         *
. *                 Removed port number from temp file name          *
. *       V5.10.B01 27/03/2001  Glenn Saunders                       *
. *                 Remove access to PTCNSNDX & associated processing*
. *                 Remove all references to PATSUR file (old s/name)*
. *       V5.08.01  21/08/2000  Caleb Sharman                        *
. *                 Changed Health Fund variables to be 8 chars      *
. *       V5.01     03.06.89 S.Barcham  SRF 100809                   *
. *                 Recompiled for locking in PATIOMAS               *
. *                 06/06/89 Graeme Williams SRF 4705                *
. *                 Allow for new PATSRCH include                    *
. *       V5.02     08/06/89 M.Ohleiter                              *
. *                 New Zealand changes                              *
. *       V5.01.01  19/06/89 M.Ohleiter                              *
. *                 Changes for new release 5.01                     *
. *                 09/07/89 J.Clark                                 *
. *                 Added Key26 variable added to                    *
. *                 PATIODOC by SOG                                  *
. *       V5.01.02  25/07/90 Paul Duncan                             *
. *                 implemented second sur file                      *
. *       V5.01.10  02/08/90 Justin Coates                           *
. *                 Modified to suit new specs                       *
. *       V5.01.121 25/01/1991    Justin Coates                      *
. *                 Added BOKRC1FD/IO                                *
. ********************************************************************
.
. ------ FD includes needed ------
.
          INC       STD001FD                * common variables
.
          INC       BOKRC1FD/INC            * booking file
          INC       IBASEQFD/INC
          INC       OUTPREFD/INC
          INC       PATCODFD/INC               
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATGSRFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       TFILEVAR/INC
          INC       WATTR1FD/INC
          INC       WATCONFD/INC
          INC       WEBERRFD/INC
.
. ------ program variables ------
.
CMDLINE   DIM       50                      * executable line
CNTPATTL  FORM      5                       * counts number of patients
DESCOPT   DIM       25                      * option description in heading
FROMDATE  DIM       8                       * FROMdate ccyymmdd
FNAME1    DIM       8                       * file name built
SAVDONE   FORM      1                       * flag for printing saved details
TODATE    DIM       8                       * TOdate ccyymmdd
WMDESC33  DIM       33                      * proc. desc to write/display
.
. ------ the temp file variables spare copy ------
.
XWMURNO   DIM       8                       * printable UR number
XPRNAME   DIM       21                      * printable name
DATE10    DIM       10                      * date of birth
XWMDESC   DIM       33                      * printable description
XPRPRTY   DIM       7                       * printable priority
XDATE20   DIM       10                      * printable sch. date
XPRCLIN   DIM       10                      * printable clinic
XPRDOCT   DIM       12                      * printable doctor
.
.
. ------ print variables for line two ------
.
PRFROM    DIM       30                      * date from for print
PRTO      DIM       30                      * date to   for print
PRPRTY    DIM       7                       * priority
DATE20    DIM       10                      * scheduled admission date
PRCLIN    DIM       10                      * clinic
PRDOCT    DIM       12                      * doctor
.
.         Key for temporary index file
.
SRTKEY    DIM       21                      * key being used to temp file
.
. ------ program constants ------
.
CATWU     INIT      "WU"   * cate for unit
CATTP     INIT      "TP"             * cat for priority
DESCOPT1  INIT      "U/R Number Sequence"
DESCOPT2  INIT      "Surname Sequence"
PREFFROM  INIT      "From Date on List : "
PREFTO    INIT      "To   Date on List : "
PRINTLIM  FORM      "54"                    * when new page needed
SORT1     INIT      "U1-8,9-17,18-19"       * for UR number sequence
SORT2     INIT      "U20-39,1-8,9-17,18-19" * for Surname sequence
START     INIT      "Start"
WATEMP    IFILE     SQL, FIXED=130, KEY=SRTKEY
.
PRGID     INIT      "IBAWAT23"
PRGNAM    INIT      "Multiple Bookings Report"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  MAINLINE                                         *
.*        Controlling Logic                                          *
.*********************************************************************
ML0000    CALL      INIT0000                * open files     
.
ML0100    CALL      OPTN0000                * select options
          BRANCH    EXIT,ML9999             * exit chosen
.
          LOAD      DESCOPT,MOPTION,DESCOPT1,DESCOPT2 * display sub-heading
          MOVE      DESCOPT,CPHDROPT        * print sub-heading
          MOVE      " - ",KEY3
          PACK      KEY60,PRGNAM,KEY3,DESCOPT
          CALL      DISPLIN2
.
          BRANCH    MOPTION,ML1000,ML2000
.
. ---------------------------------
. ------ U/R number sequence ------
. ---------------------------------
.
ML1000    CALL      KDAT0000                * keyin from/to date
          BRANCH    EXIT,ML0100             * no to date entered
.
          CALL      CONTQST                 * ok to continue ?
          BRANCH    CEXIT,ML1500,ML1000,ML0100
.                          Yes     No    Cancel
.
ML1500    CALL      TEMP0000                * create tempfile 1
.
          CALL      PROC0000                * read wattrefd, write watemp
          BRANCH    EXIT,ML0100             * empty tempfile
.
          CALL      PRTT0000                * print
          GOTO      ML0100
.
. ------------------------------
. ------ Surname sequence ------
. ------------------------------
.
ML2000    CALL      KDAT0000                * keyin from/to date
          BRANCH    EXIT,ML0100             * no to date entered
.
          CALL      CONTQST                 * ok to continue ?
          BRANCH    CEXIT,ML2500,ML2000,ML0100
.                         Yes     No    Cancel
.
ML2500    CALL      TEMP0000                * create tempfile 2
.
          CALL      PROC0000                * read wattrefd, write watemp
          BRANCH    EXIT,ML0100             * empty tempfile
.
          CALL      PRTT0000                * print
          GOTO      ML0100
.
ML9999    CHAIN     PGM
          STOP
+
.*********************************************************************
.*                  INIT0000                                         *
.*        Initialisation , opening files                             *
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A2,"wattr1af"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY7;*122,WTCNTPUC,*185,WTCNTPDS
.
INIT9000  DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                                         *
.*        Option menu                                                *
.*********************************************************************
OPTN0000  CALL      DISPHEAD
          CALL      SCRO0000                       * display main optionscreen
          DISPLAY   *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION:
                    *P17:8,*DV,MOPTION
.
          COMPARE   ZERO,MOPTION            * was exit chosen ??
          GOTO      OPTN9000 IF EQUAL       * yes
.
          MOVE      FALSE,EXIT              * set exit status
          BRANCH    MOPTION,OPTN9999,OPTN9999
.
          BEEP
          GOTO      OPTN1000                * invalid entry
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  SCRO0000                                         *
.*        Main option-screen                                         *
.*********************************************************************
SCRO0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = U/R number Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Surname Sequence"
SCRO9999  RETURN
+
.*********************************************************************
.*                  KDAT0000                                         *
.*        Keyin from / to date                                       *
.*        date on list (=wmdate1) should be between these dates      *
.*********************************************************************
KDAT0000  DISPLAY   *P1:9,*EF:
                    *P1:10,"From Date on List : ":
                    *P1:11,"To   Date on List : "
.
. ------ enter the FROM date ------
.
KDAT1000  MOVE      "21",CCOL               * column for input
          MOVE      TEN,CVERT               * row for input
          MOVE      CCC,CCENT               * default to current century
          UNPACK    SP6,CYEAR,CMON,CDAY     * no default
.
          CALL      KEYDATE                 * enter the date
          BRANCH    OVRCD,KDAT1500          * nothing entered
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          CALL      PACDATE
          PACK      PRFROM,PREFFROM,CPCDATE * print from date
          GOTO      KDAT2000                * enter the to date
.
KDAT1500  PACK      FROMDATE,SP8            * only enter hit
          DISPLAY   *P21:10,*V2LON,START    * display start
          PACK      PRFROM,PREFFROM,START,SP5 * print from date
.
. ------ enter the TO date ------
.
KDAT2000  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      "21",CCOL
          MOVE      TEN1,CVERT
          MOVE      CCC,CCENT
.
          CALL      KEYDATE                 * enter the date
          BRANCH    OVRCD,KDAT9000          * nothing entered
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          CALL      PACDATE
.
. ------ see if TO date < FROM date : invalid ------
.
          MATCH     START,FROMDATE          * was start entered ??
          GOTO      KDAT5000 IF EQUAL       * yes, dont do check
.
          MATCH     FROMDATE,TODATE         * is TO date < FROM date ??
          GOTO      KDAT8000 IF LESS        * yes, invalid
.
KDAT5000  PACK      PRTO,PREFTO,CPCDATE     * print to date
.
          MOVE      FALSE,EXIT              * set exit status
          GOTO      KDAT9999
.
. ------ ERROR: TO date < FROM date ------
.
KDAT8000  DISPLAY   *P1:24,*B,"The TO date must be after the FROM date.  ",*EL;
          CALL      HITENTER
          GOTO      KDAT2000
.
KDAT9000  MOVE      TRUE,EXIT
.
KDAT9999  RETURN
+
.*********************************************************************
.*                  TEMP0000                                         *
.*        Open tempfile for U/R     sequence                         *
.*                          Surname sequence                         *
.*********************************************************************
TEMP0000  CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME1
.
. ----- Kill indexed tempfile -----
.
          CLOSE     WATEMP
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * delete the temp file
.
. ----- Create indexed tempfile -----
.
          DISPLAY   *P1:24,*V2LON,*BLINKON,"Please wait.",*EL
          LOAD      SRTKEY,MOPTION,SORT1,SORT2 * get the correct index
          CLEAR     CMDLINE                 * build the command line
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 130 ",CMDLINE
          APPEND    SRTKEY,CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create the temp file
          OPEN      WATEMP,FNAME1           * open the temp file
          DISPLAY   *P1:24,*EL
.
TEMP9999  RETURN
+
.*********************************************************************
.*                  PROC0000                     Called by : ML0000  *
.*        Read WATTR1FD only patients with treatment status =1       *
.*        Option 1 (=By U/R number)                                  *
.*        Option 2 (=By Surname)                                     *
.*********************************************************************
PROC0000  PACK      KEY27,FROMDATE,SP20     * key for positional read
          CALL      RDSTREA3                * positional read
          DISPLAY   *P1:24,"Obtaining the valid entries now.",*EL
.
. ------ next read on treatment file ------
.
PROC4000  CALL      RDKTREA3                * next read
          BRANCH    OVRCD TO PROC9999       * at end of search
.
          DISPLAY   *P70:24,*EL,WMURNO
.
          COMPARE   TWO,WMSTAT              * is the status two ??
          GOTO      PROC4000 IF NOT EQUAL   * status is not 2, read next record
.
          MATCH     WMDATE1,TODATE          * is date before to date ??
          GOTO      PROC9999 IF LESS        * no, date1 > todate therefore done
.
          PACK      KEY8,WMURNO             * UR number as key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,PROC4000          * invalid UR so ignore
.
. ------ write the temp file record ------
.
PROC5000  BRANCH    MOPTION,PROC5005,PROC5010
.
. ------ write using U/R number key ------
.
PROC5005  PACK      KEY19,WMURNO,WMCODE,DWMCNT
          RESET     KEY19
          MOVE      WMDESC,WMDESC33         * shorten desc
          WRITE     WATEMP,KEY19;WMURNO,WMCODE,DWMCNT,PSNAME,PGNAME,PTITL:
                                 PBDATE,WMDESC33,WMPTY,WMDATE3,WMUNIT:
                                 WMDOCTOR
.
          DISPLAY   *P70:24,*EL,*V2LON,WMURNO
          GOTO      PROC4000
.
. ------ write using surname key ------
.
PROC5010  PACK      KEY39,PSNAME,WMURNO,WMCODE,DWMCNT
          RESET     KEY39
          MOVE      WMDESC,WMDESC33         * shorten desc
          WRITE     WATEMP,KEY39;WMURNO,WMCODE,DWMCNT,PSNAME,PGNAME,PTITL:
                                 PBDATE,WMDESC33,WMPTY,WMDATE3,WMUNIT:
                                 WMDOCTOR
.
          DISPLAY   *P70:24,*EL,*V2LON,WMURNO
          GOTO      PROC4000
.
PROC9999  RETURN
+
.*********************************************************************
.*                  PRTT0000                                         *
.*        Produce the report                                         *
.*        Only display the people with more than one booking         *
.*********************************************************************
PRTT0000  MOVE      ZERO,CNTPATTL          * clear total count
          MOVE      ZERO,CPAGENO           * initialize page counter
          CALL      IBACLOCK               * get current date and time
          MOVE      "MULTIPLE BOOKINGS REPORT -",PRGNAM  * set up report name
          DISPLAY   *P1:24,"Producing the report now.",*EL
.
. ------ do the initial read on the temp file ------
.
PRTT1000  BRANCH    MOPTION,PRTT1100,PRTT1200
.
. ------ initial read on U/R number index ------
.
PRTT1100  PACK      KEY19,SP20
          READ      WATEMP,KEY19;;          * positional read
          GOTO      PRTT1500
.
. ------ initial read on surname index ------
.
PRTT1200  PACK      KEY39,SP20,SP20
          READ      WATEMP,KEY39;;          * positional read
.
. -------------------------------------------------
. ------ next read on temp file : number one ------
. -------------------------------------------------
.
PRTT1500  READKS    WATEMP;WMURNO,WMCODE,DWMCNT,PSNAME,PGNAME,PTITL:
                           PBDATE,WMDESC33,WMPTY,WMDATE3,WMUNIT:
                           WMDOCTOR
          GOTO      PRTT7000 IF OVER
          MOVE      DWMCNT,WMCNT
.
          CALL      NEWP0000                * display heading
.
. ------ save the values just read in ------
.
PRTT2000  CALL       SAVE0000               * save the values 
.
. -------------------------------------------------
. ------ next read on temp file : number two ------
. -------------------------------------------------
.
PRTT4000  READKS    WATEMP;WMURNO,WMCODE,DWMCNT,PSNAME,PGNAME,PTITL:
                           PBDATE,WMDESC33,WMPTY,WMDATE3,WMUNIT:
                           WMDOCTOR
          GOTO      PRTT9000 IF OVER
          MOVE      DWMCNT,WMCNT
.
. ------ see if it is a different UR number ------
.
PRTT5400  MATCH     WMURNO,XWMURNO          * are the UR numbers the same ??
          GOTO      PRTT2000 IF NOT EQUAL   * no, store details,get next person
.
. ------ have a patient with at least two bookings ------
. ------ display the first two bookings            ------
. ------ print the line but first see if it will fit on page ------
.
          COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      PRTT6000 IF NOT LESS    * no
.
PRTT5600  CALL      NEWP0000                * print new page heading
.
PRTT6000  CALL      PPAT0000                * print the line
          GOTO      PRTT4000                * read next record
.
. ------ there is no data obtained for this report ------
.
PRTT7000  CALL      NEWP0000                * print a page heading
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values."
          GOTO      PRTT9050
.
. --------------------------------
. ------ report is finished ------
. --------------------------------
.
PRTT9000  PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients Printed : ",CNTPATTL
.
PRTT9050  PRINT     *N,*N,*N,"***  END OF REPORT ****"
.
. ----- Kill indexed tempfile -----
.
          CLOSE     WATEMP                  * close the file
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      "MULTIPLE BOOKINGS REPORT  ",PRGNAM  * set up program name
.
PRTT9999  RETURN
+
.*********************************************************************
.*                  FINF0000                    Called by : PROC0000 *
.*        Obtain and format all info on patient                      *
.*********************************************************************
FINF0000  
.
. ------ format the priority ------
.
          MOVE      SP20,TDESC              * clear tdesc
          PACK      KEY5,CATTP,WMPTY        * key of priority
          CALL      RDCODE1                 * read codes file
          MOVE      TDESC,PRPRTY            * set up priority
.
. ------ format the scheduled admission date ------
.
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE20
.
. ------ format the unit/clinic ------
.
          PACK      KEY5,CATWU,WMUNIT       * key of unit
          CALL      RDCODE1                 * read codes file
          PACK      PRCLIN,TDESC            * set up display var
.
. ------ format the doctors name ------
.
          PACK      KEY6,WMDOCTOR           * docs name as key
          CALL      FDOC0000                * get docs name
.
FINF9999  RETURN
+
.*********************************************************************
.*                  FDOC0000                    Called by : lots     *
.*        Obtain the doctors name and format it                      *
.*        Paras   : KEY6          contains the doctors code          *
.*        Returns : PRDOCT        doctors name (12 chars)            *
.*********************************************************************
FDOC0000  PACK      PRDOCT,SP20             * clear the variable
          CALL      RDDOCT1                 * read doct file
          BRANCH    OVRCD,FDOC9999          * invalid code
          MOVE      DGNAME,ANS              * given name
          MOVE      DSNAME,PACSNAME         * surname
          PACK      PRDOCT,ANS,DOT,DSNAME   * format the name
.
FDOC9999  RETURN
+
.*********************************************************************
.*                  NEWP0000                    Called by : PRTT0000 *
.*        Prints the new page heading for a report          DORP0000 *
.*        Para's  : CPHRDOPT      sub heading                        *
.*********************************************************************
NEWP0000  MOVE      ZERO,CNOUNDLN           * underlines at end of page
.
NEWP0500  CALL      HEAD132                 * do the new heading
.
          PRINT     *1,PRFROM:
                    *N,PRTO
.
. ------ print the table heading ------
.
          MOVE      SIX,CVERT               * set line counter
          CALL      PTBH0000                * print a table heading
.
NEWP9999  RETURN
+
.*********************************************************************
.*                  PPAT0000                    Called by : PRTT0000 *
.*        Prints a 1/2    data line of the report                    *
.*        ie. the information about a patients procedure             *
.*        Updates : CNTPATTL      count of total patients            *
.*                  CVERT         current row number                 *
.*********************************************************************
PPAT0000  BRANCH    SAVDONE,PPAT2500
.
. -------------------------------------------
. ------ print line one about patient ------
. -------------------------------------------
.
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,"| ",XWMURNO,*12,"| ",XPRNAME,*35,"| ",DATE10,*48,"| ":
                    XWMDESC,*83,"| ",XPRPRTY,*92,"| ",XDATE20,*105,"| ",XPRCLIN:
                    *117,"| ",XPRDOCT,*132,"|"
.
          DISPLAY   *P70:24,XWMURNO
          ADD       ONE,CVERT               * add to line counter
          ADD       ONE,CNTPATTL            * add to patient counter
          MOVE      ONE,SAVDONE             * indicate save details printed
.
. -------------------------------------------
. ------ print line two about patient ------
. -------------------------------------------
.
PPAT2500  CALL      FINF0000                * format the information
          PRINT     *1,"|",*12,"|",*35,"|",*48,"| ",WMDESC33,*83,"| ",PRPRTY:
                    *92,"| ",DATE20,*105,"| ",PRCLIN,*117,"| ",PRDOCT:
                    *132,"|"
.
          ADD       ONE,CVERT               * add to line counter
.
PPAT9999  RETURN
+
.*********************************************************************
.*                  PTBH0000                    Called by : NEWP0000 *
.*        Prints a new table heading                        PRTT0000 *
.*        Needed at a new page or for a new unit/clinic     DORP0000 *
.*        Paras/Returns : CVERT      current row number              *
.*********************************************************************
PTBH0000  PRINT     *N,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*1,"|",*3,"U/R No ",*12,"| Patient Name",*35,"|":
                    *39,"D.O.B.",*48,"| ",WTCNTPDS," Description",*83:
                    "|Priority| Scheduled  |Unit/Clinic| Doctor Name  |"
.
          ADD       THREE,CVERT             * add to line count
.
PTBH9999  RETURN
+
.*********************************************************************
.*                  SAVE0000                    Called by : PRTT0000 *
.*        Save the variables just read in                            *
.*********************************************************************
SAVE0000  MOVE      WMDESC33,XWMDESC        * save the proc description
          MOVE      WMURNO,XWMURNO          * save the UR number
          MOVE      ZERO,SAVDONE            * indicate details not printed
.
. ------ format the saved display variables ------
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE10
.
          MOVE      PSNAME,PACSNAME        * patients name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,XPRNAME
.
          MOVE      SP20,TDESC              * priority
          PACK      KEY5,CATTP,WMPTY
          CALL      RDCODE1
          MOVE      TDESC,XPRPRTY
.
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,XDATE20
.
          MOVE      SP20,TDESC
          PACK      KEY5,CATWU,WMUNIT       * unit/clinic description
          CALL      RDCODE1
          MOVE      TDESC,XPRCLIN
.
          PACK      KEY6,WMDOCTOR           * doctors name
          CALL      FDOC0000
          MOVE      PRDOCT,XPRDOCT
.
SAVE9999  RETURN
+
.
. ------ I/O includes needed ------
.
          INC       STD001IO       
          INC       TFILENAM
.
          INC       BOKRC1IO/INC            * booking file
          INC       WATTR1IO/INC
          INC       IBASEQIO/INC
          INC       OUTPREIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATGSRIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       WEBERRIO/INC 
................................................................................

.******************************************************************************
.* System         : Mental Health System                                      *
.* Program        : IBAMEH98.PBL                                              *
.* Name           : System Parameter Maintenance                              *
.******************************************************************************
.* Date           : 13/08/1991                                                *
.* Author         : Justin Coates                                             *
.* Function       : To maintain all system paramters for MEH                  *
.* Modifications  :                                                           *
.*        V9.02.01  15/10/2002  Jill Habasque                                 *
.*                  Added MHCNDVIS - Security level to display MH visits      *
.*        V5.03.03  04/01/1996  Greg Horvat                                   *
.*                  Removed the menu program, security key, IBA screen        *
.*                  password & data directory keyins & changed to use a       *
.*                  standard keyin for the IBA screen security password       *
.*        V5.03.02  18/12/1995    Justin Coates  (SUHT mods)                  *
.*                  Added MHCNUVOP                                            *
.*        V5.03.01  05/12/1995    Justin Coates  (SUHT mods)                  *
.*                  Added MHCNUDSA                                            *
.******************************************************************************
.*        V5.01.03  11/07/94   Whirlpool                                      *
.*                  Added MHCNKLER for the EDI extract to the IBA SCREEN      *
.*        V5.01.02  11/02/92   ROD                                            *
.*                  Fixed Lofty's speling of Hosptial too Hospital            *
.******************************************************************************
.
.         FD's required
.
          INC       STD001FD/PBL            * standard variables
          INC       IBACONFD/INC
          INC       MEHCONFD/INC            * control file
.
.         program variables
.
OPTION2   FORM      2         * submenu keyin
.
DESCYN01  DIM       3         * yes/no description 01
DESCYN02  DIM       3         * yes/no description 02
DESCYN03  DIM       3         * yes/no description 03
DESCYN04  DIM       3         * yes/no description 04
DESCYN05  DIM       3         * yes/no description 05
DESCYN06  DIM       3         * yes/no description 06
DESCYN07  DIM       3         * yes/no description 07
DESCYN08  DIM       3         * yes/no description 08
DESCYN09  DIM       3         * yes/no description 09
DESCYN10  DIM       3         * yes/no description 10
DESCYN11  DIM       3         * yes/no description 11
DESCYN12  DIM       3         * yes/no description 12
DESCYN13  DIM       3         * yes/no description 13
DESCYN14  DIM       3         * yes/no description 14
DESCYN15  DIM       3         * yes/no description 15
DESCYN16  DIM       3         * yes/no description 16
DESCYN17  DIM       3         * yes/no description 17
DESCYN18  DIM       3         * yes/no description 18
DESCYN19  DIM       3         * yes/no description 19
DESCYN20  DIM       3         * yes/no description 20
DESCYN21  DIM       3         * yes/no description 21
DESCYN22  DIM       3         * yes/no description 22
DESCYN23  DIM       3         * yes/no description 23
DESCYN24  DIM       3         * yes/no description 24
DESCYN25  DIM       3         * yes/no description 25
DESCYN26  DIM       3         * yes/no description 26
.
FORM8     FORM      8         * entered number for password
PASS      DIM       8         * entered password
REPFLAG   FORM      1
.
.         program constants
.
.
PRGID     INIT      "IBAMEH98"
PRGNAM    INIT      "System Parameter Maintenance"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000               * initialisation
.
ML0500    CALL      MENU0000
          BRANCH    EXIT,ML9999             * exit selected
          BRANCH    OPTION,ML1000,ML2000,ML3000
.
.         audit screen
.
ML1000    CALL      AUDT0000                * process audits
          GOTO      ML0500
.
.         hospital screens
.
ML2000    CALL      HOSP0000                * process hospital screens
          GOTO      ML0500
.
.         iba screens
.
ML3000    CALL      IBASECUR                * Keyin IBA screen security passwd
          BRANCH    EXIT,ML0500
.
          CALL      PIBA0000                * process iba screens
          GOTO      ML0500
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : xxxx0000 *
.*        Initialisation                                             *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening ",*EL;
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
+
.*********************************************************************
.*                  MENU0000                    Called by : ML0500   *
.*        Display the available options                              *
.*********************************************************************
MENU0000  HLINE     *G37,2,54,80
          MOVE      ZERO,REPFLAG
          DISPLAY   *P1:3,*EF,*V2LON:
                    *P2:4,"0",*P2:5,"1",*P2:6,"2",*P2:7,"3",*HOFF:
                    *P3:4,". Exit":
                    *P3:5,". Audits":
                    *P3:6,". Hospital screens":
                    *P3:7,". IBA screens":
                    *P1:9,"Select option : "
.
MENU1000  KEYIN     *P17:09,*DV,UNDLN2:
                    *P17:09,*V2LON,OPTION:
                    *P17:09,*DV,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      MENU9100 IF EQUAL       * exit
.
          BRANCH    OPTION,MENU9000,MENU9000,MENU9000
.
          BEEP
          GOTO      MENU1000
.
MENU9000  MOVE      ZERO,EXIT
          GOTO      MENU9999
.
MENU9100  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.*********************************************************************
.*                  AUDT0000                    Called by : ML1000   *
.*        Display and keyin all audit details                        *
.*********************************************************************
AUDT0000  DISPLAY   *P54:2,*V2LON,"- Audits ",*P1:3,*EF:
                    *P1:4," 1",*P1:5," 2",*P1:6," 3",*P1:7," 4",*P1:8," 5":
                    *P1:9," 6",*P1:10," 7",*P1:11," 8",*P1:12," 9",*P1:13,"10":
                    *P1:14,"11",*P1:15,"12",*P1:16,"13":
                    *P41:4,"14",*P41:5,"15",*P41:6,"16",*P41:7,"17",*P41:8,"18":
                    *P41:9,"19",*P41:10,"20",*P41:11,"21",*P41:12,"22":
                    *P41:13,"23",*P41:14,"24",*P41:15,"25",*P41:16,"26",*HOFF:
                    *P03:04,". Visit file         : ":
                    *P03:05,". Discharge file     : ":
                    *P03:06,". Discharge to leave : ":
                    *P03:07,". Axis codes         : ":
                    *P03:08,". Audit E (Not Used) : ":
                    *P03:09,". Audit F (Not Used) : ":
                    *P03:10,". Audit G (Not Used) : ":
                    *P03:11,". Audit H (Not Used) : ":
                    *P03:12,". Audit I (Not Used) : ":
                    *P03:13,". Audit J (Not Used) : ":
                    *P03:14,". Audit K (Not Used) : ":
                    *P03:15,". Audit L (Not Used) : ":
                    *P03:16,". Audit M (Not Used) : ":
                    *P43:04,". Audit N (Not Used) : ":
                    *P43:05,". Audit O (Not Used) : ":
                    *P43:06,". Audit P (Not Used) : ":
                    *P43:07,". Audit Q (Not Used) : ":
                    *P43:08,". Audit R (Not Used) : ":
                    *P43:09,". Audit S (Not Used) : ":
                    *P43:10,". Audit T (Not Used) : ":
                    *P43:11,". Audit U (Not Used) : ":
                    *P43:12,". Audit V (Not Used) : ":
                    *P43:13,". Audit W (Not Used) : ":
                    *P43:14,". Audit X (Not Used) : ":
                    *P43:15,". Audit Y (Not Used) : ":
                    *P43:16,". Audit Z (Not Used) : "
.
.         display the valid codes
.
          READ      CONTROLF,SIXTY9;*43,MHCNAUDA,MHCNAUDB,MHCNAUDC,MHCNAUDD:
                                        MHCNAUDE,MHCNAUDF,MHCNAUDG,MHCNAUDH:
                                        MHCNAUDI,MHCNAUDJ,MHCNAUDK,MHCNAUDL:
                                        MHCNAUDM,MHCNAUDN,MHCNAUDO,MHCNAUDP:
                                        MHCNAUDQ,MHCNAUDR,MHCNAUDS,MHCNAUDT:
                                        MHCNAUDU,MHCNAUDV,MHCNAUDW,MHCNAUDX:
                                        MHCNAUDY,MHCNAUDZ
.
          MOVE      DYES,DESCYN01
          LOAD      DESCYN01,MHCNAUDA,DNO
          DISPLAY   *P26:4,*V2LON,DESCYN01
.
          MOVE      DYES,DESCYN02
          LOAD      DESCYN02,MHCNAUDB,DNO
          DISPLAY   *P26:5,*V2LON,DESCYN02
.
          MOVE      DYES,DESCYN03
          LOAD      DESCYN03,MHCNAUDC,DNO
          DISPLAY   *P26:6,*V2LON,DESCYN03
.
          MOVE      DYES,DESCYN04
          LOAD      DESCYN04,MHCNAUDD,DNO
          DISPLAY   *P26:7,*V2LON,DESCYN04
.
.         Select item
.
AUDT1000  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      AUDT8000 IF EQUAL       * post
          GOTO      AUDT9999 IF LESS        * cancel
.
          BRANCH    CCITEM,AUDT2000,AUDT2000,AUDT2000,AUDT2000
.
          BEEP
          GOTO      AUDT1000
.
.         get the line and column number for keyin
.
AUDT2000  LOAD      CVERT,CCITEM,FOUR,FIVE,SIX,SEVEN,EIGHT:
                                 NINE,TEN,TEN1,TEN2,TEN3:
                                 TEN4,TEN5,TEN6:
                                 FOUR,FIVE,SIX,SEVEN,EIGHT:
                                 NINE,TEN,TEN1,TEN2,TEN3:
                                 TEN4,TEN5,TEN6
.
          LOAD      CCOL,CCITEM,TWENTY6,TWENTY6,TWENTY6,TWENTY6,TWENTY6:
                                TWENTY6,TWENTY6,TWENTY6,TWENTY6,TWENTY6:
                                TWENTY6,TWENTY6,TWENTY6:
                                SIXTY6,SIXTY6,SIXTY6,SIXTY6,SIXTY6:
                                SIXTY6,SIXTY6,SIXTY6,SIXTY6,SIXTY6:
                                SIXTY6,SIXTY6,SIXTY6
.
AUDT2500  KEYIN     *PCCOL:CVERT,*DV,UNDLN1,*DV,SP2:
                    *PCCOL:CVERT,*V2LON,ANS
          REP       UPPLOW,ANS
.
          MATCH     "Y",ANS
          GOTO      AUDT2600 IF EQUAL       * Yes entered
.
          MATCH     "N",ANS
          GOTO      AUDT2700 IF EQUAL       * No entered
.
          BEEP
          GOTO      AUDT2500
.
AUDT2600  MOVE      ZERO,FORM1              * set as Yes = 0
          DISPLAY   *PCCOL:CVERT,*V2LON,DYES
          GOTO      AUDT3000
.
AUDT2700  MOVE      ONE,FORM1               * set as No = 1 
          DISPLAY   *PCCOL:CVERT,*V2LON,DNO
.
AUDT3000  STORE     FORM1,CCITEM,MHCNAUDA,MHCNAUDB,MHCNAUDC,MHCNAUDD:
                                 MHCNAUDE,MHCNAUDF,MHCNAUDG,MHCNAUDH:
                                 MHCNAUDI,MHCNAUDJ,MHCNAUDK,MHCNAUDL:
                                 MHCNAUDM,MHCNAUDN,MHCNAUDO,MHCNAUDP:
                                 MHCNAUDQ,MHCNAUDR,MHCNAUDS,MHCNAUDT:
                                 MHCNAUDU,MHCNAUDV,MHCNAUDW,MHCNAUDX:
                                 MHCNAUDY,MHCNAUDZ
          GOTO      AUDT1000
.
.         Post selected
.
AUDT8000  WRITAB    CONTROLF,SIXTY9;*43,MHCNAUDA,MHCNAUDB,MHCNAUDC,MHCNAUDD:
                                        MHCNAUDE,MHCNAUDF,MHCNAUDG,MHCNAUDH:
                                        MHCNAUDI,MHCNAUDJ,MHCNAUDK,MHCNAUDL:
                                        MHCNAUDM,MHCNAUDN,MHCNAUDO,MHCNAUDP:
                                        MHCNAUDQ,MHCNAUDR,MHCNAUDS,MHCNAUDT:
                                        MHCNAUDU,MHCNAUDV,MHCNAUDW,MHCNAUDX:
                                        MHCNAUDY,MHCNAUDZ
          DISPLAY   *P60:24,*EL,*V2LON,"*** Posted ***",*W2;
AUDT9999  RETURN
+
.*********************************************************************
.*                  HOSP0000                    Called by : ML2000   *
.*        Controls all inputs for Hospital controlled screens        *
.*********************************************************************
HOSP0000  DISPLAY   *P54:2,*V2LON,"- Hospital screens ",*P1:3,*EF,*V2LON:
                    *P1:4," 0",*P1:5," 1",*HOFF:
                    *P3:4,". Exit":
                    *P3:5,". Screen 1":
                    *P1:7,"Select option : "
.
HOSP0500  KEYIN     *P17:7,*DV,UNDLN2:
                    *P17:7,*V2LON,OPTION2:
                    *P17:7,*DV,OPTION2
.
          COMPARE   ZERO,OPTION2
          GOTO      HOSP9999 IF EQUAL
.
          BRANCH    OPTION2,HOSP1000
.
          BEEP
          GOTO      HOSP0500
.
.         screen 1
.
HOSP1000  CALL      DHSA0000                * display screen
          CALL      KHSA0000                * keyin values
          BRANCH    EXIT,HOSP0000           * cancel selected
.
.         post the new values
.
          WRITAB    CONTROLF,SIXTY9;*70,MHCNSUPL,MHCNMINH,MHCNMINT,MHCNMAXH:
                                        MHCNMAXT,*82,MHCNDVIS
          DISPLAY   *P60:24,*EL,*V2LON,"*** Posted ***",*W2;
          GOTO      HOSP0000
.
HOSP9999  RETURN
+
.*********************************************************************
.*                  PIBA0000                    Called by : ML3000   *
.*        Controls all inputs for IBA controlled screens             *
.*********************************************************************
PIBA0000  DISPLAY   *P54:2,*V2LON,"- IBA screens ",*P1:3,*EF,*V2LON:
                    *P1:4," 0",*P1:5," 1",*HOFF:
                    *P3:4,". Exit":
                    *P3:5,". Screen 1":
                    *P1:7,"Select option : "
.
PIBA0500  KEYIN     *P17:7,*V2LON,OPTION2:
                    *P17:7,*DV,OPTION2
.
          COMPARE   ZERO,OPTION2
          GOTO      PIBA9999 IF EQUAL
.
          BRANCH    OPTION2,PIBA1000
.
          BEEP
          GOTO      PIBA0500
.
.         screen 1
.
PIBA1000  CALL      DIBA0000                * display screen
          CALL      KIBA0000                * keyin values
          BRANCH    EXIT,PIBA0000           * cancel selected
          CALL      WIBA0000                * write details
          GOTO      PIBA0000
.
PIBA9999  RETURN
+
.*********************************************************************
.*                  DHSA0000                    Called by : HOSP1000 *
.*        Display all fields and data for hospital screen A          *
.*********************************************************************
DHSA0000  READ      CONTROLF,SIXTY9;*70,MHCNSUPL,MHCNMINH,MHCNMINT,MHCNMAXH:
                                        MHCNMAXT,*82,MHCNDVIS
.
          DISPLAY   *P1:3,*EF,*V2LON:
                    *P1:4," 1",*P1:5," 2",*P1:6," 3",*P1:7," 4",*P1:8," 5":
                    *P1:9," 6",*HOFF:
                    *P3:4,". Minimum security level to see suppressed":
                    " diagnosis  : ":
                    *P3:5,". Minimum number of days for holiday leave":
                    "            : ":
                    *P3:6,". Minimum number of days for trial leave  ":
                    "            : ":
                    *P3:7,". Maximum number of months for holiday leave":
                    "          : ":
                    *P3:8,". Maximum number of years for trial leave ":
                    "            : ":
                    *P3:9,". Minimum security level to see MH visits ":
                    " in PATWEB98: " 
.
.         display actual values
.
          DISPLAY   *P59:4,*V2LON,MHCNSUPL,*P59:5,MHCNMINH,*P59:6,MHCNMINT:
                    *P59:7,MHCNMAXH,*P59:8,MHCNMAXT,*P59:9,MHCNDVIS
.
DHSA9999  RETURN
+
.*********************************************************************
.*                  KHSA0000                    Called by : HOSP1000 *
.*        Keyin any changes to data   for hospital screen A          *
.*********************************************************************
KHSA0000  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      KHSA9900 IF EQUAL       * post
          GOTO      KHSA9910 IF LESS        * cancel
.
          BRANCH    CCITEM,KHSA0100,KHSA0200,KHSA0300,KHSA0400,KHSA0500:
                           KHSA0600
.
          BEEP
          GOTO      KHSA0000
.
.         minimum security level to see suppressed diagnosis
.
KHSA0100  KEYIN     *P59:4,*DV,UNDLN1:
                    *P59:4,*V2LON,MHCNSUPL
          GOTO      KHSA0000
.
.         minimum number of days for holiday leave
.
KHSA0200  KEYIN     *P59:5,*DV,UNDLN2:
                    *P59:5,*V2LON,MHCNMINH:
                    *P59:5,*DV,MHCNMINH
. 
          COMPARE   ZERO,MHCNMINH
          GOTO      KHSA0000 IF NOT LESS    * valid amount
.
          BEEP
          GOTO      KHSA0200
.
.         minimum number of days for trial leave
.
KHSA0300  KEYIN     *P59:6,*DV,UNDLN2:
                    *P59:6,*V2LON,MHCNMINT:
                    *P59:6,*DV,MHCNMINT
. 
          COMPARE   ZERO,MHCNMINT
          GOTO      KHSA0000 IF NOT LESS    * valid amount
.
          BEEP
          GOTO      KHSA0300
.
.         Maximum number of months for holiday leave
.
KHSA0400  KEYIN     *P59:7,*DV,UNDLN2:
                    *P59:7,*V2LON,MHCNMAXH:
                    *P59:7,*DV,MHCNMAXH
. 
          COMPARE   ZERO,MHCNMAXH
          GOTO      KHSA0000 IF NOT LESS    * valid amount
.
          BEEP
          GOTO      KHSA0400
.
.         Maximum number of months for holiday leave
.
KHSA0500  KEYIN     *P59:8,*DV,UNDLN2:
                    *P59:8,*V2LON,MHCNMAXT:
                    *P59:8,*DV,MHCNMAXT
. 
          COMPARE   ZERO,MHCNMAXT
          GOTO      KHSA0000 IF NOT LESS    * valid amount
.
          BEEP
          GOTO      KHSA0500
.
.         Minimum security level to view MH visits in PATWEB98
.
KHSA0600  KEYIN     *P59:9,*DV,UNDLN2:
                    *P59:9,*V2LON,MHCNDVIS:
                    *P59:9,*DV,MHCNDVIS
.
          COMPARE   NINTY9,MHCNDVIS
          GOTO      KHSA0000 IF LESS    * valid amount
.
          BEEP
          GOTO      KHSA0600
.
.         set exit flags
.
KHSA9900  MOVE      ZERO,EXIT               * POST
          GOTO      KHSA9999
.
KHSA9910  MOVE      ONE,EXIT                * CANCEL
.
KHSA9999  RETURN
+
.*********************************************************************
.*                  DIBA0000                    Called by : PIBA1000 *
.*        Display all fields and data for IBA screen A               *
.*********************************************************************
DIBA0000  READ      CONTROLF,SIXTY9;*69,MHCNUSE,*79,MHCNKLER,*80,MHCNUDSA:
                                    MHCNUVOP
.
          DISPLAY   *P1:3,*EF,*V2LON:
                    *P2:4,"1",*P1:5," 2",*P1:6," 3",*P1:7," 4",*P1:8," 5":
                    *P1:9," 6",*P1:10," 7",*P1:11," 8",*HOFF:
                    *P3:04,". (No longer used)                           : ":
                    *P3:05,". (No longer used)                           : ":
                    *P3:06,". (No longer used)                           : ":
                    *P3:07,". (No longer used)                           : ":
                    *P3:08,". Using Mental Health System                 : ":
                    *P3:09,". Keying in Leave End Reasons in IBABIL01 Y/N: ":
                    *P3:10,". Using Diagnosis Screen in Admissions    Y/N: ":
                    *P3:11,". Using Valid Operators Security          Y/N: "
.
.         display the data
.
          MOVE      DNO,DESCYN01
          LOAD      DESCYN01,MHCNUSE,DYES

          MOVE      DNO,DESCYN02
          LOAD      DESCYN02,MHCNKLER,DYES
          MOVE      DNO,DESCYN07
          LOAD      DESCYN07,MHCNUDSA,DYES
          MOVE      DNO,DESCYN08
          LOAD      DESCYN08,MHCNUVOP,DYES
.
.
          DISPLAY   *P50:8,*V2LON,DESCYN01,*P50:9,DESCYN02:
                    *P50:10,DESCYN07,*P50:11,DESCYN08
.
DIBA9999  RETURN
+
.*********************************************************************
.*                  KIBA0000                    Called by : PIBA1000 *
.*        Keyin any changes to data   for IBA screen A               *
.*********************************************************************
KIBA0000  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      KIBA9900 IF EQUAL       * post
          GOTO      KIBA9910 IF LESS        * cancel
.
          BRANCH    CCITEM,KIBA0100,KIBA0200,KIBA0300,KIBA0400,KIBA0500:
                           KIBA0600,KIBA0700,KIBA0800
.
          BEEP
          GOTO      KIBA0000
.
KIBA0100  GOTO      KIBA0000
.
KIBA0200  GOTO      KIBA0000
.
KIBA0300  GOTO      KIBA0000
.
KIBA0400  GOTO      KIBA0000
.
.         using mental health system ?
.
KIBA0500  KEYIN     *P50:8,*DV,UNDLN1,*EL:
                    *P50:8,*V2LON,ANS
          REP       UPPLOW,ANS
.
          MATCH     "Y",ANS
          GOTO      KIBA0510 IF EQUAL       * yes
.
          MATCH     "N",ANS
          GOTO      KIBA0520 IF EQUAL       * no
.
          BEEP
          GOTO      KIBA0500
.
KIBA0510  MOVE      ONE,MHCNUSE             * set to 1 = Yes
          MOVE      DYES,DESCYN01
          GOTO      KIBA0590
.
KIBA0520  MOVE      ZERO,MHCNUSE            * set to 0 = No
          MOVE      DNO,DESCYN01
.
KIBA0590  DISPLAY   *P50:8,*V2LON,DESCYN01
          GOTO      KIBA0000
.
. Keyin leave end reason in BIL01
.
KIBA0600  MOVE      "50",CCOL
          MOVE      "09",CVERT
          CALL      KYN0000
          MOVE      EXIT,MHCNKLER
          GOTO      KIBA0000
.
. using diagnosis screen in admissions
.
KIBA0700  MOVE      "50",CCOL
          MOVE      "10",CVERT
          CALL      KYN0000
          MOVE      EXIT,MHCNUDSA
          GOTO      KIBA0000
.
. using valid operator security file (mehvopaf)
.
KIBA0800  MOVE      "50",CCOL
          MOVE      "11",CVERT
          CALL      KYN0000
          MOVE      EXIT,MHCNUVOP
          GOTO      KIBA0000
.
.         set exit flags
.
KIBA9900  MOVE      ZERO,EXIT               * POST
          GOTO      KIBA9999
.
KIBA9910  MOVE      ONE,EXIT                * CANCEL
.
KIBA9999  RETURN
+
.*********************************************************************
.         write the details for IBA Screen 1
.*********************************************************************
WIBA0000  WRITAB    CONTROLF,SIXTY9;*69,MHCNUSE,*79,MHCNKLER,*80,MHCNUDSA:
                                    MHCNUVOP
          DISPLAY   *P60:24,*EL,*V2LON,"*** Posted ***",*W2;
WIBA9999  RETURN
+
.------------------------------------------------------------------------------
.         Yes/No standard keyin
.------------------------------------------------------------------------------
KYN0000   KEYIN     *PCCOL:CVERT,*DV,SP3:    * clear previous display
                    *PCCOL:CVERT,*V2LON,ANS:
                    *PCCOL:CVERT,*DV,ANS;
.
          TYPE      ANS                      * check if a number was input
          GOTO      KYN9000 IF EQUAL         * yes, invalid
.
          REP       "Y1y1N0n0",ANS           * convert valid answers to numerics
.
          TYPE      ANS                      * check if now a number
          GOTO      KYN9000 IF NOT EQUAL     * no, invalid
.
          MOVE      ANS,EXIT                 * set exit to Y/N value
          MOVE      DNO,KEY3                * assume answer was "No "
          LOAD      KEY3,EXIT,DYES,DNO      * unless otherwise stated
          DISPLAY   *PCCOL:CVERT,*V2LON,KEY3
          GOTO      KYN9999
.
KYN9000   BEEP
          GOTO      KYN0000
.
KYN9999   RETURN
.
.
.         I/O's required
.
          INC       STD001IO/PBL            * all standard routines
          INC       IBASECUR                * Keyin IBA screen security passwd
...............................................................................

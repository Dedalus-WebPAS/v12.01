. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAADT94                                            *
. *                                                                           *
. *     FUNCTION:         PATIENT CASEMIX STATUS LISTING                      *
. *                                                                           *
. *     DATE WRITTEN:     16/03/04                                            *
. *                                                                           *
. *     AUTHOR:           GUOMIN FU                                           *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *       V10.13.01 05/12/2018  Don Nguyen       TSK 0838558                  *
. *                 Deleted temp files (PSTROL & PSTROL1) after processing.   *
. *                 Removed PORT from temp filenames (FNAMER, FNAMET, etc).   *
. *       V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
. *                 Added pmsvx1af                                            *
. *       V9.03.00  28/04/2004  Guomin Fu  CAR 49320                          *
. *                 Added "total" logic for each heatlh                       *
. *                 fund in CALSTA00                                          *
. *                 16/03/2004  Guomin Fu                                     *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATMA1FD/INC
          INC       HL7BMTFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATCODFD/INC
.
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.        	WORK AREA
.
PSTROL     FILE      ASCII, FIXED=256
PSTROL1    FILE      ASCII, FIXED=256
SORTSTR    DIM       20
SORT1      INIT      "uc1-8,104-112"
SORT2      INIT      "uc9-28,104-112"
SORT3      INIT      "uc87-92,104-112"
TEMPFILE   IFILE     SQL, FIXED=112, KEY=SORTSTR
TEMP1      IFILE     SQL, FIXED=112, KEY=SORT3
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
XDAADMNO        DIM        8          8             1
.PSNAME         DIM       20         20             9
.PGNAME         DIM       25         25            29
.ADMDATE        DIM       10         10            54
.DIM15          DIM       15         15            64
.AURNO          DIM        8          8            79
.HFNAME         DIM        6          6            87
.CMXYN          DIM        1          1            93
.DISDATE        DIM       10         10            94
DUNIQUE         DIM        8          8           104
. ----- EOR 112-----
.
.
A          INIT      "A "
ADMDATE    DIM       10
.
CMDLINE    DIM       50
CONAME     DIM       35
.
DALERG     DIM       3
DANS       DIM       3
DAYCASE    DIM       3
DHOSP      DIM       3
DILLN      DIM       3
DIM2A      DIM       2
DIM15      DIM       15
DISDATE    DIM       10
HFNAME     DIM       6
CMXYN      DIM       1
.
FNAMET     DIM       8
FNAMET1    DIM       8
FNAMER     DIM       8
FNAMER1    DIM       8
FROMDATE   DIM       8        * FROM DATE IN (CCYYMMDD) FORMAT
.
.
.
LNO        FORM      2
.
OPCND      FORM      1
.
PAGENO     FORM      3
PATNUM     FORM      5
.
SEC        FORM      2
SP12       INIT      "            "
SP14       INIT      "              "
SP28       INIT      "                            "
STATUS     FORM      1
SAVHFUND   DIM       6
.
.
TDATE      DIM       8        * ACTUAL TRANSLATED DATE (CCYYMMDD)
TODATE     DIM       8        * TO DATE IN (CCYYMMDD) FORMAT
TOTCMXY    FORM      3
TOTCMXN    FORM      3
HFCMXY     FORM      3
HFCMXN     FORM      3
.
UNIQUE     FORM      8
.
XDAY1      DIM       2
XDAY2      DIM       2
XMON1      DIM       2
XMON2      DIM       2
XYEAR1     DIM       2
XYEAR2     DIM       2
XCENT1     DIM       2
XCENT2     DIM       2
.
Z6        INIT       "ZZZZZZ"
.
PRGID     INIT      "IBAADT94"
PRGNAM    INIT      "PATIENT CASEMIX STATUS LISTING"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL          DISPHEAD
          DISPLAY       *P56:24,"Opening patmi1af";
          OPEN          PATMI1A3,"patmi1af"
          OPEN          PMSVX1A1,"pmsvx1af"
.
          DISPLAY       *P64:24,"patma1af";
          OPEN          PATMA1A1,"patma1af"
.
          DISPLAY       *P64:24,"patmx1af";
          OPEN          PATMX1A1,"patmx1af"
.
          DISPLAY       *P64:24,"patdo1af";
          OPEN          PATDO1A1,"patdo1af"
.
          DISPLAY       *P64:24,"patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"patdschf";
          OPEN          PATDSCH1,"patdschf"
.
OPFIL     DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
.         READ      CONTROLF,EIGHTY;*1,HLBMDGUP,HLBMDGUA,HLBMDGMU:
.                                      HLBMDGUU:
.                                 *105,HLBMDGAD,HLBMDGDS,HLBMDGTR:
.                                      HLBMDGRG
          READ          CONTROLF,ZERO;*2,CDD,CMM,CYY
          READ          CONTROLF,TWO;*4,CONAME
.
.       MOVE          PORT TO DIM2A
.
.       CLEAR           FNAMER
.       APPEND          "cmxrol",FNAMER
.       APPEND          DIM2A,FNAMER
.       RESET           FNAMER
.       REP             " 0" IN FNAMER
.
.       CLEAR           FNAMET
.       APPEND          "cmxidx",FNAMET
.       APPEND          DIM2A,FNAMET
.       RESET           FNAMET
.       REP             " 0" IN FNAMET
.       
.       CLEAR           FNAMER1
.       APPEND          "cmxrhf",FNAMER1
.       APPEND          DIM2A,FNAMER1
.       RESET           FNAMER1
.       REP             " 0" IN FNAMER1
.
.       CLEAR           FNAMET1
.       APPEND          "cmxihf",FNAMET1
.       APPEND          DIM2A,FNAMET1
.       RESET           FNAMET1
.       REP             " 0" IN FNAMET1
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER1
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET1
.
.           MAIN        MENU
.
START   MOVE       ZERO,PAGENO
        CLOCK      TIME,CTIMEIS
.
        CALL      DISPHEAD
        DISPLAY   *P1:3,*EF:
                  *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = Admission Number Sequence":
                  *P1:6,*V2LON,TWO,*HOFF," = Patient Surname Sequence"
.
        KEYIN     *P1:8,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
.
BRNCH   BRANCH    OPTION OF KDATE,KSURN
        BEEP
        GOTO      START
.
KDATE   DISPLAY   *P49:2,"- ",*V2LON,"Admission Sequence"
        MOVE      SORT1,SORTSTR
        GOTO      KADATE
.
KSURN   DISPLAY   *P49:2,"- ",*V2LON,"Surname Sequence"
        MOVE      SORT2,SORTSTR
        GOTO      KADATE
.
NOADMN  DISPLAY   *P20:24,*B,*V2LON:
                  "** No Admissions on File **",*W3;
        GOTO      START
.
KADATE  DISPLAY   *P1:4,*EF,"Keyin From date : ":
                      *P1:6,"  Keyin To date : "
+
.             KEYIN TO DATE
.
KADAT1    UNPACK    SP6,CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          MOVE      "20",CCOL
          MOVE      "4",CVERT
          MOVE      "1",CDEFDTE
          MOVE      "0",CHIGHLT
          DISPLAY   *P20:6,*EL,*P20:4,*EL
.
          CALL      KEYDATE
          BRANCH    OVRCD,START
.
          MOVE      CDAY,IDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
          PACK      FROMDATE WITH ICENT,IYEAR,IMON,IDAY
          REP       " 0",FROMDATE
          UNPACK    FROMDATE INTO XCENT1,XYEAR1,XMON1,XDAY1
+
.             DISPLAY TO DATE 
.
KADAT2    UNPACK    SP6,CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          MOVE      "20",CCOL
          MOVE      "6",CVERT
          MOVE      "1",CDEFDTE
          MOVE      "0",CHIGHLT
          DISPLAY   *P20:6,*EL
.
          CALL      KEYDATE
          BRANCH    OVRCD,ENDAD2R
.
          MOVE      CDAY,IDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
          GOTO      ENDAD2
.
ENDAD2R MOVE            XDAY1,XDAY2
        MOVE            XMON1,XMON2
        MOVE            XYEAR1,XYEAR2
        MOVE            XCENT1,XCENT2
        MOVE            FROMDATE,TODATE
        GOTO            REKEY
.
ENDAD2
        PACK            TODATE WITH ICENT,IYEAR,IMON,IDAY
        REP             " 0",TODATE
        UNPACK          TODATE INTO XCENT2,XYEAR2,XMON2,XDAY2
.
        MATCH           FROMDATE TO TODATE * 'FROM' DATE MUST BE 
        GOTO            REKEY IF NOT LESS          * >= 'TO' DATE
.
INVDATG DISPLAY        *P1:24,*EL,*B,*V2LON,"** FROM DATE MUST BE GREATER ":
                       "THAN TO DATE **",*W2,*P1:24,*EL;
        GOTO           KADAT1
.
..U.C.. DISPLAY         *P20:6,*V2LON,IDAY,SLASH,IMON,SLASH,XYEAR2
.
.        CHECK IF DATES ARE OK OR NOT
.
REKEY   KEYIN           *P1:24,*EF," Dates ok (":
			*V2LON,"Y",*HOFF,"/":
			*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
        REP             UPPLOW,ANS
.
        MATCH           "N" TO ANS
        GOTO            KADAT1 IF EQUAL
.
        MATCH           "Y" TO ANS
        GOTO            CNTCHK IF EQUAL
.
        BEEP
        GOTO           REKEY
.
INVMASTA
        DISPLAY   *P20:23,*B,*EL:
                  "** Admission ",*V2LON,DAADMNO,*HOFF:
                  " has an invalid U/R ":
                  *V2LON,AURNO,*HOFF," ***",*W2
.
        PRINT     "** Admission ",DAADMNO," has an invalid U/R ":
                  AURNO," ... Please Contact I.B.A **"
        GOTO      NEXTA
.
CNTCHK  CALL       KILLIDX
.
        CALL       CREAIDX
.
        OPEN       TEMPFILE,FNAMET
        MOVE       ONE TO UNIQUE
        MOVE       UNIQUE,DUNIQUE
        MOVE       ZERO TO PATNUM
        OPEN       TEMP1,FNAMET1
+
.
ADMNNO  PACK       KEY16 WITH FROMDATE,SP8
        CALL       RDSMISS3
        MOVE       "60",LNO
.
        DISPLAY    *P1:24,*EL,*P20:24,"Admission Number :":
                        *P60:24,"Scanning :";
.
NEXTA   CALL       RDKMISS3
        BRANCH     OVRCD OF SORTD
.
	       DISPLAY    *P72:24,DAADMNO;
.
.       PATIENT MUST BE ADMITTED, DISCHARGED OR ON LEAVE
.
        COMPARE     ONE,ASTAT
        GOTO        NEXTA IF EQUAL
.
        COMPARE     FIVE,ASTAT
        GOTO        NEXTA IF EQUAL
.
        MOVE        AURNO,KEY8
        CALL        RDMAST1
        BRANCH      OVRCD OF INVMASTA
.
        UNPACK      ADATE INTO CCENT,CYEAR,CMON,CDAY
        CALL        PACDATE
        MOVE        CPCDATE,ADMDATE
        PACK        TDATE WITH CCENT,CYEAR,CMON,CDAY
.
        MATCH       TDATE,TODATE
        GOTO        SORTD IF LESS
.
A1OK    MOVE        AFUNDH,HFNAME
.
A1OK1   MOVE        PTMICMXP,CMXYN
.
        MOVE        SP8 TO DISDATE
        COMPARE     THREE TO ASTAT
        GOTO        A11OK IF NOT EQUAL
.
        MOVE        DAADMNO TO KEY8
        CALL        RDDSCH1
        BRANCH      OVRCD TO A11OK
.
        UNPACK      DDATE INTO CCENT,CYEAR,CMON,CDAY
        CALL        PACDATE
        MOVE        CPCDATE,DISDATE
.
A11OK   BRANCH      OPTION TO NUMSEQ,SURSEQ
.
NUMSEQ  MOVE        UNIQUE,DUNIQUE
        PACK        KEY16 WITH DAADMNO,DUNIQUE
        PACK        KEY14 WITH HFNAME,DUNIQUE
        MOVE        DAADMNO,XDAADMNO
        WRITE       TEMPFILE,KEY16;XDAADMNO,PSNAME,PGNAME,ADMDATE,DIM15,AURNO:
                                   HFNAME,CMXYN,DISDATE,DUNIQUE
        WRITE       TEMP1,KEY14;XDAADMNO,PSNAME,PGNAME,ADMDATE,DIM15,AURNO:
                                   HFNAME,CMXYN,DISDATE,DUNIQUE
        ADD         ONE TO UNIQUE
        DISPLAY     *P40:24,*EL,DAADMNO;
        GOTO        NEXTA
.
SURSEQ  MOVE        UNIQUE,DUNIQUE
        PACK        KEY28 WITH PSNAME,DUNIQUE
        PACK        KEY14 WITH HFNAME,DUNIQUE
        MOVE        DAADMNO,XDAADMNO
        WRITE       TEMPFILE,KEY28;XDAADMNO,PSNAME,PGNAME,ADMDATE,DIM15,AURNO:
                                   HFNAME,CMXYN,DISDATE,DUNIQUE
.
        WRITE       TEMP1,KEY14;XDAADMNO,PSNAME,PGNAME,ADMDATE,DIM15,AURNO:
                                   HFNAME,CMXYN,DISDATE,DUNIQUE
        ADD         ONE TO UNIQUE
        DISPLAY     *P40:24,*EL,DAADMNO;
        GOTO        NEXTA
.
SORTD   DISPLAY    *P1:24,*EL,*P20:24,"** PROCESSING STATISTICS **",*W;
        GOTO       SETLOOP
.
NOOPEN  NORETURN
        MOVE       ONE,OPCND
        DISPLAY    *P20:24,"** THE STATISTICS FILE IS EMPTY **",*W2;
        GOTO       ENDP
.
SETLOOP DISPLAY    *P1:24,*EL,*P20:24,"Admission Number :";
        MOVE       SP28 TO KEY28
        READ       TEMPFILE,KEY28;;
.
READLOP
        READKS      TEMPFILE;XDAADMNO,PSNAME,PGNAME,ADMDATE,DIM15,AURNO:
                             HFNAME,CMXYN,DISDATE,DUNIQUE
        GOTO        ENDP IF OVER
        MOVE        XDAADMNO,DAADMNO
.
        DISPLAY       *P40:24,*EL,DAADMNO;
.
        CALL          DISPDA
        GOTO          READLOP
+
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP0    NORETURN
ENDP     COMPARE   ZERO,PAGENO
         GOTO      NOADMN IF EQUAL
.
         CALL      PAGEND
         MOVE      SP14,KEY14
         PRINT     *N,*N,*10,"Casemix Yes",*30,"Casemix No" 
         CALL      CALSTA00                 * calculating totals
.
         DISPLAY   *P10:24,*EL,*V2LON:
                   "** Report Generation Completed **",*W2;
         CALL      KILLIDX
         GOTO      START
.+
CALSTA00 MOVE       ZERO,TOTCMXY
         MOVE       ZERO,TOTCMXN
         MOVE       ZERO,HFCMXY
         MOVE       ZERO,HFCMXN
         MOVE       Z6,SAVHFUND
         READ       TEMP1,KEY14;;
CALSTA10 READKS     TEMP1;XDAADMNO,PSNAME,PGNAME,ADMDATE,DIM15,AURNO:
                             HFNAME,CMXYN,DISDATE,DUNIQUE
         GOTO        CALSTA95 IF OVER
         MATCH       "Y",CMXYN
         IF          @EQUAL
           ADD         ONE,TOTCMXY
         ELSE
           ADD         ONE,TOTCMXN
         ENDIF
....
         MATCH       HFNAME,SAVHFUND
         IF          !@EQUAL
           MATCH       Z6,SAVHFUND 
           IF          !@EQUAL
               MATCH       SP6,SAVHFUND
               IF          @EQUAL
                 PRINT       *N,*1,"UNKNOWN",*10,HFCMXY,*30,HFCMXN;
               ELSE
                 PRINT       *N,*1,SAVHFUND,*10,HFCMXY,*30,HFCMXN;
               ENDIF
               MOVE        SP6,SAVHFUND
               MOVE        ZERO,HFCMXY
               MOVE        ZERO,HFCMXN
               MATCH       "Y",CMXYN
               IF          @EQUAL
                ADD         ONE,HFCMXY
               ELSE
                ADD         ONE,HFCMXN
               ENDIF
           ELSE
             MATCH      "Y",CMXYN
             IF        @EQUAL
               MOVE      ONE,HFCMXY
             ELSE
               MOVE      ONE,HFCMXN
             ENDIF
           ENDIF
         ELSE
           MATCH      "Y",CMXYN
             IF        @EQUAL
               ADD       ONE,HFCMXY
             ELSE
               ADD       ONE,HFCMXN
             ENDIF    
           ENDIF
         MOVE         HFNAME,SAVHFUND
         GOTO         CALSTA10
.           
CALSTA95 PRINT      *N,*1,SAVHFUND,*10,HFCMXY,*30,HFCMXN:
                    *N,*N,*1,"Total ",*10,TOTCMXY,*30,TOTCMXN:
                   *N,"  ***  END OF REPORT  ***"

CALSTA99 RETURN
.
.     PRINT ACTUAL DATA 
.
DISPDA  COMPARE   "53",LNO
        CALL      HEAD IF NOT LESS
.
         PRINT     *1,"!",DAADMNO,*11,"!",ADMDATE:
                   *22,"! ",PSNAME,PGNAME,*69,"! ",HFNAME:
                   *82,"! ",DISDATE,*94,"! ",CMXYN,*107,"!"
.
         ADD       ONE,LNO
         ADD       ONE,PATNUM
         RETURN
+
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,LNO
          RETURN
+
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,PAGENO
.
	  COMPARE   ONE,PAGENO
	  CALL      PAGEND IF NOT EQUAL
.
          CALL      HEAD132
.
          BRANCH    OPTION OF PRNTA,PRNTC
.
PRNTA     PRINT     " (ADMISSION NUMBER - SEQUENCE)";
          GOTO      PRTCONT
.
PRNTC     PRINT     " (SURNAME - SEQUENCE)";
.
PRTCONT   PRINT     *45,"From Date : ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *70,"To Date : ",XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2,*N
.
          CALL      PAGEND
.
.         PRINT     *1,"! Adm No.",*11,"! Adm Date":
.                   *22,"! Admission Type",*39,"! U/R No.":
.                   *49,"! Patients Name",*89,"!  C/MIX Y/N!":
.                   *102,"! Dis Date",*115,"!H/Fund",*132,"!"
.
          PRINT     *1,"! Adm No.",*11,"! Adm Date":
                    *22,"! Patients Name",*69,"! Health Fund":
                    *82,"! Dis Date",*94,"! Casemix Y/N",*107,"!"
          CALL     PAGEND
.
          MOVE      "8",LNO
          RETURN
+
.       Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLOSE     TEMPFILE
          CLOSE     TEMP1
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          DISPLAY   *P1:8,*EF;
          RETURN
.
CREAIDX   PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 112 ",SORTSTR           
          WEOF      PSTROL,SEQ
.
          PREP      PSTROL1,FNAMER1
          WRITE     PSTROL1,SEQ;"isbuild ",FNAMET1," 112 ",SORT3  
          WEOF      PSTROL1,SEQ
.
          MOVE      ONE,STATUS
          DISPLAY   *P1:13,*EF,*P1:14
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          BRANCH    STATUS TO INDOK1
          GOTO      INDOK2
.
INDOK1    CLEAR     CMDLINE         
          APPEND    "sh ",CMDLINE  
          APPEND    FNAMER1,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          BRANCH    STATUS TO INDOK
INDOK2    MOVE      ONE,OPCND
          DISPLAY   *P20:24,*B,*V2LON:
                    "** The Index File Not Created **",*W3;
.
INDOK     DISPLAY   *P1:13,*EF,*P1:14
          CLOSE     PSTROL,DELETE
          CLOSE     PSTROL1,DELETE
          RETURN  
+
          INC       PATMA1IO/INC
          INC       HL7BMTIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDSCIO/INC
          INC       PATDO1IO/INC
          INC       PATCODIO/INC
.
          INC       STD001IO
.
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
          INC       TFILENAM
.
ENDIT    CHAIN      PGM
         STOP

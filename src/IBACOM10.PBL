.*******************************************************************************
.* System    :   Referrals / Waiting List                                      *
.* Program   :   IBACOM10                                                      *
.* Desc      :   Process an eReferral XML File                                 *
.*******************************************************************************
.* Date      :   08/01/2018                                                    *
.* Author    :   Ebon Clements                                                 *
.* Function  :   This program will open an eReferral XML File and              *
.*               load the details into comerhaf and comerdaf                   *
.* Modification :                                                              *
.* V11.04.01     11.01.2024 DA Sarkies       TSK 0941573
.*               Added code to change " to '
.* V11.03.02     11.01.2024 DA Sarkies       TSK 0941573
.*               Added code to change " to '
.* V11.03.01     14/11/2023 Ebon Clements    TSK 0939966 (0908102)             *
.*               Added blank validation to CMRDVALU before write to comerdaf   *
.* V10.14.01     27/08/2019 Ebon Clements    TSK 0880418                       *
.*               Added mandatory Referral ID error - WRHD9000                  *
.* V10.13.02     17/09/2018 Don Nguyen       TSK 0845295                       *
.*               Increased size of command line & full file path variable      *
.* V10.13.01     01/08/2018 Ebon Clements    TSK 0845295                       *
.*               Changed to accept path and file as two keyings                *
.*               Added move of file to processed or error directory            *
.* V10.12.00     08/01/2018 Ebon Clements    TSK 0845295                       *
.*               Created program from EADMNXML                                 *
.*******************************************************************************
          INC       STD002FD            * command line
.
          INC       PATCONFD/INC
          INC       COMERDFD/INC        * eReferral pending detail table
          INC       PMSEERFD/INC        * eReferral pending error table
          INC       COMERHFD/INC        * eReferral pending header table
          INC       TFILEVAR/INC 
          INC       WEBERRFD/INC 
.
. eReferral header file
. ------------------------------------------------------------------------------
EREFHTXT  FILE      HL7,FIXED=2000
.
.Name     Type      Size      Start
XMRHEDAT  DIM       8         Exp Referral/Admission Date CCYYMMDD
XMRHHOSP  DIM       3         Hospital (pathspaf)
XMRHBTYP  DIM       3         Booking Type (cat BK)
XMRHADOC  DIM       100       Admitting Doctor
XMRHEREF  DIM       20        eReferral Id
XMRHUNIQ  DIM       20        eReferral Unique
XMRHTIME  DIM       8         eReferral Time
XMRHPCOD  DIM       9         Procedure Code
XMRHPCNT  DIM       2         Procedure Count
.
. eReferral detail file
. ------------------------------------------------------------------------------
EREFDTXT  FILE      HL7,FIXED=2000
.
.Name     Type      Size      Start
XMRDUNIQ  DIM       20        Unique ID eReferral Id
XMRDPRID  DIM       8         Property ID
XMRDWBID  DIM       8         webPAS ID (cgi name)
XMRDVALU  DIM       600       Value
.
. Program variables
. ------------------------------------------------------------------------------
CMDLINE   DIM       600
DIM8      DIM       8
FORM20    FORM      20
TODAY     DIM       8
.
HEADERFN  DIM       8     * Header Temp File Name
DETAILFN  DIM       8     * Detail Temp File Name
.
FILENAM1  DIM       20
FILENAM2  DIM       20
XMLFILEN  DIM       127   * XML File Name
XMLFPATH  DIM       127   * XML File Path
XMLFLNAM  DIM       300   * XML File Name (full path)
.
SP600     INIT      "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  "
CNVQUOTE  INIT      "#"'"
.
. Program constants
. ------------------------------------------------------------------------------
PRGID     INIT      "IBACOM10"
PRGNAM    INIT      "eReferral XML load"
VERSION   INIT      "V12.01.00"
+
.*******************************************************************************
.*                              MAIN0000                                       *
.*                      Controlling Logic (Mainline code)                      *
.*******************************************************************************
.
MAIN0000  CALL      SETX0000
          CALL      INIT0000               * initialisation and open files
.
          CALL      EPIS0000               * using Episft Interface
          BRANCH    EXIT,MAIN9999          * no    
.
MAIN0500  CALL      GPTH0000               * get path name
          BRANCH    EXIT,MAIN9999          * nothing input
.
          CALL      GFIL0000               * get filename
          BRANCH    EXIT,MAIN9999          * nothing input
.
          CALL      CFIL0000               * copy file
          BRANCH    EXIT,MAIN9999          * unable to copy
.
          CALL      OPEN0000               * open text file
          BRANCH    EXIT,MAIN9999          * unable to open .txt file
.
          CALL      PROC0000               * process
.
MAIN9999  STOP                             * chain back to program
+
.*******************************************************************************
.*                                INIT0000               Called by: MAIN0000   *
.*  Initialisation routine                                                     *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          OPEN      COMERHA1,"comerhaf"
          OPEN      COMERDA1,"comerdaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND19;*182,PTCNEPIS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,HEADERFN
          REP       " 0",HEADERFN
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,DETAILFN
          REP       " 0",DETAILFN
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                EPIS0000               Called by: MAIN0000   *
.*  Using Episoft Interface                                                    *
.*******************************************************************************
EPIS0000  MATCH     "1",PTCNEPIS                 * Using Episoft Interface
          GOTO      EPIS9100 IF NOT EQUAL
.
EPIS9000  MOVE      ZERO,EXIT
          GOTO      EPIS9999
.
EPIS9100  DISPLAY   *P1:24,"ERROR: Not Using Episoft Interface.";
          CALL      HITENTER
          CALL      MERR0000                * Move file to errors directory
          MOVE      ONE,EXIT
.
EPIS9999  RETURN
+
.*******************************************************************************
.*                               OPEN0000                Called by: MAIN0000   *
.* Open the eReferral temporary files for processing                          *
.*******************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EREFHTXT,HEADERFN            * open text file
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Header File not found.  ";
            CALL      HITENTER
            GOTO      OPEN9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EREFDTXT,DETAILFN           * open text file
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"File not found.  ";
            CALL      HITENTER
            GOTO      OPEN9100
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9100  CALL      MERR0000                * Move file to errors directory
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9999  RETURN
+
.*******************************************************************************
.*                               PROC0000                Called by: MAIN0000   *
.* Loop through the eReferral temporary files and store data in tables        *
.*******************************************************************************
.
PROC0000  CALL      WRHD0000
          BRANCH    EXIT,PROC9999
.
          CALL      WRDD0000
          CLOSE     EREFHTXT,DELETE        * Delete Temporary File
          CLOSE     EREFDTXT,DELETE        * Delete Temporary File
          CALL      MPRC0000               * Move file to processed directory
.
PROC9999  RETURN
+
.*******************************************************************************
.*                                WRDD0000               Called by: PROC0000   *
.* Write eReferral detail table                                               *
.*******************************************************************************
WRDD0000  
.
WRDD1000  READ      EREFDTXT,SEQ;XMRDPRID,XMRDWBID,XMRDVALU
          REP       CNVQUOTE,XMRDVALU
          GOTO      WRDD9999 IF OVER
.
          MATCH     SP600,XMRDVALU
          GOTO      WRDD1000 IF EQUAL
          GOTO      WRDD1000 IF EOS
.
          PACK      KEY28,CMRHUNIQ,XMRDPRID,SP70
          CALL      RDCMERD1
          IF        OVRCD=1
            PACK      CMRDUNIQ,CMRHUNIQ,SP70
            PACK      CMRDPRID,XMRDPRID,SP70
            PACK      CMRDWBID,XMRDWBID,SP70
            PACK      CMRDVALU,XMRDVALU,SP70
            MOVE      ZERO,CMRDNVAL
            PACK      CMRDDVAL,SP70
            PACK      CMRDSPAR,SP70,SP70,SP70
            CALL      WRCMERD1
          ELSE
            DISPLAY   *R,"Duplicate Found in Load on ",CMRHUNIQ,XMRDPRID
          ENDIF
          GOTO      WRDD1000
.
WRDD9999  RETURN
+
.*******************************************************************************
.*                                WRHD0000               Called by: PROC0000   *
.* Write eReferral header table                                               *
.*******************************************************************************
WRHD0000  READ      EREFHTXT,SEQ;CMRHUNIQ,XMRHEREF,XMRHEDAT,XMRHTIME,XMRHHOSP:
                                 XMRHBTYP,XMRHADOC,XMRHPCOD,XMRHPCNT
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
          MOVE      DIM8,TODAY
          CLOCK     TIME,CTIMEIS
          PACK      CMRHDATE,TODAY,SP70
          PACK      CMRHTIME,CTIMEIS,SP70
          PACK      CMRHSTAT,SP1,ONE,SP70
          PACK      CMRHEDAT,XMRHEDAT,SP70
          PACK      CMRHURNO,SP70
          PACK      CMRHVISN,SP70
          PACK      CMRHPCOD,XMRHPCOD,SP70
          PACK      CMRHPCNT,XMRHPCNT,SP70
          PACK      CMRHHOSP,XMRHHOSP,SP70
          PACK      CMRHBTYP,XMRHBTYP,SP70
          PACK      CMRHADOC,XMRHADOC,SP70
          PACK      CMRHEREF,XMRHEREF,SP70
          REP       " 0",CMRHEREF
.
          MATCH     "00000000000000000000",CMRHEREF  * Error if no ReferralID
          GOTO      WRHD9000 IF EQUAL                * supplied
.
          PACK      CMRHLHRD,SP70
          PACK      CMRHLHDT,SP70
          PACK      CMRHUTIM,SP70
          PACK      CMRHUUID,SP70         
          PACK      CMRHSPAR,SP70,SP70    
.
          MOVE      ZERO,FORM20
.
          MOVE      "  0",PRXCODE                 * System Lock Sector 0
          CALL      GETSLK00                      * Get System Lock-Sector 0
          READ      CONTROLF,HUND16;*227,PTCNNEID    * next message id
.
          TYPE      PTCNNEID                      * If not numeric default to 1
          GOTO      WRHD8000 IF NOT EQUAL
            
          MOVE      PTCNNEID,FORM20
.
WRHD8000  ADD       ONE,FORM20
          MOVE      FORM20,PTCNNEID
.         
          MOVE      PTCNNEID,CMRHUNIQ
.         
          WRITAB    CONTROLF,HUND16;*227,PTCNNEID
          CALL      RELSLK00  
          CALL      WRCMERH1
          MOVE      ZERO,EXIT
          GOTO      WRHD9999
.
WRHD9000  DISPLAY   *P1:24,"ERROR: XML ReferralID is mandatory.";
          CALL      HITENTER
          CLOSE     EREFHTXT,DELETE         * Delete Temporary File
          CLOSE     EREFDTXT,DELETE         * Delete Temporary File
          CALL      MERR0000                * Move file to errors directory
          MOVE      ONE,EXIT
          GOTO      WRHD9999
.
WRHD9999  RETURN
.*******************************************************************************
.*                          CFIL0000                   Called by: MAIN0000     *
.* Copy the XML file to a ".txt" file so TBL can open it                       *
.* Requires: XMLFLNAM - eReferral XML Response File name                      *
.* Returns : TEMPFILE - temporary ".txt" filename                              *
.*           EXIT  0 = file successfully copied                                *
.*                 1 = unable to copy file                                     *
.*******************************************************************************
.
CFIL0000  STRIP     XMLFLNAM
          CLEAR     CMDLINE
          APPEND    "ibacom10.us1 ",CMDLINE
          APPEND    HEADERFN,CMDLINE
          APPEND    " ",CMDLINE
          APPEND    DETAILFN,CMDLINE
          APPEND    " ",CMDLINE
          APPEND    XMLFLNAM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MATCH     "0       ",TASKID
          IF        @EQUAL
            MOVE      ZERO,EXIT
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Unable to convert XML file.  ";
            CALL      HITENTER
            CALL      MERR0000                * Move file to errors directory
            MOVE      ONE,EXIT
          ENDIF
.
CFIL9999  RETURN
+
.*******************************************************************************
.*                          GPTH0000                 Called by: MAIN0000       *
.* Keyin the eReferral XML file path name                                     *
.*******************************************************************************
.
GPTH0000  KEYIN     *P1:11,*EL,"File Path : ":
                    *P13:11,XMLFPATH
.
          PACK      XMLFPATH,XMLFPATH,SP70
          MATCH     SP70,XMLFPATH
          GOTO      GPTH9100 IF EQUAL
.
          SQUEEZE   XMLFPATH
          ENDSET    XMLFPATH
          CMATCH    SLASH,XMLFPATH
          IF        !@EQUAL
            APPEND    SLASH,XMLFPATH
          ENDIF
          RESET     XMLFPATH
.
          MOVE      ZERO,EXIT
          GOTO      GFIL9999
.
GPTH9100  MOVE      ONE,EXIT
.
GPTH9999  RETURN
+
.*******************************************************************************
.*                          GFIL0000                 Called by: MAIN0000       *
.* Keyin the eReferral XML file name                                          *
.*******************************************************************************
.
GFIL0000  KEYIN     *P1:12,*EL,"Filename (excluding path): ":
                    *P28:12,XMLFILEN
.
          PACK      XMLFILEN,XMLFILEN,SP30
          MATCH     SP30,XMLFILEN
          GOTO      GFIL9100 IF EQUAL
.
          PACK      XMLFLNAM,XMLFPATH,XMLFILEN,SP70   * Full name and path
          SQUEEZE   XMLFLNAM
          MOVE      ZERO,EXIT
          GOTO      GFIL9999
.
GFIL9100  MOVE      ONE,EXIT
.
GFIL9999  RETURN
+
.*******************************************************************************
.* Move input file to processed directory                                      *
.*******************************************************************************
MPRC0000  MATCH     SP70,XMLFPATH
          GOTO      MPRC9999 IF EQUAL
          GOTO      MPRC9999 IF EOS
.
          MATCH     SP70,XMLFLNAM
          GOTO      MPRC9999 IF EQUAL
          GOTO      MPRC9999 IF EOS
.
          CLEAR     CMDLINE
          APPEND    "mv ",CMDLINE
          APPEND    XMLFLNAM,CMDLINE
          APPEND    " ",CMDLINE
          APPEND    XMLFPATH,CMDLINE
          APPEND    "processed/",CMDLINE
          APPEND    XMLFILEN,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MPRC9999  RETURN
+
.*******************************************************************************
.* Move input file to error directory                                          *
.*******************************************************************************
MERR0000  MATCH     SP70,XMLFPATH
          GOTO      MERR9999 IF EQUAL
          GOTO      MERR9999 IF EOS
.
          MATCH     SP70,XMLFLNAM
          GOTO      MERR9999 IF EQUAL
          GOTO      MERR9999 IF EOS
.
          CLEAR     CMDLINE
          APPEND    "mv ",CMDLINE
          APPEND    XMLFLNAM,CMDLINE
          APPEND    " ",CMDLINE
          APPEND    XMLFPATH,CMDLINE
          APPEND    "errors/",CMDLINE
          APPEND    XMLFILEN,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MERR9999  RETURN
+
.*******************************************************************************
.* I/O Includes                                                                *
.*******************************************************************************
          INC       STD002IO
          INC       TFILENAM     
          INC       PMSEERIO/INC        *eReferral pending error table
          INC       WEBERRIO/INC
          INC       COMERHIO/INC
          INC       COMERDIO/INC

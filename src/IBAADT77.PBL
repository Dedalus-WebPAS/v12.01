.******************************************************************************
.* System    :   Patient Management                                           *
.* Program   :   IBAADT77  -  DAILY INPATIENT SCAN REPORT                     *
.* Desc      :                                                                *
.******************************************************************************
.* Date      :   01/10/2009                                                   *
.* Author    :   Mike Laming                                                  *
.* Mods      :                                                                *
.*        V12.00.01 29/05/2025  J.Tan          TSK 0955096                    *
.*                  Added Alphanumeric visit number changes                   *
.*        V10.05.01 01/02/2015  Ania P         CAR 261630                     *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 03/02/2011 Jill Parkinson  CAR 233088                     *
.*                  Added pmsvx1af                                            *
.*        V9.12.01  22/10/2009  Jill Parkinson CAR 206344                     *
.*                  Fixed surname given name                                  *
.*        V9.11.00  01/10/2009  Mike Laming    CAR 206344                     *
.*                  New Program                                               *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC    * codes file
          INC       PATCONFD/INC    * codes file
          INC       PATMA1FD/INC    * patient master file
          INC       PATMI1FD/INC    * admission file
          INC       PMSVX1FD/INC    * admission file
          INC       PATTRNFD/INC    * patient tran file
          INC       PATWR1FD/INC    * ward file
          INC       PMSPX2FD/INC
          INC       PATDO1FD/INC    * Doctor file
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.  Tempfile for Inpatients 
.
TEMP1     IFILE     SQL,FIXED=121, KEY="U1-3,4-6,7-14"               * KEY14
TEMP2     IFILE     SQL,FIXED=121, KEY="U20-39,40-59,1-3,4-6,7-14"    * KEY54
.
.Name     Type      Length    Physical  Position  Description
.----     ----      ------    --------  --------  -----------
XWARD     DIM       3            3        1       Ward
XWBED     DIM       3            3        4       Bed
XADMNO    DIM       8            8        7       Admission No.
XWARDTYP  DIM       1            1        15      Ward Type flag (0,1 or 2)
XPHONE    DIM       4            4        16      Phone Extension
XSNAME    DIM       20           20       20      Patient Surname
XGNAME    DIM       20           20       40      Patient Given name
XATTDOC   DIM       20           20       60      Attending Doctor
XLEAVDT   DIM       20           20       80      Leave Start Date/time
XNEWADM   DIM       1            1        100     Admitted Current day flag
XCOMMNT   DIM       20           20       101     Other Comment          
.
.End of Record                            121
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CMDLINE   DIM       50
CURRDATE  DIM       8               * current date
CNTADMNS  FORM      5
CNTLEAVS  FORM      5
CNTTOTAL  FORM      5
DIM12A    DIM       12
DIM12B    DIM       12
DIM12C    DIM       12
FOUNDADM  FORM      1
FOUNDSBY  FORM      1
LEAVDATE  DIM       20
SKEY6     DIM       6
THISWARD  DIM       3
THISTYPE  DIM       1
THISINAC  FORM      1
TMPFNAME  DIM       8
.
ADT77A    INIT      "adt77a"
SLSH      INIT      "/"
.
.
PRGID     INIT      "IBAADT77"
PRGNAM    INIT      "Daily Inpatient Scan Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                              ML0000                                        *
.*                      Controlling Logic (Mainline code)                     *
.******************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      CREA0000               * create/open temp files
.
ML2000    CALL      EXTR0000               * Extract data      
          CALL      PRNT0000               * Print Report
.
....      CALL      KILL0000               * delete the tempfiles
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,"Opening patwr1af";  * ward file
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A2,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
.
          DISPLAY   *P64:24,"patmi1af";       * admission file
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patcodes";       * codes
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pattranf";       * tran file
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patma1af";       * master file
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patdo1af";       * doctor file
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print Daily Inpatient Report"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                               CREA0000                                   *
.*                        Create Temporary Files                            *
.****************************************************************************
.                                           * to handle Oracle temp tables
CREA0000  MOVE      ZERO,EXIT
          CALL      TFILENAM
          MOVE      TFILNAME,TMPFNAME
.
          TRAP      OVERCOND IF IO
          OPEN      TEMP1,TMPFNAME
          BRANCH    OVRCD,CREA2000
          TRAPCLR   IO
          CALL      CLER0000                * clear existing temp file
          GOTO      CREA9000
.
CREA2000  PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFNAME,CMDLINE
          APPEND    " 121 U1-3,4-6,7-14",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    TMPFNAME,CMDLINE
          APPEND    " U20-39,40-59,1-3,4-6,7-14",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID

CREA9000  OPEN      TEMP1,TMPFNAME
          OPEN      TEMP2,TMPFNAME
.
CREA9999  RETURN
.
.****************************************************************************
.*                               KILL0000                                   *
.*                        Remove Temporary Files                            *
.****************************************************************************
.
KILL0000  CLOSE     TEMP1
          CLOSE     TEMP2
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MATCH     "0",TASKID
          GOTO      KILL9999 IF EQUAL
.
          CALL      CLER0000                * to handle Oracle temp tables
.
KILL9999  RETURN
+
.******************************************************************************
.*                             CLER0000                 Called by:            *
.*               close and erase the temporary file                           *
.******************************************************************************
.
CLER0000  PACK      KEY14,SP20
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY14,XWARD,XWBED,XADMNO,SP20
          CALL      DETEMP1
.
          GOTO      CLER0000
.
CLER9999  RETURN
+
.****************************************************************************
.*                               EXTR0000                Called by: ML0000  *
.*               Extract WARD/BED data, Extract Admission data              *
.****************************************************************************
EXTR0000  MOVE      ZERO,EXIT
.                                           * load Ward/Bed data to tempfile
          CALL      CLTMP000
          UNPACK    SP10,THISWARD,THISTYPE
.
          MOVE      SP6,KEY6
          CALL      RDSWARD1
EXTR1000  CALL      RDKWARD1
          BRANCH    OVRCD,EXTR4000
.
          BRANCH    WACTIVE,EXTR1000        * bypass inactive Ward/bed
.
          MATCH     SP3,WBED                * is this the Ward header
          IF        @EQUAL 
            MOVE      WWARD,THISWARD
            MOVE      WINPUT,THISTYPE
            MOVE      WACTIVE,THISINAC
            GOTO      EXTR1000              * bypass Ward+Bed headers
          ENDIF
.
          BRANCH    WACTIVE,EXTR1000        * bypass inactive Ward/bed
.
.                                           * load empty ward/beds only
          MOVE      WWARD,XWARD
          MOVE      WBED,XWBED
          MOVE      SP8,XADMNO
          MATCH     ZEROVISN,WADMNO
          GOTO      EXTR1000 IF NOT EQUAL
          MOVE      WEXTN,XPHONE
          MATCH     THISWARD,WWARD
          IF        @EQUAL
            MOVE      THISTYPE,XWARDTYP
          ELSE
            MOVE      WINPUT,XWARDTYP
          ENDIF
.
          PACK      KEY14,XWARD,XWBED,XADMNO
          CALL      WRTEMP1
          GOTO      EXTR1000
.
.                                           * read thru current admissions incl.
.                                           * those on-leave
EXTR4000  MOVE      ZERO,CNTADMNS
          MOVE      ZERO,CNTLEAVS
          MOVE      "2",KEY1
          PACK      KEY9,KEY1,SP20
          CALL      RDSMISS2
EXTR4500  CALL      RDKMISS2
          BRANCH    OVRCD,EXTR8000
.                                   current          on-leave
          BRANCH    ASTAT,EXTR4500,EXTR5000,EXTR4500,EXTR5000
          GOTO      EXTR8000
.
.
EXTR5000  MOVE      ZERO,FOUNDADM           * find the allocated ward/bed
          MOVE      ZERO,FOUNDSBY
          CALL      CLTMP000
          CALL      CLWR1000
.
          MOVE      AWARD,XWARD
          MOVE      ABED,XWBED
          MOVE      DAADMNO,XADMNO
          PACK      SKEY6,AWARD,ABED,SP10
.
          IF        ASTAT = 4
            CALL      LEAV0000              * get Leave info
            MOVE      LEAVDATE,XLEAVDT
            ADD       ONE,CNTLEAVS
          ELSE
            ADD       ONE,CNTADMNS
          ENDIF
.
EXTR5100  MATCH     SP6,SKEY6               * if AWARD/ABED is blank check W/B  
          IF        @EQUAL
EXTR5111    PACK      KEY14,DAADMNO,SP20
            CALL      RDSWARD2
            CALL      RDKWARD2
            MATCH     DAADMNO,DWADMNO
            IF        @EQUAL
              MOVE      WWARD,XWARD
              MOVE      WBED,XWBED
              MOVE      ONE,FOUNDADM
              GOTO      EXTR5400
            ELSE
              CALL      CLWR1000
              CALL      RDSWARD4
              CALL      RDKWARD4
              MATCH     DAADMNO,DWSTBY      * check if Standby
              IF        @EQUAL
                MOVE      WWARD,XWARD
                MOVE      WBED,XWBED
                MOVE      ONE,FOUNDSBY
                GOTO      EXTR5400
              ENDIF
            ENDIF
            GOTO      EXTR6000
          ENDIF
.
EXTR5200  MATCH     SP3,ABED                * if ABED is blank check Ward/Bed
          IF        @EQUAL
            PACK      KEY6,AWARD,SP10       
            CALL      RDWARD1               * read Ward header for phone no
            MOVE      ONE,FOUNDADM
            GOTO      EXTR5400
          ENDIF       
.
          CALL      CLWR1000
          PACK      KEY14,DAADMNO,AWARD,ABED,SP20
          CALL      RDWARD2                 * read ward/bed for Admn.no
          IF        OVRCD = 1
            CALL      RDWARD4               * read ward/bed for Standby bed
            MOVE      ONE,FOUNDSBY
          ELSE
            MOVE      ONE,FOUNDADM
          ENDIF
.
EXTR5400  MOVE      WEXTN,XPHONE
.
.                                           * set up Admission data
EXTR6000  MATCH     CURRDATE,ADATE
          IF        @EQUAL
            MOVE      "Y",XNEWADM
          ENDIF
.
          MOVE      SP70,DTITL,DGNAME,DSNAME
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1                 * read the doctors file
          IF        OVRCD = 0
            MOVE      DGNAME,KEY1
            SQUEEZE   DTITL
            SQUEEZE   DSNAME
            PACK      XATTDOC,DSNAME,SP2,DTITL,SP1,KEY1
          ENDIF
.
          IF        FOUNDSBY = 1
            MOVE      "Standby Patient",XCOMMNT
          ENDIF
.                                           * get Master et al.              
          UNPACK    SP70,PSNAME,PGNAME
          PACK      KEY8,AURNO,SP10
          CALL      RDMAST1
          MOVE      PSNAME,XSNAME           * set up patients name
          MOVE      PGNAME,XGNAME  
.
.                                           * write the record
          PACK     KEY14,XWARD,XWBED,XADMNO,SP20
          CALL     WRTEMP1
.
          GOTO      EXTR4500
.
EXTR8000  
.
EXTR9999  RETURN
.
.****************************************************************************
.*                               CLTMP000                                   *
.*                        Clear Temp file variables                         *
.****************************************************************************
CLTMP000  UNPACK    SP70,XWARD,XWBED,XADMNO,XWARDTYP,XPHONE
          UNPACK    SP70,XSNAME,XGNAME,XATTDOC
          UNPACK    SP70,XLEAVDT,XNEWADM,XCOMMNT
.
CLTMP999  RETURN
.
.****************************************************************************
.*                               CLWR1000                                   *
.*                        Clear "patwr1af" variables                        *
.****************************************************************************
CLWR1000  UNPACK    SP70,WWARD,WBED,WBDESC,DWADMNO,WEXTN,WRTYPE,WEFRBT,WSERV
          MOVE      ZERO,WBLINE
          MOVE      ZERO,WNBEDS
          MOVE      ZERO,WHCSSUB
          MOVE      ZERO,WPLUR
          MOVE      ZERO,WINPUT
          MOVE      ZERO,WOCCBED
          UNPACK    SP70,WCLASS,DWSTBY,PTWDHOSN
          MOVE      ZERO,WACTIVE
          MOVE      ZERO,PTWDDNIN
.
CLWR1999  RETURN
.
.****************************************************************************
.*                               LEAV0000                                   *
.*                        Retrieve Leave Dates                              *
.****************************************************************************
LEAV0000  MOVE      ZERO,EXIT
          MOVE      SP20,LEAVDATE
.
          PACK      KEY30,DAADMNO,SP30
          CALL      RDSTRAN2
LEAV1000  CALL      RDKTRAN2
          BRANCH    OVRCD,LEAV9999
.
          MATCH     DAADMNO,DTADMN
          GOTO      LEAV9999 IF NOT EQUAL
.
          MATCH     TDATE,CURRDATE
          GOTO      LEAV9999 IF LESS
.
          CMATCH    "R",TMOVE
          IF        @EQUAL
            MOVE      SP20,LEAVDATE
            GOTO      LEAV1000
          ENDIF
.
          CMATCH    "L",TMOVE
          GOTO      LEAV1000 IF NOT EQUAL
.
          UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      " fr.",KEY4
          MOVE      TTIME,KEY5
          PACK      LEAVDATE,KEY4,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR,SP1,KEY5,SP10
          GOTO      LEAV1000
.
LEAV9999  RETURN
+
.****************************************************************************
.*                               PRNT0000              Called by: ML0000    *
.*                          Print the Report                                *
.****************************************************************************
.
PRNT0000  MOVE      ZERO,EXIT
          MOVE      "99",CLNO
          PACK      KEY54,SP70
          CALL      RSTEMP2
PRNT1000  CALL      RKTEMP2
          BRANCH    OVRCD,PRNT9000
.
          REP       "Y*",XNEWADM
          MOVE      SP1,KEY1
          CMATCH    "S",XCOMMNT
          IF        @EQUAL
            MOVE      "S",KEY1
          ENDIF
.
          MATCH     SP3,XWBED
          IF        @EQUAL
            PACK      KEY7,XWARD,SP10
          ELSE
            PACK      KEY7,XWARD,SLSH,XWBED
          ENDIF
.
          COMPARE   "63",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      XSNAME,DIM12A
          MOVE      XGNAME,DIM12B
          MOVE      XATTDOC,DIM12C
          MOVE      XLEAVDT,KEY14
.
          PRINT     *1,XNEWADM,KEY1,SP1,XADMNO,SP1,KEY7,SP1,XPHONE:
                    SP2,DIM12A,SP1,DIM12B,SP1,DIM12C,SP1,KEY14
.
          ADD       ONE,CLNO
.
          GOTO PRNT1000
.
PRNT9000  CALL      UND80 
          ASSIGN    (CNTADMNS+CNTLEAVS),CNTTOTAL 
          PRINT     *1,*N,SP2,CNTADMNS,SP2,"Patients",SP10,CNTLEAVS,SP2:
                    "On-Leave",SP10,SP2,"Total",CNTTOTAL:
                    *N,*N,SP2,"Legend :  * = Admitted today   S = Bed Standby":
                    *N,*N,*N,"  *** End of Report ***"
.
PRNT9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PROC0000 *
.*                                 Heading                                    *
.******************************************************************************
HEAD0000  CALL      HEAD80 
.                       ....*....1....*....2....*....3....*....4....*....5
          PRINT     *1,"    Patient Wrd/Bed Phone Patient Name            ":
.                       ....*....6....*....7....*....8....*....9
                       "  Doctor Name    Started Leave"
          CALL      UND80 
          MOVE      "6",CLNO
HEAD9999  RETURN
+
.****************************************************************************
. TEMP FILE I/O
.
RSTEMP1   READ      TEMP1,KEY14;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;XWARD,XWBED,XADMNO,XWARDTYP:
                          XPHONE,XSNAME,XGNAME,XATTDOC,XLEAVDT:
                          XNEWADM,XCOMMNT
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      TEMP1,KEY14;XWARD,XWBED,XADMNO,XWARDTYP:
                          XPHONE,XSNAME,XGNAME,XATTDOC,XLEAVDT:
                          XNEWADM,XCOMMNT
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   MOVE      ZERO,OVRCD
          UPDATE    TEMP1;XWARD,XWBED,XADMNO,XWARDTYP:
                          XPHONE,XSNAME,XGNAME,XATTDOC,XLEAVDT:
                          XNEWADM,XCOMMNT
          RETURN
.
WRTEMP1   MOVE      ZERO,OVRCD
          WRITE     TEMP1,KEY14;XWARD,XWBED,XADMNO,XWARDTYP:
                          XPHONE,XSNAME,XGNAME,XATTDOC,XLEAVDT:
                          XNEWADM,XCOMMNT
          RETURN
.
DETEMP1   MOVE      ZERO,OVRCD
          RESET     KEY14
          DELETE    TEMP1,KEY14
          RETURN
.
.
.
RSTEMP2   READ      TEMP2,KEY54;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMP2;XWARD,XWBED,XADMNO,XWARDTYP:
                          XPHONE,XSNAME,XGNAME,XATTDOC,XLEAVDT:
                          XNEWADM,XCOMMNT
          GOTO      OVERCOND IF OVER
          RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCODIO/INC        * codes file
          INC       PATMA1IO/INC        * patient master file
          INC       PATMI1IO/INC        * admission file
          INC       PMSVX1IO/INC        * admission file
          INC       PATTRNIO/INC        * patient tran file
          INC       PATWR1IO/INC        * ward file
          INC       PATDO1IO/INC        * Doctor file
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

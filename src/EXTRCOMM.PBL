.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   EXTRCOMM                                                    *
.* Desc      :   Patient Comments Data Extraction Program                    *
.*****************************************************************************
.* Date      :   03/03/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will extract patient U/R comments from         *
.*               pmsuchaf and pmsucdaf tables and write it to the CONVCOMM.  *
.* Mods:                                                                     *
.*****************************************************************************
.*        V10.11.01 22/09/2017  Thanh T.         TSK 0821710                 *
.*                  Audit Amission/outpatient and U/R comment                *
.*        V10.04.00 03/03/2014  Steve Armstrong  CAR 296128                  *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
.
. U/R Comment Extract/Load File
.
URCOMEXT  FILE      HL7,FIXED=4096         * Pipe delimited upload file
.
. Fields must be pipe delimited and in the following sequence.
. Field lengths are irrelevant other than the fact that where
. a field is longer than allowed, the extra data will be ignored.
.
. U/R comment header upload file layout
.
.Name     Type      Length start Description
.----     ----      ------ ----- -----------
CEXTTYPE  DIM       1      1     Comment Header Type (0)
.PIPE     DIM       1      2     Pipe Delimiter
CEXTURNO  DIM       8      3     U/R Number
.PIPE     DIM       1      11    Pipe Delimiter
CMUHCTYP  DIM       3      12    Comment Type
.PIPE     DIM       1      15    Pipe Delimiter
CMUHCNUM  DIM       6      16    Counter/Note Number
.PIPE     DIM       1      22    Pipe Delimiter
CMUHCUID  DIM       10     23    User Who Created Record
.PIPE     DIM       1      33    Pipe Delimiter
CMUHCDAT  DIM       8      34    Date Record Created (ccyymmdd)
.PIPE     DIM       1      42    Pipe Delimiter
CMUHCTIM  DIM       8      43    Time Record Created (hh:mm:ss)
.PIPE     DIM       1      51    Pipe Delimiter
CMUHOCCG  DIM       3      52    Occupational Group WEB User ID
.PIPE     DIM       1      55    Pipe Delimiter
CMUHDELT  DIM       1      56    Delete Indicator (0=No, 1=Yes)
.PIPE     DIM       1      57    Pipe Delimiter
CMUHDUID  DIM       10     58    User Who Deleted Record (websecaf)
.PIPE     DIM       1      68    Pipe Delimiter
CMUHDDAT  DIM       8      69    Date Record Deleted (ccyymmdd)
.PIPE     DIM       1      77    Pipe Delimiter
CMUHDTIM  DIM       8      78    Time Record Deleted (hh:mm:ss)
.PIPE     DIM       1      86    Pipe Delimiter
CMUHDOCG  DIM       3      87    Occupational Grp WEB UserID Deleted
.
.End of Record             90
.
.
. U/R comment detail upload file layout
.
.Name     Type      Length start Description
.----     ----      ------ ----- -----------
.CEXTTYPE DIM       1      1     Comment Details Type (1)
.PIPE     DIM       1      2     Pipe Delimiter
.CEXTURNO DIM       8      3     U/R Number
.PIPE     DIM       1      11    Pipe Delimiter
CMUCCTTY  DIM       3      12    Comment Type
.PIPE     DIM       1      15    Pipe Delimiter
CMUCCNUM  DIM       6      16    Counter/Note Number
.PIPE     DIM       1      22    Pipe Delimiter
CMUCLNUM  DIM       3      23    Line Number
.PIPE     DIM       1      26    Pipe Delimiter
CMUCCOMM  DIM       70     27    Comment
.
.End of Record             97
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
PIPE      INIT      "|"
EXTCNTHR  FORM      10
EXTCNTDT  FORM      10
PRCCNTHR  FORM      10
PRCCNTDT  FORM      10
RECCOUNT  FORM      10
CMNTLINE  DIM       92   
.
.-----------------------------------------------------------------------
PRGID     INIT      "EXTRCOMM"
PRGNAM    INIT      "Patient Comments Extraction"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * Comments extract
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      CFIL0000               * create file
          BRANCH    EXIT,MAIN0100          * file exists
.
          CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsuchaf";
          OPEN      PMSUCHA1,"pmsuchaf"
          OPEN      PMSUCDA1,"pmsucdaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data extraction                     *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF:
                    ". Patient U/R Comments Data Extraction"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run extraction
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                                  CFIL0000       Called by: MAIN0000       *
.*                         Create the extract file                           *
.*****************************************************************************
.
CFIL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      URCOMEXT,"convcomm"     * extract file exists ?
          TRAPCLR   IO
          BRANCH    OVRCD,CFIL5000          * no - create it
.
CFIL1000  KEYIN     *P1:23,*B,*EF,"The extract file convcomm.txt already ":
                    "exists.":
                    *P1:24,"Overwrite it (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,")?":
                    *P21:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS                * "Y" entered ?
          GOTO      CFIL2000 IF EQUAL       * yes - continue
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      CFIL9100 IF EQUAL       * no
.
          BEEP
          GOTO      CFIL1000
.
.         Delete the existing file
.
CFIL2000  CLOSE     URCOMEXT,DELETE
.
.         Create a new extract file
.
CFIL5000  PREP      URCOMEXT,"convcomm"
.
          MOVE      ZERO,EXIT
          GOTO      CFIL9999
.
CFIL9100  MOVE      ONE,EXIT
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through the Patient Comments file and extract each record     *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          MOVE      ZERO,EXTCNTHR                * initialise extract count
          MOVE      ZERO,EXTCNTDT
          MOVE      ZERO,PRCCNTHR                * initialise line count
          MOVE      ZERO,PRCCNTDT 
          DISPLAY   *P1:24,*EL,"Processing:";
.
          CALL      EXCMTA00
.
          DISPLAY   *P1:19,*EF,"Extraction complete.":
                    *P1:20,*V2LON,PRCCNTHR,*HOFF:
                    " pmsuchaf records processed. ":
                    *P1:21,*V2LON,PRCCNTDT,*HOFF:
                    " pmsucdaf records processed. ":
                    *P1:22,*V2LON,EXTCNTHR,*HOFF:
                    " pmsuchaf records extracted. ":
                    *P1:23,*V2LON,EXTCNTDT,*HOFF:
                    " pmsucdaf records extracted. "
          CALL      HITENTER
.
PROC9999  RETURN
.
.-----------------------------------------------------------------------
. Extract all the active U/R comment details
.-----------------------------------------------------------------------
EXCMTA00  MOVE      SP70,KEY17
          CALL      RSPMUCH1
EXCMTA10  CALL      RKPMUCH1
          BRANCH    OVRCD,EXCMTA99
.
          ADD       ONE,PRCCNTHR 
          DISPLAY   *P13:24,*V2LON,PRCCNTHR
.
          MATCH     "1",PMUHDELT            * is comment deleted
          GOTO      EXCMTA10 IF EQUAL
.
. Extract comment header
.
          MOVE      "0",CEXTTYPE 
          MOVE      PMUHURNO,CEXTURNO
          MOVE      PMUHCTYP,CMUHCTYP
          MOVE      PMUHCNUM,CMUHCNUM
          MOVE      PMUHCUID,CMUHCUID
          MOVE      PMUHCDAT,CMUHCDAT
          MOVE      PMUHCTIM,CMUHCTIM
          MOVE      PMUHOCCG,CMUHOCCG
          MOVE      PMUHDELT,CMUHDELT
          MOVE      PMUHDUID,CMUHDUID
          MOVE      PMUHDDAT,CMUHDDAT
          MOVE      PMUHDTIM,CMUHDTIM
          MOVE      PMUHDOCG,CMUHDOCG 
.
          SQUEEZE   CEXTURNO  
          SQUEEZE   CMUHCTYP
          SQUEEZE   CMUHCNUM
          SQUEEZE   CMUHCUID
          SQUEEZE   CMUHCDAT
          SQUEEZE   CMUHCTIM
          SQUEEZE   CMUHOCCG
          SQUEEZE   CMUHDELT
          SQUEEZE   CMUHDUID
          SQUEEZE   CMUHDDAT
          SQUEEZE   CMUHDTIM
          SQUEEZE   CMUHDOCG
.
          WRITE     URCOMEXT,SEQ;*+,CEXTTYPE,PIPE,CEXTURNO,PIPE,CMUHCTYP,PIPE:
                                    CMUHCNUM,PIPE,CMUHCUID,PIPE,CMUHCDAT,PIPE:
                                    CMUHCTIM,PIPE,CMUHOCCG,PIPE,CMUHDELT,PIPE:
                                    CMUHDUID,PIPE,CMUHDDAT,PIPE,CMUHDTIM,PIPE:
                                    CMUHDOCG,*-  
          ADD       ONE,EXTCNTHR
.
          CALL      EXCMXA00
.
          GOTO      EXCMTA10
.
EXCMTA99  RETURN
.
.---------------------------------------------------------------------
. Extract UR comment text details
.---------------------------------------------------------------------
EXCMXA00  PACK      KEY20,PMUHURNO,PMUHCTYP,PMUHCNUM,SP70
          CALL      RSPMUCD1
EXCMXA20  CALL      RKPMUCD1
          BRANCH    OVRCD,EXCMXA99
.
          MATCH     PMUHURNO,PMUCURNO
          GOTO      EXCMXA99 IF NOT EQUAL
.
          MATCH     PMUHCTYP,PMUCCTTY
          GOTO      EXCMXA99 IF NOT EQUAL
.
          MATCH     PMUHCNUM,PMUCCNUM
          GOTO      EXCMXA99 IF NOT EQUAL
.
          ADD       ONE,PRCCNTDT 
.
. Extract comment details
.
          MOVE      "1",CEXTTYPE 
          MOVE      PMUCURNO,CEXTURNO
          MOVE      PMUCCTTY,CMUCCTTY
          MOVE      PMUCCNUM,CMUCCNUM
          MOVE      PMUCLNUM,CMUCLNUM
          MOVE      PMUCCOMM,CMUCCOMM
.      
          SQUEEZE   CEXTURNO  
          SQUEEZE   CMUCCTTY	
          SQUEEZE   CMUCCNUM	
          SQUEEZE   CMUCLNUM	
          STRIP     CMUCCOMM  
.
          WRITE     URCOMEXT,SEQ;*+,CEXTTYPE,PIPE,CEXTURNO,PIPE,CMUCCTTY,PIPE:
                                    CMUCCNUM,PIPE,CMUCLNUM,PIPE,CMUCCOMM,*-
          ADD       ONE,EXTCNTDT
.
          GOTO      EXCMXA20
.
EXCMXA99  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC

.******************************************************************************
.* System   :  OUTPATIENTS                                                    *
.* Program  :  IBAOUT69                                                       *
.* Desc     :  SET-UP LETTERS                                                 *
.******************************************************************************
.* Date     :  23/10/89                                                       *
.* Author   :  Steve O'Gorman                                                 *
.* Comment  :                                                                 *
.******************************************************************************
.* V11.05.01 08/09/2025  Thanh T          TSK 0949457                         *
.*           Recompiled for OUTLVLET/OUTPLTCD changes                         *
.******************************************************************************
.*        V11.04.03 22/07/2024  Thanh T           TSK 0939648                 *
.*                  Recompiled for OUTLVLET changes                           *
.*        V11.04.02 15/04/2024  Sunny             TSK 0893817                 *
.*                  Recompiled for OUTPLTCD                                   *
.*        V11.04.01 14/11/2023  Sunny             TSK 0893817                 *
.*                  Recompiled for OUTPLTCD                                   *
.******************************************************************************
.*        V11.02.01 04/04/2022  Sunny             TSK 0899125                 *
.*                  Recompiled for OUTLVLET                                   *
.*        V11.00.03 16/07/2020  Tracey Nguyen     TSK 0893621                 *
.*                  Recompiled for OUTLVLET                                   *
.*                   - Added %smsusrnm (LCCSMSUN/LXXSMSUN)                    *
.*        V11.00.02 20/03/2020  Peter Vela     TSK 0889143                    *
.*                  Fixed OUTLET IO calls                                     *
.*        V11.00.01 18/03/2020  Peter Vela     TSK 0889143                    *
.*                  Recompiled for OUTLETFD                                   *
.*        V10.15.01 05/12/2019  Thanh T           TSK 0885188                 *
.*                  Recompiled for OUTLVLET                                   *
.******************************************************************************
.*        V10.14.03 25/07/2019  Sunny             TSK 0877616                 *
.*                  Recompiled for OTBBSRVD - Service delievery description   *
.*        V10.14.02 04/06/2019  Nicole Torrisi    TSK 0870873                 *
.*                  Recompiled for OUTPLTCD and OUTLVLET                      *
.*        V10.14.01 12/03/2019  Nicole Torrisi    TSK 0870242                 *
.*                  Recompiled for OUTPLTCD                                   *
.*        V10.13.01 05/12/2018  Don Nguyen      TSK 0838558                   *
.*                  Deleted temp file (TEMP1) on program exit                 *
.*        V10.11.01 11/08/2017  Ania P          TSK 0843055                   *
.*                  Recomp for OUTPLTCD                                       *
.*        V10.10.01 23/03/2017 Thanh T.         TSK 083206                    *
.*                  Recompiled for OUTPLTCD                                   *
.*        V10.07.01 11/01/2016 Ebon Clements    TSK 0294154                   *
.*                  Added %smsid, %smscode, %smskey, %smsurno, %smsuser,      *
.*                  %smsphone                                                 *
.*        V10.06.01 10/04/2015 Ania P           CAR 261630                    *
.*                  Removed the use of temporary file name                    *
.*        V10.03.04 12/11/2013 Davin            CAR 292499                    *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V10.03.03 18/04/2013 Ania P           CAR 281663                    *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V10.03.02 03/12/2012 Peter Vela       CAR 262297                    *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V10.03.01 05/12/2012  Jill Parkinson  CAR 273375                    *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V9.11.01  31/10/2008 Sandra Barcham   CAR 178932                    *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.07.02  01/09/2006  Peter Vela      CAR 114297                    *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.07.01  21/06/2006  Peter Vela      CAR 108110                    *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.06.03  31/05/2006  Peter Vela      CAR 106057                    *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*                  Added PMSRELFD and PMSRELIO                               *
.*        V9.06.02  30/05/2006  Peter Vela      CAR 104807                    *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.06.01  28/04/2006  Peter Vela      CAR 102166                    *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.05.01  30/03/2006  Peter Vela      CAR 85995                     *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.04.06  03/11/2005  Peter Vela      CAR 79842                     *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.04.05  01/09/2005  Peter Vela      CAR 62749                     *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.04.04  09/06/2005  Peter Vela      CAR 62622                     *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.04.03  23/03/2005  Peter Vela      CAR 59618                     *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*                  23/03/2005  Peter Vela      CAR 57244                     *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*                  23/03/2005  Peter Vela      CAR 59277                     *
.*                  Recompiled for OUTLVLET and OUTPLTCD                      *
.*        V9.04.02  24/09/2004  Peter Vela      CAR 53708                     *
.*                  Recompiled for OUTLVLET (add variables to Letters)        *
.*        V9.04.01  31/08/2004  Guomin Fu       CAR 40390                     *
.*                  Recompiled for OUTLVLET (add variables to Letters)        *
.*        V9.03.03  30/06/2004  Mike Laming     CAR 40390                     *
.*                  Recompiled for OUTLVLET (add variables to Letters)        *
.*        V9.03.02  09/02/2004  Sylvek Litewka  CAR 47000                     *
.*                  Recompiled for OUTLETFD changes. Added clear of new       *
.*                  variable OTLTTYPE.                                        *
.*        V9.03.01  26/06/2003  CAR 39819   Mike Laming                       *
.*                  Recompiled for OUTLVLET & OUTPLTCD -                      *
.*                    Added %rflcldes (Referral Clinic Description)           *
.*       V5.08.B01  14/01/2000  J.Tan                                         *
.*                  Fixed o/p referral letter fields (v5.08 changes)          *
.*       V5.07.01   09/12/1999  J.Tan                                         *
.*                  Added o/p referral letter fields (v5.08 changes)          *
.*       V5.04.04  15/04/1997  howellsy   RHA                                 *
.*                  Added user def 5-8, rank                                  *
.*        V5.04.03  11/04/1997  Steve Armstrong    SRF 642798                 *
.*                  Fixed UK bug (not padding out work variable)              *
.*        V5.04.02  25/03/1997  Greg Horvat      SRF 617348                   *
.*                  Recompiled for OUTLVLET - Added xtra screen variables     *
.*        V5.04.01  18/02/97 Howellsy     WHT                                 *
.*                  Added Cell Phone, Postal Add1-4, Postal Post Code         *
.*        V5.03.02  25/10/1995    Justin Coates  (SUHT mods)                  *
.*                  added mail merge file name and new variables              *
.*        V5.03.01  10/07/1995    Max White  SRF 441324 / Quote 440069        *
.*                  Added %day     - clinic day (Monday, Tuesday ..)          *
.*                        %cldate  - clinic date (1st, 2nd ...)               *
.*                        %month   - clinic month (January, February ..)      *
.*                        %year    - clinic century & year (1995 ...)         *
.*                        %loctype - clinic location type                     *
.*                        %locn    - clinic location                          *
.******************************************************************************
.
         INC       STD001FD
         INC       OUTLETFD/INC
         INC       OUTLVLET/INC             * LC and LX variables
         INC       OUTRF1FD/INC
         INC       PMSRELFD/INC
         INC       VISCMTFD/INC
         INC       IBASEQFD/INC 
         INC       TFILEVAR/INC
         INC       WEBERRFD/INC
         INC       WEBSECFD/INC

+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
REPSTR    INIT      "a0b0c0d0e0f0g0h0i0j0k0l0m0n0o0p0q0r0s0t0u0v0w0x0y0z0102030405060708090"
.
SRCHNUM   FORM      3
SRCHVAR   DIM       8
SPECCHAR  INIT      "%"
KEYCOL    FORM      "27"
XXXXXX    INIT      "XXXXXXXXXXXXXXXXXXXX"
.
TEMP1     FILE      ASCII, FIXED=79
+
.**********************************************************************
.*                        GLOBAL  VARIABLES                           *
.**********************************************************************
.  
BOTMARG   FORM      2
.
CMDLINE   DIM       50
COUNT     FORM      3 
CODECZ    INIT      "CZ"
.
DIM3      DIM       3
DIM30     DIM       30
DIM70     DIM       70
DISPSTRN  DIM       70
.
ENDSTR    FORM      2
.
FORM3     FORM      3
FORM3A    FORM      3
FNAMER    DIM       8
FPPOS     FORM      2
.
.
LEFTMARG  FORM      2
LETDESC   DIM       20
LETTNAME  DIM       20
LINE      FORM      3
LINENO    FORM      3
LLPOS     FORM      2
LSTPRINT  FORM      3
.
MAXLINES  FORM      3
.
OPTN      DIM       6
.
PERCPOS   FORM      2
PHYSPAGE  FORM      3
POS       FORM      2
PRTSTRNG  DIM       70
.
STARTSTR  FORM      2
.
TEMP2     FORM      2
TEMP3     FORM      2
TEMP70    DIM       70
TMPLETT   DIM       3
TMPLETNO  FORM      3
TOPMARG   FORM      2
.
WORDMODE  FORM      2
.
VERTPOS   FORM      2
.
.----------------------------------------------------------------------
PRGID     INIT      "IBAOUT69"
PRGNAM    INIT      "Set-Up Letters"
VERSION   INIT      "V12.01.00"
.----------------------------------------------------------------------
+
.********************************************************************** 
.*                             MAINLINE                               *
.**********************************************************************
.
ML0000   CALL      INIT0000                      * Initialize
.
ML1000
         CALL      CLR0000                       * Clear Variables
         CALL      SCR0000                       * Call Option Screen
         BRANCH    EXIT,ML9999
         BRANCH    OPTION,ML2000,ML3000,ML4000:
                          ML5000,ML6000,ML7000
.
.        -----  add a letter  -----
.
ML2000    MOVE      "Add",OPTN
          CALL      ILET0000                     * Input the Letter Number
          BRANCH    EXIT,ML1000
.
          CALL      LDET0000                     * Display Parameter Fields
          BRANCH    EXIT,ML2000
.
          CALL      INDT0000                     * Input Letter Details
.
          CALL      SEL0000                      * Select Field to Update
          BRANCH    EXIT,ML2000
.
          CALL      WP0000                       * Call the word processor
          CALL      ADDL0000                     * Add a Letter to the File
          PREP      TEMP1,FNAMER
          CLOSE     TEMP1
          PREP      TEMP1,FNAMER
          GOTO      ML2000
.
.        -----  view a letter  -----
.
ML3000   MOVE      "View",OPTN
         CALL      INLT0000                      * Input the Letter Number
         BRANCH    EXIT,ML1000
.
         CALL      OLCK0000                      * Check if Letter Exists
         BRANCH    EXIT,ML3000
.
         CALL      PARA0000                      * Display the Letter Param's.
         GOTO      ML3000
.
.        -----  view a letter  -----
.
ML4000   MOVE      "Verify",OPTN
         CALL      INLT0000                      * Input the Letter Number
         BRANCH    EXIT,ML1000
.
         CALL      OLCK0000                      * Check if Letter Exists
.
         CALL      VERF0000                      * Modify the line to show
         BRANCH    EXIT,ML4000                   * Maximum Variable Length
.
         DISPLAY   *P1:24,*EL,"End Of Text. ";
         CALL      HITENTER
         GOTO      ML4000
.
.        -----  change a letter  -----
.
ML5000   MOVE      "Change",OPTN
         CALL      INLT0000                      * Input the Letter Number
         BRANCH    EXIT,ML1000
.
         CALL      OLCK0000                      * Check if Letter Exists
         CALL      DETL0000                      * Display the Letters Details
.
         CALL      SEL0000                       * Select Item to Modify
         BRANCH    EXIT,ML1000
.
         CALL      UPDT0000                      * Update Letter in w.p.
         GOTO      ML5000
.
.        ----- DELETE   a letter  -----
.
ML6000   MOVE      "Delete",OPTN
         CALL      INLT0000                      * Input the Letter to DELETE  
         BRANCH    EXIT,ML1000
.
         CALL      OLCK0000                      * Check if Record Exists
         BRANCH    EXIT,ML6000
.
         CALL      PARA0000
         CALL      DEL0000                        * DELETE   Record Procedure
         BRANCH    EXIT,ML1000
         GOTO      ML6000
.
.        ----- print a letter  -----
.
ML7000   MOVE      "Print",OPTN
         CALL      INLT0000                      * Input the Letter to Print
         BRANCH    EXIT,ML1000
.
         CALL      OLCK0000                      * Check if Letter Exists
         BRANCH    EXIT,ML7000
.
         CALL      PRT0000                       * Print the Extended Letter
         GOTO      ML7000
.
ML9999   PREP      TEMP1,FNAMER
         CLOSE     TEMP1,DELETE
         CHAIN     PGM
+
.********************************************************************** 
.*                  INIT0000 - Initialization                         *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          OPEN      WEBSECA3,"websecaf"
          OPEN      IBASEQA1,"ibaseqaf"
          DISPLAY   *P57:24,"Opening outletrf"
          OPEN      OUTLETR1,"outletrf"
          OPEN      OUTLETR2,"outletrf"
          DISPLAY   *P65:24,"controlf"
          OPEN      CONTROLF,"controlf"
          OPEN      OUTRF1A1,"outrf1af"
          DISPLAY   *P65:24,"pmsrelaf"
          OPEN      PMSRELA1,"pmsrelaf"
          DISPLAY   *P65:24,"viscmtaf"
          OPEN      VISCMTA1,"viscmtaf"
.
          MOVE      ONE,CNEWDS
.
. ------- set up port dependancy for temp file -------
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
          CLEAR     CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
INIT9999  RETURN
+
.********************************************************************** 
.*                   CLR0000 - Clear Variables                        *
.**********************************************************************
.
CLR0000   HLINE     *G37,2,47,80            * Clear the mode display
.
          PACK      DIM70,SP30,SP30,SP10
          MOVE      ZERO,OTLTPBOT
          MOVE      ZERO,OTLTPTOP
          MOVE      ZERO,OTLTPPAG
          MOVE      ZERO,OTLTPTAB
          MOVE      SP8,OTLTMMFN
          MOVE      SP8,OTLTTYPE
          MOVE      ZERO,LINE
          MOVE      SP20,LETTNAME
          MOVE      ZERO,TOPMARG
          MOVE      ZERO,LEFTMARG
          MOVE      ZERO,FORM3
.
CLR9999   RETURN
+
.********************************************************************** 
.*                   SCR0000 - Main Option Screen                     *
.**********************************************************************
.
SCR0000   MOVE      FALSE,EXIT
.
          CALL      ULOTLET1
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Add a Letter":
                    *P1:6,*V2LON,"2",*HOFF," = View a Letter":
                    *P1:7,*V2LON,"3",*HOFF," = Verify a Letter":
                    *P1:8,*V2LON,"4",*HOFF," = Change a Letter":
                    *P1:9,*V2LON,"5",*HOFF," = Delete a Letter":
                    *P1:10,*V2LON,"6",*HOFF," = Print a Letter":
                    *P1:12,"Select Option :";
.
SCR0500   KEYIN     *P17:12,*V2LON,*EL,OPTION:
                    *P17:12,*V2LON,*DV,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
.
          BRANCH    OPTION,SCR9999,SCR9999,SCR9999:
                           SCR9999,SCR9999,SCR9999
.
          BEEP
          GOTO      SCR0500
.
SCR9000   MOVE      TRUE,EXIT
.
SCR9999   RETURN
.
+
.****************************************************************************
.*                    ILET0000 - Input the Letter Number Insert mode        *
.****************************************************************************
.
ILET0000  MOVE      FALSE,EXIT
.
          CALL      ULOTLET1
          DISPLAY   *P47:2,*V2LON,"- ",*+,OPTN,SP1;
          DISPLAY   *P1:3,*EF:
                    *P4:4,"Letter Number        :";
.
          MOVE      KEYCOL,ECOL
          MOVE      FOUR,EVERT
          MOVE      ONE,CKYIMODE
          MOVE      ONE,CNEWDS
          CALL      OUTLETKY
          BRANCH    EXIT,ILET9000,ILET9000
          MOVE      DIM3,TMPLETNO
          GOTO      ILET9999
.
ILET9000  MOVE      TRUE,EXIT
.
ILET9999  RETURN
+
.****************************************************************************
.*                    INLT0000 - Input the Letter Number                    *
.****************************************************************************
.
INLT0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P47:2,*V2LON,"- ",*+,OPTN,SP1;
.
          DISPLAY   *P1:3,*EF:
                    *P4:4,"Letter Number       :";
.
          MOVE      "26",ECOL
          MOVE      FOUR,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      ZERO,CKYIMODE
          MOVE      SP3,CKYIDEF3
.
          CALL      OUTLETKY
          BRANCH    EXIT,INLT9999,INLT9000
.
          CALL      RLOTLET1
          IF        OVRCD = 2
            CALL      RECLOCK
            GOTO      INLT0000
          ENDIF
          MOVE      DIM3,TMPLETNO
          MOVE      ZERO,EXIT
          GOTO      INLT9999
.
INLT9000  MOVE      TRUE,EXIT
.
INLT9999  RETURN
.
.****************************************************************************
.*            NWCK0000 - Check if Letter is not on File (New for Add)       *
.****************************************************************************
.
NWCK0000  MOVE      FALSE,EXIT
          MOVE      ZERO,FORM3
          PACK      KEY6,TMPLETNO,FORM3
          CALL      RDOTLET1
          BRANCH    OVRCD,NWCK9999
.
          DISPLAY   *P1:24,*EL,*B,"Letter Already Exists on File. ";
          CALL      HITENTER
.
          MOVE      TRUE,EXIT
.
NWCK9999  RETURN
+
.****************************************************************************
.*            OLCK0000 - Check that letter exists on file                   *
.*                       For : view , verify , change , DELETE   , print      *
.****************************************************************************
.
OLCK0000  MOVE      FALSE,EXIT
.
          MOVE      ZERO,FORM3
          PACK      KEY6,TMPLETNO,FORM3
          CALL      RDOTLET1
          BRANCH    OVRCD,OLCK9000
.
          MOVE      OTLTTEXT,LETTNAME
          MOVE      OTLTPTOP,TOPMARG
          MOVE      OTLTPTAB,LEFTMARG
          MOVE      OTLTPPAG,PHYSPAGE
          GOTO      OLCK9999
.
OLCK9000  DISPLAY   *P1:24,*EL,*B,"Letter Not Found On File. ";
          CALL      HITENTER
.
OLCK9999  RETURN
.
+
.****************************************************************************
.*            LDET0000 - Input the Details of the new Letter                *
.****************************************************************************
.
LDET0000  DISPLAY   *P1:5,*EF:
                   *P1:05,"   Name                 :":
                   *P1:07,*V2LON,"1",*HOFF,". Paper Page Length    :":
                   *P1:08,*V2LON,"2",*HOFF,". Top Margin Size      :":
                   *P1:09,*V2LON,"3",*HOFF,". Bottom Margin Size   :":
                   *P1:10,*V2LON,"4",*HOFF,". Left Margin Size     :":
                   *P1:11,*V2LON,"5",*HOFF,". Mail Merge File Name :"
.
LDET9999  RETURN
+
.****************************************************************************
.*              INDT0000 - Input the Letter Details                         *
.****************************************************************************
.
INDT0000  CALL      NAME0000            * Input the Letter Name
          CALL      PAPL0000            * Input the Paper Page Length
          CALL      TMAR0000            * Input the Top Margin Size
          CALL      BMAR0000            * Input the Bottom Margin Size
          CALL      LMAR0000            * Input the Left Margin Size
          CALL      MMFN0000            * Input the Mail Merge File Name
.
INDT9999  RETURN
+
.****************************************************************************
.*    SEL0000 - Select Field to Update on the Input Letter Detail Screen    *
.****************************************************************************
.
SEL0000   MOVE      FALSE,EXIT
          CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      SEL0999 IF EQUAL
          COMPARE   "-1",CCITEM
          GOTO      SEL0900 IF EQUAL
          COMPARE   EIGHT,CCITEM
          GOTO      SEL0000 IF NOT LESS
.
SEL0100   BRANCH    CCITEM,SEL0200,SEL0300,SEL0400,SEL0500,SEL0600
          GOTO      SEL0000
.
SEL0200   CALL      PAPL0000            * Input the Paper Page Length
          GOTO      SEL0000
.
SEL0300   CALL      TMAR0000            * Input the Top Magin Size
          GOTO      SEL0000
.
SEL0400   CALL      BMAR0000            * Input the Bottom Margin Size
          GOTO      SEL0000
.
SEL0500   CALL      LMAR0000            * Input the Left Margin Size
          GOTO      SEL0000
.
SEL0600   CALL      MMFN0000            * Input the Mail Merge File Name
          GOTO      SEL0000
.
SEL0900   MOVE      TRUE,EXIT
.
SEL0999   RETURN
+
.****************************************************************************
.*              NAME0000 - Input the Letter Name                            *
.****************************************************************************
.
NAME0000  KEYIN     *PKEYCOL:5,*V2LON,LETTNAME:
                    *PKEYCOL:5,*DV,LETTNAME;
.
          RETURN
+
.****************************************************************************
.*             PAPL0000 - Input the Physical Paper Length                   *
.****************************************************************************
.
PAPL0000  KEYIN     *PKEYCOL:7,*V2LON,OTLTPPAG:
                    *PKEYCOL:7,*DV,OTLTPPAG;
.
          COMPARE   ZERO,OTLTPPAG
          GOTO      PAPL0000 IF EQUAL
.
          COMPARE   OTLTPPAG,OTLTPBOT
          GOTO      PAPL9000 IF LESS
          DISPLAY   *P1:24,*EL,*B,"Page Length Must Be Greater Than The ":
                           "Bottom Margin. ";
          CALL      HITENTER
          GOTO      PAPL0000
.
PAPL9000  CALL      MARG0000
          BRANCH    EXIT,PAPL0000
.
PAPL9999  RETURN
+
.****************************************************************************
.*             TMAR0000 - Input the top Margin Size                         *
.****************************************************************************
.
TMAR0000  KEYIN     *PKEYCOL:8,*V2LON,OTLTPTOP:
                    *PKEYCOL:8,*DV,OTLTPTOP;
.
          COMPARE   ZERO,OTLTPTOP
          GOTO      TMAR0000 IF EQUAL
          COMPARE   OTLTPPAG,OTLTPTOP
          GOTO      TMAR9000 IF LESS
          DISPLAY   *P1:24,*EL,*B,"Top Margin Must Be Less than Page length. ";
          CALL      HITENTER
          GOTO      TMAR0000
.
TMAR9000  MOVE      OTLTPPAG,FORM3
          SUB       TWO,FORM3
          COMPARE   FORM3,OTLTPTOP
          GOTO      TMAR9900 IF LESS
          DISPLAY   *P1:24,*EL,*B,"Top Margin Too Big. ";
          CALL      HITENTER
          GOTO      TMAR0000
.
TMAR9900  CALL      MARG0000
          BRANCH    EXIT,TMAR0000
.
TMAR9999  RETURN
+
.****************************************************************************
.*              BMAR0000 - Input the Bottom Margin                          *
.****************************************************************************
.
BMAR0000  KEYIN     *PKEYCOL:9,*V2LON,OTLTPBOT:
                    *PKEYCOL:9,*DV,OTLTPBOT;
.
          COMPARE   ZERO,OTLTPBOT
          GOTO      BMAR0000 IF EQUAL
.
          COMPARE   OTLTPPAG,OTLTPBOT
          GOTO      BMAR9000 IF LESS
          DISPLAY   *P1:24,*EL,*B,"Bottom Margin Must Be Less Than ":
                    "Page length. ";
          CALL      HITENTER
          GOTO      BMAR0000
.
BMAR9000  MOVE      OTLTPPAG,FORM3
          SUB       TWO,FORM3
          COMPARE   FORM3,OTLTPBOT
          GOTO      BMAR9900 IF LESS
          DISPLAY   *P1:24,*EL,*B,"Bottom Margin Too Big. ";
          CALL      HITENTER
          GOTO      BMAR0000
.
BMAR9900  CALL      MARG0000
          BRANCH    EXIT,BMAR0000
.
BMAR9999  RETURN
+
.****************************************************************************
.*             LMAR0000 - Input the Left Margin                             *
.****************************************************************************
.
LMAR0000  KEYIN     *PKEYCOL:10,*V2LON,OTLTPTAB:
                    *PKEYCOL:10,*DV,OTLTPTAB;
.
          COMPARE   ZERO,OTLTPTAB
          GOTO      LMAR0000 IF EQUAL
.
          COMPARE   TWENTY1,OTLTPTAB
          GOTO      LMAR9999 IF LESS
.
          DISPLAY   *P1:24,*EL,*B,"Left Margin May Not Exceed 20. ";
          CALL      HITENTER
          GOTO      LMAR0000
.
LMAR9999  RETURN
+
.****************************************************************************
.         enter Mail Merge File Name
.****************************************************************************
MMFN0000   KEYIN     *PKEYCOL:11,*V2LON,OTLTMMFN:
                     *PKEYCOL:11,*DV,OTLTMMFN;
           RETURN
+
.****************************************************************************
.*              MARG0000 - Verify if Top + Bottom < Page Length             *
.****************************************************************************
.
MARG0000  MOVE      FALSE,EXIT
          MOVE      OTLTPTOP,FORM3
          ADD       OTLTPBOT,FORM3
          COMPARE   OTLTPPAG,FORM3
          GOTO      MARG9999 IF LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Top & Bottom Margin Combination. ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
.
MARG9999  RETURN
+
.****************************************************************************
.*              HEAD0000 - Write the Header Record to the Letter File       *
.****************************************************************************
.
HEAD0000  MOVE      ZERO,FORM3
          MOVE      LETTNAME,OTLTTEXT
          PACK      KEY6,TMPLETNO,FORM3
.
          MOVE      TMPLETNO,OTLTLTNO
          MOVE      FORM3,OTLTLINO
.
          CALL      RAOTLET1
          BRANCH    OVRCD,HEAD1000
.
          CALL      UPOTLET1
          GOTO      HEAD9999
.
HEAD1000  CALL      WROTLET1
.
HEAD9999  RETURN
+
.****************************************************************************
.*              SET UP THE TEMP FILE AND CALL THE WORD PROCESSOR            *
.****************************************************************************
.
WP0000    DISPLAY   *P1:6,*EF;
          MOVE      EIGHT,WORDMODE
          PREP      TEMP1,FNAMER
          CLOSE     TEMP1
          WORD      CMDLINE,99,5,7,76,23,WORDMODE
.
          RETURN
+
.************************************************************ 
.*       ADDL0000 - Add a Letter to the Letter File         *
.************************************************************ 
.
ADDL0000  CALL      POSTQST
          BRANCH    CEXIT,ADDL0500,ADDL9999,ADDL9999
.
ADDL0500  
.         DISPLAY   *P1:24,*EL,"Posting Changes to the Letter",*BLINKON,*V2LON:
.                          " - Please Wait";
.
ADDL1000  CALL      HEAD0000                      * Write Header Record
.          PREP      TEMP1,FNAMER                  * Reading
.          CLOSE     TEMP1
.          PREP      TEMP1,FNAMER                  * Reading
.    
          OPEN      TEMP1,FNAMER
          MOVE      ZERO,COUNT
.
ADDL2000  MOVE      ZERO,OTLTLVAR                    * Reset the "%" Sign Marker
          PACK      DIM70,SP30,SP30,SP10
          PACK      OTLTTEXT,SP30,SP30,SP10
          READ      TEMP1,SEQ;DIM70               * Read next line in temp file
          GOTO      ADDL9999 IF OVER
          ADD       ONE,COUNT                     * Increment count for the key
          MOVE      DIM70,OTLTTEXT
          SCAN      SPECCHAR,OTLTTEXT             * Set the Marker in the Letter
          GOTO      ADDL2100 IF NOT EQUAL         * File if the line of text
          MOVE      ONE,OTLTLVAR                     * contains a "%"
.
ADDL2100  PACK      KEY6,TMPLETNO,COUNT           * Set up the key with Letter
.                                                * Number & Line Number
          MOVE      TMPLETNO,OTLTLTNO
          MOVE      COUNT,OTLTLINO
.
          CALL      WROTLET1           
          GOTO      ADDL2000
.
ADDL9999  RETURN
.
.*********************************************************************
.         display the variables
.*********************************************************************
DLVR0000  DISPLAY   *PKEYCOL:5,*V2LON:
                    *PKEYCOL:5,LETTNAME:
                    *PKEYCOL:7,OTLTPPAG:
                    *PKEYCOL:8,OTLTPTOP:
                    *PKEYCOL:9,OTLTPBOT:
                    *PKEYCOL:10,OTLTPTAB:
                    *PKEYCOL:11,OTLTMMFN 
          RETURN
+
.********************************************************************** 
.*              PARA0000 - Display the Letter Parameters              *
.**********************************************************************
.
PARA0000  CALL      DETL0000
.
. ----- Load Current Details Into the File
.
          PREP      TEMP1,FNAMER
          MOVE      ZERO,LINE
          PACK      KEY6,TMPLETNO,LINE
          CALL      RSOTLET1
PARA1000  CALL      RKOTLET1
          BRANCH    OVRCD,PARA5000
.
          COMPARE   TMPLETNO,OTLTLTNO
          GOTO      PARA5000 IF NOT EQUAL
.
          APPEND    OTLTTEXT,DIM70
          RESET     DIM70
          WRITE     TEMP1,SEQ;DIM70
          GOTO      PARA1000
.
PARA5000  MOVE      FOUR,WORDMODE
          CLOSE     TEMP1
          WORD      CMDLINE,99,5,12,76,23,WORDMODE
.
PARA9999  RETURN
+
.********************************************************************** 
.*                DISP0000 - Display the Letter Text                  *
.**********************************************************************
.
DISP0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P1:7,*EF;
          BOX       1,5,6,76,23
          MOVE      SIX,LINE
.
          MOVE      ZERO,FORM3
          PACK      KEY6,TMPLETNO,FORM3
          CALL      RSOTLET1
DISP1000  CALL      RKOTLET1
          BRANCH    OVRCD,DISP9999
.
          ADD       ONE,LINE
          COMPARE   TWENTY3,LINE
          GOTO      DISP9999 IF NOT LESS
.
          DISPLAY   *P5:LINE,OTLTTEXT;
          GOTO      DISP1000
.
DISP9999  RETURN
+
+
.********************************************************************** 
.*                VERF0000 - Display the Full Coded Text              *
.**********************************************************************
.
VERF0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P4:5,"Letter Name         : ",*V2LON,LETTNAME;
.
          MOVE      ONE,FORM3
          PACK      KEY6,TMPLETNO,FORM3
          CALL      RDOTLET1
          BRANCH    OVRCD,VERF9000
          MOVE      SEVEN,LINE
.
          ADD       SIX,LEFTMARG
          ADD       LINE,TOPMARG
.
          DISPLAY   *P1:6,*EF;
          BOX       1,5,6,76,23
.
VERF0500  ADD       ONE,LINE
          COMPARE   TWENTY3,LINE
          CALL      PAGE0000 IF NOT LESS
          BRANCH    EXIT,VERF9999
          COMPARE   LINE,TOPMARG
          GOTO      VERF0500 IF NOT EQUAL
.
VERF1000  MOVE      OTLTTEXT,DIM70
          CALL      MOD0000
          RESET     PRTSTRNG
          STRIP     PRTSTRNG
.
          DISPLAY   *PLEFTMARG:LINE,*+,PRTSTRNG;
          CALL      RKOTLET1
          BRANCH    OVRCD,VERF9999
          COMPARE   OTLTLTNO,TMPLETNO
          GOTO      VERF9999 IF NOT EQUAL
          ADD       ONE,LINE
          COMPARE   TWENTY3,LINE
          CALL      PAGE0000 IF NOT LESS
          BRANCH    EXIT,VERF9999
          GOTO      VERF1000
.
VERF9000  MOVE      TRUE,EXIT
          DISPLAY   *P1:24,*EL,*B,"No Text Entered For This Letter. "; 
          CALL      HITENTER
.
VERF9999  RETURN
+
.********************************************************************** 
.*    PAGE0000 - message at the end of a page of text                 *
.**********************************************************************
.
PAGE0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:24,*EL,"(",*V2LON,"N",*HOFF,")ext Page ":
                           "(",*V2LON,"E",*HOFF,")xit : ";
PAGE1000  KEYIN     *P22:24,*V2LON,ANS;
          REP       UPPLOW,ANS
          DISPLAY   *P22:24,*V2LON,ANS;
.
          MATCH     "N",ANS
          GOTO      PAGE2000 IF EQUAL
          MATCH     "E",ANS
          GOTO      PAGE9000 IF EQUAL
.
          BEEP
          GOTO      PAGE1000
.
PAGE2000  MOVE      SEVEN,LINE                    * Reset line to top of print
          DISPLAY   *P1:6,*EF 
          BOX       1,5,6,76,23
          SUB       LINE,TOPMARG                  * Reduce top margin by the no.
          GOTO      PAGE9999
.                                                * of lines printed
PAGE9000  MOVE      TRUE,EXIT
.
PAGE9999  RETURN
+
.********************************************************************** 
.*    MOD0000 - MODIFY A LINE TO SHOW MAXIMUM VARIABLE LENGTH         *
.**********************************************************************
.
MOD0000   PACK      PRTSTRNG,SP30,SP30,SP10      * Initialize the PRTSTRNG
          RESET     PRTSTRNG,0                   * and pointer positions
          MOVE      ONE,STARTSTR
          MOVE      ONE,ENDSTR
.
MOD2000   SCAN      SPECCHAR,DIM70               * Scan the Input Line for
          GOTO      MOD8000 IF NOT EQUAL         * a "%" sign
.
MOD3000   MOVEFPTR  DIM70,PERCPOS
          BUMP      DIM70,-1                     * Go to the character before
.                                                * the "%" sign
          GOTO      MOD3100 IF NOT EOS           * Test if the % sign is the
          RESET     DIM70,0                      * first character on the line
.                                                * set f.p. to 0 if it is
MOD3100   MOVEFPTR  DIM70,ENDSTR                 * Store endstr at position
.                                                * 1 char before the "%" sign
.
          COMPARE   STARTSTR,ENDSTR              * Ensure that end and start
          GOTO      MOD3200 IF NOT LESS          * positions don't overlap
.
          RESET     DIM70,STARTSTR               * Set pointer to start i.e
          GOTO      MOD9000 IF EOS               * to "%" if end < start
          GOTO      MOD3300
.
MOD3200   RESET     DIM70,STARTSTR               * Reset the pointers to 
          SETLPTR   DIM70,ENDSTR                 * extract text preceeding
.                                                * the "%" sign
.
          APPEND    DIM70,PRTSTRNG               * Append preceeding text to
.                                                * the print string
.
MOD3300   SETLPTR   DIM70,70                     * Set the pointers to start at
          RESET     DIM70,PERCPOS                * "%" and go to position 70
.
.         SRF 642798
.
          MOVE       DIM70,TEMP70                * DELETE   preceeding text
          PACK       DIM70,TEMP70,SP30,SP30,SP30 * from the dim70 string
.
MOD4000   CALL      GETV0000                     * Call the extraction routine
.                                                * to get the % variable
          MOVEFPTR  DIM70,STARTSTR               * Store finish pos of var.
          BRANCH    EXIT,MOD5000
          RESET     SRCHVAR                      * reset search string
          SEARCH    SRCHVAR,LCPAGE,LCNOVARS,SRCHNUM * Search the list of const.
.                   search string,starting point,how many items,posn located
.
          COMPARE   ZERO,SRCHNUM                 * Exit if not a % variable
          GOTO      MOD5000 IF EQUAL
.
          COMPARE   SIX,OPTION
          GOTO      MOD4100 IF NOT EQUAL         * not print so skip next test
.
          BRANCH    SRCHNUM,MOD7000              * page break
.
.  -----  Load the maximum length of the variable into the print string -----
.
MOD4100   CALL      LDAT0000
.
          APPEND    DISPSTRN,PRTSTRNG            * Add the full description 
          GOTO      MOD8000 IF EOS               * to the print string 
          GOTO      MOD2000       
.
MOD5000   RESET     SRCHVAR                      * Add the string starting with
          APPEND    SRCHVAR,PRTSTRNG             * a % if not found in the 
          GOTO      MOD2000                      * variable list
.
MOD7000   PRINT     *N;                          * Page break
          COMPARE   COUNT,PHYSPAGE
          GOTO      MOD7100 IF EQUAL
          ADD       ONE,COUNT
          GOTO      MOD7000
.
MOD7100   MOVE      ZERO,COUNT
.
MOD7400   ADD       ONE,COUNT
          COMPARE   COUNT,TOPMARG
          GOTO      MOD7500 IF EQUAL
          GOTO      MOD7500 IF LESS
          PRINT     *N;
          GOTO      MOD7400
.
MOD7500   GOTO      MOD9999
.
MOD8000   APPEND    DIM70,PRTSTRNG               * Add remaining text to print
                                                 * string if no "%" signs left
MOD9000   MOVE      FALSE,EXIT
.
MOD9999   RETURN
.
+
.********************************************************************** 
.*    GETV0000 - GETS A LETTER VARIABLE FROM TEXT AFTER THE SCAN      *
.**********************************************************************
.
GETV0000  MOVE      FALSE,EXIT
          PACK      SRCHVAR,SP30,SP30,SP10
          RESET     SRCHVAR,0
          MOVE      DIM70,ANS
          APPEND    ANS,SRCHVAR
          RESET     SRCHVAR,0
          BUMP      DIM70
          MOVE      DIM70,ANS
          REPLACE   REPSTR,ANS
          MATCH     "0",ANS
          GOTO      GETV9000 IF NOT EQUAL
          BUMP      DIM70,-1
          PACK      SRCHVAR,SP30,SP30,SP10
          RESET     SRCHVAR,0
.
GETV1000  MOVE      DIM70,ANS
          APPEND    ANS,SRCHVAR
.
          BUMP      DIM70
          GOTO      GETV9999 IF EOS
          MOVE      DIM70,ANS
          REPLACE   REPSTR,ANS
          MATCH     "0",ANS
          GOTO      GETV9999 IF NOT EQUAL
          GOTO      GETV1000
.
GETV9000  MOVE      TRUE,EXIT
.
GETV9999  RETURN
.
+
.****************************************************************************
.*              DISPLAY AND UPDATE THE PARAMETERS OF THE LETTER             *
.****************************************************************************
.
DETL0000  CALL      LDET0000
          CALL      DLVR0000
.
DETL9999  RETURN
+
.****************************************************************************
.*              SET UP THE TEMP FILE AND CALL THE WORD PROCESSOR            *
.****************************************************************************
.
UPDT0000  MOVE      TMPLETNO,OTLTLTNO
          MOVE      ZERO,OTLTLINO
          MOVE      LETTNAME,OTLTTEXT
.
          CALL      RAOTLET1
.
          CALL      UPOTLET1
.
. ------  Set up the Header for the Word Processor -----
.
          DISPLAY   *P1:7,*EF;
          PREP      TEMP1,FNAMER
.
.************************************************************ 
.*  read in the old call details from OUTLETR to tempsoln   *
.************************************************************
.
UPDT1000  MOVE      ONE,LINENO               * Reads in the previous letter
          MOVE      ZERO,MAXLINES            * details from the OUTLETR file
          PACK      KEY6,TMPLETNO,LINENO     * based on the Letter No. entered.
          CALL      RDOTLET1                   * The MAXLINES variable is used
          COMPARE   ONE,OVRCD                * keep a count of the no. of lines
          GOTO      UPDT2999 IF EQUAL        * of text read from the OUTLETR
.
UPDT2000  WRITE     TEMP1,SEQ;OTLTTEXT         * file.
          ADD       ONE,MAXLINES             * The data is loaded into the 
          PACK      OTLTTEXT,SP30,SP30,SP10    * temp file for use by the word
          CALL      RKOTLET1                  * processor.
          COMPARE   ONE,OVRCD
          GOTO      UPDT2999 IF EQUAL
          COMPARE   TMPLETNO,OTLTLTNO
          GOTO      UPDT2999 IF NOT EQUAL
          GOTO      UPDT2000
UPDT2999
+
.************************************************************ 
.*              execute the word processor                  *
.************************************************************
.
          MOVE      ZERO,WORDMODE
          CLOSE     TEMP1
.
          WORD      FNAMER,99,4,7,76,23,WORDMODE
.
UPDT3000  CALL      POSTQST
          BRANCH    CEXIT,UPDT3050,UPDT3999,UPDT3999
.
UPDT3050  CLOSE     TEMP1                    * Reads the new call details out
          OPEN      TEMP1,FNAMER             * of the temp file and puts them
          MOVE      ZERO,LINENO               * back into the OUTLETR file.
          MOVE      TMPLETNO,OTLTLTNO          * Records are updated if the key
.                                             * exists or are written if the 
UPDT3100  PACK      DIM70,SP30,SP30,SP10     * key dosen't exist.
          PACK      OTLTTEXT,SP30,SP30,SP10
          MOVE      ZERO,OTLTLVAR
          READ      TEMP1,SEQ;DIM70          
          GOTO      UPDT3700 IF OVER 
.
          ADD       ONE,LINENO
          SCAN      SPECCHAR,DIM70
          GOTO      UPDT3150 IF NOT EQUAL
          MOVE      ONE,OTLTLVAR
.
UPDT3150  SETLPTR   DIM70,70                
          RESET     DIM70                  
          MOVE      LINENO,OTLTLINO
          PACK      KEY6,OTLTLTNO,OTLTLINO    
          CALL      RAOTLET1              
          COMPARE   ZERO,OVRCD
          GOTO      UPDT3200 IF EQUAL
          MOVE      DIM70,OTLTTEXT
          CALL      WROTLET1
          GOTO      UPDT3100
.
UPDT3200  MOVE      DIM70,OTLTTEXT
          CALL      UPOTLET1
          GOTO      UPDT3100
.
UPDT3700  COMPARE   MAXLINES,LINENO          * This loop acts to DELETE   any
          GOTO      UPDT3999 IF NOT LESS     * lines of text which are 
          PACK      KEY6,OTLTLTNO,MAXLINES     * extra information left from
          CALL      DEOTLET1                   * the previous call details.
          SUB       ONE,MAXLINES
          GOTO      UPDT3700                 * This is only performed if
.
UPDT3999  RETURN                             * LINENO < MAXLINES
.
+
.**********************************************************************
.*          DEL0000 - Delete a Letter from the Letter File            *
.**********************************************************************
.
DEL0000   MOVE      FALSE,EXIT
.
          CALL      DELQST
          BRANCH    CEXIT,DEL1000,DEL9999,DEL9000  
.
DEL1000   MOVE      ZERO,FORM3
          PACK      KEY6,TMPLETNO,FORM3
          CALL      DEOTLET1
.
DEL2000   ADD       ONE,FORM3
          PACK      KEY6,TMPLETNO,FORM3
          CALL      RDOTLET1
          BRANCH    OVRCD,DEL9999
          CALL      DEOTLET1
          GOTO      DEL2000
.
DEL9000   MOVE      TRUE,EXIT
.
DEL9999   RETURN
+
.**********************************************************************
.*          PRT0000 - Print the Verified Version of the Letter        *
.**********************************************************************
.
PRT0000   MOVE      FALSE,EXIT
          MOVE      ZERO,FORM3
          MOVE      ZERO,COUNT
          PACK      KEY6,TMPLETNO,FORM3
          CALL      RDOTLET1
          MOVE      OTLTPPAG,PHYSPAGE
          MOVE      OTLTPBOT,BOTMARG
          MOVE      OTLTPTOP,TOPMARG
          SUB       OTLTPBOT,OTLTPPAG
          MOVE      OTLTPPAG,LSTPRINT
.
PRT0050   ADD       ONE,COUNT
          COMPARE   COUNT,TOPMARG
          GOTO      PRT1000 IF LESS
          PRINT     *N;
          GOTO      PRT0050
.
PRT1000   CALL      RKOTLET1
          BRANCH    OVRCD,PRT9000
          COMPARE   OTLTLTNO,TMPLETNO
          GOTO      PRT9000 IF NOT EQUAL
.
          BRANCH    OTLTLVAR,PRT2000
          PRINT     *N,*LEFTMARG,OTLTTEXT;
          GOTO      PRT3000
.
PRT2000   MOVE      OTLTTEXT,DIM70
          CALL      MOD0000
          PRINT     *N,*LEFTMARG,PRTSTRNG;
.
PRT3000   ADD       ONE,COUNT
          COMPARE   COUNT,LSTPRINT
          CALL      PGE0000 IF LESS
          GOTO      PRT1000
.
PRT9000   PRINT     *N;
          COMPARE   PHYSPAGE,COUNT
          GOTO      PRT9999 IF EQUAL
          GOTO      PRT9999 IF NOT LESS
          ADD       ONE,COUNT
          GOTO      PRT9000
.
PRT9999   DISPLAY   *P1:24,*EL;
.
          RETURN
+
.**********************************************************************
.*                              PGE0000                              *
.*        Routine to paginate if a new page is required               *
.**********************************************************************
PGE0000   MOVE      OTLTLTNO,FORM3
          MOVE      OTLTLINO,FORM3A
          CALL      RKOTLET1
          BRANCH    OVRCD,PGE8000
          COMPARE   TMPLETNO,OTLTLTNO
          GOTO      PGE8000 IF NOT EQUAL
          MOVE      TOPMARG,TEMP2
          ADD       BOTMARG,TOPMARG
          MOVE      ONE,COUNT
.
PGE1000   PRINT     *N;
          COMPARE   COUNT,TOPMARG
          GOTO      PGE9000 IF EQUAL
          ADD       ONE,COUNT
          GOTO      PGE1000
.
PGE8000   PACK      KEY6,FORM3,FORM3A  
          CALL      RDOTLET1
          GOTO      PGE9999
.
PGE9000   MOVE      TEMP2,COUNT
          ADD       ONE,COUNT
          MOVE      TEMP2,TOPMARG
          PACK      KEY6,FORM3,FORM3A
          CALL      RDOTLET1
.
PGE9999   RETURN
+
.*********************************************************************
.         load the variables for printing
.*********************************************************************
LDAT0000  MOVE      XXXXXX,DISPSTRN
          LOAD      DISPSTRN,SRCHNUM,LXPAGE,LXADDA,LXADDB,LXADDC,LXADDD:
                                LXAGE,LXATTCDE,LXATDTE,LXATTIM,LXBDATE:
                                LXBOKDTE,LXDOKTIM,LXBRTCNT,LXWTELE,LXCLNDSC:
                                LXCLNID,LXCLNTYP,LXCMPELG,LXDATE,LXDGNAM:
                                LXDSNAM,LXDTITL,LXDOCTOR,LXFNAME,LXGNAME:
                                LXINSTAT,LXISUM,LXLOCDOC,LXMEDNO,LXOUTNUM:
                                LXPATCAT,LXPATCLS,LXHTELE,LXPRTY,LXPCODE:
                                LXSEX,LXSITE,LXSLOT,LXSNAME,LXSRCREF:
                                LXSTIME,LXTITLE,LXURNO,LXNKNAME,LXNKADD1:
                                LXNKADD2,LXNKADD3,LXNKADD4,LXNKPOST,LXRGGPSN:
                                LXRGGPGN,LXRGGPA1,LXRGGPA2,LXRGGPA3,LXRGGPA4:
                                LXRGGPPC,LXRFGPSN,LXRFGPGN,LXRFGPA1,LXRFGPA2:
                                LXRFGPA3,LXRFGPA4,LXRFGPPC,LXHSPNAM,LXHSPAD1:
                                LXHSPAD2,LXHSPAD3,LXHSPAD4,LXHSPOST,LXHSPHON:
                                LXRESFRM,LXRESTO,LXCNLDTE,LXRSHTIM,LXRGPRNM:
                                LXRGPRA1,LXRGPRA2,LXRGPRA3,LXRGPRA4,LXRGPRPC:
                                LXRGPRPH,LXRFPRNM,LXRFPRA1,LXRFPRA2,LXRFPRA3:
                                LXRFPRA4,LXRFPRPC,LXRFPRPH,LXDAY,LXCLDATE:
                                LXMONTH,LXYEAR,LXCLLTYP,LXCLCLOC,LXECNAM:
                                LXECADA,LXECADB,LXECADC,LXECADD,LXECPCD:
                                LXNRNAM,LXNRADA,LXNRADB,LXNRADC,LXNRADD:
                                LXNRPCD,LXNATID,LXCELL,LXPADDA,LXPADDB:
                                LXPADDC,LXPADDD,LXPPOST,LXXSDCCD,LXXSDCPN:
                                LXXSRFDT
LDAT9999  RETURN
+
+
.**********************************************************************
.*          I/O INCLUDES FOR THE EXTERNAL ROUTINES                    *
.**********************************************************************
.
          INC       SMSMESID
          INC       OUTLETKY
          INC       OUTLETIO/INC
          INC       OUTRF1IO/INC
          INC       PMSRELIO/INC
          INC       VISCMTIO/INC
          INC       STD001IO
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
          INC       WEBSECIO/INC
.

.*****************************************************************************
.*    Program      : IBAWAT49                                                *
.*    Description  : Waiting List Letter History Maintenance                 *
.*                                                                           *
.*    Author       : ROD   22/12/92                                          *
.*    Function     : Maintain Letter History file                            *
.*                                                                           *
.*    Modifications:                                                         *
.*                                                                           *
.*****************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                        *
.*           Recompiled as WATTR1FD changes                                  *
.*****************************************************************************
.*       V11.00.02  20/03/2020  Peter Vela       TSK 0889143                 *
.*                  Fixed WATLET IO calls                                    *
.*       V11.00.01  19/03/2020  Peter Vela       TSK 0889143                 *
.*                  Recompiled for WATLETFD                                  *
.*       V10.05.01  01/08/2015  Ania P           CAR 261630                  *
.*                  Removed use of PORT for temp file naming                 *
.*       V10.04.01 31/12/2013  Don Nguyen        CAR 294389                  *
.*                 Recompiled for changes to WATLETFD & WATLETIO             *
.*       V10.02.01 07/07/2011  Steve Armstrong   CAR 240722                  *
.*                 Added RDPMPX21 prior to calling PMIHEAD                   *
.*       V9.02.01  13/02/2003  Lina Vo      SRF 22709                        *
.*                 Removed open of outpreaf - not used.                      *
.*       V5.08.01  21/08/2000  Caleb Sharman                                 *
.*                 Changed Health Fund variables to be 8 chars               *
.*       V5.08.B01 30/12/1999  Steve Armstrong                               *
.*                 Fixed global recompile bugs                               *
.*       V5.04.01  15/04/1997  howellsy   RHA                                *
.*                 Recompiled for WATDSPRO                                   *
.*       V5.01.02  19/01/93  ROD                                             *
.*                 Fix redisplay after "?" on U/R                            *
.*                 Open correct wattr1af/wattr1af                            *
.*       V5.01.03  16/03/93  ROD                                             *
.*                 Added security check on W/L procedure                     *
.*       V5.01.04  26/04/93  Whirlpool  (I'm Back)                           *
.*                 Modified letter selection                                 *
.*       V5.01.05  27/04/93  Whirlpool  (I'm Back)                           *
.*                 Modified to display letters in date order etc.            *
.*       V5.01.06  10/05/93  Steve O'Gorman                                  *
.*                 Fixed Security Access Check                               *
.*       V5.01.07  26/08/93  ROD                                             *
.*                 Recompiled for NZHIS                                      *
.*       V5.01.08  13/01/1994  Steve Armstrong                               *
.*                 Fixed bugs for global recompile                           *
.*       V5.01.09  26/05/1994 Ian Rutt                                       *
.*                 Fixed Global Recompile Bugs                               * 
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       IBAPASFD/INC
          INC       PATALRFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WATLHIFD/INC
          INC       WATLETFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       BOKRC1FD/INC            * booking file
          INC       OUTPREFD/INC            * OP pre-attendance file
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATMA1FD/INC            * patient master file
          INC       PATMI1FD/INC            * admission file
          INC       PATNIDFD/INC
          INC       PATPR1FD/INC            * pre-admission file
          INC       PATGSRFD/INC            * surname file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       WATCONFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
ACTNDESC  DIM       20
CJDAY     FORM      3
BJDAY     FORM      3
CODE      DIM       2
CMDLINE   DIM       50
COL       FORM      3
COUNTER   FORM      2
DIM15     DIM       15
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
ITEMSELC  FORM      2
LETT      DIM       30[15]
RPLYDESC  DIM       20
SAVCOUNT  FORM      2
SCNDGNAM  DIM       40
SCREEN    FORM      2
SCRNITEM  FORM      2
SITEM     DIM       2
STATUS    DIM       18
STATUS1   INIT      "Unscheduled      "
STATUS2   INIT      "Scheduled        "
STATUS3   INIT      "Pre-Admitted     "
STATUS4   INIT      "Admitted         "
STATUS5   INIT      "Discharged       "
STATUS6   INIT      "Removed from list"
STATUS7   INIT      "Performed        "
STATUS8   INIT      "Not Removed      "

TODAY     DIM       8
VERT      FORM      3
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
LETRFIL1  IFILE     SQL, FIXED=29, KEY="u1-22"
.
DATE      DIM       8
DISPACT   FORM      1
PROCCODE  DIM       9
DCOUNT    DIM       2
DLETRNUM  DIM       3
REPLY     DIM       3
ACTION    DIM       3
NMPNUMB   DIM       20
PCOUNT    FORM      2
LETRNUMB  FORM      3
.
PRGID     INIT      "IBAWAT49"
PRGNAM    INIT      "Letter History Maintenance"
VERSION   INIT      "V12.01.00"
+
.*************************************************************************
.*  ML0000  :  Mainline code                                             *
.*************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      PROC0000
          BRANCH    EXIT,ML9999
.
ML3000    CALL      ELHI0000                * Extract lhi details
          CALL      HPLT0000                      * Handle letters for patient
          GOTO      ML1000          
.
ML9999    CHAIN     PGM
+
.*************************************************************************
.*  INIT0000  :  Initialisation                                          *
.*************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
.
          DISPLAY   *P58:24,"watletrf"
          OPEN      WATLETR1,"watletrf"
          DISPLAY   *P58:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA3,"watproaf"
          DISPLAY   *P58:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"
          DISPLAY   *P58:24,"watlhiaf"
          OPEN      WATLHIA1,"watlhiaf"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"          * files to open
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          READ      CONTROLF,THIRTY7;*185,WTCNTPDS,*211,WTCNWLSC
.
          READ      CONTROLF,FIFTY9;*231,PTCNRGNS
          OPEN      PATGSRN1,"patgsrnf"     * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          IF        WTCNWLSC=1
            DISPLAY   *P64:24,"watseaaf";
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P64:24,"watseiaf";
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.*************************************************************************
.*  PROC0000  :  Keyin Key fields                                        *
.*************************************************************************
PROC0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*P5:4,"U/R Number       : "
.
PROC1000  CALL      WURN0000                      * keyin U/R Number
          BRANCH    EXIT,PROC9999                 * nothing input
.
          PACK      KEY30,WTLHURNO,SP30                                
          CALL      RSWTLHI1
          CALL      RKWTLHI1
          IF        OVRCD=1
              DISPLAY   *P1:24,*EL,*B,"No Letters exist for this Patient. ";
              CALL      HITENTER
              GOTO      PROC1000
          ELSE
            MATCH     PURNO,WTLHURNO
            IF        !@EQUAL
              DISPLAY   *P1:24,*EL,*B,"No Letters exist for this Patient. ";
              CALL      HITENTER
              GOTO      PROC1000
            ENDIF
          ENDIF
.
          CALL      PMIHEAD
.
PROC9999  RETURN
+
.*************************************************************************
.*  SCRN0000  :  Display screen                                          *
.*************************************************************************
SCRN0000  DISPLAY   *P1:7,*EF:
                    *P1:7,*V2LON," 1",*HOFF,". Patient Reply    : ":
                    *P1:8,*V2LON," 2",*HOFF,". Action Taken     : "
.
. if in change mode display fields
.
            MATCH     SP3,WTLHRPLY
            GOTO      SCRN1000 IF EQUAL

            MOVE      "<<Missing>>",TDESC
            PACK      KEY5,ANSW,ANSB,WTLHRPLY
            CALL      RDCODE1
            MOVE      TDESC,RPLYDESC
.
            DISPLAY   *P24:7,*EL,*V2LON,WTLHRPLY,*HOFF,SP3,RPLYDESC
.
SCRN1000    MATCH     SP3,WTLHACTN
            GOTO      SCRN9999 IF EQUAL
.
            MOVE      "<<Missing>>",TDESC
            PACK      KEY5,ANSW,ANSC,WTLHACTN
            CALL      RDCODE1
            MOVE      TDESC,ACTNDESC
.
            DISPLAY   *P24:8,*EL,*V2LON,WTLHACTN,*HOFF,SP3,ACTNDESC
.
SCRN9999  RETURN
+
.*************************************************************************
.*  WCHG0000  :  Keyin Change mode                                       *
.*************************************************************************
WCHG0000  CALL      MAINQST
.
          COMPARE   ZERO,CCITEM
          GOTO      WCHG8000 IF EQUAL             * Post
          GOTO      WCHG9000 IF LESS              * cancel
.
          BRANCH    CCITEM,WCHG1000,WCHG2000
          BEEP
          GOTO      WCHG0000
.
WCHG1000  CALL      RPLY0000                      * keyin reply
          BRANCH    EXIT,WCHG9000                 * EXITCHAR
          GOTO      WCHG0000
.
WCHG2000  CALL      ACTN0000                      * keyin action
          BRANCH    EXIT,WCHG9000                 * EXITCHAR
          GOTO      WCHG0000
.
WCHG8000  MOVE      ZERO,EXIT                     * Post
          GOTO      WCHG9999
.
WCHG9000  MOVE      ONE,EXIT                      * Cancel / EXITCHAR
.
WCHG9999  RETURN
+
.*************************************************************************
.*  WADD0000  :  Keyin Add mode                                          *
.*************************************************************************
WADD0000  CALL      RPLY0000                      * keyin reply
          BRANCH    EXIT,WADD9999                 * EXITCHAR
.
          CALL      ACTN0000                      * keyin action
          BRANCH    EXIT,WADD9999                 * EXITCHAR
.
WADD9999  RETURN
+
.*************************************************************************
.*  RPLY0000  :  Keyin Reply                                             *
.*************************************************************************
RPLY0000  MOVE      "WB",CODE
          MOVE      WTLHRPLY,CKYIDEF3             * no default
          MOVE      ZERO,CKYIMAND                 * not mandatory
          MOVE      "24",ECOL
          MOVE      "7",EVERT
.
          CALL      PATCODKY
          IF        EXIT=2
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      RPLY9999
          ENDIF
.
          MOVE      ACODE,WTLHRPLY
          MOVE      ACODE,REPLY
          DISPLAY   *P30:7,TDESC
          MOVE      ZERO,EXIT
.
RPLY9999  RETURN
+
.*************************************************************************
.*  ACTN0000  :  Keyin Action                                            *
.*************************************************************************
ACTN0000  MOVE      "WC",CODE
          MOVE      WTLHACTN,CKYIDEF3             * no default
          MOVE      ZERO,CKYIMAND                 * not mandatory
          MOVE      "24",ECOL
          MOVE      "8",EVERT
.
          CALL      PATCODKY
          IF        EXIT=2
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      ACTN9999
          ENDIF
.
          MOVE      ACODE,WTLHACTN
          MOVE      ACODE,ACTION
          DISPLAY   *P30:8,TDESC
          MOVE      ZERO,EXIT
.
ACTN9999  RETURN
+
.**************************************************************************
.*  POST0000  :  Post (Insert and Change)                                 *
.**************************************************************************
POST0000  DISPLAY   *P1:24,*EL,*P35:24,"Posting...";
.
. change mode
.
POST2000  PACK      KEY19,WTLHURNO,WTLHCODE,WTLHCONT,WTLHDATE,WTLHLNUM
          CALL      RDWTLHI1
          BRANCH    OVRCD,POST9999
.
          MOVE      REPLY,WTLHRPLY
          MOVE      ACTION,WTLHACTN
          CALL      UPWTLHI1
          CALL      UPLETR1
          GOTO      POST9999
.
POST9999  RETURN
+
.****************************************************************************
.*  WURN0000  :  Keyin U/R Number                                           *
.****************************************************************************
WURN0000  DISPLAY   *P5:4,"U/R Number       : "
          MOVE      "24",CCOL
          MOVE      "4",CVERT
.
          CALL      KURN
          BRANCH    EXIT,WURN9999
          BRANCH    OVRCD,WURN9000
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,WURN9000
.
          MOVE      PURNO,WTLHURNO
          GOTO      WURN9999
.
WURN9000  DISPLAY   *P1:24,*EL,*B,"Invalid U/R Number.  ";
          CALL      HITENTER
          GOTO      WURN0000
.
WURN9999  RETURN
+
+
.
.*************************************************************************** 
.*                                HEAD0000            Called by: ML0000    *
.*                    Display screen coloum  headings                      *
.***************************************************************************
.
HEAD0000
          DISPLAY   *P1:7,*EF,*V2LON,*ULON:
                    *P5:7,"Let",*P9:7,"Description   ":
                    *P25:7,"Date Sent ",*P36:7,"Procedure   ":
                    *P49:7,"W/L Status        ",*P68:7:
                    "Reply",*P75:7,"Action"                       
HEAD9999  RETURN
.
.*************************************************************************** 
.*                                HPLT0000            Called by: ML0000    *
.*    328 LINES       keyin Patient from the booking list                  *
.***************************************************************************
HPLT0000  CALL      HEAD0000
.
          MOVE      ZERO,EXIT
          MOVE      EIGHT,CVERT                   * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,SCREEN                    * init screen no.
          MOVE      ONE,SCRNITEM                  * init stock no.
.
          PACK      KEY22,ZEDS                 
          CALL      RSLETR1
HPLT1000  CALL      RPLETR1                      * read next record
          BRANCH    OVRCD,HPLT5000               * no more records
 
          PACK      KEY22,DATE,PROCCODE,PCOUNT,LETRNUMB                 
.
. check if enough room for display
.
HPLT1500  COMPARE   TEN6,SCRNITEM
          GOTO      HPLT3000 IF LESS
.
. we need a new screen
.
          CALL      ST1K0000                      * ask question with Next scrn
          BRANCH    EXIT,HPLT2000:                * Previous screen selected
                         HPLT2500:                * Next screen selected
                         HPLT9000                 * Exit was selected
          GOTO      HPLT8000                      * Number Entered
.
. previous screen was selected
.
HPLT2000  
.
          PACK      KEY22,LETT[1]
          CALL      RSLETR1                       * pos on 1st key on screen
.
. read back x records until 1st key on previous screen is reached
.
          MOVE      ZERO,COUNTER                  * init counter
HPLT2200  CALL      RKLETR1                       * read back 1 record
.
          ADD       ONE,COUNTER                   * increment counter
          COMPARE   TEN5,COUNTER                  * check if read far enough
          GOTO      HPLT2200 IF NOT EQUAL
.
. we have read back far enough
.
          DISPLAY   *P1:8,*EF                     * erase screen
          MOVE      EIGHT,CVERT                    * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,SCRNITEM                  * init screen disp counter
          SUB       ONE,SCREEN                    * update screen no.
          GOTO      HPLT3000
.
. Next screen was selected
.
HPLT2500  ADD       ONE,SCREEN                    * increment screen no.
.
. save the current key
.
          DISPLAY   *P1:8,*EF                   * erase screen
.
          MOVE      EIGHT,CVERT                   * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,SCRNITEM                  * init screen disp counter
.
. display record
.
HPLT3000  
          CALL      GLDT0000                   * get letter details
.
          DISPLAY   *PCCOL:CVERT,*V2LON,SCRNITEM,". ",*HOFF,*P5:CVERT,LETRNUMB:
                    *P9:CVERT,DIM15,*P25:CVERT,CPCDATE:
                    *P36:CVERT,PROCCODE,SLASH,PCOUNT:
                    *P49:CVERT,STATUS,*P69:CVERT,REPLY,*P76:CVERT,ACTION

          PACK      LETT[SCRNITEM],DATE,PROCCODE,PCOUNT,LETRNUMB 
.
.    save each record as it is displayed (for selection purposes)
.
          ADD       ONE,CVERT                     * update row pos
          ADD       ONE,SCRNITEM                  * increment counter
          GOTO      HPLT1000                      * get next record
.
. we have no more records to display
.
HPLT5000  IF       SCREEN = 1
            IF       SCRNITEM = 1
              DISPLAY  *P1:24,*B,*EL,"No Records found. ";
              CALL     HITENTER
              GOTO     HPLT9000
            ENDIF
          ENDIF
          CALL     ST2K0000
          BRANCH   EXIT,HPLT2000:                 * Previous screen selected
                        HPLT9000                  * Exit selected
.
. a valid class. was entered (so get record selected)
.
HPLT8000  
          PACK      KEY22,LETT[ITEMSELC]
          CALL      RDLETR1
          BRANCH    OVRCD,HPLT9000
.
          PACK      KEY30,PURNO,PROCCODE,PCOUNT,DATE,LETRNUMB
          CALL      RDWTLHI1
          BRANCH    OVRCD,HPLT9000
.
          MOVE      ZERO,EXIT
.
          CALL      SCRN0000                      * display screen
          CALL      WADD0000                      * keyin add mode
          BRANCH    EXIT,HPLT8200                 * EXITCHAR
.
          CALL      WCHG0000                      * keyin change mode
          BRANCH    EXIT,HPLT8200                 * Cancel
.
          CALL      POST0000                      * Post
.
HPLT8200  PACK      KEY22,LETT[1]
          CALL      RSLETR1                      * pos on 1st key on screen
          CALL      RKLETR1 
          MOVE      EIGHT,CVERT                   * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,SCRNITEM                  * init stock no.
          CALL      HEAD0000
          GOTO      HPLT1000
.
HPLT9000  MOVE      ONE,EXIT                      * Exit was selected
.
HPLT9999  RETURN
+
.**************************************************************************
.*                                STIK0000            Called by: HPLT0000 *
.*                 ask line 24 question with (N)ext                       *
.**************************************************************************
ST1K0000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,ST1K0500
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL,"Select Item, (",*V2LON:
                    "N",*HOFF,")ext, (",*V2LON,"P",*HOFF,")revious, (":
                    *V2LON,"E",*HOFF,")xit ?"
.
          MOVE      "43",CCOL                 * keyin position
          GOTO      ST1K1000                  * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
ST1K0500  DISPLAY   *P1:24,*EL,"Select Item, (",*V2LON:
                    "N",*HOFF,")ext, (":
                    *V2LON,"E",*HOFF,")xit ?"
.
          MOVE      "31",CCOL                   * keyin position
.
. now get keyin
.
ST1K1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,SITEM:
                    *PCCOL:24,*DV,SITEM
.
          ENDSET    SITEM
          APPEND    SP2,SITEM
          RESET     SITEM
.
          REP       UPPLOW,SITEM                  * Change to lower case
.
          TYPE      SITEM                         * was a numeric entered
          GOTO      ST1K1200 IF EQUAL
.
. a character was entered
.
          REP       "P1N2E3",SITEM
.
          TYPE      SITEM                         * valid letter entered?
          GOTO      ST1K1500 IF NOT EQUAL         * No.
.
          MOVE      SITEM,ITEMSELC
          BRANCH    ITEMSELC,ST1K7000,ST1K2000,ST1K4000
.
          BEEP 
          GOTO      ST1K1000
.
. a numeric was entered so validate
.
ST1K1200  MOVE      SITEM,ITEMSELC                * move to form field
          COMPARE   ONE,ITEMSELC                  * < 1 ?
          GOTO      ST1K1500 IF LESS
          COMPARE   TEN6,ITEMSELC                 * in range ?
          GOTO      ST1K6500 IF LESS 
.
. error in input
.
ST1K1500  BEEP  
          GOTO      ST1K1000
.
ST1K2000  MOVE      TWO,EXIT                      * Next was selected
          GOTO      ST1K9999
.
ST1K4000  MOVE      THREE,EXIT                    * Exit was selected
          GOTO      ST1K9999
.
ST1K6500  MOVE      FOUR,EXIT                   * Number Selected
          GOTO      ST1K9999
           
.
.
. Previous was selected so check if it was asked
.
ST1K7000  BRANCH    SCREEN,ST1K7200
          MOVE      ONE,EXIT                    * Previous was selected
          GOTO      ST1K9999
.
. Previous was selected but wasn't asked so error
.
ST1K7200  BEEP
          GOTO      ST1K1000
.
ST1K9999  RETURN
+
.**************************************************************************
.*                                ST2K0000            Called by: HPLT0000 *
.*                 ask line 24 question without (N)ext                    *
.**************************************************************************
ST2K0000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,ST2K0500
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL,"Select Item, (",*V2LON:
                    "P",*HOFF,")revious, (",*V2LON:
                    "E",*HOFF,")xit ?"
.
          MOVE      "35",CCOL                   * keyin position
          GOTO      ST2K1000                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
ST2K0500  DISPLAY   *P1:24,*EL,"Select Item, (",*V2LON:
                    "E",*HOFF,")xit ?"
.
          MOVE      "23",CCOL                   * keyin position
.
. now get keyin
.
ST2K1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,SITEM:
                    *PCCOL:24,*DV,SITEM
.
          ENDSET    SITEM
          APPEND    SP2,SITEM
          RESET     SITEM
.
          REP       UPPLOW,SITEM              
.
          TYPE      SITEM                         * was a numeric entered
          GOTO      ST2K1200 IF EQUAL
.
. a character was entered
.
          REP       "P1E2",SITEM
.
          TYPE      SITEM                         * valid letter entered?
          GOTO      ST2K1500 IF NOT EQUAL         * No.
.
          MOVE      SITEM,ITEMSELC
          BRANCH    ITEMSELC,ST2K7000,ST2K5000
. 
          BEEP
          GOTO      ST2K1000
.
. a numeric was entered so validate
.
ST2K1200  MOVE      SITEM,ITEMSELC                * move to form field
          COMPARE   ONE,ITEMSELC                  * < 1 ?
          GOTO      ST2K1500 IF LESS
          COMPARE   SCRNITEM,ITEMSELC             * in range ?
          GOTO      ST2K6500 IF LESS 
.
. error in input
.
ST2K1500  BEEP  
          GOTO      ST2K1000
.
ST2K5000  MOVE      TWO,EXIT                     * Exit was selected
          GOTO      ST2K9999
.
ST2K6500  MOVE      THREE,EXIT                   * Number Selected
          GOTO      ST2K9999
.
. Previous was selected so check if it was asked
.
ST2K7000  BRANCH    SCREEN,ST2K7200
          MOVE      ONE,EXIT                    * Previous was selected
          GOTO      ST2K9999
.
. Previous was selected but wasn't asked so error
.
ST2K7200  BEEP
          GOTO      ST2K1000
.
ST2K9999  RETURN
.
.****************************************************************************
.*                                GLDT0000                                  *
.*                     Get Letter    details for display                    *
.****************************************************************************
.
GLDT0000  
.
          UNPACK    DATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PACK      KEY6,LETRNUMB,SP1,SP1,ZERO
          CALL      RDLET1
          BRANCH    OVRCD,GLDT1000
.
          MOVE      WLTEXT,DIM15
.
.
GLDT1000  PACK      KEY19,PURNO,PROCCODE,PCOUNT
          CALL      RDTREA1
          BRANCH    OVRCD,GLDT9999
.
          LOAD      STATUS,WMSTAT,STATUS1,STATUS2,STATUS3,STATUS4,STATUS5:
                    STATUS6,STATUS7,STATUS8
.
GLDT9999  RETURN
.
.
.*****************************************************************************
.*  CWLS0000  :  Check Waiting List security level                           *
.*****************************************************************************
CWLS0000  COMPARE   ONE,WTCNWLSC                  * using security?
          GOTO      CWLS8000 IF NOT EQUAL
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          COMPARE   ZERO,OVRCD                    * ALL Access
          GOTO      CWLS8000 IF EQUAL
          PACK      KEY13,PASSCODE,WMDOCTOR,WMUNIT
          CALL      RDWTSEI2
          BRANCH    OVRCD,CWLS9000                * no access
.
CWLS8000  MOVE      ZERO,EXIT                     * user has access
          GOTO      CWLS9999
.
CWLS9000  MOVE      ONE,EXIT                      * user doesn't have access
.
CWLS9999  RETURN
.
. -----------------------------------------------------------------------
.
REDISPS   DISPLAY   *P1:4,*EF:
                    *P1:4,*P5:4,"U/R Number       : ":
                    *P5:5,"Procedure Code   : ":
                    *P5:6,"Procedure Count  : ":
                    *P5:7,"Date Letter sent : ":
                    *P5:8,"Letter Number    : "
GETSVAR   RETURN
+
.
.*************************************************************************** 
.*                                ELHI0000                                 *
.*    328 LINES       Extract letters from the history file                *
.***************************************************************************
ELHI0000  CALL      CTBF0000                * create the file
          PACK      KEY30,PURNO,SP30   
          CALL      RSWTLHI1
ELHI1000  CALL      RKWTLHI1                      * read next record
          BRANCH    OVRCD,ELHI9999                * no more records
.
          MATCH     PURNO,WTLHURNO 
          GOTO      ELHI9999 IF NOT EQUAL
.
          MOVE      WTLHDATE,DATE
          MOVE      WTLHCODE,PROCCODE
          MOVE      WTLHCONT,PCOUNT
          MOVE      WTLHLNUM,LETRNUMB
          MOVE      WTLHRPLY,REPLY
          MOVE      WTLHACTN,ACTION
.
          PACK      KEY19,WTLHURNO,WTLHCODE,WTLHCONT
          CALL      RDTREA1
          BRANCH    OVRCD,ELHI1000
.
          CALL      CWLS0000                     * Check Security
          BRANCH    EXIT,ELHI1000                * Invalid Security Access
.
          PACK      KEY22,DATE,PROCCODE,PCOUNT,LETRNUMB
          CALL      WRLETR1
          GOTO      ELHI1000
.
ELHI9999  RETURN
.
.*****************************************************************************
.*                           CTBF0000                                        *
.*                        Create the 1st Tempfile                            *
.*****************************************************************************
.
CTBF0000  CALL      KTBF0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 29 U1-22",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      LETRFIL1,TFILNAME
CTBF9999  RETURN
.
.*****************************************************************************
.*                           KTBF0000                                        *
.*                        Kill the 1st Tempfile                              *
.*****************************************************************************
.
KTBF0000  CLOSE     LETRFIL1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          
KTBF9999  RETURN
.
.==========================
.
RDLETR1   RESET     KEY22
          MOVE      ZERO,OVRCD
          READ      LETRFIL1,KEY22;DATE,PROCCODE,DCOUNT,DLETRNUM,REPLY,ACTION
          GOTO      OVERCOND IF OVER
          MOVE      DCOUNT,PCOUNT
          MOVE      DLETRNUM,LETRNUMB
          RETURN
.
RSLETR1   RESET     KEY22
          READ      LETRFIL1,KEY22;;         
          RETURN
.
RKLETR1   MOVE      ZERO,OVRCD
          READKS    LETRFIL1;DATE,PROCCODE,DCOUNT,DLETRNUM,REPLY,ACTION
          GOTO      OVERCOND IF OVER
          MOVE      DCOUNT,PCOUNT
          MOVE      DLETRNUM,LETRNUMB
          RETURN
.
RPLETR1   MOVE      ZERO,OVRCD
          READKP    LETRFIL1;DATE,PROCCODE,DCOUNT,DLETRNUM,REPLY,ACTION
          GOTO      OVERCOND IF OVER
          MOVE      DCOUNT,PCOUNT
          MOVE      DLETRNUM,LETRNUMB
          RETURN
.
WRLETR1   RESET     KEY22
          MOVE      PCOUNT,DCOUNT
          MOVE      LETRNUMB,DLETRNUM
          WRITE     LETRFIL1,KEY22;DATE,PROCCODE,DCOUNT,DLETRNUM,REPLY,ACTION
          RETURN
.
UPLETR1  
          MOVE      PCOUNT,DCOUNT
          MOVE      LETRNUMB,DLETRNUM
          UPDATE    LETRFIL1;DATE,PROCCODE,DCOUNT,DLETRNUM,REPLY,ACTION
          RETURN
.
.         I/O includes needed
.
          INC       STD001IO/PBL
          INC       CALCAGE
          INC       PMIHEAD
          INC       PATALERT
          INC       BOKRC1IO/INC            * booking file
          INC       OUTPREIO/INC            * OP pre-attendance file
          INC       PATCOMN1/PBL            * holds KURN
          INC       PATMA1IO/INC            * patient master file
          INC       PATMI1IO/INC            * admission file
          INC       PATNIDIO/INC
          INC       PATPR1IO/INC            * pre-admission file
          INC       PATGSRIO/INC            * surname file
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATSNDX/PBL             * ? for surnames only
          INC       PATSNX2/PBL             * ? for surnames/given names
          INC       PATCODKY
          INC       WATDSPRO
          INC       PATCODIO/INC
          INC       WATLHIIO/INC
          INC       WATTR1IO/INC
          INC       WATPROIO/INC
          INC       WATLETIO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       PATALRIO/INC
          INC       NZCOMPIO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
.
...............................................................................
.         Required Labels, code will not compile if deleted                   .
...............................................................................

AGECHK
CLPATMAS  RETURN


.***************************************************************************
.* System    :   Booking System                                            *
.* Program   :   FX290323                                                  *
.***************************************************************************
.* Date      :   06/09/2013                                                *
.* Author    :   Sandra Barcham                                            *
.* Mods      :                                                             *
.***************************************************************************
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       BOKRC1FD/INC
          INC       BOKDIAFD/INC
          INC       PATCODFD/INC
.
CATBK     INIT      "BK"
COUNTER   FORM      5
.
PRGID     INIT      "FX290323"
PRGNAM    INIT      "Fix Duplicates in bokdiaaf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select FROM and TO
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PROC0000               * process records for update
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"bokdiaaf";
          OPEN      BOKDIAA1,"bokdiaaf"          * booking extension file
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"          * booking extension file
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"          * booking extension file
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Update Duplicate Diagnosis":
                    *P1:6,*V2LON,TWO,*HOFF," = Print Only"
.
OPTN1000  KEYIN     *P1:8,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN2000,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,OPTN9000,OPTN1000,OPTN9500 * Yes, no, cancel
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                PROC0000            Called by: ML0000     *
.*              read through bokdiaaf                                       *
.****************************************************************************
PROC0000  MOVE      ZERO,COUNTER
          DISPLAY   *P1:24,*EL,"Processing Diagnosis Number: ";
.
          MOVE      SP70,KEY18
PROC0500  CALL      RSBKDIA1                     * position at start of file
PROC1000  CALL      RKBKDIA1                     * read next record
          BRANCH    OVRCD,PROC9000               * end of file
.
          DISPLAY   *P30:24,*EL,*V2LON,BKDIBOOK,"/",BKDIUNIQ;
.
          PACK       KEY8,BKDIBOOK,SP70
          CALL       RDBKREC1
          BRANCH     OVRCD,PROC3000
.
          MATCH     "         1",BKDIUNIQ
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      KEY5,CATBK,BKRETYPE
          CALL      RDCODE1
          BRANCH    OVRCD,PROC1000
.
          MATCH     "THE",THCSCOD
          GOTO      PROC1000 IF NOT EQUAL
.
PROC3000  PRINT     BKDIBOOK," ",BKDIUNIQ
          ADD       ONE,COUNTER
.
          IF        COPTION=1
            PACK      KEY18,BKDIBOOK,BKDIUNIQ,SP70
            CALL      DEBKDIA1
            GOTO      PROC0500
          ENDIF
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:20,*EL,COUNTER," Records Deleted  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       BOKDIAIO/INC
          INC       BOKRC1IO/INC
          INC       PATCODIO/INC

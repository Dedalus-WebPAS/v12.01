.******************************************************************************
.* System         : Emergency                                                 *
.* Program        : IBAEMR55.PBL                                              *
.* Name           : ACT HDP Emergency Department Extraction                   *
.******************************************************************************
.* Date           : 17/08/2004                                                *
.* Author         : Jill Habasque                                             *
.* Function       : Extract emergency patient details via a date range & load *
.*                  the details into a text file.                             *
.* Modifications  :                                                           *
.*        V10.13.01 05/12/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (EXTRACTF) on program exit              *
.*        V10.03.01 17/11/2011  Mike Laming     CAR 240184                    *
.*                  Changes to IBAPOSTF Post Code table - added State to Keys *
.*        V10.00.04 26/10/2010 Ebon Clements     CAR 230086                   *
.*                  Removed unpack into SP70 in MSTD0000                      *
.*        V10.00.03 21/09/2010 Ebon Clements     CAR 230086                   *
.*                  Check if first triage cat allocated via supervisor screen *
.*        V10.00.02 05/05/2010 Jill Parkinson    CAR 221348                   *
.*                  July 2010 changes                                         *
.*        V10.00.01 14/04/2010 Steve Armstrong   CAR 219933                   *
.*                  Recompiled for changes to EMRVCDFD                        *
.******************************************************************************
.*        V9.12.01  07/10/2009 Ian Farnell    CAR 175936                      *
.*                  mods to MOVE2500 to not send error report                 *
.*        V9.11.02  04/11/2008 Ian Farnell    CAR 175936                      *
.*                  Changed Don't print error for did not wait from Ready     *
.*                  for Departure date & time to Seen Date and Time           *
.*        V9.11.01  22/10/2008 Sandra Barcham CAR 175936                      *
.*                  Remove admission number in middle of error messages       *
.*                  Don't print error for did not wait                        *
.*        V9.10.02  18/08/2008 Sandra Barcham CAR 175936                      *
.*                  Fix blank error messages                                  *
.*        V9.10.01  14/04/2008  Jill Habasque CAR 166189                      *
.*                  July 2008 mods                                            *
.*        V9.09.02  12/12/2007  Jill Habasque CAR 140056                      *
.*                  Added EMR prefix to filename                              *
.*        V9.09.01  13/09/2007  Ebon Clements CAR 150129                      *
.*                  Fixed ready for depature date test in VPTD0000            *
.*        V9.08.02  01/05/2007  Ebon Clements CAR 139411                      *
.*                  2007 Mods                                                 *
.*        V9.08.01  31/01/2007  Peter Vela    CAR 127930                      *
.*                  Recompiled for EMRSITFD                                   *
.*        V9.06.02  19/06/2006  Jill Habasque CAR 108891                      *
.*                  2006 Mods                                                 *
.*        V9.06.01  01/08/2005  Jill Habasque CAR 55845                       *
.*                  Changed to use index 6 on emrvisaf                        *
.*        V9.04.08  18/07/2005  Jill Habasque CAR 55845                       *
.*                  Commented out all rep spaces with zero for dates & times  *
.*        V9.04.07  11/04/2005  Jill Habasque CAR 55845                       *
.*                  Added clear of form2 for DVA                              *
.*        V9.04.06  11/03/2005  Guomin Fu     CAR 55845                       *
.*                  Changes for SSLA, DRTIME,TRTIME and SNTIME                *
.*        V9.04.05  01/03/2005  Peter Vela    CAR 57547                       *
.*                  Recompiled for PATVADFD and PATVADIO                      *
.*        V9.04.04  21/12/2004  Jill Habasque CAR 55845                       *
.*                  Added locality and error messages                         *
.*        V9.04.03  07/12/2004  Jill Habasque CAR 55627                       *
.*                  Added site code entry (to determine hospid)               *
.*                  Changed DVA to default to 2, changed SRCREF to come from  * .*                  Category em instead of S                                  *
.*        V9.04.02  30/09/2004  Jill Habasque CAR 53678                       *
.*                  Added fields for Ready for Departure and Departure Dest   *
.*        V9.04.01  17/08/2004  Jill Habasque                                 *
.*                  Created extract                                           *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       IBAPOSFD/INC            * Postcode file
          INC       PATHSPFD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       EMRCHAFD/INC
          INC       EMRSITFD/INC
          INC       EMRVISFD/INC
          INC       EMRICDFD/INC
          INC       EMRVCDFD/INC
          INC       PATCALAG/INC            * calcage   
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Patient control file
          INC       PATDRGFD/INC            * Period Date Range
          INC       PATDSCFD/INC            * Discharge file
          INC       PATMA1FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATVADFD/INC            * Transfer source file
          INC       PATCT1FD/INC
.
EXTRACTF  FILE      ASCII, FIXED=209
.
. Layout after 01/07/2007
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
HOSPID   DIM          2        1  Hospital ID
PMI      DIM          8        3  Patient ID number
PIN      DIM          8       11  Patient ID number
EVENTID  DIM          8       19  Event ID
STATID   DIM          1       27  State Identifier
POSTCODE DIM          4       28  Post Code
SEX      DIM          1       32  Sex
DOB      DIM          8       33  Date of birth
DOBEST   DIM          3       41  Estimated Date of birth
SACC     DIM          4       44  Country of Birth
INDIGEN  DIM          1       48  Indigenous status
MCAT     DIM          1       49  Marital Status
COMPENSE DIM          1       50  Compenstation status
INSURE   DIM          1       51  Insurance Status
MEDICARE DIM          1       52  Medicare Eligibility
DVA      DIM          1       53  Department of Veterans Affairs
SCEREF   DIM          2       54  Source of Referral
ARRMODE  DIM          2       56  Arrival Transport
LANGUAGE DIM          4       58  Preferred Language
VISTYPE  DIM          1       62  Visit Type
CLDATE   DIM          8       63  Date Registered by clerk
CLTIME   DIM          4       71  Time Registered by clerk
TRDATE   DIM          8       75  Date of Triage
TRTIME   DIM          4       83  Time of Triage
TRCAT    DIM          1       87  Triage Category
SNDATE   DIM          8       88  Date of Service Event
SNTIME   DIM          4       96  Time of Service Event
DEPSTAT  DIM          1      100  Departure Status
DEPDEST  DIM          1      101  Departure Destination
DEPREF   DIM          2      102  Referral on Departure
DRDATE   DIM          8      104  Date ready for departure
DRTIME   DIM          4      112  Time ready for departure
ADDATE   DIM          8      116  Date of actual departure
ADTIME   DIM          4      124  Time of actual departure
INJPOIS  DIM          1      128  Injury/Poisoning flag
PDX      DIM          6      129  Principal diagnosis
LOCALITY DIM          50     135  Locality
ADMEPIS  DIM          8      185  Admission number
ADMWARD  DIM          4      193
INPTDATE DIM          8      197  Inpatient Admission Date
INPTTIME DIM          4      205  Inpatient Admission Date
.
.End of Record                209
.
. ----- Program Variables -----
BJDAY     FORM      3
CDYSDAYS  FORM      6                       * For CALCDAYS
CDYSFDTE  DIM       8                       * For CALCDAYS
CDYSTDTE  DIM       8                       * For CALCDAYS
CDYSYEAR  FORM      2                       * For CALCDAYS
CJDAY     FORM      3
CMDLINE   DIM       127
CPTDATE   DIM       8
CURDTE    DIM       8
DATOLIST  DIM       10
DHOUR     DIM       2
DMIN      DIM       2
DSEC      DIM       2
DSPCNT    FORM      5
DTLURGIN  DIM       8
DIM4      DIM       4
DIM4A     DIM       4
ENDDTE    DIM       8
ERRMSG    DIM       98
FCMON     FORM      2
FCYEAR    FORM      2
FILENAME  DIM       8
IBCNMHOS  FORM      1
NEWLAYT   FORM      1
NRFCDAYS  FORM      3
PSAGECON  DIM       3
PSAGETYP  DIM       1
RECCNT    FORM      4
SAVINDC3  DIM       1
SITE      DIM       3
STRTDTE   DIM       8
TESTDTE   DIM       8
TOTALDAY  FORM      4
.
CATem     INIT      "em"
CATA8     INIT      "A8"
CATEC     INIT      "ec"
CATel     INIT      "el"
CATEE     INIT      "ee"
MTHNAM3   DIM       3
JAN       INIT      "Jan"
FEB       INIT      "Feb"
MAR       INIT      "Mar"
APR       INIT      "Apr"
MAY       INIT      "May"
JUN       INIT      "Jun"
JUL       INIT      "Jul"
AUG       INIT      "Aug"
SEP       INIT      "Sep"
OCT       INIT      "Oct"
NOV       INIT      "Nov"
DEC       INIT      "Dec"
.
PRGID     INIT      "IBAEMR55"
PRGNAM    INIT      "ACT HDP Emergency Extraction"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.****************************************************************************** 
.
ML0000    MOVE      ZERO,NEWLAYT
          CALL      INIT0000                * Initialise variables & open files 
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000
          BRANCH    EXIT,ML9999
.
ML2000    CALL      IDAT0000                * Keyin the end date
          BRANCH    EXIT,ML9999
.
          CALL      KSIT0000                * Keyin the site ID 
          BRANCH    EXIT,ML9999
.
          CALL      FILE0000                * Keyin the extract filename
          BRANCH    EXIT,ML9999
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML2000,ML1000
.
ML4000    DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
          CALL      PROC0000
.
          CALL      MVEXT000                * Move Extract for Web
          GOTO      ML1000
.
ML9999    CLOSE     EXTRACTF,DELETE
          CHAIN     PGM
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"emrchaaf";
          OPEN      EMRCHAA1,"emrchaaf"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA6,"emrvisaf"
.
          DISPLAY   *P64:24,"emrsitaf";
          OPEN      EMRSITA1,"emrsitaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"emricdaf";
          OPEN      EMRICDA1,"emricdaf"
.
          DISPLAY   *P64:24,"emrvcdaf";
          OPEN      EMRVCDA1,"emrvcdaf"
.
          DISPLAY   *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patct1cf";
          OPEN      PATCT1C1,"patct1cf"
.
          CLOCK     TIME,CTIMEIS
          PACK      CURDTE,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE
          READ      CONTROLF,TEN;*54,CFILENO
          READ      CONTROLF,NINTY8;*81,PTCNHCOD
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run":
                    *P1:8,"Select Option : ";
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  IDAT0000                                  *
.*        input ending date                              called by : MAINLINE *
.******************************************************************************
IDAT0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:11,*EF,"Ending date : ";
.
.         set up input requirements for KEYDATE
.
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      "15",CCOL
          MOVE      "11",CVERT
.
.         input ending date
.
IDAT1000  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,IDAT9500
.
          PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE
.
          MATCH    "20100701",ENDDTE
          IF       !@LESS
            MOVE     ONE,NEWLAYT
          ENDIF
.
          CALL     CSFY0000       * Get start of financial year
          GOTO     IDAT9999
.
IDAT9500  MOVE      ONE,EXIT
IDAT9999  RETURN
.
.******************************************************************************
.*                                  KSIT0000                                  *
.*        keyin the site code                            called by : MAINLINE *
.******************************************************************************
KSIT0000  MOVE      ZERO,EXIT
          KEYIN     *P1:13,*EF,"Site Code : ",SITE;
.
          PACK      KEY3,SITE
          CALL      RDEMSIT1
          BRANCH    OVRCD,KSIT9000
.
          GOTO      KSIT9999
.
KSIT9000  PRINT     *1,"Invalid Site Code"
          MOVE      ONE,EXIT
.
KSIT9999  RETURN
+
.*******************************************************************************
.*                              CSFY0000                  CALLED BY : ML1000   *
.*                                                                    ML2000   *
.* Calculate the Start of financial year from the end of current month/quarter *
.*******************************************************************************
CSFY0000  UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      CMON,FCMON
          COMPARE   FCMON,SIX                * Month > June
          GOTO      CSFY1000 IF LESS         * Yes
.
          MOVE      CYEAR,FCYEAR             * Mth 1-6:CYEAR is the previous yr
          SUB       ONE,FCYEAR
          MOVE      FCYEAR,CYEAR
.
CSFY1000  PACK      STRTDTE,CCENT,CYEAR,ZERO,SEVEN,ZERO,ONE * Start of fin. Yr
          REP       " 0",STRTDTE
.
CSFY9999  RETURN
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                         Keyin The Extract Filename                         *
.******************************************************************************
FILE0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
FILE1000  MOVE      "EMR",KEY3
          UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      CPTDATE,ENDDTE
          CALL      GPERD
          PACK      FILENAME,KEY3,MTHNAM3,CYEAR,SP70
          REPLACE   " 0",FILENAME
.
          DISPLAY   *P1:12,*EL,"Extract Filename":
                    *PCCOL:12,*EL,*V2LON,*+,FILENAME,".txt";
.
          PREP      EXTRACTF,FILENAME
.
FILE9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Run the extract                               *
.******************************************************************************
PROC0000  CALL      HEAD0000
          UNPACK    STRTDTE,CCENT,CYEAR
          PACK      TESTDTE,CCENT,CYEAR,ZERO,SIX,ZERO,ONE
          REP       " 0",TESTDTE
          PACK      KEY27,SITE,TESTDTE,SP70
          CALL      RSEMVIS6                * Position on & read a treatment
PROC1000  CALL      RKEMVIS6                  file record
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,DSPCNT              * Increment the record counter
          IF        DSPCNT%10=1
            DISPLAY   *P12:24,*V2LON,EMVIURNO;
          ENDIF
.
          MATCH     SITE,EMVISITE
          GOTO      PROC9999 IF NOT EQUAL
.
          COMPARE   THREE,EMVISTAT
          GOTO      PROC1000 IF NOT EQUAL
.
          CALL      VPTD0000                * Validate patient details
          BRANCH    EXIT,PROC1000
.

          CALL      CLEAR000
          CALL      MOVE0000
          CALL      WRITE000
          GOTO      PROC1000
.
PROC9999  RETURN
+
.******************************************************************************
.*                                  VPTD0000              Called by: GTRE0000 *
.*                          Validate Patient Details                          *
.******************************************************************************
VPTD0000  MOVE      ZERO,EXIT
.
          MATCH     SP8,EMVIUD01           * Is there a ready for departure 
          GOTO      VPTD2000 IF EQUAL       * date
.
          MATCH     STRTDTE,EMVIUD01        * Ready for departure
          GOTO      VPTD9500 IF LESS        * date < Start date
.
          MATCH     EMVIUD01,ENDDTE         * End date < Ready for
          GOTO      VPTD9500 IF LESS        * departure date
.
          GOTO      VPTD3000
.
VPTD2000  MATCH     STRTDTE,EMVIDDAT
          GOTO      VPTD9500 IF LESS        * Discharge date < Start date
.
          MATCH     EMVIDDAT,ENDDTE
          GOTO      VPTD9500 IF LESS        * End date < discharge date
.
VPTD3000  PACK      KEY8,EMVIURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "Missing master file record",ERRMSG
            CALL      PRNT0000          * Print error message
            GOTO      VPTD9500
          ENDIF
          PACK      KEY8,EMVIADMN,SP70
          CALL      RDPTVIS1
          IF        OVRCD=1
            MOVE      "Missing visit file record",ERRMSG
            CALL      PRNT0000          * Print error message
            GOTO      VPTD9500
          ENDIF
          CALL      RDPMVX11
          IF        OVRCD=1
            MOVE      "Missing visit file record",ERRMSG
            CALL      PRNT0000          * Print error message
            GOTO      VPTD9500
          ENDIF
.
.
VPTD9000  MOVE      ZERO,EXIT
          GOTO      VPTD9999
.
VPTD9500  MOVE      ONE,EXIT                * Ignore flag - yes
          GOTO      VPTD9999
.
VPTD9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: ML0000   *
.*                             Print Report Header                 & PRNT0000 *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print report header
          PRINT     *N;
          UNPACK    STRTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *35,"Starting Date : ",CPCDATE
          UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *35,"Ending Date   : ",CPCDATE
.
          CALL      UND132
          PRINT     *1,"| U/R No  ",*12,"| Visit No":
                    *23,"| Error Message",*132,"|"
          CALL      UND132
          ADD       "2",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: VALD0000 *
.*                             Print Error Message                            *
.******************************************************************************
PRNT0000  COMPARE   CLNO,FIFTY8
          CALL      HEAD0000 IF LESS        * Print report header
.
.         PACK      ERRMSG,ERRMSG,SP70,SP70
          RESET     ERRMSG
          PRINT     *1,"| ",EMVIURNO:
                    *12,"| ",EMVIADMN:
                    *23,"| ",ERRMSG,"|"
          ADD       ONE,CLNO
          PACK      ERRMSG,SP70,SP70
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
. Move Extract to Web Directory
.******************************************************************************
MVEXT000  CLEAR     CMDLINE
          APPEND    "ibaemr55.us1 ",CMDLINE
          APPEND    HOSPID,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    " YTD",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
+
.******************************************************************************
.*                                  MOVE0000              Called by: PROC0000 *
.*                         Move in the extract values                         *
.******************************************************************************
MOVE0000  MOVE      CFILENO,HOSPID
          PACK      KEY3,SITE,SP70
          CALL      RDEMSIT1
          IF        OVRCD<>1
            PACK      KEY3,EMSTHSNO,SP70
            CALL      RDPTHSP1
            IF        OVRCD<>1
              PACK      HOSPID,PTHSHDPC,SP70
            ENDIF
          ENDIF
          PACK      PMI,EMVIURNO,SP70
          PACK      PIN,EMVIURNO,SP70
          PACK      EVENTID,DEMVIADM,SP70
.
          CALL      MSTD0000
.
          MATCH     SP3,PMVXABRG
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSA,PMVXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,INDIGEN          * Aboriginal
            ENDIF
          ENDIF
.
          MOVE       ZERO,FORM2
          MOVE       TWO,DVA
          PACK       KEY5,ANSC,ANSL,EMVICOMP,SP70
          CALL       RDCODE1
          IF         OVRCD=0
            MOVE      TCINDC6,COMPENSE       * Compensable status
            MOVE      TCINDC7,INSURE         * Insurance status
MOVE1000    ADD       ONE,FORM2
            COMPARE   SIX,FORM2
            GOTO      MOVE2000 IF EQUAL
.
            LOAD      ANS,FORM2,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
            CMATCH    "V",ANS
            IF        @EQUAL
              MOVE      ONE,DVA
              GOTO      MOVE2000
            ENDIF
            GOTO      MOVE1000
          ENDIF
.
MOVE2000  MATCH     SP3,PTYPE
          IF        !@EQUAL
            PACK      KEY5,ANST,SP1,PTYPE,SP70
            CALL      RDCODE1
            IF        OVRCD <> 1
              MOVE      THCSCOD,MEDICARE
              REP       " 0",MEDICARE
            ENDIF
          ENDIF
.
          PACK      KEY5,CATem,EMVIUC21,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      THCSCOD,SCEREF
          ENDIF
.
          PACK      KEY5,ANSE,ANSA,EMVIMODE,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      THCSCOD,ARRMODE
          ENDIF
.
          PACK      KEY5,ANSL,ANSA,PMVXLNG1,SP70
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,LANGUAGE
            REP       " 0",LANGUAGE
          ENDIF
.
          PACK      KEY5,CATEE,EMVIUC13,SP70
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,VISTYPE
          ENDIF
.
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          PACK      CLDATE,CDAY,CMON,CCENT,CYEAR
.
          UNPACK    SP70,DHOUR,DMIN,DSEC
          UNPACK    EMVITIME,DHOUR,DIM1,DMIN,DIM1,DSEC
          PACK      CLTIME,DHOUR,DMIN,DSEC,SP70
.
          UNPACK    EMVITRDT,CCENT,CYEAR,CMON,CDAY
          PACK      TRDATE,CDAY,CMON,CCENT,CYEAR
.
          MATCH     SP8,TRDATE
          IF        @EQUAL
            MOVE      "Missing triage date",ERRMSG
            CALL      PRNT0000          * Print error message
          ENDIF
.
          UNPACK    SP70,DHOUR,DMIN,DSEC
          UNPACK    EMVITRTM,DHOUR,DIM1,DMIN,DIM1,DSEC
          PACK      TRTIME,DHOUR,DMIN,SP70      * C-55845
.
          MATCH     SP4,TRTIME   
          IF        @EQUAL
            MOVE      "Missing triage time",ERRMSG
            CALL      PRNT0000          * Print error message
          ENDIF
.
          PACK      KEY5,ANSA,ANSA,EMVITRIG,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TCINDC1,TRCAT
          ENDIF
.
.   ***  check if triage category has been changed or report the ***
.   ***  first triage category allocated                         ***
.
          PACK      KEY24,DEMVIADM,SP70
          CALL      RSEMCHA1
MOVE2050  CALL      RKEMCHA1
          BRANCH    OVRCD,MOVE2070
.
          MATCH     DEMVIADM,EMCHVISN
          GOTO      MOVE2070 IF NOT EQUAL
.
          MATCH     "03",EMCHUPTY
          GOTO      MOVE2050 IF NOT EQUAL
.
          MATCH     SP70,EMCHOVAL
          IF        !@EQUAL
            PACK      KEY5,ANSA,ANSA,EMCHOVAL,SP70
            GOTO      MOVE2060
          ENDIF
.    
          MATCH     SP70,EMCHCVAL
          IF        !@EQUAL
            PACK      KEY5,ANSA,ANSA,EMCHCVAL,SP70
            GOTO      MOVE2060
          ENDIF
.
          GOTO      MOVE2050
.         
MOVE2060  CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TCINDC1,TRCAT
          ENDIF
.
MOVE2070  MATCH     " ",TRCAT
          IF        @EQUAL
            MOVE      "Missing triage category",ERRMSG
            CALL      PRNT0000          * Print error message
          ENDIF
.
MOVE2100  PACK      KEY5,ANSE,ANSD,EMVIDSTA,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MOVE2200
.
          MOVE      THCSCOD,DEPSTAT
          MATCH     "4",DEPSTAT
          GOTO      MOVE2200 IF EQUAL
.
          UNPACK    EMVIDRDT,CCENT,CYEAR,CMON,CDAY
          PACK      SNDATE,CDAY,CMON,CCENT,CYEAR
.
          MATCH     SP8,SNDATE
          IF        @EQUAL
            MOVE      "Missing doctor seen date",ERRMSG
            CALL      PRNT0000          * Print error message
          ENDIF
.
          UNPACK    SP70,DHOUR,DMIN,DSEC
          UNPACK    EMVIDRTM,DHOUR,DIM1,DMIN,DIM1,DSEC
          PACK      SNTIME,DHOUR,DMIN,SP70          * C-55845
.
          MATCH     SP4,SNTIME
          IF        @EQUAL
            MOVE      "Missing doctor seen time",ERRMSG
            CALL      PRNT0000          * Print error message
          ENDIF
.
MOVE2200  PACK      KEY5,ANSE,ANSD,EMVIDSTA,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MOVE3000
.
          MOVE      THCSCOD,DEPSTAT
          MATCH     "1",DEPSTAT
          GOTO      MOVE2500 IF NOT EQUAL
.
          MATCH     SP8,EMVIPADM
          IF        @EQUAL
            CLEAR     ERRMSG
            APPEND    "Emergency visit ",ERRMSG
            APPEND    "has dep status of 1 and Inpat Adm is blank",ERRMSG
            RESET     ERRMSG
            CALL      PRNT0000          * Print error message
          ENDIF
          PACK      ADMEPIS,EMVIPADM,SP70
          PACK      KEY30,EMVIPADM,SP70
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,MOVE3000
          MATCH     DTADMN,EMVIPADM
          GOTO      MOVE3000 IF NOT EQUAL
          PACK      ADMWARD,TWARD,SP70
          PACK      KEY6,TWARD,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            MOVE      "Invalid admitting ward",ERRMSG
            CALL      PRNT0000
          ENDIF
          MATCH     PTWDHOSN,EMSTHSNO
          IF        !@EQUAL
            CLEAR     ERRMSG
            APPEND    "Emergency visit ",ERRMSG
            APPEND    " has dep status of 1",ERRMSG
            APPEND    " and NOT admitted to ward in hospital ID ",ERRMSG
            APPEND    EMSTHSNO,ERRMSG
            RESET     ERRMSG
            CALL      PRNT0000          * Print error message
          ENDIF
          UNPACK    SP70,DHOUR,DMIN,DSEC
          UNPACK    TTIME,DHOUR,DIM1,DMIN,DIM1,DSEC
          PACK      INPTTIME,DHOUR,DMIN,SP70
.
          UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
          PACK      INPTDATE,CDAY,CMON,CCENT,CYEAR
.
          GOTO      MOVE3000
.
MOVE2500
.         MATCH     "3",DEPSTAT
.         IF        @EQUAL
.           MATCH     SP8,EMVIPADM
.           IF        !@EQUAL
.             CLEAR     ERRMSG
.             APPEND    "Emergency visit ",ERRMSG
.             APPEND    " has dep status of 3 and Inpat Adm is ",ERRMSG
.             APPEND    EMVIPADM,ERRMSG
.             RESET     ERRMSG
.             CALL      PRNT0000          * Print error message
.           ENDIF
.         ENDIF
.
MOVE3000  PACK      KEY5,CATEC,EMVIUC11,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      THCSCOD,DEPREF  
          ENDIF
.
          PACK      KEY5,CATA8,EMVIUC08,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      THCSCOD,DEPDEST  
          ENDIF
.
.         MATCH     "4",DEPSTAT
.         GOTO      MOVE4000 IF EQUAL
.
          UNPACK    EMVIUD01,CCENT,CYEAR,CMON,CDAY
          PACK      DRDATE,CDAY,CMON,CCENT,CYEAR
.
          MATCH     SP8,DRDATE
          IF        @EQUAL
            MOVE      "Missing ready for departure date",ERRMSG
            CALL      PRNT0000          * Print error message
          ENDIF
.
          UNPACK    SP70,DHOUR,DMIN,DSEC
          UNPACK    EMVIUT01,DHOUR,DIM1,DMIN,DIM1,DSEC
          PACK      DRTIME,DHOUR,DMIN,SP70            * C-55845
.
          MATCH     SP4,DRTIME
          IF        @EQUAL
            MOVE      "Missing ready for departure time",ERRMSG
            CALL      PRNT0000          * Print error message
          ENDIF
.
MOVE4000  UNPACK    EMVIDDAT,CCENT,CYEAR,CMON,CDAY
          PACK      ADDATE,CDAY,CMON,CCENT,CYEAR
.
          UNPACK    SP70,DHOUR,DMIN,DSEC
          UNPACK    EMVIDTIM,DHOUR,DIM1,DMIN,DIM1,DSEC
          PACK      ADTIME,DHOUR,DMIN,DSEC,SP70
.
          PACK      INJPOIS,TWO,SP70
          PACK      KEY5,CATel,EMVIUC20,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     ANSI,TCINDC11
            IF        @EQUAL
              PACK      INJPOIS,ONE,SP70
            ENDIF
          ENDIF
.
          CALL      PRDG0000
.
MOVE9999  RETURN
+
.******************************************************************************
.*                                  PRDG0000              Called by: MOVE0000 *
.*                         Extract principal diagnosis                        *
.******************************************************************************
PRDG0000  PACK      KEY14,EMVIADMN,ZERO,ZERO,FIVE,SP70
          CALL      RSEMVCD1                     * position for admission no.
PRDG2000  CALL      RKEMVCD1                     * read next record
          BRANCH    OVRCD,PRDG9999               * eof - finished primary diag.
.
          MATCH     EMVCVIST,DEMVIADM            * same visit no. still ?
          GOTO      PRDG2000 IF NOT EQUAL        * no - finished prim. diag.
.
          MATCH     "000",EMVCSEQU               * primary diagnosis ?
          GOTO      PRDG2000 IF NOT EQUAL        * no - ignore
.
          MATCH     "005",EMVCTYPE
          GOTO      PRDG2000 IF NOT EQUAL
.
          PACK      KEY9,EMVCMNCD,SP70
          CALL      RDEMICD1
          IF        OVRCD<>1
            MATCH     SP9,EMICVEMD
            IF        !@EQUAL
              PACK      PDX,EMICVEMD,SP70
            ENDIF
          ENDIF
          GOTO      PRDG9999
.
PRDG9999  RETURN
.
.******************************************************************************
.*                                  MSTD0000              Called by: WRIT0000 *
.*                         Extract master details                             *
.******************************************************************************
MSTD0000  MOVE      SP10,SACC             * Country of Birth
          PACK      KEY5,ANSC,SP1,PCONT,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            UNPACK    TCFORM6,DIM2,SACC
.... I-55845
            IF        TCFORM6<1000
              REP       " 0",SACC
            ENDIF
          ENDIF
.
          MOVE      PSEX,SEX
          REP       "M1F2I3R3U9",SEX
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DOB,CDAY,CMON,CCENT,CYEAR,SP70
.
          PACK      POSTCODE,PPOST,SP70
          MATCH     "9988",POSTCODE
          IF        @EQUAL
            PACK      POSTCODE,SP70
          ENDIF
          REP       " 0",POSTCODE
.
          MOVE      SP70,DOBEST
.
.         Marital Status
.
          MOVE      "6",MCAT
          PACK      KEY5,ANSM,SP1,PMSTAT,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      THCSCOD,MCAT          * Marital status
          ENDIF
.
          CALL      IBACLOCK
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          CALL      CALCAGE
          IF        PSAGEYRS>15
            GOTO      MSTD1000
          ENDIF
.
          MATCH     "2",MCAT
          GOTO      MSTD0500 IF EQUAL
.
          MATCH     "3",MCAT
          GOTO      MSTD0500 IF EQUAL
.
          MATCH     "4",MCAT
          GOTO      MSTD0500 IF EQUAL
.
          MATCH     "5",MCAT
          GOTO      MSTD1000 IF NOT EQUAL
.
MSTD0500  MOVE      "Invalid marital status for age",ERRMSG
          CALL      PRNT0000          * Print error message
.

MSTD1000  PACK      KEY8,POSTCODE,SP10
          PACK      KEY56,KEY8,PSUBRB,SP10,PADD4,SP70                 *C-240184
          CALL      RDIBPOS1
          IF        OVRCD = 0
            UNPACK      IBPOASGC,DIM1,DIM4,DIM4A
            PACK        STATID,DIM1
          ELSE
            PACK      KEY56,PPOST,PSUBRB,SP70
            CALL      RSIBPOS1
            CALL      RKIBPOS1
            BRANCH    OVRCD,MSTD9999
            MATCH     KEY8,IBPOPCOD
            IF        @EQUAL
              MATCH     PSUBRB,IBPOSUBR
              IF        @EQUAL
                UNPACK      IBPOASGC,DIM1,DIM4,DIM4A
                PACK        STATID,DIM1
              ENDIF
            ENDIF
          ENDIF
.
MSTD9999  REP       " 0",STATID
          PACK      LOCALITY,PSUBRB,SP70
          RETURN
+
.******************************************************************************
.*                                  CLEAR000              Called by: ML0000   *
.*                         Clear the extract variables                        *
.******************************************************************************
CLEAR000  UNPACK    SP70,HOSPID   
          UNPACK    SP70,PMI      
          UNPACK    SP70,PIN      
          UNPACK    SP70,EVENTID  
.
.
          UNPACK    SP70,STATID                  * after 01/07/2007
.
          UNPACK    SP70,POSTCODE 
          UNPACK    SP70,SEX      
          UNPACK    SP70,DOB      
          UNPACK    SP70,DOBEST   
          UNPACK    SP70,SACC     
          UNPACK    SP70,INDIGEN  
          UNPACK    SP70,MCAT     
          UNPACK    SP70,COMPENSE 
          UNPACK    SP70,INSURE   
          UNPACK    SP70,MEDICARE 
          UNPACK    SP70,DVA      
          UNPACK    SP70,SCEREF   
          UNPACK    SP70,ARRMODE  
          UNPACK    SP70,LANGUAGE 
          UNPACK    SP70,VISTYPE  
          UNPACK    SP70,CLDATE   
          UNPACK    SP70,CLTIME   
          UNPACK    SP70,TRDATE   
          UNPACK    SP70,TRTIME   
          UNPACK    SP70,TRCAT    
          UNPACK    SP70,SNDATE   
          UNPACK    SP70,SNTIME   
          UNPACK    SP70,DEPSTAT  
          UNPACK    SP70,DEPDEST  
          UNPACK    SP70,DEPREF   
          UNPACK    SP70,DRDATE   
          UNPACK    SP70,DRTIME   
          UNPACK    SP70,ADDATE   
          UNPACK    SP70,ADTIME   
          UNPACK    SP70,INJPOIS  
          UNPACK    SP70,PDX      
          UNPACK    SP70,LOCALITY 
          UNPACK    SP70,ADMEPIS   
.
          UNPACK    SP70,ADMWARD                 * after 01/07/2007
          UNPACK    SP70,INPTDATE                * after 01/07/2010
          UNPACK    SP70,INPTTIME                * after 01/07/2010
.
CLEAR999  RETURN
.******************************************************************************
.*                                  WRITE000              Called by: ML0000   *
.*                         Write the extract variables                        *
.******************************************************************************
WRITE000  IF        NEWLAYT=1
            WRITE     EXTRACTF,SEQ;HOSPID,PMI,PIN,EVENTID,STATID:
                                   POSTCODE,SEX,DOB,DOBEST,SACC:
                                   INDIGEN,MCAT,COMPENSE,INSURE,MEDICARE:
                                   DVA,SCEREF,ARRMODE,LANGUAGE,VISTYPE:
                                   CLDATE,CLTIME,TRDATE,TRTIME,TRCAT:
                                   SNDATE,SNTIME,DEPSTAT,DEPDEST,DEPREF:
                                   DRDATE,DRTIME,ADDATE,ADTIME,INJPOIS,PDX:
                                   LOCALITY,ADMEPIS,ADMWARD,INPTDATE,INPTTIME
          ELSE
            WRITE     EXTRACTF,SEQ;HOSPID,PMI,PIN,EVENTID,STATID:
                                   POSTCODE,SEX,DOB,DOBEST,SACC:
                                   INDIGEN,MCAT,COMPENSE,INSURE,MEDICARE:
                                   DVA,SCEREF,ARRMODE,LANGUAGE,VISTYPE:
                                   CLDATE,CLTIME,TRDATE,TRTIME,TRCAT:
                                   SNDATE,SNTIME,DEPSTAT,DEPDEST,DEPREF:
                                   DRDATE,DRTIME,ADDATE,ADTIME,INJPOIS,PDX:
                                   LOCALITY,ADMEPIS,ADMWARD
          ENDIF
.
WRITE999  RETURN
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS                * Calculate diff between dates
          INC       CALCAGE 
          INC       PATCOMN3
.
          INC       IBAPOSIO/INC            * Postcode file
          INC       EMRSITIO/INC
          INC       PATHSPIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       EMRCHAIO/INC
          INC       EMRVISIO/INC
          INC       EMRICDIO/INC
          INC       EMRVCDIO/INC
          INC       PATDSCIO/INC            * Discharge file
          INC       PATCODIO/INC            * Codes file
          INC       PATDRGIO/INC            * Date Range
          INC       PATMA1IO/INC            * Master file
          INC       PATVADIO/INC            * Transfer source file
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PATCT1IO/INC
+
.


.******************************************************************************
.* System         : Patient                                                   *
.* Program        : IBAINP83.PBL                                              *
.* Name           : Cancer Register Reports                                   *
.******************************************************************************
.* Date           : 22/05/1995                                                *
.* Author         : Matt Surridge                                             *
.* Function       : Allows the user to enter and maintain Cancer Registration *
.*                  details. Details can be entered via U/R or Admission No.  *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.01.01 16/02/2026  Ebon CLements  TSK 0972180                           *
.*           Report the language spoken at home from the PMI if it's not      *
.*           recorded at the visit level - XLNGHOME                           *
.* V12.00.01 16/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.* V11.04.07 05/08/2024  Nikitha B      TSK 0944481                           *
.*           Modified field name from "Sex" to "Sex at Birth' in XML report   *
.* V11.04.06 29/07/2024  Nikitha B      TSK 0944481                           *
.*           2024-25 Victorian Cancer Reg stat changes.                       *
.*           Modified field name from "Sex" to "Sex at Birth'                 *
.*           Modified typo in internal reports header to ‘From Coding Date’   *
.* V11.04.05 08/02/2024  Nikitha B      TSK 0941850                           *
.*           Added Indicator 8 of Catcode "G " to display Sex at Birth in     *
.*           Vic Cancer Reg Extract.                                          *
.* V11.04.04 24/01/2024  Ebon Clements  TSK 0939233                           *
.*           Format facility previously diagnosed for VIC                     *
.*           as it's stored on blocks of 80 character for a textarea          *
.*           XWDIAGNS PTCDFCPR                                                * 
.* V11.04.03 17/01/2024  Ebon Clements  TSK 0940233                           *
.*           Added coversion of overseas post codes to 8888 - PCOS0000        *
.*           11/12/2023  PJ Le Febour   TSK 0936820a                          *
.*           add APPEND PMHCADDR2,XLMOADDS and STRIP PMHCADR2 in LOGP0000     *
.*           cater for change to XADDRESS now 200                             *
.* V11.04.02 27/11/2023  Alina Bhari    TSK 0940027                           *
.*           U/R Number field added to the extract format in postition 1      * 
.*           - TASURNO,SETTAS00,WRTTAS00,WRTTAS00                             * 
.* V11.04.01 21/11/2023  Thanh T        TSK 0939233                           *
.*           Initialize PTCDFCPR field with 280 spaces intead of 90 spaces    * 
.* V11.03.05 17/11/2023  Nikitha B      TSK 0940027                           *
.*           Moved reading of TASDBSDG and TASDLATR from WRTTAS00 to SETTAS00 *
.*           routine to stop wrong display of BasisofDiagnosis and Laterality * 
.* V11.03.04 15/11/2023  PJ Le Febour   TSK 0936820                           *
.*           Data elements changes XADDRESS to 200                            *
.*           XTDGNAM2 to 30 XTDADDRS to 250 XWDIAGNS to 250 XLMOADDS to 250   *
.* V11.03.03 25/05/2023  Peter Vela     TSK 0923957e                          *
.*           Fixed Prev/Maiden Last Name output                               *
.*           Fixed Basis of Diagnosis                                         *
.*           Fixed Laterality                                                 *
.* V11.03.02 24/05/2023  PJ Le Febour   TSK 0923957d                          *
.*           corrected Basis of Diagnosis field in extract file               *
.* V11.03.01 19/04/2023  PJ Le Febour   TSK 0923957                           *
.*           Removed TASHOSPN TASPADD2 TASALNAM TASAFNAM                      *
.*           Added   PMPXSNAM PMPXPFGN PTCDBSDG PTCDLATR                      *
.* V11.03.01 19/04/2023  Ebon Clements  TSK 0909763                           *
.*           Report user name instead of user id in XCANNOTE                  *
.*           Report medicare reference number at end of XPMEDI                *
.* V11.01.01 19/08/2021  Ebon Clements  TSK 0909763                           *
.*           Report user name instead of user id in XCANNOTE                  *
.*           Report medicare reference number at end of XPMEDI                *
.******************************************************************************
.* V10.14.02 18/04/2018  Thanh T.       TSK 0858797                           *
.*           Changed to correct the invalid data for Ingigenousd status,      *
.*           Primary site and Morph                                           *
.* V10.14.01 05/04/2018  Thanh T.       TSK 0858797                           *
.*           Added Extract File Type parameter for XML and ASCII format       *
.******************************************************************************
.* V10.12.02 26/03/2018  J.Tan          TSK 0851813                           *
.*           Added TNM Stage - T, N and M Code                                *
.* V10.08.07 03/10/2016  Jill Parkinson TSK 0821563                           *
.*           Added write of blank 1100 line as webPAS doesn't collect building*
.* V10.08.06 09/08/2016  Jill Parkinson TSK 0821563                           *
.*           Changed to use pmvxrhc1 instead of adoctl for field 2225         *
.* V10.08.05 08/08/2016  Jill Parkinson TSK 0821563                           *
.*           Changed to write blank 1060 line if no alias exists              *
.* V10.08.04 06/06/2016  Peter Vela                                TSK 0321157*
.*           Recompiled for VISCODFD                                          *
.* V10.08.03 17/05/2016  Peter Vela                                TSK 0813958*
.*           Fixed extraction for XSTGCSYS                                    *
.* V10.08.02 13/04/2016  Peter Vela                                TSK 0813958*
.*           2016 Victorian Cancer Reg stat changes                           * 
.*           Removed 1080 (Already removed) - Marital Status                  *
.*           Removed 1374 - Degree of Spread                                  *
.*           Added 1225 - Treating Doctors Provider Number                    *
.*           Added 1390 - Stage of Cancer Flag                                *
.*           Added 1391 - Stage of Cancer at Diagnosis                        *
.*           Added 1392 - Cancer Staging System                               *
.*           Added 2226 - Local GP Provider Number                            *
.* V10.08.01 12/04/2016  Steve Armstrong                           TSK 0813958*
.*           Added missing PATCRDFD variables to CLRCRD00                     *
.******************************************************************************
.* V10.04.02 04/06/2014  Jill Parkinson CAR 291342                            *
.*           Mods to print an error message if the cancer morphology code     *
.*           on patcrdaf does not match the patecdaf morphology code          *
.******************************************************************************
.* V10.04.01 04/04/2014  Patrick Adair CAR 296532                             *
.*           Increase XBASDIAG, XBATDIAG & XBASDESC to 50 characters          *
.******************************************************************************
.* V10.03.07 26/11/2013  J.Tan         CAR 291934                             *
.*           Set XHCAMPUS/XHOSCODE to Hospital PTHSNAME/PTHSHDPC (not blank)  *
.*           for Non Multi Campus                                             *
.* V10.03.06 04/06/2013  Davin         CAR 283509                             *
.*           Mods for VIC cancer reg. 2013                                    *
.* V10.03.05 11/12/2012  Patrick Adair CAR 277897                             *
.*           Replace all spaces in EXTRCTFN with _                            *
.* V10.03.04 01/10/2012  Mike Laming   CAR 264529                             *
.*           Add Century to BirthDate, DischargeDate & Date of Death          *
.* V10.03.03 20/09/2012  Mike Laming   CAR 264529                             *
.*           Corrected Discharge Date & Date of Death dates in SETTAS00       *
.* V10.03.02 20/08/2012  Mike Laming   CAR 264529                             *
.*           Modify CPRT0000 & created CREXT000 to handle multiple Extract    *
.*           formats for different States. Added PREVADMN & CURRADMN to       *
.*           identify change of Admission so extract can be written after.    *
.* V10.03.01 16/04/2012  Mike Laming   CAR 260916                             *
.*           Mods to UCAN0000 NCOD0000 NDIS0000 & GMORP000 for multi Primary's*
.******************************************************************************
.* V10.02.04 05/09/2011 Saroeun Soeur  CAR 250003                             *
.*           Changed XHOSCODE from dim 3 to a dim 4                           *
.* V10.02.03 13/07/2011  Steve Armstrong   CAR 240722                         *
.*           Further mods to cater for second given name as part of PATGSRFD  *
.*           keys                                                             *
.* V10.02.02 22/06/2011  Steve Armstrong   CAR 240722                         *
.*           Mods to cater for changes to PATGSRFD:                           *
.*                - GSRSNAM & GSRGNAM extended to 30 chars.                   *
.*                - PTGSGNM2 added.                                           *
.*                - all indexes changed in length.                            *
.* V10.02.01 28/03/2011  Mike Laming   CAR 240107                             *
.*           Mods to PATECDaf & PATECOaf variables & Keys - file change       *
.******************************************************************************
.* V10.01.02 22/03/2011 Sandra Barcham CAR 240108                             *
.*            Replace variable METC8090 with METC7990 (C79.9 Cancer code)     *
.* V10.01.01 15/02/2011  Mike Laming   CAR 237278                             *
.*            Replace variable METC7988 with METC8090 (C80.9 Cancer code)     *
.******************************************************************************
.* V10.00.04 28/07/2010  Ebon Clements CAR 226012                             *
.*           Changed GMORP000 to include secondary cancer morphology codes    *
.* V10.00.03 06/05/2010  Mike Laming   CAR 221253                             *
.*           More mods to GMORP000 - add code to sort Morphology codes 3,6,9  *
.* V10.00.02 06/05/2010  Mike Laming   CAR 221253                             *
.*           Mods for multiple Morphology Codes in routine GMORP000           *
.* V10.00.01 31/03/2010  Mike Laming   CAR 208570                             *
.*           Add PTCDMET4 to PTCDMET6 Metastatic Codes at SETEXT20            *
.*           Recompiled for changes to PATCRDFD                               *
.******************************************************************************
.* V9.12.03  10/03/2010  Mike Laming   CAR 217537                             *
.*           More mods for Feb 2010 changes (to XMORPHC1, XBASDIAG & XBASDESC *
.* V9.12.02  03/02/2010  Mike Laming   CAR 208570                             *
.*           Correct previous changes, add Admiss. No to report               * 
.* V9.12.01  30/11/2009  Mike Laming   CAR 208570                             *
.*           Mods for Feb 2010 - Changes as per Spec. + add Metast. Cancer Range
.******************************************************************************
.* V9.11.02  08/04/2009 Ebon Clements  CAR 188098                             *
.*           Added update of extract sent date to report option 1             *
.*           Added option 3 unsent cancer registrations                       *
.* V9.11.01  02/12/2008 Sandra Barcham CAR 184500                             *
.*           Fix the XHCAMPUS & XHOSCODE fields when running as All Campuses  *
.******************************************************************************
.* V9.09.02  12/12/2007  Jill Habasque CAR 152877                             *
.*           Mods to output hospital ID for us1 script                        *
.* V9.09.01  18/10/2007  Mike Laming   CAR 146004                             *
.*           Mods to output of XURNUMBR, XESTDATE, XBASDIAG, XCOMPDAT and add *
.*           XBATDIAG                                                         *
.******************************************************************************
.* V9.08.06  25/05/2007  Mike Laming   CAR 125991                             *
.*           Re-compiled for PATCRDFD/IO - New fields in file                 *
.* V9.08.05  25/05/2007  Mike Laming   CAR 125991                             *
.*           Re-compiled for PATCRDFD/IO - Record increased                   *
.* V9.08.04  19/04/2006  J.Tan            CAR 131574                          *
.*           Fixed line 1271 - Estimate date flag (0 or 1)                    *
.* V9.08.03  31/01/2007  Peter Vela    CAR 127930                             *
.*           Recompled for MRTCONFD                                           *
.* V9.08.02  13/11/2006  J.Tan            CAR 124641                          *
.*           Fixed line 1235 and 1230 - Hospital code                         *
.* V9.08.01  13/11/2006  J.Tan            CAR 124641                          *
.*           Fixed line 1271, 1370 and LMO name to use pmpxrhc1               *
.******************************************************************************
.* V9.07.01  23/08/2006  J.Tan            CAR 116977                          *
.*           Added medicare and fixed field for sex                           *
.******************************************************************************
.* V9.06.02  21/06/2006  J.Tan            CAR 96476                           *
.*           Mods for Multi hospital                                          *
.* V9.06.01  07/06/2006  J.Tan            CAR 96476                           *
.*           Mods for 2006 changes                                            *
.******************************************************************************
.* V9.04.01  29/04/2005  Peter Vela       CAR 55214                           *
.*           Recompiled for MRTCONFD                                          *
.******************************************************************************
.* V9.03.04  22/07/2004 Lina Vo        CAR 51463                              *
.*           Removed redundant information off the Report.                    *
.* V9.03.03  31/12/2003 Sylvek Litewka CAR 20222                              *
.*           Replaced calls to RDICD1 with new IO routine for reading ICD     *
.*           Disease file - RDPTICD1. Also mods to use disease code descripton*
.*           from patecdaf instead if reading ICD Disease file.               *
.*           18/12/2003 Peter Vela   CAR 43134                                *
.*           Recompiled for MRTCONFD                                          *
.* V9.03.02  29/10/2003 Sandra Barcham   43783                                *
.*           Recompiled for NZHISIIO and NZHISIUP                             *
.* V9.03.01  25/09/2003  Lina Vo  CAR18262                                    *
.*           Changed XBASDIAG output format.                                  *
.******************************************************************************
.* V9.02.00  28/04/2003  Lina Vo                                              *
.*           Ported to V9 and changed to use new cancer table.                *
.******************************************************************************
.*        V5.08.B02 24/11/2000  J.Tan                                         *
.*                  Added option for extraction                               *
.*        V5.08.03  25.10.2000 J.Tan                                          *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.02  12/10/2000  Ebon Clements                                 *
.*                  Fixed spelling in options                                 *
.*        V5.08.01  23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*                  Change print to be correct                                *
.*        V5.08.B01 15/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*        V5.07.01  20/05/1999 Jill Habasque SRF 645542                       *
.*                  Fixed page break                                          *
.*                  V5.07.B03  29/01/1999  Nicole Harrington                  *
.*                  507 rebounds                                              *
.*                  V5.07.B02  19/11/1998  Nicole Harrington                  *
.*                  Changed for 507 fixes                                     *
.*                  V5.03.B4   13/06/1995     MAtt                            *
.*                  Get Pahtology code from patmdiaf as it is no longer on    *
.*                  patcrdaf.                                                 *
.*                  V5.03.B3   13/06/1995     MAtt                            *
.*                  Recompiled for PATCRGIO & PATCRDIO                        *
.*                  V5.03.B2   08/06/1995     MAtt                            *
.*                  Recompiled for PATCRGFD & PATCRDFD                        *
.******************************************************************************
.
.         FD's required
.
          INC       STD001FD/PBL
          INC       IBAPOSFD/INC            * Postcodes
          INC       MRTCONFD/INC
          INC       NHIMASFD/INC            * patient Alerts for 0 U/R's
          INC       NZHISIFD/INC
          INC       PATALRFD/INC            * patient Alerts for 0 U/R's
          INC       PATCALAG/INC            * for calcage
          INC       PATCANFD/INC            * patient codes file
          INC       PATCODFD/INC            * patient codes file
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATCRDFD/INC            * Cancer Register Detail File
          INC       PATCRGFD/INC            * Cancer Register Header File 
          INC       PATDHEAD/INC            * for PMI head
          INC       PATDO1FD/INC            * Patient Doctors File
          INC       PATDSCFD/INC            * Discharge File
          INC       PATECDFD/INC            * Patient episode disease file
          INC       PATGSRFD/INC            * Patient name file           
          INC       PATHSPFD/INC            * Hospital file           
          INC       PATICDFD/INC            * ICD-9 Codes
          INC       PATMA1FD/INC            * patient master file
          INC       PATMI1FD/INC            * admission file
          INC       PATNIDFD/INC            * patient national number file (nz)
          INC       PATSMWFD/INC            * medical warning for 0 U/R's
          INC       PATVISFD/INC            * Visit File
          INC       PMSPX2FD/INC            * PMI Extension file
          INC       PMSHCPFD/INC            * HCP file
          INC       PMIVARS/INC
          INC       PMSVX1FD/INC
          INC       VISCODFD/INC
          INC       WEBSECFD/INC
.
Z15       INIT      "zzzzzzzzzzzzzzz"
.
. ------- program variables -------
.
ADDDOT    FORM      1
ADD       DIM       2
ADMDATE   DIM       10
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
DCC       DIM       2
DDD       DIM       2
DMM       DIM       2
DYY       DIM       2
BJDAY     FORM      4
CONAME    DIM       35
CHG       FORM      1
CJDAY     FORM      4
CMDLINE   DIM       80
CATVA     INIT      "VA"
CODCs     INIT      "Cs"
CODED     INIT      "Coding"
CODEA     INIT      "A"
CODEN     INIT      "N"
CODEY     INIT      "Y"
COUNT     FORM      3
CODEBD    INIT      "BD"
CODESG    INIT      "SG"
CODEIL    INIT      "IL"
CODELB    INIT      "LB"
CODEU1    INIT      "U1"
CODEU2    INIT      "U2"
CODEU3    INIT      "U3"
CODEU4    INIT      "U4"
CODEU5    INIT      "U5"
CODEU6    INIT      "U6"
CODEU7    INIT      "U7"
METC770   INIT      "C77.0    "                                       *I-208570
..METC7988  INIT      "C79.88   "                                     *D-237278
METC7990  INIT      "C79.9    "                                       *C-237278
.
DQ        INIT      "#""
SL        INIT      "/"
PP        INIT      "|"
CO        INIT      ","
.
SP200     DIM       200
SP250     DIM       250
DIM1A     DIM       1
DIM1B     DIM       1
DIM4      DIM       4
DIM5      DIM       5 
DIM9      DIM       9 
DIM50     DIM       50
DIM80A    DIM       80
DIM80B    DIM       80
DIM80C    DIM       80
DIM80D    DIM       80
DIM150    DIM       150                                               *I-208570
DIM400    DIM       400                                               *I-208570
DISCHD    INIT      "Discharge"
DOB       DIM       10
DISC2018  FORM      1
ENDDATE   DIM       8 
FORM3     FORM      3
FOUNDPRI  FORM      1
FOUNDMOR  FORM      1
FOUNDCAN  FORM      1
FROMDATE  DIM       10
HEADHOSP  DIM       3
IBCNMHOS  FORM      1
KEY30A    DIM       30
MORPHCOD  DIM       9[10]
NAMEHOSP  DIM       50
CURRADMN  DIM       8
PREVADMN  DIM       8
SAVADMNO  DIM       8
SAVPDIAG  DIM       9
SAVKEY13  DIM       13
SAVKEY16  DIM       16
SAVKEY33  DIM       33
SAVMORPH  DIM       9
SAVURNO   DIM       8
SECNDFLG  FORM      1
TODATE    DIM       10
THEDATE   DIM       10
.
BASEDESC  DIM       20
STAGDESC  DIM       20
INVADESC  DIM       20
LABDESC   DIM       20
PATHDESC  DIM       50
PATHD40   DIM       40
DIAGD40   DIM       40
DIAGDESC  DIM       50
.
TAGNAME   DIM       50
TAGNAME1  DIM       50
TAGNAME2  DIM       50
TAGVALUE  DIM       250
TAGSTRNG  DIM       250
LESSERTH  INIT      "<"
GREATRTH  INIT      ">"
ECSTGSTR  INIT      "<![CDATA["
ECSTGEND  INIT      "]]>"
EXTRCTHD  INIT      "CancerExtract" 
EXTRCTRG  INIT      "CancerRegRec" 
HED0001   INIT      "<?xml version=#"1.0#" encoding=#"UTF-8#" ?>"
QUOTE     INIT      "#""
EXTRCTYN  FORM      1
EXTRFILE  FILE      ASCII, FIXED=400     * Extract File (Adjust Length to Suit)
EXTRCTFN  DIM       8                    * Extract File Name
EXTRTYPE  DIM       1                    * Extract Type
TABDELM   EQU       0x09                 * Tab Delimiter
.
DDATED    DIM        8       Discharge date
.
. Field   Type      Size     Description
. -----   ----      ----     ----------------
XPSNAME   DIM       30       Surname
XPGNAM1   DIM       30       First Given Name
XPGNAM2   DIM       30       Second Given Name
XPBDATE   DIM       15       Date of birth
XPATSEX   DIM        1       Sex
XPMEDI    DIM       11       Medicare
XINDHEID  DIM       16       Individual Health Identifier
XPRALIAS  DIM       50       Previous/alias names
XABORIGN  DIM        1       Aboriginal/TS Islander Status 1=Yes,2=No,9=Unknown
XMARSTAT  DIM        1       Marital Status
XADDRESS  DIM      200       Street Address
XPSUBURB  DIM       30       Suburb
XPOSTCOD  DIM        4       Postcode
XCNBIRTH  DIM        4       Country of birth
XLNGHOME  DIM        4       Language spoken at home
XTDSNAME  DIM       30       Treating doctor surname
XTDGNAM1  DIM       30       Treating doctor first name
.XTDGNAM2  DIM       20       Treating doctor second name
XTDGNAM2  DIM       30       Treating doctor second name
.XTDADDRS  DIM       50       Treating doctor address
XTDADDRS  DIM      250       Treating doctor address
XHCAMPUS  DIM       30       Hospital Campus/Unit Name
XHOSCODE  DIM        4       Hospital Campus Code
XURNUMBR  DIM       15       U/R Number
XADMDATE  DIM       15       Admission date/Consulatation date
XDIADATE  DIM       15       Diagnosis date
XESTDATE  DIM        1       Estimated/Unknown Date Flag
XCANCDIA  DIM        1       Cancer Diagnosed Prior to Admission/Consulatation
.XWDIAGNS  DIM       75       If Yes, where Diagnosed
XWDIAGNS  DIM      250       If Yes, where Diagnosed
XDISDATE  DIM       15       Date of Separation if admitted
XVITSTAT  DIM        1       Patient Vital Status
XPRIMCAN  DIM        2       Total number of Primary Cancers in this person
XICDPRI1  DIM       45       ICD9-CM/ICD10-AM Primary Site Code 1
.** XICDPRI2  DIM       45       ICD9-CM/ICD10-AM Primary Site Code 2
XLATERAL  DIM        1       Laterality
XPRSTEXT  DIM       75       Primary site text description
XMETFLG1  DIM        1       Metastatic site code 1 populated (Y=Yes, N=No, U=un
XMETCOD1  DIM       75       ICD Metastatic site code assoc.with primary cancer
XMETCOD2  DIM       75       ICD Metastatic site code assoc.with primary cancer
XMETCOD3  DIM       75       ICD Metastatic site code assoc.with primary cancer
.** XMETDESC  DIM       75       Metastatic site text description
XMETDES1  DIM       75       Metastatic site text description site 1
XMORPHC1  DIM       75       ICD9-CM Morphology code 1
XMORPHC2  DIM       75       ICD9-CM Morphology code 2
XMORPDES  DIM       75       Morphology Text Description
XHISGRAD  DIM        1       Histological Grade
XBASDIAG  DIM       50       Basis of diagnosis of cancer at your facility
XBASDESC  DIM       50       Basis of diagnosis Description for "Other" ("9")
XECOGSTA  DIM        3       ECOG performance status (Cat.EO)
XSPRDIN1  DIM       75       Additional information for degree of spread 1
XSPRDIN2  DIM       75       Additional information for degree of spread 2
XSPRDIN3  DIM       75       Additional information for degree of spread 3
XDEGSPRD  DIM       10       Degree of spread (Cat.SZ) stored in viscodaf
XBATDIAG  DIM       50       Basis of diagnosis of cancer at other facility
XTUMDATE  DIM       15       Date of tumour recurrence
XMETDATE  DIM       15       Date of metastatic spread
XDEATHDT  DIM       15       Date of Death
XAUTOPSY  DIM        1       Autopsy Performed
XLMSNAME  DIM       30       Local Medical Officer Surname
XLMGNAM1  DIM       30       Local Medical Officer First Given Name
XLMGNAM2  DIM       30       Local Medical Officer Second Given Name/Initial
.XLMOADDS  DIM       75       Local Medical Officer Address
XLMOADDS  DIM      250       Local Medical Officer Address
XCANNOTE  DIM       30       Person completing cancer notification screen
XCOMPDAT  DIM       15       Date screen completed
XTRDCPRV  DIM       10       Treating Doctor Provider Number
XSTGCFLG  DIM        1       Stage of Cancer flag
XSTGCDIA  DIM       10       Stage of Cancer at Diagnosis 
XSTGCSYS  DIM        2       Cancer Staging System
XLCGPPRV  DIM       10       Local GP Provider Number
.
XSTGCSGT  DIM       10       Clinical Stage T
XSTGCSGN  DIM       10       Clinical Stage N
XSTGCSGM  DIM       10       Clinical Stage M
.EOR 
.
. Field   Type      Size     Description
. -----   ----      ----     ----------------
TASURNO   DIM       11       Patient UR No.
TASADMN   DIM       11       Admission No.
TASHOSPN  DIM       8        Hospital No. (PTHSHDPC Health Department code ?)
TASHOSDS  DIM       100      Hospital Name
TASPSNAM  DIM       32       Patient Last Name
TASPGNAM  DIM       32       Patient First Name
TASPSEX   DIM       3        Patient sex
TASPADD1  DIM       100      Patient Address Line 1
TASPADD2  DIM       100      Patient Address Line 2
TASPSURB  DIM       40       Patient Suburb
TASPCODE  DIM       10       Patient Post Code
TASPDOB   DIM       12       Patient Date of Birth  (dd/mm/ccyy)
TASDISCH  DIM       12       Patient Discharge Date (dd/mm/ccyy)
TASDEATH  DIM       12       Patient Date of Death  (dd/mm/ccyy)
TASPRIM   DIM       5[5]     Patient Primary Cancer codes
TASPRDES  DIM       70[5]    Patient Primary Cancer Descriptions
TASMORPH  DIM       7[5]     Patient Morphology codes
TASBIRTH  DIM       80       Patient Country of Birth
TASINDIG  DIM       80       Patient Indigenous Status
TASTDLNM  DIM       32       Treating Doctor Last Name
TASTDFNM  DIM       32       Treating Doctor First Name
TASALNAM  DIM       32       Patient Alias Last Name
TASAFNAM  DIM       32       Patient Alias First Name
TASMLNAM  DIM       32       Patient Maiden  Last Name
.
TASPRIMX  DIM       30       5 Primary codes (format "C222 C223 ...")
TASPRDEX  DIM       400      5 Primary Descriptions
TASMORPX  DIM       50       5 Morphology codes (format "M123455 M123456 ..")
.
TASXSNAM  DIM       32       Patient Middle Name                
TASXPFGN  DIM       32       Patient Pref Given Name           
TASDBSDG  DIM       3        Basis of Diagnosis CAT HDP Pos 1 
TASDLATR  DIM       3        Laterality         CAT LY  Pos 1
. ------- program constants -------
.
.--------------------------------------------------------------------
PRGID     INIT      "IBAINP83"
PRGNAM    INIT      "Cancer Register Reports"
VERSION   INIT      "V12.01.01"
.--------------------------------------------------------------------
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000                * initilisation
.
ML0500    CALL      OPTN0000                * Get Report Option (Reg OR Not)
          BRANCH    OPTION,ML1000,ML2000,ML3000
.
          GOTO      ML9999
.
.
ML1000    CALL      CREG0000                * Registered Details
          GOTO      ML4000
.
ML2000    CALL      NREG0000                * Details Not Registered 
          GOTO      ML4000
.
ML3000    CALL      UREG0000                * Unsent Registrations 
          GOTO      ML4000
.
ML4000    PERFORM   EXTRCTYN,MVEXT000       * Move Extract for Web
          GOTO      ML0500
.
ML9999    CHAIN     PGM
.
+
.*********************************************************************
.         initilisation
.*********************************************************************
.
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P58:24,"patcrdaf"
          OPEN      PATCRDA1,"patcrdaf"
.
          DISPLAY   *P58:24,"patcrdaf"
          OPEN      PATCRDA2,"patcrdaf"
.
          DISPLAY   *P58:24,"patcrgaf"
          OPEN      PATCRGA1,"patcrgaf"
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"          * files to open
.
          DISPLAY   *P58:24,"patecdaf"
          OPEN      PATECDA1,"patecdaf"
          OPEN      PATECDA2,"patecdaf"
          OPEN      PATECDA5,"patecdaf"
.
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A4,"patmi1af"
.
          DISPLAY   *P58:24,"patdocaf"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P58:24,"patcanaf"
          OPEN      PATCANA1,"patcanaf"
.
          DISPLAY   *P58:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P58:24,"paticddf"
          CALL      OPICD1
.
          DISPLAY   *P58:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmshcpaf"
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"viscodaf"
          OPEN      VISCODA1,"viscodaf"
.
          DISPLAY   *P64:24,"websecaf"
          OPEN      WEBSECA1,"websecaf"
.
          DISPLAY   *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TWO;*4,CONAME
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS                     *I-264529
          READ      CONTROLF,EIGHTY8;*242,PNSWRACE
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D
          READ      CONTROLF,NINTY7;*151,MRCNTDAD,*177,MRCNMORP

          READ      CONTROLF,TEN;*54,CFILENO,*235,CMDISP
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,TWENTY;*91,PTCNHSEM
          READ      CONTROLF,FIFTY9;*132,CALRDESC
.
          DISPLAY   *P58:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"     * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
.         Set Printer Stuff 
.
          CLOCK     TIME,CTIMEIS
          MOVE      CODED,DIM4
          LOAD      DIM4,PTCNHSEM,DISCHD
          MOVE      CODED,DIM9
          LOAD      DIM9,PTCNHSEM,DISCHD
          STRIP     DIM9
          MOVE      ONE,CNOUNDLN
INIT9999  RETURN
+
.
.*********************************************************************
.         OPTN0000 Get Main Program Option   Called By ML0000
.                  Display and get U/R or Adno option.
.                  Returns : OPTION  1 = Register Details
.                                    2 = Non Registered Details
.*********************************************************************
.
OPTN0000  CALL      DISPHEAD
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Cancer Registrations":
                    *P1:6,*V2LON," 2",*HOFF," = Cancer Details Not Registered":
                    *P1:7,*V2LON," 3",*HOFF," = Unsent Cancer Registrations";
.
OPTN1000  KEYIN     *P1:9," Select Option : ",*V2LON,*P18:9,OPTION;
.
          BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999
.
          IF        OPTION=ZERO
            GOTO      OPTN9999
          ENDIF
.
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
.
+
.*********************************************************************
.         KHOS000 Get Main Program Option   Called By ML0000
.*********************************************************************
KHOS000 COMPARE    ONE,IBCNMHOS
        GOTO       KHOS999 IF NOT EQUAL
.
        DISPLAY    *P1:15,*EL,"Hospital Id : ";
        MOVE       TEN5,ECOL
        MOVE       "15",EVERT
        MOVE       SP3,CKYIDEF3
        MOVE       ZERO,CKYIMAND           * Not Mandatory
        OPEN       PATHSPA1,"pathspaf"
.
        CALL       PATHSPKY
        BRANCH     EXIT,KHOS100
        GOTO       KHOS900
.
KHOS100 DISPLAY    *P18:15,"All Hospitals";
        MOVE       SP10,PTHSHOSP
        MOVE       "All Hospitals",PTHSNAME
.
KHOS900 MOVE       PTHSHOSP,HEADHOSP
        MOVE       PTHSNAME,NAMEHOSP
KHOS999 RETURN
+
.**************************************************************************
. Routine to call the apopriate loop routine to get the Cancer Registration
. Details.
.**************************************************************************
.
CREG0000  MOVE      ZERO,EXIT
          CALL      EDAT0000
          BRANCH    EXIT,CREG9999
.
          MATCH    ANSX,EXTRTYPE
          IF       @EQUAL
            CALL      XMLHDR00
          ENDIF
.  
          MOVE      ZERO,CLNO  
          MOVE      ZERO,CPAGENO
          CALL      HEDC0000               * Print a header
          MOVE      SP10,SAVURNO
.
          BRANCH    PTCNHSEM,CREG1000      * Using Discharge Date
.
          CALL      NCOD0000               * Use coding date
          GOTO      CREG9000
.
CREG1000  CALL      NDIS0000               * Use Discharge Date
.
CREG9000  PRINT     *1,"*** End of Report ***"
.
          MATCH    ANSX,EXTRTYPE
          IF       @EQUAL
            CALL      XMLHDE00
          ENDIF
.
CREG9999  RETURN
.
.---------------------------------------------------------------------------
. Write <CancerExtract> tag
.---------------------------------------------------------------------------
XMLHDR00  WRITE     EXTRFILE,SEQ;HED0001
          PACK      TAGNAME1,LESSERTH,EXTRCTHD,GREATRTH
          WRITE     EXTRFILE,SEQ;TAGNAME1
XMLHDR99  RETURN
.
.---------------------------------------------------------------------------
. Write </CancerExtract> tag
.---------------------------------------------------------------------------
XMLHDE00  PACK      TAGNAME1,LESSERTH,SLASH,EXTRCTHD,GREATRTH
          WRITE     EXTRFILE,SEQ;TAGNAME1
XMLHDE99  RETURN
+
.**************************************************************************
. Routine to call the appopriate loop routine to get the Cancer register 
. Details not entered.
.**************************************************************************
NREG0000  MOVE      ZERO,EXIT
          CALL      EDAT0000
          BRANCH    EXIT,NREG9999
.
          MOVE      ZERO,CLNO  
          MOVE      ZERO,CPAGENO
          CALL      HEDN0000
          MOVE      SP10,SAVURNO
.
          BRANCH    PTCNHSEM,NREG1000      * Using Discharge Date
.
          CALL      NCOD0000               * Use coding date
          GOTO      NREG9000
.
NREG1000  CALL      NDIS0000               * use Discharge Date
.
NREG9000  PRINT     *1,"*** End of Report ***"
.
NREG9999  RETURN
+
.****************************************************************************
.*  EXTR0000  :  Option for extraction                                      *
.****************************************************************************
EXTR0000  MOVE      ZERO,EXTRCTYN
          IF        OPTION = 2
            GOTO      EXTR9999              * don't create txt file for Option 2
          ENDIF
.
          MOVE      ANSN,ANS
          KEYIN     *P1:17,*EL,"Do you want to Extract Data (":
                    *DV,*V2LON,ANSY,*HOFF,"/",*V2LON,*DV,ANSN,*HOFF,") ? ":
                    *DV,ANS:
                    *P37:17,*RV,ANS;
          REP       UPPLOW,ANS
          CMATCH    ANSN,ANS
          GOTO      EXTR9999 IF EQUAL
          CMATCH    ANSY,ANS
          GOTO      EXTR0000 IF NOT EQUAL
          MOVE      ONE,EXTRCTYN
.
          COMPARE  SEVEN,PTCNHDPS             * check if TAS extract
          GOTO     EXTR2100 IF EQUAL
.                                                                    
EXTR1000  MOVE      SP70,ANS
          KEYIN     *P1:19,*B,*EL,"Extract Type ":
                    "(",*V2LON,"X-XML",*HOFF,",":
                    *V2LON,"A-ASCII",*HOFF,") : ":
                    *P33:19,*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      EXTR2000 IF EQUAL
.
          MATCH     ANSA,ANS
          GOTO      EXTR2000 IF EQUAL
.
          BEEP
          GOTO      EXTR1000
.
EXTR2000  MOVE      ANS,EXTRTYPE
.
EXTR2100  KEYIN     *P1:20,*EL,"Extract Filename : ":
                    *P21:20,*EL,*V2LON,*+,EXTRCTFN,".txt";
.
          REP       " _",EXTRCTFN                * Remove Spaces from File name
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EXTRFILE,EXTRCTFN
          TRAPCLR   IO
          BRANCH    OVRCD,EXTR4000
          CLOSE     EXTRFILE
.
EXTR3000  KEYIN     *P1:22,*B,*EL,"An extraction file with this name":
                    " already exists. Overwrite it (",*V2LON,*DV,ANSY:
                    *HOFF,"/",*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      EXTR4000 IF EQUAL       * Overwrite file

          MATCH     ANSN,ANS
          GOTO      EXTR9100 IF EQUAL       * Keyin filename again
.
          BEEP
          GOTO      EXTR3000
.
EXTR4000  PREP      EXTRFILE,EXTRCTFN
          MOVE      ZERO,EXIT
          GOTO      EXTR9999
.
EXTR9100  MOVE      ONE,EXIT
          GOTO      EXTR9999
.
EXTR9999  RETURN
+
.**************************************************************************
. Routine to get the from and to dates.
.
.**************************************************************************
.
EDAT0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:4,*EF,*P2:4,"From Date : ":
                    *P2:6,"To Date : ";
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
EDAT1000  MOVE      TEN4,CCOL
          MOVE      FOUR,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,EDAT1000
          PACK      FROMDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",FROMDATE
          MOVE      CDAY,ADD
          MOVE      CMON,AMM
          MOVE      CYEAR,AYY
          MOVE      CCENT,ACC
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
EDAT2000  MOVE      TEN4,CCOL
          MOVE      SIX,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,EDAT2000
          PACK      TODATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",TODATE
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          CALL      KHOS000                 * Keyin Hospital id
          MOVE      ZERO,EXIT
          CALL      EXTR0000                * option to extract
.
          CALL      CONTQST
          BRANCH    CEXIT,EDAT9999,EDAT9000,EDAT0000
.
EDAT9000  MOVE      ONE,EXIT
.
EDAT9999  RETURN
+
.**************************************************************************
.   Loop over files for coding date
.**************************************************************************
.
NCOD0000  MOVE      ZERO,EXIT
          MOVE      SP8,PREVADMN                                      *I-264529
.
          PACK      KEY16,ACC,AYY,AMM,ADD,SP20
          CALL      RDSMISS4
NCOD1000  CALL      RDKMISS4
          BRANCH    OVRCD,NCOD9000
.
          MATCH     ACODDTE,ENDDATE     * In Date range
          GOTO      NCOD9000 IF LESS
.
          UNPACK    ACODDTE,CCENT,CYEAR,CMON,CDAY
          PACK      THEDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          PACK      KEY8,AADMNO
          CALL      RDPMVX11                      * read visit extension file
          BRANCH    OVRCD,NCOD1000
.
          IF        IBCNMHOS=1
            MATCH       SP70,HEADHOSP
            IF          !@EQUAL
              MATCH       PMVXMHOS,HEADHOSP
              GOTO        NCOD1000 IF NOT EQUAL
            ELSE                                                      *I-264529
               PACK       KEY3,PMVXMHOS,SP70
               CALL       RDPTHSP1
            ENDIF
          ENDIF
.
          MOVE      ZERO,FOUNDCAN
          PACK      KEY16,AADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
NCOD2000  CALL      RKPTECD2
          BRANCH    OVRCD,NCOD1000
.
          MATCH     AADMNO,PTEDADMN
          GOTO      NCOD1000 IF NOT EQUAL
. 
.                                           * find the Primary Cancer
          IF        FOUNDCAN = 0
            PACK      KEY9,PTEDCODE         * Is diagnosis Cancer ?
            CALL      RDPTCAN1
            IF        OVRCD = 0
              MATCH     METC770,PTEDCODE    * is this a Primary Cancer ?
              IF        !@LESS
                MATCH     PTEDCODE,METC7990
                GOTO      NCOD2000 IF NOT LESS
              ENDIF
.
              MOVE      ONE,FOUNDCAN        * found a Primary, save data
              MOVE      PTEDADMN,SAVADMNO
              MOVE      PTEDCODE,SAVPDIAG 
              MOVE      PTEDDESC,DIAGDESC
              MOVE      PTEDDESC,DIAGD40
              PACK      SAVKEY16,DPTEDADM,DPTEDUNQ,DPTEDEPN,DPTEDCNT,SP20
            ENDIF
            GOTO      NCOD2000              * read next record
          ENDIF
.
          MOVE      SP10,PT0DACRQ           * is this a Morphology Code?
          PACK      KEY9,PTEDCODE,SP70
          CALL      RDPTICD1
          MATCH     "4",PT0DACRQ            * check for Morphology code
          GOTO      NCOD2000 IF NOT EQUAL
.                                           * Morphology found - save data
          MOVE      PTEDCODE,SAVMORPH
          MOVE      PTEDDESC,PATHDESC
          MOVE      PTEDDESC,PATHD40
          MOVE      ZERO,FOUNDCAN           * clear Primary switch for next pass
.
.
NCOD3000  CALL      CLRCRD00           * clear cancer details variables
          PACK      KEY25,SAVADMNO,SAVPDIAG,SP20    
          CALL      RSPTCRD1           * Get cancer registation record
          CALL      RKPTCRD1           * Get cancer registation record
          IF        OVRCD=0
            MATCH     SAVADMNO,PTCDDADN
            IF        !@EQUAL
              MOVE      ONE,OVRCD
            ELSE                            * set current Admiss. No.
              MOVE      SAVADMNO,CURRADMN                             *I-264529
            ENDIF
            MATCH     SAVPDIAG,PTCDPRMM
            IF        !@EQUAL
              MOVE      ONE,OVRCD
            ENDIF
          ENDIF
.
          BRANCH    OPTION,NCOD4000,NCOD5000,NCOD9999
.
NCOD4000  BRANCH    OVRCD,NCOD6000
.
          CALL      CPRT0000                * Print registration if exists
          CALL      CREXT000                * create Extract data     *I-264529
.
          IF        EXTRCTYN=1
            PACK      PTCDDTEX,CCC,CYY,CMM,CDD,SP10  * Extract sent date
            REP       " 0",PTCDDTEX
            CALL      UPPTCRD1              * update "Extract Sent" date field
          ENDIF
.
          GOTO      NCOD6000
.
NCOD5000  PERFORM   OVRCD,NPRT0000     * Print Primary Cancer if not registered
          GOTO      NCOD6000
.
NCOD6000  MOVE      SAVKEY16,KEY16
          CALL      RDPTECD2           * reposition on previous Primary Cancer
          MOVE      ZERO,FOUNDCAN
          GOTO      NCOD2000           * return to read next patecdaf record
.
.                                                                     *I-264529
NCOD9000  IF        PTCNHDPS = 7
            COMPARE   ONE,EXTRCTYN
            GOTO      NCOD9999 IF NOT EQUAL
            MATCH     SP9,TASADMN           * check if data still to be written
            IF        !@EQUAL
              CALL      WRTTAS00
              CALL      CLRTAS00
            ENDIF
          ENDIF
.
NCOD9999  RETURN
.
+
.*****************************************************************
. Rountine to loop over discharge file for the routine.
.*****************************************************************
.
NDIS0000  MOVE      ZERO,EXIT
          MOVE      SP8,PREVADMN                                      *I-264529
.
          PACK      KEY16,ACC,AYY,AMM,ADD,SP10
          CALL      RDSDSCH2
NDIS1000  CALL      RDKDSCH2          * Loop over discharges
          BRANCH    OVRCD,NDIS9000
.
          MATCH     DDATE,ENDDATE     * In date range
          GOTO      NDIS9000 IF LESS
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY 
          PACK      THEDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          PACK      KEY8,DADMNO
          CALL      RDPMVX11                      * read visit extension file
          BRANCH    OVRCD,NDIS1000
.
          IF        IBCNMHOS=1
            MATCH       SP70,HEADHOSP
            IF          @EQUAL
               PACK       KEY3,PMVXMHOS,SP70
               CALL       RDPTHSP1
            ELSE
              MATCH       PMVXMHOS,HEADHOSP
              GOTO        NDIS1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      ZERO,FOUNDCAN
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
NDIS2000  CALL      RKPTECD2
          BRANCH    OVRCD,NDIS1000
.
          MOVE      DADMNO,SAVADMNO
          MATCH     PTEDADMN,SAVADMNO
          GOTO      NDIS1000 IF NOT EQUAL
.
.                                           * find the Primary Cancer
          IF        FOUNDCAN = 0
            PACK      KEY9,PTEDCODE         * Is diagnosis Cancer ?
            CALL      RDPTCAN1
            IF        OVRCD = 0
              MATCH     METC770,PTEDCODE    * is this a Primary Cancer ?
              IF        !@LESS
                MATCH     PTEDCODE,METC7990
                GOTO      NDIS2000 IF NOT LESS
              ENDIF
.
              MOVE      ONE,FOUNDCAN        * found a Primary, save data
              MOVE      PTEDADMN,SAVADMNO
              MOVE      PTEDCODE,SAVPDIAG
              MOVE      PTEDDESC,DIAGDESC
              MOVE      PTEDDESC,DIAGD40
              PACK      SAVKEY16,DPTEDADM,DPTEDUNQ,DPTEDEPN,DPTEDCNT,SP20
            ENDIF
            GOTO      NDIS2000              * read next record
          ENDIF

          MOVE      SP10,PT0DACRQ           * is this a Morphology Code?
          PACK      KEY9,PTEDCODE,SP70
          CALL      RDPTICD1
          MATCH     "4",PT0DACRQ            * check for Morphology code
          GOTO      NDIS2000 IF NOT EQUAL
.                                           * Morphology found - save data
          MOVE      PTEDCODE,SAVMORPH
          MOVE      PTEDDESC,PATHDESC
          MOVE      PTEDDESC,PATHD40
          MOVE      ZERO,FOUNDCAN           * clear Primary switch for next pass
.
.
NDIS3000  CALL      CLRCRD00                * clear cancer details variables
          PACK      KEY25,SAVADMNO,SAVPDIAG,SP20   
          CALL      RSPTCRD1                * Get cancer registation record
          CALL      RKPTCRD1                * Get cancer registation record
          IF        OVRCD=0
            MATCH     SAVADMNO,PTCDDADN
            IF        !@EQUAL
              MOVE      ONE,OVRCD
            ELSE                            * set current Admiss. No.
              MOVE      SAVADMNO,CURRADMN                             *I-264529
            ENDIF
            MATCH     SAVPDIAG,PTCDPRMM
            IF        !@EQUAL
              MOVE      ONE,OVRCD
            ENDIF
          ENDIF
.
          BRANCH    OPTION,NDIS4000,NDIS5000,NDIS9999
.
NDIS4000  BRANCH    OVRCD,NDIS6000
.
          CALL      CPRT0000                * Print registration if exists
          CALL      CREXT000                * create Extract data     *I-264529
.
          IF        EXTRCTYN=1
            PACK      PTCDDTEX,CCC,CYY,CMM,CDD,SP10  * Extract sent date
            REP       " 0",PTCDDTEX
            CALL      UPPTCRD1              * update "Extract Sent" date field
          ENDIF
.
          GOTO      NDIS6000
.
NDIS5000  PERFORM   OVRCD,NPRT0000     * Print Primary Cancer if not registered
          GOTO      NDIS6000
.
NDIS6000  MOVE      SAVKEY16,KEY16
          CALL      RDPTECD2           * reposition on previous Primary Cancer
          MOVE      ZERO,FOUNDCAN
          GOTO      NDIS2000           * return to read next patecdaf record
.
.
NDIS9000  IF        PTCNHDPS = 7
            COMPARE   ONE,EXTRCTYN
            GOTO      NDIS9999 IF NOT EQUAL

            MATCH     SP9,TASADMN      * check if TAS data still to be written
            IF        !@EQUAL
              CALL      WRTTAS00
              CALL      CLRTAS00
            ENDIF
          ENDIF
.
NDIS9999  RETURN
.
+
.*****************************************************************
.* Clear Cancer Registration Details
.*****************************************************************
CLRCRD00  MOVE      ZEROVISN,PTCDDADN
          MOVE      ZERO,PTCDCNRG
          UNPACK    SP70,PTCDADTE,PTCDDDTE,PTCDTMRC,PTCDMTSP
          MOVE      ZERO,PTCDUKPR
          UNPACK    SP70,PTCDPRMM,PTCDHTYP,PTCDMET1,PTCDMET2,PTCDMET3
          UNPACK    SP70,PTCDMET4,PTCDMET5,PTCDMET6 
          UNPACK    SP70,PTCDLATR,PTCDSTAG
          UNPACK    SP70,PTCDBT01,PTCDBT02,PTCDBT03,PTCDBT04,PTCDBT05
          UNPACK    SP70,PTCDBT06,PTCDBT07,PTCDBT08,PTCDBT09,PTCDBT10
          MOVE      ZERO,PTCDRFLG
          UNPACK    SP70,PTCDOTDG,PTCDPATH,PTCDDGPR
          PACK      PTCDFCPR,SP70,SP70,SP70,SP70
          UNPACK    SP70,PTCDBO01,PTCDBO02,PTCDBO03,PTCDBO04,PTCDBO05
          UNPACK    SP70,PTCDBO06,PTCDBO07,PTCDBO08,PTCDBO09,PTCDDGOT
          MOVE      ZERO,PTCDAUTP
          MOVE      ZERO,PTCDBRLW
          MOVE      ZERO,PTCDBTSZ
          MOVE      ZERO,PTCDGLSC
          UNPACK    SP70,PTCDMLIN,PTCDRPLB,PTCDCKLV,PTCDCOMM         
          UNPACK    SP70,PTCDDTEX,PTCDDTRC,PTCDTMCR,PTCDUSCR         
          UNPACK    SP70,PTCDDTUP,PTCDTMUP,PTCDUSUP,PTCDBSDG         
.
          MOVE      SP70,PTCDCELT
          MOVE      SP70,PTCDDOCC
          MOVE      SP70,PTCDDOCN
          MOVE      SP70,PTCDNTDT
          MOVE      SP70,PTCDSNT1
          MOVE      SP70,PTCDSNT2
          MOVE      SP70,PTCDSNT3
          MOVE      SP70,PTCDBOD1
          MOVE      SP70,PTCDBOD2
          MOVE      SP70,PTCDBOD3
          MOVE      SP70,PTCDBOD4
          MOVE      SP70,PTCDBOD5
          MOVE      SP70,PTCDBOL5
          MOVE      SP70,PTCDBOD6
          MOVE      SP70,PTCDBOL6
          MOVE      SP70,PTCDBOD7
          MOVE      SP70,PTCDBOL7
          MOVE      SP70,PTCDGD01
          MOVE      SP70,PTCDGD02
          MOVE      SP70,PTCDGD03
          MOVE      SP70,PTCDGD04
          MOVE      SP70,PTCDGD05
          MOVE      SP70,PTCDGD06
          MOVE      SP70,PTCDGD07
          MOVE      SP70,PTCDGD08
          MOVE      SP70,PTCDGD09
          MOVE      SP70,PTCDPLOD
          MOVE      SP70,PTCDCOD1
          MOVE      SP70,PTCDCOD2
          MOVE      SP70,PTCDCSGT
          MOVE      SP70,PTCDCSGN
          MOVE      SP70,PTCDCSGM
          MOVE      SP70,PTCDCSSG
          MOVE      SP70,PTCDCSCM
          MOVE      SP70,PTCDTNON
          MOVE      SP70,PTCDTSUR
          MOVE      SP70,PTCDTSDT
          MOVE      SP70,PTCDTRAD
          MOVE      SP70,PTCDTRDT
          MOVE      SP70,PTCDTCHM
          MOVE      SP70,PTCDTCDT
          MOVE      SP70,PTCDTHOR
          MOVE      SP70,PTCDTHDT
          MOVE      SP70,PTCDTBIO
          MOVE      SP70,PTCDTBDT
          MOVE      SP70,PTCDRLDT
          MOVE      SP70,PTCDNDCL
          MOVE      SP70,PTCDTSAB
          MOVE      SP70,PTCDTOTH
          MOVE      SP70,PTCDTHOS
          MOVE      SP70,PTCDRST1
          MOVE      SP70,PTCDRST2
          MOVE      SP70,PTCDRST3
          MOVE      SP70,PTCDRST4
          MOVE      SP70,PTCDRST5
          MOVE      SP70,PTCDSURB
          MOVE      SP70,PTCDSTTE
          MOVE      SP70,PTCDPOST
          MOVE      SP70,PTCDMET4
          MOVE      SP70,PTCDMET5
          MOVE      SP70,PTCDMET6
          MOVE      SP70,PTCDECOG
          MOVE      SP70,PTCDRADI
          MOVE      SP70,PTCDSYST
          MOVE      SP70,PTCDSCFL
          MOVE      SP70,PTCDCSSY
          MOVE      SP70,PTCDBDIA
          MOVE      SP70,PTCDSPAR
.
CLRCRD99  RETURN
.*****************************************************************
.* Print the Cancer Details Report Records
.*****************************************************************
.
CPRT0000  CALL      GETC0000          * Gets all the Info
          BRANCH    EXIT,CPRT9999
.
          MATCH     PURNO,SAVURNO     * Check if same U/R print PMI is NO
          IF        !@EQUAL
            MOVE      PACFNAME,KEY28
            PRINT     *1,"| U/R Number",*18,": ",PURNO,*34,"Admn No.":
.                     *34,"Admn No.",*43,": ",DAADMNO:
                      *43,": ",DAADMNO:
                      *54,"| Diagnosis",*76,": ",DIAGDESC,*132,"|" 
            PRINT     *1,"| Name",*18,": ",KEY28,*54,"| Pathology",*76,": ":
                    PATHDESC,*132,"|"
            PRINT     *1,"| Date of Birth",*18,": ",DOB,*54,"| ",*132,"|"
            IF        PTCNHDPS=3
              PRINT     *1,"| Sex At Birth",*18,": ",PSEX,*54,"| ",*132,"|"
            ELSE
              PRINT     *1,"| Sex",*18,": ",PSEX,*54,"| ",*132,"|"
            ENDIF
            PRINT     *1,"| Admission Date : ",ADMDATE,*32,"(",PTHSHOSP:
                            ")",*54,"| ",*132,"|"
            PRINT     *1,"| ",DIM9," Date",*18,": ",THEDATE,*54,"| ",*132,"|"
          ELSE
            PRINT     *1,"|",*54,"| Diagnosis",*76,": ",DIAGDESC,*132,"|" 
            PRINT     *1,"|",*54,"| Pathology",*76,": ",PATHDESC,*132,"|"
          ENDIF
          CALL      UND132
          ADD       SEVEN,CLNO  
          COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
          CALL      HEDC0000 IF LESS 
          CALL      HEDC0000 IF EQUAL
          MOVE      PURNO,SAVURNO
.
CPRT9999  RETURN
.
+
.*****************************************************************
.* Routine to print cancer details not registered.
.*****************************************************************
.
NPRT0000  CALL      GETN0000         * Get Info
          BRANCH    EXIT,NPRT9999 
.
.                                    * not registered, so can't extract
.    
          IF        PTCNHDPS=3
            MATCH     PURNO,SAVURNO    * Check same U/R print PMI if Not
            IF        !@EQUAL
              MOVE      PACFNAME,KEY28
              PRINT     *1,"| ",PURNO,*14,"| ",KEY28,*44,"| ",DOB,*57:
                        "|  ",PSEX,"   | ",ADMDATE," | ",THEDATE," | ":
                        DIAGD40,*132,"|"
              PRINT     *1,"|",*14,"|",*44,"|  ",*57,"|",*63," |",*77,"|":
                        *90,"| ",PATHD40,*132,"|"
            ELSE
              PRINT     *1,"|",*14,"|",*44,"|",*57,"| ",*64,"|",*77,"|":
                        *90,"| ",DIAGD40,*132,"|"
              PRINT     *1,"|",*14,"|",*44,"|",*57,"| ",*64,"|",*77,"|":
                        *90,"| ",PATHD40,*132,"|"
            ENDIF          
          ELSE
            MATCH     PURNO,SAVURNO    * Check same U/R print PMI if Not
            IF        !@EQUAL
              MOVE      PACFNAME,KEY28
              PRINT     *1,"| ",PURNO,*14,"| ",KEY28,*44,"| ",DOB,*57:
                        "|  ",PSEX,"  | ",ADMDATE," | ",THEDATE," | ":
                        DIAGD40,*132,"|"
              PRINT     *1,"|",*14,"|",*44,"|",*57,"|",*63,"|",*76,"|":
                        *89,"| ",PATHD40,*132,"|"
            ELSE
              PRINT     *1,"|",*14,"|",*44,"|",*57,"|",*63,"|",*76,"|":
                        *89,"| ",DIAGD40,*132,"|"
              PRINT     *1,"|",*14,"|",*44,"|",*57,"|",*63,"|",*76,"|":
                        *89,"| ",PATHD40,*132,"|"
            ENDIF
          ENDIF
.
          CALL      UND132
          ADD       THREE,CLNO  
          COMPARE   CLNO,FIFTY5  
          CALL      HEDN0000 IF LESS
          CALL      HEDN0000 IF EQUAL
          MOVE      PURNO,SAVURNO
.
NPRT9999  RETURN
.
+
.*****************************************************************
.* Print a cancer details Header
.*****************************************************************
HEDC0000  CALL      HEAD132
.
          PRINT     *30,"Cancer Register Details"
          PRINT     *30,"From ",DIM9," Date : ",FROMDATE," To : ",TODATE
.
          IF        IBCNMHOS =1
            PRINT     *30,"Hospital : ",HEADHOSP,"    ",NAMEHOSP
            MOVE      SIX,CLNO
          ELSE
            MOVE      FIVE,CLNO  
          ENDIF
          CALL      UND132
HEDC9999  RETURN
.
.*****************************************************************
.* Print a cancer details Header
.*****************************************************************
HEDU0000  CALL      HEAD132
.
          PRINT     *30,"Unsent Cancer Registration Details"
.
          IF        IBCNMHOS =1
            PRINT     *30,"Hospital : ",HEADHOSP,"    ",NAMEHOSP
            MOVE      FIVE,CLNO
          ELSE
            MOVE      FOUR,CLNO
          ENDIF
          CALL      UND132
HEDU9999  RETURN
.
+
.*****************************************************************
.* Print a cancer details not registered Header
.*****************************************************************
HEDN0000  CALL      HEAD132
.
          PRINT     *30,"Cancer Details Not Registered"
          PRINT     *30,"From ",DIM9," Date : ",FROMDATE," To : ",TODATE
          IF        IBCNMHOS =1
            PRINT     *30,"Hospital : ",HEADHOSP,"    ",NAMEHOSP
            MOVE      EIGHT,CLNO
          ELSE
            MOVE      SEVEN,CLNO  
          ENDIF
.
          CALL      UND132
          IF        PTCNHDPS=3
            PRINT     *1,"| U/R Number | Name",*44,"|   D.O.B    |Sex At| ":
                      "Adm. Date  | ",DIM4," Date  | Diagnosis",*132,"|"
            PRINT     *1,"|            |",*44,"|",*57,"|Birth |",*77,"|",*90:
                      "| Pathology",*132,"|"
          ELSE
            PRINT     *1,"| U/R Number | Name",*44,"|   D.O.B    | Sex | ":
                      "Adm. Date  | ",DIM4," Date  | Diagnosis",*132,"|"
            PRINT     *1,"|            |",*44,"|",*57,"|     |",*76,"|",*89:
                      "| Pathology",*132,"|"
          ENDIF
          
          CALL      UND132
.
HEDN9999  RETURN
.
+
.*****************************************************************
.* Get details for for cancer register
.*****************************************************************
GETC0000  MOVE      ZERO,EXIT
.
          PACK      KEY8,SAVADMNO,SP10
          CALL      RDMISS1            * Get admission details
          BRANCH    OVRCD,GETC9000
          UNPACK    ADATE,ACC,AYY,AMM,ADD
          PACK      ADMDATE,ADD,SLASH,AMM,SLASH,ACC,AYY
.
          PACK      KEY8,SAVADMNO,SP10
          CALL      RDPTCRG1            * Get register header
          BRANCH    OVRCD,GETC9000
.
          PACK      KEY8,PTCRURNO,SP10
          CALL      RDMAST1            * Get PMI Details
          BRANCH    OVRCD,GETC9000
          CALL      RDPMPX21
.
          UNPACK    PBDATE,ACC,AYY,AMM,ADD
          PACK      DOB,ADD,SLASH,AMM,SLASH,ACC,AYY
          MOVE      TWO,PACFRMT   
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
.
GETC2000  PACK      BASEDESC,SP20,SP20
          PACK      STAGDESC,SP20,SP20
          PACK      INVADESC,SP20,SP20
          PACK      LABDESC,SP20,SP20
.
GETC6000  GOTO      GETC9999
.
GETC9000  MOVE      ONE,EXIT
.
GETC9999  RETURN
.
+
.*****************************************************************
.* Get details for unregistered Cancer code
.*****************************************************************
GETN0000  MOVE      ZERO,EXIT
.
          PACK      KEY8,SAVADMNO,SP10
          CALL      RDVISA1            * Get visit details
          BRANCH    OVRCD,GETN9000
.
          PACK      KEY8,SAVADMNO,SP10
          CALL      RDMISS1            * Get admission details
          BRANCH    OVRCD,GETN9000
          UNPACK    ADATE,ACC,AYY,AMM,ADD
          PACK      ADMDATE,ADD,SLASH,AMM,SLASH,ACC,AYY
.
          PACK      KEY8,PVIURNO,SP10
          CALL      RDMAST1            * Get PMI Details
          BRANCH    OVRCD,GETN9000
          CALL      RDPMPX21
.
          UNPACK    PBDATE,ACC,AYY,AMM,ADD
          PACK      DOB,ADD,SLASH,AMM,SLASH,ACC,AYY
          MOVE      TWO,PACFRMT   
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
.
          MOVE      SAVMORPH,ANS
          MATCH     CODEM,ANS          * Check pathology code
          IF        !@EQUAL
            PACK      PATHD40,SP20,SP20
            GOTO      GETN1000
          ENDIF
.
          GOTO      GETN9999
.
GETN1000  CALL      RPPTECD2
          GOTO      GETN9999
.
GETN9000  MOVE      ONE,EXIT
.
GETN9999  RETURN
+
.------------------------------------------------------------
.  Create Extract data and write to output file
.------------------------------------------------------------
.
CREXT000  COMPARE   ONE,EXTRCTYN            * check Extract flag
          GOTO      CREXT999 IF NOT EQUAL
.
          COMPARE   SEVEN,PTCNHDPS          * check TAS state code
          GOTO      CREXT070 IF EQUAL
.
.                                           * default State format - VIC
CREXT040  MOVE      ZERO,EXIT
          CALL      INTEXT00                * initialise extract fields
          CALL      SETEXT00                * setup extract fields
          CALL      WRTEXT00                * write to extract file - ASCII
          CALL      WRTEXX00                * write to extract file - XML
          GOTO      CREXT999
.
.
CREXT070  MOVE      ZERO,EXIT               * TAS (7) Cancer Reg Extract
          MATCH     SP8,PREVADMN
          IF        @EQUAL
            CALL      CLRTAS00              * clear vars for first Admission
            MOVE      CURRADMN,PREVADMN
          ENDIF
              
          MATCH     PREVADMN,CURRADMN
          IF        @EQUAL
            CALL      SETTAS00              * setup TAS extract data fields
          ELSE
            CALL      WRTTAS00              * write to TAS extract file
            CALL      CLRTAS00              * clear vars for next Admission
            MOVE      CURRADMN,PREVADMN
            CALL      SETTAS00              * setup extract data fields
          ENDIF
          GOTO      CREXT999
.
.
CREXT999  RETURN
.------------------------------------------------------------
. Initialise variables
.------------------------------------------------------------
INTEXT00  PACK     SP200,SP70,SP70,SP70
          PACK     SP250,SP70,SP70,SP70,SP70

          MOVE     SP70,XPSNAME
          UNPACK   SP70,XPGNAM1,XPGNAM2
          UNPACK   SP70,XPBDATE,XPATSEX,XPRALIAS,XPMEDI
          MOVE     SP70,XINDHEID
.         UNPACK   SP70,XABORIGN,XMARSTAT,XADDRESS
          UNPACK   SP70,XABORIGN,XMARSTAT         
          UNPACK   SP200,XADDRESS                 
          UNPACK   SP70,XPSUBURB,XPOSTCOD,XCNBIRTH,XLNGHOME
          UNPACK   SP70,XTDSNAME
          UNPACK   SP70,XTDGNAM1,XTDGNAM2
.         UNPACK   SP70,XTDADDRS
          UNPACK   SP250,XTDADDRS
          UNPACK   SP70,XHCAMPUS,XHOSCODE,XURNUMBER
          UNPACK   SP70,XADMDATE,XDIADATE,XCANCDIA
          MOVE     "0",XESTDATE                           * reinsert  *C-208570
.         PACK     XWDIAGNS,SP70,SP20
          PACK     XWDIAGNS,SP250
          UNPACK   SP70,XDISDATE,XVITSTAT,XPRIMCAN
          PACK     XICDPRI1,SP70
          PACK     XPRSTEXT,SP70,SP20
          PACK     XMETFLG1,SP20                                      *I-208570
          PACK     XMETCOD1,SP70,SP20
          PACK     XMETCOD2,SP70,SP20
          PACK     XMETCOD3,SP70,SP20
          PACK     XMETDES1,SP70,SP20                                 *I-208570
          PACK     XMORPHC1,SP70,SP20
          PACK     XMORPHC2,SP70,SP20
          PACK     XMORPDES,SP70,SP20
          MOVE     SP70,XHISGRAD
          UNPACK   SP70,XLATERAL,XBASDIAG,XBATDIAG,XTUMDATE
          UNPACK   SP70,XBASDESC                                      *I-217537
          UNPACK   SP70,XMETDATE,XDEATHDT,XAUTOPSY
.         UNPACK   SP70,XLMSNAME,XLMOADDS
          UNPACK   SP70,XLMSNAME         
          UNPACK   SP250,XLMOADDS
          UNPACK   SP70,XLMGNAM1,XLMGNAM2
          MOVE     SP70,XCANNOTE
          UNPACK   SP70,XCOMPDAT,XDEGSPRD,XECOGSTA
          PACK     XSPRDIN1,SP70,SP70
          PACK     XSPRDIN2,SP70,SP70
          PACK     XSPRDIN3,SP70,SP70
          UNPACK   SP70,XTRDCPRV,XSTGCFLG,XSTGCDIA,XSTGCSYS,XLCGPPRV
          UNPACK   SP70,XSTGCSGT,XSTGCSGN,XSTGCSGM
.
          PACK     XADDRESS,SP200     
          PACK     XTDADDRS,SP250     
          PACK     XWDIAGNS,SP250      
.
          MOVE     ZERO,DISC2018    * set flag for Discharged after 01/07/2018
INTEXT99  RETURN
+
.------------------------------------------------------------
. Setup variables
.------------------------------------------------------------
SETEXT00  MOVE     ZERO,DISC2018
          IF       ASTAT=3
            MOVE     AADMNO,KEY8
            CALL     RDDSCH1
            IF       OVRCD=0
              MOVE     DDATE,ICDRDDTE       * Used for reading ICD Disease file
              UNPACK   DDATE,XCENT,XYEAR,XMON,XDAY
              PACK     XDISDATE,XDAY,XMON,XCENT,XYEAR   * Discharged date
.
              MATCH     "20180701",DDATE
              IF        !@LESS
                MOVE      ONE,DISC2018    * Discharged after 01/07/2018
              ENDIF
            ENDIF
          ENDIF
.
          PACK     XPSNAME,PSNAME,SP70
          MOVE     PGNAME,KEY25
          CALL     GNAM0000                 * Get given names
          MOVE     KEY30,XPGNAM1
          MOVE     KEY30A,XPGNAM2
          UNPACK   PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK     XPBDATE,XDAY,XMON,XCENT,XYEAR,SP20
.
          MOVE     PSEX,XPATSEX
          IF       DISC2018=1
            MATCH    SP70,PSEX
            IF       !@EQUAL
              PACK     KEY5,ANSG,SP1,PSEX,SP70
              CALL     RDCODE1
              IF       OVRCD = 0
                MOVE     TCINDC8,XPATSEX    * Indicator 8 - Sex at Birth
              ENDIF
            ENDIF
          ELSE
            REP      "M1F2I3R3U9",XPATSEX
          ENDIF           
.
          SQUEEZE  PTMXMCCD
          PACK     XPMEDI,PMEDI,PTMXMCCD
.
          UNPACK   SP10,KEY2,KEY3,XABORIGN
          LOAD     KEY2,PNSWRACE,CODEP1,CODEP2,CODEP3,CODEP4,CODEP5,CODEP6:
                                 CODEU1,CODEU2,CODEU3,CODEU4,CODEU5,CODEU6:
                                 CODEU7,CATVA
          LOAD     KEY3,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5,PUSR6:
                                 AUSR1,AUSR2,AUSR3,AUSR4,AUSR5,AUSR6,AUSR7:
                                 PMVXABRG
          MATCH    SP3,KEY3
          IF       !@EQUAL
            MOVE     SP10,THCSCOD
            PACK     KEY5,KEY2,KEY3
            CALL     RDCODE1
            IF       OVRCD = 0
.                                                           * start   *C-208570
              UNPACK   THCSCOD,DIM1A,DIM1B  * A= prev year, B= curr year
              MOVE     ZERO,F1
              MOVE     DIM1B,F1
              IF        F1=1 | F1=2 | F1=3 | F1=4 | F1=8 | F1=9
                MOVE      DIM1B,XABORIGN    * Aboriginal  *
              ENDIF
.                                                           * end     *C-208570
            ENDIF
          ENDIF
.
          PACK     KEY5,ANSM,SP1,PMSTAT
          CALL     RDCODE1
          IF       OVRCD=0
            UNPACK   THCSCOD,XMARSTAT       * marital status
          ENDIF
.
.         PACK     XADDRESS,PADD1           * address
          PACK     XADDRESS,PADD1,PADD2     * address
          PACK     XPSUBURB,PSUBRB
          PACK     XPOSTCOD,PPOST
.
          CALL     PCOS0000                 * Post code conversion for OS
.
          PACK     KEY5,ANSC,SP1,PCONT
          CALL     RDCODE1
          IF       OVRCD=0
            UNPACK   PTCDNHCP,XCNBIRTH      * Country of birth        *I-208570
          ENDIF
.
          MATCH    SP70,PMVXLNG1
          IF       @EQUAL
            PACK     KEY5,ANSL,ANSA,PMPXLNG1,SP70     * Report PMI value
            CALL     RDCODE1
            IF       OVRCD=0
              PACK     XLNGHOME,PTCDNHCP    * Language
            ENDIF
          ELSE
            PACK     KEY5,ANSL,ANSA,PMVXLNG1,SP70
            CALL     RDCODE1
            IF       OVRCD=0
              PACK     XLNGHOME,PTCDNHCP    * Language                *I-283509
            ENDIF
          ENDIF
.
          PACK     KEY6,ADOCTA              * attending doctor
          CALL     RDDOCT1
          IF       OVRCD=0
            PACK     XTDSNAME,DSNAME        * surname
            MOVE     DGNAME,KEY25           * given name
            CALL     GNAM0000
            MOVE     KEY30,XTDGNAM1
            MOVE     KEY30A,XTDGNAM2
            CALL     SADD0000               * setup address
          ENDIF 
.
          MATCH    SP70,ADOCTA
          IF       !@EQUAL
            PACK     KEY10,ADOCTA,SP70
            CALL     RDPMHCP1
            IF       OVRCD=0
              PACK     XTRDCPRV,PMHCPRV1,SP70
            ENDIF 
          ENDIF
.
          MATCH    SP70,PMVXRHC1
          IF       !@EQUAL
            PACK     KEY10,PMVXRHC1,SP70
            CALL     RDPMHCP1
            IF       OVRCD=0
              PACK     XLCGPPRV,PMHCPRV1,SP70
            ENDIF
          ENDIF
.
          PACK     XHCAMPUS,CONAME          * hospital name
          PACK     XHOSCODE,CFILENO         * hospital file No. (not Id.)
          IF       IBCNMHOS=1
            PACK     XHCAMPUS,PTHSNAME,SP70
            PACK     XHOSCODE,PTHSHDPC,SP70 * (Health Department code)
          ELSE
            OPEN     PATHSPA1,"pathspaf"
            PACK     KEY3,PMVXMHOS,SP70
            CALL     RDPTHSP1
            IF       OVRCD=0
              MATCH    SP70,PTHSHDPC
              IF       !@EQUAL
                PACK     XHCAMPUS,PTHSNAME,SP70
                PACK     XHOSCODE,PTHSHDPC,SP70 * (Health Department code)
              ENDIF
            ENDIF
          ENDIF
          PACK     XURNUMBR,PURNO           * U/R number
          SQUEEZE  XURNUMBR                                           *I-146004
          UNPACK   ADATE,XCENT,XYEAR,XMON,XDAY
          PACK     XADMDATE,XDAY,XMON,XCENT,XYEAR   * Admission date
.
          UNPACK   PTCDDDTE,XCENT,XYEAR,XMON,XDAY
          PACK     XDIADATE,XDAY,XMON,XCENT,XYEAR   * Diagnosis date
.
          MATCH    "1",PTCDESTM
          IF       @EQUAL
            MOVE     "1",XESTDATE           * Estimate date flag
          ELSE
            MOVE     "0",XESTDATE           *            back to "0"  *C-208570
          ENDIF
.
          MOVE     PTCDDGPR,F1              * Cancer diagnosis prior to adm.
          IF       F1=0
            MOVE    ANSN,XCANCDIA
          ELSE
            IF       F1=1
              MOVE     ANSY,XCANCDIA
            ELSE
              MOVE     ANSU,XCANCDIA
              MOVE     "1",XESTDATE         * Estimate date flag
            ENDIF
          ENDIF

          PACK     XWDIAGNS,PTCDFCPR        * Where ?
.
. Vic stores this as a textrea of 80 character blocks. Strip white space
. from each line and join back together with one space between lines.
.
          IF       PTCNHDPS = 3
            PACK     DIM80,SP70,SP70
            PACK     DIM80A,SP70,SP70
            PACK     DIM80B,SP70,SP70
            PACK     DIM80C,SP70,SP70
            PACK     DIM80D,SP70,SP70
            UNPACK   PTCDFCPR,DIM80,DIM80A,DIM80B,DIM80C,DIM80D
            STRIP    DIM80
            STRIP    DIM80A
            STRIP    DIM80B
            STRIP    DIM80C
            STRIP    DIM80D
            PACK     XWDIAGNS,DIM80,SP1,DIM80A,SP1,DIM80B,SP1:
                              DIM80C,SP1,DIM80D
            PACK     DIM80,SP70,SP70
            PACK     DIM80A,SP70,SP70
            PACK     DIM80B,SP70,SP70
            PACK     DIM80C,SP70,SP70
            PACK     DIM80D,SP70,SP70
          ENDIF
.
          MOVE     ANSA,XVITSTAT            * Alive
          IF       PCEASE=1
            MOVE     ANSD,XVITSTAT          * Deceased
            UNPACK   PDECDTE,XCENT,XYEAR,XMON,XDAY
            PACK     XDEATHDT,XDAY,XMON,XCENT,XYEAR     * Date of death
            IF       PAUTOPY=0
              MOVE     ANSN,XAUTOPSY
            ELSE
              MOVE     ANSY,XAUTOPSY
            ENDIF
          ENDIF
.
.                                 * now process Cancer Reg data record ( read in
.                                 * UCAN0000/NCOD0000/NDIS0000)
          MOVE     PTCDPRMM,KEY9
          CALL     STCOD000                 * remove special chars from Code
          PACK     XICDPRI1,KEY9            * primary cancer
          MATCH    SP10,PTCDPRMM
          IF       !@EQUAL
            MOVE     ZERO,F3
            MOVE     PTCRMULT,F3
            IF       F3 > ZERO
              PACK     XPRIMCAN,PTCRMULT    * Total number of Primary Cancers
            ELSE
              MOVE     "1",XPRIMCAN
            ENDIF
          ENDIF
.
          PACK     KEY5,ANSL,ANSY,PTCDLATR       * Laterality
          CALL     RDCODE1
          IF       OVRCD=0
            UNPACK    THCSCOD,XLATERAL,DIM5      * Laterality
          ENDIF
.
          PACK     DDESC,SP70,SP20
          MOVE     PTCDPRMM,KEY9
          CALL     OPICD1
          CALL     RDPTICD1
          MOVE     DDESC,XPRSTEXT           * Primary site text description

.         ICD Metastatic codes
.
          MOVE     "N",XMETFLG1                                       *I-208570
          PACK     DIM150,SP70,SP70,SP10                              *I-208570
          CLEAR    DIM150                                             *I-208570
          CLEAR    XMETCOD1
.
SETEXT20  MOVE     ZERO,FORM1                          * start        *C-208570
          MOVE     ZERO,ADDDOT
          REPEAT
            ADD      ONE,FORM1
            LOAD     KEY9,FORM1,PTCDMET1,PTCDMET2,PTCDMET3,PTCDMET4,PTCDMET5:
                                PTCDMET6
            MATCH    SP9,KEY9
            IF       !@EQUAL
              UNPACK   SP70,DDESC           * Metastatic site description
              CALL     OPICD1
              CALL     RDPTICD1
              STRIP    DDESC
              IF       ADDDOT = 1
                APPEND   ". ",DIM150
              ENDIF
              APPEND   DDESC,DIM150         * append description
              MOVE     ONE,ADDDOT
.                                           * Metastatic code
              CALL     STCOD000             * remove "." & "/"s
              APPEND   KEY9,XMETCOD1
              APPEND   SP1,XMETCOD1
            ENDIF
          UNTIL      FORM1 = 6
.
          RESET    DIM150
          PACK     XMETDES1,DIM150,SP70
          MATCH    SP10,XMETDES1
          IF       !@EQUAL
            MOVE     "Y",XMETFLG1
          ENDIF
.
          CALL     GMORP000                 * get morphology codes
.
.                                           * Histological Grade
          MOVE     PTCDCELT,XHISGRAD
.
.                                           * Basis of Diagnosis
          CLEAR    XBASDIAG
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBT08,FORM1
          IF       FORM1=ONE
            APPEND   "8 ",XBASDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBT07,FORM1
          IF       FORM1=ONE
            APPEND   "7 ",XBASDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBT06,FORM1
          IF       FORM1=ONE
            APPEND   "6 ",XBASDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBT05,FORM1
          IF       FORM1=ONE
            APPEND   "5 ",XBASDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBT04,FORM1
          IF       FORM1=ONE
            APPEND   "4 ",XBASDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBT03,FORM1
          IF       FORM1=ONE
            APPEND   "3 ",XBASDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBT02,FORM1
          IF       FORM1=ONE
            APPEND   "2 ",XBASDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBT01,FORM1
          IF       FORM1=ONE
            APPEND   "1 ",XBASDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBT09,FORM1
          IF       FORM1=ONE
            APPEND   "9 ",XBASDIAG
            PACK     XBASDESC,PTCDOTDG                                *I-217537
          ENDIF
.
          RESET    XBASDIAG
          STRIP    XBASDIAG                                           *I-146004
          RESET    XBASDIAG
.
.                                           *  Basis of diagnosis other facility
          CLEAR    XBATDIAG
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBO08,FORM1
          IF       FORM1=ONE
            APPEND   "8 ",XBATDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBO07,FORM1
          IF       FORM1=ONE
            APPEND   "7 ",XBATDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBO06,FORM1
          IF       FORM1=ONE
            APPEND   "6 ",XBATDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBO05,FORM1
          IF       FORM1=ONE
            APPEND   "5 ",XBATDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBO04,FORM1
          IF       FORM1=ONE
            APPEND   "4 ",XBATDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBO03,FORM1
          IF       FORM1=ONE
            APPEND   "3 ",XBATDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBO02,FORM1
          IF       FORM1=ONE
            APPEND   "2 ",XBATDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBO01,FORM1
          IF       FORM1=ONE
            APPEND   "1 ",XBATDIAG
          ENDIF
.
          MOVE     ZERO,FORM1
          MOVE     PTCDBO09,FORM1
          IF       FORM1=ONE
            APPEND   "9 ",XBATDIAG
          ENDIF
.
          RESET    XBATDIAG
          STRIP    XBATDIAG
          RESET    XBATDIAG
.                                           *   end of addition       *I-146004
.
          PACK     XTUMDATE,PTCDTMRC        * Date of tumor recurrence
          PACK     XMETDATE,PTCDMTSP        * Date of metastatic spread
.
.         PACK     XLMOADDS,SP70            * LMO address
          PACK     XLMOADDS,SP250            * LMO address
          CALL     LOGP0000                  * Set LMO name
.
          PACK     KEY10,PTCDUSCR,SP70
          CALL     RDWBSE1
          IF       OVRCD=1
            MOVE     PTCDUSCR,WBSENAM
          ENDIF
          MOVE    WBSENAM,XCANNOTE          * Person completing screen
.
          MATCH    SP8,PTCDDTRC             * Date screen completed
          IF       !@EQUAL
            UNPACK   PTCDDTRC,DCC,DYY,DMM,DDD                         *I-146004
            PACK     XCOMPDAT,DDD,DMM,DCC,DYY                         *I-146004
          ENDIF
.
          PACK     KEY5,ANSE,ANSO,PTCDECOG,SP70
          CALL     RDCODE1
          IF       OVRCD=0
            MOVE     THCSCOD,XECOGSTA       * ECOG Performance Status *I-283509
          ENDIF
.
          CLEAR     XDEGSPRD
          PACK      KEY2,ZERO,TWO           * viscod link type = cancer reg
          PACK      D2,ANSS,ANSZ            * category SZ
          PACK      KEY30,PTCDDADN,PTCDPRMM,PTCDCNRG,SP70  * cancer reg key
          PACK      KEY37,KEY2,KEY30,D2,SP70
          CALL      RSVSCOD1
SETEXT50  CALL      RKVSCOD1                * get degree of spread
          BRANCH    OVRCD,SETEXT60
.
          MATCH     KEY2,VSCOTYPE           * Correct link type
          GOTO      SETEXT50 IF NOT EQUAL
.
          MATCH     KEY30,VSCOTKEY          * Correct link key
          GOTO      SETEXT50 IF NOT EQUAL
.
          MATCH     D2,VSCOCATG             * Correct link category
          GOTO      SETEXT50 IF NOT EQUAL
.
          PACK      KEY5,D2,VSCOCODE,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      THCSCOD,D1
            APPEND    D1,XDEGSPRD           * degree of spread        *I-283509
            APPEND    SP1,XDEGSPRD
          ENDIF
.
          MATCH     SP70,VSCOTEXT
          IF        !@EQUAL
            UNPACK    VSCOTEXT,XSPRDIN1,XSPRDIN2,XSPRDIN3
          ENDIF
.
          GOTO      SETEXT50
.
SETEXT60  MOVE      "N",XSTGCFLG
          MATCH     "1",PTCDSCFL
          IF        @EQUAL
            MOVE      "Y",XSTGCFLG
          ENDIF
.
          PACK      XSTGCDIA,PTCDCSSG,SP70       * Cat SG
          IF        DISC2018=1
            MATCH     SP70,PTCDCSSG
            IF        !@EQUAL
              PACK      KEY5,CODESG,PTCDCSSG
              CALL      RDCODE1
              IF        OVRCD=0
                MOVE      THCSCOD,XSTGCDIA   *  Stage of Cancer at Diagnosis 
              ENDIF
            ENDIF
          ENDIF
.
.         PACK      XSTGCSYS,PTCDCSSY,SP70
.
          MATCH     SP70,PTCDCSSY
          IF        !@EQUAL
            PACK      KEY5,CODCs,PTCDCSSY,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      THCSCOD,XSTGCSYS
            ENDIF
          ENDIF          
.
          UNPACK    PTCDCSGT,XSTGCSGT   * Clinical Stage T
          UNPACK    PTCDCSGN,XSTGCSGN   * Clinical Stage N
          UNPACK    PTCDCSGM,XSTGCSGM   * Clinical Stage M
.
SETEXT99  RETURN
+
.------------------------------------------------------------
. Setup ICD Code - remove any slashes and dots
.------------------------------------------------------------
STCOD000  REP       ". ",KEY9
          REP       "/ ",KEY9
          SQUEEZE   KEY9
STCOD999  RETURN
+
.------------------------------------------------------------
. Store Morphology Codes from GMORP000       
.------------------------------------------------------------
STRMOR00  MOVE      ZERO,EXIT
          MOVE     PTEDCODE,KEY9
          CALL     STCOD000                 * remove special chars from Code
.
          ENDSET   KEY9
          CMATCH   "3",KEY9
          IF       @EQUAL
            RESET    KEY9
            MATCH    SP9,MORPHCOD[1]
            IF       @EQUAL
              PACK     MORPHCOD[1],KEY9,SP10
            ENDIF
            MATCH    KEY9,MORPHCOD[1]
            GOTO     STRMOR99 IF EQUAL
          ENDIF
.
          ENDSET   KEY9
          CMATCH   "6",KEY9
          IF       @EQUAL
            RESET    KEY9
            MATCH    SP9,MORPHCOD[2]
            IF       @EQUAL
              PACK     MORPHCOD[2],KEY9,SP10
            ENDIF
            MATCH    KEY9,MORPHCOD[2]
            GOTO     STRMOR99 IF EQUAL
          ENDIF
.
          ENDSET   KEY9
          CMATCH    "9",KEY9
          IF        @EQUAL
            RESET    KEY9
            MATCH     SP9,MORPHCOD[3]
            IF        @EQUAL
              PACK      MORPHCOD[3],KEY9,SP10
            ENDIF
            MATCH    KEY9,MORPHCOD[3]
            GOTO     STRMOR99 IF EQUAL
          ENDIF
.                                           * process any others
          COMPARE   TEN,F2
          GOTO      STRMOR99 IF NOT LESS
.
          ADD       ONE,F2
          RESET     KEY9
          PACK      MORPHCOD[F2],KEY9,SP10
.
STRMOR99  RETURN
.------------------------------------------------------------
. Get Morphology code
.------------------------------------------------------------
GMORP000  MOVE      ZERO,FOUNDPRI
          MOVE      ZERO,FOUNDMOR
.
          CLEAR     XMORPHC1
          MOVE      ZERO,F2
          REPEAT                            * clear the sort area
            ADD       ONE,F2
            PACK      MORPHCOD[F2],SP10
          UNTIL     F2 = 10
          MOVE      THREE,F2
.
          PACK      KEY13,PTCDDADN,SP30
          CALL      RSPTECD1
GMORP100  CALL      RKPTECD1
          BRANCH    OVRCD,GMORP500
.
          MATCH     PTCDDADN,PTEDADMN
          GOTO      GMORP500 IF NOT EQUAL
.                                           * find Primary Cancer in patedcaf
          IF        FOUNDPRI = 0
            MATCH     PTCDPRMM,PTEDCODE
            IF        @EQUAL
              MOVE      ONE,FOUNDPRI
            ENDIF
            GOTO      GMORP100              * read next PATECDaf record
          ENDIF
.                                           * find Morphology code for Primary
          IF        FOUNDMOR = 0
            MATCH     PTCDHTYP,PTEDCODE
            IF        @EQUAL
              MOVE      ONE,FOUNDMOR
              PACK      SAVKEY13,DPTEDADM,DPTEDEPN,DPTEDCNT,SP20
              CALL      STRMOR00            * add Morphology code to Sort area
              GOTO      GMORP200            * 
            ENDIF
            GOTO      GMORP100              * read next PATECDaf record
          ENDIF
.
.                                      * Primary & Morphology have been found 
.                                      * in patecdaf - now find each Secondary
.                                      * cancer (for its Morphology) until a
.                                      * Primary is found. Then stop
GMORP200  PACK      KEY13,SAVKEY13
          CALL      RSPTECD1
GMORP220  CALL      RKPTECD1                * loop thru patecdaf for Morphology
          BRANCH    OVRCD,GMORP500
.
          MATCH     PTCDDADN,PTEDADMN  
          GOTO      GMORP500 IF NOT EQUAL
.
          PACK      KEY9,PTEDCODE,SP70      * is this a Principal Cancer Code ?
          CALL      RDPTCAN1
	  IF        OVRCD = 0
            MATCH     METC770,PTEDCODE
            GOTO      GMORP500 IF LESS      * found Primary Cancer - finish this
            MATCH     PTEDCODE,METC7990
            GOTO      GMORP500 IF LESS      * found Primary Cancer - finish this
          ENDIF
.
          PACK      KEY9,PTEDCODE
          CALL      OPICD1
          CALL      RDPTICD1
          IF        OVRCD = 0
            MATCH     "4",PT0DACRQ          * is this code a Morphology ?
            IF        @EQUAL
              CALL      STRMOR00            * add Morphology code to Sort area
            ENDIF
          ENDIF
          GOTO      GMORP220
.
.                                           * output Morphologies from array
GMORP500  MOVE     ZERO,FOUNDMOR
          CLEAR    XMORPHC1                 * Morphology code/s
          MOVE     ZERO,F2
.
          REPEAT
            ADD      ONE,F2
            MATCH    SP9,MORPHCOD[F2]
            GOTO     GMORP509 IF EQUAL
.
            UNPACK   MORPHCOD[F2],KEY1,KEY8  * remove leading "M"
            STRIP    KEY8
            BRANCH   FOUNDMOR,GMORP505
.
            APPEND   KEY8,XMORPHC1
            ENDSET   XMORPHC1
.
            MOVE     DDESC,XMORPDES         * Morphology text description
            MOVE     ONE,FOUNDMOR           * set the Morphology Codes flag
.
            IF       DISC2018=1
              MOVE     TEN,F2               * Only store one Morphology codes
            ENDIF
            GOTO     GMORP509
.
GMORP505    APPEND   " ",XMORPHC1
            APPEND   KEY8,XMORPHC1
            ENDSET   XMORPHC1
.
GMORP509    MOVE     SP1,KEY1
          UNTIL    F2 = 10
.
.
GMORP900  APPEND   SP70,XMORPHC1
          RESET    XMORPHC1
.
GMORP999  MOVE     ZERO,FOUNDPRI
          RETURN
.------------------------------------------------------------
. Get first given name
.------------------------------------------------------------
GNAM0000  MOVE     ZERO,SECNDFLG
          UNPACK   SP70,KEY30,KEY30A
          CLEAR    KEY30
          CLEAR    KEY30A
          RESET    KEY25
          CMATCH   SP1,KEY25
          GOTO     GNAM9000 IF EQUAL
          GOTO     GNAM1200
.
GNAM1000  BUMP     KEY25
          GOTO     GNAM9000 IF EOS
.
          CMATCH   SP1,KEY25
          GOTO     GNAM1200 IF NOT EQUAL
          COMPARE  ONE,SECNDFLG
          GOTO     GNAM2000 IF NOT EQUAL
.
GNAM1200  MOVE     KEY25,ANS
          IF       SECNDFLG=0
            APPEND   ANS,KEY30     * first given name
          ELSE
            APPEND   ANS,KEY30A    * Second given name
          ENDIF
          GOTO     GNAM1000
.
GNAM2000  MOVE     ONE,SECNDFLG    * Flag for second given name
          GOTO     GNAM1000
.
GNAM9000  APPEND   SP30,KEY30
          RESET    KEY30
          APPEND   SP30,KEY30A
          RESET    KEY30A
GNAM9999  RETURN
+
.------------------------------------------------------------
. Get alias
.------------------------------------------------------------
ALIA0000  MOVE     AURNO,GSRURNO
          MOVE     ZERO,F1
          PACK     KEY115,GSRURNO,SP70,SP70
          CALL     RSPTGSR1
ALIA2000  CALL     RKPTGSR1
          BRANCH   OVRCD,ALIA9500
.
ALIA4000  MATCH    AURNO,GSRURNO              * check U/R
          GOTO     ALIA9500 IF NOT EQUAL
.
          MATCH    GSRSNAM,PTMASNAM           * check surname
          GOTO     ALIA5000 IF NOT EQUAL
.
          MATCH    GSRGNAM,PMPXFNAM           * check 1st given name
          GOTO     ALIA5000 IF NOT EQUAL
.
          MATCH    PTGSGNM2,PMPXSNAM          * check 2nd given name
          GOTO     ALIA5000 IF NOT EQUAL
.
          MOVE     ZERO,GSRURNO               * overkill ?
          GOTO     ALIA2000
.
ALIA5000  COMPARE  SEVEN,PTCNHDPS             * check if TAS extract
          GOTO     ALIA7000 IF EQUAL
.
          PACK     XPRALIAS,GSRSNAM,SP70
          STRIP    XPRALIAS
          ENDSET   XPRALIAS
          APPEND   SP1,XPRALIAS
          APPEND   GSRGNAM,XPRALIAS
          RESET    XPRALIAS
          STRIP    XPRALIAS
          ENDSET   XPRALIAS
          APPEND   SP1,XPRALIAS
          APPEND   PTGSGNM2,XPRALIAS
          APPEND   SP70,XPRALIAS
          RESET    XPRALIAS
.
          MATCH    ANSA,EXTRTYPE
          IF       @EQUAL
            WRITE    EXTRFILE,SEQ;"1060  ",XPRALIAS
          ENDIF
          MOVE     ONE,F1
          GOTO     ALIA2000
.                                             * TAS Extract           *I-264529
ALIA7000  MATCH    GSRSNAM,PTMASNAM           * check surname
          IF       !@EQUAL
            SQUEEZE  GSRSNAM
            PACK     TASALNAM,DQ,GSRSNAM,DQ
          ENDIF
          MATCH    GSRGNAM,PMPXFNAM           * check 1st given name
          IF       !@EQUAL
            SQUEEZE  GSRGNAM
            PACK     TASAFNAM,DQ,GSRGNAM,DQ
          ENDIF
          GOTO     ALIA2000
.
ALIA9500  COMPARE  ZERO,F1
          GOTO     ALIA9999 IF NOT EQUAL
.
          COMPARE  SEVEN,PTCNHDPS             * check if TAS extract
          GOTO     ALIA9999 IF EQUAL
.
          MATCH    ANSA,EXTRTYPE
          IF       @EQUAL
            WRITE    EXTRFILE,SEQ;"1060  ",SP70
          ENDIF
          GOTO     ALIA9999
.                         
ALIA9999  RETURN
+
.------------------------------------------------------------
. Set up doctor address
.------------------------------------------------------------
SADD0000  COMPARE  MRCNTDAD,ONE
          GOTO     SADD4000 IF EQUAL
.
.         Treating Doctor Address
.
SADD2000  CLEAR    XTDADDRS
          APPEND   DSADD1,XTDADDRS         * address 1
          CALL     FNDE0000
          APPEND   " ",XTDADDRS
          APPEND   DSADD2,XTDADDRS         * address 2
          CALL     FNDE0000
          APPEND   " ",XTDADDRS
          APPEND   DSADD3,XTDADDRS         * address 3
          CALL     FNDE0000
          APPEND   " ",XTDADDRS
          APPEND   DSADD4,XTDADDRS         * address 4
          CALL     FNDE0000
          APPEND   " ",XTDADDRS
          APPEND   DSPOST,XTDADDRS         * postcode
          APPEND   SP70,XTDADDRS
          RESET    XTDADDRS
.
.         Use Facility Address if Treating Doctor Address blank
.
          MATCH    SP70,XTDADDRS
          GOTO     SADD9999 IF NOT EQUAL
.
          COMPARE  MRCNTDAD,THREE
          GOTO     SADD9999 IF NOT EQUAL
.
.         Facility Address
.
SADD4000  READ      CONTROLF,TEN3;*1,CHADD1,*31,CHADD2,*61,CHPOST:
                                  *65,CHPADD1,*95,CHPADD2,*125,CHPPOST
          CLEAR    XTDADDRS
          APPEND   CHADD1,XTDADDRS         * address 1
          CALL     FNDE0000
          APPEND   " ",XTDADDRS
          APPEND   CHADD2,XTDADDRS         * address 2
          CALL     FNDE0000
          APPEND   " ",XTDADDRS
          APPEND   CHPOST,XTDADDRS         * postcode
          APPEND   SP70,XTDADDRS
          RESET    XTDADDRS
          GOTO     SADD9999
.
SADD9999  RETURN
+
.------------------------------------------------------------
. Find end of string
.------------------------------------------------------------
FNDE0000  CMATCH   SP1,XTDADDRS
          GOTO     FNDE9999 IF NOT EQUAL
          BUMP     XTDADDRS,-1
          GOTO     FNDE0000 IF NOT EOS
          RESET    XTDADDRS
FNDE9999  RETURN
+
.------------------------------------------------------------
. Local Medical Office Name - format (given names, surname)
.------------------------------------------------------------
LMON0000  MOVE     ZERO,FORM1
          MOVE     ZERO,OVRCD
          RESET    ADOCTL
.
LMON1000  CMATCH   SP1,ADOCTL
          GOTO     LMON2000 IF NOT EQUAL
          IF       OVRCD=0
            ADD      ONE,FORM1
          ENDIF
          BUMP     ADOCTL                  * move pointer 1 char
          GOTO     LMON9000 IF EOS
          MOVE     ONE,OVRCD
          GOTO     LMON1000
.
LMON2000  MOVE     ZERO,OVRCD
          MOVE     ADOCTL,ANS
          BRANCH   FORM1,LMON2200,LMON2300
          APPEND   ANS,XLMGNAM1             * first given name
          GOTO     LMON5000
.
LMON2200  APPEND   ANS,XLMGNAM2             * second given name
          GOTO     LMON5000
.
LMON2300  APPEND   ANS,XLMSNAME             * last name
.
LMON5000  BUMP     ADOCTL                  * move pointer 1 char
          GOTO     LMON9000 IF EOS
          GOTO     LMON1000
.
LMON9000  APPEND   SP70,XLMGNAM1
          RESET    XLMGNAM1
          APPEND   SP70,XLMGNAM2
          RESET    XLMGNAM2
.
          MATCH    SP70,XLMSNAME
          IF       @EQUAL
            PACK     XLMSNAME,XLMGNAM2,SP70 * assuming last word is the surname
            PACK     XLMGNAM2,SP70
          ELSE
            APPEND   SP70,XLMSNAME
            RESET    XLMSNAME
          ENDIF
LMON9999  RETURN
+
.------------------------------------------------------------
. Local GP 
.------------------------------------------------------------
LOGP0000  MOVE     AURNO,KEY8
          CALL     RDPMPX21
          BRANCH   OVRCD,LOGP9999
.
          PACK     KEY10,PMPXRHC1,SP70
          CALL     RDPMHCP1
          BRANCH   OVRCD,LOGP9999
.
          PACK     XLMSNAME,PMHCSNAM,SP70     * Surname
          PACK     XLMGNAM1,PMHCGNAM,SP70     * Given name
          PACK     XLMGNAM2,PMHCINIT,SP70     * Initials
          STRIP    PMHCADR1
          STRIP    PMHCADR2
          STRIP    PMHCADR3
          STRIP    PMHCPOST
.
          MOVE     PMHCADR1,XLMOADDS
          ENDSET   XLMOADDS
.
          APPEND   SP1,XLMOADDS
          APPEND   PMHCADR2,XLMOADDS
          ENDSET   XLMOADDS
.
          APPEND   SP1,XLMOADDS
          APPEND   PMHCADR3,XLMOADDS
          ENDSET   XLMOADDS
.
          APPEND   SP1,XLMOADDS
          APPEND   PMHCPOST,XLMOADDS
          ENDSET   XLMOADDS
          RESET    XLMOADDS
.
LOGP9999  RETURN
+
.------------------------------------------------------------
. Write to Extract File - ASCII Format
.------------------------------------------------------------
WRTEXT00  MATCH    ANSX,EXTRTYPE
          GOTO     WRTEXT99 IF EQUAL
.
          WRITE    EXTRFILE,SEQ;"1000"
          WRITE    EXTRFILE,SEQ;"1010  ",XPSNAME
          WRITE    EXTRFILE,SEQ;"1020  ",XPGNAM1
          WRITE    EXTRFILE,SEQ;"1030  ",XPGNAM2
          WRITE    EXTRFILE,SEQ;"1040  ",XPBDATE
          WRITE    EXTRFILE,SEQ;"1050  ",XPATSEX
          WRITE    EXTRFILE,SEQ;"1055  ",XPMEDI
          WRITE    EXTRFILE,SEQ;"1056  ",XINDHEID               * 283509
          CALL     ALIA0000                      * Alias
          WRITE    EXTRFILE,SEQ;"1070  ",XABORIGN
.         WRITE    EXTRFILE,SEQ;"1080  ",XMARSTAT
          WRITE    EXTRFILE,SEQ;"1100  "
          WRITE    EXTRFILE,SEQ;"1110  ",XADDRESS
          WRITE    EXTRFILE,SEQ;"1120  ",XPSUBURB
          WRITE    EXTRFILE,SEQ;"1130  ",XPOSTCOD
          WRITE    EXTRFILE,SEQ;"1140  ",XCNBIRTH
          WRITE    EXTRFILE,SEQ;"1150  ",XLNGHOME               * 283509
          WRITE    EXTRFILE,SEQ;"1210  ",XTDSNAME
          WRITE    EXTRFILE,SEQ;"1215  ",XTDGNAM1
          WRITE    EXTRFILE,SEQ;"1216  ",XTDGNAM2
          WRITE    EXTRFILE,SEQ;"1220  ",XTDADDRS
          WRITE    EXTRFILE,SEQ;"1225  ",XTRDCPRV
          WRITE    EXTRFILE,SEQ;"1230  ",XHCAMPUS
          WRITE    EXTRFILE,SEQ;"1235  ",XHOSCODE
          WRITE    EXTRFILE,SEQ;"1240  ",XURNUMBR
          WRITE    EXTRFILE,SEQ;"1260  ",XADMDATE
          WRITE    EXTRFILE,SEQ;"1270  ",XDIADATE
          WRITE    EXTRFILE,SEQ;"1271  ",XESTDATE
          WRITE    EXTRFILE,SEQ;"1280  ",XCANCDIA
          WRITE    EXTRFILE,SEQ;"1285  ",XWDIAGNS
          WRITE    EXTRFILE,SEQ;"1290  ",XDISDATE
....      WRITE    EXTRFILE,SEQ;"1295  ",XVITSTAT                     *D-208570
...       WRITE    EXTRFILE,SEQ;"1310  ",XPRIMCAN
          WRITE    EXTRFILE,SEQ;"1320  ",XICDPRI1
          WRITE    EXTRFILE,SEQ;"1325  ",XLATERAL
          IF       DISC2018<>1
            WRITE    EXTRFILE,SEQ;"1330  ",XPRSTEXT
            WRITE    EXTRFILE,SEQ;"1335  ",XMETFLG1                   *I-208570
          ENDIF
.
          WRITE    EXTRFILE,SEQ;"1340  ",XMETCOD1
...       WRITE    EXTRFILE,SEQ;"1341  ",XMETCOD2
...       WRITE    EXTRFILE,SEQ;"1342  ",XMETCOD3
.
          IF       DISC2018<>1
            WRITE    EXTRFILE,SEQ;"1345  ",XMETDES1                   *I-208570
          ENDIF
.
          WRITE    EXTRFILE,SEQ;"1360  ",XMORPHC1
..       WRITE    EXTRFILE,SEQ;"1361  ",XMORPHC2
.
...       WRITE    EXTRFILE,SEQ;"1365  ",XMORPDES
          WRITE    EXTRFILE,SEQ;"1365  ",XHISGRAD
.
          WRITE    EXTRFILE,SEQ;"1370  ",XBASDIAG
          WRITE    EXTRFILE,SEQ;"1371  ",XBASDESC                     *I-217537
....      WRITE    EXTRFILE,SEQ;"1371  ",XBATDIAG           *I-146004 *D-217537
.
          WRITE    EXTRFILE,SEQ;"1372  ",XECOGSTA                     *I-283509
          WRITE    EXTRFILE,SEQ;"1373  ",XSPRDIN1                     *I-283509
          WRITE    EXTRFILE,SEQ;"1373  ",XSPRDIN2                     *I-283509
          WRITE    EXTRFILE,SEQ;"1373  ",XSPRDIN3                     *I-283509
....      WRITE    EXTRFILE,SEQ;"1374  ",XDEGSPRD                     *I-283509
.
....      WRITE    EXTRFILE,SEQ;"1375  ",XTUMDATE                     *D-208570
....      WRITE    EXTRFILE,SEQ;"1376  ",XMETDATE                     *D-208570
.
          IF       DISC2018<>1
            WRITE    EXTRFILE,SEQ;"1390  ",XSTGCFLG
          ENDIF
          WRITE    EXTRFILE,SEQ;"1391  ",XSTGCDIA
          WRITE    EXTRFILE,SEQ;"1392  ",XSTGCSYS
.
.
          IF       DISC2018=1
            WRITE    EXTRFILE,SEQ;"1393  ",XSTGCSGT   * Clinical Stage T
            WRITE    EXTRFILE,SEQ;"1394  ",XSTGCSGN   * Clinical Stage N
            WRITE    EXTRFILE,SEQ;"1395  ",XSTGCSGM   * Clinical Stage M
          ENDIF
.
....      WRITE    EXTRFILE,SEQ;"1700  ",XDEATHDT                     *D-208570
....      WRITE    EXTRFILE,SEQ;"1710  ",XAUTOPSY                     *D-208570
          WRITE    EXTRFILE,SEQ;"2210  ",XLMSNAME
          WRITE    EXTRFILE,SEQ;"2215  ",XLMGNAM1
          WRITE    EXTRFILE,SEQ;"2216  ",XLMGNAM2
          WRITE    EXTRFILE,SEQ;"2220  ",XLMOADDS
          WRITE    EXTRFILE,SEQ;"2225  ",XLCGPPRV
          WRITE    EXTRFILE,SEQ;"2900  ",XCANNOTE
          WRITE    EXTRFILE,SEQ;"2910  ",XCOMPDAT
          WRITE    EXTRFILE,SEQ;"2999"
.
          COMPARE   ZERO,FOUNDMOR
          IF        @EQUAL
            PRINT     *1,"********** Cancer Morphology/ICD Morphology Code mismatch **********"
          ENDIF
.
WRTEXT99  RETURN
.
.------------------------------------------------------------
. Write to Extract File - XML Format
.------------------------------------------------------------
WRTEXX00  MATCH     ANSX,EXTRTYPE
          GOTO      WRTEXX99 IF NOT EQUAL
.
          PACK      TAGNAME1,SP2,LESSERTH,EXTRCTRG,GREATRTH
          WRITE     EXTRFILE,SEQ;*+,TAGNAME1,*-
          PACK      TAGNAME1,SP70
.                     
          CALL      ALIA0000
          CALL      CLRTAG00
.
          MOVE      "PatSurname",TAGNAME
          MOVE      XPSNAME,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "PatFirstName",TAGNAME
          MOVE      XPGNAM1,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "PatSecondName",TAGNAME
          MOVE      XPGNAM2,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "PatOtherName",TAGNAME
          MOVE      XPRALIAS,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "DOB",TAGNAME
          MOVE      XPBDATE,TAGVALUE
          CALL      WRTTAG00
.
          IF        PTCNHDPS=3
            MOVE      "SexAtBirth",TAGNAME
          ELSE
            MOVE      "Sex",TAGNAME
          ENDIF
          MOVE      XPATSEX,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "MedicareNo",TAGNAME
          MOVE      XPMEDI,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "IHI",TAGNAME
          MOVE      XINDHEID,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "COB",TAGNAME
          MOVE      XCNBIRTH,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "Language",TAGNAME
          MOVE      XLNGHOME,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "IndigStatus",TAGNAME
          MOVE      XABORIGN,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "PropertyName",TAGNAME
          MOVE      "",TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "StreetAddr",TAGNAME
          MOVE      XADDRESS,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "Suburb",TAGNAME
          MOVE      XPSUBURB,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "Postcode",TAGNAME
          MOVE      XPOSTCOD,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "TDSurname",TAGNAME
          MOVE      XTDSNAME,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "TDFirstName",TAGNAME
          MOVE      XTDGNAM1,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "TDSecondName",TAGNAME
          MOVE      XTDGNAM2,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "TDAddress",TAGNAME
          MOVE      XTDADDRS,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "TDMediProvidNo",TAGNAME
          MOVE      XTRDCPRV,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "HospitalName",TAGNAME
          MOVE      XHCAMPUS,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "CampusCode",TAGNAME
          MOVE      XHOSCODE,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "URN",TAGNAME
          MOVE      XURNUMBR,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "AdmissionDate",TAGNAME
          MOVE      XADMDATE,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "DischargeDate",TAGNAME
          MOVE      XDISDATE,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "DiagnosisDate",TAGNAME
          MOVE      XDIADATE,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "EstDateFlag",TAGNAME
          MOVE      XESTDATE,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "PriorDiagFlag",TAGNAME
          MOVE      XCANCDIA,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "WhereDiagnosed",TAGNAME
          MOVE      XWDIAGNS,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "PrimarySite",TAGNAME
          MOVE      XICDPRI1,TAGVALUE
          CALL      TAGESC00
          CALL      WRTTAG00
.
          MOVE      "Laterality",TAGNAME
          MOVE      XLATERAL,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "Morph",TAGNAME
          MOVE      XMORPHC1,TAGVALUE
          CALL      TAGESC00
          CALL      WRTTAG00
.
          MOVE      "Grade",TAGNAME
          MOVE      XHISGRAD,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "Investigations",TAGNAME
          PACK      TAGVALUE,XBASDIAG 
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "ECOG",TAGNAME
          MOVE      XECOGSTA,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "MetSite",TAGNAME
          MOVE      XMETDES1,TAGVALUE
          CALL      TAGESC00
          CALL      WRTTAG00
.
          MOVE      "AdditInfo",TAGNAME
          PACK      VSCOTEXT,XSPRDIN1,XSPRDIN2,XSPRDIN3
          MOVE      VSCOTEXT,TAGVALUE
          CALL      TAGESC00
          CALL      WRTTAG00
.
          MOVE      "Stage",TAGNAME
          MOVE      XSTGCFLG,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "StagingSystem",TAGNAME
          MOVE      XSTGCSYS,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "TNM-T",TAGNAME
          MOVE      XSTGCSGT,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "TNM-N",TAGNAME
          MOVE      XSTGCSGN,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "TNM-M",TAGNAME
          MOVE      XSTGCSGM,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "GPSurname",TAGNAME
          MOVE      XLMSNAME,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "GPFirstName",TAGNAME
          MOVE      XLMGNAM1,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "GPSecondName",TAGNAME
          MOVE      XLMGNAM2,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "GPAddress",TAGNAME
          MOVE      XLMOADDS,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "GPMediProvidNo",TAGNAME
          MOVE      XLCGPPRV,TAGVALUE
          CALL      WRTTAG00
.
          MOVE      "RegName",TAGNAME
          MOVE      XCANNOTE,TAGVALUE
          CALL      TAGESC00 
          CALL      WRTTAG00
.
          MOVE      "RegDate",TAGNAME
          MOVE      XCOMPDAT,TAGVALUE
          CALL      WRTTAG00
.
          PACK      TAGNAME1,SP2,LESSERTH,SLASH,EXTRCTRG,GREATRTH
          WRITE     EXTRFILE,SEQ;TAGNAME1
          PACK      TAGNAME1,SP70
.
          COMPARE   ZERO,FOUNDMOR
          IF        @EQUAL
            PRINT     *1,"********** Cancer Morphology/ICD Morphology Code mismatch **********"
          ENDIF
.
WRTEXX99  RETURN 
.
.---------------------------------------------------------------------------
. Clear XML Tag fields
.---------------------------------------------------------------------------
CLRTAG00  PACK      TAGNAME,SP70
          PACK      TAGNAME1,SP70
          PACK      TAGNAME2,SP70
          PACK      TAGVALUE,SP70,SP70
CLRTAG99  RETURN 
.
.---------------------------------------------------------------------------
. Write out one tag field
.---------------------------------------------------------------------------
WRTTAG00  PACK      TAGNAME1,LESSERTH,TAGNAME,GREATRTH
          CLEAR     TAGSTRNG
          APPEND    TAGNAME1,TAGSTRNG
          STRIP     TAGVALUE
          APPEND    TAGVALUE,TAGSTRNG           
          PACK      TAGNAME2,LESSERTH,SLASH,TAGNAME,GREATRTH,SP70
          APPEND    TAGNAME2,TAGSTRNG
          RESET     TAGSTRNG
          WRITE     EXTRFILE,SEQ;*+,SP4,TAGSTRNG,*-
.
          CALL      CLRTAG00
.
WRTTAG99  RETURN 
.
.---------------------------------------------------------------------------
. This is used when the name or description may contains characters that
. are not work with XML tag. Ie. < > & etc
.---------------------------------------------------------------------------
TAGESC00  CLEAR     TAGSTRNG
          APPEND    ECSTGSTR,TAGSTRNG
          STRIP     TAGVALUE
          APPEND    TAGVALUE,TAGSTRNG           
          APPEND    ECSTGEND,TAGSTRNG
          RESET     TAGSTRNG
          MOVE      TAGSTRNG,TAGVALUE
.
TAGESC99  RETURN 
+
.------------------------------------------------------------
. Clear TAS Extract fields           
.------------------------------------------------------------
CLRTAS00  MOVE      ZERO,EXIT
          PACK      TASURNO,SP30
          PACK      TASADMN,SP30
          PACK      TASHOSPN,SP30
          PACK      TASHOSDS,SP70,SP30
          PACK      TASPSNAM,SP70
          PACK      TASPGNAM,SP70
          PACK      TASPSEX,SP70
          PACK      TASPADD1,SP70,SP30
          PACK      TASPADD2,SP70,SP30
          PACK      TASPSURB,SP70
          PACK      TASPCODE,SP70
          PACK      TASPDOB,SP70
          PACK      TASDISCH,SP70
          PACK      TASDEATH,SP70
          PACK      TASPRIM[1],SP70
          PACK      TASPRIM[2],SP70
          PACK      TASPRIM[3],SP70
          PACK      TASPRIM[4],SP70
          PACK      TASPRIM[5],SP70
          PACK      TASPRDES[1],SP70
          PACK      TASPRDES[2],SP70
          PACK      TASPRDES[3],SP70
          PACK      TASPRDES[4],SP70
          PACK      TASPRDES[5],SP70
          PACK      TASMORPH[1],SP70
          PACK      TASMORPH[2],SP70
          PACK      TASMORPH[3],SP70
          PACK      TASMORPH[4],SP70
          PACK      TASMORPH[5],SP70
          PACK      TASBIRTH,SP70,SP30
          PACK      TASINDIG,SP70,SP30
          PACK      TASTDLNM,SP70
          PACK      TASTDFNM,SP70
          PACK      TASALNAM,SP70
          PACK      TASAFNAM,SP70
          PACK      TASMLNAM,SP70
          PACK      TASXSNAM,SP70                        
          PACK      TASXPFGN,SP70                       
          PACK      TASDBSDG,SP70                      
          PACK      TASDLATR,SP70                     
          MOVE      ZERO,DISC2018    * set flag for Discharged after 01/07/2018
.
CLRTAS99  RETURN
+
.------------------------------------------------------------
. Set TAS Extract fields           
.------------------------------------------------------------
SETTAS00  MOVE      ZERO,EXIT
          MOVE      ZERO,DISC2018
.                                           * store Master data (once only)
          MATCH     SP9,TASADMN
          IF        @EQUAL
            MOVE      SAVADMNO,KEY10  
            SQUEEZE   KEY10  
            PACK      TASADMN,DQ,KEY10,DQ
            MOVE      SP8,DDATE
            IF        ASTAT=3
              MOVE      AADMNO,KEY8
              CALL      RDDSCH1
              IF        OVRCD=0
                MATCH     "20180701",DDATE
                IF        !@LESS
                  MOVE      ONE,DISC2018    * Discharged after 01/07/2018
                ENDIF
              ENDIF
            ENDIF
.                                           * Hospital No. and Hospital Name
            IF        IBCNMHOS=1
              PACK      DIM150,PTHSNAME,SP70
              PACK      KEY10,PTHSHDPC,SP70
            ELSE
              PACK      DIM150,CONAME
              PACK      KEY10,CFILENO
            ENDIF
            STRIP     KEY10
            PACK      TASHOSPN,DQ,KEY10,DQ
            STRIP     DIM150
            PACK      TASHOSDS,DQ,DIM150,DQ
.                                           * Patient UR No
            MOVE      SP8,KEY8
            MOVE      PURNO,KEY8
            SQUEEZE   KEY8
            PACK      TASURNO,DQ,KEY8,DQ
.
            STRIP     PSNAME
            PACK      TASPSNAM,DQ,PSNAME,DQ    * Last Name
            MATCH     SP10,PMPXFNAM
            IF        @EQUAL
              MOVE     PGNAME,KEY25
              CALL     GNAM0000                * Get old given name
              STRIP    KEY30
              PACK      TASPGNAM,DQ,KEY30,DQ     * First Name
            ELSE
              STRIP     PMPXFNAM
              PACK      TASPGNAM,DQ,PMPXFNAM,DQ  * First Name
            ENDIF

.           MATCH     SP40,PMPXSNAM
            STRIP     PMPXSNAM
            PACK      TASXSNAM,DQ,PMPXSNAM,DQ  * Patient Second Name 
            
            MATCH     SP25,PMPXPFGN 
            STRIP     PMPXPFGN
            PACK      TASXPFGN,DQ,PMPXPFGN,DQ  * Preferred Given Name

            PACK      TASPSEX,DQ,PSEX,DQ       * Sex
            STRIP     PADD1
            PACK      TASPADD1,DQ,PADD1,DQ     * Address Line 1
            STRIP     PADD2
            PACK      TASPADD2,DQ,PADD2,DQ     * Address Line 2
            STRIP     PSUBRB
            PACK      TASPSURB,DQ,PSUBRB,DQ    * Suburb
            SQUEEZE   PPOST
            PACK      TASPCODE,DQ,PPOST,DQ     * Post Code
            UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY             * Date of Birth
            PACK      TASPDOB,DQ,XDAY,SL,XMON,SL,XCENT,XYEAR,DQ
.
            MATCH     SP8,DDATE
            IF        !@EQUAL
              UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY            * Discharge date
              PACK      TASDISCH,DQ,XDAY,SL,XMON,SL,XCENT,XYEAR,DQ
            ELSE
              PACK      TASDISCH,DQ,DQ
            ENDIF
.
            MATCH     SP8,PDECDTE
            IF        !@EQUAL
              UNPACK    PDECDTE,XCENT,XYEAR,XMON,XDAY          * Date of Death
              PACK      TASDEATH,DQ,XDAY,SL,XMON,SL,XCENT,XYEAR,DQ
            ELSE
              PACK      TASDEATH,DQ,DQ
            ENDIF
.
            PACK      TDESC,SP30
            PACK      KEY5,ANSC,SP1,PCONT
            CALL      RDCODE1
            STRIP     TDESC
            PACK      TASBIRTH,DQ,TDESC,DQ     * Country of Birth
.
            PACK      TDESC,SP30
            PACK      KEY5,CATVA,PMVXABRG,SP10
            CALL      RDCODE1
            STRIP     TDESC
            PACK      TASINDIG,DQ,TDESC,DQ     * Indigenous Status
.
            PACK      KEY6,ADOCTA              * Attending doctor
            CALL      RDDOCT1
            IF        OVRCD=0
              STRIP     DSNAME
              PACK      TASTDLNM,DQ,DSNAME,DQ  * Surname
              SCAN      " ",DGNAME
              IF        @EQUAL
                MOVEFPTR  DGNAME,F5
                RESET     DGNAME
                SETLPTR   DGNAME,F5
              ENDIF
              STRIP     DGNAME
              PACK      TASTDFNM,DQ,DGNAME,DQ  * Given name
            ELSE
              PACK      TASTDLNM,DQ,DQ
              PACK      TASTDFNM,DQ,DQ
            ENDIF
.
            PACK      TASALNAM,DQ,DQ           * Patient Alias
            PACK      TASAFNAM,DQ,DQ
            CALL      ALIA0000
.
            PACK      TASMLNAM,DQ,DQ           * Maiden Last Name (not in use)
          ENDIF
.
.                                              * store multiple Primary Cancers
SETTAS10  MOVE      PTCDPRMM,KEY9
          CALL      STCOD000
          MOVE      ONE,F1
          REPEAT
            MATCH     SP5,TASPRIM[F1]
            IF        @EQUAL
              PACK      TASPRIM[F1],KEY9       * store Primary Cancer Code
              STRIP     TASPRIM[F1]
              MOVE      SIX,F1
            ELSE
              ADD       ONE,F1
            ENDIF
          UNTIL     F1 > 5
.
SETTAS20  PACK     DDESC,SP70,SP20
          MOVE     PTCDPRMM,KEY9
          CALL     OPICD1
          CALL     RDPTICD1
          STRIP    DDESC
          MOVE      ONE,F1
          REPEAT
            MATCH     SP20,TASPRDES[F1]
            IF        @EQUAL
              PACK      TASPRDES[F1],DDESC      * store Primary Cancer Descrip
              MOVE      SIX,F1
            ELSE
              ADD       ONE,F1
            ENDIF
          UNTIL     F1 > 5
.
SETTAS30  MOVE      PTCDHTYP,KEY9
          CALL      STCOD000
          MOVE      ONE,F1
          REPEAT
            MATCH     SP7,TASMORPH[F1]
            IF        @EQUAL
              PACK      TASMORPH[F1],KEY9       * store Morphology Code
              STRIP     TASMORPH[F1]
              MOVE      SIX,F1
            ELSE
              ADD       ONE,F1
            ENDIF
          UNTIL     F1 > 5
.
          MOVE      SP70,DIM1
          MOVE      SP70,THCSCOD
          PACK      KEY5,ANSB,ANSD,PTCDBSDG
          CALL      RDCODE1                        * Cat "BD"
          IF        OVRCD=0
            MOVE      THCSCOD,DIM1
            PACK      TASDBSDG,DQ,DIM1,DQ
          ENDIF
.
          MOVE     SP70,DIM1
          MOVE     SP70,THCSCOD
          PACK     KEY5,ANSL,ANSY,PTCDLATR,SP70    * Laterality
          CALL     RDCODE1
          IF       OVRCD=0
            MOVE   THCSCOD,DIM1
            PACK   TASDLATR,DQ,DIM1,DQ
          ELSE
            MOVE   NINE,DIM1                   * Unknown laterality
            PACK   TASDLATR,DQ,DIM1,DQ
          ENDIF
.
SETTAS99  RETURN
+
.------------------------------------------------------------
. Write TAS Extract fields           
.------------------------------------------------------------
WRTTAS00  MOVE      ZERO,EXIT
.                                           * format Code fields & Description
          PACK      DIM50,SP70
          CLEAR     DIM50
          MOVE      ONE,F1
          REPEAT
            MATCH     SP5,TASPRIM[F1]
            IF        @EQUAL
              MOVE      SIX,F1
            ELSE
              IF        F1 > 1
                APPEND    SP1,DIM50
              ENDIF
              APPEND      TASPRIM[F1],DIM50
            ENDIF
            ADD       ONE,F1
          UNTIL     F1 > 5
          RESET     DIM50
          STRIP     DIM50
          PACK      TASPRIMX,DQ,DIM50,DQ    * up to 5 Primary Cancer codes
.
          PACK      DIM400,SP70,SP70,SP70,SP70,SP70
          CLEAR     DIM400
          MOVE      ONE,F1
          REPEAT
            MATCH     SP20,TASPRDES[F1]
            IF        @EQUAL
              MOVE      SIX,F1
            ELSE
              IF        F1 > 1
                APPEND    PP,DIM400
              ENDIF
              APPEND      TASPRDES[F1],DIM400
            ENDIF
            ADD       ONE,F1
          UNTIL     F1 > 5
          RESET     DIM400
          STRIP     DIM400
          PACK      TASPRDEX,DQ,DIM400,DQ   * up to 5 Primary Cancer Descripts.
.
          PACK      DIM50,SP70
          CLEAR     DIM50
          MOVE      ONE,F1
          REPEAT
            MATCH     SP5,TASMORPH[F1]
            IF        @EQUAL
              MOVE      SIX,F1
            ELSE
              IF        F1 > 1
                APPEND    SP1,DIM50
              ENDIF
              APPEND      TASMORPH[F1],DIM50
            ENDIF
            ADD       ONE,F1
          UNTIL     F1 > 5
          RESET     DIM50
          STRIP     DIM50
          PACK      TASMORPX,DQ,DIM50,DQ     * up to 5 Morphology Codes
.
          WRITE    EXTRFILE,SEQ;*+,TASURNO,CO,TASADMN,CO,TASHOSDS,CO:
                       TASPSNAM,CO,TASPGNAM,CO:
                       TASXSNAM,CO,TASXPFGN,CO:
                       TASPSEX,CO,TASPADD1,CO:
                       TASPSURB,CO,TASPCODE,CO,TASPDOB,CO:
                       TASDISCH,CO,TASDEATH,CO,TASPRIMX,CO,TASPRDEX,CO:
                       TASMORPX,CO,TASBIRTH,CO,TASINDIG,CO,TASTDLNM,CO:
                       TASTDFNM,CO,TASMLNAM,CO:
                       TASDBSDG,CO,TASDLATR
. 
          WRITE    EXTRFILE,SEQ;" "          * blank line between extracts
.
WRTTAS99  RETURN
+
.------------------------------------------------------------
. Move Extract to Web Directory
.------------------------------------------------------------
MVEXT000  CLEAR     CMDLINE                 * header file
          APPEND    "ibainp83.us1 ",CMDLINE
          APPEND    EXTRCTFN,CMDLINE
          APPEND    ".txt",CMDLINE
          IF        IBCNMHOS=1
            APPEND    SP1,CMDLINE
            APPEND    HEADHOSP,CMDLINE
          ENDIF
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
+
.**************************************************************************
. Routine to call the appopriate loop routine to get the Cancer registration
. details that are registered but not extracted
.**************************************************************************
UREG0000  MOVE      ZERO,EXIT
          CALL      KHOS000                 * Keyin Hospital id
.
UREG0500  MOVE      ZERO,EXIT
          CALL      EXTR0000                * option to extract
          BRANCH    EXIT,UREG0500
.
          CALL      CONTQST
          BRANCH    CEXIT,UREG1000,UREG9500,UREG0000
          GOTO      UREG9500
.
UREG1000  MOVE      ZERO,EXIT
          MOVE      ZERO,CLNO
          MOVE      ZERO,CPAGENO
          CALL      HEDU0000
          MOVE      SP10,SAVURNO
.
          MATCH    ANSX,EXTRTYPE
          IF       @EQUAL
            CALL      XMLHDR00
          ENDIF
.
          CALL      UCAN0000               * Use coding date
.
UREG9000  PRINT     *1,"*** End of Report ***"
.
          MATCH    ANSX,EXTRTYPE
          IF       @EQUAL
            CALL      XMLHDE00
          ENDIF
.
          GOTO      UREG9999
.
UREG9500  MOVE      ONE,EXIT
UREG9999  RETURN
+
.**************************************************************************
. Loop cancer registration file for all records that have not been extracted
. eg. Blank date extracted PTCDDTEX
.**************************************************************************
UCAN0000  MOVE      ZERO,EXIT
          MOVE      SP8,PREVADMN                                      *I-264529
.
          PACK      KEY33,SP70
          CALL      RSPTCRD2
UCAN1000  CALL      RKPTCRD2                * use "Extract Date" as primary fld
          BRANCH    OVRCD,UCAN9000
.
          MATCH     SP70,PTCDDTEX                 * Exit if cancer reg has
          GOTO      UCAN9000 IF NOT EQUAL         * already been extracted
.
          PACK      SAVKEY33,PTCDDTEX,PTCDDADN,PTCDPRMM,PTCDCNRG,SP70
.
UCAN1500  PACK      KEY8,PTCDDADN,SP70
          CALL      RDMISS1                       * read admission details
          BRANCH    OVRCD,UCAN1000
.
          UNPACK    ACODDTE,CCENT,CYEAR,CMON,CDAY
          PACK      THEDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11                      * read visit extension file
          BRANCH    OVRCD,UCAN1000
.
          IF        IBCNMHOS=1
            MATCH     SP70,HEADHOSP
            IF        !@EQUAL
              MATCH     PMVXMHOS,HEADHOSP
              GOTO      UCAN1000 IF NOT EQUAL
            ELSE                                      
               PACK       KEY3,PMVXMHOS,SP70
               CALL       RDPTHSP1
            ENDIF
          ENDIF
.
.                                      * now find the Cancer Code in patecdaf
          MOVE      ZERO,FOUNDCAN
          PACK      KEY16,AADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
UCAN2000  CALL      RKPTECD2
          BRANCH    OVRCD,UCAN1000
.
          MATCH     AADMNO,PTEDADMN
          GOTO      UCAN1000 IF NOT EQUAL
.
.                                           * find the Primary Cancer
          IF        FOUNDCAN = 0
            MATCH     PTCDPRMM,PTEDCODE     * is it same as Cancer Reg Primary?
            GOTO      UCAN2000 IF NOT EQUAL
.
            MOVE      ONE,FOUNDCAN
            MOVE      PTEDADMN,SAVADMNO     * save data for cancer report
            MOVE      PTEDCODE,SAVPDIAG
            MOVE      PTEDDESC,DIAGDESC
            MOVE      PTEDDESC,DIAGD40
            GOTO      UCAN2000
          ENDIF
.
          PACK      KEY9,PTEDCODE,SP70
          CALL      RDPTICD1
          BRANCH    OVRCD,UCAN2000
.
          MATCH     "4",PT0DACRQ            * Morphology Code
          GOTO      UCAN2000 IF NOT EQUAL
.
UCAN3000  MOVE      PTEDCODE,SAVMORPH       * save morphology data for report
          MOVE      PTEDDESC,PATHDESC
          MOVE      PTEDDESC,PATHD40
          MOVE      ZERO,FOUNDCAN
.
          MOVE      SAVADMNO,CURRADMN                                 *I-264529
.
          CALL      CPRT0000                * Print registration if one exists
          CALL      CREXT000                * create Extract data     *I-264529
.
          IF        EXTRCTYN = 1
            PACK      KEY33,SAVKEY33,SP70
            CALL      RDPTCRD2
            IF        OVRCD=0
              PACK      PTCDDTEX,CCC,CYY,CMM,CDD,SP10  * Extract sent date
              REP       " 0",PTCDDTEX
              CALL      UPPTCRD2            * update "Extract Sent" date field
            ENDIF
            PACK      KEY33,SAVKEY33,SP70
            CALL      RSPTCRD2
          ENDIF
.
          GOTO      UCAN1000
.
.
UCAN9000  IF        PTCNHDPS = 7
            COMPARE   ONE,EXTRCTYN
            GOTO      UCAN9999 IF NOT EQUAL
            MATCH     SP9,TASADMN           * check if data still to be written
            IF        !@EQUAL
              CALL      WRTTAS00
              CALL      CLRTAS00
            ENDIF
          ENDIF
.
UCAN9999  RETURN
.
.----------------------------------------------------------------------------
. Convert overseas postcodes to 8888
.----------------------------------------------------------------------------
PCOS0000  MATCH     "8888",PPOST                 * already VIC OS postcode
          GOTO      PCOS9999 IF EQUAL
.
          MATCH     PADD4,SP70                   * blank state ?
          GOTO      PCOS2000 IF EQUAL            * yes
.
.         Check if the postcode/suburb/state combination is valid
.
          PACK      KEY45,PSUBRB,SP70
          PACK      KEY56,PPOST,KEY45,PADD4,SP70
          CALL      RDIBPOS1                     * pcode/suburb/state on file ?
          COMPARE   ZERO,OVRCD
          GOTO      PCOS9000 IF EQUAL            * yes
.
.         Check if the postcode/suburb combination is valid
.
PCOS2000  PACK      KEY56,PPOST,PSUBRB,SP70
          CALL      RDIBPOS1                     * postcode/suburb on file ?
          COMPARE   ZERO,OVRCD
          GOTO      PCOS9000 IF EQUAL            * yes - valid
.
          CALL      RKIBPOS1                     * no - get next record
          BRANCH    OVRCD,PCOS9999
.
          MATCH     PPOST,IBPOPCOD               * same postcode still ?
          GOTO      PCOS9999 IF NOT EQUAL        * no - error
.
          MATCH     PSUBRB,IBPOSUBR              * same suburb still ?
          GOTO      PCOS9999 IF NOT EQUAL        * yes
.
.         We have a valid suburb/postcode combination, so check if it
.         is an overseas address
.
PCOS9000  MATCH     "OS ",IBPOSTAT               * overseas address ?
          IF        @EQUAL
            MOVE      "8888",XPOSTCOD
          ENDIF
.
PCOS9999  RETURN
.
.----------------------------------------------------------------------------
.         I/O includes needed
.----------------------------------------------------------------------------
.
          INC       STD001IO/PBL
.
          INC       AGECHK
          INC       CALCAGE                 * calculate age
          INC       CLPATMAS                * 
.          INC       NZHISIIO                * 
.          INC       NZIBSRCH                * 
          INC       PATALERT                * PATALERT
          INC       PATHSPKY                * Keyin hospital id
          INC       IBAPOSIO/INC            * Postcodes
          INC       PATALRIO/INC            * patient Alerts for 0 U/R's
          INC       PATCANIO/INC            * Cancer Register Detail File
          INC       PATCRDIO/INC            * Cancer Register Detail File
          INC       PATCRGIO/INC            * Cancer Register Header File
          INC       PATCODIO/INC            * patient codes file
          INC       PATDO1IO/INC            * Patient Doctors File
          INC       PATDSCIO/INC            * Discharge File
          INC       PATECDIO/INC            * Patient episode disease file
          INC       PATGSRIO/INC            * Patient name file           
          INC       PATHSPIO/INC            * Hospital file           
          INC       PATICDIO/INC            * ICD-9 File
          INC       PATMA1IO/INC            * patient master file
          INC       PATMI1IO/INC            * admission file
          INC       PATNIDIO/INC            * patient national number file
          INC       NHIMASIO/INC            * national/iba code translation file
          INC       PATSMWIO/INC            * medical warning for 0 U/R's
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PMSPX2IO/INC            * PMI Extension file
          INC       PMSHCPIO/INC            * HCP file
          INC       VISCODIO/INC
          INC       WEBSECIO/INC
..............................................................................

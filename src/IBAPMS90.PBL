. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAPMS90                                            *
. *                                                                           *
. *     FUNCTION:         FINANCIAL BALANCING PROGRAM 2                       *
. *                                                                           *
. *     DATE WRITTEN:     07/08/89                                            *
. *                                                                           *
. *     AUTHOR:           Jeni Tan        (I.B.A)                             *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *****************************************************************************
. * V12.00.02 14/07/2025 J.Tan           TSK 0961623                          *
. *           Add Lumpsum/GST to total DTR amnt & TRAPCLR Open Outpatient DTR *
. * V12.00.01 20/05/2025 Thanh T         TSK 0955096                          *
. *           Changed for alphanumeric visit number                           *
. *****************************************************************************
. *        V11.00.01 11/03/2020  J.Tan          TSK 0838262                   *
. *                  Added Effective from and to date to MBS Item file        *
. *        V10.14.01 22/03/2019  J.Tan            TSK 0857392                 *
. *                  Changed RCP Transaction count from DIM3 to DIM5          *
. *        V10.13.01 07/12/2013 J.Tan         TSK 0859743                     *
. *                  Mod for OTRECTYP=6 for Theatre item                      *
. *        V10.12.01 15.01.2018  J.Tan   TSK 0848146                          *
. *                  Added CACCFEE=5 - Accumulation FI by Claim code          *
. *        V10.05.01 01/19/2015  Ania P                      CAR 261630       *
. *                  Removed use of PORT for temp file naming                 *
. *        V10.01.01 20/12/2010  Mike Laming   CAR 233046                     *
. *                  Mods to Misc.Charges PATMCHFD - Key change, logic changes*
. *        V10.00.01 30/04/2010  J.Tan     CAR 220887                         *
. *                  Mods checking for Active/Inactive of Misc.charge         *
. *        V9.10.01  13/05/2008 J.Tan      CAR 162313                         *
. *                  Added Deposit status to comdepaf                         *
. *        V9.09.01  21/02/2008 J.Tan      CAR 160240                         *
. *                  Fixed Emergency Procedure to be included in Theatre      *
. *        V9.04.02  23/11/2005 Peter Vela CAR 83987                          *
. *                  Added open of RCPDTEA1                                   *
. *                  Fixed read and write to WORK3 tempfile                   *
. *        V9.04.01  12/08/2005  Jeni Tan       CAR 62750                     *
. *                  Removed redefined variables                              *
. *        V9.03.06  19/12/2003 J.Tan   CAR 44093                             *
. *                  Replaced patcashf with comdepaf                          *
. *        V9.03.05  17/09/2003  Lina Vo       CAR 40497                      *
. *                  Changed index and read of patmchgf to include effective  *
. *                  date.                                                    *
. *        V9.03.04  14/08/2003  Lina Vo       CAR 41196                      *
. *                  Recompiled for big changes in PATSELFN.                  *
. *        V9.03.03  30/06/2003  Dean Cramer   CAR 38200                      *
. *                  Fixed were journal fiel not positioned properly due to   *
. *                  wrong key                                                *
. *                  Fix bad print format for journals.                       *
. *        V9.03.02  16/06/2003  Dean Cramer   CAR 38200                      *
. *                  Fixed were payments were not reported when system option *
. *                  selected is CONSOLIDATED                                 *
. *        V9.03.01  05/06/2003  Dean Cramer   CAR 38200                      *
. *                  Fixed headings as requested                              *
. *        V9.02.01  02/04/2003  Dean Cramer   CAR 36519                      *
. *                  Fixed for web where printing overlaps                    *
. *                  Fixed where admission number truncated in Vets options   *
. *                  Fixed where Outpatients - All sites has never worked     *
. *        V5.08.01  16/08/2000  Caleb Sharman                                *
. *                  Changed Health Fund variables to be 8 chars              *
. *        V5.08.B02 28/02/2000  J.Tan                                        *
. *                  Mods to use effective date instead of last changed date  *
. *        V5.08.B01 08/01/2000  Steve Armstrong                              *
. *                  Mods to use PATSTRYR to determine start of year date     *
. *****************************************************************************
. *        V5.07.B02 27/01/1999  Jim Stathopoulos                             *
. *                  Display Modifications                                    *
. *                  09/11/1998  Glenn Berry      5.07 changes                *
. *****************************************************************************
. *        V5.05.01  18/06/1997  D.Di.Paola                                   *
. *                  Changed version for recompile.....                       *
. *        V5.04.02  18/06/1997  Nick Read                                    *
. *                  Removed rollout file garbage and replaced with standard  *
. *                  temporary file code. Also put in a couple of             *
. *                  REP   UPPLOW,ANS's while I was there.                    *
. *        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                 *
. *                  Changed temp file name "pstidx" to standard format       *
. *****************************************************************************
. *                       W5.51 09/08/89 J.Tan    (I.B.A.)                    *
. *                       Mods to print Inv.date if before                    *
. *                       the 'fromdate' and check Misc grp                   *
. *                       V5.52 25/10/89 L.P. SRF102694                       *
. *                       Mods to report printing.                            *
. *                       V5.53 17/11/89 J.Tan SRF 102252                     *
. *                       Add other financ.opt to PATSELFN                    *
. *                       V5.54 22/05/90 J.Tan   SRF 104901                   *
. *                             Mods PATITMFD                                 *
. *                       V5.55 06/12/90 DWJ                                  *
. *                             Recompile for MBS extension                   *
. *                       V5.56 25/04/91 Graeme Williams                      *
. *                             Altered 2nd index on DTR's                    *
. *                       V5.57 01/12/1992 Steve Armstrong                    *
. *                             Quote 7437  SRF 115599                        *
. *                             Mods for extra CACCFEE                        *
. *                             option.                                       *
. *                    V5.01.01 04/05/1993 Gab Cameron                        *
. *                             SFR 121936                                    *
. *                             To run in release 5.01                        *
. *        V5.03.B1  06/06/95  Paul Howells                  SRF 136735       *
. *                  Recompiled for PATSELFN                                  *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEDTRFD/INC
          INC       COMDEPFD/INC
.
          INC       IBAPASFD/INC
          INC       IBASECFD/INC
.
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
.
          INC       PATCODFD/INC
          INC       PATDRGFD/INC
          INC       PATDTRFD/INC
          INC       PATINVFD/INC
          INC       PATITMFD/INC
          INC       PATJRNFD/INC
          INC       PATMA1FD/INC
          INC       PATMCHFD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATSELDT/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
.
          INC       RCPDTEFD/INC
          INC       RCPBDTFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
.         Temporary index file for the accumulation of totals
.
.               Work file for Invoice checking
.
WORK1     IFILE    SQL, FIXED=16, KEY="u1-7"
.
.Name     Type     Size     Physical      Start
.----     ----     ----     --------      -----
XTYPE     DIM       1          1            1
.CODE     DIM       6          6            2
.TOTAL    FORM      12.2       8            8
.                        End of Record     16
.
.               Work file for Journals checking
.
WORK2     IFILE    SQL, FIXED=96, KEY="u1-29"
.
.Name     Type     Size     Physical      Start
.----     ----     ----     --------      -----
.XTYPE    DIM       1          1            1
.CODE     DIM       6          6            2
DXADMNO   DIM       8          8            8
.TREF     DIM       8          8           16
XTTRANS   DIM       6          6           24
.TPATAMTT FORM     12.2        8           30
.PSNAME   DIM       20         20          38
.PGNAME   DIM       25         25          58
.DANS     DIM       9          9           83
.TFMONTH  FORM      2          2           92
.TFYEAR   FORM      2          2           94
.                        End of Record     96
.Redefine form fields from key
.----------------------------
XADMNO    FORM      8
.
.               Work file for Cash checking
.
WORK3     IFILE    SQL, FIXED=113, KEY="u1-29,101-112"
.
.Name     Type     Size     Physical      Start
.----     ----     ----     --------      -----
.XTYPE    DIM       1          1            1
.CODE     DIM       6          6            2
.DXADMNO  DIM       8          8            8
.TREF     DIM       8          8           16
.XTTRANS  DIM       6          6           24
.TPAYTYPE FORM      1          2           30
.TPATAMTT FORM      12.2       8           32
.PSNAME   DIM       20         20          40
.PGNAME   DIM       25         25          60
.PTDTEFFD DIM       8           8          85
.PINVDTE  DIM       8           8          93
.CMDERECN DIM       12         12          101
.                        End of Record    113
.Redefine form fields from key
.----------------------------
.XADMNO   FORM      8
.
.
. LOCAL VARIABLES
. ---------------
CACCFEE   FORM      1
CATEG     DIM       20
CJRNREP   FORM      1
COTHSFN   FORM      1
CMDLINE   DIM       50
CODE      DIM       6
CURRDATE  DIM       8
CSTRTYR   DIM       8                             * for PATSTRYR
.
DANS      DIM       9
DIM2A     DIM       2
DIM3      DIM       3
DIM4A     DIM       4
DIM4B     DIM       4
DIM6A     DIM       6
DIM6B     DIM       6
DDFORM    FORM      2
MMFORM    FORM      2
YYFORM    FORM      2
CCFORM    FORM      2
.
FNAMEI    DIM       8
FNAMER    DIM       8
FROMDATE  DIM       8
FORM12P2  FORM      12.2
.
HAVEIND   FORM      1
.
IBCNMESS  FORM      6                   Next Message Number
IBCNAESN  DIM       3                   AAE System Number
IBCNOPSN  DIM       3                   Outpatients System Number
IBCNBLSN  DIM       3                   Patient Billing System Number
IBCNMASN  DIM       3                   Mamography System Number
IBCNRASN  DIM       3                   Radiology System Number
IBCNINSN  DIM       3                   Inpatient System Number
IBCNMHOS  FORM      1                   Multi Hospital Indicator
IDATET    DIM       8
INVACCM   FORM      12.2
INVCASH   FORM      12.2
INVGOVT   FORM      12.2
INVJOUR   FORM      12.2
INVMISC   FORM      12.2
INVTHEA   FORM      12.2
.
KEY21A    DIM       21
KEY26A    DIM       26
.
OPTHD     DIM       24
OUTKEY21  DIM       21
OUTKEY23  DIM       23
.
PATKEY21  DIM       21
PATKEY23  DIM       23
PATSTCUR  DIM       4                            * for PATSTRYR
PMETH     DIM       6
.
QCKJRNL   FORM      1
.
RECFLG    FORM      1
.
SAVCODE   DIM       6
SAVTYPE   FORM      1
SYSTEM    FORM      1
.
STOTAL    FORM      12.2
SUBTOT    FORM      12.2
.
TODATE    DIM       8
TOTACCM   FORM      12.2
TOTADJ    FORM      12.2
TOTAL     FORM      12.2
TOTBAD    FORM      12.2
TOTCASH   FORM      12.2
TOTCHEQ   FORM      12.2
TOTCRED   FORM      12.2
TOTMERC   FORM      12.2
TOTMISC   FORM      12.2
TOTNORM   FORM      12.2
TOTONLY   FORM      1
TOTREB    FORM      12.2
TOTTHEA   FORM      12.2
TYPE      FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
ADJUST    INIT      "Adjustment Journals "
BADDEBT   INIT      "Bad Debt Journals   "
CASH      INIT      "CASH  "
CHEQUE    INIT      "CHEQUE"
CODEA     INIT      "A "
CODECL    INIT      "CL"
CODEJ     INIT      "J "
CREDIT    INIT      "CREDIT"
DEBIT     INIT      "DEBIT "
DEPOSIT   INIT      " By Deposits        "
FIVE9     FORM      "59"
HEALTH    INIT      " By Health Fund     "
INSUR     INIT      " By Insurance Code  "
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
MERCH     INIT      "MERCH"
NORMAL    INIT      "Normal Journals     "
OPT1      INIT      "Check Invoice Data      "
OPT2      INIT      "Check Payment Data      "
OPT3      INIT      "Check Journal Data      "
OPT4      INIT      "Check All Financial Data"
PATIENT   INIT      " By Patient         "
REBATE    INIT      "Rebate Journals     "
SP12      INIT      "            "
SP14      INIT      "              "
UKEY1     INIT      " 16 U1-7"   * Key for WORK1 File
UKEY2     INIT      " 96 U1-29"  * Key for WORK2 File
UKEY3     INIT      " 113 U1-29,101-112"  * Key for WORK3 File
UNKNOWN   INIT      " By Unknown Type    "
.
.
PRGID     INIT      "IBAPMS90"
PRGNAM    INIT      "Financial Balancing Reports 2"
VERSION   INIT      "V12.01.00"
+
...............................................................................
.         START OF PROGRAM
...............................................................................
.
          DISPLAY       *ES;
          CALL          DISPHEAD                  * call display header routine
. 
          DISPLAY       *P56:24,"Opening patma1af";
          OPEN          PATMA1A1,"patma1af"
.
          DISPLAY       *P64:24,"patmx1af";
          OPEN          PATMX1A1,"patmx1af"
.
          DISPLAY       *P64:24,"patpr1af"; 
          OPEN          PATPR1A1,"patpr1af"
.
          DISPLAY       *P64:24,"patinvrf";
          OPEN          PATINVR1,"patinvrf"
.
          DISPLAY       *P64:24,"patinvrf";
          OPEN          PATINVR2,"patinvrf"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN          PATMI1A1,"patmi1af"
.
          DISPLAY       *P64:24,"pmsvx1af";
          OPEN          PMSVX1A1,"pmsvx1af"
.
          DISPLAY       *P64:24,"patjrnlf";
          OPEN          PATJRNL1,"patjrnlf"
.
          DISPLAY       *P64:24,"patdrgaf";
          OPEN          PATDRGA2,"patdrgaf"
.
          DISPLAY       *P64:24,"patdtraf";
          OPEN          PATDTRA1,"patdtraf"
.
          DISPLAY       *P64:24,"patdtraf";
          OPEN          PATDTRA2,"patdtraf"
.
          DISPLAY       *P64:24,"comdepaf";
          OPEN          COMDEPA4,"comdepaf"
.
          DISPLAY       *P64:24,"rcpdteaf";
          OPEN          RCPDTEA1,"rcpdteaf"
.
          DISPLAY       *P64:24,"patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"patvisaf";
          OPEN          PATVISA1,"patvisaf"
.
          DISPLAY       *P64:24,"outsitaf";
          OPEN          OUTSITA1,"outsitaf"
.
          DISPLAY       *P64:24,"aaede1af";
          OPEN          AAEDE1A1,"aaede1af"
.
          DISPLAY       *P64:24,"aaedtref";
          OPEN          AAEDTRE1,"aaedtref"
.
          DISPLAY       *P64:24,"aaedtref";
          OPEN          AAEDTRE2,"aaedtref"
.
          DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
          READ          CONTROLF,TEN;*192,CACCFEE
          READ          CONTROLF,TEN8;*255,CJRNREP
.
          CALL          TFILENAM
          MOVE          TFILNAME,FNAMEI

.
.        START OF PROGRAM
.
         MOVE      ZERO,HAVEIND
.
.           MAIN        MENU
.           SELECT OPTION
.
START    MOVE       ZERO,CPAGENO
.
         HLINE     *G37,2,55,80
         DISPLAY   *P1:3,*EF:
                   *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                   *P1:5,*V2LON,ONE,*HOFF," = ",OPT1:
                   *P1:6,*V2LON,TWO,*HOFF," = ",OPT2:
                   *P1:7,*V2LON,THREE,*HOFF," = ",OPT3:
                   *P1:8,*V2LON,FOUR,*HOFF," = ",OPT4
.
REKSTRT  KEYIN     *P1:10,"Option : ",*EF,*V2LON,OPTION
.
         COMPARE   ZERO TO OPTION
         GOTO      ENDIT IF EQUAL
.
BRNCH    BRANCH    OPTION OF KDATE1,KDATE1,KDATE1,KDATE1
.
         DISPLAY   *P1:24,*EL,*V2LON,*B,"** Invalid Selection ** ";
         CALL      HITENTER
         GOTO      REKSTRT
.
KDATE1   LOAD       OPTHD USING OPTION OF OPT1,OPT2,OPT3,OPT4
         DISPLAY    *P55:2,*V2LON,"- ",OPTHD
.
.        Key in date range
.
         DISPLAY    *P1:4,*EF:
                    *P1:6,"Starting Date :"
         MOVE       SIX,CVERT
         MOVE       TEN7,CCOL
.
         CALL       PATSTRYR                     * get start of year date
.
         UNPACK     CSTRTYR,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,CDAY
         MOVE       XMON,CMON
         MOVE       XYEAR,CYEAR
         MOVE       XCENT,CCENT
         MOVE       ONE,CCANLDTE
         MOVE       ZERO,CDEFDTE
         MOVE       ZERO,CHIGHLT
.
         CALL       KEYDATE
         PACK       FROMDATE,ICENT,IYEAR,IMON,IDAY
         REP        " 0",FROMDATE
.
         DISPLAY    *P1:8,"  Ending Date :"
         MOVE       EIGHT,CVERT
KDATE2   MOVE       CDD,CDAY
         MOVE       CMM,CMON
         MOVE       CYY,CYEAR
         MOVE       CCC,CCENT
         MOVE       ONE,CCANLDTE
         MOVE       ZERO,CDEFDTE
         MOVE       ZERO,CHIGHLT
         CALL       KEYDATE
         PACK       TODATE,ICENT,IYEAR,IMON,IDAY
         REP        " 0",TODATE
.
         MATCH      FROMDATE,TODATE
         GOTO       TONLYYN IF NOT LESS
.
         DISPLAY    *P40:8,*V2LON,"** End Date must be >= Start Date ** ";
         CALL       HITENTER
         GOTO       KDATE2
.
.
.        Check if totals only are wanted
.
TONLYYN  MOVE       ZERO,TOTONLY
         BRANCH     OPTION OF MASTN0      * Option 1 doesn't need a totals only
.
         KEYIN      *P1:10,*EL,"Do you want Totals Only (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
.
         REP        UPPLOW,ANS
         CMATCH     "Y",ANS
         GOTO       TONLYY IF EQUAL
.
         CMATCH     "N",ANS
         GOTO       TONLYN IF EQUAL
.
         BEEP
         GOTO       TONLYYN
.
.        Totals Only
.
TONLYY   MOVE       ONE,TOTONLY
         GOTO       MASTN0
.
.        Detailed Report
.
TONLYN   MOVE       ZERO,TOTONLY
.
.        Check (for journals) is the quick or slow check is to be done
.
MASTN0   BRANCH     OPTION,MASTN2,MASTN2          * For journals only
.
         KEYIN      *P1:12,*EL,"Do you want a (",*V2LON,"Q",*HOFF,")uick or (":
                    *V2LON,"S",*HOFF,")low check on journals ? ",*V2LON,ANS;
.
         REP        UPPLOW,ANS
         CMATCH     ANS,ANSQ
         GOTO       QJRNLYY IF EQUAL
.
         CMATCH     ANS,ANSS
         GOTO       QJRNLNN IF EQUAL
.
         BEEP
         GOTO       MASTN0
.
QJRNLYY  MOVE       ONE,QCKJRNL                   * Quick journals
         GOTO       MASTN2  
.
QJRNLNN  MOVE       ZERO,QCKJRNL                  * Slow Journals
.

MASTN2   CALL      PATSELFN
         BRANCH    EXIT,START
.
.        Check if these details are correct
.
         KEYIN      *P1:24,*EL,"Ok to continue (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,"/":
                               *V2LON,"C",*HOFF,") ? ",*V2LON,ANS
.
         REP        UPPLOW,ANS
.
         CMATCH     "C" TO ANS
         GOTO       ENDIT IF EQUAL
.
         CMATCH     "Y" TO ANS
         GOTO       MASTN3 IF EQUAL
.
         CMATCH     "N" TO ANS
         GOTO       BRNCH IF EQUAL
.
         BEEP
         GOTO       MASTN2
.
.        Clock the starting time, then go to the appropriate option
.
MASTN3   CLOCK      TIME,CTIMEIS
         BRANCH     OPTION OF OPTION1,OPTION2,OPTION3,OPTION4
         GOTO       START
.
.        Check Invoice Data
.
OPTION1  CALL       CHECKI
         GOTO       START
.
.        Check Payment Data
.
OPTION2  CALL       CHECKP
         GOTO       START
.
.        Check Journal Data
.
OPTION3  CALL       CHECKJ
         GOTO       START
.
.        Check All Financial Data
.
OPTION4  CALL       CHECKI
         CALL       CHECKP
         CALL       CHECKJ
         GOTO       START
.
.        Subroutine to check the invoice figures for the date range specified
.
CHECKI   MOVE       "60",CLNO
.
         CALL       CRTMPIDI         * Create temporary file
.
         MOVE       ZERO,TOTACCM
         MOVE       ZERO,TOTTHEA
         MOVE       ZERO,TOTMISC
.
         DISPLAY    *P1:24,*EL,*P10:24,"Date/Inv No : ";
.
.        Loop over the invoice file for the period selected
.
         PACK       KEY16,FROMDATE,SP8
         CALL       RDSINV2
CILOOP   CALL       RDKINV2
         BRANCH     OVRCD OF CIENDLP
.
         MATCH      PINVDTE,TODATE
         GOTO       CIENDLP IF LESS
.
         UNPACK     PINVDTE WITH XCENT,XYEAR,XMON,XDAY
         DISPLAY    *P25:24,*V2LON,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,SP2,PINVNO
.
         MATCH      ZEROVISN,PINVADM
         GOTO       CILOOP IF EQUAL
.
.        Check that this is a valid system
.
         COMPARE    FSTSYS,PINVSYS
         GOTO       CILOOP IF LESS
.
         COMPARE    PINVSYS,FEDSYS
         GOTO       CILOOP IF LESS
.
.        Check for a valid site
.
         MATCH      FSTSITE,PINVSITE
         GOTO       CILOOP IF LESS
.
         MATCH      PINVSITE,FEDSITE
         GOTO       CILOOP IF LESS
.
.        Make sure the visit file record exists
.
         MOVE       PINVADM,PVIBILL
         MOVE       PVIBILL,KEY8
         CALL       RDVISA1
         BRANCH     OVRCD,CINOVIS
.
.        Depending on the system, check that the patient's details exist.
.
         BRANCH     PINVSYS,CIAAE,CIOUT,CIINP,CIOUT
.
.        We have a valid invoice number. Check that the A + E number and
.        U/R numbers are valid
.
CIAAE    MOVE       PINVADM,ADANUMB
         MOVE       ADANUMB,KEY8
         CALL       RDDETA1
         BRANCH     OVRCD OF CINOADM
.
         MOVE       ADAURNO,AURNO
         MATCH      PINVUR,ADAURNO
         CALL       CIERR1 IF NOT EQUAL
.
         GOTO       CIRDMAS
.
.        We have a valid invoice number. Check that the outpatient number
.        and U/R number are valid. Also check for a valid department.
.
CIOUT    MATCH      PVISITE,PINVSITE
         CALL       CIERR8 IF NOT EQUAL
.
         MOVE       "out",OSTFILE       * Default file prefix
         MOVE       SP3,OSTSYST         * Department code
         MOVE       PINVSITE,KEY6
         CALL       RDSITA1             * Read in the site file record
.
.        Check for a valid department
.
         MATCH      FSTDEPT,OSTSYST
         GOTO       CILOOP IF LESS
.
         MATCH      OSTSYST,FEDDEPT
         GOTO       CILOOP IF LESS
.
         CLOSE      OUTBOKA3
         PACK       CFNAME,OSTFILE,FILBOKA3
         OPEN       OUTBOKA3,CFNAME     * Open the bookings A file
.
         CLOSE      OUTBB1A1
         PACK       CFNAME,OSTFILE,FILBB1A1
         CALL       OPOTBB11,CFNAME     * Open the bookings B file
.
         MOVE       ZERO,OVRCD
         TRAP       OVERCOND IF IO
         CLOSE      OUTDTRO1
         PACK       CFNAME,OSTFILE,FILDTRO1
         OPEN       OUTDTRO1,CFNAME     * Open the dtran file
         TRAPCLR    IO
         BRANCH     OVRCD,CILOOP
.
.        Loop over the bookings file to find the records for this visit
.
         PACK       KEY36 WITH PVIURNO,PVIDATE,SP30
         CALL       RDSBOKA3
OUTDATLP CALL       RDKBOKA3
         BRANCH     OVRCD OF CINOADM
.
         MATCH      PVIBILL,OBAOUTNO
         GOTO       OUTDATFD IF EQUAL
.
         MATCH      PVIURNO,OBAURNO
         GOTO       CINOADM IF NOT EQUAL
.
         MATCH      PVIDATE,OBADATE
         GOTO       OUTDATLP IF EQUAL
.
         GOTO       CINOADM
.
.        We have found the Booking A file
.
OUTDATFD MOVE       OBAOUTNO,KEY8
         CALL       RDBOKB1             * Get the Bookings B record
         BRANCH     OVRCD,CINOBOKB
.
         MOVE       OBAURNO,AURNO
         MATCH      OBAURNO,PINVUR
         CALL       CIERR1 IF NOT EQUAL
.
         GOTO       CIRDMAS
.
.        Other financial
.
CIOTH    MOVE       "out",OSTFILE       * Default file prefix
         CLOSE      OUTDTRO1
         PACK       CFNAME,OSTFILE,FILDTRO1
         OPEN       OUTDTRO1,CFNAME     * Open the dtran file
         GOTO       CIRDMAS
.
.        We have a valid invoice number. Check that the admission number
.        and U/R numbers are valid.
.
CIINP    MOVE       PINVADM,KEY8
         CALL       RDMISS1
         BRANCH     OVRCD OF CINOADM
.
         MATCH      PINVUR,AURNO
         CALL       CIERR1 IF NOT EQUAL
.
CIRDMAS  MOVE       PINVUR,KEY8
         CALL       RDMAST1
         BRANCH     OVRCD OF CINOUR
.
.        Loop over PATDTRFD file and check the various invoice totals
.
CISTDLP  MOVE       ZERO,INVACCM
         MOVE       ZERO,INVTHEA
         MOVE       ZERO,INVMISC
         MOVE       ZERO,INVGOVT
         MOVE       ZERO,INVCASH
         MOVE       ZERO,INVJOUR
.
         PACK       KEY22 WITH PINVADM,PINVNO,SP10
         CALL       RDSDTR1
CIDLP    CALL       RDKDTR1
         BRANCH     OVRCD OF CIDEND
.
         MATCH      PINVNO,TREF
         GOTO       CIDEND IF NOT EQUAL
.
         IF         TRECTYPE=1 & PTINCMIX=1
           ADD        PTDTLSPT,FORM12P2
           ADD        PTDTLSRB,FORM12P2        * lumpsum
           ADD        PTDTGSTM,FORM12P2
         ENDIF
.
         COMPARE    ZERO,TPATAMTT
         GOTO       CIDLP IF EQUAL
.
         LOAD       FORM12P2 USING TRECTYPE OF INVACCM,INVTHEA,INVMISC,INVGOVT:
                                             INVCASH,INVJOUR,INVCASH,INVTHEA
         ADD        TPATAMTT,FORM12P2
         ADD        PTDTGSTM,FORM12P2
.
         STORE      FORM12P2 USING TRECTYPE OF INVACCM,INVTHEA,INVMISC,INVGOVT:
                                             INVCASH,INVJOUR,INVCASH,INVTHEA
.
.        Accumulate details of TRECTYPE's 1,2,3 & 4
.
         BRANCH     TRECTYPE OF CIACCM,CIMISC,CIMISC,CIGOVT,CIDLP:
                                CIDLP,CIDLP,CIMISC
         GOTO       CIDLP
.
.        Accomodation
.
CIACCM   BRANCH     CACCFEE OF CIWARD:
                               CIWARD
         COMPARE    FIVE,CACCFEE
         GOTO       CIATYP IF NOT EQUAL
.
.        Accomodation by Claim Type
.
         MOVE       TCLMTYP,DIM3
         MATCH      SP3,DIM3
         GOTO       CIAPCK IF NOT EQUAL
.
         MOVE       ACLAIM,DIM3
         GOTO       CIAPCK
.
.        Accomodation by Admission Type
.
CIATYP   MOVE       TADMTYP,DIM3
         MATCH      SP3,DIM3
         GOTO       CIAPCK IF NOT EQUAL
.
         MOVE       ATYPE,DIM3
         GOTO       CIAPCK
.
.        Accomodation by ward
.
CIWARD   MOVE       TDWARD,DIM3
         MATCH      SP3,DIM3
         GOTO       CIAPCK IF NOT EQUAL
.
.
         SCAN       "(",TDDESC
         GOTO       CINOWRD IF EOS
.
         BUMP       TDDESC
         CMATCH     SLASH,TDDESC
         GOTO       CINOWRD IF EQUAL
.
         MOVE       TDDESC,DIM3
         GOTO       CIAPCK
.
CINOWRD  MOVE       SP3,DIM3
.
CIAPCK   PACK       KEY7 WITH TRECTYPE,DIM3,SP3
         GOTO       CIADDTMP
.
.        Theatre fees and miscellaneous items
.
CIMISC   MATCH      SP3,TMISGRP
         GOTO       CIMISC9 IF NOT EQUAL
.
.        Get the miscellaneous group
.
         OPEN       PATMCHG1,"patmchgf"
         OPEN       PATITEM1,"patitemn"
.
         TYPE       TITEMNO
         GOTO       CIMISC1 IF NOT EQUAL
.
.        Code is numeric - get the misc.group from item file
.
         PACK       KEY9 WITH TITEMNO,SP5
         PACK       KEY17,KEY9,CURRDATE,SP70
         CALL       PATITMRD
         BRANCH     OVRCD OF CIMISC1
         MOVE       IMISGRP,TMISGRP
         GOTO       CIMISC2
.
.        Get the misc.group from Misc charges file
.
CIMISC1  MOVE       "0  ",MCLMCOD
         PACK       KEY29,MCLMCOD,SP6,SP3,TITEMNO,CURRDATE
         CALL       PATMCHRD                * read Misc.Charges file 
         BRANCH     EXIT,CIMISC3            * if Item is invalid, inactive, etc
.
CIMISC2  MOVE       MMSCGRP,TMISGRP
.
.        Close the files
.
CIMISC3  CLOSE      PATMCHG1
         CLOSE      PATITEM1
.
CIMISC9  PACK       KEY7 WITH TRECTYPE,TMISGRP,SP3
         GOTO       CIADDTMP
.
.        Government Subsidy
.
CIGOVT   PACK       KEY7 WITH TRECTYPE,SP7
.
.        Update the temporary file
.
CIADDTMP READ       WORK1,KEY7;XTYPE,CODE,TOTAL
         GOTO       CINEWTMP IF OVER
.
         ADD        TPATAMTT,TOTAL
         UPDATE     WORK1;XTYPE,CODE,TOTAL
         GOTO       CIDLP
.
.        Write a new record to the temporary file
.
CINEWTMP MOVE       TPATAMTT,TOTAL
         WRITE      WORK1,KEY7;KEY7,TOTAL
         GOTO       CIDLP
.
.        End of DTRAN loop file - check that totals agree with invoice file
.
CIDEND   SUB        INVGOVT,INVACCM
.
         MOVE       INVACCM,TOTAL
         ADD        INVTHEA,TOTAL
         ADD        INVMISC,TOTAL
.
         MOVE       PINVPATA,TOTCASH
         ADD        PINVHFDA,TOTCASH
         ADD        PINVOTHA,TOTCASH
.
         COMPARE    PINVAMT,TOTAL
         CALL       CIERR2 IF NOT EQUAL
.
         COMPARE    TOTCASH,INVCASH
         CALL       CIERR3 IF NOT EQUAL
.
         COMPARE    PINVJOUR,INVJOUR
         CALL       CIERR4 IF NOT EQUAL
.
.        Accumulate grand totals
.
         ADD        INVACCM,TOTACCM
         ADD        INVTHEA,TOTTHEA
         ADD        INVMISC,TOTMISC
.
.        Get next invoice
.
         GOTO       CILOOP
.
.        No admission record
.
CINOADM  CALL       CIERR5
         GOTO       CIRDMAS
.
.        No U/R record
.
CINOUR   CALL       CIERR6
         GOTO       CISTDLP
.
.        No visit record
.
CINOVIS  CALL       CIERR7
         GOTO       CIRDMAS
.
.        No booking B record
.
CINOBOKB CALL       CIERR9
         GOTO       CIRDMAS
.
.        End of loop over Invoice file. Now loop over temporary file
.
CIENDLP  MOVE       ZERO,SAVTYPE
         MOVE       SP6,SAVCODE
         MOVE       "60",CLNO
         MOVE       ZERO,TOTAL        * Grand Total
.
         DISPLAY    *P1:24,*EL,*P20:24,"** Generating Report ** ";
         CALL       HITENTER
         DISPLAY    *P10:24,*EL,"Code : ";
.
         READ       WORK1,SP7;;
CINEXT   READKS     WORK1;XTYPE,CODE,STOTAL
         GOTO       CIENDRP IF OVER
         MOVE       XTYPE,TYPE
.
         DISPLAY    *P20:24,*V2LON,TYPE,SP1,CODE;
.
         COMPARE    TYPE,SAVTYPE
         GOTO       CICNT1 IF EQUAL
.
.        New Invoice category (1=Accom, 2=Theatre Fee, 3=Misc Item, 4=Gov Sub)
.
         COMPARE    ZERO,SAVTYPE
         GOTO       CINEWCAT IF EQUAL
.
.        Print current totals
.
         CALL       CITOTAL
.
.        Save new code and initialize variables
.
CINEWCAT MOVE       TYPE,SAVTYPE
         CALL       CIINIT
.
.        Print details of this record & accumulate totals
.
CICNT1   CALL       CIPRINT
         CALL       CIACCUM
         GOTO       CINEXT
.
.        End of temporary file - print final totals and kill temporary file
.
CIENDRP  CALL       CITOTAL
.
         COMPARE    "50",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"Total Accomodation           : $",TOTACCM,*N:
                    *1,"Total Theatre Fees           : $",TOTTHEA,*N:
                    *1,"Total Miscellaneous Charges  : $",TOTMISC,*N,*N:
                    *1,"Grand Total                  : $",TOTAL
.
         CALL       KILLTMP
         RETURN
.
.        Invoice check errors
.
CIERR1   COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"INVOICE ERROR 1 - Adm rec U/R ",AURNO:
                    " different from Invoice rec U/R ",PINVUR:
                    *92,"INVOICE: Adm ",PINVADM,", Invoice ",PINVNO
         ADD        ONE,CLNO
         RETURN
.
CIERR2   COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"INVOICE ERROR 2 - DTRAN Invoice amt ",TOTAL:
                    " different from Invoice ",PINVAMT:
                    *92,"INVOICE: Adm ",PINVADM,", Invoice ",PINVNO
         ADD        ONE,CLNO
         RETURN
.
CIERR3   COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"INVOICE ERROR 3 - DTRAN Cash ",INVCASH:
                    " different from Invoice ",TOTCASH:
                    *92,"INVOICE: Adm ",PINVADM,", Invoice ",PINVNO
         ADD        ONE,CLNO
         RETURN
.
CIERR4   COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"INVOICE ERROR 4 - DTRAN Journal ",INVJOUR:
                    " different from Invoice ",PINVJOUR:
                    *92,"INVOICE: Adm ",PINVADM,", Invoice ",PINVNO
         ADD        ONE,CLNO
         RETURN
.
CIERR5   COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"INVOICE ERROR 5 - No record in Adm/Booking/Details":
                    " File ":
                    *92,"INVOICE: Adm ",PINVADM,", Invoice ",PINVNO
         ADD        ONE,CLNO
         RETURN
.
CIERR6   COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"INVOICE ERROR 6 - No record in Master File ":
                    "for U/R ",PINVUR:
                    *92,"INVOICE: Adm ",PINVADM,", Invoice ",PINVNO
         ADD        ONE,CLNO
         RETURN
.
CIERR7   COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"INVOICE ERROR 7 - No record in Visit File ":
                    *92,"INVOICE: Adm ",PINVADM,", Invoice ",PINVNO
         ADD        ONE,CLNO
         RETURN
.
CIERR8   COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"INVOICE ERROR 8 - Visit rec site ",PVISITE:
                    " different from Invoice rec site ",PINVSITE:
                    *92,"INVOICE: Adm ",PINVADM,", Invoice ",PINVNO
         ADD        ONE,CLNO
         RETURN
.
CIERR9   COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         PRINT      *1,"INVOICE ERROR 9 - No record in BOKBF File ":
                    *92,"INVOICE: Adm ",PINVADM,", Invoice ",PINVNO
         ADD        ONE,CLNO
         RETURN
.
.
.        Subroutine to initialize variables
.
CIINIT   MOVE       ZERO,SUBTOT
         RETURN
.
.        Subroutine to accumulate invoice totals
.
CIACCUM  ADD        STOTAL,SUBTOT
         ADD        STOTAL,TOTAL
         RETURN
.
.        Subroutine to print a data line for invoices
.
CIPRINT  COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         BRANCH     SAVTYPE OF CIPACCM,CIPMISC,CIPMISC,CIPGOVT
.
.        Print an accomodation line
.
CIPACCM  BRANCH     CACCFEE OF CIPWARD:
                               CIPWARD
         COMPARE    FIVE,CACCFEE
         GOTO       CIPATYP IF NOT EQUAL
.
         MOVE       "Unknown Clm Type    ",TDESC
         PACK       KEY5 WITH CODECL,CODE
         CALL       RDCODE1
.
         PRINT      *20,"! ",CODE,SP1,TDESC,*50,"!",*52,STOTAL,*68,"!"
         ADD        ONE,CLNO
         RETURN
.
CIPATYP  MOVE       "Unknown Adm Type    ",TDESC
         PACK       KEY5 WITH CODEA,CODE
         CALL       RDCODE1
.
         PRINT      *20,"! ",CODE,SP1,TDESC,*50,"!",*52,STOTAL,*68,"!"
         ADD        ONE,CLNO
         RETURN
.
CIPWARD  PRINT      *20,"! WARD : ",CODE,*50,"!",*52,STOTAL,*68,"!"
         ADD        ONE,CLNO
         RETURN
.
.        Print a miscellaneous item line
.
CIPMISC  PRINT      *20,"! MISC.GROUP: ",CODE,*50,"!",*52,STOTAL,*68,"!"
         ADD        ONE,CLNO
         RETURN
.
.        Print a government subsidy line
.
CIPGOVT  PRINT      *20,"! GOVERNMENT SUBSIDY",*50,"!",*52,STOTAL,*68,"!"
         ADD        ONE,CLNO
         RETURN
.
.        Print the total for this TRECTYPE
.
CITOTAL  COMPARE    "55",CLNO
         CALL       CIHEAD IF NOT LESS
.
         BRANCH     SAVTYPE OF CITACCM,CITTHEA,CITMISC
         RETURN
.
CITACCM  CALL       CIENDSCT
         PRINT      *20,"! TOTAL ACCOMODATION",*50,"!",*52,SUBTOT,*68,"!"
         CALL       CIENDSCT
         MOVE       "60",CLNO
         RETURN
.
CITTHEA  CALL       CIENDSCT
         PRINT      *20,"! TOTAL THEATRE FEES",*50,"!",*52,SUBTOT,*68,"!"
         CALL       CIENDSCT
         MOVE       "60",CLNO
         RETURN
.
CITMISC  CALL       CIENDSCT
         PRINT      *20,"! TOTAL MISCELLANEOUS CHARGES":
                    *50,"!",*52,SUBTOT,*68,"!"
         CALL       CIENDSCT
         MOVE       "60",CLNO
         RETURN
.
.        Print Invoice Heading
.
CIHEAD   ADD        ONE,CPAGENO
.
         UNPACK     FROMDATE,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,DDFORM
         MOVE       XMON,MMFORM
         MOVE       XYEAR,YYFORM
         MOVE       XCENT,CCFORM
         MOVE       XYEAR,DIM2
.
         UNPACK     TODATE,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,IDAY
         MOVE       XMON,IMON
         MOVE       XYEAR,IYEAR
         MOVE       XCENT,ICENT
.
         PRINT      *F,*1,PRGID,*25,CNAME,*88,"DATE : ",CDATE:
                    *N,*1,VERSION,*25,PRGNAM," - ",OPT1:
                    *88,"TIME :   ",CTIMEIS:
                    *N,*88,"PAGE : ",*102,CPAGENO,*N:
                    *25,"PERIOD FROM ",DDFORM,SLASH,MMFORM,SLASH,CCFORM,DIM2:
                    " TO ",IDAY,SLASH,IMON,SLASH,ICENT,XYEAR,*N
         MOVE       FIVE,CLNO
.
         CALL       SUBHEAD
.
         CALL       CIENDSCT
         PRINT      *20,"! Code",*50,"!       Amount",*68,"!"
         ADD        ONE,CLNO
         CALL       CIENDSCT
.
.        Clock the time for the next page heading
.
         CLOCK      TIME,CTIMEIS
         RETURN
.
.        Subroutine to print the end of an invoice section
.
CIENDSCT PRINT      *20,"*-----------------------------------------------*"
         ADD        ONE,CLNO
         RETURN
.
.        Subroutine to check the journal figures for the date range specified
.
CHECKJ   MOVE       "60",CLNO
.
         CALL       CRTMPIDJ         * Create temporary file
.
         MOVE       ZERO,TOTADJ
         MOVE       ZERO,TOTNORM
         MOVE       ZERO,TOTBAD
         MOVE       ZERO,TOTREB
         MOVE       ZERO,TOTAL
         MOVE       ZERO,RECFLG
.
.        Loop over Dtran file for data to be examined
.
         DISPLAY    *P1:24,*EL,*P10:24,"Date/Adm No : ";
.
.        Check if we are doing the quick check, or the slow check
.
         BRANCH     QCKJRNL,CJQSTRT     * Goto start for quick check
.
.        Loop over the systems to be checked
.
         MOVE       FSTSYS,SYSTEM       * Starting system
         GOTO       CJINLP
.
CJSYSLP  COMPARE    SYSTEM,FEDSYS       * Check if we have finished
         GOTO       CJENDLP IF EQUAL
         ADD        ONE,SYSTEM          * Get the next system
.
.        Check if we need to loop over all available sites
.
CJINLP   MOVE       SP6,OSTSITE
         BRANCH     SYSTEM,CJRDS,CJRSIT,CJRDS,CJROUT
         GOTO       CJSYSLP
.
.        Using "out" files
.
CJROUT   MOVE       "out",OSTFILE
         GOTO       CJOFIL
.
.        Loop over the site file
.
CJRSIT   MOVE       SP6,KEY6
         CALL       RDSSITA1
CJNSIT   CALL       RDKSITA1
         BRANCH     OVRCD,CJSYSLP
.
.        Check for a valid site
.
         MATCH      FSTSITE,OSTSITE
         GOTO       CJNSIT IF LESS
.
         MATCH      OSTSITE,FEDSITE
         GOTO       CJSYSLP IF LESS
.
.        Check for a valid department
.
         MATCH      FSTDEPT,OSTSYST
         GOTO       CJNSIT IF LESS
.
         MATCH      OSTSYST,FEDDEPT
         GOTO       CJNSIT IF LESS
.
.        Open the various site dependent files
.
CJOFIL   CLOSE      OUTBOKA3
         PACK       CFNAME,OSTFILE,FILBOKA3
         OPEN       OUTBOKA3,CFNAME     * Open the bookings A file
.
         CLOSE      OUTBB1A1
         PACK       CFNAME,OSTFILE,FILBB1A1
         CALL       OPOTBB11,CFNAME     * Open the bookings B file
.
         MOVE       ZERO,OVRCD
         TRAP       OVERCOND IF IO
         CLOSE      OUTDTRO1
         PACK       CFNAME,OSTFILE,FILDTRO1
         OPEN       OUTDTRO1,CFNAME     * Open the dtran file
         TRAPCLR    IO
         BRANCH     OVRCD,CJENDDT
.
         CLOSE      OUTDTRO2
         PACK       CFNAME,OSTFILE,FILDTRO2
         OPEN       OUTDTRO2,CFNAME     * Open the dtran file
         GOTO       CJRDS
.
.        End of dtran file loop. Check what we do next
.
CJENDDT  BRANCH     QCKJRNL,CJNXTJ      * Quick journal loop ?
.
         BRANCH     SYSTEM,CJSYSLP,CJNSIT,CJSYSLP,CJSYSLP
         GOTO       CJSYSLP
.
.        Initialise for quick check
.
CJQSTRT  PACK       KEY5,CODEJ,SP3
         CALL       RDCODE1
CJNXTJ   CALL       RDKCODE1                  * Get the next journal code
         BRANCH     OVRCD,CJENDLP
.
         MATCH      TCODE,CODEJ               * Make sure we still have journal
         GOTO       CJENDLP IF NOT EQUAL
.
         MOVE       ACODE,JCODE
         PACK       KEY24,JCODE,FROMDATE,SP20 * Starting key for journals file
         CALL       RDSJRNL1
         GOTO       CJLOOP
.
.        Loop over the dtran file for this system/site
.
CJRDS    PACK       PATKEY21 WITH SIX,SP20    * Key for PATDTRA2
         PACK       OUTKEY21 WITH THREE,SP20  * Key for AAEDTRE2, OUTDTRE2
         CALL       RDSDTR2
.
.        Read the next dtran or journal file record
.
CJLOOP   BRANCH     QCKJRNL,CJLOOPQ           * Quick journals
.
         CALL       RDKDTR2
         BRANCH     OVRCD OF CJENDDT
.
         COMPARE    SIX,TRECTYPE
         GOTO       CJENDDT IF NOT EQUAL
.
         DISPLAY    *P25:24,*V2LON,TFDAY,SLASH,TFMONTH,SLASH,TFYEAR:
                    SP3,TADMNO;
.
.        Check if we are using the Journal date, or the date of input
.
         PACK       XDATE,TFCENT,TFYEAR,TFMONTH,TFDAY
         REP        " 0",XDATE
.
         BRANCH     CJRNREP,CJTDTE
.
         MOVE       PTDTEFFD,XDATE
.
.        Check for a valid date
.
CJTDTE   MATCH      FROMDATE,XDATE
         GOTO       CJLOOP IF LESS
.
         MATCH      XDATE,TODATE
         GOTO       CJLOOP IF LESS
.
.        Validate this record against the invoice file
.        Validation invoices checking the admission & invoice numbers agree
.
         MOVE       TREF,KEY8
         CALL       RDINV1
         BRANCH     OVRCD OF CJNOINV
.
.        Ignore everything that is not the current site.
.
         MATCH      OSTSITE,PINVSITE
         GOTO       CJLOOP IF NOT EQUAL
.
         MATCH      PINVADM,TADMNO
         CALL       CJERR2 IF NOT EQUAL
.
.        Check if this journal was backdated
.
         MOVE       "NO       ",DANS
         MATCH      PTDTEFFD,XDATE
         GOTO       CJRDCOD IF EQUAL
.
         MATCH      SP6,PTDTEFFD
         GOTO       CJRDCOD IF EQUAL
.
.        Journal was backdated - only report it if it was entered in a
.        different month/year
.
         MOVE      PTDTEFFD,DIM6A
         MOVE      XDATE,DIM6B
         MATCH     DIM6A,DIM6B
         GOTO      CJRDCOD IF EQUAL
.
         MOVE      "YES ",DIM4A
         UNPACK    PTDTEFFD WITH XCENT,XYEAR,XMON,XDAY
         MOVE      XMON,IMON
         MOVE      XYEAR,IYEAR
         MOVE      XCENT,ICENT
         PACK      DANS WITH DIM4A,IMON,SLASH,IYEAR
.
CJRDCOD  MOVE       SP4,THCSCOD
         PACK       KEY5 WITH CODEJ,TTYPE,SP1
         CALL       RDCODE1
         COMPARE    ZERO,OVRCD
         CALL       CJERR1 IF NOT EQUAL
.
         GOTO       CJRDVIS
.
.        Quick check for journals
.
CJLOOPQ  CALL       RDKJRNL1
         BRANCH     OVRCD,CJENDLP                 * Finished file
.
         MATCH      JCODE,ACODE
         GOTO       CJENDDT IF NOT EQUAL          * Finished this code
.
         MATCH      FROMDATE,JDATE
         GOTO       CJENDDT IF LESS
.
         MATCH      JDATE,TODATE
         GOTO       CJENDDT IF LESS               * Finished this code
.
         UNPACK     JDATE,CCENT,CYEAR,CMON,CDAY
         DISPLAY    *P25:24,*V2LON,CDAY,SLASH,CMON,SLASH,CYEAR:
                    SP3,JADMN,SP2,JCODE;
.
         MOVE       JINVN,KEY8
         CALL       RDINV1
         BRANCH     OVRCD,CJNOINV
.
.        Validate system
.
         COMPARE    FSTSYS,PINVSYS
         GOTO       CJLOOP IF LESS
.
         COMPARE    PINVSYS,FEDSYS
         GOTO       CJLOOP IF LESS
.
.        Validate site
.
         MATCH      FSTSITE,PINVSITE
         GOTO       CJLOOP IF LESS
.
         MATCH      PINVSITE,FEDSITE
         GOTO       CJLOOP IF LESS
.
.        Validate the department
.
         BRANCH     PINVSYS,CJRDDT,CJCKSIT,CJRDDT,CJCKS10
.
.        Read the site record
.
CJCKSIT  MOVE       "out",OSTFILE
         MOVE       SP6,OSTSYST
         MOVE       PINVSITE,KEY6
         CALL       RDSITA1
.
         MATCH      FSTDEPT,OSTSYST
         GOTO       CJLOOPQ IF LESS
.
         MATCH      OSTSYST,FEDDEPT
         GOTO       CJLOOPQ IF LESS
.
.        Open the various site dependent files
.
         CLOSE      OUTBOKA3
         PACK       CFNAME,OSTFILE,FILBOKA3
         OPEN       OUTBOKA3,CFNAME     * Open the bookings A file
.
         CLOSE      OUTBB1A1
         PACK       CFNAME,OSTFILE,FILBB1A1
         CALL       OPOTBB11,CFNAME     * Open the bookings B file
         GOTO       CJCKS20
.
CJCKS10  MOVE       "out",OSTFILE
.
CJCKS20  MOVE       ZERO,OVRCD
         TRAP       OVERCOND IF IO
         CLOSE      OUTDTRO1
         PACK       CFNAME,OSTFILE,FILDTRO1
         OPEN       OUTDTRO1,CFNAME     * Open the dtran file
         TRAPCLR    IO
         BRANCH     OVRCD,CJLOOPQ
.
         CLOSE      OUTDTRO2
         PACK       CFNAME,OSTFILE,FILDTRO2
         OPEN       OUTDTRO2,CFNAME     * Open the dtran file
.
.        Read the dtran file record for this journal record
.
CJRDDT   PACK       KEY22,JADMN,JINVN,JTRNS
         MOVE       PINVSYS,SYSTEM
         CALL       RDDTR1
         BRANCH     OVRCD,CJNODTR
.
.        Check if this journal was backdated
.
         MOVE       "NO       ",DANS
         MATCH      PTDTEFFD,JDATE
         GOTO       CJRDVIS IF EQUAL
.
         MATCH      SP8,PTDTEFFD
         GOTO       CJRDVIS IF EQUAL
.
.        Journal was backdated - only report it if it was entered in a
.        different month/year
.
         MOVE      PTDTEFFD,DIM6A
         MOVE      JDATE,DIM6B
         MATCH     DIM6A,DIM6B
         GOTO      CJRDVIS IF EQUAL
.
         MOVE      "YES ",DIM4A
         UNPACK    PTDTEFFD WITH XCENT,XYEAR,XMON,XDAY
         MOVE      XMON,IMON
         MOVE      XYEAR,IYEAR
         MOVE      XCENT,ICENT
         PACK      DANS WITH DIM4A,IMON,SLASH,IYEAR
.
.        Make sure the visit file record exists
.
CJRDVIS  MOVE       TADMNO,PVIBILL
         MOVE       PVIBILL,KEY8
         CALL       RDVISA1
         BRANCH     OVRCD,CJNOVIS
.
.        Depending on the system, check that the patient's details exist.
.
CJRDDET  BRANCH     PINVSYS,CJAAE,CJOUT,CJINP,CJRDMAS
.
.        We have a valid invoice number. Check that the A + E number and
.        U/R numbers are valid
.
CJAAE    MOVE       PINVADM,ADANUMB
         MOVE       ADANUMB,KEY8
         CALL       RDDETA1
         BRANCH     OVRCD OF CJNOADM
.
         MOVE       ADAURNO,AURNO
         MATCH      PINVUR,ADAURNO
         CALL       CJERR3 IF NOT EQUAL
.
         GOTO       CJRDMAS
.
.        We have a valid invoice number. Check that the outpatient number
.        and U/R number are valid. Also check for a valid department.
.
CJOUT    MATCH      PVISITE,PINVSITE
         CALL       CJERR8 IF NOT EQUAL
.
.        Loop over the bookings file to find the records for this visit
.
         PACK       KEY36 WITH PVIURNO,PVIDATE,SP30
         CALL       RDSBOKA3
CJODATLP CALL       RDKBOKA3
         BRANCH     OVRCD OF CJNOADM
.
         MATCH      PVIBILL,OBAOUTNO
         GOTO       CJODATFD IF EQUAL
.
         MATCH      PVIURNO,OBAURNO
         GOTO       CJNOADM IF NOT EQUAL
.
         MATCH      PVIDATE,OBADATE
         GOTO       CJODATLP IF EQUAL
.
         GOTO       CJNOADM
.
.        We have found the Booking A file
.
CJODATFD MOVE       OBAOUTNO,KEY8
         CALL       RDBOKB1             * Get the Bookings B record
         BRANCH     OVRCD,CJNOBOKB
.
         MOVE       OBAURNO,AURNO
         MATCH      OBAURNO,PINVUR
         CALL       CJERR3 IF NOT EQUAL
.
         GOTO       CJRDMAS
.
.        We have a valid invoice number. Check that the admission number
.        and U/R numbers are valid.
.
CJINP    MOVE       PINVADM,KEY8
         CALL       RDMISS1
         BRANCH     OVRCD OF CJNOADM
.
         MATCH      PINVUR,AURNO
         CALL       CJERR3 IF NOT EQUAL
.
CJRDMAS  MOVE       PINVUR,KEY8
         CALL       RDMAST1
         BRANCH     OVRCD OF CJNOUR
.
.        We have a valid record - Accumulate totals and add to temporary file
.
CJPTOT   CMATCH     SP1,THCSCOD
         GOTO       CJNORM IF EQUAL
.
         CMATCH     "A",THCSCOD
         GOTO       CJADJ IF EQUAL
.
         CMATCH     "B",THCSCOD
         GOTO       CJBAD IF EQUAL
.
         CMATCH     "R",THCSCOD
         GOTO       CJREB IF EQUAL
.
         PACK       KEY29 WITH FIVE,TTYPE,SP4,TADMNO,TREF,TTRANSN1,SP70
         GOTO       CJWRTTMP
.
CJNORM   PACK       KEY29 WITH ONE,TTYPE,SP4,TADMNO,TREF,TTRANSN1,SP70
         SUB        TPATAMTT,TOTNORM
         GOTO       CJWRTTMP
.
CJADJ    PACK       KEY29 WITH TWO,TTYPE,SP4,TADMNO,TREF,TTRANSN1,SP70
         SUB        TPATAMTT,TOTADJ
         GOTO       CJWRTTMP
.
CJBAD    PACK       KEY29 WITH THREE,TTYPE,SP4,TADMNO,TREF,TTRANSN1,SP70
         SUB        TPATAMTT,TOTBAD
         GOTO       CJWRTTMP
.
CJREB    PACK       KEY29 WITH FOUR,TTYPE,SP4,TADMNO,TREF,TTRANSN1,SP70
         MOVE       TPATAMTP,TPATAMTT
         SUB        TPATAMTT,TOTREB
.
CJWRTTMP WRITE      WORK2,KEY29;KEY29,TPATAMTT,PSNAME,PGNAME,DANS:
                               TFMONTH,TFYEAR
.
.        Check that this record exists in the indexes of the PATDTRFD file
.
CJDCHK   PACK       KEY22 WITH TADMNO,TREF,TTRANSN1
.
         CALL       RDDTR1
         COMPARE    ZERO,OVRCD
         CALL       CJERR4 IF NOT EQUAL
.
         GOTO       CJLOOP
.
.        Invoice record not found
.
CJNOINV  CALL       CJERR5
         GOTO       CJLOOP
.
.        Admission record not found
.
CJNOVIS  CALL       CJERR0
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CJRDDET
.
.        Admission record not found
.
CJNOADM  CALL       CJERR6
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CJRDMAS
.
.        U/R record not found
.
CJNOUR   CALL       CJERR7
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CJPTOT
.
.        No dtran record found
.
CJNODTR  CALL       CJERR10
         GOTO       CJLOOP
.
.        No Booking B record
.
CJNOBOKB CALL       CJERR9
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CJPTOT
.
.        End of loop over Dtran file. Now loop over temporary file
.
CJENDLP  MOVE       ZERO,SAVTYPE
         MOVE       SP6,SAVCODE
         MOVE       "60",CLNO
.
         DISPLAY    *P1:24,*EL,*P20:24,"** Generating Report ** ";
         CALL       HITENTER
         DISPLAY    *P10:24,*EL,"Admission : ";
.
         READ       WORK2,SP30;;
CJNEXT   READKS     WORK2;XTYPE,CODE,DXADMNO,TREF,XTTRANS,TPATAMTT:
                              PSNAME,PGNAME,DANS,TFMONTH,TFYEAR
         GOTO       CJENDRP IF OVER
         MOVE       XTYPE,TYPE
         MOVE       DXADMNO,XADMNO
         MOVE       XADMNO,TADMNO
         MOVE       XTTRANS,TTRANSN1
.
         DISPLAY    *P25:24,*V2LON,TYPE,SP1,CODE,SP1,TADMNO,SP1,PSNAME;
.
         COMPARE    TYPE,SAVTYPE
         GOTO       CJCNT1 IF EQUAL
.
.        New Journal category (1=Normal, 2=Adj., 3=Bad debt, 4=Rebate, 5=Unk)
.
         COMPARE    ZERO,SAVTYPE
         GOTO       CJNEWCAT IF EQUAL
.
.        Print current totals
.
         CALL       CJSUBTOT
         CALL       CJTOTAL
.
.        Save new code and initialize variables
.
CJNEWCAT MOVE       TYPE,SAVTYPE
         MOVE       CODE,SAVCODE
         CALL       CJINITT
.
.        Check if a sub-total is needed
.
CJCNT1   MATCH      CODE,SAVCODE
         GOTO       CJCNT2 IF EQUAL
.
         CALL       CJSUBTOT
         MOVE       CODE,SAVCODE
         CALL       CJINITS
.
.        Accumulate totals and print details of this record
.
CJCNT2   CALL       CJPRINT
         CALL       CJACCUM
         GOTO       CJNEXT
.
.        End of temporary file - print final totals and kill temporary file
.
CJENDRP  COMPARE    ZERO,RECFLG
         GOTO       CJENDR1 IF EQUAL
.
         CALL       CJSUBTOT
         CALL       CJTOTAL
.
CJENDR1  COMPARE    "50",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"Total Normal Journals        : $",TOTNORM,*N:
                    *1,"Total Adjustment Journals    : $",TOTADJ,*N:
                    *1,"Total Bad Debt Journals      : $",TOTBAD,*N:
                    *1,"Total Rebate Journals        : $",TOTREB,*N,*N:
                    *1,"Grand Total                  : $",TOTAL
.
         CALL       KILLTMP
         RETURN
.
.        First stage Journal Check errors
.
CJERR0   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 0 - No record in Visit File ":
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR1   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 1 - Journal Type ",TTYPE:
                    " Not Found in Codes File ":
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR2   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 2 - Invoice record has different ":
                    "Admission number ",PINVADM:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR3   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 3 - U/R number in invoice record ":
                    PINVUR," different from details file ",AURNO:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR4   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 4 - No record in Dtran 1st index ":
                    "- Transaction ",TTRANSN1," Date ":
                    TFDAY,SLASH,TFMONTH,SLASH,TFYEAR:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR5   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 5 - No record in Invoice File ":
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR6   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 6 - No record in Admission File ":
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR7   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 7 - No record in Master File ":
                    "for U/R ",AURNO:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR8   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 8 - Invoice site ",PINVSITE:
                    " differs from Visit file ",PVISITE:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR9   COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 9 - No Booking B record":
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CJERR10  COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         PRINT      *1,"JOURNAL ERROR 10 - No Dtran record for":
                       " journal file record ",JTRNS:
                    *80,"JRNLS: Adm ",JADMN,", Invoice ",JINVN
         ADD        ONE,CLNO
         RETURN
.
.        Accumulate Payment Totals
.
CJACCUM  ADD        TPATAMTT,SUBTOT
         ADD        TPATAMTT,STOTAL
         ADD        TPATAMTT,TOTAL
         RETURN
.
.        Initialize Payment Subtotals
.
CJINITS  MOVE       ZERO,SUBTOT
         RETURN
.
.        Initialize Payment Totals
.
CJINITT  CALL       CJINITS
         MOVE       ZERO,STOTAL
         LOAD       CATEG USING SAVTYPE OF NORMAL,ADJUST,BADDEBT,REBATE,UNKNOWN
         RETURN
.
.        Print Payment Details
.
CJPRINT  COMPARE    "55",CLNO
         CALL       CJHEAD IF NOT LESS
.
         MOVE       ONE,RECFLG
         BRANCH     TOTONLY OF CJPRTR
.
         MULT       SEQ,TPATAMTT          * Make Positive
.
         MOVE       CREDIT,PMETH
         COMPARE    ZERO,TPATAMTT
         GOTO       CJPRTZ IF NOT LESS
.
         MOVE       DEBIT,PMETH
.
CJPRTZ   MOVE       TFMONTH,DIM2
         REP        " 0",DIM2
         MOVE       TFYEAR,DIM2A
         REP        " 0",DIM2A
         PRINT      *1,"!",*3,CODE,*10,"!",TADMNO:
                    *19,"!",*21,TREF,*30,"!",*32,PSNAME,PGNAME:
                    *78,"!",*80,PMETH,*87,"!",*88,TPATAMTT,*103,"!":
                    *104,TFDAY,SLASH,DIM2,SLASH,DIM2A,*113,"!":
                    *115,DANS,*129,"!"
         ADD        ONE,CLNO
CJPRTR   RETURN
.
.        Print a Subtotal Section
.
CJSUBTOT CALL       CJENDSCT
         PRINT      *1,"!",*50,"SUB-TOTAL (",SAVCODE,")":
                    *87,"!",*88,SUBTOT,*103,"!",*129,"!"
         ADD        ONE,CLNO
         CALL       CJENDSCT
.
         COMPARE    TYPE,SAVTYPE
         RETURN     IF NOT EQUAL
.
         BRANCH     TOTONLY OF CJPRTR
.
         MOVE       "60",CLNO         * force a new page for each Code
         RETURN
.
.        Print a Section Total
.
CJTOTAL  PRINT      *1,"!",*50,"TOTAL (",CATEG,")":
                    *87,"!",*88,STOTAL,*103,"!",*129,"!"
         ADD        ONE,CLNO
         CALL       CJENDSCT
.
         MOVE       "60",CLNO         * force a new page for each type
         RETURN
.
.        Print Payment Heading
.
CJHEAD   ADD        ONE,CPAGENO
.
         UNPACK     FROMDATE,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,DDFORM
         MOVE       XMON,MMFORM
         MOVE       XYEAR,YYFORM
         MOVE       XCENT,CCFORM
         MOVE       XYEAR,DIM2
.
         UNPACK     TODATE,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,IDAY
         MOVE       XMON,IMON
         MOVE       XYEAR,IYEAR
         MOVE       XCENT,ICENT
.
         PRINT      *F,*1,PRGID,*35,CNAME,*112,"DATE : ",CDATE:
                    *N,*1,VERSION,*35,PRGNAM," - ",OPT3:
                    *112,"TIME :   ",CTIMEIS:
                    *N,*112,"PAGE : ",*126,CPAGENO,*N:
                    *35,"PERIOD FROM ",DDFORM,SLASH,MMFORM,SLASH,CCFORM,DIM2:
                    " TO ",IDAY,SLASH,IMON,SLASH,ICENT,XYEAR,*N,*N:
                    *35,"Journal Type - ",CATEG,*N
         MOVE       SEVEN,CLNO
.
         CALL       SUBHEAD
.
         CALL       CJENDSCT
         PRINT      *1,"! Code",*10,"! Adm No":
                    *19,"!  Invoice",*30,"! Patient Name":
                    *78,"! Type",*87,"! Journal Amt":
                    *103,"!  Date",*113,"! Backdated ?",*129,"!"
         ADD        ONE,CLNO
         CALL       CJENDSCT
.
.        Clock the time for the next page heading
.
         CLOCK      TIME,CTIMEIS
         RETURN
.
.        End of a Payment Section
.
CJENDSCT PRINT      "*-----------------------------------------":
                    "------------------------------------------":
                    "--------------------------------------------*"
         ADD        ONE,CLNO
         RETURN
.
.        Subroutine to check the payment figures for the date range specified
.
CHECKP   MOVE       "60",CLNO
.
         CALL       CRTMPIDP         * Create temporary file
.
         MOVE       ZERO,TOTCASH
         MOVE       ZERO,TOTCHEQ
         MOVE       ZERO,TOTCRED
         MOVE       ZERO,TOTMERC
         MOVE       ZERO,TOTAL
.
         DISPLAY    *P1:24,*EL,*P10:24,"Date/Adm No : ";
.
.        Loop over the systems to be checked
.
         MOVE       FSTSYS,SYSTEM       * Starting system
         GOTO       CPINLP
.
CPSYSLP  COMPARE    SYSTEM,FEDSYS       * Check if we have finished
         GOTO       CPENDLP IF EQUAL
         ADD        ONE,SYSTEM          * Get the next system
.
.        Check if we need to loop over all available sites
.
CPINLP   MOVE       SP6,OSTSITE
         BRANCH     SYSTEM,CPRDS,CPRSIT,CPRDS,CPROUT
         GOTO       CPSYSLP
.
.        Using "out" files
.
CPROUT   MOVE       "out",OSTFILE
         GOTO       CPOFIL
.
.        Loop over the site file
.
CPRSIT   MOVE       SP6,KEY6
         CALL       RDSSITA1
CPNSIT   CALL       RDKSITA1
         BRANCH     OVRCD,CPSYSLP
.
.        Check for a valid site
.
         MATCH      FSTSITE,OSTSITE
         GOTO       CPNSIT IF LESS
.
         MATCH      OSTSITE,FEDSITE
         GOTO       CPSYSLP IF LESS
.
.        Check for a valid department
.
         MATCH      FSTDEPT,OSTSYST
         GOTO       CPNSIT IF LESS
.
         MATCH      OSTSYST,FEDDEPT
         GOTO       CPNSIT IF LESS
.
.        Open the various site dependent files
.
CPOFIL   CLOSE      OUTBOKA3
         PACK       CFNAME,OSTFILE,FILBOKA3
         OPEN       OUTBOKA3,CFNAME     * Open the bookings A file
.
         CLOSE      OUTBB1A1
         PACK       CFNAME,OSTFILE,FILBB1A1
         CALL       OPOTBB11,CFNAME     * Open the bookings B file
.
         MOVE       ZERO,OVRCD
         TRAP       OVERCOND IF IO
         CLOSE      OUTDTRO1
         PACK       CFNAME,OSTFILE,FILDTRO1
         OPEN       OUTDTRO1,CFNAME     * Open the dtran file
         TRAPCLR    IO
         BRANCH     OVRCD,CPNSIT
.
         CLOSE      OUTDTRO2
         PACK       CFNAME,OSTFILE,FILDTRO2
         OPEN       OUTDTRO2,CFNAME     * Open the dtran file
         GOTO       CPRDS
.
.        End of dtran file loop. Check what we do next
.
CPENDDT  BRANCH     SYSTEM,CPSYSLP,CPNSIT,CPSYSLP,CPSYSLP
         GOTO       CPSYSLP
.
.        Loop over Dtran file for data to be examined
.
CPRDS    PACK       PATKEY21 WITH FIVE,FROMDATE,SP20
         PACK       OUTKEY21 WITH TWO,FROMDATE,SP20
         CALL       RDSDTR2
CPLOOP   CALL       RDKDTR2
         BRANCH     OVRCD OF CPENDDT
.
         COMPARE    FIVE,TRECTYPE
         GOTO       CPENDDT IF NOT EQUAL
.
         MATCH      PTDTEFFD,TODATE
         GOTO       CPENDDT IF LESS
.
         UNPACK     PTDTEFFD,CCENT,CYEAR,CMON,CDAY
         DISPLAY    *P25:24,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    SP3,TADMNO;
.
         MATCH      "PY",TTYPE
         CALL       CPERR1 IF NOT EQUAL
.
.        Validate this record against the invoice file
.        Validation invoices checking the admission & invoice numbers agree
.
         MOVE       TREF,KEY8
         CALL       RDINV1
         BRANCH     OVRCD OF CJNOINV
.
.        Ignore everything that is not the current site.
.
         MATCH      OSTSITE,PINVSITE
         GOTO       CPLOOP IF NOT EQUAL
.
         MATCH      PINVADM,TADMNO
         CALL       CPERR2 IF NOT EQUAL
.
.        Make sure the visit file record exists
.
         MOVE       TADMNO,PVIBILL
         MOVE       PVIBILL,KEY8
         CALL       RDVISA1
         BRANCH     OVRCD,CPNOVIS
.
.        Depending on the system, check that the patient's details exist.
.
CPRDDET  BRANCH     PINVSYS,CPAAE,CPOUT,CPINP,CPOUT
.
.        We have a valid invoice number. Check that the A + E number and
.        U/R numbers are valid
.
CPAAE    MOVE       PINVADM,ADANUMB
         MOVE       ADANUMB,KEY8
         CALL       RDDETA1
         BRANCH     OVRCD OF CPNOADM
.
         MOVE       ADAURNO,AURNO
         MATCH      PINVUR,ADAURNO
         CALL       CPERR3 IF NOT EQUAL
.
         GOTO       CPRDMAS
.
.        We have a valid invoice number. Check that the outpatient number
.        and U/R number are valid.
.
CPOUT    MATCH      PVISITE,PINVSITE
         CALL       CPERR10 IF NOT EQUAL
.
.        Loop over the bookings file to find the records for this visit
.
         PACK       KEY36 WITH PVIURNO,PVIDATE,SP30
         CALL       RDSBOKA3
CPODATLP CALL       RDKBOKA3
         BRANCH     OVRCD OF CPNOADM
.
         MATCH      PVIBILL,OBAOUTNO
         GOTO       CPODATFD IF EQUAL
.
         MATCH      PVIURNO,OBAURNO
         GOTO       CPNOADM IF NOT EQUAL
.
         MATCH      PVIDATE,OBADATE
         GOTO       CPODATLP IF EQUAL
.
         GOTO       CPNOADM
.
.        We have found the Booking A file
.
CPODATFD MOVE       OBAOUTNO,KEY8
         CALL       RDBOKB1             * Get the Bookings B record
         BRANCH     OVRCD,CPNOBOKB
.
         MOVE       OBAURNO,AURNO
         MATCH      OBAURNO,PINVUR
         CALL       CPERR3 IF NOT EQUAL
.
         GOTO       CPRDMAS
.
.        We have a valid invoice number. Check that the admission number
.        and U/R numbers are valid.
.
CPINP    MOVE       PINVADM,KEY8
         CALL       RDMISS1
         BRANCH     OVRCD OF CPNOADM
.
         MATCH      PINVUR,AURNO
         CALL       CPERR3 IF NOT EQUAL
.
CPRDMAS  MOVE       PINVUR,KEY8
         CALL       RDMAST1
         BRANCH     OVRCD OF CPNOUR
.
.        We have a valid record - Accumulate totals and add to temporary file
.
CPPTOT   LOAD       FORM12P2 USING TPAYTYPE OF TOTCASH,TOTCHEQ,TOTCRED,TOTMERC
         SUB        TPATAMTT,FORM12P2
         STORE      FORM12P2 USING TPAYTYPE OF TOTCASH,TOTCHEQ,TOTCRED,TOTMERC
.
         MATCH      PINVDTE,TODATE
         GOTO       CPDEP IF LESS
.
         CMATCH     "P",TTYPEDEB
         GOTO       CPPAT IF EQUAL
.
         CMATCH     "H",TTYPEDEB
         GOTO       CPHF IF EQUAL
.
         CMATCH     "I",TTYPEDEB
         GOTO       CPINS IF EQUAL
.
         PACK       KEY41 WITH FOUR,SP6,TADMNO,TREF,TTRANSN1,SP70
         GOTO       CPWRTTMP
.
CPPAT    PACK       KEY41 WITH ONE,SP6,TADMNO,TREF,TTRANSN1,SP70
         GOTO       CPWRTTMP
.
CPHF     MOVE       TDCODE,CODE
         PACK       KEY41 WITH TWO,CODE,TADMNO,TREF,TTRANSN1,SP70
         GOTO       CPWRTTMP
.
CPINS    MOVE       TDCODE,CODE
         PACK       KEY41 WITH THREE,CODE,TADMNO,TREF,TTRANSN1,SP70
         GOTO       CPWRTTMP
.
CPDEP    UNPACK     PINVDTE WITH XCENT,XYEAR,XMON,XDAY
         PACK       KEY41 WITH FIVE,XMON,SLASH,XCENT,XYEAR,SP1:
                    TADMNO,TREF,TTRANSN1,SP70
.
CPWRTTMP MOVE       KEY41,KEY29
         WRITE      WORK3,KEY41;KEY29,TPAYTYPE,TPATAMTT,PSNAME,PGNAME,PTDTEFFD:
                                      PINVDTE,SP12
.
.        Check that this record exists in the indexes of the PATDTRFD file
.
         PACK       KEY22 WITH TADMNO,TREF,TTRANSN1
.
         CALL       RDDTR1
         COMPARE    ZERO,OVRCD
         CALL       CPERR4 IF NOT EQUAL
.
         GOTO       CPLOOP
.
.        Invoice record not found
.
CPNOINV  CALL       CPERR5
         GOTO       CPPTOT
.
.        Visit record not found
.
CPNOVIS  CALL       CPERR0
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CPRDDET
.
.        Admission record not found
.
CPNOADM  CALL       CPERR6
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CPPTOT
.
.        U/R record not found
.
CPNOUR   CALL       CPERR7
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CPPTOT
.
.        No Booking B record
.
CPNOBOKB CALL       CPERR11
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CPPTOT
.
.        End of loop over Dtran file. Now loop over temporary cash file
.
CPENDLP  MOVE       "0",CMDESTAT
         PACK       KEY33,CMDESTAT,SP70
         CALL       RSCMDEP4                      * Read through cash file
CPCPLP   CALL       RKCMDEP4
         BRANCH     OVRCD OF CPENDCLP
.
         MATCH      "0",CMDESTAT                  * deposit held?
         GOTO       CPENDCLP IF NOT EQUAL
.
         PACK       KEY17,CMDERECN,CMDETCNT
         CALL       RDRCDTE1
         BRANCH     OVRCD,CPCPLP
.
         UNPACK     CMDETDAT,XCENT,XYEAR,XMON,XDAY
         DISPLAY    *P25:24,*V2LON,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    SP3,CMDEADMN;
.
.        Make sure this cash is inside the requested range
.
         MOVE       CMDETDAT,XDATE
         REP        " 0",XDATE
.
         MATCH      FROMDATE,XDATE
         GOTO       CPCPLP IF LESS
.
         MATCH      XDATE,TODATE
         GOTO       CPCPLP IF LESS
.
.        Validate the system
.
         COMPARE    FEDSYS,FSTSYS
         IF   !@EQUAL
            GOTO   CPCPLP1
         ENDIF
.
         COMPARE    CMDESYST,FSTSYS
         GOTO       CPCPLP IF LESS
.
         COMPARE    FEDSYS,CMDESYST
         GOTO       CPCPLP IF LESS
.
.        Depending on the system, check that the patient's details exist.
.
CPCPLP1  BRANCH     CMDESYST,CPPAAE,CPPOUT,CPPINP
         GOTO       CPCPLP
.
.        Check that the A + E number and U/R numbers are valid
.
CPPAAE   MOVE       CMDEADMN,ADANUMB
         MOVE       ADANUMB,KEY8
         CALL       RDDETA1
         BRANCH     OVRCD OF CPNOCAD
.
         MOVE       ADAURNO,PURNO
         MOVE       PURNO,KEY8
         CALL       RDMAST1
         BRANCH     OVRCD,CPNOCUR
.
         GOTO       CPCPWTMP
.
.        Read the visit file record, and check that the outpatient number
.        and U/R number are valid.
.
CPPOUT   MOVE       CMDEADMN,PVIBILL
         MOVE       PVIBILL,KEY8
         CALL       RDVISA1
         BRANCH     OVRCD,CPNOCAD
.
.        Validate the site
.
         MATCH      FSTSITE,PVISITE
         GOTO       CPCPLP IF LESS
.
         MATCH      PVISITE,FEDSITE
         GOTO       CPCPLP IF LESS
.
         MOVE       SP3,OSTSYST
         MOVE       "out",OSTFILE
         MOVE       PVISITE,KEY6
         CALL       RDSITA1
.
.        Validate the department
.
         MATCH      FSTDEPT,OSTSYST
         GOTO       CPCPLP IF LESS
.
         MATCH      OSTSYST,FEDDEPT
         GOTO       CPCPLP IF LESS
.
.        Open the bookings files
.
         CLOSE      OUTBOKA3
         PACK       CFNAME,OSTFILE,FILBOKA3
         OPEN       OUTBOKA3,CFNAME     * Open the bookings A file
.
         CLOSE      OUTBB1A1
         PACK       CFNAME,OSTFILE,FILBB1A1
         CALL       OPOTBB11,CFNAME     * Open the bookings B file
.
.        Loop over the bookings file to find the records for this visit
.
         PACK       KEY36 WITH PVIURNO,PVIDATE,SP30
         CALL       RDSBOKA3
CPPDATLP CALL       RDKBOKA3
         BRANCH     OVRCD OF CPNOCAD
.
         MATCH      PVIBILL,OBAOUTNO
         GOTO       CPPDATFD IF EQUAL
.
         MATCH      PVIURNO,OBAURNO
         GOTO       CPNOCAD IF NOT EQUAL
.
         MATCH      PVIDATE,OBADATE
         GOTO       CPPDATLP IF EQUAL
.
         GOTO       CPNOCAD
.
.        We have found the Booking A file
.
CPPDATFD MOVE       OBAOUTNO,KEY8
         CALL       RDBOKB1             * Get the Bookings B record
         BRANCH     OVRCD,CPNOCAD
.
.        Read in the master file record
.
         MOVE       OBAURNO,PURNO
         MOVE       PURNO,KEY8
         CALL       RDMAST1
         BRANCH     OVRCD,CPNOCUR
.
         GOTO       CPCPWTMP
.
CPPOTH   MOVE       CMDEADMN,PVIBILL
         MOVE       PVIBILL,KEY8
         CALL       RDVISA1
         BRANCH     OVRCD,CPNOCAD
.
.        Read in the master file record
.
         MOVE       OBAURNO,PURNO
         MOVE       PURNO,KEY8
         CALL       RDMAST1
         BRANCH     OVRCD,CPNOCUR
.
         GOTO       CPCPWTMP
.
.        Validate the admission number, and read in U/R record
.
CPPINP   MOVE       CMDEADMN,KEY8
         CALL       RDMISS1
         BRANCH     OVRCD OF CPNOCAD
.
         MATCH      "0",AURNO
         GOTO       CPPREAD IF EQUAL
.
         MOVE       AURNO,KEY8
         CALL       RDMAST1
         BRANCH     OVRCD OF CPNOCUR
.
         GOTO       CPCPWTMP
.
CPPREAD  MOVE       AADMNO,KEY8
         CALL       RDPRAM1
         BRANCH     OVRCD OF CPNOCUR
.
.        Write the cash pending record to the temporary file
.
CPCPWTMP MULT       SEQ,RCDTAMNT
         PACK       KEY41 WITH FIVE,SP6,CMDEADMN,SP8,CMDETCNT,SP3,CMDERECN,SP70
.
         MOVE       CMDETDAT,PTDTEFFD
         REP        " 0",PTDTEFFD
.
.        RESET      KEY29
.        READ       WORK3,KEY29;ANS
.        GOTO       CPCPLP IF NOT OVER
.
         MOVE       KEY41,KEY29
.
         MOVE       ONE,FORM1              * set payment type to cash
         WRITE      WORK3,KEY41;KEY29,FORM1,RCDTAMNT,PSNAME,PGNAME:
                                PTDTEFFD,PINVDTE,CMDERECN
.        LOAD       FORM12P2 USING FORM1 OF TOTCASH,TOTCHEQ,TOTCRED,TOTMERC
.        SUB        CMDTAMNT,FORM12P2
.        STORE      FORM12P2 USING FORM1 OF TOTCASH,TOTCHEQ,TOTCRED,TOTMERC
.
         GOTO       CPCPLP
.
.        Admission record not found
.
CPNOCAD  CALL       CPERR8
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CPCPWTMP
.
.        U/R record not found
.
CPNOCUR  CALL       CPERR9
         MOVE       SP30,PSNAME
         MOVE       SP30,PGNAME
         GOTO       CPCPWTMP
.
.        End of cash pending file, now loop over temporary file to print report
.
CPENDCLP MOVE       ZERO,SAVTYPE
         MOVE       SP6,SAVCODE
         MOVE       "60",CLNO
         MOVE       ZERO,RECFLG
.
         DISPLAY    *P1:24,*EL,*P20:24,"** Generating Report ** ";
         CALL       HITENTER
         DISPLAY    *P10:24,*EL,"Admission : ";
.
         READ       WORK3,SP30;;
CPNEXT   READKS     WORK3;XTYPE,CODE,DXADMNO,TREF,XTTRANS,TPAYTYPE,TPATAMTT:
                              PSNAME,PGNAME,PTDTEFFD,PINVDTE
         GOTO       CPENDRP IF OVER
         MOVE       XTYPE,TYPE
         MOVE       DXADMNO,XADMNO
         MOVE       XADMNO,TADMNO
         MOVE       XTTRANS,TTRANSN1
.
         DISPLAY    *P25:24,*V2LON,TYPE,SP1,CODE,SP1,TADMNO,SP1,PSNAME;
.
         COMPARE    TYPE,SAVTYPE
         GOTO       CPCNT1 IF EQUAL
.
.        New Payment category (1=Patient, 2=H.F., 3=Ins., 4=Unk, 5=Deposit)
.
         COMPARE    ZERO,SAVTYPE
         GOTO       CPNEWCAT IF EQUAL
.
.        Print current totals
.
         CALL       CPSUBTOT
         CALL       CPTOTAL
.
.        Save new code and initialize variables
.
CPNEWCAT MOVE       TYPE,SAVTYPE
         MOVE       CODE,SAVCODE
         CALL       CPINITT
.
.        Check if a sub-total is needed
.
CPCNT1   MATCH      CODE,SAVCODE
         GOTO       CPCNT2 IF EQUAL
.
         CALL       CPSUBTOT
         MOVE       CODE,SAVCODE
         CALL       CPINITS
.
.        Accumulate totals and print details of this record
.
CPCNT2   CALL       CPPRINT
         CALL       CPACCUM
         GOTO       CPNEXT
.
.        End of temporary file - print final totals and kill temporary file
.
CPENDRP  COMPARE    ZERO,RECFLG
         GOTO       CPENDR1 IF EQUAL
.
         CALL       CPSUBTOT
         CALL       CPTOTAL
.
CPENDR1  COMPARE    "50",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"Total Cash Payments          : $",TOTCASH,*N:
                    *1,"Total Cheque Payments        : $",TOTCHEQ,*N:
                    *1,"Total Credit Card Payments   : $",TOTCRED,*N:
                    *1,"Total Merchant Card Payments : $",TOTMERC,*N,*N:
                    *1,"Grand Total                  : $",TOTAL
.
         CALL       KILLTMP
         RETURN
.
. --------------------------------------------------------------------------
.        First stage Payment Check errors
. --------------------------------------------------------------------------
CPERR0   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 0 - No record in Visit File ":
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
CPERR1   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 1 - Invalid Transaction Type ",TTYPE:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CPERR2   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 2 - Invoice record has different ":
                    "Admission number ",PINVADM:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CPERR3   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 3 - Invoice record has different ":
                    "U/R number ",PINVUR," to Details rec ",AURNO:
                    *98,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CPERR4   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 4 - No record in AAEDTRE/OUTDTRO/":
                    "PATDTRA - Transaction ",TTRANSN1," Date ":
                    TFDAY,SLASH,TFMONTH,SLASH,TFCENT,TFYEAR:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CPERR5   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 5 - No record in Invoice File ":
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CPERR6   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 6 - No record in Admission File ":
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CPERR7   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 7 - No record in Master File ":
                    "for U/R ",AURNO:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CPERR8   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 8 - No record in Admission File ":
                    *80,"CASH : Adm ",CMDEADMN,", Trans ",CMDETCNT
         ADD        ONE,CLNO
         RETURN
.
CPERR9   COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 9 - No record in Master File ":
                    "for U/R ",AURNO:
                    *80,"CASH : Adm ",CMDEADMN,", Trans ",CMDETCNT
         ADD        ONE,CLNO
         RETURN
.
CPERR10  COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 10 - Invoice site ",PINVSITE:
                    " differs from Visit file ",PVISITE:
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
CPERR11  COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         PRINT      *1,"PAYMENT ERROR 11 - No Booking B record":
                    *80,"DTRAN: Adm ",TADMNO,", Invoice ",TREF
         ADD        ONE,CLNO
         RETURN
.
.        Accumulate Payment Totals
.
CPACCUM  ADD        TPATAMTT,SUBTOT
         ADD        TPATAMTT,STOTAL
         ADD        TPATAMTT,TOTAL
         RETURN
.
.        Initialize Payment Subtotals
.
CPINITS  MOVE       ZERO,SUBTOT
         RETURN
.
.        Initialize Payment Totals
.
CPINITT  CALL       CPINITS
         MOVE       ZERO,STOTAL
         LOAD       CATEG USING SAVTYPE OF PATIENT,HEALTH,INSUR,UNKNOWN,DEPOSIT
         RETURN
.
.        Print Payment Details
.
CPPRINT  COMPARE    "55",CLNO
         CALL       CPHEAD IF NOT LESS
.
         MOVE       ONE,RECFLG
         BRANCH     TOTONLY OF CPPRTR1
.
         MULT       SEQ,TPATAMTT          * Make Positive
         MOVE       SP6,PMETH
         LOAD       PMETH USING TPAYTYPE OF CASH,CHEQUE,CREDIT,MERCH
.
         UNPACK     PTDTEFFD WITH XCENT,XYEAR,XMON,XDAY
.
         PRINT      *1,"!",*3,CODE,*10,"!",TADMNO:
                    *19,"!",*21,TREF,*30,"!",*32,PSNAME,PGNAME:
                    *78,"!",*80,PMETH,*87,"!",*88,TPATAMTT:
                    *103,"!",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,*114,"!";
         ADD        ONE,CLNO
.
.        Display the invoice date if it is before the 'Fromdate'
.
         MATCH      FROMDATE,PINVDTE
         GOTO       CPPRTR IF NOT LESS
.
         UNPACK     PINVDTE WITH XCENT,XYEAR,XMON,XDAY
         PRINT      *116,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
CPPRTR   PRINT      *128,"!"
CPPRTR1  RETURN
.
.        Print a Subtotal Section
.
CPSUBTOT CALL       CPENDSCT
         PRINT      *1,"!",*50,"SUB-TOTAL (",SAVCODE,")":
                    *87,"!",*88,SUBTOT,*103,"!",*114,"!",*128,"!"
         ADD        ONE,CLNO
         CALL       CPENDSCT
.
         COMPARE    TYPE,SAVTYPE
         RETURN     IF NOT EQUAL
.
         BRANCH     TOTONLY OF CPPRTR1
.
         MOVE       "60",CLNO         * force a new page for each Code
         RETURN
.
.        Print a Section Total
.
CPTOTAL  PRINT      *1,"!",*50,"TOTAL (",CATEG,")":
                    *87,"!",*88,STOTAL,*103,"!",*114,"!",*128,"!"
         ADD        ONE,CLNO
         CALL       CPENDSCT
.
         MOVE       "60",CLNO         * force a new page for each type
         RETURN
.
.        Print Payment Heading
.
CPHEAD   ADD        ONE,CPAGENO
.
         UNPACK     FROMDATE,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,DDFORM
         MOVE       XMON,MMFORM
         MOVE       XYEAR,YYFORM
         MOVE       XCENT,CCFORM
         MOVE       XYEAR,DIM2
.
         UNPACK     TODATE,XCENT,XYEAR,XMON,XDAY
         MOVE       XDAY,IDAY
         MOVE       XMON,IMON
         MOVE       XYEAR,IYEAR
         MOVE       XCENT,ICENT
.
         PRINT      *F,*1,PRGID,*35,CNAME,*88,"DATE - ",CDATE:
                    *N,*1,VERSION,*35,PRGNAM," - ",OPT2:
                    *88,"TIME :   ",CTIMEIS:
                    *N,*88,"PAGE : ",*102,CPAGENO,*N:
                    *35,"PERIOD FROM ",DDFORM,SLASH,MMFORM,SLASH,CCFORM,DIM2:
                    " TO ",IDAY,SLASH,IMON,SLASH,ICENT,XYEAR,*N,*N:
                    *35,"Cash Receipts ",CATEG,*N
         MOVE       SEVEN,CLNO
.
         CALL       SUBHEAD
.
         CALL       CPENDSCT
         PRINT      *1,"! Code",*10,"! Adm No":
                    *19,"!  Invoice",*30,"! Patient Name":
                    *78,"! Payment",*87,"! Payment Amt":
                    *103,"!Date",*114,"! Inv.Date",*128,"!"
         ADD        ONE,CLNO
         CALL       CPENDSCT
.
.        Clock the time for the next page heading
.
         CLOCK      TIME,CTIMEIS
         RETURN
.
.        End of a Payment Section
.
CPENDSCT PRINT      "*--------------------------------------------------":
                    "---------------------------------------------------":
                    "-------------------------*"
         ADD        ONE,CLNO
         RETURN
.
.        Subroutine to print the subheading
.
SUBHEAD  BRANCH     FSTSYS,SUBHD1,SUBHD2,SUBHD3,SUBHD4
.
.        Consolidated report
.
         PRINT      *40,"CONSOLIDATED REPORT",*N
         ADD        TWO,CLNO
         RETURN
.
.        A + E report
.
SUBHD1   PRINT      *40,"A + E REPORT",*N
         ADD        TWO,CLNO
         RETURN
.
.        Outpatient report
.
SUBHD2   PRINT      *40,"OUTPATIENT REPORT";
.
.        Check if we are doing all departments, one department, or one site
.
         MATCH      SP6,FSTSITE
         GOTO       SUBHD2A IF NOT EQUAL
.
         MATCH      SP3,FSTDEPT
         GOTO       SUBHD2B IF NOT EQUAL
.
         PRINT      *60,"- ALL DEPARTMENTS",*N
         ADD        TWO,CLNO
         RETURN
.
SUBHD2A  PRINT      *60,"- SITE ",FSTSITE,*N
         ADD        TWO,CLNO
         RETURN
.
SUBHD2B  PRINT      *60,"- DEPARTMENT ",FSTDEPT,*N
         ADD        TWO,CLNO
         RETURN
.
.        Inpatient report
.
SUBHD3   PRINT      *40,"INPATIENT REPORT",*N
         ADD        TWO,CLNO
         RETURN
.
.        Other financial report
.
SUBHD4   PRINT      *40,"OTHER FINANCIAL REPORT",*N
         ADD        TWO,CLNO
         RETURN
+
.
.         Subroutine to key in a valid date
.
KEYINDTE  KEYIN    *P17:CVERT,*EL,*DV,IDAY,"/",*DV,IMON,"/",*DV,ICENT,IYEAR:
                   *P17:CVERT,*V2LON,*+,*DE,*JR,XDAY;
          RESET    XDAY
          RETURN   IF EOS
.
          MOVE     XDAY,DDFORM
          COMPARE  ONE,DDFORM
          GOTO     INVALDAY IF LESS
.
          COMPARE  "32",DDFORM
          GOTO     INVALDAY IF NOT LESS
.
          KEYIN    *P17:CVERT,*V2LON,*EL,*DV,DDFORM,*HOFF,"/":
                   *V2LON,*+,MMFORM;
.
          COMPARE  ONE,MMFORM
          GOTO     INVALMON IF LESS
.
          COMPARE  "13",MMFORM
          GOTO     INVALMON IF NOT LESS
.
          KEYIN    *P20:CVERT,*V2LON,*DV,MMFORM,*HOFF,"/":
                   *V2LON,CCFORM,YYFORM
.
          MOVE     DDFORM,IDAY
          MOVE     MMFORM,IMON
          MOVE     YYFORM,IYEAR
          MOVE     CCFORM,ICENT
.
          RETURN
.
INVALDAY  DISPLAY  *P40:CVERT,*V2LON,*B,"** Invalid Day ** ";
          CALL     HITENTER
          GOTO     KEYINDTE
.
INVALMON  DISPLAY  *P40:CVERT,*V2LON,*B,"** Invalid Month ** ";
          CALL     HITENTER
          GOTO     KEYINDTE
+
.
.         Create a Temporary Index file for the accumulation of data
.
CRTMPIDI CALL          KILLTMP
.
.
         CLEAR       CMDLINE
         PACK        CMDLINE,ISBUILD,FNAMEI,UKEY1
         DISPLAY     *P1:12,*EF;
         EXECUTE     CMDLINE,TASKID
.
.        Check that file was created successfully
.
         TRAP        NOTMPER IF IO
         OPEN	     WORK1,FNAMEI
         TRAPCLR     IO
         RETURN
.
NOTMPER  DISPLAY     *P1:24,*EL,*B,*V2LON:
                     "** Temporary Index File ":
                     FNAMEI," Not Found ** ";
         CALL        HITENTER
         RETURN
.
.         Create a Temporary Index file for the accumulation of data
.
CRTMPIDJ CALL          KILLTMP
.
         CLEAR       CMDLINE
         PACK        CMDLINE,ISBUILD,FNAMEI,UKEY2
         DISPLAY     *P1:12,*EF;
         EXECUTE     CMDLINE,TASKID
.
.        Check that file was created successfully
.
         TRAP        NOTMPER IF IO
         OPEN	     WORK2,FNAMEI
         TRAPCLR     IO
         RETURN
.
.         Create a Temporary Index file for the accumulation of data
.
CRTMPIDP CALL          KILLTMP
.
         CLEAR       CMDLINE
         PACK        CMDLINE,ISBUILD,FNAMEI,UKEY3
         DISPLAY     *P1:12,*EF;
         EXECUTE     CMDLINE,TASKID
.
.        Check that file was created successfully
.
         TRAP        NOTMPER IF IO
         OPEN	     WORK3,FNAMEI
         TRAPCLR     IO
         RETURN
+
.
.       Deleting the temporay work file
.
KILLTMP CLOSE      WORK1
        CLOSE      WORK2
        CLOSE      WORK3
.
        CLEAR      CMDLINE
        PACK       CMDLINE,ISERASE,FNAMEI
        RESET      CMDLINE
.
        DISPLAY    *P1:12,*EF;
        EXECUTE    CMDLINE,TASKID
.
        RETURN
+
.
.         I/O routines for the Dtran files
.
RDSDTR1   BRANCH    PINVSYS,RDSAAE1,RDSOUT1,RDSINP1,RDSOUT1
.
.         A + E Dtran
.
RDSAAE1   CALL      RDSDTRE1
          RETURN
.
.         Outpatient Dtran
.
RDSOUT1   CALL      RDSDTRO1
          RETURN
.
.         Inpatient Dtran
.
RDSINP1   CALL      RDSDTRN1
          RETURN
.
RDKDTR1   BRANCH    PINVSYS,RDKAAE1,RDKOUT1,RDKINP1,RDKOUT1
.
.         A + E Dtran
.
RDKAAE1   CALL      RDKDTRE1
          MOVE      ATINVNO,TREF
          MOVE      ATPATAMT,TPATAMTT
          LOAD      TRECTYPE,ATRECTYP,THREE,FIVE,SIX,FIVE,TWO
          MOVE      ATITEMNO,TITEMNO
          RETURN
.
.         Outpatient Dtran
.
RDKOUT1   CALL      RDKDTRO1
          MOVE      OTINVNO,TREF
          MOVE      OTPATAMT,TPATAMTT
          LOAD      TRECTYPE,OTRECTYP,THREE,FIVE,SIX
          IF        OTRECTYP=6
            MOVE      TWO,TRECTYPE
          ENDIF
          MOVE      OTITEMNO,TITEMNO
          RETURN
.
.         Inpatient Dtran
.
RDKINP1   CALL      RDKDTRN1
          RETURN
.
RDDTR1    BRANCH    SYSTEM,RDAAE1,RDOUT1,RDINP1,RDOUT1
.
.         A + E Dtran
.
RDAAE1    CALL      RDDTRE1
          COMPARE   ZERO,OVRCD
          RETURN    IF NOT EQUAL
.
          MOVE      ATNUMB,TADMNO
          MOVE      ATINVNO,TREF
          MOVE      ATTRANS,TTRANSN1
          MOVE      ATPATAMT,TPATAMTT
          LOAD      TRECTYPE,ATRECTYP,THREE,FIVE,SIX,FIVE,TWO
          MOVE      ATITEMNO,TITEMNO
          UNPACK    ATDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          MOVE      ATDTEFFD,PTDTEFFD
          MOVE      ATTYPE,TTYPE
          MOVE      ANSP,TTYPEDEB
          MOVE      ATPAYTYP,TPAYTYPE
          RETURN
.
.         Outpatient Dtran
.
RDOUT1    CALL      RDDTRO1
          COMPARE   ZERO,OVRCD
          RETURN    IF NOT EQUAL
.
          MOVE      OTNUMB,TADMNO
          MOVE      OTINVNO,TREF
          MOVE      OTTRANS,TTRANSN1
          MOVE      OTPATAMT,TPATAMTT
          LOAD      TRECTYPE,OTRECTYP,THREE,FIVE,SIX
          IF        OTRECTYP=6
            MOVE      TWO,TRECTYPE
          ENDIF
          MOVE      OTITEMNO,TITEMNO
          UNPACK    OTDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          MOVE      OTDTEFFD,PTDTEFFD
          MOVE      OTTYPE,TTYPE
          MOVE      ANSP,TTYPEDEB
          MOVE      OTPAYTYP,TPAYTYPE
          RETURN
.
.         Inpatient Dtran
.
RDINP1    CALL      RDDTRN1
          RETURN
.
.         I/O routines for the Dtran files
.
RDSDTR2   BRANCH    SYSTEM,RDSAAE2,RDSOUT2,RDSINP2,RDSOUT2
.
.         A + E Dtran
.
RDSAAE2   MOVE      OUTKEY21,KEY21
          CALL      RDSDTRE2
          RETURN
.
.         Outpatient Dtran
.
RDSOUT2   MOVE      OUTKEY23,KEY23
          CALL      RDSDTRO2
          RETURN
.
.         Inpatient Dtran
.
RDSINP2   MOVE      PATKEY23,KEY23
          CALL      RDSDTRN2
          RETURN
.
RDKDTR2   BRANCH    SYSTEM,RDKAAE2,RDKOUT2,RDKINP2,RDKOUT2
.
.         A + E Dtran
.
RDKAAE2   CALL      RDKDTRE2
          MOVE      ATNUMB,TADMNO
          MOVE      ATINVNO,TREF
          MOVE      ATTRANS,TTRANSN1
          MOVE      ATPATAMT,TPATAMTT
          LOAD      TRECTYPE,ATRECTYP,THREE,FIVE,SIX,FIVE,TWO
          MOVE      ATITEMNO,TITEMNO
          UNPACK    ATDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          MOVE      ATDTEFFD,PTDTEFFD
          MOVE      ATTYPE,TTYPE
          MOVE      ANSP,TTYPEDEB
          MOVE      ATPAYTYP,TPAYTYPE
          RETURN
.
.         Outpatient Dtran
.
RDKOUT2   CALL      RDKDTRO2
          MOVE      OTNUMB,TADMNO
          MOVE      OTINVNO,TREF
          MOVE      OTTRANS,TTRANSN1
          MOVE      OTPATAMT,TPATAMTT
          LOAD      TRECTYPE,OTRECTYP,THREE,FIVE,SIX
          IF        OTRECTYP=6
            MOVE      TWO,TRECTYPE
          ENDIF
          MOVE      OTITEMNO,TITEMNO
          UNPACK    OTDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          MOVE      OTDTEFFD,PTDTEFFD
          MOVE      OTTYPE,TTYPE
          MOVE      ANSP,TTYPEDEB
          MOVE      OTPAYTYP,TPAYTYPE
          RETURN
.
.         Inpatient Dtran
.
RDKINP2   CALL      RDKDTRN2
          RETURN
+
          INC       STD001IO
.
          INC       PATCODKY
          INC       PATSELFN
          INC       PATSTRYR
          INC       PATMCHRD                * Miscellaneous Charges read routine
          INC       PATITMRD
.
          INC       AAEDE1IO/INC
          INC       AAEDTRIO/INC
          INC       COMDEPIO/INC
.
          INC       IBASECIO/INC
          INC       IBAPASIO/INC
.
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
.
          INC       PATCODIO/INC
          INC       PATDRGIO/INC
          INC       PATDTRIO/INC
          INC       PATINVIO/INC
          INC       PATITMIO/INC
          INC       PATJRNIO/INC
          INC       PATMCHIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       RCPDTEIO/INC
          INC       RCPBDTIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
.
ENDIT     CHAIN     PGM
          STOP

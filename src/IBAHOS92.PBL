******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAHOS92                                                 *
.* Description    :  Upload Revenue breakdown                                 *
.******************************************************************************
.* Date           :  08/04/2011                                               *
.* Author         :  J.Tan                                                    *
.* Function       :  Upload Revenue breakdown                                 *
.* Modifications  :                                                           *
.*       V10.15.01  28/01/2020 J.Tan    TSK 0880801                           *
.*                  Fixed Upload by Health Fund group                         *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC            * Codes file
          INC       PATCMCFD/INC            * Casemix Code file
          INC       PATFN1FD/INC            * Health fund file
          INC       PATFX1FD/INC            * Health fund file
          INC       PATHGPFD/INC            * Health fund group
          INC       PMSCIDFD/INC            * Contract ID file
          INC       PATOVBFD/INC            * Overnight revenue breakdown file
          INC       PATSRBFD/INC            * Daycase revenue breakdown file
          INC       PATHLFFD/INC            * Overnight lumpsum file
          INC       PATLSDFD/INC            * Daycase lumpsum file
.
.         Temporary file for Revenue breakdown records which
.         are loaded from the upload file provided by the customer (PROC0000)
.
.         Revenue breakdown Upload file
.
UREVBTXT  FILE       HL7, FIXED=1000        * Upload file for revenue breakdown
.
.Name     Type     Size     Start
.----     ----     ----     -----
XCNTID    DIM         6     Contract ID
XTYPCD    DIM         1     Type of Breakdown O=Overnight S=Sameday
XCLMCD    DIM         3     Claim Code
XHFCOD    DIM         6     Health Fund code
XHFTYP    DIM         3     Health Fund table type
XCASCD    DIM         9     Casemix code
XRBCOD    DIM         3     Revenue breakdown code
XRBAMT    FORM        5.2   Amount
.
.End of Record
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      8
CONTRCID  DIM       6
CMDLINE   DIM       127
ERRCOUNT  FORM      8
ERRORMSG  DIM       35
ERRORLNE  DIM       50
FNAMEI    DIM       150
HEADFLAG  FORM      1
KYCLAIM   DIM       3
KYHFGRP   DIM       6
KYHFUND   DIM       6
KYHFTAB   FORM      1
LINECNTR  FORM      8
LUMPAMNT  FORM      12.2
NOFEEFLG  FORM      1
PATHNAME  DIM       150
SAVKEY20  DIM       20
SKEY27    DIM       27
TOTLSAMT  FORM      12.2
USERID    DIM       10
VALDPASS  FORM      1
ZERO8     INIT      "0000    "
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
XFLD0006  DIM       30
XFLD0007  DIM       30
XFLD0008  DIM       30
.
PRGID     INIT      "IBAHOS92"
PRGNAM    INIT      "Upload Revenue Breakdown"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initailize variables & open files
          CALL      KOPT0000                * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
ML1000    CALL      KASC0000                * Keyin ascii file
          BRANCH    EXIT,ML9900
.
          CALL      OPTN0000                * Choose options
          BRANCH    EXIT,ML9999
.
ML1100    CALL      SCRD0000                * Display screen
.
          CALL      KFLD0000                * Keyin fields
          BRANCH    EXIT,ML0000
.
          CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000                * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      SFLD0000
          BRANCH    EXIT,ML1100
.
          CALL      PRHD0000                * Print header
.
          CALL      CTOT0000                * Clear total vars
.
          MOVE      ONE,VALDPASS            * Validation Pass Set to Yes
          CALL      POST0000         
.
          IF        ERRCOUNT=0
            MOVE      ZERO,VALDPASS         * If no errors proceed         
            CALL      POST0000              * with Updating file
          ENDIF
.
          CALL      PTOT0000                * Print report totals
.
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patfn1af"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATFN1A3,"patfn1af"
.
          DISPLAY   *P64:24,"pathgrpf"
          OPEN      PATHGRP1,"pathgrpf"
.
          DISPLAY   *P64:24,"patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P64:24,"pmscidaf";
          OPEN      PMSCIDA1,"pmscidaf"
.
          DISPLAY   *P64:24,"patovbaf";
          OPEN      PATOVBA1,"patovbaf"
.
          DISPLAY   *P64:24,"patsrbaf";
          OPEN      PATSRBA1,"patsrbaf"
.
          DISPLAY   *P64:24,"pathlfaf";
          OPEN      PATHLFA1,"pathlfaf"
.
          DISPLAY   *P64:24,"patlsdaf";
          OPEN      PATLSDA1,"patlsdaf"
.
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Choose Upload or Update files                   *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload Revenue Breakdown";
          DISPLAY   *P1:9,"Select Option : ";
.
KOPT1000  KEYIN     *P17:9,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000                                   *
.*                            Choose HF group or HF code                      *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = By Health fund Group":
                    *P1:8,*V2LON,"2",*HOFF," = By Health fund":
                    *P1:9,*V2LON,"3",*HOFF," = Default Health fund/table";
          DISPLAY   *P1:10,"Select Option : ";
.
OPTN1000  KEYIN     *P17:10,*V2LON,OPTION;
          BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"Bed Fees Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      UREVBTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                 KFLD0000                                   *
.*                            Keyin Fields                                    *
.******************************************************************************
KFLD0000  CALL      IVAR0000                * Initialise variables
.
          CALL      KCNT0000                * Keyin Contract ID
          BRANCH    EXIT,KFLD9999
.
          CALL      KCLM0000                * Keyin claim code
          BRANCH    EXIT,KFLD9999
.
          COMPARE   THREE,OPTION
          GOTO      KFLD2000 IF EQUAL       * default hf/table
.
          CALL      KFND0000                * Keyin HF code/ HF Group
          BRANCH    EXIT,KFLD9999
.
          CALL      KTAB0000                * Keyin health fund table 
          BRANCH    EXIT,KFLD9999           * exit
.
KFLD2000
.
          MOVE      ZERO,EXIT
KFLD9999  RETURN
+
.******************************************************************************
.*                                 IVAR0000                                   *
.*                            Initialise variable                             *
.******************************************************************************
IVAR0000  MOVE      SP70,KYCLAIM
          MOVE      SP70,KYHFGRP
          MOVE      SP70,KYHFUND
          MOVE      ZERO,KYHFTAB
IVAR9999  RETURN
+
.******************************************************************************
.*                                 SCRD0000                                   *
.*                            Screen display                                  *
.******************************************************************************
SCRD0000  DISPLAY   *P1:11,*EF:
                    *P1:11," 1. Contract id        :":
                    *P1:12," 2. Claim Code         :";
          COMPARE   THREE,OPTION
          GOTO      SCRD3000 IF NOT EQUAL
.
          DISPLAY   *P1:15," 3. Keyin User id      :":
                    *P1:16," 4. Keyin Path name    :";
          GOTO      SCRD9999
.
SCRD3000  IF        OPTION=1
            DISPLAY   *P1:13," 3. Health Fund Group  :";
          ELSE
            DISPLAY   *P1:13," 3. Health Fund Code   :";
          ENDIF
.
SCRD4000  DISPLAY   *P1:14," 4. Health Fund Table  :":
                    *P1:15," 5. Keyin User id      :":
                    *P1:16," 6. Keyin Path name    :";
SCRD9999  RETURN
+
.******************************************************************************
.*                                 KCNT0000                                   *
.*                            Keyin Contract ID                               *
.******************************************************************************
KCNT0000  KEYIN     *P26:11,*EL,*V2LON,CONTRCID;
          ENDSET     CONTRCID
          APPEND     SP70,CONTRCID
          RESET      CONTRCID
.
          MATCH      SP70,CONTRCID
          GOTO       KCNT9000 IF EQUAL
.
          MOVE       ZERO,EXIT
          GOTO       KCNT9999
.
KCNT9000  MOVE       ONE,EXIT
KCNT9999  RETURN
+
.******************************************************************************
.*                                 KCLM0000                                   *
.*                            Keyin Claim code                                *
.******************************************************************************
KCLM0000  MOVE      SP10,KYCLAIM
          MOVE      ZERO,EXIT
          KEYIN     *P26:12,*EL,*V2LON,KYCLAIM;
          ENDSET    KYCLAIM
          APPEND    SP3,KYCLAIM
          RESET     KYCLAIM
.
          MATCH     SP3,KYCLAIM
          GOTO      KCLM9000 IF EQUAL
.
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,KYCLAIM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,KCLM2000
.
          DISPLAY   *P26:12,*V2LON,KYCLAIM,*P30:12,TDESC;
          MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Claim code.  ";
          CALL      HITENTER
          GOTO      KCLM0000
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.******************************************************************************
.*                                 KFGR0000                                   *
.*                            Keyin Health fund group                         *
.******************************************************************************
KFGR0000  MOVE      SP10,KYHFGRP
          MOVE      ZERO,EXIT
          KEYIN     *P26:13,*EL,*V2LON,KYHFGRP;
          ENDSET    KYHFGRP
          APPEND    SP3,KYHFGRP
          RESET     KYHFGRP
.
          MATCH     SP3,KYHFGRP
          GOTO      KFGR9000 IF EQUAL
.
          PACK      KEY6,KYHFGRP
          CALL      RDPAHGP1
          BRANCH    OVRCD,KFGR2000
.
          DISPLAY   *P26:13,*V2LON,KYHFGRP,*P30:13,PTHGDESC;
          GOTO      KFGR8000
.
KFGR2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Health fund Group code.  ";
          CALL      HITENTER
          GOTO      KFGR0000
.
KFGR8000  MOVE      ZERO,EXIT
          GOTO      KFGR9999
.
KFGR9000  MOVE      ONE,EXIT
KFGR9999  RETURN
+
.******************************************************************************
.*                                 KHFN0000                                   *
.*                            Keyin Health fund code                          *
.******************************************************************************
KHFN0000  MOVE      SP10,KYHFUND
          MOVE      ZERO,EXIT
.
          KEYIN     *P26:13,*EL,*EL,*V2LON,KYHFUND;
          RESET     KYHFUND
          GOTO      KHFN9999 IF EOS
.
          ENDSET    KYHFUND
          APPEND    SP6,KYHFUND
          RESET     KYHFUND
.
          CMATCH    EXITCHAR,KYHFUND
          GOTO      KHFN9000 IF EQUAL
.
          MATCH     SP6,KYHFUND
          GOTO      KHFN9200 IF EQUAL
.
          PACK      KEY14,KYHFUND,ZERO8
          CALL      RDFUND1
          BRANCH    OVRCD,KHFN2000
.
          DISPLAY   *P26:13,*V2LON,KYHFUND,*P30:13,HFNAME;
          GOTO      KHFN9999
.
KHFN2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Health Fund code.  "
          CALL      HITENTER
          GOTO      KHFN0000        
.
KHFN9000  MOVE      ONE,EXIT        * exit char. entered
          GOTO      KHFN9999
.
KHFN9200  MOVE      TWO,EXIT        * spaces entered
          GOTO      KHFN9999
.
KHFN9999  RETURN
+
.******************************************************************************
.*                                 KTAB0000                                   *
.*                            Keyin HF Table                                  *
.******************************************************************************
KTAB0000  MOVE      ZERO,KYHFTAB
          MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"1 = All, 2 = As per Upload";
          KEYIN     *P26:14,*EL,*V2LON,KYHFTAB;
          BRANCH    KYHFTAB,KTAB1000,KTAB1000
          DISPLAY   *P1:24,*EL,*B,*V2LON,"Must be one or two.  ";
          CALL      HITENTER
          GOTO      KTAB0000
.
KTAB1000  IF        KYHFTAB=1
            MOVE      "All                ",TDESC
          ELSE
            MOVE      "As Per Upload      ",TDESC
          ENDIF
          DISPLAY   *P26:14,*V2LON,KYHFTAB,*P36:14,TDESC;
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      KTAB9999
.
KTAB9000  MOVE      ONE,EXIT
KTAB9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P26:15,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:16,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  SFLD0000                                  *
.*                        Select Field                                        *
.******************************************************************************
SFLD0000  CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SFLD9999 IF EQUAL
          GOTO      SFLD1000 IF LESS
.
          MOVE      ZERO,EXIT
          IF        OPTION=3
            PERFORM   CCITEM,KCNT0000:             * Keyin contract
                             KCLM0000:             * Keyin claim code
                             KUSR0000:             * Keyin user id
                             KPTH0000              * Keyin pathname
          ELSE
            PERFORM   CCITEM,KCNT0000:             * Keyin contract
                             KCLM0000:             * Keyin claim code
                             KFND0000:             * Keyin Health fund
                             KTAB0000:             * Keyin health fund table 
                             KUSR0000:             * Keyin user id
                             KPTH0000              * Keyin pathname
          ENDIF
          BRANCH    EXIT,SFLD9999
          GOTO      SFLD0000
.
SFLD1000  MOVE      ONE,EXIT
SFLD9999  RETURN
+
.******************************************************************************
.         Keyin health fund group or health fund code
.******************************************************************************
KFND0000  IF        OPTION=1
            CALL      KFGR0000                * Keyin health fund group code
          ELSE
            CALL      KHFN0000                * Keyin health fund code
          ENDIF
KFND9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.*                        Post record                                         *
.******************************************************************************
POST0000  BRANCH    OPTION,POST2000     * by health fund group
.
          IF        OPTION=3
            MOVE      SP70,KYHFUND      * blank health fund
          ENDIF
          CALL      GFND0000            * Generate fees for the health fund
          GOTO      POST9999
.
.         Get health funds for the HF group
.
POST2000  MOVE      SP70,SAVKEY20
          PACK      KEY20,KYHFGRP,SP70
          CALL      RDSFUND3
POST4000  CALL      RDKFUND3
          BRANCH    OVRCD,POST9999
          MATCH     KYHFGRP,PTHFHGRP    * Same group?
          GOTO      POST9999 IF NOT EQUAL
          MATCH     "0000    ",HFTABL
          GOTO      POST4000 IF NOT EQUAL
.
          MOVE      HCODE,KYHFUND
          PACK      SAVKEY20,PTHFHGRP,HCODE,HFTABL,SP70
.
          CALL      GFND0000            * Generate fees for the health fund
.
          MOVE      SAVKEY20,KEY20
          CALL      RDFUND3             * Reposition
.
          GOTO      POST4000
.
POST9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Upload Revenue Breakdown":
                    *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"Contrac ID : ",CONTRCID
.
          PRINT     *N,"Claim Code : ",KYCLAIM
.
          IF        OPTION=1
            PRINT     *N,"By Health Fund Group : ",KYHFGRP
          ELSE
            IF        OPTION=2
              PRINT     *N,"By Health Fund  : ",KYHFUND
            ELSE
              PRINT     *N,"Default Health Fund/table"
            ENDIF
          ENDIF
.
          IF        KYHFTAB=1
            MOVE      "All          ",KEY15
          ELSE
            MOVE      "As Per Upload",KEY15
          ENDIF
          PRINT     *N,"Health Fund Table          : ",KEY15:
                    *N,"Bed Fees Load file         : ":
                    *N,"     ",PATHNAME
.
          CALL      UND132
.
          PRINT     *1,"| Line",*12,"| Error ",*50,"| Record Detail":
                    *132,"|"
          CALL      UND132
.
PRHD9999  RETURN
+
.******************************************************************************
.*                                  GFND0000                                  *
.*                        Generate Fees of a health fund                      *
.******************************************************************************
GFND0000  MOVE      ZERO,LINECNTR
          MOVE      ONE,HEADFLAG           * default to header record
          MOVE      ZERO,LUMPAMNT          * Initialise lumpsum amount
          MOVE      ZERO,TOTLSAMT          * accummulated total amt of Lumpsum
          MOVE      ZERO,NOFEEFLG
.
          OPEN      UREVBTXT,FNAMEI
.
GFND1000  READ      UREVBTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008
          GOTO      GFND8000 IF OVER
.
.         If Contract ID, claim code, health fund table/type and Casemix code 
.         are blank, then it must be the subsequence breakdown code
.
          IF        OPTION=3
            MOVE      SP70,XFLD0004
            MOVE      SP70,XFLD0005
          ENDIF
.
          CLEAR     KEY28
          APPEND    XFLD0001,KEY28
          APPEND    XFLD0002,KEY28
          APPEND    XFLD0003,KEY28
          APPEND    XFLD0004,KEY28
          APPEND    XFLD0005,KEY28
          APPEND    XFLD0006,KEY28
          APPEND    SP70,KEY28
          RESET     KEY28
.
          MATCH     SP70,KEY28
          GOTO      GFND1200 IF NOT EQUAL
.
          PACK      XRBCOD,XFLD0007,SP70
          MATCH     SP70,XRBCOD
          GOTO      GFND1000 IF EQUAL
.
          COMPARE   ZERO,LINECNTR
          GOTO      GFND1000 IF EQUAL     * missing header record
.
.         Previous record must be a header record
.
          IF        HEADFLAG=1
            MOVE      ZERO,HEADFLAG       * flag for subsequence rev.breakdown
          ELSE
            IF        NOFEEFLG=1
              ADD       ONE,LINECNTR      * Text file line counter for error
              GOTO      GFND1000
            ENDIF
          ENDIF
          GOTO      GFND2000
.
GFND1200  MOVE      ONE,HEADFLAG
          IF        OPTION=1
            PACK      XHFCOD,XFLD0004,SP70
            MATCH     KYHFUND,XHFCOD
            GOTO      GFND1000 IF NOT EQUAL
          ENDIF
          CALL      DREV0000              * deleting existing revenue breakdown
.
GFND2000  ADD       ONE,LINECNTR          * Text file line counter for error
.
          IF        VALDPASS=1 & HEADFLAG=1
            SUB       ONE,LINECNTR        * Text file line counter for error
            CALL      CHRV0000            * Check if total rev=lumpsum amt
            ADD       ONE,LINECNTR        * Text file line counter for error
            CALL      GLSA0000            * get lumpsum amount
            IF        EXIT=1
              MOVE      ZERO,HEADFLAG
              GOTO      GFND1000
            ENDIF
          ENDIF
          CALL      VFLD0000            * Validation 
.
          COMPARE   ONE,HEADFLAG
          GOTO      GFND3600 IF NOT EQUAL * subsequent revenue breakdown code
.
          PACK      XCNTID,XFLD0001,SP70
          MATCH     XCNTID,CONTRCID
          IF        !@EQUAL
            MOVE      "Contract Id Mismatch",ERRORMSG
            CALL      PERR0000
            GOTO      GFND1000
          ENDIF
.
          PACK      XTYPCD,XFLD0002,SP70
          PACK      XCLMCD,XFLD0003,SP70
          PACK      XHFCOD,XFLD0004,SP70
.
          MATCH     XCLMCD,KYCLAIM
          IF        !@EQUAL
            MOVE      "Claim Code Mismatch",ERRORMSG
            CALL      PERR0000
            GOTO      GFND1000
          ENDIF
.
          BRANCH    OPTION,GFND2100,GFND2200,GFND2400
          GOTO      GFND1000
.
GFND2100  PACK      XHFCOD,KYHFUND,SP70
          PACK      XHFTYP,XFLD0005,SP70
          GOTO      GFND3000
.
GFND2200  MATCH     XHFCOD,KYHFUND
          IF        !@EQUAL
            MOVE      "Health Fund Mismatch",ERRORMSG
            CALL      PERR0000
            GOTO      GFND1000
          ENDIF
.
          PACK      XHFTYP,XFLD0005,SP70
          GOTO      GFND3000
.
GFND2400  MOVE      SP70,XHFCOD
          MOVE      SP70,XHFTYP
.
GFND3000  PACK      XCASCD,XFLD0006,SP70
.
GFND3600  PACK      XRBCOD,XFLD0007,SP70
          MOVE      XFLD0008,XRBAMT
          ADD       XRBAMT,TOTLSAMT
.
          COMPARE   THREE,OPTION
          GOTO      GFND3800 IF EQUAL       * Default blank HF/table
.
          BRANCH    KYHFTAB,GFND4000        * All Health fund table
.
.         as per Upload file
.
GFND3800  IF        VALDPASS=0
            CALL      WRRB0000                  * Write to revenue breakdown
          ENDIF
          GOTO      GFND1000
.
.         All health fund table type of the health fund
.
GFND4000  PACK      KEY14,KYHFUND,SP70
          CALL      RDSFUND1
GFND5000  CALL      RDKFUND1
          BRANCH    OVRCD,GFND1000
          MATCH     KYHFUND,HCODE
          GOTO      GFND1000 IF NOT EQUAL
.
          MATCH     "0000    ",HFTABL
          GOTO      GFND5000 IF EQUAL  
.
          BRANCH    PHFTACT,GFND5000        * Inactive
.
          MOVE      PTHFBCAT,XHFTYP         * Health fund Table type
          IF        VALDPASS=0
            CALL      WRRB0000                  * Write to revenue breakdown
          ENDIF
          GOTO      GFND5000                * get next record
.
GFND8000  COMPARE   ONE,VALDPASS
          GOTO      GFND9000 IF NOT EQUAL
.
          CALL      CHRV0000              * Check if total rev=lumpsum amt
.
GFND9000  CLOSE     UREVBTXT
.
GFND9999  RETURN
+
.******************************************************************************
.*                                  CHRV0000                                  *
.*                     Check if total revenue equals to lumpsum amount        *
.******************************************************************************
CHRV0000  BRANCH    NOFEEFLG,CHRV9000
.
.         If we are doing validation, check if the lumpsum amount
.         equals to the total amount of all breakdown codes
.
          COMPARE   ONE,LINECNTR
          GOTO      CHRV9999 IF EQUAL         * first record
.
          COMPARE   TOTLSAMT,LUMPAMNT
          GOTO      CHRV9000 IF EQUAL
.
          MOVE      "Total Revenue Not equal to Lumpsum.",ERRORMSG
          CALL      PERR0000
.
CHRV9000  MOVE      ZERO,TOTLSAMT
CHRV9999  RETURN
+
.******************************************************************************
.*                                  GLSA0000                                  *
.*                     Get Lumpsum amount                                     *
.******************************************************************************
GLSA0000  MOVE      ZERO,EXIT
          MOVE      ZERO,NOFEEFLG
          PACK      KEY27,XCNTID,XCLMCD,XHFCOD,XHFTYP,XCASCD,SP70
          MATCH     "O",XTYPCD
          GOTO      GLSA2000 IF NOT EQUAL     * Daycase
.
.         Get the lumpsum amount for Overnight
.
          CALL      RDPTHLF1
          BRANCH    OVRCD,GLSA9000            * Missing overnight lumpsum record
.
          MOVE      PTHLLREB,LUMPAMNT
          ADD       PTHLLPAT,LUMPAMNT
          GOTO      GLSA9999
.
.         Get the lumpsum amount for Daycase
.
GLSA2000  CALL      RDPTLSD1
          BRANCH    OVRCD,GLSA9000            * Missing daycase lumpsum record
.
          MOVE      PTLSLREB,LUMPAMNT
          ADD       PTLSLPAT,LUMPAMNT
          GOTO      GLSA9999
.
GLSA9000  PACK      ERRORLNE,XCNTID,SP1,XTYPCD,SP1,XCLMCD,SP1,XHFCOD,SP1,XHFTYP:
                             SP1,XCASCD,SP70
          MATCH     "O",XTYPCD
          IF        @EQUAL
            MOVE      "Missing Overnight fees",ERRORMSG
          ELSE
            MOVE      "Missing Daycase fees",ERRORMSG
          ENDIF
          MOVE      ONE,NOFEEFLG
          CALL      PERR0000
          MOVE      ZERO,HEADFLAG
          MOVE      ONE,EXIT
GLSA9999  RETURN
+
.******************************************************************************
.         Write Revenue breakdown record
.******************************************************************************
WRRB0000  BRANCH    VALDPASS,WRRB9999
.
          PACK      KEY30,XCNTID,XCLMCD,XHFCOD,XHFTYP,XCASCD:
                          XRBCOD,SP70
          MATCH     "O",XTYPCD
          GOTO      WRRB2000 IF NOT EQUAL     * Daycase
.
.         Overnight
.
          UNPACK    KEY30,PTOBCNID,PTOBCODE,PTOBFUND,PTOBTBLT,PTOBCSCD:
                          PTOBBRCD
          CALL      RDPTOVB1
          IF        OVRCD=1
            MOVE      XRBAMT,PTOBAMNT
            CALL      WRPTOVB1
          ELSE
            MOVE      XRBAMT,PTOBAMNT
            CALL      UPPTOVB1
          ENDIF
          ADD       ONE,ADDCOUNT
          GOTO      WRRB9999
.
.         Daycase
.
WRRB2000  UNPACK    KEY30,PTSBCNID,PTSBCODE,PTSBFUND,PTSBTBLT,PTSBCSCD:
                          PTSBBRCD
          CALL      RDPTSRB1
          IF        OVRCD=1
            MOVE      XRBAMT,PTSBAMNT
            CALL      WRPTSRB1
          ELSE
            MOVE      XRBAMT,PTSBAMNT
            CALL      UPPTSRB1
          ENDIF
          ADD       ONE,ADDCOUNT
.
WRRB9999  RETURN
+
.******************************************************************************
.*                                  DREV0000                                  *
.*                     Delete existing revenue breakdown record               *
.******************************************************************************
DREV0000  PACK      XCNTID,XFLD0001,SP70
          PACK      XTYPCD,XFLD0002,SP70
          PACK      XCLMCD,XFLD0003,SP70
          PACK      XHFCOD,XFLD0004,SP70
          PACK      XHFTYP,XFLD0005,SP70
          PACK      XCASCD,XFLD0006,SP70
.
          BRANCH    VALDPASS,DREV9999           * validating only
.
          PACK       KEY27,XCNTID,XCLMCD,XHFCOD,XHFTYP,XCASCD,SP70
          MATCH      "O",XTYPCD
          GOTO       DREV6000 IF NOT EQUAL       * must be daycase
.
.         Overnight
.
DREV1000  PACK       KEY30,KEY27,SP70
          CALL       RSPTOVB1
          CALL       RKPTOVB1
          BRANCH     OVRCD,DREV9999
.
          PACK       SKEY27,PTOBCNID,PTOBCODE,PTOBFUND,PTOBTBLT,PTOBCSCD,SP70
          MATCH      KEY27,SKEY27
          GOTO       DREV9999 IF NOT EQUAL
.
          PACK       KEY30,KEY27,PTOBBRCD,SP70
          CALL       DEPTOVB1
          GOTO       DREV1000
.
.         Daycase
.
DREV6000  PACK       KEY30,KEY27,SP70
          CALL       RSPTSRB1
          CALL       RKPTSRB1
          BRANCH     OVRCD,DREV9999
.
          PACK       SKEY27,PTSBCNID,PTSBCODE,PTSBFUND,PTSBTBLT,PTSBCSCD,SP70
          MATCH      KEY27,SKEY27
          GOTO       DREV9999 IF NOT EQUAL
.
          PACK       KEY30,KEY27,PTSBBRCD,SP70
          CALL       DEPTSRB1
          GOTO       DREV6000
.
DREV9999  RETURN
+
.******************************************************************************

.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibahos92.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
. Validate bed fee input text file fields
.******************************************************************************
VFLD0000  MOVE      ZERO,EXIT
          COMPARE   ONE,VALDPASS
          GOTO      VFLD9999 IF NOT EQUAL         * Not doing validation
          COMPARE   ONE,HEADFLAG
          GOTO      VFLD8000 IF NOT EQUAL         * not a header record
.
          PACK      XCNTID,XFLD0001,SP70
          PACK      XTYPCD,XFLD0002,SP70
          PACK      XCLMCD,XFLD0003,SP70
          PACK      XHFCOD,XFLD0004,SP70
          PACK      XHFTYP,XFLD0005,SP70
          PACK      XCASCD,XFLD0006,SP70
          PACK      XRBCOD,XFLD0007,SP70
          MOVE      XFLD0008,XRBAMT
          PACK      ERRORLNE,XCNTID,SP1,XTYPCD,SP1,XCLMCD,SP1,XHFCOD,SP1,XHFTYP:
                             SP1,XCASCD,SP70
.
          MATCH     SP70,XFLD0001
          IF        !@EQUAL
            PACK      KEY6,XFLD0001,SP70
            CALL      RDPMCID1
            IF        OVRCD=1
              MOVE      "Invalid Contract Id",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MATCH     "O",XTYPCD
          GOTO      VFLD1000 IF EQUAL        * Overnight
.
          MATCH     "S",XTYPCD
          GOTO      VFLD1000 IF EQUAL        * Sameday
.
          MOVE      "Invalid Type of Contract",ERRORMSG
          CALL      PERR0000
          MOVE      ONE,EXIT
.
VFLD1000  MATCH     SP70,XCLMCD
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,XCLMCD,SP70
            CALL      RDCODE1      
            IF        OVRCD=1
              MOVE      "Invalid Claim Code",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MATCH     SP70,XHFCOD
          IF        !@EQUAL
            PACK      KEY14,XHFCOD,ZERO8,SP70
            CALL      RDFUND1
            IF        OVRCD=1
              MOVE      "Invalid Health Fund",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MATCH     SP70,XHFTYP
          IF        !@EQUAL
            PACK      KEY5,ANSH,ANST,XHFTYP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Health Fund Table Type",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MATCH     SP70,XCASCD
          IF        !@EQUAL
            PACK      KEY9,XCASCD,SP70
            CALL      RDPTCMC1
            IF        OVRCD=1
              MOVE      "Invalid Casemix Code",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
VFLD8000  MATCH     SP70,XRBCOD
          IF        !@EQUAL
            PACK      KEY5,ANSF,ANSI,XRBCOD,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Breakdown Revenue Code",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.      
VFLD9999  RETURN
+
.******************************************************************************
. Print error line
.******************************************************************************
PERR0000  ADD       ONE,ERRCOUNT            * Total error count
.
          PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*50,"| ":
                    ERRORLNE,*132,"|"
.
PERR9999  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ADDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
. Print total line
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PATCMCIO/INC            * Casemix Code file
          INC       PMSCIDIO/INC            * Contract ID file
          INC       PATFN1IO/INC            * Health fund file
          INC       PATFX1IO/INC            * Health fund file
          INC       PATHGPIO/INC            * Health fund group
          INC       PATOVBIO/INC            * Overnight revenue breakdown file
          INC       PATSRBIO/INC            * Daycase revenue breakdown file
          INC       PATHLFIO/INC            * Overnight lumpsum file
          INC       PATLSDIO/INC            * Daycase lumpsum file

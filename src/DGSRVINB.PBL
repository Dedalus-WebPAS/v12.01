.******************************************************************************
.* System    :   Datagate                                                     *
.* Program   :   DGSRVINB                                                     *
.* Desc      :   HL7 Inbound Server                                           *
.******************************************************************************
.* Date      :   10/11/2000                                                   *
.* Author    :   Steve Downing                                                *
.* Function  :   This program will send messages from IBA to Datagate         *
.* Mods      :                                                                *
.*        V5.08.01  14/12/2000  Greg Horvat                                   *
.*                  Changed to not reopen the server connection each time a   *
.*                  message is sent                                           *
.*                                                                            *
.******************************************************************************
.
          INC       STD002FD
.
          INC       HL7EVNFD/INC
          INC       HL7INBFD/INC
          INC       HL7MRGFD/INC
          INC       HL7MSHFD/INC
          INC       HL7NK1FD/INC
          INC       HL7PIDFD/INC
          INC       HL7PV1FD/INC
          INC       HL7QRDFD/INC
          INC       HL7ZADFD/INC
          INC       HL7ZIDFD/INC
          INC       HL7ZPNFD/INC
          INC       PATCONFD/INC
.
DGATEIN   DFILE                                   * datagate outbound file
DGATEOUT  DFILE                                   * datagate outbound file
.
. ----- Program variables -----
.
A04SNT    FORM      8                       * A04 sent counter
A19SNT    FORM      8                       * A19 sent counter
A28SNT    FORM      8                       * A28 sent counter
A31SNT    FORM      8                       * A31 sent counter
A34SNT    FORM      8                       * A34 sent counter
.
A04SUC    FORM      8                       * A04 successful counter
A19SUC    FORM      8                       * A19 successful counter
A28SUC    FORM      8                       * A28 successful counter
A31SUC    FORM      8                       * A31 successful counter
A34SUC    FORM      8                       * A34 successful counter
.
A04USC    FORM      8                       * A04 unsuccessful counter
A19USC    FORM      8                       * A19 unsuccessful counter
A28USC    FORM      8                       * A28 unsuccessful counter
A31USC    FORM      8                       * A31 unsuccessful counter
A34USC    FORM      8                       * A34 unsuccessful counter
.
HL7CMP    DIM       127[20]
HL7STR    DIM       127
MESSTYPE  DIM       3                       * message type
NEXTSEG   DIM       4
.
. ----- Program constants -----
.
CARET     INIT      "^"
CRFORM    INIT      015
SP127     INIT      "                                          ":
                    "                                          ":
                    "                                           "
.
PRGID     INIT      "DGSRVINB"
PRGNAM    INIT      "HL7 Inbound Server"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                       Controlling Logic (Mainline code)                    *
.******************************************************************************
ML0000    CALL      SETX0000                * Setup common variables
          CALL      INIT0000                * Initialise variables & open files
          BRANCH    OVRCD,ML9999
.
          CALL      BACK0000                * Display background screen
          CALL      RDIN0000                * Read HL7 inbound file
          CLOSE     DGATEIN
.
ML9999    STOP
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Varibales & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"hl7evnaf";
          OPEN      HL7EVN01,"hl7evnaf"
.
          DISPLAY   *P64:24,"hl7inbaf";
          OPEN      HL7INB01,"hl7inbaf"
          OPEN      HL7INB02,"hl7inbaf"
.
          DISPLAY   *P64:24,"hl7mrgaf";
          OPEN      HL7MRG01,"hl7mrgaf"
.
          DISPLAY   *P64:24,"hl7mshaf";
          OPEN      HL7MSH01,"hl7mshaf"
.
          DISPLAY   *P64:24,"hl7nk1af";
          OPEN      HL7NK101,"hl7nk1af"
.
          DISPLAY   *P64:24,"hl7pidaf";
          OPEN      HL7PID01,"hl7pidaf"
.
          DISPLAY   *P64:24,"hl7pv1af";
          OPEN      HL7PV101,"hl7pv1af"
.
          DISPLAY   *P64:24,"hl7qrdaf";
          OPEN      HL7QRD01,"hl7qrdaf"
.
          READ      CONTROLF,HUND05;*1,PTCNACTI,*26,PTCNINSN
.
          IF        PTCNACTI=1
            DISPLAY   *P64:24,"hl7zadaf";
            OPEN      HL7ZAD01,"hl7zadaf"
.
            DISPLAY   *P64:24,"hl7zidaf";
            OPEN      HL7ZID01,"hl7zidaf"
.
            DISPLAY   *P64:24,"hl7zpnaf";
            OPEN      HL7ZPN01,"hl7zpnaf"
          ENDIF
.
          MOVE      ZERO,OVRCD
          STRIP     PTCNINSN
          TRAP      OVERCOND IF IO
          OPEN      DGATEIN,PTCNINSN        * Open inbound TCP/IP port
          TRAPCLR   IO
          COMPARE   ONE,OVRCD
          GOTO      INIT9000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Service not currently available.  ";
          CALL      HITEXIT
          GOTO      INIT9500
.
INIT9000  MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  BACK0000              Called by: ML0000   *
.*                          Display Background Screen                         *
.******************************************************************************
BACK0000  DISPLAY   *P1:4,*EF
          BOX       16,3,6,78,23
          HLINE     *G37,11,3,78
          HLINE     *G33,11,3,3
          HLINE     *G34,11,78,78
          DISPLAY   *P30:6,*HON,*V2LON," MESSAGE STATISTICS ";
.
          CALL      IBACLOCK
          PACK      KEY8,CCC,CYY,CMM,CDD
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          CLOCK     TIME,CTIMEIS
.
          DISPLAY   *P5:8,"Application Activated on ",*V2LON,CPCDATE,*HOFF:
                    "  @  ",*V2LON,CTIMEIS,*HOFF:
                    *P5:10,"Current status... ",*V2LON,*HON,"Waiting";
.
          DISPLAY   *P5:12,*V2LON,*ULON,"Messages",SP20,SP3:
                    *P39:12,"Sent",SP8,*P52:12,"Successful  ":
                    *P65:12,"Unsuccessful";
.
          DISPLAY   *P5:14,"Register A Patient        (A04)":
                    *P5:15,"Patient Query             (A19)":
                    *P5:16,"Add Person Information    (A28)":
                    *P5:17,"Update Person Information (A31)":
                    *P5:18,"Query Patient Information (A34)";
.
          MOVE      ONE,FORM2
          REPEAT
            STORE     ZERO,FORM2,A04SNT,A19SNT,A28SNT,A31SNT,A34SNT:
                                 A04SUC,A19SUC,A28SUC,A31SUC,A34SUC:
                                 A04USC,A19USC,A28USC,A31USC,A34USC
            ADD       ONE,FORM2
          UNTIL     FORM2>15
.
BACK9000  MOVE      ZERO,EXIT
BACK9999  RETURN
+
.******************************************************************************
.*                                  RDIN0000              Called by: ML0000   *
.*                            Read HL7 Inbound File                           *
.******************************************************************************
RDIN0000  PACK      KEY37,ANST,SP70
          CALL      RSH7INB2                * Position on & read an inbound
RDIN1000  CALL      RKH7INB2                  file record
          BRANCH    OVRCD,RDIN0000
.
          DISPLAY   *P23:10,*V2LON,*HON,"Processing":
                    *HOFF,*V2LON," (",H7INMETP,")"
.
          MATCH     "A04",H7INMETP
          CALL      WA040000 IF EQUAL       * Write a register message type
.
          MATCH     "A19",H7INMETP
          CALL      WA190000 IF EQUAL       * Write a query message type
.
          MATCH     "A28",H7INMETP
          CALL      WA280000 IF EQUAL       * Write an add person message type
.
          MATCH     "A31",H7INMETP
          CALL      WA310000 IF EQUAL       * Write an update person mess type
.
          MATCH     "A34",H7INMETP
          CALL      WA340000 IF EQUAL       * Write a merge patient message type
.
          BRANCH    EXIT,RDIN2000           * Errors found
.
          WRITE     DGATEIN;CRFORM
.
          PACK      KEY20,H7INMESI,SP20     
          CALL      DEH7INB1                * Delete an inbound file record
.
          DISPLAY   *P23:10,SP20,*P23:10,*V2LON,*HON,"Waiting";
          GOTO      RDIN0000
.
RDIN2000  MOVE      ANSR,H7INSTAT           * Status - Rejected
          CALL      UPH7INB1                * Update an inbound file record
.
          DISPLAY   *P23:10,SP20,*P23:10,*V2LON,*HON,"Waiting";
          GOTO      RDIN1000
.
RDIN9000  MOVE      ZERO,EXIT
RDIN9999  RETURN
+
.******************************************************************************
.*                                  WA040000              Called by: RDIN0000 *
.*                        Write A Register Message Type                       *
.******************************************************************************
WA040000  ADD       ONE,A04SNT
          DISPLAY   *P39:14,*V2LON,A04SNT;
.
          CALL      CMSH0000                * Check message header file
          BRANCH    EXIT,WA041000
.
          CALL      CEVN0000                * Check event type file
          BRANCH    EXIT,WA041000
.
          CALL      CPID0000                * Check patient id file
          BRANCH    EXIT,WA041000
.
          CALL      CZID0000                * Check patient id extension file
          BRANCH    EXIT,WA041000
.
          CALL      CPV10000                * Check patient visit file
          BRANCH    EXIT,WA041000
.
          CALL      WMSH0000                * Write message header
          CALL      WEVN0000                * Write event type
          CALL      WPID0000                * Write patient id
          CALL      WZID0000                * Write patient id extension
          CALL      WNK10000                * Write next of kin
          CALL      WPV10000                * Write patient visit
.
          ADD       ONE,A04SUC
          DISPLAY   *P52:14,*V2LON,A04SUC;
          GOTO      WA049000
.
WA041000  ADD       ONE,A04USC
          DISPLAY   *P65:14,*V2LON,A04USC;
          GOTO      WA049500
.
WA049000  MOVE      ZERO,EXIT
          GOTO      WA049999
.
WA049500  MOVE      ONE,EXIT
WA049999  RETURN
+
.******************************************************************************
.*                                  WA190000              Called by: RDIN0000 *
.*                         Write A Query Message Type                         *
.******************************************************************************
WA190000  ADD       ONE,A19SNT
          DISPLAY   *P39:15,*V2LON,A19SNT;
.
          CALL      CMSH0000                * Check message header file
          BRANCH    EXIT,WA191000
.
          CALL      CQRD0000                * Check query file
          BRANCH    EXIT,WA191000
.
          CALL      WMSH0000                * Write message header
          CALL      WQRD0000                * Write query definition
.
          ADD       ONE,A19SUC
          DISPLAY   *P52:15,*V2LON,A19SUC;
          GOTO      WA199000
.
WA191000  ADD       ONE,A19USC
          DISPLAY   *P65:15,*V2LON,A19USC;
          GOTO      WA199500
.
WA199000  MOVE      ZERO,EXIT
          GOTO      WA199999
.
WA199500  MOVE      ONE,EXIT
WA199999  RETURN
+
.******************************************************************************
.*                                  WA280000              Called by: RDIN0000 *
.*                       Write An Add Person Message Type                     *
.******************************************************************************
WA280000  ADD       ONE,A28SNT
          DISPLAY   *P39:16,*V2LON,A28SNT;
.
          CALL      CMSH0000                * Check message header file
          BRANCH    EXIT,WA281000
.
          CALL      CEVN0000                * Check event type file
          BRANCH    EXIT,WA281000
.
          CALL      CPID0000                * Check patient id file
          BRANCH    EXIT,WA281000
.
          CALL      CPV10000                * Check patient visit file
          BRANCH    EXIT,WA281000
.
          CALL      WMSH0000                * Write message header
          CALL      WEVN0000                * Write event type
          CALL      WPID0000                * Write patient id
          CALL      WNK10000                * Write next of kin
          CALL      WPV10000                * Write patient visit
.
          ADD       ONE,A28SUC
          DISPLAY   *P52:16,*V2LON,A28SUC;
          GOTO      WA289000
.
WA281000  ADD       ONE,A28USC
          DISPLAY   *P65:16,*V2LON,A28USC;
          GOTO      WA289500
.
WA289000  MOVE      ZERO,EXIT
          GOTO      WA289999
.
WA289500  MOVE      ONE,EXIT
WA289999  RETURN
+
.******************************************************************************
.*                                  WA310000              Called by: RDIN0000 *
.*                     Write An Update Person Message Type                    *
.******************************************************************************
WA310000  ADD       ONE,A31SNT
          DISPLAY   *P39:17,*V2LON,A31SNT;
.
          CALL      CMSH0000                * Check message header file
          BRANCH    EXIT,WA311000
.
          CALL      CEVN0000                * Check event type file
          BRANCH    EXIT,WA311000
.
          CALL      CPID0000                * Check patient id file
          BRANCH    EXIT,WA311000
.
          CALL      CZID0000                * Check patient id extension file
          BRANCH    EXIT,WA311000
.
          CALL      CPV10000                * Check patient visit file
          BRANCH    EXIT,WA311000
.
          CALL      WMSH0000                * Write message header
          CALL      WEVN0000                * Write event type
          CALL      WPID0000                * Write patient id
          CALL      WZID0000                * Write patient id extension
          CALL      WNK10000                * Write next of kin
          CALL      WPV10000                * Write patient visit
.
          ADD       ONE,A31SUC
          DISPLAY   *P52:17,*V2LON,A31SUC;
          GOTO      WA319000
.
WA311000  ADD       ONE,A31USC
          DISPLAY   *P65:17,*V2LON,A31USC;
          GOTO      WA319500
.
WA319000  MOVE      ZERO,EXIT
          GOTO      WA319999
.
WA319500  MOVE      ONE,EXIT
WA319999  RETURN
+
.******************************************************************************
.*                                  WA340000              Called by: RDIN0000 *
.*                     Write A Merge Patient Message Type                     *
.******************************************************************************
WA340000  ADD       ONE,A34SNT
          DISPLAY   *P39:18,*V2LON,A34SNT;
.
          CALL      CMSH0000                * Check message header file
          BRANCH    EXIT,WA341000
.
          CALL      CEVN0000                * Check event type file
          BRANCH    EXIT,WA341000
.
          CALL      CPID0000                * Check patient id file
          BRANCH    EXIT,WA341000
.
          CALL      CMRG0000                * Check merge file
          BRANCH    EXIT,WA341000
.
          CALL      WMSH0000                * Write message header
          CALL      WEVN0000                * Write event type
          CALL      WPID0000                * Write patient id
          CALL      WMRG0000                * Write merge information
.
          ADD       ONE,A34SUC
          DISPLAY   *P52:18,*V2LON,A34SUC;
          GOTO      WA349000
.
WA341000  ADD       ONE,A34USC
          DISPLAY   *P65:18,*V2LON,A34USC;
          GOTO      WA349500
.
WA349000  MOVE      ZERO,EXIT
          GOTO      WA349999
.
WA349500  MOVE      ONE,EXIT
WA349999  RETURN
+
.******************************************************************************
.*                                  CEVN0000              Called by: RDIN0000 *
.*                            Check Event Type File                           *
.******************************************************************************
CEVN0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7EVN1                * Position on & read an event type
          CALL      RKH7EVN1                  file record
          BRANCH    OVRCD,CEVN9500
.
          COMPARE   H7INMESI,EVNIN01
          GOTO      CEVN9500 IF NOT EQUAL   * Different message id
.
CEVN9000  MOVE      ZERO,EXIT
          GOTO      CEVN9999
.
CEVN9500  MOVE      ONE,EXIT
CEVN9999  RETURN
+
.******************************************************************************
.*                                  CMRG0000              Called by: RDIN0000 *
.*                              Check Merge File                              *
.******************************************************************************
CMRG0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7MRG1                * Position on & read a merge file
          CALL      RKH7MRG1                  record
          BRANCH    OVRCD,CMRG9500
.
          COMPARE   H7INMESI,MRGIN01
          GOTO      CMRG9500 IF NOT EQUAL   * Different message id
.
CMRG9000  MOVE      ZERO,EXIT
          GOTO      CMRG9999
.
CMRG9500  MOVE      ONE,EXIT
CMRG9999  RETURN
+
.******************************************************************************
.*                                  CMSH0000              Called by: RDIN0000 *
.*                          Check Message Header File                         *
.******************************************************************************
CMSH0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7MSH1                * Position on & read a message
          CALL      RKH7MSH1                  header file record
          BRANCH    OVRCD,CMSH9500
.
          COMPARE   H7INMESI,MSHIN01
          GOTO      CMSH9500 IF NOT EQUAL   * Different message id
.
CMSH9000  MOVE      ZERO,EXIT
          GOTO      CMSH9999
.
CMSH9500  MOVE      ONE,EXIT
CMSH9999  RETURN
+
.******************************************************************************
.*                                  CPID0000              Called by: RDIN0000 *
.*                            Check Patient Id File                           *
.******************************************************************************
CPID0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7PID1                * Position on & read a patient id
          CALL      RKH7PID1                  file record
          BRANCH    OVRCD,CPID9500
.
          COMPARE   H7INMESI,PIDIN01
          GOTO      CPID9500 IF NOT EQUAL   * Different message id
.
CPID9000  MOVE      ZERO,EXIT
          GOTO      CPID9999
.
CPID9500  MOVE      ONE,EXIT
CPID9999  RETURN
+
.******************************************************************************
.*                                  CPV10000              Called by: RDIN0000 *
.*                          Check Patient Visit File                          *
.******************************************************************************
CPV10000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7PV11                * Position on & read a patient
          CALL      RKH7PV11                  visit file record
          BRANCH    OVRCD,CPV19500
.
          COMPARE   H7INMESI,PV1IN01
          GOTO      CPV19500 IF NOT EQUAL   * Different message id
.
CPV19000  MOVE      ZERO,EXIT
          GOTO      CPV19999
.
CPV19500  MOVE      ONE,EXIT
CPV19999  RETURN
+
.******************************************************************************
.*                                  CQRD0000              Called by: RDIN0000 *
.*                              Check Query File                              *
.******************************************************************************
CQRD0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7QRD1                * Position on & read a query file
          CALL      RKH7QRD1                  record
          BRANCH    OVRCD,CQRD9500
.
          COMPARE   H7INMESI,QRDIN01
          GOTO      CQRD9500 IF NOT EQUAL   * Different message id
.
CQRD9000  MOVE      ZERO,EXIT
          GOTO      CQRD9999
.
CQRD9500  MOVE      ONE,EXIT
CQRD9999  RETURN
+
.******************************************************************************
.*                                  CZID0000              Called by: RDIN0000 *
.*                   Check Patient Id Extension Header File                   *
.******************************************************************************
CZID0000  COMPARE   ONE,PTCNACTI
          GOTO      CZID9000 IF NOT EQUAL   * Not using ACT interface
.
          PACK      KEY22,H7INMESI,SP30
          CALL      RSH7ZID1                * Position on & read a patient id
          CALL      RKH7ZID1                  ext file record
          BRANCH    OVRCD,CZID9500
.
          COMPARE   H7INMESI,ZIDIN01
          GOTO      CZID9500 IF NOT EQUAL   * Different message id
.
CZID9000  MOVE      ZERO,EXIT
          GOTO      CZID9999
.
CZID9500  MOVE      ONE,EXIT
CZID9999  RETURN
+
.******************************************************************************
.*                                  WEVN0000              Called by: Lots     *
.*                              Write Event Type                              *
.******************************************************************************
WEVN0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7EVN1                * Position on & read an event type
          CALL      RKH7EVN1                  file record
          BRANCH    OVRCD,WEVN9000
.
          COMPARE   H7INMESI,EVNIN01
          GOTO      WEVN9000 IF NOT EQUAL   * Different message id
.
          MOVE      "EVN",KEY3
          PACK      KEY4,CRFORM,KEY3
          WRITE     DGATEIN;KEY4,EVN001,EVN002,EVN003,EVN004:
                            EVN005,EVN006;
.
          PACK      KEY22,EVNIN01,EVNIN02
          CALL      DEH7EVN1                * Delete an event type file record
          GOTO      WEVN0000
.
WEVN9000  MOVE      ZERO,EXIT
WEVN9999  RETURN
+
.******************************************************************************
.*                                  WMRG0000              Called by: Lots     *
.*                           Write Merge Information                          *
.******************************************************************************
WMRG0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7MRG1                * Position on & read a merge info
          CALL      RKH7MRG1                  file record
          BRANCH    OVRCD,WMRG9000
.
          COMPARE   H7INMESI,MRGIN01
          GOTO      WMRG9000 IF NOT EQUAL   * Different message id
.
          MOVE      "MRG",KEY3
          PACK      KEY4,CRFORM,KEY3
          WRITE     DGATEIN;KEY4,MRG001,MRG002,MRG003,MRG004:
                            MRG005,MRG006,MRG007;
.
          PACK      KEY22,MRGIN01,MRGIN02
          CALL      DEH7MRG1                * Delete a merge info file record
          GOTO      WMRG0000
.
WMRG9000  MOVE      ZERO,EXIT
WMRG9999  RETURN
+
.******************************************************************************
.*                                  WMSH0000              Called by: Lots     *
.*                            Write Message Header                            *
.******************************************************************************
WMSH0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7MSH1                * Position on read a message header
          CALL      RKH7MSH1                  file record
          BRANCH    OVRCD,WMSH9000
.
          COMPARE   H7INMESI,MSHIN01
          GOTO      WMSH9000 IF NOT EQUAL   * Different message id
.
          MOVE      "MSH",KEY3
          WRITE     DGATEIN;KEY3,MSH002,MSH003,MSH004:
                            MSH005,MSH006,MSH007,MSH008,MSH009:
                            MSH010,MSH011,MSH012,MSH013,MSH01401:
                            MSH01402,MSH015,MSH016,MSH017,MSH018:
                            MSH019;
.
          PACK      KEY22,MSHIN01,MSHIN02
          CALL      DEH7MSH1                * Delete a message header file rec
          GOTO      WMSH0000
.
WMSH9000  MOVE      ZERO,EXIT
WMSH9999  RETURN
+
.******************************************************************************
.*                                  WNK10000              Called by: Lots     *
.*                              Write Next Of Kin                             *
.******************************************************************************
WNK10000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7NK11                * Position on & read a next of kin
          CALL      RKH7NK11                  file record
          BRANCH    OVRCD,WNK19000
.
          COMPARE   H7INMESI,NK1IN01
          GOTO      WNK19000 IF NOT EQUAL   * Different message id
.
          BRANCH    PTCNACTI,WNK11000       * Using the ACT interface
.
          MOVE      "NK1",KEY3
          PACK      KEY4,CRFORM,KEY3
          WRITE     DGATEIN;KEY4,NK1001,NK1002,NK1003,NK1004:
                            NK1005,NK1006,NK1007,NK1008,NK1009:
                            NK1010,NK1011,NK1012,NK1013,NK1014:
                            NK1015,NK1016,NK1017,NK1018,NK1019:
                            NK1020,NK1021,NK1022,NK1023,NK1024:
                            NK1025,NK1026,NK1027,NK1028,NK1029:
                            NK1030,NK1031,NK1032,NK1033,NK1034:
                            NK1035,NK1036,NK1037;
          GOTO      WNK12000
.
WNK11000  PACK      KEY6,NK1IN02,SP1,TWO,NK1001
          CALL      WZPN0000                * Write patient name
          MOVE      ZPN001,NK100201
          MOVE      ZPN002,NK100202
          MOVE      ZPN003,NK100203
          MOVE      ZPN004,NK100204
          MOVE      ZPN005,NK100205
          MOVE      ZPN006,NK100206
          MOVE      ZPN007,NK100207
          MOVE      ZPN008,NK100208
.
          PACK      KEY6,NK1IN02,SP1,FOUR,NK1001
          CALL      WZAD0000                * Write patient address
          MOVE      ZAD001,NK100401
          MOVE      ZAD002,NK100402
          MOVE      ZAD003,NK100403
          MOVE      ZAD004,NK100404
          MOVE      ZAD005,NK100405
          MOVE      ZAD006,NK100406
          MOVE      ZAD007,NK100407
          MOVE      ZAD008,NK100408
          MOVE      ZAD009,NK100409
.
          MOVE      "NK1",KEY3
          PACK      KEY4,CRFORM,KEY3
          WRITE     DGATEIN;KEY4,NK1001:
                      *SS:0,NK100201,NK100202,NK100203,NK100204,NK100205:
                            NK100206,NK100207,NK100208,*SF:
                            NK1003:
                      *SS:0,NK100401,NK100402,NK100403,NK100404,NK100405:
                            NK100406,NK100407,NK100408,NK100409,*SF:
                            NK1005,NK1006,NK1007,NK1008,NK1009:
                            NK1010,NK1011,NK1012,NK1013,NK1014:
                            NK1015,NK1016,NK1017,NK1018,NK1019:
                            NK1020,NK1021,NK1022,NK1023,NK1024:
                            NK1025,NK1026,NK1027,NK1028,NK1029:
                            NK1030,NK1031,NK1032,NK1033,NK1034:
                            NK1035,NK1036,NK1037;
.
WNK12000  PACK      KEY22,NK1IN01,NK1IN02
          CALL      DEH7NK11                * Delete a next of kin file record
          GOTO      WNK10000
.
WNK19000  MOVE      ZERO,EXIT
WNK19999  RETURN
+
.******************************************************************************
.*                                  WPID0000              Called by: Lots     *
.*                              Write Patient Id                              *
.******************************************************************************
WPID0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7PID1                * Position on & read a patient id
          CALL      RKH7PID1                  file record
          BRANCH    OVRCD,WPID9000
.
          COMPARE   H7INMESI,PIDIN01
          GOTO      WPID9000 IF NOT EQUAL   * Different message id
.
          BRANCH    PTCNACTI,WPID1000       * Using the ACT interface
.
          MOVE      "PID",KEY3
          PACK      KEY4,CRFORM,KEY3
          WRITE     DGATEIN;KEY4,PID001,PID002,PID003,PID004:
                            PID005,PID006,PID007,PID008,PID009:
                            PID010,PID011,PID012,PID013,PID014:
                            PID015,PID016,PID017,PID018,PID019:
                            PID020,PID021,PID022,PID023,PID024:
                            PID025,PID026,PID027,PID028,PID029:
                            PID030;
          GOTO      WPID2000
.
WPID1000  PACK      KEY6,PIDIN02,SP1,FIVE,SP1,ONE
          CALL      WZPN0000                * Write patient name
          MOVE      ZPN001,PID00506
          MOVE      ZPN002,PID00507
          MOVE      ZPN003,PID00508
          MOVE      ZPN004,PID00509
          MOVE      ZPN005,PID00510
          MOVE      ZPN006,PID00511
          MOVE      ZPN007,PID00512
          MOVE      ZPN008,PID00513
.
          PACK      KEY6,PIDIN02,SP1,NINE,SP1,ONE
          CALL      WZPN0000                * Write patient name
          MOVE      ZPN001,PID00901
          MOVE      ZPN002,PID00902
          MOVE      ZPN003,PID00903
          MOVE      ZPN004,PID00904
          MOVE      ZPN005,PID00905
          MOVE      ZPN006,PID00906
          MOVE      ZPN007,PID00907
          MOVE      ZPN008,PID00908
.
          PACK      KEY6,PIDIN02,TEN1,SP1,ONE
          CALL      WZAD0000                * Write patient address
          MOVE      ZAD001,PID01106
          MOVE      ZAD002,PID01107
          MOVE      ZAD003,PID01108
          MOVE      ZAD004,PID01109
          MOVE      ZAD005,PID01110
          MOVE      ZAD006,PID01111
          MOVE      ZAD007,PID01112
          MOVE      ZAD008,PID01113
          MOVE      ZAD009,PID01114
.
          MOVE      "PID",KEY3
          PACK      KEY4,CRFORM,KEY3
          WRITE     DGATEIN;KEY4,PID001,PID002,PID003,PID004:
                      *SS:0,PID00506,PID00507,PID00508,PID00509,PID00510:
                            PID00511,PID00512,PID00513,*SF:
                            PID006,PID007,PID008:
                      *SS:0,PID00901,PID00902,PID00903,PID00904,PID00905:
                            PID00906,PID00907,PID00908,*SF:
                            PID010:
                      *SS:0,PID01101,PID01102,PID01103,PID01104,PID01105:
                            PID01106,PID01107,PID01108,PID01109,*SF:
                            PID012,PID013,PID014,PID015,PID016:
                            PID017,PID018,PID019,PID020,PID021:
                            PID022,PID023,PID024,PID025,PID026:
                            PID027,PID028,PID029,PID030;
.
WPID2000  PACK      KEY22,PIDIN01,PIDIN02
          CALL      DEH7PID1                * Delete a patient id file record
          GOTO      WPID0000
.
WPID9000  MOVE      ZERO,EXIT
WPID9999  RETURN
+
.******************************************************************************
.*                                  WPV10000              Called by: Lots     *
.*                             Write Patient Visit                            *
.******************************************************************************
WPV10000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7PV11                * Position on & read a patient
          CALL      RKH7PV11                  visit file record
          BRANCH    OVRCD,WPV19000
.
          COMPARE   H7INMESI,PV1IN01
          GOTO      WPV19000 IF NOT EQUAL   * Different message id
.
          MOVE      "PV1",KEY3
          PACK      KEY4,CRFORM,KEY3
          WRITE     DGATEIN;KEY4,PV1001,PV1002,PV1003,PV1004,PV1005:
                            PV1006,PV1007,PV1008,PV1009,PV1010,PV1011:
                            PV1012,PV1013,PV1014,PV1015,PV1016,PV1017:
                            PV1018,PV1019,PV1020,PV1021,PV1022,PV1023:
                            PV1024,PV1025,PV1026,PV1027,PV1028,PV1029:
                            PV1030,PV1031,PV1032,PV1033,PV1034,PV1035:
                            PV1036,PV1037,PV1038,PV1039,PV1040,PV1041:
                            PV1042,PV1043,PV1044,PV1045,PV1046,PV1047:
                            PV1048,PV1049,PV1050,PV1051,PV1052;
.
          PACK      KEY22,PV1IN01,PV1IN02
          CALL      DEH7PV11                * Delete a patient visit file rec
          GOTO      WPV10000
.
WPV19000  MOVE      ZERO,EXIT
WPV19999  RETURN
+
.******************************************************************************
.*                                  WQRD0000              Called by: Lots     *
.*                           Write Query Definition                           *
.******************************************************************************
WQRD0000  PACK      KEY22,H7INMESI,SP30
          CALL      RSH7QRD1                * Position on & read a query file
          CALL      RKH7QRD1                  record
          BRANCH    OVRCD,WQRD9000
.
          COMPARE   H7INMESI,QRDIN01
          GOTO      WQRD9000 IF NOT EQUAL   * Different message id
.
          MOVE      "QRD",KEY3
          PACK      KEY4,CRFORM,KEY3
          WRITE     DGATEIN;KEY4,QRD001,QRD002,QRD003,QRD004:
                            QRD005,QRD006,QRD007,QRD008,QRD009:
                            QRD010,QRD011,QRD012;
.
          PACK      KEY22,QRDIN01,QRDIN02
          CALL      DEH7QRD1                * Delete a query file record
          GOTO      WQRD0000
.
WQRD9000  MOVE      ZERO,EXIT
WQRD9999  RETURN
+
.******************************************************************************
.*                                  WZID0000              Called by: Lots     *
.*                         Write Patient Id Extension                         *
.******************************************************************************
WZID0000  COMPARE   ONE,PTCNACTI
          GOTO      WZID9000 IF NOT EQUAL   * Not using ACT interface
.
          PACK      KEY22,H7INMESI,SP30
          CALL      RSH7ZID1                * Position on & read a patient id
          CALL      RKH7ZID1                  ext file record
          BRANCH    OVRCD,WZID9000
.
          COMPARE   H7INMESI,ZIDIN01
          GOTO      WZID9000 IF NOT EQUAL   * Different message id
.
          MOVE      "ZID",KEY3
          PACK      KEY4,CRFORM,KEY3
          WRITE     DGATEIN;KEY4,ZID001,ZID002,ZID003,ZID004:
                            ZID005,ZID006,ZID007,ZID008,ZID009:
                            ZID010,ZID011;
.
          PACK      KEY22,ZIDIN01,ZIDIN02
          CALL      DEH7ZID1                * Delete a patient id ext file rec
          GOTO      WZID0000
.
WZID9000  MOVE      ZERO,EXIT
WZID9999  RETURN
+
.******************************************************************************
.*                                  WZAD0000              Called by: WNK10000 *
.*                            Write Patient Address                & WPID0000 *
.******************************************************************************
WZAD0000  UNPACK    SP127,ZAD001,ZAD002,ZAD003,ZAD004,ZAD005
          UNPACK    SP127,ZAD006,ZAD007,ZAD008,ZAD009
          PACK      KEY29,H7INMESI,H7INMETP,KEY6
          CALL      RDH7ZAD1                * Read a patient address file record
          BRANCH    OVRCD,WZAD9000
.
          PACK      KEY29,ZADIN01,ZADIN02,ZADIN03,ZADIN04,ZADIN05
          CALL      DEH7ZAD1                * Delete a patient address file rec
.
WZAD9000  MOVE      ZERO,EXIT
WZAD9999  RETURN
+
.******************************************************************************
.*                                  WZPN0000              Called by: WNK10000 *
.*                             Write Patient Name                  & WPID0000 *
.******************************************************************************
WZPN0000  UNPACK    SP127,ZPN001,ZPN002,ZPN003,ZPN004,ZPN005
          UNPACK    SP127,ZPN006,ZPN007,ZPN008
          PACK      KEY29,H7INMESI,H7INMETP,KEY6
          CALL      RDH7ZPN1                * Read a patient name file record
          BRANCH    OVRCD,WZPN9000
.
          PACK      KEY29,ZPNIN01,ZPNIN02,ZPNIN03,ZPNIN04,ZPNIN05
          CALL      DEH7ZPN1                * Delete a patient name file record
.
WZPN9000  MOVE      ZERO,EXIT
WZPN9999  RETURN
.
HL7BRK00  RETURN
+
.==============================================================================
.
          INC       STD002IO
.
          INC       HL7EVNIO/INC
          INC       HL7INBIO/INC
          INC       HL7MRGIO/INC
          INC       HL7MSHIO/INC
          INC       HL7NK1IO/INC
          INC       HL7PIDIO/INC
          INC       HL7PV1IO/INC
          INC       HL7QRDIO/INC
          INC       HL7ZADIO/INC
          INC       HL7ZIDIO/INC
          INC       HL7ZPNIO/INC
+
.


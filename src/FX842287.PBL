.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   FX842287                                                  *
.* Desc      :   Fix for new field 'Best Basis for Diagnosis'              *
.***************************************************************************
.* Date      :   13/03/2018                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   Mapping the previously stored data ptcdbt06,ptcdbt07 and  *
.*               ptcdbt09 to the new category code field (cat Bd) ptcdbdia)*
.* Modifications  :                                                        *
.*        V10.12.00 13/03/2018  J.Tan         TSK 0842287                  *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCRDFD/INC
          INC       PATCODFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
BBASDIAG  DIM       3
RECCOUNT  FORM      8
ERRCOUNT  FORM      8
.
PRGID     INIT      "FX842287"
PRGNAM    INIT      "Fix New field - Best Basis for Diagnosis"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process patcrdaf
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"patcrdaf";
          OPEN      PATCRDA1,"patcrdaf"
.
          DISPLAY   *P64:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". List Program":
                    *P1:6,*V2LON,TWO,*HOFF,". Fix Program"
.
OPTN0500  KEYIN     *P1:10,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:10,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  CALL      IBACLOCK
          MOVE      "Fixit - Best Basis for Diagnosis",PRGNAM
.
          CALL      HEAD80
          MOVE      ZERO,RECCOUNT          * Number of records updated
          MOVE      ZERO,ERRCOUNT          * Number of errors
.
          CALL      UND80
          PRINT     *1,"| Visit",*12,"| Cancer Reg.No.",*80,"|"
          CALL      UND80
.
          PACK      KEY25,SP70
          CALL      RSPTCRD1
PROC1000  CALL      RKPTCRD1               * Loop Cancer Reg.file
          BRANCH    OVRCD,PROC9000
.
          DISPLAY   *P20:24,"Visit No.: ",DPTCDDAD
.
          CALL      CBDI0000          * Check if more than one field selected
          BRANCH    EXIT,PROC2000          * valid - only one was selected
.
          ADD       ONE,ERRCOUNT           * Number of errors
          PRINT     *1,"| ",DPTCDDAD,*12,"| ",DPTCDCNR:
.                   *25,BBASDIAG:
                    *80,"|"
          GOTO      PROC1000
.
PROC2000  BRANCH    COPTION,PROC1000     * List only
.
          ADD       ONE,RECCOUNT         * Number of records updated
          CALL      SBDI0000             * Set new field for Best Basis of Diag.
          CALL      UPPTCRD1             * Option 2 - Report and Update
.
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:16,*EF,"Total number of errors : ":
                      *V2LON,ERRCOUNT,*HOFF,SP5;
.
          IF        ERRCOUNT<>0
            DISPLAY   *P1:18,"** Please fix Patients with more than one":
                      " check box field selected."
          ENDIF
.
          CALL      UND80
          PRINT     *N,*1,"Total number of errors : ",ERRCOUNT
.
          IF        COPTION=2
            DISPLAY   *P1:22,*EL,"Total Records Updated : ":
                      *V2LON,RECCOUNT,*HOFF,SP5;
.
            PRINT     *N,*1,"Total Records Updated : ",RECCOUNT
          ENDIF
.
          DISPLAY   *P1:23;
          CALL      HITENTER
.
PROC9999  RETURN
+
.**************************************************************************
.*                               CBDI0000              Called by: PROC0000*
.*       Check if more than one check box selected for Best Basis of Diag.*
.**************************************************************************
CBDI0000  MOVE      SP70,BBASDIAG
          PACK      BBASDIAG,PTCDBT06,PTCDBT07,PTCDBT09,SP70
          REP       " 0",BBASDIAG
          MATCH     "000",BBASDIAG
          GOTO      CBDI9000 IF EQUAL      * All blank
          MATCH     "100",BBASDIAG
          GOTO      CBDI9000 IF EQUAL      * PTCDB06 is one
          MATCH     "010",BBASDIAG
          GOTO      CBDI9000 IF EQUAL      * PTCDB07 is one
          MATCH     "001",BBASDIAG
          GOTO      CBDI9000 IF EQUAL      * PTCDB09 is one
.
          MOVE      ZERO,EXIT              * more than one selected
          GOTO      CBDI9999
.
CBDI9000  MOVE      ONE,EXIT               * valid-only one field selected
CBDI9999  RETURN
+
.**************************************************************************
.*                               SBDI0000              Called by: PROC0000*
.*        Set the new field for Best Basis of Diagnosis                   *
.**************************************************************************
SBDI0000  MOVE      SP70,PTCDBDIA
.
          PACK      BBASDIAG,PTCDBT06,PTCDBT07,PTCDBT09,SP70
          REP       " 0",BBASDIAG
          MATCH     "000",BBASDIAG
          GOTO      SBDI9999 IF EQUAL        * no field selected
.
          MOVE      "Bd",KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
SBDI1000  CALL      RDKCODE1
          BRANCH    OVRCD,SBDI9999
.
          MATCH     KEY2,TCODE
          GOTO      SBDI9999 IF NOT EQUAL    * Cat 'Bd' not seup
.
          MATCH     "5",THCSCOD
          IF        @EQUAL
            MATCH     "100",BBASDIAG
            IF        @EQUAL
              MOVE      ACODE,PTCDBDIA       * Cytology selected
              GOTO      SBDI9999
            ENDIF
          ENDIF
.
          MATCH     "6",THCSCOD
          IF        @EQUAL
            MATCH     "010",BBASDIAG
            IF        @EQUAL
              MOVE      ACODE,PTCDBDIA       * Histology selected
              GOTO      SBDI9999
            ENDIF
          ENDIF
.
          MATCH     "2",THCSCOD
          IF        @EQUAL
            MATCH     "001",BBASDIAG
            IF        @EQUAL
              MOVE      ACODE,PTCDBDIA       * Other selected
              GOTO      SBDI9999
            ENDIF
          ENDIF
          GOTO       SBDI1000
.
SBDI9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCRDIO/INC
          INC       PATCODIO/INC

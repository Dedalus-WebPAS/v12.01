.*****************************************************************************
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:     IBAPMS54                                            *
.*                                                                           *
.*     FUNCTION:         This program will group and stores the AECC Class   *
.*                       data for Emergency Events                           *
.*                                                                           *
.*     AUTHOR:           Jeni Tan                                            *
.*                                                                           *
.*     DATE WRITTEN:     21/07/2021                                          *
.*                                                                           *
.*     MODIFICATIONS:                                                        *
.*                                                                           *
.*     V12.00.01 15.05.2025  DA Sarkies     TSK 0955096                      *
.*               Updated for Alpha-Numeric Visit Number                      *
.*     V11.03.01 04/10/2023  J.Tan          TASK 0938230                     *
.*               Recompiled for ABFAECCL - Emergency ABF                     *
.*     V11.02.02 18/07/2022 J.Tan    TSK 0916596                             *
.*               Recompiled for ABFAECCL - Added NWAUFLAG Claim code Indic2=N*
.*     V11.02.01 29/06/2022 J.Tan           TSK 0920712                      *
.*               Recompiled for ABFAECCL - I*D on emrvcdaf file              *
.*     V11.01.02 27/10/2021 J.Tan           TSK 0913056                      *
.*               Recompiled for ABFAECCL-ADMFLAG and CCAR0000(Clinical Care) *
.*     V11.01.01 13/10/2021 J.Tan           TSK 0905305                      *
.*               Recompiled for ABFAECCL - Adjustment type 068 & 069         *
.*     V11.01.00 21/07/2021 J.Tan  TSK 0905305                               *
.*               Created program                                             *
.*                                                                           *
.******************************************************************************
.
          INC       STD001FD 
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       IBAPASFD/INC
          INC       IBASEQFD/INC
          INC       PATCONFD/INC
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
.
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATINVFD/INC
.
          INC       AAEDE1FD/INC
          INC       EMRVISFD/INC
          INC       EMRVCDFD/INC
          INC       EMRCHAFD/INC
.
+
AECNAECC  DIM       8
CODE      DIM       2
CJDAY     FORM      5
CMDLINE   DIM       220
NWAUFLAG  FORM      "0"
.
FNAMEN    DIM       8
.
FROMDATE  DIM       10
TODATE    DIM       10
ECOL1     FORM      2
ENDDATE   DIM       8
STRTDATE  DIM       8
.
TIMEIS    DIM       8
USERID    DIM       10
.
PRGID     INIT      "IBAPMS54"
PRGNAM    INIT      "Group ED AECC Class"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.         START OF PROGRAM
.******************************************************************************
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN1000  CALL      DSCR0000                      * Display screen
.
          CALL      KDSC0000                      * keyin range of disc.dates
          BRANCH    EXIT,MAIN9999
.
          CALL      KUSR0000                      * Keyin user id
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN2000,MAIN1000,MAIN9999 * Yes, no, cancel
.
MAIN2000  CALL      PROC0000                      * processing
.
MAIN9999  CHAIN     PGM
          STOP
+
.******************************************************************************
.*                          INIT0000                                          *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"emrchaaf";
          OPEN      EMRCHAA1,"emrchaaf"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      EMRVISA2,"emrvisaf"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"emrvcdaf";
          OPEN      EMRVCDA1,"emrvcdaf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,HUND28;*105,PTCNUABF
.
          MOVE      ZERO,EXIT
          READ      CONTROLF,SEVENTY8;*66,AECNAECC
          REP       " 0",AECNAECC
          MATCH     "00000000",AECNAECC
          IF        @EQUAL
            DISPLAY   *P1:20,*EL,"Please set the Start date for AECC Class";
            MOVE      ONE,EXIT
            CALL      HITENTER;
          ENDIF
.
...  used in ABFAECCL
          CALL      TFILENAM                * Get file name calculating Exponent
          MOVE      TFILNAME,FNAMEN
.
INIT9999  RETURN
+
.******************************************************************************
.         Display Screen
.******************************************************************************
DSCR0000  DISPLAY   *P1:5,*EF:
                        *P1:9,"Starting Discharge Date : ":
                        *P1:10,"Ending   Discharge Date : ":
                        *P1:12,"Keyin User id           : "
.
DSCR9999  RETURN
+
.******************************************************************************
.       Keyin Range of Discharge dates
.******************************************************************************
KDSC0000  MOVE      SP70,STRTDATE
          MOVE      SP70,ENDDATE
          UNPACK    SP70,FROMDATE,TODATE
.
          MOVE      "9",CVERT
          MOVE      "27",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,KDSC9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          MATCH     AECNAECC,STRTDATE       * check against start date for AECC
          GOTO      KDSC8000 IF LESS
.
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,FROMDATE
.
KDSC2000  MOVE      "10",CVERT
          MOVE      "27",CCOL
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,KDSC0000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the starting date
.
. ----- Check That The Ending Date Is Greater Than The Starting Date -----
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending date must be greater than the ":
                      "starting date. ";
            CALL      HITENTER
            GOTO      KDSC2000
          ENDIF
          MOVE      CPCDATE,TODATE
          MOVE      ZERO,EXIT
          GOTO      KDSC9999
.
KDSC8000  PACK      AECNAECC,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the starting date
          DISPLAY   *P1:24,*EL,*B,"Start date must be greater than or equal to ",CPCDATE;
          CALL      HITENTER
.
KDSC9000  MOVE      ONE,EXIT
KDSC9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P26:12,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.       Get the discharge patients
.******************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Discharges **";
.
          MOVE      ZERO,NWAUFLAG
          MOVE      "60",CLNO
          MOVE      "2",FORM1
          PACK      KEY9,FORM1,SP70
          CALL      RSEMVIS2
PROC1000  CALL      RKEMVIS2
          BRANCH    OVRCD,PROC8000
.
.         Discharged status 2 (incomplete) or 3 (complete)
.
          BRANCH    EMVISTAT,PROC1000,PROC1200,PROC1200
          GOTO      PROC8000
.
PROC1200  MATCH     SP70,EMVIDDAT
          GOTO      PROC1000 IF EQUAL
.
          MATCH     STRTDATE,EMVIDDAT
          GOTO      PROC1000 IF LESS
          MATCH     EMVIDDAT,ENDDATE    
          GOTO      PROC1000 IF LESS          * out of range
.
          DISPLAY   *P66:24,EMVIADMN;
.
          MOVE      EMVIURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY8,EMVIADMN,SP70
          CALL      RDDETA1
          BRANCH    OVRCD,PROC1000
.
          CALL      RINV0000              * Check if ABF invoice had been raised
          BRANCH    EXIT,PROC1000         * invoiced raised
.
.         If it has been grouped, check Change audit file for the date range
.
.         CALL      CVCD0000               * Check for record in coding file
.         IF        EXIT=1
.           CALL      CCHA00000            * Check for change audit
.           BRANCH    EXIT,PROC1000        * No change record for the date range
.         ENDIF
.
          PROC      ABFAECCL               * AECC class
.
          GOTO      PROC1000
.
PROC8000  CALL      ENDP0000                * Check End of Report

PROC9999  RETURN
+
.******************************************************************************
.         Check if invoice has been raised
.******************************************************************************
RINV0000  MOVE      ZERO,EXIT
          PACK      KEY16,EMVIADMN,Z70
          CALL      RSPTINV3
RINV1000  CALL      RPPTINV3
          BRANCH    OVRCD,RINV9999
.
          MATCH     PINVADM,EMVIADMN
          GOTO      RINV9999 IF NOT EQUAL
.
          REP       " 0",PTINCRST
          MATCH     "0",PTINCRST
          GOTO      RINV1000 IF NOT EQUAL  * Credit note
.
          COMPARE   THREE,PTINCMIX
          GOTO      RINV1000 IF NOT EQUAL
.
          MOVE      ONE,EXIT          * found an ABF Invoice
          GOTO      RINV9999
.
RINV9999  RETURN
+
.******************************************************************************
.         Check if patient has been 'grouped' previously
.******************************************************************************
CVCD0000  MOVE      ZERO,EXIT
          MOVE      "006",KEY3              * for AECC class
          PACK      KEY14,EMVIADMN,KEY3,SP70
          CALL      RSEMVCD1
CVCD1000  CALL      RKEMVCD1
          BRANCH    OVRCD,CVCD9999
.
          MATCH     DEMVIADM,EMVCVIST
          GOTO      CVCD9999 IF NOT EQUAL
          MATCH     KEY3,EMVCTYPE           * Check coding type 
          GOTO      CVCD9999 IF NOT EQUAL
          MATCH     "1",EMVCDELE            * Deleted ?
          GOTO      CVCD1000 IF EQUAL
.
          MOVE      ONE,EXIT
CVCD9999  RETURN
+
.******************************************************************************
.         Check if patient has a change audit record
.******************************************************************************
CCHA0000  MOVE      ZERO,EXIT
          PACK      KEY24,EMVIADMN,STRTDATE,SP70
          CALL      RSEMCHA1
CCHA1000  CALL      RKEMCHA1
          BRANCH    OVRCD,CCHA9000
.
          MATCH     DEMVIADM,EMCHVISN
          GOTO      CCHA9000 IF NOT EQUAL
.
          MATCH     EMCHDATE,ENDDATE       * check end date
          GOTO      CCHA1000 IF LESS
.
          MOVE      ZERO,EXIT
          GOTO      CCHA9999
.
CCHA9000  MOVE      ONE,EXIT
CCHA9999  RETURN
+
.******************************************************************************
.         Heading
.******************************************************************************
PHED0000  CALL      IBACLOCK
          MOVE      ZERO,CNOUNDLN
          CALL      HEAD132
          MOVE      THREE,CLNO
.
          PRINT     *40,"Discharge Date from : ",FROMDATE," To: ",TODATE
          CALL      UND132
          PRINT     *1,"| Admission",*13,"| Error ":
                    *132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
PHED9999  RETURN
+
.******************************************************************************
.         End of Report
.******************************************************************************
ENDP0000  CALL      UND132
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "***  Report Generation Completed  ***",*W2;
.
          PRINT     *N," ***  End of Report  ***"
.
ENDP9999  RETURN
+
.******************************************************************************
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
.
          INC       ABFAECCL
.
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       IBAPASIO/INC
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
.
          INC       PATMA1IO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PATINVIO/INC
.
          INC       AAEDE1IO/INC
          INC       EMRVISIO/INC
          INC       EMRVCDIO/INC
          INC       EMRCHAIO/INC
.

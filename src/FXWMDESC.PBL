.***************************************************************************
.* System    :   Waiting list                                              *
.* Program   :   FXWMDESC                                                  *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   04/07/2005                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will update all wattr1af records to remove   *
.*               remove control characters                                 *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       WATTR1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNT     FORM      2
CARAT    INIT      160
WMDESC1   DIM       50
.
PRGID     INIT      "FXWMDESC"
PRGNAM    INIT      "Update wattr1af to remove control characters"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      WATTR1A1,"wattr1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY19,SP70
          CALL      RDSTREA1
PROC1000  CALL      RDKTREA1
          BRANCH    OVRCD,PROC9999
          SCAN      CARAT,WMDESC
          GOTO      PROC1000 IF NOT EQUAL
.
          RESET     WMDESC
          MOVE      WMDESC,WMDESC1
          SCAN      CARAT,WMDESC1
          GOTO      PROC1000 IF NOT EQUAL
          BUMP      WMDESC1,-1
          LENSET    WMDESC1
          RESET     WMDESC1
          PACK      WMDESC,WMDESC1,SP70
          CALL      UPTREA1
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       WATTR1IO/INC

.******************************************************************************
.* System    : Patient                                                        *
.* Program   : IBAADT41                                                       *
.* Desc      : Casemix Codes Report Program                                   *
.******************************************************************************
.* Date      : 10/10/00                                                       *
.* Author    : Caleb Sharman                                                  *
.* Modifications :                                                            *
.*                                                                            *
.******************************************************************************
+
         INC        STD001FD
         INC        STDHLPDF
         INC        STDKWSDF
.
         INC        PATCMCFD/INC
.
. ----- Program Variables -----
.
DIM3      DIM       3
INSOPT    DIM       1
INSRTCD   DIM       9
INSRTCD1  DIM       9
INSRTCD2  DIM       9
KYIND     FORM      1                       * indicates item selected
LINECNT   FORM      2
PRNTCD1   DIM       9
PRNTCD2   DIM       9
TOTREC    FORM      5
WCHKEY    FORM      1
.
. ----- Program Constants -----
.
FINISH    INIT      "Finish"
START     INIT      "Start"
Z9        INIT      "ZZZZZZZZZ"
ZERO9     INIT      "000000000"
                    "                              "
PRGID     INIT      "IBAADT41"
PRGNAM    INIT      "Casemix Code Range Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                 MAINLINE                                   *
.*                             Controlling Logic                              *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialization and open files
.
ML1000    CALL      OPTN0000                * display options
          BRANCH    OPTION,ML2000
.
          GOTO      ML9999                  * exit selected
.
ML2000    CALL      RPRT0000                * enquiry screen    
          BRANCH    EXIT,ML3000  
          GOTO      ML1000                  * return to options
.
ML3000    CALL      PRNT0000                * print routine
.
ML9999    CHAIN     PGM                     * Chain back to program
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                              Initialization                                *
.*  The initialization routine is used to display headings and open files.    *
.*  Any other general initialization can also be performed here.              *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display heading
.
          DISPLAY   *P56:24,"Opening patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 DISP0000               Called by: 
.*                           Display template                        RPRT0000 *
.******************************************************************************
DISP0000  DISPLAY   *P8:9,*EF,"   Casemix Code Start  :":
                    *P8:10,"   Casemix Code Finish :";
.
DISP9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000               Called by: ML0000   *
.*                         Get user options or exit                           *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Casemix Code Range Report":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
          IF        OPTION > ONE
            GOTO      OPTN1000
          ENDIF
.
OPTN9999  RETURN
.******************************************************************************
.*                                 RPRT0000               Called by: ML3000   *
.*                           Find Casemix Code                                *
.******************************************************************************
RPRT0000  CALL      DISP0000
          MOVE      SP9,INSRTCD
          MOVE      ONE,WCHKEY
          MOVE      ZERO,EXIT
.
          KEYIN     *P34:9,INSRTCD           * keyin first code
.
          RESET     INSRTCD
          MATCH     SP9,INSRTCD              * nothing entered ?
          IF        @EQUAL
            DISPLAY   *P34:9,"Start    "   
            MOVE       ZERO9,INSRTCD1
            GOTO       RPRT1000
          ENDIF
.
          MATCH     INSRTCD,QUEST            * question mark entered ?
          IF        @EQUAL
            CALL      HPTCAA00
            MATCH     KEY9,SP9
            GOTO      RPRT0000 IF EQUAL
.
            MOVE      KEY9,INSRTCD1
            DISPLAY   *P34:9,INSRTCD1
            GOTO      RPRT1000
          ENDIF
.
          PACK      KEY9,INSRTCD
          CALL      RDPTCMC1
          BRANCH    OVRCD,RPRT3000
          MOVE      INSRTCD,INSRTCD1
.
RPRT1000  MOVE      SP9,INSRTCD
          KEYIN     *P34:10,INSRTCD          * keyin second code
.
          MOVE      TWO,WCHKEY
          RESET     INSRTCD
          MATCH     SP9,INSRTCD              * nothing entered ?
          IF        @EQUAL
            DISPLAY   *P34:10,"Finish   "
            MOVE       Z9,INSRTCD2
            GOTO       RPRT8500
          ENDIF
.
          MATCH     INSRTCD,QUEST            * question mark entered ?
          IF        @EQUAL
            CALL      HPTCAA00
            MATCH     KEY9,SP9
            GOTO      RPRT1000 IF EQUAL
            MOVE      KEY9,INSRTCD2
.
            MATCH     INSRTCD1,INSRTCD2
            GOTO      RPRT2000 IF LESS
.
            DISPLAY   *P34:10,INSRTCD2
            GOTO      RPRT8500
          ENDIF
.
          PACK      KEY9,INSRTCD
          CALL      RDPTCMC1
          BRANCH    OVRCD,RPRT3000
          MOVE      INSRTCD,INSRTCD2
          MATCH     INSRTCD1,INSRTCD2
          GOTO      RPRT2000 IF LESS
          GOTO      RPRT8500
.
RPRT2000  DISPLAY   *P2:24,"Finish code cannot precede Start code.       ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          BRANCH    WCHKEY,RPRT0000,RPRT1000
.
          GOTO      RPRT0000
.
RPRT3000  DISPLAY   *P2:24,"This code does not exist.       ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          BRANCH    WCHKEY,RPRT0000,RPRT1000
.
          GOTO      RPRT0000
RPRT8500  DISPLAY   *P2:24,"OK to Continue (Y/N/C) : ";
.
          KEYIN     *P27:24,INSOPT
.
          REP       UPPLOW,INSOPT
          MATCH     ANSY,INSOPT           * yes selected ?
          GOTO      RPRT9000 IF EQUAL
.
          MATCH     ANSN,INSOPT           * no selected ?
          GOTO      RPRT9999 IF EQUAL
.
          MATCH     ANSC,INSOPT           * cancel selected ?
          GOTO      RPRT9999 IF EQUAL
.
          GOTO      RPRT8500              * invalid entry
.
RPRT9000  MOVE      ONE,EXIT
          GOTO      RPRT9999
.
RPRT9999  RETURN
+
.
.****************************************************************************
.*                                HEAD0000             Called by: PRINT000  *
.*               Print the Header for each page to the file.                *
.****************************************************************************
HEAD0000  MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
          PRINT     *25,"Casemix Code - from:",SP2,PRNTCD1:
                    *65,"Casemix Code - to:",SP2,PRNTCD2:
                    *N,*N,*1,"*---------------------------------------":
                    "----------------------------------------------":
                    "---------------------------------------------*":
                    *N,*1,"| Casemix Code",*16,"|                Description":
                    *82,"| Expected LOS",*98,"| Active  ":
                    *N,*1,"*---------------------------------------":
                    "----------------------------------------------":
                    "---------------------------------------------*":
                    *N,*1,"*---------------------------------------":
                    "----------------------------------------------":
                    "---------------------------------------------*"
.
          MOVE      ZERO,LINECNT
.
HEAD9999  RETURN
+
.****************************************************************************
.*                                PRNT0000             Called by: ML0000    *
.*                           Prints Report.                                 *
.****************************************************************************
PRNT0000  MOVE      INSRTCD1,PRNTCD1
          MOVE      INSRTCD2,PRNTCD2
          MOVE      ZERO,TOTREC
.
          MATCH     PRNTCD1,ZERO9
          IF        @EQUAL
            PACK      PRNTCD1,START,SP10
          ENDIF
.
          MATCH     PRNTCD2,Z9
          IF        @EQUAL
            PACK      PRNTCD2,FINISH,SP10
          ENDIF
.
          MOVE      "61",CLNO
.
          PACK      KEY9,INSRTCD1
          MATCH     INSRTCD1,ZERO9
          GOTO      PRNT3000 IF EQUAL
.
          CALL      RDPTCMC1                * direct read
          GOTO      PRNT2000
.
PRNT1000  CALL      RKPTCMC1                * read next
          BRANCH    OVRCD,PRNT9000
.
PRNT2000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          IF        PTCAACTV = 1
            MOVE      "Yes",DIM3            * print 'yes'
          ELSE
            MOVE      "No",DIM3             * print 'no'
          ENDIF
.
          PRINT     *1,"|  ",PTCACODE,*16,"|   ",PTCADES1,PTCADES2:
                    *82,"| ",PTCAELOS,*98,"| ",DIM3,*132,"|"
.
          PRINT     *1,"*---------------------------------------":
                    "----------------------------------------------":
                    "---------------------------------------------*"
          ADD       TWO,CLNO
          ADD       ONE,TOTREC
.
          MATCH     PTCACODE,INSRTCD2       * compare current code & finish
          GOTO      PRNT9000 IF EQUAL       * end ?
.
          GOTO      PRNT1000                * read next record
.
PRNT3000  CALL      RSPTCMC1                * positional read
          CALL      RKPTCMC1                * read next
          GOTO      PRNT2000
.
PRNT9000  DISPLAY   *P40:23,*EL,*V2LON:
                    "** Report Generation Completed **"
          PRINT     *N,"TOTAL NUMBER OF RECORDS : ",TOTREC
.
          PRINT     *N,*N,"  ***  END OF REPORT  ***"
.
PRNT9999  RETURN
+
.
.****************************************************************************
.==============================================================================
.
          INC       STD001IO
          INC       STDHLPCD
.
          INC       PATCMCIO/INC
          INC       PATCMCKY                   * casemix codes keyin routine
+
.


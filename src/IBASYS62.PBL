.************************************************************************
.*  Program   :  IBASYS62                                               *
.*  Function  :  PABX CHarge file maintenance                           *
.*  Author    :  ROD   21/10/1996                                       *
.*  Mods      :                                                         *
.*        V9.02.00  06/05/2002  J.Tan                                   *
.*                  Ported from r6.09                                   *
.*         V6.04.02  17/01/1997  Steve Armstrong                        *
.*                   Fixed bugs from global recompile (TBC 7)           *
.************************************************************************
.
          INC       STD001FD
          INC       IBACHGFD/INC
          INC       PATCODFD/INC
.
CODE      DIM       2
EXISTS    FORM      1
FLDSEL    FORM      1
.
PRGID     INIT      "IBASYS62"
PRGNAM    INIT      "PABX Charge file Maintenance"
VERSION   INIT      "V12.01.00"
+
.************************************************************************
.*  ML0000  :  Mainline                                                 *
.************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      KCHG0000
          BRANCH    EXIT,ML9999,ML9999
          CALL      DISP0000                      * display details for change
.
ML2000    CALL      CHNG0000                      * keyin fields Change mode
          BRANCH    EXIT,ML1000,ML5000            * Cancel,Delete   
.
          CALL      POST0000                      * post
          GOTO      ML1000
.
ML5000    CALL      DELE0000                      * delete record if desired
          BRANCH    EXIT,ML2000
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.************************************************************************
.*  INIT0000  :  Initialisation                                         *
.************************************************************************
INIT0000  CALL      DISPHEAD
.
          OPEN      IBACHGA1,"ibachgaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
INIT9999  RETURN
+
.************************************************************************
.*  KCHG0000  :  Keyin Charge code                                      *
.************************************************************************
KCHG0000  DISPLAY   *P1:4,*EF:
                    *P5:4,"Charge Type : "
.
          MOVE      "22",ECOL
          MOVE      "4",EVERT
          MOVE      "CY",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      ONE,CNEWDS
.
          CALL      PATCODKY
          BRANCH    EXIT,KCHG9999,KCHG9999
.
          MOVE      ACODE,IBCHCHRG
.
          DISPLAY   *P30:4,TDESC
.
          MOVE      ZERO,IBCHRATE                      * clear field 
          MOVE      ZERO,EXISTS                        * assume doesn't exist
.
          PACK      KEY3,IBCHCHRG
          CALL      RDIBCHG1
          BRANCH    OVRCD,KCHG8000
.
          MOVE      ONE,EXISTS
.
. everything is Ok!
.
KCHG8000  MOVE      ZERO,EXIT
.
KCHG9999  RETURN
+
.************************************************************************
.*  KRAT0000  :  Keyin Charge Rate                                      *
.************************************************************************
KRAT0000  DISPLAY   *P1:6,*V2LON," 1",*HOFF,". Charge Rate $ : "
.
KRAT1000  KEYIN     *P21:6,*EL,*V2LON,*RV,IBCHRATE
.
          COMPARE   ZERO,IBCHRATE
          GOTO      KRAT1000 IF LESS
.
KRAT9999  RETURN
+
.************************************************************************
.*  INSR0000  :  Keyin fields insert mode                               *
.************************************************************************
INSR0000  CALL      KRAT0000                      * keyin charge rate
.
. any future keyins go in here
.
INSR9999  RETURN
+
.************************************************************************
.*  SOPT0000  :  get line 24 options                                    *
.************************************************************************
SOPT0000  DISPLAY   *P1:24,*EL,"Select Item, (",*V2LON:
                    "P",*HOFF,")ost, (",*V2LON,"C",*HOFF,")ancel, (":
                    *V2LON,"D",*HOFF,")elete ? "
.
SOPT1000  KEYIN     *P43:24,*DV,UNDLN1:
                    *P43:24,*V2LON,*JR,ANS:
                    *P43:24,*DV,ANS
.
          ENDSET    ANS
          APPEND    SP2,ANS
          RESET     ANS
.
          REP       UPPLOW,ANS                  * Change to lower case
.
          TYPE      ANS                         * was a numeric entered
          GOTO      SOPT1200 IF EQUAL
.
. a character was entered
.
          REP       "P1C2D3",ANS
.
          TYPE      ANS                         * valid letter entered?
          GOTO      SOPT1100 IF NOT EQUAL         * No.
.
          MOVE      ANS,FORM1
          BRANCH    FORM1,SOPT5000,SOPT6000,SOPT7000
.
SOPT1100  BEEP 
          GOTO      SOPT1000
.
. a numeric was entered so validate
.
SOPT1200  MOVE      ANS,FLDSEL                * move to form field
          IF        FLDSEL <> 1
            BEEP
            GOTO      SOPT1000
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      SOPT9999
.
SOPT5000  MOVE      ONE,EXIT         * post
          GOTO      SOPT9999
.
SOPT6000  MOVE      TWO,EXIT         * cancel
          GOTO      SOPT9999
.
SOPT7000  MOVE      THREE,EXIT       * delete
          GOTO      SOPT9999
.
SOPT9999  RETURN
+
.************************************************************************
.*  CHNG0000  :  change mode                                            *
.************************************************************************
CHNG0000  CALL      SOPT0000                      * Select item,Post,Canc,Del
          BRANCH    EXIT,CHNG8000,CHNG9000,CHNG9500
.
          BRANCH    FLDSEL,CHNG1000
.
          BEEP 
          GOTO      CHNG0000
.
CHNG1000  CALL      KRAT0000                      * keyin charge rate
          GOTO      CHNG0000
.
CHNG8000  MOVE      ZERO,EXIT                     * Post selected
          GOTO      CHNG9999
.
CHNG9000  MOVE      ONE,EXIT                      * Cancel selected
          GOTO      CHNG9999
.
CHNG9500  MOVE      TWO,EXIT                      * Delete selected
          GOTO      CHNG9999
.
CHNG9999  RETURN
+
.************************************************************************
.*  DISP0000  :  display details for change mode                        *
.************************************************************************
DISP0000  DISPLAY   *P1:6,*EF,*V2LON," 1",*HOFF,". Charge Rate $ : ":
                    *V2LON,IBCHRATE
.
DISP9999  RETURN
+
.************************************************************************
.*  POST0000  :  post data                                              *
.************************************************************************
POST0000  DISPLAY   *P1:24,*EL,*P35:24,"Posting...";
.
          BRANCH    EXISTS,POST2000               * insert or change mode?
.
. Insert mode
.
POST1000  PACK      KEY3,IBCHCHRG          
          CALL      WRIBCHG1
          GOTO      POST9999
.
. change mode
.
POST2000  CALL      UPIBCHG1
.
POST9999  RETURN
+
.************************************************************************
.*  DELE0000  :  delete record                                          *
.************************************************************************
DELE0000  CALL      DELQST
          BRANCH    CEXIT,DELE1000,DELE8000,DELE9000
.
DELE1000  DISPLAY   *P1:24,*EL,*P35:24,"Deleting...";
.
          PACK      KEY3,IBCHCHRG
          CALL      DEIBCHG1
          MOVE      ZERO,EXIT
          GOTO      DELE9999
.
DELE8000  MOVE      ONE,EXIT
          GOTO      DELE9999
.
DELE9000  MOVE      ZERO,EXIT
.
DELE9999  RETURN
.
.
          INC       STD001IO
          INC       IBACHGIO/INC
          INC       PATCODIO/INC
          INC       PATCODKY
.

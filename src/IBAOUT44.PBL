.
.***************************************************************************
.* System    :   OUTPATIENTS                                               *
.* Program   :   IBAOUT44                                                  *
.* Desc      :   Comparison Attendance Statistics                          *
.***************************************************************************
.* Date      :   27/04/89                                                  *
.* Author    :   Eric Phan                                                 *
.* Function  :   This program prints attendance statistics from the        *
.*               location file.                                            *
.* Mods      :                                                             *
.*       V10.05.01  15/07/2014  Ebon Clements     CAR 261630               *
.*                  Removed port number from temp file name                *
.*       V10.02.01  27/06/2011  Steve Armstrong   CAR 245347               *
.*                  Mods for changes to OUTLOCIO call routines             *
.***************************************************************************
.*       V5.07.01   15/10/1999  Steve Armstrong                            *
.*                  Removed reference to CSTRTYR (not used)                *
.*           V5.07.B02 18/01/1999  Nicole Harrington 507 rebound 095       *
.*                     Mods to use HEAD132                                 *
.*           V5.07.B01 30/10/1998  Jim Stathopoulos                        *
.*                     507 Changes                                         *
.***************************************************************************
.*               V5.01 29.05.89 ETP Added line separator to highlight      *
.*                     totals.                                             *
.*               V5.02 26.06.89 KP  Fixed doubling of totals SRF 101020    *
.*            V5.01.01 16/07/89 KP  Changed Adm & U/R to 8 characters      *
.*            V5.01.02 03/10/89 KP  Changed Month to Period  SRF 102550    *
.*            V5.02.03 14/12/89 E.Phan                                     *
.*                     Prev vers didn't cater for 13 week periods. Fixed.  *
.*            V5.01.04 26/05/91 J.Tan   SRF 108541                         *
.*                     Fixed the report heading                            *
.***************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCOMM/INC
          INC       OUTLOCFD/INC
          INC       OUTCTYFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDRGFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
CMDLINE   DIM       50
CODECG    INIT      "CG"          * Group Code
COUNT     FORM      2
COUNTY    FORM      2
CURPER    DIM       6
CSTPER    DIM       6
.
DIM3      DIM       3
DIM4      DIM       4
.
VENDDATE  DIM       6
.
FIRSTREC  FORM      1
FORM7     FORM      7
FORM7Y    FORM      7
.
.
MPERREQD  FORM      2         * Required period (calculated for every record)
MPERUSED  FORM      2         * Number of months used (calculated initially)
MPRTPOS   FORM      3         * Variable print position
.
NPYEAR    FORM      2
.
OUTTMP    DIM       8
OUTTMPXX  IFILE     SQL, FIXED=75, KEY="U1-9"
.
PDDESC    DIM       6         * Period date description (mmm yy)
PENDDATE  DIM       7
PSTRDATE  DIM       7
.
SAVGROUP  DIM       3
VSTRDATE  DIM       6
.
TGROUP    DIM       3         * (3) OUTTMPXX - Group Code
TCTYPE    DIM       6         * (6)          - Clinic Type
TCPER1    FORM      7         * (5)          - Totals for period 1
TCPER2    FORM      7         * (5)                       period 2
TCPER3    FORM      7         * (5)                       period 3
TCPER4    FORM      7         * (5)                       period 4
TCPER5    FORM      7         * (5)                       period 5
TCPER6    FORM      7         * (5)                       period 6
TCPER7    FORM      7         * (5)                       period 7
TCPER8    FORM      7         * (5)                       period 8
TCPER9    FORM      7         * (5)                       period 9
TCPER10   FORM      7         * (5)                       period 10
TCPER11   FORM      7         * (5)                       period 11
TCPER12   FORM      7         * (5)                       period 12
TCPER13   FORM      7         * (5)                       period 13
TCPERTOT  FORM      7         * period totals for Clinic Type
TCYTD1    FORM      7         * Clinic Type YTD totals for period 1
TCYTD2    FORM      7         *                            period 2
TCYTD3    FORM      7         *                            period 3
TCYTD4    FORM      7         *                            period 4
TCYTD5    FORM      7         *                            period 5
TCYTD6    FORM      7         *                            period 6
TCYTD7    FORM      7         *                            period 7
TCYTD8    FORM      7         *                            period 8
TCYTD9    FORM      7         *                            period 9
TCYTD10   FORM      7         *                            period 10
TCYTD11   FORM      7         *                            period 11
TCYTD12   FORM      7         *                            period 12
TCYTD13   FORM      7         *                            period 13
.
TGPER1    FORM      7         *** GROUP TOTALS ***
TGPER2    FORM      7
TGPER3    FORM      7
TGPER4    FORM      7
TGPER5    FORM      7
TGPER6    FORM      7
TGPER7    FORM      7
TGPER8    FORM      7
TGPER9    FORM      7
TGPER10   FORM      7
TGPER11   FORM      7
TGPER12   FORM      7
TGPER13   FORM      7
TGPERTOT  FORM      7
TGYTD1    FORM      7
TGYTD2    FORM      7
TGYTD3    FORM      7
TGYTD4    FORM      7
TGYTD5    FORM      7
TGYTD6    FORM      7
TGYTD7    FORM      7
TGYTD8    FORM      7
TGYTD9    FORM      7
TGYTD10   FORM      7
TGYTD11   FORM      7
TGYTD12   FORM      7
TGYTD13   FORM      7
.
TZPER1    FORM      7         *** GRAND TOTALS ***
TZPER2    FORM      7
TZPER3    FORM      7
TZPER4    FORM      7
TZPER5    FORM      7
TZPER6    FORM      7
TZPER7    FORM      7
TZPER8    FORM      7
TZPER9    FORM      7
TZPER10   FORM      7
TZPER11   FORM      7
TZPER12   FORM      7
TZPER13   FORM      7
TZPERTOT  FORM      7
TZYTD1    FORM      7
TZYTD2    FORM      7
TZYTD3    FORM      7
TZYTD4    FORM      7
TZYTD5    FORM      7
TZYTD6    FORM      7
TZYTD7    FORM      7
TZYTD8    FORM      7
TZYTD9    FORM      7
TZYTD10   FORM      7
TZYTD11   FORM      7
TZYTD12   FORM      7
TZYTD13   FORM      7
.
PRGID     INIT      "IBAOUT44"
PRGNAM    INIT      "Comparison Attendance Statistics"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
.
          CALL      INIT0000          * initialization and open files
          BRANCH    EXIT,ML9900
.
ML0100    CALL      OPTN0000          * select options
          BRANCH    EXIT,ML9900       * EXIT=1 if "0" chosen in menu
.
          CALL      PROC0000          * Process data
          BRANCH    FIRSTREC,ML9500   * If set, no records found
.
          CALL      PRNT0000          * Print data
.
          GOTO      ML0100
.
.--------------------------------------------------------------------------
.         No records found in processing pass ...
.--------------------------------------------------------------------------
.
ML9500    DISPLAY   *P30:24,*EL,*B,*V2LON,"*** No records found ***",*W;
          GOTO      ML0100
.
.--------------------------------------------------------------------------
.         The end ...
.--------------------------------------------------------------------------
.
ML9900    CALL      KILL0000
          CHAIN     PGM               * chain back to appropriate menu
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
.
INIT0000  MOVE        ZERO,EXIT
          CALL        DISPHEAD
.
          DISPLAY    *P56:24,"Opening controlf"
          OPEN       CONTROLF,"controlf"
          READ       CONTROLF,FIFTY9;*27,CPERMTH
.
          DISPLAY    *P64:24,"patcodes"
          OPEN       PATCODE1,"patcodes"
.
          DISPLAY    *P64:24,"patdrgaf"
          OPEN       PATDRGA1,"patdrgaf"
          OPEN       PATDRGA3,"patdrgaf"
.
          PACK       CFNAME,UID,FILCTYA1
          DISPLAY    *P64:24,CFNAME
          OPEN       OUTCTYA1,CFNAME
.
          PACK       CFNAME,UID,FILLOCA1
          DISPLAY    *P64:24,CFNAME
          OPEN       OUTLOCA1,CFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OUTTMP
.
          DISPLAY    *P1:24,*EL
.
.         Work out what the current period is, and the first period in the
.         current financial year
.
          PACK       CPTDATE,CCC,CYY,CMM,CDD
          REP        " 0",CPTDATE
          CALL       GPERD                  * Find the current period
          BRANCH     EXIT,INIT9000          * No current period found
.
          PACK       CURPER,DRGCYR,DRGCNUM
.
          PACK       KEY6,DRGYR,SP2
          CALL       RDSDRGA1
          CALL       RDKDRGA1
.
          PACK       CSTPER,DRGCYR,DRGCNUM
.
.         Now count the number of periods in a financial year
.
          MOVE       ZERO,NPYEAR
          MOVE       DRGYR,DIM4
          PACK       KEY6,DRGYR,SP2
          CALL       RDSDRGA1
INIT8000  CALL       RDKDRGA1
          BRANCH     OVRCD,INIT8100
.
          MATCH      DRGYR,DIM4
          GOTO       INIT8100 IF NOT EQUAL
.
          ADD        ONE,NPYEAR
          GOTO       INIT8000
.
.         Finished initialisation
.
INIT8100  DISPLAY    *P1:24,*EL
          MOVE       ZERO,EXIT
          GOTO       INIT9999
.
.         No current period on file
.
INIT9000  KEYIN      *P1:24,*EL,*B,*V2LON:
                     "** Error: Current date not found in Period file. ":
                     "Hit <ENTER> to exit ",*EOFF,ANS,*P1:24;
          MOVE       ONE,EXIT
.
INIT9999  RETURN 
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.****************************************************************************
.
OPTN0000   MOVE       ONE,EXIT
           DISPLAY    *P1:3,*EF:
                      *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                      *P1:5,*V2LON,ONE,*HOFF,". Group Code sequence"
OPTN0500   KEYIN      *P1:7,"Please Select : ",*V2LON,OPTION
           COMPARE    ZERO,OPTION
           GOTO       OPTN9999 IF EQUAL
           MOVE       ZERO,EXIT
           BRANCH     OPTION,OPTN3000
           BEEP
           GOTO       OPTN0500
.
.----------------------------------------------------------------------------
.          Keyin dates
.----------------------------------------------------------------------------
.
OPTN3000  DISPLAY   *P1:9,*EF,"Generate Report from "
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT
          MOVE      TWENTY2,CCOL
          MOVE      NINE,CVERT
          CALL      KEYPER
          BRANCH    OVRCD,OPTN0000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,OPTN3050
.
          PACK      VSTRDATE,DRGYR,DRGNUM
          PACK      PSTRDATE,DRGCNUM,SLASH,DRGCYR
.
.         Get the to date
.
OPTN3020  DISPLAY   *P30:9,"to "
          UNPACK    CURPER,CCENT,CYEAR,CMON
          MOVE      "33",CCOL
          CALL      KEYPER
          BRANCH    OVRCD,OPTN3000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    EXIT,OPTN3060
.
          PACK      VENDDATE,DRGYR,DRGNUM
          PACK      PENDDATE,DRGCNUM,SLASH,DRGCYR
.
          MATCH     VSTRDATE,VENDDATE
          GOTO      OPTN3100 IF NOT LESS
          DISPLAY   *P10:24,*V2LON,*B,"*** TO date cannot be ":
                    "before FROM date *** ",*W,*P1:24,*EL;
          GOTO      OPTN3000
.
.         From date not on file
.
OPTN3050  DISPLAY   *P10:24,*V2LON,*B,"*** Invalid Date *** ",*W,*P1:24,*EL;
          GOTO      OPTN3000
.
.         To date not on file
.
OPTN3060  DISPLAY   *P10:24,*V2LON,*B,"*** Invalid Date *** ",*W,*P1:24,*EL;
          GOTO      OPTN3020
.
.         Calculate MPERUSED - # of periods used
.
OPTN3100  MOVE      ZERO,MPERUSED
          PACK      KEY6,VSTRDATE
          CALL      RDDRGA1
          BRANCH    OVRCD,OPTN3200
          GOTO      OPTN3250
OPTN3200  CALL      RDKDRGA1
          BRANCH    OVRCD,OPTN3300
.
OPTN3250  PACK      XDATE,DRGYR,DRGNUM
          MATCH     XDATE,VENDDATE
          GOTO      OPTN3300 IF LESS
.
          ADD       ONE,MPERUSED
          GOTO      OPTN3200
.
.         Validate the number of periods
.
OPTN3300  COMPARE   TEN4,MPERUSED
          GOTO      OPTN9000 IF LESS
.
          DISPLAY   *P10:24,*V2LON,*B,"*** Maximum 13 periods allowed *** ":
                    *W,*P1:24,*EL;
          GOTO      OPTN3000
.
.----------------------------------------------------------------------------
.         Ok to proceed ?
.----------------------------------------------------------------------------
.
OPTN9000  MOVE      SP1,ANS
          KEYIN     *P1:24,"Ok to proceed (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS
          CMATCH    ANSY,ANS
          GOTO      OPTN9999 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      OPTN0000 IF EQUAL
          BEEP
          GOTO      OPTN9000
.
OPTN9999  RETURN
.
+
.**************************************************************************
.*                              PROC0000                                  *
.*                            Process data                                *
.**************************************************************************
.
PROC0000  CALL      CREA0000               * Create temp file
          MOVE      ONE,FIRSTREC           * 1st record not read yet
.
          DISPLAY   *P1:11,*EF,*V2LON:
                    "*** Processing Records ***",*HOFF:
                    *P1:13,"Group Code  :":
                    *P1:14,"Clinic Type :":
                    *P1:15,"Date        :"
.
          PACK      KEY24,IDDD,SP30
          CALL      RSOTLOC1
PROC1000  CALL      RKOTLOC1
          BRANCH    OVRCD,PROC9999         * No more records
.
          MATCH     LOASITE,IDDD
          GOTO      PROC9999 IF NOT EQUAL  * Different site - exit
          MATCH     VSTRDATE,LOADATE
          GOTO      PROC1000 IF LESS       * Before start of date range
          MATCH     LOADATE,VENDDATE
          GOTO      PROC1000 IF LESS       * Past end of date range
.
          UNPACK    LOADATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:13,*V2LON,LOAGRUP,*P15:14,LOATYPE:
                    *P15:15,CMON,SLASH,CCENT,CYEAR
          MOVE      ZERO,FIRSTREC          * Got a record
.
          CALL      CALM0000          * Calculate period # for this month
.
          PACK      KEY9,LOAGRUP,LOATYPE
          CALL      RDTMP1
          BRANCH    OVRCD,PROC4000
.
.         Update existing record
.
          LOAD      FORM7,MPERREQD,TCPER1,TCPER2,TCPER3,TCPER4:
                                   TCPER5,TCPER6,TCPER7,TCPER8:
                                   TCPER9,TCPER10,TCPER11,TCPER12:
                                   TCPER13
          ADD       LOANUMB,FORM7
          STORE     FORM7,MPERREQD,TCPER1,TCPER2,TCPER3,TCPER4:
                                   TCPER5,TCPER6,TCPER7,TCPER8:
                                   TCPER9,TCPER10,TCPER11,TCPER12:
                                   TCPER13
          CALL      UPTMP1
          GOTO      PROC1000
.
.         Write new record
.
PROC4000  MOVE      LOAGRUP,TGROUP
          MOVE      LOATYPE,TCTYPE
          MOVE      ZERO,TCPER1
          MOVE      ZERO,TCPER2
          MOVE      ZERO,TCPER3
          MOVE      ZERO,TCPER4
          MOVE      ZERO,TCPER5
          MOVE      ZERO,TCPER6
          MOVE      ZERO,TCPER7
          MOVE      ZERO,TCPER8
          MOVE      ZERO,TCPER9
          MOVE      ZERO,TCPER10
          MOVE      ZERO,TCPER11
          MOVE      ZERO,TCPER12
          MOVE      ZERO,TCPER13
          STORE     LOANUMB,MPERREQD,TCPER1,TCPER2,TCPER3,TCPER4:
                                     TCPER5,TCPER6,TCPER7,TCPER8:
                                     TCPER9,TCPER10,TCPER11,TCPER12,TCPER13
          CALL      WRTMP1
          GOTO      PROC1000
.
PROC9999  RETURN
.
.----------------------------------------------------------------------------
.         Calculate the period for a particular month - value in MPERREQD
.----------------------------------------------------------------------------
.
CALM0000  UNPACK    VSTRDATE,XCENT,XYEAR,XMON
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
          MOVE      XCENT,ICENT
.
          UNPACK    LOADATE,CCENT,CYEAR,CMON
          MOVE      CMON,MPERREQD
.
          MATCH     XYEAR,CYEAR
          GOTO      CALM1000 IF EQUAL
.
          ADD       NPYEAR,MPERREQD          * Different year
.
CALM1000  SUB       IMON,MPERREQD          * Same year
          ADD       ONE,MPERREQD
.
CALM9999  RETURN
.
+
.**************************************************************************
.*                              PRNT0000                                  *
.*                              Print data                                *
.**************************************************************************
.
PRNT0000  MOVE      "80",CLNO
          UNPACK    SP30,KEY9,SAVGROUP
          DISPLAY   *P1:11,*EF,*V2LON:
                    "*** Printing Records ***",*HOFF:
                    *P1:13,"Group Code  :":
                    *P1:14,"Clinic Type :"
.
          CALL      RDSTMP1
.
PRNT1000  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT8000
.
          DISPLAY   *P15:13,*V2LON,TGROUP,*P15:14,TCTYPE
.
          MATCH     SP3,SAVGROUP
          GOTO      PRNT2000 IF EQUAL
          MATCH     SAVGROUP,TGROUP
          GOTO      PRNT2000 IF EQUAL
.
          CALL      PRNX3000              * Print group totals
.
PRNT2000  MOVE      TGROUP,SAVGROUP
          CALL      PRNX1000              * Print record
.
          GOTO      PRNT1000
.
PRNT8000  CALL      PRNX3000              * Print last group totals
          CALL      PRNX4000              * Print grand totals
          CALL      PRNX7000              * Print end of report
.
PRNT9999  RETURN
.
+
.****************************************************************************
.*                                                                          *
.*                        Printing subroutines                              *
.*                                                                          *
.****************************************************************************
.         Print Record
.----------------------------------------------------------------------------
.
PRNX1000  COMPARE   "55",CLNO
          GOTO      PRNX1100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX1100  MOVE      "**ERROR** Unknown Clinic Type ",OCTDESC
          PACK      KEY12,IDDD,TCTYPE
          CALL      RDCTYA1
.
          PRINT     *N,TCTYPE,*8,OCTDESC,*31,"MTH:";
.
          MOVE      ZERO,TCPERTOT    * Initialise totals & YTD variables
          MOVE      ZERO,TCYTD1
          MOVE      ZERO,TCYTD2
          MOVE      ZERO,TCYTD3
          MOVE      ZERO,TCYTD4
          MOVE      ZERO,TCYTD5
          MOVE      ZERO,TCYTD6
          MOVE      ZERO,TCYTD7
          MOVE      ZERO,TCYTD8
          MOVE      ZERO,TCYTD9
          MOVE      ZERO,TCYTD10
          MOVE      ZERO,TCYTD11
          MOVE      ZERO,TCYTD12
          MOVE      ZERO,TCYTD13
.
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX1200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX1500 IF LESS
          LOAD      FORM7,COUNT,TCPER1,TCPER2,TCPER3,TCPER4,TCPER5,TCPER6:
                                TCPER7,TCPER8,TCPER9,TCPER10,TCPER11,TCPER12:
                                TCPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to group monthly figures
.
          LOAD      FORM7Y,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5,TGPER6:
                                 TGPER7,TGPER8,TGPER9,TGPER10,TGPER11,TGPER12:
                                 TGPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5,TGPER6:
                                 TGPER7,TGPER8,TGPER9,TGPER10,TGPER11,TGPER12:
                                 TGPER13
.
.         Add to clinic type total & calculate YTD figures
.
          ADD       FORM7,TCPERTOT
          MOVE      COUNT,COUNTY      * Start from current processed period
.
PRNX1300  LOAD      FORM7Y,COUNTY,TCYTD1,TCYTD2,TCYTD3,TCYTD4,TCYTD5,TCYTD6:
                                  TCYTD7,TCYTD8,TCYTD9,TCYTD10,TCYTD11,TCYTD12:
                                  TCYTD13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNTY,TCYTD1,TCYTD2,TCYTD3,TCYTD4,TCYTD5,TCYTD6:
                                  TCYTD7,TCYTD8,TCYTD9,TCYTD10,TCYTD11,TCYTD12:
                                  TCYTD13
          ADD       ONE,COUNTY
          COMPARE   COUNTY,MPERUSED
          GOTO      PRNX1400 IF LESS
          GOTO      PRNX1300
.
PRNX1400  GOTO      PRNX1200
.
.         Finished clinic type monthly figures, now print YTD figures
.
PRNX1500  PRINT     *MPRTPOS,TCPERTOT,*N,*31,"YTD:";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX1600  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX1900 IF LESS
          LOAD      FORM7,COUNT,TCYTD1,TCYTD2,TCYTD3,TCYTD4,TCYTD5,TCYTD6:
                                TCYTD7,TCYTD8,TCYTD9,TCYTD10,TCYTD11,TCYTD12:
                                TCYTD13
          PRINT     *MPRTPOS,FORM7;
.
          GOTO      PRNX1600
.
PRNX1900  PRINT     *1                    * Get to next line
          ADD       THREE,CLNO
          RETURN
.
.----------------------------------------------------------------------------
.         Print New Page
.----------------------------------------------------------------------------
.
PRNX2000  MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
.
PRNX2100  CALL      HEAD132
          PRINT     *N,*40,"PERIOD FROM ",PSTRDATE," TO ":
                       PENDDATE:
                    *N,*N,"Clinic Type/Description";
.
.         Loop over the period file and get the description for each column
.
          MOVE      "35",MPRTPOS  * Location to start actual print
          MOVE      VSTRDATE,KEY6
          CALL      RDDRGA1
PRNX2200  PACK      XDATE,DRGYR,DRGNUM
.
          MATCH     XDATE,VENDDATE
          GOTO      PRNX2300 IF LESS
.
          UNPACK    DRGCYR,CCENT,CYEAR
.
          PRINT     *MPRTPOS,SP1,DRGSDSC,SP1,CYEAR;
          ADD       SEVEN,MPRTPOS
.
          CALL      RDKDRGA1
          BRANCH    OVRCD,PRNX2300
          GOTO      PRNX2200
.
PRNX2300  PRINT     *MPRTPOS,"  Total":
                    *N,"----------------------------------";
.
          CALL      PRNX5000         * Print rest of separator lines
.
          MOVE      SEVEN,CLNO
          RETURN
.
.----------------------------------------------------------------------------
.         Print group totals
.----------------------------------------------------------------------------
.
PRNX3000  COMPARE   "55",CLNO
          GOTO      PRNX3100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX3100  MOVE      "*ERROR*Unknown Group",TDESC
          PACK      KEY5,CODECG,SAVGROUP
          CALL      RDCODE1
.
          CALL      PRNX5000            * Print separator lines
          PRINT     *1,"Total Group ",SAVGROUP,SP1,TDESC,*31,"MTH:";
.
          MOVE      ZERO,TGPERTOT    * Initialise totals & YTD variables
          MOVE      ZERO,TGYTD1
          MOVE      ZERO,TGYTD2
          MOVE      ZERO,TGYTD3
          MOVE      ZERO,TGYTD4
          MOVE      ZERO,TGYTD5
          MOVE      ZERO,TGYTD6
          MOVE      ZERO,TGYTD7
          MOVE      ZERO,TGYTD8
          MOVE      ZERO,TGYTD9
          MOVE      ZERO,TGYTD10
          MOVE      ZERO,TGYTD11
          MOVE      ZERO,TGYTD12
          MOVE      ZERO,TGYTD13
.
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX3200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX3500 IF LESS
          LOAD      FORM7,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5,TGPER6:
                                TGPER7,TGPER8,TGPER9,TGPER10,TGPER11,TGPER12:
                                TGPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to grand total monthly figures
.
          LOAD      FORM7Y,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5,TZPER6:
                                 TZPER7,TZPER8,TZPER9,TZPER10,TZPER11,TZPER12:
                                 TZPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5,TZPER6:
                                 TZPER7,TZPER8,TZPER9,TZPER10,TZPER11,TZPER12:
                                 TZPER13
.
.         Add to group total & calculate YTD figures
.
          ADD       FORM7,TGPERTOT
          MOVE      COUNT,COUNTY      * Start from current processed period
.
PRNX3300  LOAD      FORM7Y,COUNTY,TGYTD1,TGYTD2,TGYTD3,TGYTD4,TGYTD5,TGYTD6:
                                  TGYTD7,TGYTD8,TGYTD9,TGYTD10,TGYTD11,TGYTD12:
                                  TGYTD13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNTY,TGYTD1,TGYTD2,TGYTD3,TGYTD4,TGYTD5,TGYTD6:
                                  TGYTD7,TGYTD8,TGYTD9,TGYTD10,TGYTD11,TGYTD12:
                                  TGYTD13
          ADD       ONE,COUNTY
          COMPARE   COUNTY,MPERUSED
          GOTO      PRNX3400 IF LESS
          GOTO      PRNX3300
.
PRNX3400  GOTO      PRNX3200
.
.         Finished group monthly figures, now print YTD figures
.
PRNX3500  PRINT     *MPRTPOS,TGPERTOT,*N,*31,"YTD:";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX3600  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX3900 IF LESS
          LOAD      FORM7,COUNT,TGYTD1,TGYTD2,TGYTD3,TGYTD4,TGYTD5,TGYTD6:
                                TGYTD7,TGYTD8,TGYTD9,TGYTD10,TGYTD11,TGYTD12:
                                TGYTD13
          PRINT     *MPRTPOS,FORM7;
.
          GOTO      PRNX3600
.
PRNX3900  PRINT     *1                    * Get to next line
          CALL      PRNX5000              * Print separator lines
          ADD       FOUR,CLNO
.
          MOVE      ZERO,TGPER1
          MOVE      ZERO,TGPER2
          MOVE      ZERO,TGPER3
          MOVE      ZERO,TGPER4
          MOVE      ZERO,TGPER5
          MOVE      ZERO,TGPER6
          MOVE      ZERO,TGPER7
          MOVE      ZERO,TGPER8
          MOVE      ZERO,TGPER9
          MOVE      ZERO,TGPER10
          MOVE      ZERO,TGPER11
          MOVE      ZERO,TGPER12
          MOVE      ZERO,TGPER13
.
          MOVE      ZERO,TGPERTOT    
          MOVE      ZERO,TGYTD1
          MOVE      ZERO,TGYTD2
          MOVE      ZERO,TGYTD3
          MOVE      ZERO,TGYTD4
          MOVE      ZERO,TGYTD5
          MOVE      ZERO,TGYTD6
          MOVE      ZERO,TGYTD7
          MOVE      ZERO,TGYTD8
          MOVE      ZERO,TGYTD9
          MOVE      ZERO,TGYTD10
          MOVE      ZERO,TGYTD11
          MOVE      ZERO,TGYTD12
          MOVE      ZERO,TGYTD13
.
          RETURN
.
.----------------------------------------------------------------------------
.         Print grand totals
.----------------------------------------------------------------------------
.
PRNX4000  COMPARE   "55",CLNO
          GOTO      PRNX4100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX4100  PRINT     *1,"Grand Total",*31,"MTH:";
.
          MOVE      ZERO,TZPERTOT    * Initialise totals & YTD variables
          MOVE      ZERO,TZYTD1
          MOVE      ZERO,TZYTD2
          MOVE      ZERO,TZYTD3
          MOVE      ZERO,TZYTD4
          MOVE      ZERO,TZYTD5
          MOVE      ZERO,TZYTD6
          MOVE      ZERO,TZYTD7
          MOVE      ZERO,TZYTD8
          MOVE      ZERO,TZYTD9
          MOVE      ZERO,TZYTD10
          MOVE      ZERO,TZYTD11
          MOVE      ZERO,TZYTD12
          MOVE      ZERO,TZYTD13
.
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX4200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX4500 IF LESS
          LOAD      FORM7,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5,TZPER6:
                                TZPER7,TZPER8,TZPER9,TZPER10,TZPER11,TZPER12:
                                TZPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to group total & calculate YTD figures
.
          ADD       FORM7,TZPERTOT
          MOVE      COUNT,COUNTY      * Start from current processed period
.
PRNX4300  LOAD      FORM7Y,COUNTY,TZYTD1,TZYTD2,TZYTD3,TZYTD4,TZYTD5,TZYTD6:
                                  TZYTD7,TZYTD8,TZYTD9,TZYTD10,TZYTD11,TZYTD12:
                                  TZYTD13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNTY,TZYTD1,TZYTD2,TZYTD3,TZYTD4,TZYTD5,TZYTD6:
                                  TZYTD7,TZYTD8,TZYTD9,TZYTD10,TZYTD11,TZYTD12:
                                  TZYTD13
          ADD       ONE,COUNTY
          COMPARE   COUNTY,MPERUSED
          GOTO      PRNX4400 IF LESS
          GOTO      PRNX4300
.
PRNX4400  GOTO      PRNX4200
.
.         Finished group monthly figures, now print YTD figures
.
PRNX4500  PRINT     *MPRTPOS,TZPERTOT,*N,*31,"YTD:";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX4600  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX4900 IF LESS
          LOAD      FORM7,COUNT,TZYTD1,TZYTD2,TZYTD3,TZYTD4,TZYTD5,TZYTD6:
                                TZYTD7,TZYTD8,TZYTD9,TZYTD10,TZYTD11,TZYTD12:
                                TZYTD13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to YTD total
.
          ADD       FORM7,TGPERTOT
          GOTO      PRNX4600
.
PRNX4900  PRINT     *1                    * Get to next line
          CALL      PRNX6000              * Print separator lines
          RETURN
.
.----------------------------------------------------------------------------
.         Print page separator type 1
.----------------------------------------------------------------------------
.
PRNX5000  MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
PRNX5100  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX5200 IF LESS
          PRINT     *MPRTPOS," ------";
          GOTO      PRNX5100
PRNX5200  PRINT     *MPRTPOS," ------"
          RETURN
.
.----------------------------------------------------------------------------
.         Print page separator type 2
.----------------------------------------------------------------------------
.
PRNX6000  MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
PRNX6100  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX6200 IF LESS
          PRINT     *MPRTPOS," ======";
          GOTO      PRNX6100
PRNX6200  PRINT     *MPRTPOS," ======"
          RETURN
.
.----------------------------------------------------------------------------
.         Print tail of report
.----------------------------------------------------------------------------
.
PRNX7000  PRINT     *N,*N,"*** END OF REPORT ***"
          RETURN
+
.**************************************************************************
.*                              CREA0000                                  *
.*                   Create temp file                                     *
.**************************************************************************
.
CREA0000  CALL      KILL0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          APPEND    " 75 u1-9",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      OUTTMPXX,OUTTMP
          RETURN
+
.**************************************************************************
.*                              KILL0000                                  *
.*                   Remove temp file                                     *
.**************************************************************************
.
KILL0000  PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          RESET     CMDLINE
          CLOSE     OUTTMPXX
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.------------------------------------------------------------------------------
.         Temp file I/O's
.------------------------------------------------------------------------------
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OUTTMPXX,KEY9;TGROUP,TCTYPE,TCPER1,TCPER2,TCPER3,TCPER4:
                                                TCPER5,TCPER6,TCPER7,TCPER8:
                                                TCPER9,TCPER10,TCPER11,TCPER12:
                                                TCPER13
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OUTTMPXX,KEY9;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    OUTTMPXX;TGROUP,TCTYPE,TCPER1,TCPER2,TCPER3,TCPER4:
                                           TCPER5,TCPER6,TCPER7,TCPER8:
                                           TCPER9,TCPER10,TCPER11,TCPER12:
                                           TCPER13
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    MOVE      ZERO,OVRCD
          UPDATE    OUTTMPXX;TGROUP,TCTYPE,TCPER1,TCPER2,TCPER3,TCPER4:
                                           TCPER5,TCPER6,TCPER7,TCPER8:
                                           TCPER9,TCPER10,TCPER11,TCPER12:
                                           TCPER13
          RETURN
.
WRTMP1    MOVE      ZERO,OVRCD
          RESET     KEY9
          WRITE     OUTTMPXX,KEY9;KEY9,TCPER1,TCPER2,TCPER3,TCPER4:
                                       TCPER5,TCPER6,TCPER7,TCPER8:
                                       TCPER9,TCPER10,TCPER11,TCPER12:
                                       TCPER13
          GOTO      OVERCOND IF OVER
          RETURN
+
. =========================================================================
. Includes for all file I/O would be included here.
. Includes for all procedural routines would be included here.
. =========================================================================
.
           INC        STD001IO
           INC        PATCOMN3
           INC       TFILENAM
           INC       IBASEQIO/INC
           INC       OUTCTYIO/INC
           INC       OUTLOCIO/INC
           INC       PATCODIO/INC
           INC        PATDRGIO/INC
           INC       WEBERRIO/INC
           
.

.----------------------------------------------------------------------
. Program       : LEGWEB02.PBL
.
. Function      : Alternate (Customised) Legacy patient visit enquiry server
.
. Modifications :
.----------------------------------------------------------------------
. V11.02.01 09/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V10.11.02 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.01 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
.----------------------------------------------------------------------
. V10.10.01 06/04/2017  Don Nguyen   TSK 0834292
.           Modified LEGTXT00 to use LGTXADAT, LGTXATIM, LGTXEVNO
. V10.08.01 29/06/2016  Peter Vela   TSK 0821714
.           Added Event Types 4 - 7 to DISVIS15
. V10.07.03 26/11/2015  Ania P       CAR 321333
.           Added URNUMBER to CLRPAR00 to fix a multitude of issues
. V10.07.02 12/11/2015  Ania P       CAR 321333
.           Added new legacm variables to CLRPAR00
. V10.07.01 01/10/2015  Ania P       CAR 321333
.           Removed LEGERROR TRAP for legacmaf
. V10.06.01 22/09/2015  Ania P       CAR 321333 
.           Merged NOMORE00/VISITS00/ALLTAB00 as these are single call
.           blocks of code. Changes to generically match filter to row
.           Added output VIST0000 for new tablesort legweb0201001.html
. V10.04.02 07/04/2014  Peter Vela   CAR 286258
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
. V10.03.02 06/08/2013  Ania P       CAR 289619
.           Added missing </tr> tag to NOMORE00
. V10.03.01 01/10/2012  Peter Vela   CAR 275398
.           Added wahealth table format to ALLTAB00
. V10.02.01 26/07/2011  Steve Armstrong   CAR 240684
.           Mods for KEY change in LEGALTFD (KEY24 to KEY32).
.----------------------------------------------------------------------
. V9.09.04  11/04/2008  Ebon Clements CAR 166050
.           Changed VSTATUS to DIM 50 
. V9.09.03  10/08/2007  Mike Laming   CAR 146598
.           Change LGATSTAT to "strip" instead of "squeeze", remove " at "
. V9.09.02  07/08/2007  Mike Laming   CAR 146598
.           Add a second format (for NZ) for Alt Legacy screen (SFORMAT)
. V9.09.01  29/06/2007  Mike Laming   CAR 144012
.           Correct Next & Previous routines to link to LEGWEB02
. V9.08.02  19/02/2007 Peter Vela     CAR 135241  
.           Recompiled for WEBDINVS - St. John of God invoice layout
. V9.08.01  31/01/2007  Peter Vela     CAR 127930
.           Recompiled for PATMASTM
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B00 02/03/2006   Mike Laming   CAR 92179
.           Created Web Server.
.----------------------------------------------------------------------
          INC       IBAWEBDF
          INC       WEBDATDF
.
          INC       PATDO1FD/INC   * Doctor File
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       PATHSPFD/INC
          INC       OUTSITFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PATIN1FD/INC
          INC       APSMASFD/INC
          INC       LEGACMFD/INC
.
          INC       PMSCCDFD/INC            * Concession Card Details
          INC       PMSPX2TD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCPTD/INC
.
. Legacy Files
.
          INC       LEGALTFD/INC
          INC       WEBDINVS/INC
.
.----------------------------------------------------------------------
DISPSUR   DIM       8                          * Display Saved U/R Number
DISPMUR   DIM       8                          * Display Merged U/R Number
DISPDATT  DIM       25
DOCTCODE  DIM       8
FORM8     FORM      8
FILTVTYP  FORM      2
INVOICEN  DIM       8
LEGERROR  FORM      1
LGTXADAT  DIM       8       * Variable to save value of LGACADAT
LGTXATIM  DIM       8       * Variable to save value of LGACATIM
LGTXEVNO  DIM       8       * Variable to save value of LGACEVNO
.
NEXTVKEY  DIM       26
PREVVKEY  DIM       26
MRGEFLAG  FORM      1
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
PREVFLAG  FORM      1
PSAGETYP  DIM       3
PSAGECON  DIM       3
SFORMAT   DIM       3
SHOWFLAG  FORM      1
STARTKEY  DIM       10      * Starting Key
SAVEURNO  DIM       8
UPDTTYPE  DIM       1       * Update Type
VISTEMER  INIT      "EMR"
VISTOUTP  INIT      "OP"
VISTINPT  INIT      "IP"
VISTWLST  INIT      "WL"
VISTREFF  INIT      "RF"
VISTTHEA  INIT      "THE"
VISTOTHE  INIT      "OTH"
VTXTEMER  INIT      "Emergency"
VTXTOUTP  INIT      "Outpatient"
VTXTINPT  INIT      "Inpatient"
VTXTWLST  INIT      "Waiting List"
VTXTREFF  INIT      "Referrals"
VTXTTHEA  INIT      "Theatre"
VTXTOTHE  INIT      "Other Event"
VISTTYPE  DIM       5
VSTATUS   DIM       50
VISITTYP  FORM      1
.
NJAN      INIT      "January"
NFEB      INIT      "February"
NMAR      INIT      "March"
NAPR      INIT      "April"
NMAY      INIT      "May"
NJUN      INIT      "June"
NJUL      INIT      "July"
NAUG      INIT      "August"
NSEP      INIT      "September"
NOCT      INIT      "October"
NNOV      INIT      "November"
NDEC      INIT      "December"
.
.----------------------------------------------------------------------
PRGID     INIT      "LEGWEB02"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Legacy Visits"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option - LEGWEB02",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      LEGPAT00        * Get Patient Details
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      LEGACM00        * Get Patient Free Text (legacmaf)
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATIN1A1,"patin1af"
          OPEN      PMSCCDA1,"pmsccdaf"
.
          OPEN      LEGALTA1,"legaltaf"
          OPEN      LEGACMA1,"legacmaf"
.
          READ      CONTROLF,ZERO;*85,IBCNUGST
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      SP70,URNUMBER
          MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,SAVEURNO
          MOVE      ZERO,MRGEFLAG
          MOVE      ZERO,FILTVTYP
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,PREVFLAG
          MOVE      SP70,ADMISSNO
          MOVE      SP70,NEXTVKEY
          MOVE      SP70,PREVVKEY
          MOVE      ZERO,VISITTYP
          MOVE      SP70,INVOICEN
          MOVE      SP70,LGTXADAT
          MOVE      SP70,LGTXATIM
          MOVE      SP70,LGTXEVNO
          MOVE      SP70,LGACURNO
          MOVE      SP70,LGACADAT
          MOVE      SP70,LGACATIM
          MOVE      SP70,LGACEVNO
          MOVE      SP70,LGACLINE
          MOVE      SP70,LGACCOMM
          MOVE      SP70,LGATURNO
          MOVE      SP70,LGATADAT
          MOVE      SP70,LGATATIM
          MOVE      SP70,LGATEVNO
          MOVE      SP70,LGATTYPE
          MOVE      SP70,LGATSTAT
          MOVE      SP70,LGATDDRG
          MOVE      SP70,LGATDDAT
          MOVE      SP70,LGATDTIM
          MOVE      SP70,LGATHOSC
          MOVE      SP70,LGATATDR
          MOVE      SP70,LGATPRPF
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtvtyp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTVTYP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prevflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PREVFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextvkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTVKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prevvkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PREVVKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "visittyp=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,VISITTYP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "evntdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LGTXADAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "evnttime=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LGTXATIM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "evntnumb=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LGTXEVNO
            GOTO      SETPAR99
          ENDIF
.

SETPAR99  RETURN
.
.------------------------------------------------------------
. Alert that this is a merged U/R
.------------------------------------------------------------
MRGALT00  MATCH     SP70,URNUMBER
          GOTO      MRGALT99 IF EQUAL
.
          MATCH     SP70,SAVEURNO
          GOTO      MRGALT99 IF EQUAL
.
          MOVE      SAVEURNO,DISPSUR
          SQUEEZE   DISPSUR
          MOVE      URNUMBER,DISPMUR
          SQUEEZE   DISPMUR
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " alert('U/R Number ",DISPSUR," is merged with ":
                             DISPMUR," \n Using ",DISPMUR,"');":
                             "}":
                             "</script>"
          GOTO      MRGALT99
.
MRGALT99  RETURN
.
.------------------------------------------------------------
. Process Fields
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20
.
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000     * Master File Info
          GOTO      PROFLD99
.
PROFLD12  CALL      TABL0000     * Table Displays
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22
          GOTO      PROFLD99
.
PROFLD21  CALL      LEGTXT00     * Legacy Alt Comments Text
          GOTO      PROFLD99
.
PROFLD22  CALL      LGACMT00     * Legacy Alt Comments Fields
          GOTO      PROFLD99
.
PROFLD99  RETURN
.
.------------------------------------------------------------------------------
. Table Outputs (Visit,etc)
.------------------------------------------------------------------------------
TABL0000  BRANCH    FLDITMNO,TABL0100,TABL0200,TABL0300,TABL0400,TABL0500
          GOTO      TABL9999
.
TABL0100  CALL      ALLTAB00                 * Visit list by type
          GOTO      TABL9999
.
TABL0200  WRITE     HTMLFILE;FILTVTYP;       * Visit Type Filter
          GOTO      TABL9999
.
TABL0300  CALL      PREVGR00                 * Previous group
          GOTO      TABL9999
.
TABL0400  CALL      NEXTGR00                 * Next group
          GOTO      TABL9999
.
TABL0500  CALL      VIST0000                 * Visit Sort
          GOTO      TABL9999
.
TABL9999  RETURN
.------------------------------------------------------------------------------
. Link to Prev Group
.------------------------------------------------------------------------------
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",ADMISSNO
          REP       " +",URNUMBER
.
          WRITE     HTMLFILE;"legweb02.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&filtvtyp=",FILTVTYP:
                                 "&urnumber=",URNUMBER:
                                 "&admissno=",ADMISSNO;
.
          REP       "+ ",ADMISSNO
          REP       "+ ",URNUMBER
.
PREVGR99  RETURN
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",ADMISSNO
          REP       " +",URNUMBER
.
          WRITE     HTMLFILE;"legweb02.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&filtvtyp=",FILTVTYP:
                                 "&urnumber=",URNUMBER:
                                 "&admissno=",ADMISSNO;
.
          REP       "+ ",ADMISSNO
          REP       "+ ",URNUMBER
.
NEXTGR99  RETURN
.
.------------------------------------------------------------
. VIST0000 - Visits' Table Sort
.------------------------------------------------------------
VIST0000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MATCH     "       0",URNUMBER
          GOTO      VIST9999 IF EQUAL
.
          PACK      KEY32,URNUMBER,Z70
          CALL      RSLGALT1
VIST0010  CALL      RPLGALT1
          BRANCH    OVRCD,VIST9999
.
          MATCH     URNUMBER,LGATURNO
          GOTO      VIST9999 IF NOT EQUAL
.
          COMPARE   ZERO,FILTVTYP           * All Visits Filter?
          IF        !@EQUAL
            COMPARE   FILTVTYP,LGATTYPE     * Visit Filter Selected
            GOTO      VIST0010 IF NOT EQUAL * Visit Filter does not match Record
          ENDIF
.
          LOAD      VISTTYPE,LGATTYPE,VISTEMER,VISTOUTP,VISTINPT:
                    VISTWLST,VISTREFF,VISTTHEA,VISTOTHE
.
          CALL      DISVIS00
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkPatientAltComments('",HTMLLINK
          APPEND    LGATURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    LGATADAT,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    LGATATIM,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    LGATEVNO,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
VIST0020  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":                   *0
                                      "#"",LGATADAT,LGATATIM,"#",":          *1
                                      "#"",VISTTYPE,"#",":                   *2
                                      "#"",VSTATUS,"<br>",LGATPRPF,"#",":    *3
                                      "#"",LGATEVNO,"#",":                   *4
                                      "#"",LGATATDR,"#",":                   *5
                                      "#"",LGATHOSC,"#",":                   *6
                                      "#"",LGATDDRG,"#");"                   *7
.
          GOTO      VIST0010
.
VIST9999  RETURN
.------------------------------------------------------------
. Display visit details according to the visit type
.------------------------------------------------------------
ALLTAB00  MOVE      ONE,SHOWFLAG            * Show no more visits message
.
          IF        NORECORD=0
            MOVE    "10",NORECORD           * Numb of records=0 Default to 10
          ENDIF
          MOVE      ZERO,RECNUMB            * Init Number of records Read
          MOVE      ZERO,DISNUMB            * Init number of records output
.
          MATCH     "       0",URNUMBER
          GOTO      ALLTAB99 IF EQUAL
.
          UNPACK    DIM127,D1,SFORMAT,DIM127
.
          PACK      KEY32,URNUMBER,Z70
          CALL      RSLGALT1
ALLTAB10  CALL      RPLGALT1
          BRANCH    OVRCD,ALLTAB95
.
          MATCH     URNUMBER,LGATURNO
          GOTO      ALLTAB95 IF NOT EQUAL
.
          COMPARE   ZERO,FILTVTYP           * All Visits Filter?   
          IF        !@EQUAL
            COMPARE   FILTVTYP,LGATTYPE     * Visit Filter Selected
            GOTO      ALLTAB10 IF NOT EQUAL * Visit Filter does not match Record
          ENDIF
.
ALLTAB20  ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      ALLTAB10
          ENDIF
.
          CALL      DISVIS00                * format data fields for display
.
          IF        (DISNUMB%2) = 0
            WRITE     HTMLFILE;"<tr class=TableRowOdd><td nowrap>"
          ELSE
            WRITE     HTMLFILE;"<tr class=TableRowEven><td nowrap>"
          ENDIF
.
          MOVE      ZERO,FORM3
          MOVE      SFORMAT,FORM3
          IF        FORM3 = 1
            WRITE     HTMLFILE;"&nbsp; &nbsp;",DISPDATT,"</td>":
                               "<td>&nbsp;",VISTTYPE,"</td>":
                               "<td>&nbsp;",VSTATUS,"<br>",LGATPRPF,"</td>":
                               "<td>&nbsp;",LGATEVNO,"</td>":
                               "<td>&nbsp;",LGATATDR,"</td>":
                               "<td>&nbsp;",LGATHOSC,"</td>":
                               "</tr>"
          ELSE                              * format 0 - original
            IF        FORM3 = 2
             WRITE     HTMLFILE;"<img src=../images/AppointmentIcon.gif":
                               " class=ListIcon ":
                               " onclick='linkPatientAltComments(":
                               "#"",LGATURNO,"#",":
                               "#"",LGATADAT,"#",":
                               "#"",LGATATIM,"#",":
                               "#"",LGATEVNO,"#");'>";
 
             WRITE     HTMLFILE;"&nbsp; &nbsp;",DISPDATT,"</td>":
                                 "<td>&nbsp;",VISTTYPE,"</td>":
                                 "<td>&nbsp;",VSTATUS,"<br>",LGATPRPF,"</td>":
                                 "<td>&nbsp;",LGATEVNO,"</td>":
                                 "<td>&nbsp;",LGATATDR,"</td>":
                                 "<td>&nbsp;",LGATHOSC,"</td>":
                                 "<td>&nbsp;",LGATDDRG,"</td>":
                                 "</tr>"
            ELSE
              WRITE     HTMLFILE;"&nbsp; &nbsp;",DISPDATT,"</td>":
                                 "<td>&nbsp;",VISTTYPE,"</td>":
                                 "<td>&nbsp;",VSTATUS,"</td>":
                                 "<td>&nbsp;",LGATEVNO,"</td>":
                                 "<td>&nbsp;",LGATDDRG,"</td>":
                                 "</tr>"
            ENDIF
          ENDIF
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      ALLTAB10 IF NOT EQUAL
          MOVE      ZERO,SHOWFLAG
.
ALLTAB95  COMPARE    ONE,SHOWFLAG
          GOTO       ALLTAB99 IF NOT EQUAL
.
          WRITE      HTMLFILE;"<tr bgcolor=#FF0000>":
                              "<td colspan=8 align=center>":
                              "No more visits</td></tr>"
.
ALLTAB99  RETURN
.
.------------------------------------------------------------
. Get Visit Details
.------------------------------------------------------------
DISVIS00  MOVE      ZERO,EXIT
.
          PACK      VISTTYPE,SP70
          PACK      VSTATUS,SP70
.
          LOAD      VISTTYPE,LGATTYPE,VISTEMER,VISTOUTP,VISTINPT: 
                    VISTWLST,VISTREFF,VISTTHEA,VISTOTHE
.
          UNPACK    LGATADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          MATCH     SP70,LGATATIM
          IF        !@EQUAL
            ENDSET    DISPDATT
            APPEND    " ",DISPDATT
            APPEND    LGATATIM,DISPDATT
            RESET     DISPDATT
          ENDIF
.
          BRANCH    LGATTYPE,DISVIS05,DISVIS10,DISVIS15,DISVIS15,DISVIS15:
                             DISVIS15,DISVIS15
          GOTO      DISVIS99
.
.         Emergency  **************************
.
DISVIS05  PACK      VSTATUS,SP70
          CLEAR     VSTATUS
.
          MATCH     SP70,LGATSTAT
          IF        !@EQUAL
....        SQUEEZE   LGATSTAT
            STRIP     LGATSTAT
            APPEND    LGATSTAT,VSTATUS
          ENDIF
.
          UNPACK    LGATDDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          APPEND    SP1,VSTATUS
          APPEND    CPCDATE,VSTATUS
          APPEND    SP1,VSTATUS
          APPEND    LGATDTIM,VSTATUS
          APPEND    SP70,VSTATUS
          RESET     VSTATUS
.
          GOTO      DISVIS99
.
.         Outpatient ************************
.
DISVIS10  PACK      VSTATUS,SP70
          CLEAR     VSTATUS
.
          MATCH     SP70,LGATSTAT
          IF        !@EQUAL
....        SQUEEZE   LGATSTAT
            STRIP     LGATSTAT
            APPEND    LGATSTAT,VSTATUS
          ENDIF
.
          GOTO      DISVIS99
.
.         Inpatient/WL/Referrals/Theatre/Other *****
.
DISVIS15  PACK      VSTATUS,SP70
          CLEAR     VSTATUS
.
          MATCH     SP70,LGATSTAT
          IF        !@EQUAL
....        SQUEEZE   LGATSTAT
            STRIP     LGATSTAT
            APPEND    LGATSTAT,VSTATUS
          ENDIF
.
          UNPACK    LGATDDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          APPEND    SP1,VSTATUS
          APPEND    CPCDATE,VSTATUS
          APPEND    SP1,VSTATUS
          APPEND    LGATDTIM,VSTATUS
          APPEND    SP70,VSTATUS
          RESET     VSTATUS
.
DISVIS99  RETURN
.
.------------------------------------------------------------
. Output Patient Enquiry Page
.------------------------------------------------------------
LEGPAT00  MOVE      ZERO,MASTFLAG
          MOVE      ZERO,MISSFLAG
          MOVE      ZERO,RESPFLAG
          CALL      IBACLOCK
          CALL      CLPATMAS
          CALL      CLPMSPX2
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          MOVE      OVRCD,MASTFLAG
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          IF        PSTAT=1 | PSTAT=2
            MOVE      PURNO,SAVEURNO
            MOVE      PPSNAME,URNUMBER
            MOVE      ONE,MRGEFLAG
            GOTO      LEGPAT00
          ENDIF
.
          IF        MASTFLAG=1
            MOVE      "Can't find patient master details",WEBTITLE
            CALL      WEBERR00
            GOTO      LEGPAT99
          ENDIF
.
LEGPAT05  CALL      PATNAA00                * format name without bold tags
.                                                 * read nutrition file
.    Webtplaf must be read here to get correct description for WEBTITLE
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          PACK      KEY13,PRGID,KEY2,TEMPLATE,SP70
          CALL      RDWBTP1
          CLEAR     WEBTITLE
          APPEND    WBTPDES,WEBTITLE
          APPEND    PATFNAME,WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LEGPAT99
.
          CALL      WEBBOD00   * Output Templated Body of HTML
          IF        MRGEFLAG=1
            CALL      MRGALT00
          ENDIF
.
LEGPAT90  CALL      WEBEND00   * Output End of Web Page
.
LEGPAT99  MOVE      ONE,EXIT
          RETURN

.-------------------------------------------------------------------------------
. Output Patient Free Text Comments
.-------------------------------------------------------------------------------
LEGACM00  MOVE      ZERO,MASTFLAG
          MOVE      ZERO,MISSFLAG
          MOVE      ZERO,RESPFLAG
          CALL      IBACLOCK
          CALL      CLPATMAS
          CALL      CLPMSPX2
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          MOVE      OVRCD,MASTFLAG
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          IF        MASTFLAG=1
            MOVE      "Can't find patient master details",WEBTITLE
            CALL      WEBERR00
            GOTO      LEGACM99
          ENDIF
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          PACK      KEY32,URNUMBER,LGTXADAT,LGTXATIM,LGTXEVNO,SP70
          CALL      RDLGALT1  
.
          PACK      KEY13,PRGID,KEY2,TEMPLATE,SP70
          CALL      RDWBTP1
          CLEAR     WEBTITLE
          APPEND    WBTPDES,WEBTITLE
          APPEND    PATFNAME,WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LEGACM99
.
          CALL      WEBBOD00   * Output Templated Body of HTML
          IF        MRGEFLAG=1
            CALL      MRGALT00
          ENDIF
.
LEGACM90  CALL      WEBEND00   * Output End of Web Page
.
LEGACM99  MOVE      ONE,EXIT
          RETURN
.
.-------------------------------------------------------------------------------
. LEGTXT00 - Output tags for Legacy Free Text Comments
.-------------------------------------------------------------------------------
LEGTXT00  MATCH     "       0",URNUMBER
          GOTO      ALLTAB99 IF EQUAL
.
          PACK      KEY35,URNUMBER,LGTXADAT,LGTXATIM,LGTXEVNO,SP70
          CALL      RSLGACM1
LEGTXT10  CALL      RKLGACM1
          BRANCH    OVRCD,LEGTXT99
.
          MATCH     URNUMBER,LGACURNO
          GOTO      LEGTXT99 IF NOT EQUAL
.
          MATCH     LGTXADAT,LGACADAT
          GOTO      LEGTXT99 IF NOT EQUAL
.
          MATCH     LGTXATIM,LGACATIM
          GOTO      LEGTXT99 IF NOT EQUAL
.
          MATCH     LGTXEVNO,LGACEVNO
          GOTO      LEGTXT99 IF NOT EQUAL
.
          CLEAR     FORM3
          MOVE      LGACLINE,FORM3
          COMPARE   FORM3,THIRTY3
          GOTO      LEGTXT99 IF LESS             * Max lines reached
.
          WRITE     HTMLFILE;LGACCOMM
          GOTO      LEGTXT10
.
LEGTXT99  RETURN
.
.-------------------------------------------------------------------------------
. LGACMT00 - Output tags for Legacy Alt Comments - legacmaf
.-------------------------------------------------------------------------------
LGACMT00  BRANCH   FLDITMNO,LGACMT01,LGACMT02,LGACMT03,LGACMT04,LGACMT05:
                            LGACMT06,LGACMT07,LGACMT08,LGACMT09,LGACMT10:
                            LGACMT11,LGACMT12,LGACMT13,LGACMT14,LGACMT15:
                            LGACMT16,LGACMT17,LGACMT18
.
          WRITE    HTMLFILE;"Invalid IBA Tag"
          GOTO     LGACMT99
.
LGACMT01  WRITE    HTMLFILE;LGACURNO;
          GOTO     LGACMT99
.
LGACMT02  WRITE    HTMLFILE;LGACADAT;
          GOTO     LGACMT99
.
LGACMT03  WRITE    HTMLFILE;LGACATIM;
          GOTO     LGACMT99
.
LGACMT04  WRITE    HTMLFILE;LGACEVNO;
          GOTO     LGACMT99
.
LGACMT05  WRITE    HTMLFILE;LGACLINE;
          GOTO     LGACMT99
.
LGACMT06  WRITE    HTMLFILE;LGACCOMM;
          GOTO     LGACMT99
.
LGACMT07  WRITE    HTMLFILE;LGATURNO;
          GOTO     LGACMT99
.
LGACMT08  UNPACK   LGATADAT,CCENT,CYEAR,CMON,CDAY
          MOVE     CMON,F2
          LOAD     MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK     DISPDATT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          WRITE    HTMLFILE;DISPDATT;
          GOTO     LGACMT99
.
LGACMT09  WRITE    HTMLFILE;LGATATIM;
          GOTO     LGACMT99
.
LGACMT10  WRITE    HTMLFILE;LGATEVNO;
          GOTO     LGACMT99
.
LGACMT11  LOAD     KEY20,LGATTYPE,VTXTEMER,VTXTOUTP,VTXTINPT: 
                   VTXTWLST,VTXTREFF,VTXTTHEA,VTXTOTHE
          WRITE    HTMLFILE;KEY20;
          GOTO     LGACMT99
.
LGACMT12  WRITE    HTMLFILE;LGATSTAT;
          GOTO     LGACMT99
.
LGACMT13  WRITE    HTMLFILE;LGATDDRG;
          GOTO     LGACMT99
.
LGACMT14  WRITE    HTMLFILE;LGATDDAT;
          GOTO     LGACMT99
.
LGACMT15  WRITE    HTMLFILE;LGATDTIM;
          GOTO     LGACMT99
.
LGACMT16  WRITE    HTMLFILE;LGATHOSC;
          GOTO     LGACMT99
.
LGACMT17  WRITE    HTMLFILE;LGATATDR;
          GOTO     LGACMT99
.
LGACMT18  WRITE    HTMLFILE;LGATPRPF;
          GOTO     LGACMT99
.
LGACMT99  RETURN
.-------------------------------------------------------------------------------
.         I/O INCLUDES
.-------------------------------------------------------------------------------

          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       PATMA1IO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       PATDO1IO/INC
          INC       OUTSITIO/INC
          INC       PATWR1IO/INC
          INC       PMSCCDIO/INC            * Concession Card Details
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       PATIN1IO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATFN1IO/INC
          INC       MRTMASIO/INC
          INC       APSMASIO/INC
.
. Legacy Files
.
          INC       LEGALTIO/INC
          INC       LEGACMIO/INC
.
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       PATMASTM
          INC       PMSPX2TM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PATDOCTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       CLPATMAS
          INC       PATMSXTM

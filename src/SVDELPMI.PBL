.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   SVDELPMI                                                  *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   03/10/2005                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will delete UR numbers                       *
.* Modification:                                                           *
.***************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                      *
.*           Recompiled as WATTR1FD changes                                *
.***************************************************************************
.*        V10.11.03 27/11/2017  Thanh T        TSK 0821710                 *
.*                  Recompiled as PATMASTD changed                         *
.*        V10.11.02 07/09/2017  Thanh T.           TSK 0821710             *
.*                  Audit Amission/outpatient visit comments               *
.*        V10.11.01 07/09/2017  Thanh T.           TSK 0821710             *
.*                  Audit Amission/outpatient visit comments               *
.***************************************************************************
.*        V10.08.01 18/04/2016  Peter Vela        TSK 0294177              *
.*                  Changed read to prihdb to KEY27                        *
.*        V10.04.01 22/01/2014  Don Nguyen        CAR 295972               *
.*                  Changed KEY16 to KEY24 for IO calls on patpa1af        *
.***************************************************************************
.*        V10.03.01 30/11/2011  Jill Parkinson    CAR 249362               *
.*                  Changed alert table keys                               *
.***************************************************************************
.*        V10.02.03 13/07/2011  Steve Armstrong   CAR 240722               *
.*                  Further mods to cater for second given name as         *
.*                  part of PATGSRFD keys                                  *
.*        V10.02.02 06/07/2011  Mike Laming      CAR 240724                *
.*                  Mods to MRT Tables for Hospital Id. - WA Health changes*
.*        V10.02.01 22/06/2011  Steve Armstrong   CAR 240722               *
.*                  Mods to cater for changes to PATLOCFD.                 *
.*                       - LSNAME & LGNAME extended to 40 chars.           *
.*                       - PTLCGNM2 added.                                 *
.*                  Mods to cater for changes to PATGSRFD:                 *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.         *
.*                       - PTGSGNM2 added.                                 *
.*                       - all indexes changed in length.                  *
.***************************************************************************
.*        V9.10.01 12/05/2008 J.Tan     CAR 162313                         *
.*                 Added new field Deposit status to comdepaf file         *
.***************************************************************************
.
          INC       STD001FD
          INC       PATMA1FD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CFILEPRE  DIM       3
DIM6      DIM       6
DIM7      DIM       7
DUMURNO   DIM       8
KEY1A     DIM       1
URNUMBER  DIM       8
WEBTITLE  DIM       80
.
.-----------------------------------------------------------------------
PRGID     INIT      "SVDELPMI"
PRGNAM    INIT      "Delete master details        "
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    IF        COPTION=1
            CALL      CANT0000               * process
          ELSE
            CALL      CANA0000
          ENDIF
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Remove TXXXXXX records":
                    *P1:6,*V2LON,TWO,*HOFF,". Remove A-XXXXXX records" 
.
          DISPLAY   *P1:15,*V2LON,"Please save a copy of patma1af, patmx1af ":
                                  "and pmspx2af "
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               CANT0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
CANT0000  PACK      KEY8,SP1,ANST,SP70
          CALL      RDSMAST1
CANT1000  CALL      RDKMAST1
          BRANCH    OVRCD,CANT9999
.
          UNPACK    KEY8,KEY1,KEY1A,DIM6
          MATCH     SP70,KEY1
          GOTO      CANT9999 IF NOT EQUAL
.
          MATCH     ANST,KEY1A
          GOTO      CANT9999 IF NOT EQUAL
.
          PACK      URNUMBER,PURNO,SP70
          PROC      CANUR000
          GOTO      CANT1000
.
CANT9999  RETURN
+
.**************************************************************************
.*                               CANA0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
CANA0000  PACK      KEY8,ANSA,SP70
          CALL      RDSMAST1
CANA1000  CALL      RDKMAST1
          BRANCH    OVRCD,CANA9999
.
          UNPACK    KEY8,KEY1,DIM7
          MATCH     ANSA,KEY1
          GOTO      CANA9999 IF NOT EQUAL
.
          PACK      URNUMBER,PURNO,SP70
          PROC      CANUR000
          GOTO      CANA1000
.
CANA9999  RETURN
+
.-----------------------------------------------------------
. CANUR000 - Cancel U/R Number
. Returns:   EXIT   0 = Ok to continue
.                   1 = Error
.-----------------------------------------------------------
          DEFPROC   CANUR000
.
          INC       CCIUTLFD/INC
          INC       OUTBOAFD/INC
          INC       PATLINFD/INC
          INC       PRIHDBFD/INC
          INC       PRIDTRFD/INC
          INC       PATVISFD/INC
          INC       WATTR1FD/INC
          INC       PATDNRFD/INC
          INC       PATLOCFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       MRTMASFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       PATPA1FD/INC
          INC       PATGSRFD/INC
          INC       PATURAFD/INC
          INC       PATNIDFD/INC
          INC       PMSPX2FD/INC
          INC       PATALRFD/INC
          INC       COMDEPFD/INC
.
CANUR000  OPEN      PRIHDBT1,"prihdbtf"
          OPEN      PRIDTRA3,"pridtraf"
          OPEN      CCIUTLL5,"cciutlaf"
          OPEN      PATLINK1,"patlinkf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      PATMWSA1,"patmwsaf"
          OPEN      PATDNRA1,"patdnraf"
          OPEN      COMDEPA3,"comdepaf"
          OPEN      PATLOCA1,"patlocaf"
          OPEN      PMSUCHA1,"pmsuchaf"
          OPEN      PMSUCDA1,"pmsucdaf"
          OPEN      MRTMASA1,"mrtmasaf"
          OPEN      BOKRC1A6,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      PATPA1A1,"patpa1af"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATURAD1,"paturadf"
          OPEN      PATURAD2,"paturadf"
          OPEN      PATALRT1,"patalrtf"
          OPEN      PMSPX2A1,"pmspx2af"
.
          MOVE      "out",CFILEPRE
          PACK      CFNAME,CFILEPRE,FILBOKA3
          OPEN      OUTBOKA3,CFNAME
.
          MOVE      URNUMBER,KEY8            * Validate UR Number
          CALL      RDMAST1
          BRANCH    OVRCD,CANUR910
.
          CALL      RDPMPX21
          BRANCH    OVRCD,CANUR910
.
. Check for the U/R number in different files,if exists then DON'T CANCEL
.
          PACK      KEY24,URNUMBER,SP70       * Patient Visit File
          CALL      RDSVISA2
          CALL      RDKVISA2
          BRANCH    OVRCD,CANUR010
.
          MATCH     URNUMBER,PVIURNO
          GOTO      CANUR920 IF EQUAL
.
CANUR010  PACK      KEY19,URNUMBER,SP70       * Waiting List File
          CALL      RDSTREA1
          CALL      RDKTREA1
          BRANCH    OVRCD,CANUR020
.
          MATCH     URNUMBER,WMURNO
          GOTO      CANUR921 IF EQUAL
.
CANUR020  PACK      KEY27,URNUMBER,SP70       * Private Practice Holding Header
          CALL      RSPRHD1
          CALL      RKPRHD1
          BRANCH    OVRCD,CANUR030
.
          MATCH     URNUMBER,PRHDDEBT
          GOTO      CANUR922 IF EQUAL
.
CANUR030  PACK      KEY24,URNUMBER,SP70       * Private Practice Tranasaction
          CALL      RSPRDT3
          CALL      RKPRDT3
          BRANCH    OVRCD,CANUR040
.
          MATCH     URNUMBER,PRDTDEBT
          GOTO      CANUR922 IF EQUAL
.
CANUR040  PACK      KEY42,URNUMBER,SP70       * Clinic Costing Interface
          CALL      RSCCUTL5
          CALL      RKCCUTL5
          BRANCH    OVRCD,CANUR050
.
          MATCH     URNUMBER,CCUTURNO
          GOTO      CANUR923 IF EQUAL
.
CANUR050  PACK      KEY36,URNUMBER,SP70       * OutPatient Booking File
          CALL      RDSBOKA3
          CALL      RDKBOKA3
          BRANCH    OVRCD,CANUR055
.
          MATCH     URNUMBER,OBAURNO
          GOTO      CANUR924 IF EQUAL
.
CANUR055  MOVE      "0",CMDESTAT
          PACK      KEY32,URNUMBER,CMDESTAT,SP70       * Deposit File
          CALL      RSCMDEP3
          CALL      RKCMDEP3
          BRANCH    OVRCD,CANUR060
.
          MATCH     URNUMBER,CMDEURNO
          GOTO      CANUR060 IF NOT EQUAL
          MATCH     "0",CMDESTAT                       * Deposit held?
          GOTO      CANUR925 IF EQUAL
.
. No Records found in any of the files. Start deleting records
.
CANUR060  MOVE      URNUMBER,KEY8               * Patient Master File
          CALL      RDMAST1
          BRANCH    OVRCD,CANUR910
.
CANUR200  PACK      KEY14,URNUMBER,SP70         * Medical Warning File
          CALL      RSPTMWS1
          CALL      RKPTMWS1
          BRANCH    OVRCD,CANUR210
.
          MATCH     URNUMBER,PTMWURNO
          GOTO      CANUR210 IF NOT EQUAL
.
          PACK      KEY14,PTMWURNO,PTMWCLSS,PTMWCNT
          CALL      DEPTMWS1
          GOTO      CANUR200
.
CANUR210  PACK      KEY10,URNUMBER,SP70         * Patient Donor Details
          CALL      RSPTDNR1
          CALL      RKPTDNR1
          BRANCH    OVRCD,CANUR220
.
          MATCH     URNUMBER,PTDNURNO
          GOTO      CANUR220 IF NOT EQUAL
.
          PACK      KEY10,PTDNURNO,PTDNCNT
          CALL      DEPTDNR1
          GOTO      CANUR210
.
CANUR220  PACK      KEY16,URNUMBER,SP70         * Patient U/R Linkage
          CALL      RDSLINK1
          CALL      RDKLINK1
          BRANCH    OVRCD,CANUR230
.
          MATCH     URNUMBER,LINKFUR
          GOTO      CANUR230 IF NOT EQUAL
.
          MOVE      LINKFUR,DUMURNO
          PACK      KEY16,LINKFUR,LINKTUR
          CALL      DELINK1
.
          PACK      KEY16,DUMURNO,SP70
          CALL      RDSLINK1
          CALL      RDKLINK1
          BRANCH    OVRCD,CANUR230
.
          MATCH     DUMURNO,LINKFUR
          GOTO      CANUR230 IF NOT EQUAL
.
          PACK      KEY16,LINKFUR,LINKTUR
          CALL      DELINK1
.
CANUR230  PACK      KEY88,SP70,SP70              * Patient Location File
          CALL      RDSLOCA1
          CALL      RDKLOCA1
          BRANCH    OVRCD,CANUR240
.
          MATCH     URNUMBER,LURNO
          GOTO      CANUR240 IF NOT EQUAL
.
          PACK      KEY88,LSNAME,LGNAME,LADMNO
          CALL      DELOCA1
          GOTO      CANUR230
.
.-----------------------------------------------------------------------
. Delete all ur comments for a given u/r number
.-----------------------------------------------------------------------
CANUR240  PACK      KEY17,URNUMBER,URCMTTYP,SP70
          CALL      RSPMUCH1
CANUR242  CALL      RKPMUCH1
          BRANCH    OVRCD,CANUR250
.
          MATCH     PMUHURNO,URNUMBER        * same ur Number?
          GOTO      CANUR250 IF NOT EQUAL
.
          MATCH     URCMTTYP,PMUHCTYP
          GOTO      CANUR250 IF NOT EQUAL
.
          PACK      KEY17,PMUHURNO,PMUHCTYP,PMUHCNUM,SP70
          CALL      DEPMUCH1
.
          PACK      KEY20,PMUHURNO,PMUHCTYP,PMUHCNUM,SP70
          CALL      RSPMUCD1
CANUR243  CALL      RKPMUCD1
          BRANCH    OVRCD,CANUR242           * end of file
.
          MATCH     PMUHURNO,PMUCURNO        * same ur Number?
          GOTO      CANUR242 IF NOT EQUAL
.
          MATCH     PMUHCTYP,PMUCCTTY
          GOTO      CANUR242 IF NOT EQUAL
.
          MATCH     PMUHCNUM,PMUCCNUM
          GOTO      CANUR242 IF NOT EQUAL
.
          PACK      KEY20,PMUCURNO,PMUCCTTY,PMUCCNUM,PMUCLNUM,SP70
          CALL      DEPMUCD1
          GOTO      CANUR243
.
CANUR250  PACK      KEY20,URNUMBER,SP70                               *C-240724
          CALL      RSMRMAS1
          CALL      RKMRMAS1
          BRANCH    OVRCD,CANUR260
.
          MATCH     URNUMBER,MRMAURNO
          GOTO      CANUR260 IF NOT EQUAL
.                                                                     *C-240724
          PACK      KEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN
          CALL      DETMAS1
          GOTO      CANUR250
.
CANUR260  PACK      KEY16,URNUMBER,SP70         * Patient Booking Master
          CALL      RSBKREC6
          CALL      RKBKREC6
          BRANCH    OVRCD,CANUR270
.
          MATCH     URNUMBER,BKREURNO
          GOTO      CANUR270 IF NOT EQUAL
.
          PACK      KEY16,BKREURNO,BKREBOOK,SP70
          CALL      DEBKREC6
.
          PACK      KEY8,BKREBOOK,SP70
          CALL      DEBKRX11
          GOTO      CANUR260
.
CANUR270  OPEN      PATNIDA1,"patnidaf"
          PACK      KEY10,CHOSINDX,URNUMBER,SP70    * Patient NMPI file
          CALL      RSPTNID1
          CALL      RKPTNID1
          BRANCH    OVRCD,CANUR280
.
          MATCH     URNUMBER,PTNILNUM
          IF        @EQUAL
            PACK      KEY10,PTNIHNUM,PTNILNUM
            CALL      DEPTNID1
            GOTO      CANUR270
          ENDIF
.
CANUR280  PACK      KEY16,URNUMBER,SP70         * Patient Alert File
          CALL      RSPTALR1
          CALL      RKPTALR1
          BRANCH    OVRCD,CANUR290
.
          MATCH     URNUMBER,PTALURNO
          GOTO      CANUR290 IF NOT EQUAL
.
          PACK      KEY16,PTALURNO,PTALCATG,PTALCODE,PTALCNTR
          CALL      DEPTALR1
          GOTO      CANUR280
.
CANUR290  PACK      KEY17,URNUMBER,SP70         * Patient U/R Audit File
          CALL      RDSURAD2
          CALL      RDKURAD2
          BRANCH    OVRCD,CANUR300
.
          MATCH     URNUMBER,URURNO
          GOTO      CANUR300 IF NOT EQUAL
.
          PACK      KEY17,URRTYPE,URDATE,URURNO
          CALL      DEURAD1
          GOTO      CANUR290
.
CANUR300  PACK      KEY24,URNUMBER,SP70         * Patient Address Entries
          CALL      RDSPADD1
          CALL      RDKPADD1
          BRANCH    OVRCD,CANUR310
.
          MATCH     URNUMBER,PAURNO
          GOTO      CANUR310 IF NOT EQUAL
.
          PACK      KEY24,PAURNO,PADATE,PATIME
          CALL      DEPADD1
          GOTO      CANUR300
.
CANUR310  PACK      KEY115,URNUMBER,SP70,SP70
          CALL      RSPTGSR1                  * Patient Surname File
          CALL      RKPTGSR1
          BRANCH    OVRCD,CANUR330
.
          MATCH     URNUMBER,GSRURNO
          GOTO      CANUR330 IF NOT EQUAL
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      DEPTGSR1
          GOTO      CANUR310
.
. Finally Delete the Master Record....
.
CANUR330  PACK      KEY8,URNUMBER
          CALL      DEMAST1
          CALL      DEPMPX21
          MOVE      "U/R Number has been cancelled",WEBTITLE
          CALL      CANERR00
          MOVE      ZERO,EXIT
          GOTO      CANUR999
.
CANUR910  MOVE      "Invalid UR Number",WEBTITLE
          CALL      CANERR00
          MOVE      ONE,EXIT
          GOTO      CANUR999
.
CANUR920  MOVE    "U/R Number can't be Cancelled - visit record exists",WEBTITLE
          CALL      CANERR00
          MOVE      ONE,EXIT
          GOTO      CANUR999
.
CANUR921  MOVE    "U/R Number can't be Cancelled - WL record exists",WEBTITLE
          CALL      CANERR00
          MOVE      ONE,EXIT
          GOTO      CANUR999
.
CANUR922  MOVE    "U/R Number can't be Cancelled - PP record exists",WEBTITLE
          CALL      CANERR00
          MOVE      ONE,EXIT
          GOTO      CANUR999
.
CANUR923  MOVE    "U/R Number can't be Cancelled - CC record exists",WEBTITLE
          CALL      CANERR00
          MOVE      ONE,EXIT
          GOTO      CANUR999
.
CANUR924  MOVE    "U/R Number can't be Cancelled - OP record exists",WEBTITLE
          CALL      CANERR00
          MOVE      ONE,EXIT
          GOTO      CANUR999
.
CANUR925  MOVE    "U/R Number can't be Cancelled - Deposit record exists",WEBTITLE
          CALL      CANERR00
          MOVE      ONE,EXIT
          GOTO      CANUR999
.
CANUR930  CALL      RELSLK00
          MOVE      "Please wait till Audit file is free",WEBTITLE
          CALL      CANERR00
          MOVE      ONE,EXIT
          GOTO      CANUR999
.
          INC       IBAOVRIO/INC
          INC       CCIUTLIO/INC
          INC       OUTBOAIO/INC
          INC       PATLINIO/INC
          INC       PRIHDBIO/INC
          INC       PRIDTRIO/INC
          INC       PATVISIO/INC
          INC       WATTR1IO/INC
          INC       PATDNRIO/INC
          INC       PATLOCIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       MRTMASIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       PATPA1IO/INC
          INC       PATGSRIO/INC
          INC       PATURAIO/INC
          INC       PATNIDIO/INC
          INC       PMSPX2IO/INC
          INC       PATALRIO/INC
          INC       COMDEPIO/INC
.
CANUR999  ENDPROC
+
CANERR00  PRINT     *1,URNUMBER,SP5,WEBTITLE
          RETURN
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATMA1IO/INC

.******************************************************************************
.* System         : Patient Management                                        *
.* Program        : IBAHOS35.PBL                                              *
.* Name           : Prosthetic Tracking for JDE Interface                     *
.******************************************************************************
.* Date           : 04/02/2008                                                *
.* Author         : Peter Vela                                                *
.* Function       : Extract Prosthetic Tracking Details for JDE Interface     *
.*                  User is given 2 options upon entering program:            *
.*                     1. Create Extract                                      *
.*                     2. Re-extract File                                     *
.*                  Option 1 will loop through pmsptdaf table and extract any *
.*                           records for the schedule date                    *
.*                  Option 2 will allow user to re-extract a file via file    *
.*                           name.                                            *
.*                                                                            *
.* Modifications  :                                                           *
.*        V9.11.02  04/12/2008  Steve Armstrong   CAR 181373                  *
.*                  Fixed Change records processing so that if the transaction*
.*                  id or the item don't match when looping back through      *
.*                  pmsptdaf using index 2, to read the previous record       *
.*                  instead of bailing out and processing the next record     *
.*                  using index 1.                                            *
.*        V9.11.01  20/11/2008  Steve Armstrong   CAR 181373                  *
.*                  Recompiled for changes to PMSPTDFD.                       *
.*                  Mods to use PMSPTDA2 to loop back and get the previous    *
.*                  record for the item being processed.                      *
.******************************************************************************
.*        V9.09.01  19/03/2008  Peter Vela          CAR 155334                *
.*                  Added shutdown command at the end of the program          *
.*        V9.09.00  04/02/2008  Peter Vela          CAR 155334                *
.*                  Created Extract Program.                                  *
.******************************************************************************
          INC       STD002FD
.
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PMSHCPFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATMI1FD/INC
          INC       PATDSCFD/INC
          INC       PMSPTDFD/INC
          INC       PMSPTHFD/INC
          INC       PATWC1FD/INC
          INC       PATWMAFD/INC
          INC       PATWVEFD/INC
.
. Variables
.
CURDTE8   DIM       8
CURDTE8A  DIM       8
DATECALC  DATE      8
DIM2A     DIM       2
DIM2B     DIM       2
DIM2C     DIM       2
DIM350    DIM       350
DUMMY     DIM       1
EXTFILNM  DIM       25    * extract file name
EXTPATH   DIM       127
FORM12D2  FORM      12.2
SAVEITEM  DIM       9
SAVEVISN  DIM       8
SAVETRAN  DIM       6
SAVKEY30  DIM       30
SAVKEY55  DIM       55
REMFILE   DIM       127
.
. Extract file definition
.
EXTFILE1  FILE      ASCII,FIXED=256
.
.Name     Type   Length   Physical   Start   Description
.----     ----   ------   --------   -----   -----------
XPMPEURN  DIM    8        8          1       U/R Number
XPMPEVIS  DIM    8        8          9       Visit Number
XPSRNAME  DIM    20       20         17      Patient Surname
XPGVNAME  DIM    25       25         37      Patient Given Names
XPTTITLE  DIM    4        4          62      Patient Title
XPATADD1  DIM    35       35         66      Patient Address Line 1
XPATADD2  DIM    35       35         101     Patient Address Line 2
XPTSUBRB  DIM    35       35         136     Patient Address Line 3 Suburb
XPTPPOST  DIM    8        8          171     Patient Post Code
XPAFUNDH  DIM    6        6          179     Fund Code
XFUNDREF  DIM    20       20         185     Fund Reference
XPMPETDA  DIM    8        8          205     Date of Service
XDOCNAME  DIM    54       54         213     Doctor's Name
XPMPEITE  DIM    9        9          267     Misc Item Code
XPTADATE  DIM    8        8          276     Admission Date
XPTDDATE  DIM    8        8          284     Discharge Date
XPMPEIDE  DIM    30       30         292     Misc Item Description
XTRANAMT  DIM    15       15         307     Transaction Amount GST Exclusive
XPMPETRD  DIM    8        8          322     Transaction Date     
XPMPETRT  DIM    8        8          330     Transaction Time
XPMPEDTE  DIM    8        8          338     Extract Date
.
. Program Constants
.
PROSRECN  INIT   "PROS_RECON_"
PIPE      INIT      124 
TXTEXT    INIT   ".txt"
RET       INIT      015
.
.------------------------------------------------------------------------------
PRGID     INIT      "IBAHOS35"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Prosthetics Tracking Extract Function"
.------------------------------------------------------------------------------
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9999             * EXIT
          BRANCH    OPTION,ML2000:          * Create File
                           ML4000           * Re-extract File
.
ML2000    CALL     PROC0000 
          GOTO     ML1000
.
ML4000    CALL     PREX0000
          GOTO     ML1000
.
ML9999    SHUTDOWN  " "
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"pmshcpaf"
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmspthaf"
          OPEN      PMSPTHA1,"pmspthaf"
.
          DISPLAY   *P64:24,"pmsptdaf"
          OPEN      PMSPTDA1,"pmsptdaf"
          OPEN      PMSPTDA2,"pmsptdaf"
          OPEN      PMSPTDA4,"pmsptdaf"
          OPEN      PMSPTDA5,"pmsptdaf"
.
          DISPLAY   *P64:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A8,"patmi1af"
.
          DISPLAY   *P64:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patwc1af"
          OPEN      PATWC1A1,"patwc1af"
.
          DISPLAY   *P64:24,"patwmabf"
          OPEN      PATWMAB1,"patwmabf"
.
          DISPLAY   *P64:24,"patwvetf"
          OPEN      PATWVET1,"patwvetf"
.
          DISPLAY   *P64:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*79,CAPPRVNO
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM
          READ      CONTROLF,SEVENTY7;*195,PTCNDLKR
          READ      CONTROLF,HUND12;*118,PTCNTRPR,*119,PTCNJPEP
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          HLINE     *G37,2,55,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Create Extract ":
                    *P1:6,*V2LON,"2",*HOFF," = Re-extract File ":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN2000,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  MATCH     "1",PTCNTRPR
          GOTO      OPTN2500 IF EQUAL
.
          DISPLAY   *P1:24,"Hospital not using Prosthetic Tracking. ";
          PRINT     *N,*1,"Hospital not using Prosthetic Tracking. ";
          CALL      HITEXIT
          GOTO      OPTN9500
.
OPTN2500  MATCH     SP70,PTCNJPEP
          IF        @EQUAL | @EOS
            DISPLAY   *P1:24,*B,"JDE Prosthetic Extract Pathname blank. ";
            PRINT     *N,*1,"JDE Prosthetic Extract Pathname blank. ";
            CALL      HITENTER
            GOTO      OPTN9500
          ENDIF
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                       Loop through pmsptdaf via schedule date              *
.******************************************************************************
PROC0000  CALL      IBACLOCK
          PACK      CURDTE8,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE8
          PACK      CURDTE8A,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE8A
          MOVE      CURDTE8,DATECALC
          SUB       ONE,DATECALC
          MOVE      DATECALC,CURDTE8
          REP       " 0",CURDTE8
          CLOCK     TIME,CTIMEIS
.         
          UNPACK    CTIMEIS,DIM2A,DUMMY,DIM2B,DUMMY,DIM2C
.         
          PACK      EXTFILNM,PROSRECN,CURDTE8,DIM2A,DIM2B,DIM2C
.         
          STRIP     PTCNJPEP
.         
          PACK      EXTPATH,SP70,SP70
          PACK      EXTPATH,PTCNJPEP,EXTFILNM,TXTEXT,SP70
          SQUEEZE   EXTPATH
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EXTFILE1,EXTPATH
          TRAPCLR   IO
.
          IF        OVRCD=0
            CLOSE     EXTFILE1
            PACK      REMFILE,SP70,SP70
            CLEAR     REMFILE
            APPEND    "rm ",REMFILE
            APPEND    EXTPATH,REMFILE
            RESET     REMFILE
            EXECUTE   REMFILE,TASKID
          ENDIF
.
          PREP      EXTFILE1,EXTPATH
.
          MOVE      ZERO,PMPTRCCT
.
PROC1000  PACK      KEY30,CURDTE8,SP70
          CALL      RSPMPTD1
PROC1100  CALL      RKPMPTD1
          BRANCH    OVRCD,PROC9000
.
          MATCH     CURDTE8,PMPETRDT
          GOTO      PROC9000 IF NOT EQUAL
.
          MATCH     "A",PMPETRTY                        * Add Record
          GOTO      PROC5000 IF EQUAL
.
          MATCH     "C",PMPETRTY                        * Change Record
          GOTO      PROC6000 IF EQUAL
.
          MATCH     "D",PMPETRTY                        * Delete Record
          GOTO      PROC7000 IF EQUAL
.
          GOTO      PROC1100
.
PROC5000  CALL      GETPAT00                            * Add Record
          BRANCH    EXIT,PROC1100
.
          CALL      PRCPTD00
          CALL      WRTPTD00
          ADD       ONE,PMPTRCCT
          GOTO      PROC1100
.
PROC6000  MOVE      SP70,SAVKEY30                       * Change Record
          PACK      SAVKEY30,PMPETRDT,PMPETRTM,PMPEVISN,PMPETRAN,SP70
          MOVE      PMPEVISN,SAVEVISN
          MOVE      PMPEDTRN,SAVETRAN
          MOVE      PMPEITEM,SAVEITEM
.
.         Loop back through pmsptdaf to get the previous Add or Change record
.         for this item
.
          PACK      KEY30,PMPEVISN,PMPETRDT,PMPETRTM,PMPETRAN
          CALL      RDPMPTD2                     * record found using index 2 ?
          BRANCH    OVRCD,PROC1100               * eof - finish (not good)
.
PROC6100  CALL      RPPMPTD2                     * read previous record
          BRANCH    OVRCD,PROC1100               * eof - finish (not good)
.
          MATCH     SAVEVISN,PMPEVISN            * same visit still ?
          GOTO      PROC1100 IF NOT EQUAL        * no - finish (not good)
.
          MATCH     SAVETRAN,PMPEDTRN            * same transaction ?
          GOTO      PROC6100 IF NOT EQUAL        * no - ignore record
.
          MATCH     SAVEITEM,PMPEITEM            * same item ?
          GOTO      PROC6100 IF NOT EQUAL        * no - ignore record
.
          MATCH     ANSD,PMPETRTY                * delete record ?
          GOTO      PROC1100 IF EQUAL            * no - finish (not good)
.
.         The previous record has been found
.
          CALL      GETPAT00
          BRANCH    EXIT,PROC1100
.
          CALL      PRCPTD00
          MOVE      XTRANAMT,FORM12D2                   * Make dollar amount
          MULT      SEQ,FORM12D2                        * negative
          MOVE      FORM12D2,XTRANAMT
          CALL      WRTPTE00
.
PROC6500  PACK      KEY30,SAVKEY30,SP70
          CALL      RDPMPTD1
.
          CALL      GETPAT00
          BRANCH    EXIT,PROC1100
.
          CALL      PRCPTD00
          CALL      WRTPTD00
          ADD       ONE,PMPTRCCT
          GOTO      PROC1100
.
PROC7000  CALL      GETPAT00                            * Delete Record
          BRANCH    EXIT,PROC1100
.
          CALL      PRCPTD00
          MOVE      XTRANAMT,FORM12D2                   * Make dollar amount
          MULT      SEQ,FORM12D2                        * negative
          MOVE      FORM12D2,XTRANAMT
          CALL      WRTPTD00
          ADD       ONE,PMPTRCCT
          GOTO      PROC1100
.
PROC9000  MOVE      CURDTE8A,PMPTEXDT                   * Write to Header file
          MOVE      CTIMEIS,PMPTEXTM
          MOVE      EXTFILNM,PMPTFLNM
          MOVE      SP70,PMPTSPAR
.
          PACK      KEY16,PMPTEXDT,PMPTEXTM
          CALL      RAPMPTH1
          IF        OVRCD=0
            CALL      UPPMPTH1
          ELSE
            CALL      WRPMPTH1
          ENDIF
.
          CALL      DELPTH00
.
PROC9999  RETURN
.******************************************************************************
.*                                  GETPAT00              Called by: PROC0000 *
.*                       Get patient details                                  *
.******************************************************************************
GETPAT00  PACK      KEY8,PMPEURNO,SP70                * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,GETPAT90
.         
          PACK      KEY8,PMPEVISN,SP70                * Read Visit File
          CALL      RDPTVIS1
          BRANCH    OVRCD,GETPAT90
.         
          COMPARE   THREE,PVITYPE                     * Inpatient visit
          GOTO      GETPAT90 IF NOT EQUAL             
.         
          PACK      KEY8,PMPEVISN,SP70                * Read Admission File
          CALL      RDMISS1
          BRANCH    OVRCD,GETPAT90
.         
          PACK      KEY8,PMPEVISN,SP70                * Read Discharge File
          CALL      RDDSCH1
          IF        OVRCD=1
            CALL      CLPATDSC
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      GETPAT99
.
GETPAT90  MOVE      ONE,EXIT
          GOTO      GETPAT99
.
GETPAT99  RETURN
.******************************************************************************
.*                                  PRCPTD00              Called by: PROC0000 *
.*                       Assign values to the extract variables / record      *
.******************************************************************************
PRCPTD00  CALL      CLRPTD00
.
          MOVE      PMPEURNO,XPMPEURN
          MOVE      PMPEVISN,XPMPEVIS
          MOVE      PSNAME,XPSRNAME
          MOVE      PGNAME,XPGVNAME
          MOVE      PTITL,XPTTITLE
          MOVE      PADD1,XPATADD1
          MOVE      PADD2,XPATADD2
          MOVE      PSUBRB,XPTSUBRB
          MOVE      PPOST,XPTPPOST
          MOVE      AFUNDH,XPAFUNDH
.
          MOVE      AFNDMM,XFUNDREF
          MATCH     SP70,ACLAIM
          IF        !@EQUAL & !@EOS
            PACK      KEY5,ANSC,ANSL,ACLAIM
            CALL      RDCODE1
            IF        OVRCD=0
              MATCH     ANSM,TCINDC1
              IF        @EQUAL
                PACK      KEY8,PMPEVISN,SP70
                CALL      RDWMAB1
                IF        OVRCD=0
                  MOVE      MREFNO,XFUNDREF
                ENDIF
              ENDIF
.
              MATCH     ANSV,TCINDC1
              IF        @EQUAL
                PACK      KEY8,PMPEVISN,SP70
                CALL      RDWVET1
                IF        OVRCD=0
                  MOVE      VCLMNO,XFUNDREF
                ENDIF
              ENDIF
.
              MATCH     ANSW,TCINDC1
              IF        @EQUAL
                PACK      KEY8,PMPEVISN,SP70
                CALL      RDWCOM1
                IF        OVRCD=0
                  MOVE      WCCLMNO,XFUNDREF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      PMPETDAT,XPMPETDA
.
          PACK      KEY10,ADOCTA,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            STRIP     PMHCTITL
            STRIP     PMHCGNAM
            STRIP     PMHCSNAM
            PACK      XDOCNAME,PMHCTITL,SP1,PMHCGNAM,SP1,PMHCSNAM,SP70 
          ENDIF 
.
          MOVE      PMPEITEM,XPMPEITE
          MOVE      ADATE,XPTADATE
          MOVE      DDATE,XPTDDATE
          MOVE      PMPEIDES,XPMPEIDE
          ASSIGN    PMPEAMTT-PMPEGSTM,FORM12D2
          MOVE      FORM12D2,XTRANAMT
          MOVE      PMPETRDT,XPMPETRD
          MOVE      PMPETRTM,XPMPETRT
.
          MOVE      CURDTE8A,XPMPEDTE
          MOVE      CURDTE8A,PMPEDTEX
          MOVE      CTIMEIS,PMPETMEX
.
          CALL      STPPTD00
.
PRCPTD99  RETURN
.
.******************************************************************************
.*                                  CLRPTD00              Called by: PRCPTD00 *
.*                         Clear all extract variables                        *
.******************************************************************************
CLRPTD00  MOVE      SP70,XPMPEURN
          MOVE      SP70,XPMPEVIS
          MOVE      SP70,XPSRNAME
          MOVE      SP70,XPGVNAME
          MOVE      SP70,XPTTITLE
          MOVE      SP70,XPATADD1
          MOVE      SP70,XPATADD2
          MOVE      SP70,XPTSUBRB
          MOVE      SP70,XPTPPOST
          MOVE      SP70,XPAFUNDH
          MOVE      SP70,XFUNDREF
          MOVE      SP70,XPMPETDA
          MOVE      SP70,XDOCNAME
          MOVE      SP70,XPMPEITE
          MOVE      SP70,XPTADATE
          MOVE      SP70,XPTDDATE
          MOVE      SP70,XPMPEIDE
          MOVE      SP70,XTRANAMT
          MOVE      SP70,XPMPETRD
          MOVE      SP70,XPMPETRT
          MOVE      SP70,XPMPEDTE
.
CLRPTD99  RETURN
.
.******************************************************************************
.*                                  STPPTD00              Called by: PRCPTD00 *
.*                    Strip all trailing spaces from extract varaibles        *
.******************************************************************************
STPPTD00  STRIP     XPMPEURN
          STRIP     XPMPEVIS
          STRIP     XPSRNAME
          STRIP     XPGVNAME
          STRIP     XPTTITLE
          STRIP     XPATADD1
          STRIP     XPATADD2
          STRIP     XPTSUBRB
          STRIP     XPTPPOST
          STRIP     XPAFUNDH
          STRIP     XFUNDREF
          STRIP     XPMPETDA
          STRIP     XDOCNAME
          STRIP     XPMPEITE
          STRIP     XPTADATE
          STRIP     XPTDDATE
          STRIP     XPMPEIDE
          STRIP     XTRANAMT
          STRIP     XPMPETRD
          STRIP     XPMPETRT
          STRIP     XPMPEDTE
.
STPPTD99  RETURN
.******************************************************************************
.*                                  WRTPTD00              Called by: PROC0000 *
.*                       Write extract record to extract file                 *
.******************************************************************************
WRTPTD00   PACK      DIM350,SP70,SP70,SP70,SP70,SP70
           APPEND    XPMPEURN,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPEVIS,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPSRNAME,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPGVNAME,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTTITLE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPATADD1,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPATADD2,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTSUBRB,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTPPOST,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPAFUNDH,DIM350
           APPEND    PIPE,DIM350
           APPEND    XFUNDREF,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPETDA,DIM350
           APPEND    PIPE,DIM350
           APPEND    XDOCNAME,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPEITE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTADATE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTDDATE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPEIDE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XTRANAMT,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPETRD,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPETRT,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPEDTE,DIM350
.
           WRITE     EXTFILE1,SEQ;DIM350
.
           MOVE      EXTFILNM,PMPEEXFN
.
           CALL      UPPMPTD1
.
WRTPTD99  RETURN
.
.******************************************************************************
.*                                  WRTPTE00              Called by: PROC0000 *
.*                       Write extract record to extract file                 *
.******************************************************************************
WRTPTE00   PACK      DIM350,SP70,SP70,SP70,SP70,SP70
           APPEND    XPMPEURN,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPEVIS,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPSRNAME,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPGVNAME,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTTITLE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPATADD1,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPATADD2,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTSUBRB,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTPPOST,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPAFUNDH,DIM350
           APPEND    PIPE,DIM350
           APPEND    XFUNDREF,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPETDA,DIM350
           APPEND    PIPE,DIM350
           APPEND    XDOCNAME,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPEITE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTADATE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPTDDATE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPEIDE,DIM350
           APPEND    PIPE,DIM350
           APPEND    XTRANAMT,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPETRD,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPETRT,DIM350
           APPEND    PIPE,DIM350
           APPEND    XPMPEDTE,DIM350
.
           WRITE     EXTFILE1,SEQ;DIM350
.
WRTPTE99  RETURN
.******************************************************************************
.*                                  PREX0000              Called by: ML0000   *
.*                       Loop through pmsptdaf via file name                  *
.******************************************************************************
PREX0000  CALL      IBACLOCK
          PACK      CURDTE8A,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE8A
          CLOCK     TIME,CTIMEIS
.
          KEYIN     *P1:12,"File Name : ",EXTFILNM
.
          STRIP     PTCNJPEP
.
          PACK      EXTPATH,SP70,SP70
          PACK      EXTPATH,PTCNJPEP,EXTFILNM,TXTEXT,SP70
          SQUEEZE   EXTPATH
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EXTFILE1,EXTPATH
          TRAPCLR   IO
.
          IF        OVRCD=0
            CLOSE     EXTFILE1
            PACK      REMFILE,SP70,SP70
            CLEAR     REMFILE
            APPEND    "rm ",REMFILE
            APPEND    EXTPATH,REMFILE
            RESET     REMFILE
            EXECUTE   REMFILE,TASKID
          ENDIF
.
          PREP      EXTFILE1,EXTPATH
.
PREX1000  PACK      KEY55,EXTFILNM,SP70
          CALL      RSPMPTD5
PREX1100  CALL      RKPMPTD5
          BRANCH    OVRCD,PREX9000
.
          MATCH     EXTFILNM,PMPEEXFN
          GOTO      PREX9000 IF NOT EQUAL
.
          MATCH     "A",PMPETRTY                        * Add Record
          GOTO      PREX5000 IF EQUAL
.
          MATCH     "C",PMPETRTY                        * Change Record
          GOTO      PREX6000 IF EQUAL
.
          MATCH     "D",PMPETRTY                        * Delete Record
          GOTO      PREX7000 IF EQUAL
.
          GOTO      PREX1100
.
PREX5000  CALL      GETPAT00                            * Add Record
          BRANCH    EXIT,PREX1100
.
          CALL      PRCPTD00
          CALL      WRTPTE00
          GOTO      PREX1100
.
PREX6000  MOVE      SP70,SAVKEY55                       * Change Record
          PACK      SAVKEY55,PMPEEXFN,PMPETRDT,PMPETRTM,PMPEVISN,PMPETRAN,SP70
          MOVE      PMPEVISN,SAVEVISN
          MOVE      PMPETRAN,SAVETRAN
.
          PACK      KEY30,PMPEVISN,PMPETRAN,PMPETRDT,PMPETRTM,SP70
          CALL      RDPMPTD4
          CALL      RPPMPTD4
          BRANCH    OVRCD,PREX6500
.
          MATCH     SAVEVISN,PMPEVISN
          GOTO      PREX6500 IF NOT EQUAL
.
          MATCH     SAVETRAN,PMPETRAN
          GOTO      PREX6500 IF NOT EQUAL
.
          CALL      GETPAT00
          BRANCH    EXIT,PREX1100
.
          CALL      PRCPTD00
          MOVE      XTRANAMT,FORM12D2                   * Make dollar amount
          MULT      SEQ,FORM12D2                        * negative
          MOVE      FORM12D2,XTRANAMT
          CALL      WRTPTE00
.
PREX6500  PACK      KEY55,SAVKEY55,SP70
          CALL      RDPMPTD5
.
          CALL      GETPAT00
          BRANCH    EXIT,PREX1100
.
          CALL      PRCPTD00
          CALL      WRTPTE00
          GOTO      PREX1100
.
PREX7000  CALL      GETPAT00                            * Delete Record
          BRANCH    EXIT,PREX1100
.
          CALL      PRCPTD00
          MOVE      XTRANAMT,FORM12D2                   * Make dollar amount
          MULT      SEQ,FORM12D2                        * negative
          MOVE      FORM12D2,XTRANAMT
          CALL      WRTPTE00
          GOTO      PREX1100
.
PREX9000  
PREX9999  RETURN
.
DELPTH00  PACK    KEY16,SP70
          CALL    RSPMPTH1
DELPTH10  CALL    RKPMPTH1
          BRANCH  OVRCD,DELPTH99
.
          MATCH   SP70,PMPTFLNM
          GOTO    DELPTH10 IF EQUAL
          GOTO    DELPTH10 IF EOS
.
          PACK    KEY55,PMPTFLNM,SP70
          CALL    RSPMPTD5
          CALL    RKPMPTD5
.
          MATCH   PMPTFLNM,PMPEEXFN
          GOTO    DELPTH10 IF EQUAL
.
          PACK    KEY16,PMPTEXDT,PMPTEXTM,SP70
          CALL    DEPMPTH1
          GOTO    DELPTH10
.
DELPTH99  RETURN
.------------------------------------------------------------------------------
          INC       STD002IO
.
          INC       PATCODIO/INC
          INC       PMSHCPIO/INC
          INC       PATHSPIO/INC            * Hospital file
          INC       PATMI1IO/INC            * Admin file
          INC       PATVISIO/INC            * Visit file
          INC       PMSVX1IO/INC            * Visit ext file
          INC       PATMA1IO/INC            * Master file
          INC       PMSPX2IO/INC            * Master Ext file
          INC       PATDSCIO/INC            * Discharge file
          INC       PMSPTDIO/INC            * Prosthetics Tracking Detail file
          INC       PMSPTHIO/INC            * Prosthetics Tracking Header file
          INC       PATWC1IO/INC
          INC       PATWMAIO/INC
          INC       PATWVEIO/INC

.
          INC       CLPATDSC

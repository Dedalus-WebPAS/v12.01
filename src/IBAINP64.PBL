.******************************************************************************
.* System    :   Patient Managment System                                     *
.* Program   :   IBAINP64                                                     *
.* Desc      :   DRG Statistics Report                                        *
.******************************************************************************
.* Date      :   01/07/94                                                     *
.* Author    :   Gabrielle Cameron                                            *
.* Function  :   This program will report statistics for DRG's using the      *
.*               following options 1 = Top DRG's by Bed Days                  *
.*                                 2 = Top DRG's by Number of Discharges      *
.*                                 3 = Top DRG's by Revenue                   *
.*                                 4 = All DRG's by DRG Number                *
.* Mods                                                                       *
.*       V12.00.01  16/05/2025  Ebon Clements    TSK 0955096                  *
.*                  Alphanueric visit number changes                          *
.*       V11.05.01  26/03/2025  Davin            TSK 0956210                  *
.*                  Added DRG120,DRG0120 and DRG 12.0 to screen selections    *
.*       V11.04.01  09/05/2024  J.Tan            TSK 0942467                  *
.*                  Mod to print DRG Version on heading                       *
.*       V11.02.01  20/04/2022  Davin            TSK 0917793                  *
.*                  Added DRG110,DRG0110 and DRG 11.0 to screen selections    *
.*       V10.14.02  27/02/2019  Richa Phogat     TSK 0870439                  *
.*                  Added DRG100,DRG0100 and DRG 10.0 to screen selections    *
.*       V10.14.01  18/02/2019  Ebon Clements    TSK 0829856                  *
.*                  Recompiled for GETEFDRG - Contract from effective date    *
.*       V10.12.01  22/03/2018  Tracey Nguyen    TSK 0852793                  *
.*                  Added DRG90,DRG090 and DRG 9.0 to screen selections       *
.*       V10.08.01  26/08/2018  Don Nguyen       TSK 0312570                  *
.*                  Modified GDRG0000 to check for PTCNDGED=1 ; call GHFDRG00 *
.*       V10.06.01  29/05/2015  Ebon Clements    CAR 317094                   *
.*                  Added DRG80 & DRG080 an added DRG 7.0 to screen selections*
.*       V10.04.01  15/06/2014  Jill Parkinson   CAR 301639                   *
.*                  Moved TFILENAM to INIT0000                                *
.*       V10.03.03  03/05/2013  Davin            CAR 284669 HDP 2013 DRG 7.0  *
.*                  Added DRG70 & DRG070 an added DRG 7.0 to screen selections*
.*       V10.03.02  28/12/2012  Patrick Adair    CAR 261630                   *
.*                  Removed port number from temp file name                   *
.*       V10.03.01  28/06/2012  Mike Laming      CAR 267797 HDP 2012 DRG 6.2  *
.*                  Added DRG62 & DRG062 an added DRG 6.2 to screen selections*
.*        V9.12.01  19/06/2009  Mike Laming      CAR 197814 HDP 2009 DRG 6.0  *
.*                  Added DRG60 & DRG060 an added DRG 6.0 to screen selections*
.*        V9.10.01  11/06/2008  J.Tan         CAR 151803                      *
.*                  Mods to check for Procedure (record type 8)               *
.*        V9.08.02  05/04/2007  Mike Laming   CAR 137125 HDP 2007 DRG 5.2     *
.*                  Added PTCNU52G and PTCNC52C for DRG Ver 5.2 (Sect.10 144) *
.*                  Change PATDGWFD to includwe DRG Ver - modify TEMP file    *
.*        V9.08.01  19/03/2007  Mike Laming   CAR 135551                      *
.*                  Added DRG Vers 5.1                                        *
.*        V9.04.04  25/10/2004  Lina Vo       CAR 53125                       *
.*                  Mods to check for credit Notes                            *
.*        V9.04.03  07/09/2004  Guomin Fu     CAR 52766                       *
.*                  Added options for DRG versions in report                  *
.*        V9.04.02  04/09/2004  Guomin Fu     CAR 52766                       *
.*                  Added options for DRG versions in report                  *
.*                  07/09/2004  Davin         CAR 53146                       *
.*                  Added Multi-Hospital processing                           *
.*        V9.04.01  24/08/2004  Mike Laming   CAR 52768                       *
.*                  Oracle can't "isadd". Add optional Indexes to CMNDLINE    *
.*                  in CTMP0000 (remove routines AIDX0000 and RIDX0000, also  *
.*                  all calls to these routines in PROC0000)                  *
.*        V9.03.01  23/03/2004  Mike Laming   CAR 47566                       *
.*                  Add Lumpsum value to Accom at DINV1000 and others         *
.*        V9.02.01  04/09/2002  Jill Habasque SRF 17573                       *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V5.08.04  16/11/2000  Steve Armstrong   srf 6495                    *
.*                  Changed "Weighted Equivalent" to be 2 decimal places.     *
.*                  13.10.2000 Dean Cramer                                    *
.*                  Incorrect Header date (end date) fixed.                   *
.*        V5.08.03  10.10.2000 Sandra Barcham                                 *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  03/05/2000  Jill Habasque                                 *
.*                  Fixed tempfile and key definitions                        *
.*        V5.07.01  24/11/1999  Davin                                         *
.*                  Changed for 4 character drgs                              *
.*        V5.07.B01 19/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.******************************************************************************
.*        V6.02.02  01/09/94  Gabrielle                                       *
.*                  Fixed printing multiple pages for multiple reports        *
.*                  Fixed Heading for the All DRG's report                    *
.*        V6.02.03  20/09/94  Gabrielle                                       *
.*                  Fixed when printing non registered codes                  *
.*        V6.02.04  28/09/1994  Graeme Williams                               *
.*                  Changed name of the DRG description file                  *
.*        V6.02.05  28/09/1994  Gabrielle                                     *
.*                  Changed description for uncoded DRGs                      *
.*        V6.02.06  06/10/1994  Graeme Williams  SRF 132659                   *
.*                  Corrected calculation of discharge bed days               *
.*        V6.02.07  17/10/1994  Gabrielle        SRF 132824                   *
.*                  SSSSorry spelt Statistics wrong                           *
.*        V6.02.08  05/12/1994  Graeme Williams  Quote 8256 (Ramsay)          *
.*                  Added PTCNJDRG to determine if journals are to be included*
.*        V5.02.00  05/12/1994  Graeme Williams                               *
.*                  Ported from release 6.02, version V6.02.08                *
.*        V5.02.01  03/04/1995  Greg Horvat                                   *
.*                  Fixed the page breaks when going over 2 pages             *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCOMM/INC
          INC       PATCFAFD/INC
          INC       PATCONFD/INC             * 
          INC       PATCODFD/INC             * patcodes
          INC       PATDGWFD/INC             * patdgwaf
          INC       PATDSCFD/INC             * patdschf
          INC       PATDTRFD/INC             * patdtraf
          INC       PATFX1FD/INC
          INC       PATHSPFD/INC             * pathspaf
          INC       PATINVFD/INC             * patinvrf
          INC       PATMI1FD/INC             * patmi1af
          INC       PATTRNFD/INC             * pattranf
          INC       PATPGRFD/INC             * patpgraf     
          INC       PATFN1FD/INC             * patfundf     
          INC       PMSVX1FD/INC             * pmsvx1af
          INC       PMSCNOFD/INC             * Credit note summary file
          INC       PMSFCIFD/INC             * Credit note file
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       PMSEDGFD/INC             * Effective DRG Dates
.
ALLDRGS   INIT      "All DRG's by DRG Code"
ASCIFILE  FORM      1                        * Generate Ascii file
ASCINAME  DIM       8                        * Ascii File Name
.
CALCBDAY  FORM     8                         * No of Bed Days for DRG Code
CALCBFEE  FORM     8.2                       * Total Bed fees invoiced
CALDAYS   FORM     3                         * Needed for NHOSPDAY
CALCDRGN  DIM      4                         * DRG Number
CALCDRGV  DIM      3                         * DRG Version            *I-137125
CALCDRGW  FORM    10                         * DRG Weighting
CALCDSCH  FORM     8                         * Discharged for DRG Code
CALCMDCN  DIM      4                         * MDC Number
CALCOFEE  FORM     8.2                       * Total Other fees invoiced
CALCTFEE  FORM     8.2                       * Total Theatre fees invoiced
CALCTREV  FORM     8.2                       * Bed + Theatre + Other Fees
CMDLINE   DIM       60                       * Command Line
COUNT     FORM      5                        * Counter
CRDNAMNT  FORM     10.2
CURRDATE  DIM       8                        * Current Date
.
DATERNG   DIM       9 
DIM35     DIM       35
DISCHRGE  INIT      "Discharge"
DRECORD   FORM      1                        * Discharged Record Found ?
.                                              1 = Yes, 0 = No
DRG31     INIT      "3.1"
DRG41     INIT      "4.1"
DRG42     INIT      "4.2"
DRG50     INIT      "5.0"
DRG51     INIT      "5.1"                                             *I-135551
DRG52     INIT      "5.2"                                             *I-137125
DRG60     INIT      "6.0"                                             *I-197814
DRG62     INIT      "6.2"                                             *I-267797
DRG70     INIT      "7.0"                                             *I-284669
DRG80     INIT      "8.0"
DRG90     INIT      "9.0"
DRG100    INIT      "10.0"
DRG110    INIT      "11.0"
DRG120    INIT      "12.0"
DRGHF     INIT      "HF DRG"
DRG031    INIT      "031"
DRG041    INIT      "041"
DRG042    INIT      "042"
DRG050    INIT      "050"
DRG051    INIT      "051"                                             *I-135551
DRG052    INIT      "052"                                             *I-137125
DRG060    INIT      "060"                                             *I-197814
DRG062    INIT      "062"                                             *I-267797
DRG070    INIT      "070"                                             *I-284669
DRG080    INIT      "080"
DRG090    INIT      "090"
DRG0100   INIT      "100"
DRG0110   INIT      "110"
DRG0120   INIT      "120"
.
CFPAYCOD  DIM       3                        * 3M Codefinder PAY Code
.
FILENAME  DIM       6                        * File Name for new file
FIRST     FORM      1                        * First Record Printed
.                                              1 = Yes, 0 = No
FROMDATE  DIM       8                        * From Date Range (CCYYMMDD)
.
IBCNMHOS  FORM      1                        * Multi Hospital Indicator
INVOICE   INIT      "Invoice"
.
JYEAR     FORM     2                         * Needed for NHOSPDAY
.
INDEX     DIM      15                        * Index for new file
INDEX2    INIT     " U17-24,5-8,1-4"         * Index for Top Bed Days
INDEX3    INIT     " U9-16,5-8,1-4"          * Index for Top Discharges
INDEX4    INIT     " U43-53,5-8,1-4"         * Index for Top Revenue
.
NUMBDRG   FORM      3                        * Number of DRG's to report
NUMBMDC   FORM      3                        * Number of MDC's to report
NBRDAYS   FORM      5                        * Number of Days
.
....PTCNJDRG  FORM      1                        * Include journals ?
.
REPTINVS  FORM     8                         * Non Discharged Invoices
REPTBDAY  FORM     8                         * Non Discharged Bed Days
REPTBFEE  FORM     8.2                       * Non Discharged Bed fees
REPTTFEE  FORM     8.2                       * Non Discharged Theatre fees
REPTOFEE  FORM     8.2                       * Non Discharged Other fees
REPTTREV  FORM     8.2                       * Non Discharged Total Revenue
RTDAYS    FORM     4
.
SAVEDRG   DIM      4                         * Save DRG Code
SAVEMDCN  DIM      4                         * Save MDC Code
SKEY30    DIM      30
STRTDATE  DIM       8                        * From Date Range (CCYYMMDD)
.
TMPFNAME  DIM       8                        * Temporary File Name for new index
TMPFNAM1  DIM       8                        * Temporary File Name
TODATE    DIM       8                        * To Date Range (CCMMYYDD)
TOPBDAY   INIT      "Top DRG's by Bed days"
TOPDSCH   INIT      "Top DRG's by Discharges"
TOPREV    INIT      "Top DRG's by Revenue"
.
TOTLBDAY  FORM      8                        * Total Number of Bed Days
TOTLBFEE  FORM      8.2                      * Total of Bed Fees
TOTLDRG   FORM      8                        * Total Number of DRG's 
TOTLDSCH  FORM      8                        * Total Number of Discharges 
TOTLMDC   FORM      8                        * Total Number of MDC's 
TOTLOFEE  FORM      8.2                      * Total Other fees invoiced
TOTLRWGT  FORM      3.6                      * Total Resource Weight
TOTLTFEE  FORM      8.2                      * Total Theatre fees invoiced
TOTLTREV  FORM      8.2                      * Total Revenue
TOTLWEQV  FORM      10.2                     * Total Weighted Equivalent
.
UNCODED   INIT     "UNC "
.
VERSNDRG  DIM      3                         * 
VERSNUMB  FORM     2                         * 
VERSDESC  DIM      10                        * 
.
WDATE1    DIM      8                         * Needed for NHOSPDAY
WDATE2    DIM      8                         * Needed for NHOSPDAY
WGHTEQIV  FORM     10.2                      * Weighted Equivalent
.
.--- Temporary File ---
.
TEMPFIL1  IFILE    SQL,FIXED=67, KEY="U5-8,1-4"       * KEY8          *C-137125
TEMPFIL2  IFILE    SQL,FIXED=67, KEY="U1-4,5-8"       * KEY8          *C-137125
.
TEMPFIL3  IFILE    SQL,FIXED=67, KEY="U17-24,5-8,1-4" * KEY16         *C-137125
TEMPFIL4  IFILE    SQL,FIXED=67, KEY="U9-16,5-8,1-4"  * KEY16         *C-137125
TEMPFIL5  IFILE    SQL,FIXED=67, KEY="U43-53,5-8,1-4" * KEY19         *C-137125
.
.Name     Type   Length   Physical  Location  Description
.------   ----   ------   --------  --------  ------------------------------
TEMPMDCN  DIM      4         4         1      MDC Number
TEMPDRGN  DIM      4         4         5      DRG Number
DTEMPDSC  DIM      8         8         9      Discharged for DRG Code
DTEMPBDA  DIM      8         8        17      No of Bed Days for DRG Code
TEMPBFEE  FORM     8.2       6        25      Total Bed fees invoiced
TEMPTFEE  FORM     8.2       6        31      Total Theatre fees invoiced
TEMPOFEE  FORM     8.2       6        37      Total Other fees invoiced
DTEMPTRE  DIM     11        11        43      Bed + Theatre + Other Fees
TEMPDRGW  DIM     10        10        54      DRG Weighting
TEMPDRGV  DIM      3         3        64      DRG Number              *I-137125
.                                    -----
.End Record                           67
.
. Redfined Variables
TEMPDSCH  FORM     8         5         7      Discharged for DRG Code
TEMPBDAY  FORM     8         5        12      No of Bed Days for DRG Code
TEMPTREV  FORM     8.2       6        35      Bed + Theatre + Other Fees
.
XXASCII1  FILE    ASCII, FIXED=161
.  
.Name     Type   Length   Physical  Location  Description
.------   ----   ------   --------  --------  ------------------------------
XXASDRGN  DIM      4         4         1      DRG Number 
XXASCOMA  INIT     ","       1         5      Comma Delimiter
XXASMDCN  DIM      4         4         6      MDC Number
.XXASCOMA INIT     ","       1        10      Comma Delimiter
XXASDESC  DIM     60        60        11      DRG Description
.XXASCOMA INIT     ","       1        71      Comma Delimiter
XXASDRGW  DIM     10        10        72      DRG Weighting
.XXASCOMA INIT     ","       1        82      Comma Delimiter
XXASDSCH  FORM     8         8        83      Discharged for DRG Code
.XXASCOMA INIT     ","       1        91      Comma Delimiter
XXASBDAY  FORM     8         8        92      No of Bed Days for DRG Code
.XXASCOMA INIT     ","       1       100      Comma Delimiter
XXASWEQV  FORM     12       12       101      Weighted Equivalent
.XXASCOMA INIT     ","       1       113      Comma Delimiter
XXASBFEE  FORM     8.2      11       114      Total Bed fees invoiced
.XXASCOMA INIT     ","       1       125      Comma Delimiter
XXASTFEE  FORM     8.2      11       126      Total Theatre fees invoiced
.XXASCOMA INIT     ","       1       137      Comma Delimiter
XXASOFEE  FORM     8.2      11       138      Total Other fees invoiced
.XXASCOMA INIT     ","       1       149      Comma Delimiter
XXASTREV  FORM     8.2      11       150      Bed + Theatre + Other Fees
.                                   -----
.End of Record                       161
PRGID     INIT      "IBAINP64"
PRGNAM    INIT      "DRG Statistics Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9000            * EXIT = 1 if 0 chosen in menu
          LOAD      DIM35,OPTION,TOPBDAY,TOPDSCH,TOPREV,ALLDRGS
          BRANCH    OPTION,ML1000,ML2000,ML3000,ML4000
.
. ---- Top DRG's by Bed Days ----
.
ML1000    DISPLAY   *P46:2,*V2LON,TOPBDAY
          CALL      RNGE0000                 * By Disch. or Invoice Date Range
          BRANCH    EXIT,ML0100              * Exit = 1 = Select Options
ML1100    CALL      KDAT0000                 * Keyin Dates 
          BRANCH    EXIT,ML1000
          CALL      NDRG0000                 * How many DRG's to report
          CALL      VDRG0000                 * 
          CALL      ASCI0000                 * Generate Ascii file ?
          CALL      KHOS0000                 * Keyin hospital
          BRANCH    EXIT,ML1100
          CALL      CONTQST
          BRANCH    CEXIT,ML1200,ML1100,ML1000
ML1200    CALL      PROC0000                 * Process Date Range
          GOTO      ML1000
.
. ---- Top DRG's by Number of Discharges ----
.
ML2000    DISPLAY   *P46:2,*V2LON,TOPDSCH
          CALL      RNGE0000                 * By Disch. or Invoice Date Range
          BRANCH    EXIT,ML0100              * Exit = 1 = Select Options
ML2100    CALL      KDAT0000                 * Keyin Dates 
          BRANCH    EXIT,ML2000
          CALL      NDRG0000                 * How many DRG's to report
          CALL      VDRG0000                 * 
          CALL      ASCI0000                 * Generate Ascii file ?
          CALL      KHOS0000                 * Keyin hospital
          BRANCH    EXIT,ML2100
          CALL      CONTQST
          BRANCH    CEXIT,ML2200,ML2100,ML2000
ML2200    CALL      PROC0000                 * Process Date Range
          GOTO      ML2000
.
. ---- Top DRG's by Revenue ----
.
ML3000    DISPLAY   *P46:2,*V2LON,TOPREV
          CALL      RNGE0000                 * By Disch. or Invoice Date Range
          BRANCH    EXIT,ML0100              * Exit = 1 = Select Options
ML3100    CALL      KDAT0000                 * Keyin Dates 
          BRANCH    EXIT,ML3000
          CALL      NDRG0000                 * How many DRG's to report
          CALL      VDRG0000                 * 
          CALL      ASCI0000                 * Generate Ascii file ?
          CALL      KHOS0000                 * Keyin hospital
          BRANCH    EXIT,ML3100
          CALL      CONTQST
          BRANCH    CEXIT,ML3200,ML3100,ML3000
ML3200    CALL      PROC0000                 * Process Date Range
          GOTO      ML3000
.
. ---- All DRG's by DRG Number ----
.
ML4000    DISPLAY   *P46:2,*V2LON,ALLDRGS
          CALL      RNGE0000                 * By Disch. or Invoice Date Range
          BRANCH    EXIT,ML0100              * Exit = 1 = Select Options
ML4100    CALL      KDAT0000                 * Keyin Dates 
          BRANCH    EXIT,ML4000
          CALL      VDRG0000
          CALL      ASCI0000                 * Generate Ascii file ?
          CALL      KHOS0000                 * Keyin hospital
          BRANCH    EXIT,ML4100
          CALL      CONTQST
          BRANCH    CEXIT,ML4200,ML4000,ML4000
ML4200    CALL      PROC0000                 * Process Date Range
          GOTO      ML4000
.
ML9000    CALL      DTMP0000                 * Delete Tempfiles
.
ML9999    CHAIN     PGM                      * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                 * display heading
.
          DISPLAY   *P56:24,"Opening "; 
.
          DISPLAY   *P64:24,"controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          DISPLAY   *P64:24,"patcodes"       * PATCODFD/INC
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdgwaf"       * PATDGWFD/INC
          OPEN      PATDGWA1,"patdgwaf"
.
          DISPLAY   *P64:24,"patdschf"       * PATDSCFD/INC
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patdtraf"       * PATDTRFD/INC
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"patinvrf"       * PATINVFD/INC
          OPEN      PATINVR2,"patinvrf"
.
          DISPLAY   *P64:24,"patmi1af"       * PATMI1FD/INC
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf"       * PATTRNFD/INC
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmsvx1af"       * PMSVX1FD/INC
          OPEN      PMSVX1A1,"pmsvx1af"
.
.... 
          DISPLAY   *P64:24,"patpgraf"       * PATPGRFD/INC
          OPEN      PATPGRA1,"patpgraf"
.
          DISPLAY   *P64:24,"patfn1af"       * PATFN1AD/INC
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pmscnoaf"
          OPEN      PMSCNOA3,"pmscnoaf"
.
          DISPLAY   *P64:24,"pmsfciaf"
          OPEN      PMSFCIA1,"pmsfciaf"
.
          DISPLAY   *P64:24,"pmsedgaf"
          OPEN      PMSEDGA1,"pmsedgaf"
.
          MOVE      ZERO,OVRCD               * assume no format error
          TRAP      OVERCOND IF FORMAT       * check for format error
          READ      CONTROLF,TEN;*38,PTCNJDRG
          TRAPCLR   FORMAT
          IF        OVRCD = 1
            MOVE      ZERO,PTCNJDRG          * format error - assume off
          ENDIF
          READ      CONTROLF,HUND05;*158,PTCNDGPV         * 
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TMPFNAM1
.
          CALL      CLER0000                  * Clear Calculated values
          CALL      CREP0000                  * Clear Report values
          MOVE      ONE,CNOUNDLN
.
INIT9999  RETURN
+
.****************************************************************************
.*                                CLER0000             Called by: INIT0000  *
.*                             Clear Variables                    UTMP0000  *
.*                                                                WTMP0000  *
.****************************************************************************
CLER0000  MOVE     SP4,CALCMDCN              * MDC Number
          MOVE     SP4,CALCDRGN              * DRG Number 
          MOVE     SP3,CALCDRGV              * DRG Version            *I-137125
          MOVE     ZERO,CALCDSCH              * Discharged for DRG Code 
          MOVE     ZERO,CALCBDAY              * No of Bed Days for DRG Code
          MOVE     ZERO,CALCBFEE             * Total Bed fees invoiced
          MOVE     ZERO,CALCTFEE             * Total Theatre fees invoiced
          MOVE     ZERO,CALCOFEE             * Total Other fees invoiced
          MOVE     ZERO,CALCTREV             * Bed + Theatre + Other Fees
          MOVE     SP10,CALCDRGW             * DRG Weighting
.
          MOVE     SP4,TEMPMDCN              * MDC Number
          MOVE     SP4,TEMPDRGN              * DRG Number 
          MOVE     SP3,TEMPDRGV              * DRG Version            *I-137125
          MOVE     ZERO,TEMPDSCH             * Discharged for DRG Code
          MOVE     ZERO,TEMPBDAY             * No of Bed Days for DRG Code
          MOVE     ZERO,TEMPBFEE             * Total Bed fees invoiced
          MOVE     ZERO,TEMPTFEE             * Total Theatre fees invoiced
          MOVE     ZERO,TEMPOFEE             * Total Other fees invoiced
          MOVE     ZERO,TEMPTREV             * Bed + Theatre + Other Fees
          MOVE     SP10,TEMPDRGW             * DRG Weighting
.
CLER9999  RETURN
+
.****************************************************************************
.*                                CREP0000             Called by: INIT0000  *
.*                       Clear Report variables                             *
.****************************************************************************
CREP0000  MOVE     ZERO,REPTINVS             * No of Bed Days for DRG Code
          MOVE     SP8,REPTBDAY              * No of Bed Days for DRG Code
          MOVE     ZERO,REPTBFEE             * Total Bed fees invoiced
          MOVE     ZERO,REPTTFEE             * Total Theatre fees invoiced
          MOVE     ZERO,REPTOFEE             * Total Other fees invoiced
          MOVE     ZERO,REPTTREV             * Bed + Theatre + Other Fees
.
          MOVE      ZERO,TOTLDSCH            * Total Discharges
          MOVE      ZERO,TOTLBDAY            * Total Bed Days
          MOVE      ZERO,TOTLWEQV            * Total Weighted Equivalent
          MOVE      ZERO,TOTLDRG             * Total DRG'S
          MOVE      ZERO,TOTLMDC             * Total MDC'S
          MOVE      ZERO,TOTLBFEE            * Total of Bed Fees
          MOVE      ZERO,TOTLTFEE            * Total Theatre fees invoiced
          MOVE      ZERO,TOTLOFEE            * Total Other fees invoiced
          MOVE      ZERO,TOTLTREV            * Total Revenue
          MOVE      ZERO,CPAGENO             * Clear Page Number
CREP9999  RETURN
.
.
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.*              OPTION  = 1  Top DRG's by Bed Days                          *
.*                        2  Top DRG's by Number of Discharges              *
.*                        3  Top DRG's by Revenue                           *
.*                        4  All DRG's by DRG Number                        *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  HLINE     *G37,2,52,80
          DISPLAY   *P1:4,*EL,*V2LON,"1",*HOFF," = Top DRG's by Bed Days":
                    *P1:5,*V2LON,"2",*HOFF," = Top DRG's by Number ":
                                           "of Discharges":
                    *P1:6,*V2LON,"3",*HOFF," = Top DRG's by Revenue":
                    *P1:7,*V2LON,"4",*HOFF," = All DRG's by DRG Number"
OPTN0100  KEYIN     *P1:9,*EF,"Select Option : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL        * exit
          BRANCH    OPTION,OPTN9900,OPTN9900,OPTN9900,OPTN9900
          BEEP                               * Error 
          GOTO      OPTN0100                 * Re Key
.
OPTN9000  MOVE      TRUE,EXIT                * set EXIT = 1
          GOTO      OPTN9999
.
OPTN9900  MOVE      FALSE,EXIT               * set EXIT = 0
OPTN9999  RETURN
+
.**************************************************************************
.*                               RNGE0000              Called by: ML0000  *
.*             Get either the Discharge or Invoice Date range             *
.*    Returns:  EXIT    = FALSE (0)  Continue with processing             *
.*                        TRUE  (1)  Exit option selected                 *
.*              COPTION  = 1  Range of Discharge Dates                    *
.*                         2  Range of Invoice Dates                      *
.**************************************************************************
RNGE0000 PACK      CURRDATE,CCC,CYY,CMM,CDD
         REP       " 0",CURRDATE
         DISPLAY   *P1:11,*V2LON,"0",*HOFF," = Exit":
                    *P1:12,*V2LON,"1",*HOFF," = Range of Discharge Dates":
                    *P1:13,*V2LON,"2",*HOFF," = Range of Invoice Dates"
RNGE0100  KEYIN     *P1:15,*EF,"Select Option : ",*V2LON,COPTION
.
          COMPARE   ZERO,COPTION             * Exit ?
          GOTO      RNGE9000 IF EQUAL        * Yes
.
          BRANCH    COPTION,RNGE9900,RNGE9900
.                           Discharge Invoice
          BEEP                               * Error
          GOTO      RNGE0100                 * Re-Key
.
RNGE9000  MOVE      TRUE,EXIT
          GOTO      RNGE9999
.
RNGE9900  MOVE      FALSE,EXIT
.
RNGE9999  RETURN
+
.**************************************************************************
.*                               KDAT0000              Called by: ML0000  *
.*                 Keyin Discharge or Invoice Date Range                  *
.*    Returns:  EXIT    = FALSE (0)  Continue with processing             *
.*                        TRUE  (1)  Exit option selected                 *
.**************************************************************************
KDAT0000  BRANCH    COPTION,KDAT1000,KDAT2000
.
KDAT1000  CALL      DRNG0000                 * Keyin Discharge Date Range
          BRANCH    EXIT,KDAT9900
          GOTO      KDAT3000
.
KDAT2000  CALL      IRNG0000                 * Keyin Invoice Date Range
          BRANCH    EXIT,KDAT9900
.
.         Validate Date Ranges
.
KDAT3000  MATCH     STRTDATE,TODATE          * Fromdate after Todate ?
          IF        @LESS  
            DISPLAY   *P1:24,*EL,*B,"From date must be before To date. ";
            CALL      HITENTER
            GOTO      DRNG0000
          ENDIF
.
KDAT9000  MOVE      FALSE,EXIT               * Continue with Processing
          CALL      CTMP0000                 * Create Temp file
          GOTO      KDAT9999
.
KDAT9900  MOVE      TRUE,EXIT                * Exit
.
KDAT9999  RETURN
+
.**************************************************************************
.*                               DRNG0000            Called by: RNGE0000  *
.*                       input Discharge date range                       *
.*    Returns:  EXIT    = FALSE (0)  Continue with processing             *
.*                        TRUE  (1)  Exit option selected                 *
.*              STRTDATE  Discharge From Date                             *
.*              TODATE    Discharge To Date                               *
.**************************************************************************
DRNG0000  DISPLAY   *P1:17,*EF,"Discharge Date From : ":
                    *P1:18,*EL,"Discharge Date To   : "
.
.         Keyin From Discharge Date
.
DRNG1000  MOVE      TWENTY3,CCOL             * Set Up Column 
          MOVE      TEN7,CVERT               * Set Up Veritcal
          CALL      FDAT0000                 * Keyin From Date
          BRANCH    EXIT,DRNG9900            * No input, exit routine
.
.         Keyin To Discharge Date
.
          MOVE      TWENTY3,CCOL             * Set Up Column
          MOVE      TEN8,CVERT               * Set Up Vertical
          CALL      TDAT0000                 * Keyin To Date
          BRANCH    EXIT,DRNG1000            * No input, keyin from date
.
          CALL      VDAT0000                 * Fromdate after Todate ?
          BRANCH    EXIT,DRNG0000            * Exit =1 = Yes
.
          MOVE      FALSE,EXIT
          GOTO      DRNG9999
.
DRNG9900  MOVE      TRUE,EXIT
.
DRNG9999  RETURN
+
.**************************************************************************
.*                               IRNG0000            Called by: RNGE0000  *
.*                       input invoice date range                         *
.*    Returns:  EXIT    = FALSE (0)  Continue with processing             *
.*                        TRUE  (1)  Exit option selected                 *
.*              STRTDATE  Invoice From Date                               *
.*              TODATE    Invoice To Date                                 *
.**************************************************************************
IRNG0000  DISPLAY   *P1:17,*EF,"Invoice Date From : ":
                    *P1:18,*EL,"Invoice Date To   : "
.
.         Keyin From Invoice Date
.
IRNG1000  MOVE      TWENTY1,CCOL             * Set Up Column
          MOVE      TEN7,CVERT               * Set Up Vertical
          CALL      FDAT0000                 * Keyin From Date
          BRANCH    EXIT,IRNG9900            * No input, exit routine
.
.         Keyin To Invoice Date
.
          MOVE      TWENTY1,CCOL             * Set Up Column
          MOVE      TEN8,CVERT               * Set Up Vertical
          CALL      TDAT0000                 * Keyin To Date
          BRANCH    EXIT,IRNG1000            * No input, keyin from date
.
          CALL      VDAT0000                 * Fromdate after Todate ?
          BRANCH    EXIT,IRNG0000            * Exit =1 = Yes
.
          MOVE      FALSE,EXIT               * Continue with processing
          GOTO      IRNG9999
.
IRNG9900  MOVE      TRUE,EXIT               * Exit
.
IRNG9999  RETURN
+
.**************************************************************************
.*                               FDAT0000            Called by: RNGE0000  *
.*                           input From Date                              *
.*    Returns:  EXIT    = FALSE (0)  Continue with processing             *
.*                        TRUE  (1)  Exit Chosen                          *
.**************************************************************************
FDAT0000  CALL      IDAT0000                 * Initialize Date Variable
          CALL      KEYDATE                  * Keyin Date
          BRANCH    OVRCD,FDAT9900           * No input, exit
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     STRTDATE,CURRDATE
          IF        @LESS  
            DISPLAY   *P1:24,*EL,*B,"Date Must Not be in the future. ";
            CALL      HITENTER
            GOTO      FDAT0000
          ENDIF
.
          MOVE      FALSE,EXIT               * Continue with processing
          GOTO      FDAT9999                 * Exit Routine
.
FDAT9900  MOVE      TRUE,EXIT                * Exit
.
FDAT9999  RETURN
+
.**************************************************************************
.*                               TDAT0000            Called by: RNGE0000  *
.*                           input To Date                                *
.*    Returns:  EXIT    = FALSE (0)  Continue with processing             *
.*                        TRUE  (1)  Exit Chosen                          *
.**************************************************************************
TDAT0000  CALL      IDAT0000                 * Initialize Date Variable
          CALL      KEYDATE                  * Keyin Date
          BRANCH    OVRCD,TDAT9900           * No input, return to fromdate
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          MATCH     TODATE,CURRDATE
          IF        @LESS  
            DISPLAY   *P1:24,*EL,*B,"Date Must Not be in the future. ";
            CALL      HITENTER
            GOTO      TDAT0000
          ENDIF
.
          MOVE      FALSE,EXIT               * Continue with processing
          GOTO      TDAT9999                 * Exit Routine
.
TDAT9900  MOVE      TRUE,EXIT                * Exit
.
TDAT9999  RETURN
+
.**************************************************************************
.*                                 IDAT0000           Called by: FDAT0000 *
.*                         Initialize Date Variables             TDAT0000 *
.**************************************************************************
IDAT0000  MOVE      ZERO,CCANLDTE
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
.
IDAT9999  RETURN
+
.**************************************************************************
.*                                 VDAT0000           Called by: FDAT0000 *
.*                         Validate Date Variables               TDAT0000 *
.*    Returns:  EXIT    = FALSE (0)  Dates Valid continue processing      *
.*                        TRUE  (1)  Dates NOT valid exit                 *
.**************************************************************************
VDAT0000  MATCH     STRTDATE,TODATE             * Fromdate after Todate ?
          GOTO      VDAT9900 IF LESS  
.
          MOVE      FALSE,EXIT
          GOTO      VDAT9999
.
VDAT9900  DISPLAY   *P1:24,*EL,*B,"From date must be before To date. ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
.
VDAT9999  RETURN
.**************************************************************************
.*                                 NDRG0000            Called by: ML0000  *
.*                         Number of DRG's to report                      *
.*    Returns:  NUMBBDRG = Number of DRG's to report                      *
.**************************************************************************
NDRG0000  KEYIN     *P1:20,*EF,"Number of DRG's to report? ",*V2LON,NUMBDRG
.
          COMPARE   NUMBDRG,ZERO             * No. of DRG's less than 0 ?
          IF        !@LESS 
            DISPLAY   *P1:24,*EL,*B,"Number of DRG's must be greater than 0. ";
            CALL      HITENTER
            GOTO      NDRG0000               * Re Key
          ENDIF
.
NDRG9999  RETURN
+
+
.**************************************************************************
.*                                 VDRG0000            Called by: ML0000  *
.*                         Version of DRG's to report            *
.*    Returns:  VERSNDRG = Version of DRG's to report                     *
.**************************************************************************
VDRG0000
          MOVE      SP10,VERSDESC
          MOVE      SP3,VERSNDRG
          MOVE      ZERO,VERSNUMB
.                                                                     *C-137125
          LOAD      VERSDESC,VERSNUMB,DRG31,DRG41,DRG42,DRG50,DRG51:
                                      DRG52,DRG60,DRG62,DRG70,DRG80:
                                      DRGHF,DRG90,DRG100,DRG110,DRG120
          DISPLAY   *P40:23,*EF,"Version of DRG's to report? ",*V2LON,VERSDESC
.
          MOVE      "60",HLEF
          MOVE      "6",HTOP
          MOVE      "78",HRIG
          MOVE      "22",HBOT                                         *C-284669
.
          CALL      GETSCR00                     * save screen
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,*V2LON,HLEF,HTOP,HRIG,HBOT
.
          DISPLAY   *P62:7,*V2LON,"1.",*HOFF," DRG Ver 3.1 ":
                    *P62:8,*V2LON,"2.",*HOFF," DRG Ver 4.1 ":
                    *P62:9,*V2LON,"3.",*HOFF," DRG Ver 4.2 ":
                    *P62:10,*V2LON,"4.",*HOFF," DRG Ver 5.0 ":
                    *P62:11,*V2LON,"5.",*HOFF," DRG Ver 5.1 ":
                    *P62:12,*V2LON,"6.",*HOFF," DRG Ver 5.2 ":
                    *P62:13,*V2LON,"7.",*HOFF," DRG Ver 6.0 ":        *I-197814
                    *P62:14,*V2LON,"8.",*HOFF," DRG Ver 6.2 ":        *I-267797
                    *P62:15,*V2LON,"9.",*HOFF," DRG Ver 7.0 ":        *I-284669
                    *P61:16,*V2LON,"10.",*HOFF," DRG Ver 8.0 ":
                    *P61:17,*V2LON,"11.",*HOFF," HF DRG      ":        *C-267797
                    *P61:18,*V2LON,"12.",*HOFF," DRG Ver 9.0 ":
                    *P61:19,*V2LON,"13.",*HOFF," DRG Ver 10.0":
                    *P61:20,*V2LON,"14.",*HOFF," DRG Ver 11.0":
                    *P61:21,*V2LON,"15.",*HOFF," DRG Ver 12.0";

          KEYIN     *P67:23,*V2LON,VERSNUMB
          CALL      PUTSCR00                     * restore screen
.                                                                     *C-267797
          IF        VERSNUMB < 1 | VERSNUMB > 15
            DISPLAY   *P1:24,"Invalid DRG Version ";
            CALL      HITENTER
            GOTO      VDRG0000
          ENDIF
.
.                                                                     *C-137125
          LOAD      VERSDESC,VERSNUMB,DRG31,DRG41,DRG42,DRG50,DRG51:
                                      DRG52,DRG60,DRG62,DRG70,DRG80:
                                      DRGHF,DRG90,DRG100,DRG110,DRG120
          DISPLAY   *P70:23,*V2LON,VERSDESC
.
VDRG9999  RETURN
+
.**************************************************************************
.*                                 ASCI0000            Called by: ML0000  *
.*                         Generate an Ascii file                         *
.*    Returns: ASCIFILE = FALSE (0)  Do NOT generate Ascii file           *
.*                        TRUE  (1)  Generate Ascii file                  *
.**************************************************************************
ASCI0000  KEYIN     *P1:22,*EL,"Generate an Ascii file (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,")? ",*V2LON,ANS
.
          REP       "yYnN",ANS               * Allow for lower case letters
.
          MATCH     ANSY,ANS                 * Generate Ascii file ?
          GOTO      ASCI1000 IF NOT EQUAL
.
          MOVE      TRUE,ASCIFILE            * Yes, ASCIFILE = 1
.
          CALL      ANAM0000                 * Get Ascii File Name
          BRANCH    EXIT,ASCI0000            * Nothing entered, re-key Y/N
          GOTO      ASCI9999
.
ASCI1000  MATCH     ANSN,ANS                 * Generate Ascii file ?
          GOTO      ASCI2000 IF NOT EQUAL    
.
          MOVE      FALSE,ASCIFILE           * No, ASCIFILE = 0
          MOVE      ZERO,CLNO                * Initialize Line Number
          GOTO      ASCI9999
.
ASCI2000  BEEP                               * Error
          GOTO      ASCI0000                 * Re-Key
.
ASCI9999  RETURN
+
.**************************************************************************
.*                                 ANAM0000           Called by: ASCI0000 *
.*                       Obtain Ascii file name                           *
.**************************************************************************
ANAM0000  KEYIN     *P36:22,"File Name : ",*V2LON,ASCINAME
.
          RESET     ASCINAME                 * File Name Entered
          GOTO      ANAM9900 IF EOS
.
          CLOSE     XXASCII1                 * Ensure file is closed
.
.         Check if file already exits
.
          TRAP      ANAM1000 IF IO           * File does not exist
          OPEN      XXASCII1,ASCINAME
          TRAPCLR   IO
.
          MOVE      SP1,ANS
          STRIP     ASCINAME
ANAM0100  DISPLAY   *P1:24,*B,*V2LON,ASCINAME,".txt",*HOFF," already exits.";
          KEYIN     " Overwrite with new file (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,")? ",*V2LON,ANS
.
          REP       "yYnN",ANS               * Allow for lower case letters
.
          MATCH     ANSY,ANS                 * Overwrite Ascii file ?
          GOTO      ANAM9999 IF EQUAL        * Yes, exit routine
.
          MATCH     ANSN,ANS                 * Overwrite Ascii file ?
          GOTO      ANAM0000 IF EQUAL        * No, Re-Key file name
.
          BEEP                               * Error
          GOTO      ANAM0100                 * Re-Key
.
ANAM1000  NORETURN 
          PREP      XXASCII1,ASCINAME  
.
          MOVE      FALSE,EXIT
          GOTO      ANAM9999
.
ANAM9900  MOVE      TRUE,EXIT
          
ANAM9999  RETURN
.
.****************************************************************************
.*                              PROC0000               Called by: ML0000    *
.*                         Process the Date Range                           *
.****************************************************************************
PROC0000  CLOCK     TIME,CTIMEIS             * report start time
.
          BRANCH    COPTION,PROC1000,PROC2000
.                           Discharge,Invoice
PROC1000  CALL      PDIS0000                 * Process Discharge Date Range
          GOTO      PROC3000
.
PROC2000  CALL      PINV0000                 * Process Invoice Date Range
.
.
PROC3000  BRANCH    OPTION,PROC3100,PROC3200,PROC3300,PROC3400
.
PROC3100  MOVE      INDEX2,INDEX 
          OPEN      TEMPFIL3,TMPFNAM1        * Report Top Bed Days
          CALL      RDAY0000
          GOTO      PROC9999                 * Exit routine
          
PROC3200  MOVE      INDEX3,INDEX 
          OPEN      TEMPFIL4,TMPFNAM1        * Report Top Discharges
          CALL      RDSC0000                 
          GOTO      PROC9999                 * Exit routine
.
PROC3300  MOVE      INDEX4,INDEX 
          OPEN      TEMPFIL5,TMPFNAM1        * Report Top Revenue  
          CALL      RREV0000                
          GOTO      PROC9999                 * Exit routine
.
PROC3400  CALL      ADRG0000                 * Report All DRG's
.
PROC9999  RETURN
+
.****************************************************************************
.*                              PDIS0000               Called by: PROC0000  *
.*                     Process Discharge Date Range                         *
.****************************************************************************
PDIS0000  PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2                 * Positional read on patdschf
PDIS1000  CALL      RDKDSCH2                 * Read next record on patdschf
          BRANCH    OVRCD,PDIS9999           * EOF, exit routine
.
          MATCH     DDATE,TODATE             * End Date after Discharge Date
          GOTO      PDIS9999 IF LESS         * Yes, exit routine
.
          COMPARE   ONE,IBCNMHOS             * check if using multihospital
          GOTO      PDIS1500 IF NOT EQUAL
.
          PACK      PTHSHOSP,PTHSHOSP,SP70
          MATCH     SP10,PTHSHOSP
          GOTO      PDIS1500 IF EQUAL
.
          MOVE      DADMNO,KEY8              * get hospital from visit extn.
          CALL      RDPMVX11
          BRANCH    OVRCD,PDIS1000
.
          MATCH     PMVXMHOS,PTHSHOSP        * check if we want this record
          GOTO      PDIS1000 IF NOT EQUAL
.
PDIS1500  UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY    * Unpack Discharge Date
.
          DISPLAY   *P1:24,*EL,*P25:24,"Discharge Date : ":
                    *V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     SP4,PTDSDDRG             * Blank DRG ?
          IF        @EQUAL
            MOVE      UNCODED,PTDSDMDC       * Uncoded MDC Code
            MOVE      UNCODED,PTDSDDRG       * Uncoded DRG Code
          ENDIF
.
          MOVE      PTDSDMDC,CALCMDCN        * MDC Code
          CALL      GDRG0000                 * 
....      MOVE      PTDSDDRG,CALCDRGN        * MDC Code
.
.         Calculate Values to write to Temp file 1
.
          MOVE      ONE,CALCDSCH             * Add 1 to No. of Discharges
          CALL      BDAY0000                 * Calculate No. of Bed Days
          BRANCH    EXIT,BDAY9999
          CALL      FEES0000                 * Calc. Bed, Theatre, Other Fees
          CALL      AREV0000                 * Add Total Revenue
.
          PACK      KEY8,CALCDRGN,PTDSDMDC   * Pack Key8 with MDC and DRG
....      PACK      KEY8,PTDSDDRG,PTDSDMDC   * Pack Key8 with MDC and DRG
          CALL      RDTEMP1                  * MDC and DRG exist ?
          BRANCH    OVRCD,PDIS2000           * No, write new record
.
          CALL      UTMP0000                 * Update the Tempfile1
          GOTO      PDIS1000                 * Read next discharge record
.
PDIS2000  CALL      WTMP0000                 * Update tempfile
          GOTO      PDIS1000                 * Read next discharge record
.
PDIS9999  RETURN
+
.****************************************************************************
.*                              PINV0000               Called by: PROC0000  *
.*                     Process Invoice Date Range                           *
.****************************************************************************
PINV0000  PACK      KEY16,STRTDATE,SP20   
          CALL      RDSINV2                  * Positional read on patinvrf
PINV1000  CALL      RDKINV2                  * Read next record on patinvrf
          BRANCH    OVRCD,PINV9999           * EOF, exit routine
.
          MATCH     PINVDTE,TODATE           * End Date after Discharge Date
          GOTO      PINV9999 IF LESS         * Yes, exit routine
.
          COMPARE   ONE,IBCNMHOS             * check if using multihospital
          GOTO      PINV1500 IF NOT EQUAL
.
          PACK      PTHSHOSP,PTHSHOSP,SP70
          MATCH     SP10,PTHSHOSP
          GOTO      PINV1500 IF EQUAL
.
          MOVE      PINVADM,KEY8             * get hospital from visit extn.
          CALL      RDPMVX11
          BRANCH    OVRCD,PINV1000
.
          MATCH     PMVXMHOS,PTHSHOSP        * check if we want this record
          GOTO      PINV1000 IF NOT EQUAL
.
PINV1500  UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P1:24,*EL,*P25:24,"Invoice Date : ":
                    *V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          PACK      KEY8,PINVADM
          CALL      RDDSCH1                  * Admission No Discharged ?
          IF        OVRCD = ZERO
            MATCH     SP4,PTDSDDRG           * Blank DRG ?
            IF        @EQUAL
              MOVE      UNCODED,PTDSDMDC     * Uncoded MDC Code
              MOVE      UNCODED,PTDSDDRG     * Uncoded DRG Code
            ENDIF
            MOVE      PTDSDMDC,CALCMDCN      * MDC Code
            CALL      GDRG0000               * 
....        MOVE      PTDSDDRG,CALCDRGN      * DRG Code
            CALL      DINV0000               * Calc. Disch. & Invoiced patients
            CALL      AREV0000               * Add Total Revenue
            PACK      KEY8,CALCDRGN,PTDSDMDC * Pack Key8 with MDC and DRG
....        PACK      KEY8,PTDSDDRG,PTDSDMDC * Pack Key8 with MDC and DRG
            CALL      RDTEMP1                * MDC and DRG exist ?
            IF        OVRCD = ONE
              CALL      WTMP0000             * Update tempfile
            ELSE
              CALL      UTMP0000             * Update the Tempfile1
            ENDIF
          ELSE
            CALL      NDIV0000               * Calc. Non Disch. but invoiced pat
            CALL      AREV0000               * Add Total Revenue
            ADD       ONE,REPTINVS           * Number of Invoices
            ADD       CALCBDAY,REPTBDAY      * Add Number of Bed Days
            ADD       CALCBFEE,REPTBFEE      * Total Bed Fees
            ADD       CALCTFEE,REPTTFEE      * Total Theatre Fees
            ADD       CALCOFEE,REPTOFEE      * Total Other Fees
            ADD       CALCTREV,REPTTREV      * Calculate Total Revenue
            CALL      CLER0000               * Clear calculation variables
          ENDIF
.
          GOTO      PINV1000                   * Read next patinvrf record
.
PINV9999  RETURN
+
.****************************************************************************
.*                              GDRG0000               Called by: PDIS0000  *
.*                     Get DRG Code from patpgraf                           *
.*    Steps followed:  Get user's choice of DRG version                     *
.*                     IF HF DRG(choice 5),use AFUNDH & AFNDTB to get       *
.*                     PTHFGRPV and then read patpgraf                      *
.*                     IF HF DRG not set up, use Cat CL with ACLAIM to read *
.*                     PTCDGRPV                                             *
.*                     IF PTCDGRPV for claim code not set up,use system     *
.*                     parameter PTCNDGPV                                   *
.*                                                                          *
.****************************************************************************
GDRG0000  COMPARE  TEN1,VERSNUMB                                     *C-284669
          GOTO     GDRG1000 IF NOT EQUAL
.
          PACK     KEY8,DDADMNO,SP10
          CALL     RDMISS1
          BRANCH   OVRCD,GDRG9999
.
          READ      CONTROLF,HUND24;*121,PTCNDGED
          MATCH     "1",PTCNDGED
          IF        @EQUAL
            OPEN      PMSEDGA1,"pmsedgaf"     * Effective DRG
            OPEN      PMSEDGA2,"pmsedgaf"     * Effective DRG
.
            GOTO      GDRG0800
          ENDIF
.
          MATCH    SP6,AFUNDH
          GOTO     GDRG0500 IF EQUAL
          PACK     KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
          CALL     RDFUND1
          BRANCH   OVRCD,GDRG0500
.
          MATCH    SP3,PTHFGRPV
          GOTO     GDRG0500 IF EQUAL
.
          MOVE     PTHFGRPV,VERSNDRG
          GOTO     GDRG1050
.
GDRG0500  MATCH    SP3,ACLAIM
          GOTO     GDRG0700 IF EQUAL
.
          PACK     KEY5,ANSC,ANSL,ACLAIM,SP10
          CALL     RDCODE1
          BRANCH   OVRCD,GDRG0700
.
          MATCH    SP3,PTCDGRPV
          GOTO     GDRG0700 IF EQUAL
.
          MOVE     PTCDGRPV,VERSNDRG
          GOTO     GDRG1050
.
GDRG0700  MOVE     PTCNDGPV,VERSNDRG
          GOTO     GDRG1050
.                                                                     *C-137125
GDRG0800  CALL     GHFDRG00                  
          BRANCH   EXIT,GDRG0700
.             
          MOVE     KEY3,VERSNDRG
          GOTO     GDRG1050
.
GDRG1000  LOAD     VERSNDRG,VERSNUMB,DRG031,DRG041,DRG042,DRG050,DRG051:
                                     DRG052,DRG060,DRG062,DRG070,DRG080:
                                     SP3,DRG090,DRG0100,DRG0110,DRG0120
.
GDRG1050  PACK     KEY13,DDADMNO,SP1,ONE,VERSNDRG,SP10    * episodeno =1
          CALL     RDPTPGR1
          BRANCH   OVRCD,GDRG9950
.
          MOVE     PTPGNDRG,CALCDRGN
          MOVE     VERSNDRG,CALCDRGV                                  *I-137125
          GOTO     GDRG9999
.
GDRG9950  MOVE     UNCODED,CALCDRGN
          MOVE     SP3,CALCDRGV                                       *I-137125
GDRG9999  RETURN
+
.****************************************************************************
.*                              DINV0000               Called by: PINV0000  *
.*                    Calculate Totals for Discharged Patients              *
.****************************************************************************
DINV0000  PACK      KEY22,PINVADM,PINVNO,SP30
          CALL      RDSDTRN1                 * Postional read patdtraf
DINV0100  CALL      RDKDTRN1                 * Read next patdtraf record
          BRANCH    OVRCD,DINV9999           * EOF, exit routine
.
          MATCH     PINVADM,TADMNO           * Same Admission Number ?
          GOTO      DINV9999 IF NOT EQUAL    * No, read exit routine
.
          MATCH     PINVNO,TREF              * Same Invoice Number ?
          GOTO      DINV9999 IF NOT EQUAL    * No, read next patdtraf record
.
          BRANCH    TRECTYPE,DINV1000,DINV2000,DINV3000,DINV0100,DINV0100:
                             DINV4000,DINV0100,DINV2000
.                           Accom.  , Theatre, Misc.,
.                           Journals
.
          GOTO      DINV0100                 * Read next patdtraf record
.
DINV1000  ADD       TNODAYS,CALCBDAY         * Add Number of Bed Days
          ADD       TPATAMTT,CALCBFEE        * Total Bed Fees
          ADD       PTDTLSPT,CALCBFEE        * Lumpsum patient         *I-47566
          ADD       PTDTLSRB,CALCBFEE        * Lumpsum rebate          *I-47566
          CALL      CRDN0000                 * Check for credit note
          IF        EXIT=0
            ADD       CRDNAMNT,CALCBFEE
          ENDIF
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY    * Unpack Discharge Date
          MOVE      CCENT,ICENT
          MOVE      CYEAR,IYEAR
          MOVE      CMON,IMON
          MOVE      CDAY,IDAY
.
          COMPARE   TTDAY,IDAY               * Same Day ?
          GOTO      DINV0100 IF NOT EQUAL    * No, read next patdtraf record
.
          COMPARE   TTMONTH,IMON             * Same Month ?
          GOTO      DINV0100 IF NOT EQUAL    * No, read next patdtraf record
.
          COMPARE   TTYEAR,IYEAR             * Same Year ?
          GOTO      DINV0100 IF NOT EQUAL    * No, read next patdtraf record
.
          ADD       ONE,CALCDSCH             * Add 1 to No. of Discharges
          GOTO      DINV0100                 * Read next patdtraf record
.
DINV2000  ADD       TPATAMTT,CALCTFEE        * Total Theatre Fees
          CALL      CRDN0000                 * Check for credit note
          BRANCH    EXIT,DINV0100
          ADD       CRDNAMNT,CALCTFEE
          GOTO      DINV0100                 * Read next patdtraf record
.
DINV3000  ADD       TPATAMTT,CALCOFEE        * Total Other Fees
          CALL      CRDN0000                 * Check for credit note
          BRANCH    EXIT,DINV0100
          ADD       CRDNAMNT,CALCOFEE
          GOTO      DINV0100                 * Read next patdtraf record
.
.         Process journals, if required
.
DINV4000  IF        PTCNJDRG <> 1
            GOTO      DINV0100
          ENDIF
          PACK      KEY5,ANSJ,SP1,TTYPE,SP3  * key to code file
          CALL      RDCODE1                  * get codes file record
          BRANCH    OVRCD,DINV0100           * no record, ignore
          CMATCH    ANSA,TCINDC1             * accommodation journal ?
          IF        @EQUAL
            ADD       TPATAMTT,CALCBFEE      * Total Bed Fees
            ADD       PTDTLSPT,CALCBFEE      * Lumpsum patient         *I-47566
            ADD       PTDTLSRB,CALCBFEE      * Lumpsum rebate          *I-47566
            GOTO      DINV0100
          ENDIF
          CMATCH    ANST,TCINDC1             * theatre journal ?
          IF        @EQUAL
            ADD       TPATAMTT,CALCTFEE      * Total Theatre Fees
            GOTO      DINV0100
          ENDIF
          CMATCH    ANSO,TCINDC1             * other journal ?
          IF        @EQUAL
            ADD       TPATAMTT,CALCOFEE      * Total Other Fees
            GOTO      DINV0100
          ENDIF
          GOTO      DINV0100
.
DINV9999  RETURN
+
.****************************************************************************
.*                              NDIV0000               Called by: PINV0000  *
.*         Calculate Totals for Non Discharged but invoiced Patients        *
.****************************************************************************
NDIV0000  PACK      KEY22,PINVADM,PINVNO,SP20
          CALL      RDSDTRN1                 * Postional read patdtraf
NDIV0100  CALL      RDKDTRN1                 * Read next patdtraf record
          BRANCH    OVRCD,NDIV9999           * EOF, exit routine
.
          MATCH     PINVADM,TADMNO           * Same Admission Number ?
          GOTO      NDIV9999 IF NOT EQUAL    * No, read exit routine
.
          MATCH     PINVNO,TREF              * Same Invoice Number ?
          GOTO      NDIV9999 IF NOT EQUAL    * No, read next patdtraf record
.
          BRANCH    TRECTYPE,NDIV1000,NDIV2000,NDIV3000,NDIV0100,NDIV0100:
                             NDIV4000,NDIV0100,NDIV2000
.                           Accom.  , Theatre, Misc.,
.                           Journals
          GOTO      NDIV0100                 * Read next patdtraf record
.
NDIV1000  ADD       TNODAYS,CALCBDAY         * Add Number of Bed Days
          ADD       TPATAMTT,CALCBFEE        * Total Bed Fees
          ADD       PTDTLSPT,CALCBFEE        * Lumpsum patient         *I-47566
          ADD       PTDTLSRB,CALCBFEE        * Lumpsum rebate          *I-47566
          CALL      CRDN0000                 * Check for credit note
          BRANCH    EXIT,NDIV0100
          ADD       CRDNAMNT,CALCBFEE
          GOTO      NDIV0100                 * Read next patdtraf record
.
NDIV2000  ADD       TPATAMTT,CALCTFEE        * Total Theatre Fees
          CALL      CRDN0000                 * Check for credit note
          BRANCH    EXIT,NDIV0100
          ADD       CRDNAMNT,CALCTFEE
          GOTO      NDIV0100                 * Read next patdtraf record
.
NDIV3000  ADD       TPATAMTT,CALCOFEE        * Total Other Fees
          CALL      CRDN0000                 * Check for credit note
          BRANCH    EXIT,NDIV0100
          ADD       CRDNAMNT,CALCOFEE
          GOTO      NDIV0100                 * Read next patdtraf record
.
.         Process journals, if required
.
NDIV4000  IF        PTCNJDRG <> 1
            GOTO      NDIV0100
          ENDIF
          PACK      KEY5,ANSJ,SP1,TTYPE,SP3  * key to code file
          CALL      RDCODE1                  * get codes file record
          BRANCH    OVRCD,NDIV0100           * no record, ignore
          CMATCH    ANSA,TCINDC1             * accommodation journal ?
          IF        @EQUAL
            ADD       TPATAMTT,CALCBFEE      * Total Bed Fees
            ADD       PTDTLSPT,CALCBFEE      * Lumpsum patient         *I-47566
            ADD       PTDTLSRB,CALCBFEE      * Lumpsum rebate          *I-47566
            GOTO      NDIV0100
          ENDIF
          CMATCH    ANST,TCINDC1             * theatre journal ?
          IF        @EQUAL
            ADD       TPATAMTT,CALCTFEE      * Total Theatre Fees
            GOTO      NDIV0100
          ENDIF
          CMATCH    ANSO,TCINDC1             * other journal ?
          IF        @EQUAL
            ADD       TPATAMTT,CALCOFEE      * Total Other Fees
            GOTO      NDIV0100
          ENDIF
          GOTO      NDIV0100
.
NDIV9999  RETURN
+
.****************************************************************************
.*                              BDAY0000               Called by: PDIS0000  *
.*                         Calculate Bed Days                               *
.*    Returns: EXIT     = FALSE (0)  Valid Date Continue with processing    *
.*                        TRUE  (1)  DADMNO not on patmi1af                 *
.****************************************************************************
BDAY0000  PACK      KEY8,DADMNO              * Set Up Key for patmi1af
          CALL      RDMISS1                  * Read patmi1af
          BRANCH    OVRCD,BDAY1000           * Record does not exist, exit
.
          MOVE      ADATE,FROMDATE           * from date, todate=end of period
          CALL      RTIODAYS                 * Calculate Bed Days
.
          MOVE      NBRDAYS,CALCBDAY
.
          MOVE      FALSE,EXIT
          GOTO      BDAY9999
.
BDAY1000  DISPLAY   *P1:24,*EL,*B,DADMNO," missing from Admis file. ":
                           "Contact IBA Immeditately. ";
                    CALL   HITENTER
          MOVE      TRUE,EXIT
.
BDAY9999  RETURN
+
.****************************************************************************
.*                              DFEE0000               Called by: PDIS0000  *
.*        Calculate Bed, Theatre, and Other fees for discharged patients    *
.****************************************************************************
FEES0000  PACK      KEY22,DADMNO,SP30        * Set Up key for patdtraf
          CALL      RDSDTRN1                 * Positional read on patdtraf
FEES0100  CALL      RDKDTRN1                 * Read next patdtraf record
          BRANCH    OVRCD,FEES9999           * EOF, exit routine
.
          MATCH     DADMNO,TADMNO            * Same Admission Number ?
          GOTO      FEES9999 IF NOT EQUAL    * No.
.
          MATCH     "00000000",TREF          * Blank Invoice Number ?
          GOTO      FEES0100 IF EQUAL        * Yes. Read next patdtraf record
.
          BRANCH    TRECTYPE,FEES1000,FEES2000,FEES3000,FEES0100,FEES0100:
                             FEES4000,FEES0100,FEES2000
.                           Accom.  , Theatre, Misc.,
.                           Journals
          GOTO      FEES0100                 * Read next patdtraf record
.
FEES1000  ADD       TPATAMTT,CALCBFEE        * Add Bed Fees
          ADD       PTDTLSPT,CALCBFEE        * Lumpsum patient         *I-47566
          ADD       PTDTLSRB,CALCBFEE        * Lumpsum rebate          *I-47566
          CALL      CRDN0000                 * Check for credit note
          BRANCH    EXIT,FEES0100
          ADD       CRDNAMNT,CALCBFEE
          GOTO      FEES0100                 * Read next patdtraf record
.
FEES2000  ADD       TPATAMTT,CALCTFEE        * Add Theatre Fees
          CALL      CRDN0000                 * Check for credit note
          BRANCH    EXIT,FEES0100
          ADD       CRDNAMNT,CALCTFEE
          GOTO      FEES0100                 * Read next patdtraf record
.
FEES3000  ADD       TPATAMTT,CALCOFEE        * Add Other Fees
          CALL      CRDN0000                 * Check for credit note
          BRANCH    EXIT,FEES0100
          ADD       CRDNAMNT,CALCOFEE
          GOTO      FEES0100                 * Read next patdtraf record
.
.         Process journals, if required
.
FEES4000  IF        PTCNJDRG <> 1
            GOTO      FEES0100
          ENDIF
          PACK      KEY5,ANSJ,SP1,TTYPE,SP3  * key to code file
          CALL      RDCODE1                  * get codes file record
          BRANCH    OVRCD,FEES0100           * no record, ignore
          CMATCH    ANSA,TCINDC1             * accommodation journal ?
          IF        @EQUAL
            ADD       TPATAMTT,CALCBFEE      * Total Bed Fees
            ADD       PTDTLSPT,CALCBFEE      * Lumpsum patient         *I-47566
            ADD       PTDTLSRB,CALCBFEE      * Lumpsum rebate          *I-47566
            GOTO      FEES0100
          ENDIF
          CMATCH    ANST,TCINDC1             * theatre journal ?
          IF        @EQUAL
            ADD       TPATAMTT,CALCTFEE      * Total Theatre Fees
            GOTO      FEES0100
          ENDIF
          CMATCH    ANSO,TCINDC1             * other journal ?
          IF        @EQUAL
            ADD       TPATAMTT,CALCOFEE      * Total Other Fees
            GOTO      FEES0100
          ENDIF
          GOTO      FEES0100
.
FEES9999  RETURN
+
.******************************************************************************
.*                                  CRDN0000              Called by: FEES0000 *
.******************************************************************************
CRDN0000  MOVE      ONE,EXIT
          MOVE      ZERO,CRDNAMNT       * Total amount of credit notes
.
          PACK      KEY16,TREF,SP70
          CALL      RSPMCNO3
CRDN1000  CALL      RKPMCNO3
          BRANCH    OVRCD,CRDN9999
          MATCH     TREF,PMCNINVN
          GOTO      CRDN9999 IF NOT EQUAL
.
          PACK      KEY30,DTADMNO,TREF,TTRANSN1,PMCNCRNO
          CALL      RDPMFCI1
          BRANCH    OVRCD,CRDN9999           * No credit note found
          ADD       PMFCCAMT,CRDNAMNT
.
          MOVE      ZERO,EXIT
          GOTO      CRDN1000
.
CRDN9999  RETURN
+
.****************************************************************************
.*                              AREV0000               Called by: PDIS0000  *
.*                        Calculate Total Revenue                           *
.****************************************************************************
AREV0000  ADD       CALCBFEE,CALCTREV        * Total Bed Fees
          ADD       CALCTFEE,CALCTREV        * Total Theatre Fees
          ADD       CALCOFEE,CALCTREV        * Total Other Fees
.
AREV9999  RETURN
+
.****************************************************************************
.*                              RDAY0000               Called by: PROC0000  *
.*                       Report Top DRG based on Bed Days                   *
.****************************************************************************
RDAY0000  MOVE      ZERO,COUNT               * Initailize Count
.
          COMPARE   FALSE,ASCIFILE           * Writing to ASCII file
          CALL      HEAD0000 IF EQUAL        * No, print report header
.
          MOVE      "zzzzzzzzzzzzzzzz",KEY16
          CALL      RSTEMP3                  * Positional read on tempfile2
RDAY1000  CALL      RPTEMP3                  * Read previous tempfile2 record 
          BRANCH    OVRCD,RDAY9000           * EOF, exit routine
.
          CALL      GRAT0000                 * Generate Report or Ascii file
          GOTO      RDAY1000                 * Read previous tempfile2 record
.
RDAY9000  COMPARE   FALSE,ASCIFILE           * Writing to ASCII file
          IF        @EQUAL
            CALL      REND0000               * No, print end of report
          ELSE
            CALL      AEND0000               * Yes, display end of Ascii
          ENDIF
.
          CALL      CREP0000                 * Clear Totals
.
RDAY9999  RETURN
+
.****************************************************************************
.*                              RDSC0000               Called by: PROC0000  *
.*                       Report Top DRG based on Discharges                 *
.****************************************************************************
RDSC0000  MOVE      ZERO,COUNT               * Initailize Count
.
          COMPARE   FALSE,ASCIFILE           * Writing to ASCII file
          CALL      HEAD0000 IF EQUAL        * No, print report header
.
          MOVE      "zzzzzzzzzzzzzzzz",KEY16
          CALL      RSTEMP4                  * Positional read on tempfile3
RDSC1000  CALL      RPTEMP4                  * Read previous tempfile3 record
          BRANCH    OVRCD,RDSC9000           * EOF, exit routine
.
          CALL      GRAT0000                 * Generate Report or Ascii file
          GOTO      RDSC1000                 * Read previous tempfile3 record
.
RDSC9000  COMPARE   FALSE,ASCIFILE           * Writing to ASCII file
          IF        @EQUAL
            CALL      REND0000               * No, print end of report
          ELSE
            CALL      AEND0000               * Yes, display end of Ascii
          ENDIF
.
          CALL      CREP0000                 * Clear Totals
.
RDSC9999  RETURN
+
.****************************************************************************
.*                              RREV0000               Called by: PROC0000  *
.*                       Report Top DRG based on Total Revenue              *
.****************************************************************************
RREV0000  MOVE      ZERO,COUNT               * Initailize Count
.
          COMPARE   FALSE,ASCIFILE           * Writing to ASCII file
          CALL      HEAD0000 IF EQUAL        * No, print report header
.
          MOVE      "zzzzzzzzzzzzzzzzzzz",KEY19
          CALL      RSTEMP5                  * Positional read on tempfile4
RREV1000  CALL      RPTEMP5                  * Read previous tempfile4 record
          BRANCH    OVRCD,RREV9000           * EOF, exit routine
.
          CALL      GRAT0000                 * Generate Report or Ascii file
          GOTO      RREV1000                 * Read previous tempfile4 record
.
RREV9000  COMPARE   FALSE,ASCIFILE           * Writing to ASCII file
          IF        @EQUAL
            CALL      REND0000               * No, print end of report
          ELSE
            CALL      AEND0000               * Yes, display end of Ascii
          ENDIF
.
          CALL      CREP0000                 * Clear Totals
.
RREV9999  RETURN
+
.****************************************************************************
.*                              ADRG0000               Called by: PROC0000  *
.*                     Generate all DRG'S                                   *
.****************************************************************************
ADRG0000  MOVE      ZERO,COUNT               * Initailize Count
          MOVE      TRUE,FIRST
.
          COMPARE   FALSE,ASCIFILE         * Writing to ASCII file
          CALL      HEAD0000 IF EQUAL      * No, print report header
.
          PACK      KEY8,SP8
          CALL      RSTEMP2                  * Positional read on tempfile1
ADRG1000  CALL      RKTEMP2                  * Read next tempfile1 record
          BRANCH    OVRCD,ADRG9000           * EOF, exit routine
.
          COMPARE   FALSE,ASCIFILE           * Writing to ASCII file
          GOTO      ADRG2000 IF NOT EQUAL    * Yes, Generate Ascii file
.
          IF        FIRST = TRUE
            MOVE      FALSE,FIRST
            MATCH     UNCODED,TEMPMDCN       * Uncoded MDC
            IF        @EQUAL
              PRINT     *1,"MDC = ",*1,"MDC = No MDC Allocated" 
            ELSE
              PRINT     *1,"MDC = ",*1,"MDC = ",TEMPMDCN 
            ENDIF
            ADD       ONE,CLNO
            CALL      HEDD0000               * Print Report header
            CALL      GRAT0000               * Generating Report or Ascii file
            MOVE      TEMPMDCN,SAVEMDCN      * Save MDC Code
            MOVE      ONE,NUMBMDC            * Accumulate Number of MDC's
            MOVE      ONE,TOTLMDC            * Total Number of MDC's
          ELSE
            MATCH     TEMPMDCN,SAVEMDCN      * Same MDC Code ?
            IF        @EQUAL
              CALL      GRAT0000             * Yes,Generate Report or Ascii file
              ADD       ONE,NUMBMDC          *     Accumulate Number of MDC's
            ELSE
              MATCH     UNCODED,SAVEMDCN     * Uncoded MDC
              IF        @EQUAL
                PRINT     *1,"Number of Unallocated MDC = ",NUMBMDC,*N
              ELSE
                PRINT     *1,"Number of ",SAVEMDCN," = ",NUMBMDC,*N
              ENDIF
              ADD       TWO,CLNO
              MOVE      TEMPMDCN,SAVEMDCN
              MATCH     UNCODED,TEMPMDCN     * Uncoded MDC
              IF        CLNO>=49
                CALL      HEAD0000           * Print header
              ELSE
                IF        @EQUAL
                  PRINT     *1,"MDC = ",*1,"MDC = No MDC Allocated" 
                ELSE
                  PRINT     *1,"MDC = ",*1,"MDC = ",TEMPMDCN 
                ENDIF
                ADD       ONE,CLNO
                CALL      HEDD0000           * Report header
              ENDIF
              CALL      GRAT0000             * Generate Report or Ascii file
              MOVE      ONE,NUMBMDC
              ADD       ONE,TOTLMDC          * Total Number of MDC's
            ENDIF
          ENDIF
          GOTO      ADRG1000                 * Read previous tempfile2 record
.
ADRG2000  CALL      GRAT0000                 * Generate Ascii file
          GOTO      ADRG1000
.
ADRG9000  COMPARE   FALSE,ASCIFILE           * Writing to ASCII file 
          IF        @EQUAL
            MATCH     UNCODED,TEMPMDCN       * Uncoded MDC
            IF        @EQUAL
              PRINT     *1,"Number of Unallocated MDC = ",NUMBMDC,*N
            ELSE
              PRINT     *1,"Number of ",SAVEMDCN," = ",NUMBMDC
            ENDIF
            ADD       TWO,CLNO
            CALL      REND0000               * No, print end of report
          ELSE
            CALL      AEND0000               * Yes, display end of Ascii
          ENDIF
          CALL      CREP0000                 * Clear Totals
.
ADRG9999  RETURN
+
.****************************************************************************
.*                              GRAT0000               Called by: RDAY0000  *
.*                     Generate Report or Ascii file              RDSC0000  *
.*                                                                RREV0000  *
.****************************************************************************
GRAT0000  MATCH     TEMPDRGN,SAVEDRG         * Same DRG as previous
          IF        !@EQUAL
            ADD     ONE,TOTLDRG              * No, add to total number of DRG's
            ADD     ONE,COUNT                * Add to Count
            MOVE    TEMPDRGN,SAVEDRG         * Save DRG Code
          ENDIF
.
          COMPARE   FOUR,OPTION              * Option = 4 ?
          GOTO      GRAT1000 IF EQUAL        * Yes, process all DRG's
.
          COMPARE   COUNT,NUMBDRG            * Count < Number of DRG's ?
          GOTO      GRAT9999 IF LESS         * No, exit routine
.
GRAT1000  IF        TRUE = ASCIFILE
            DISPLAY   *P1:24,*EL,*P25:24,*V2LON,"Generating Ascii File" 
            CALL      DATA0000               * Generate Data
            CALL      WASC0000               * Write to Ascii file
          ELSE
            DISPLAY   *P1:24,*EL,*P25:24,*V2LON,"Generating Report" 
            COMPARE   FIFTY5,CLNO            * Number of lines > 55
            CALL      HEAD0000 IF NOT LESS   * Yes, printer header
            CALL      DATA0000               * Collect/Format Data
            CALL      PRNT0000               * Print Details
          ENDIF
.
GRAT9999  RETURN
.****************************************************************************
.*                              DATA0000               Called by: RDAY0000  *
.*                        Calculate/Format Data                   RDSC0000  *
.*                                                                RREV0000  *
.*                                                                RREV0000  *
.*                                                                ADRG0000  *
.****************************************************************************
DATA0000  MATCH     UNCODED,TEMPDRGN 
          IF        @EQUAL
            MOVE      "No DRG Allocated ",PTDWDESC    * DRG has not been coded
            MOVE      ZERO,PTDWRWGT
          ELSE
            PACK      KEY7,TEMPDRGN,TEMPDRGV                          *C-137125
            CALL      RDPTDGW1                 * Read on patdwgaf
            IF        OVRCD = ONE
              MOVE      "Unknown DRG Code",PTDWDESC     * No
              MOVE      ZERO,PTDWRWGT
            ENDIF
          ENDIF
.
          ADD       TEMPDSCH,TOTLDSCH        * Total Number of Discharges 
          ADD       TEMPBDAY,TOTLBDAY        * Total Number of Bed Days
          ADD       PTDWRWGT,TOTLRWGT        * Total Resource Weight
          ADD       TEMPBFEE,TOTLBFEE        * Total Bed Fees
          ADD       TEMPTFEE,TOTLTFEE        * Total Theatre fees invoiced
          ADD       TEMPOFEE,TOTLOFEE        * Total Other fees invoiced
          ADD       TEMPTREV,TOTLTREV        * Total Revenue
.
          MOVE      TEMPDSCH,WGHTEQIV        * Calcuate weighted equivalent
          MULT      PTDWRWGT,WGHTEQIV
.
DATA9999  RETURN
+
.****************************************************************************
.*                              WASC0000               Called by: RDAY0000  *
.*                         Write to ASCII file                    RDSC0000  *
.*                                                                RREV0000  *
.*                                                                ADRG0000  *
.****************************************************************************
WASC0000  MOVE      TEMPDRGN,XXASDRGN        * DRG Number
..        MOVE      TEMPDRGV                 * DRG Version            *I-137125
          MOVE      TEMPMDCN,XXASMDCN        * MDC Number
          MOVE      PTDWDESC,XXASDESC        * DRG Description
          MOVE      PTDWRWGT,XXASDRGW        * DRG Weighting
          MOVE      TEMPDSCH,XXASDSCH        * No. of Discharges
          MOVE      TEMPBDAY,XXASBDAY        * No. of Bed Days
          MOVE      WGHTEQIV,XXASWEQV        * Weighted Eqivalent
          MOVE      TEMPBFEE,XXASBFEE        * Bed Fee
          MOVE      TEMPTFEE,XXASTFEE        * Theatre Fee
          MOVE      TEMPOFEE,XXASOFEE        * Other Fee
          MOVE      TEMPTREV,XXASTREV        * Total Revenue
.
          CALL      WRASCII1
.
WASC9999  RETURN
.
.****************************************************************************
.*                              HEAD0000               Called by: RDAY0000  *
.*                         Print Report Header                    RDSC0000  *
.*                                                                RREV0000  *
.*                                                                ADRG0000  *
.****************************************************************************
HEAD0000  LOAD      DIM35,OPTION,TOPBDAY,TOPDSCH,TOPREV,ALLDRGS
          CALL      HEAD132
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME
            ADD       ONE,CLNO
          ENDIF
          PRINT     *40,DIM35
          ADD       ONE,CLNO
.
          LOAD      DATERNG,COPTION,DISCHRGE,INVOICE
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          UNPACK    TODATE,XCENT,XYEAR,XMON,XDAY
.
.                                                                     *I-137125
          MOVE      "HF DRG",VERSDESC
          LOAD      VERSDESC,VERSNUMB,DRG31,DRG41,DRG42,DRG50,DRG51:
                                      DRG52,DRG60,DRG62,DRG70,DRG80:  *C-284669
                                      DRGHF,DRG90,DRG100,DRG110,DRG120
          MATCH     "HF DRG",VERSDESC
          IF        @EQUAL
            MOVE      "Using Health Fund DRG Version",DIM35
          ELSE
            CLEAR     DIM35
            APPEND    "Using DRG Version ",DIM35
            APPEND    VERSDESC,DIM35
            RESET     DIM35
          ENDIF
.
          PRINT     *40,DATERNG," Date from ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                                 " to ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          ADD       ONE,CLNO
.
          PRINT     *40,DIM35
          ADD       ONE,CLNO
.
          COMPARE   FOUR,OPTION
          IF        !@EQUAL
            PRINT     *40,"Number of DRG's Reported = ",NUMBDRG
            ADD       ONE,CLNO
            CALL      HEDD0000
          ELSE
            IF        FIRST = FALSE
              MATCH     UNCODED,TEMPMDCN       * Uncoded MDC
              IF        @EQUAL
                PRINT     *1,"MDC = ",*1,"MDC = No MDC Allocated" 
              ELSE
                PRINT     *1,"MDC = ",*1,"MDC = ",TEMPMDCN 
              ENDIF
              ADD       ONE,CLNO
              CALL      HEDD0000
            ENDIF
          ENDIF
.
HEAD9999  RETURN
+
.****************************************************************************
.*                              HEDD0000               Called by: RDAY0000  *
.*                         Print Header Details                   RDSC0000  *
.*                                                                RREV0000  *
.*                                                                ADRG0000  *
********************************************************************************
HEDD0000  CALL      UND132
          PRINT     *1,"|DRG":               * Top Line
                    *63,"|DRG Weighting":
                    *78,"|Weighted Eqv":
                    *93,"|Bed Days":
                    *105,"|Theatre Fee":
                    *118,"|Total Rev.":
                    *132,"|"
          ADD       ONE,CLNO
          PRINT     *1,"|Description":       * Second Line
                    *63,"|Discharges":
                    *78,"|":
                    *93,"|Bed Fees":
                    *105,"|Other Fees":
                    *118,"|":
                    *132,"|"
          ADD       ONE,CLNO
          CALL      UND132
HEDD9999  RETURN
.****************************************************************************
.*                              PRNT0000               Called by: RDAY0000  *
.*                         Print Report Details                   RDSC0000  *
.*                                                                RREV0000  *
.*                                                                ADRG0000  *
.****************************************************************************
PRNT0000  MATCH     UNCODED,TEMPDRGN
          IF        @EQUAL
            PRINT     *1,"|";                * Uncoded DRG Code
          ELSE
            PRINT     *1,"|",TEMPDRGN;       * DRG Code
          ENDIF
.
          PRINT     *63,"|",PTDWRWGT:        * DRG Weighting
                    *78,"|",WGHTEQIV:        * Weighting Equivalent
                    *93,"|",TEMPBDAY:        * Number of Bed Days
                    *105,"|",TEMPTFEE:       * Theatre Fee
                    *118,"|",TEMPTREV:       * Total Revenue
                    *132,"|"
          ADD       ONE,CLNO
.
          PRINT     *1,"|",PTDWDESC:         * DRG Description 
                    *63,"|",TEMPDSCH:        * Number of Discharges
                    *78,"|":
                    *93,"|",TEMPBFEE:        * Bed Fees
                    *105,"|",TEMPOFEE:       * Other Fees
                    *118,"|":
                    *132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
PRNT9999  RETURN
+
.****************************************************************************
.*                              REND0000               Called by: RDAY0000  *
.*                     Print End of Report Details                RDSC0000  *
.*                                                                RREV0000  *
.*                                                                ADRG0000  *
.****************************************************************************
REND0000  MOVE      TOTLDSCH,TOTLWEQV        * Calculate Total Weighted Equiv.
          MULT      TOTLRWGT,TOTLWEQV
.
          CALL      UND132
          PRINT     *1,"| Totals":
                    *63,"|":
                    *78,"|",TOTLWEQV:        * Total Weighted Equivalent
                    *93,"|",TOTLBDAY:        * Total Bed Days
                    *105,"|",TOTLTFEE:       * Total Theatre Fees
                    *118,"|",TOTLTREV:       * Total Revenue
                    *132,"|"
          ADD       ONE,CLNO
          PRINT     *1,"|":
                    *63,"|",TOTLDSCH:        * Total Number of Discharges
                    *78,"|":
                    *93,"|",TOTLBFEE:        * Total Bed Fees
                    *105,"|",TOTLOFEE:       * Total Other Fees
                    *118,"|":
                    *132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
          PRINT     *1,"Total Number of DRG's              :",TOTLDRG
          ADD       ONE,CLNO
.
          IF         OPTION = FOUR
            PRINT     *1,"Total Number of MDC's              :",TOTLMDC
            ADD       ONE,CLNO
          ENDIF
.
          IF        COPTION = TWO
             PRINT   *N,*1,"Totals for non discharged patients",*N:
                     *1,"                          Invoices : ",REPTINVS,*N:
                     *1,"                          Bed Days : ",REPTBDAY,*N:
                     *1,"                          Revenue  :$",REPTTREV,*N
            ADD      SIX,CLNO
          ENDIF
          PRINT      *1,"***** End of Report *****"

.
          DISPLAY   *P1:24,*EL,*P20:24,*B,*V2LON,"Report Generated. ";
          CALL      HITENTER
.
REND9999  RETURN
+
.****************************************************************************
.*                              AEND0000               Called by: RDAY0000  *
.*                     Print End of Ascii Details                 RDSC0000  *
.*                                                                RREV0000  *
.*                                                                ADRG0000  *
.****************************************************************************
AEND0000  DISPLAY   *P1:24,*EL,*P20:24,*B,*V2LON,"Ascii File Generated. ";
          CALL      HITENTER
.
AEND9999  RETURN
+
.****************************************************************************
.*                              UTMP0000               Called by: PDIS0000  *
.*                         Update the Temp file                             *
.****************************************************************************
UTMP0000  MOVE      CALCMDCN,TEMPMDCN        * MDC Number
          MOVE      CALCDRGN,TEMPDRGN        * DRG Number
          MOVE      CALCDRGV,TEMPDRGV        * DRG Version            *I-137125
          ADD       CALCDSCH,TEMPDSCH        * Discharged for DRG Code
          ADD       CALCBDAY,TEMPBDAY        * No of Bed Days for DRG Code
          ADD       CALCBFEE,TEMPBFEE        * Total Bed fees invoiced
          ADD       CALCTFEE,TEMPTFEE        * Total Theatre fees invoiced
          ADD       CALCOFEE,TEMPOFEE        * Total Other fees invoiced
          ADD       CALCTREV,TEMPTREV        * Bed + Theatre + Other Fees
          MOVE      CALCDRGW,TEMPDRGW        * DRG Weighting
          CALL      UPTEMP1                  * Update tempfile
          CALL      CLER0000                 * Clear calculation variables
.
UTMP9999  RETURN
+
.****************************************************************************
.*                              WTMP0000               Called by: PDIS0000  *
.*                         Write the Tempfile1                              *
.****************************************************************************
WTMP0000  MOVE      CALCMDCN,TEMPMDCN        * MDC Number
          MOVE      CALCDRGN,TEMPDRGN        * DRG Number
          MOVE      CALCDRGV,TEMPDRGV        * DRG Version            *I-137125
          MOVE      CALCDSCH,TEMPDSCH        * Discharged for DRG Code
          MOVE      CALCBDAY,TEMPBDAY        * No of Bed Days for DRG Code
          MOVE      CALCBFEE,TEMPBFEE        * Total Bed fees invoiced
          MOVE      CALCTFEE,TEMPTFEE        * Total Theatre fees invoiced
          MOVE      CALCOFEE,TEMPOFEE        * Total Other fees invoiced
          MOVE      CALCTREV,TEMPTREV        * Bed + Theatre + Other Fees
          MOVE      CALCDRGW,TEMPDRGW        * DRG Weighting
          CALL      WRTEMP1                  * Write tempfile
          CALL      CLER0000                 * Clear calculation variables
.
WTMP9999  RETURN
+
.****************************************************************************
.*                              CTMP0000               Called by: ML0000    *
.*                         Create the temp file                             *
.****************************************************************************
CTMP0000  CALL      DTMP0000                 * Ensure old tempfile is removed
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFNAM1,CMDLINE
          APPEND    " 67 U5-8,1-4 U1-4,5-8",CMDLINE                   *C-137125
.
          BRANCH    OPTION,CTMP1000,CTMP2000,CTMP3000,CTMP5000
          GOTO      CTMP5000
.
CTMP1000  APPEND    INDEX2,CMDLINE
          GOTO      CTMP5000
.
CTMP2000  APPEND    INDEX3,CMDLINE
          GOTO      CTMP5000
.
CTMP3000  APPEND    INDEX4,CMDLINE
.
CTMP5000  RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID           * Execute isbuild
          OPEN      TEMPFIL1,TMPFNAM1        * Open Files
          OPEN      TEMPFIL2,TMPFNAM1        * Open Files
.
CTMP9999  RETURN
+
.****************************************************************************
.*                              DTMP0000               Called by: ML0000    *
.*                         Delete the temp file                   DTMP0000  *
.****************************************************************************
DTMP0000  CLOSE     TEMPFIL1                 * Close tempfile
          CLOSE     TEMPFIL2                 * Close tempfile
          CLOSE     TEMPFIL3                                           *I-52768
          CLOSE     TEMPFIL4                                           *I-52768
          CLOSE     TEMPFIL5                                           *I-52768
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE       * remove tempfile
          APPEND    TMPFNAM1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID           * Execute iserase
.
DTMP9999  RETURN
+
.****************************************************************************
.*                         I/0 Routines for Tempfile                        *
.****************************************************************************
.******************************************** start of changes        *I-137125
RSTEMP1   RESET     KEY8
          READ      TEMPFIL1,KEY8;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMPFIL1,KEY8;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                                  TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFIL1;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMPFIL1;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
WRTEMP1   RESET     KEY8
          MOVE      TEMPDSCH,DTEMPDSC
          MOVE      TEMPBDAY,DTEMPBDA
          MOVE      TEMPTREV,DTEMPTRE
          WRITE     TEMPFIL1,KEY8;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                                  TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          RETURN
.
UPTEMP1   MOVE      TEMPDSCH,DTEMPDSC
          MOVE      TEMPBDAY,DTEMPBDA
          MOVE      TEMPTREV,DTEMPTRE
          UPDATE    TEMPFIL1;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          RETURN
.
RSTEMP2   RESET     KEY8
          READ      TEMPFIL2,KEY8;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMPFIL2;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RSTEMP3   RESET     KEY16
          READ      TEMPFIL3,KEY16;;
          RETURN
.
RDTEMP3   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMPFIL3,KEY16;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                                  TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RKTEMP3   MOVE      ZERO,OVRCD
          READKS    TEMPFIL3;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RPTEMP3   MOVE      ZERO,OVRCD
          READKP    TEMPFIL3;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RSTEMP4   RESET     KEY16
          READ      TEMPFIL4,KEY16;;
          RETURN
.
RDTEMP4   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMPFIL4,KEY16;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                                  TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RKTEMP4   MOVE      ZERO,OVRCD
          READKS    TEMPFIL4;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RPTEMP4   MOVE      ZERO,OVRCD
          READKP    TEMPFIL4;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RSTEMP5   RESET     KEY19
          READ      TEMPFIL5,KEY19;;
          RETURN
.
RDTEMP5   MOVE      ZERO,OVRCD
          RESET     KEY19
          READ      TEMPFIL5,KEY19;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                                  TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RKTEMP5   MOVE      ZERO,OVRCD
          READKS    TEMPFIL5;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.
RPTEMP5   MOVE      ZERO,OVRCD
          READKP    TEMPFIL5;TEMPMDCN,TEMPDRGN,DTEMPDSC,DTEMPBDA,TEMPBFEE:
                             TEMPTFEE,TEMPOFEE,DTEMPTRE,TEMPDRGW,TEMPDRGV
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPDSC,TEMPDSCH
          MOVE      DTEMPBDA,TEMPBDAY
          MOVE      DTEMPTRE,TEMPTREV
          RETURN
.********************************************   end of changes        *I-137125
.
WRASCII1  WRITE     XXASCII1,SEQ;XXASDRGN,XXASCOMA,XXASMDCN,XXASCOMA,XXASDESC:
                                 XXASCOMA,XXASDRGW,XXASCOMA,XXASDSCH,XXASCOMA:
                                 XXASBDAY,XXASCOMA,XXASWEQV,XXASCOMA,XXASBFEE:
                                 XXASCOMA,XXASTFEE,XXASCOMA,XXASOFEE,XXASCOMA:
                                 XXASTREV
          RETURN
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:23,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      "23",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:23,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.
. =========================================================================
. Includes for all file I/O would be included here.
. Includes for all procedural routines would be included here.
. =========================================================================
.
          INC       STD001IO
          INC       PATHSPKY
          INC       PATCFAIO/INC
          INC       PATHSPIO/INC             * pathspaf
          INC       PATDGWIO/INC             * patdgwaf
          INC       PATCODIO/INC             * patcodes
          INC       PATDSCIO/INC             * patdschf
          INC       PATDTRIO/INC             * patdtraf
          INC       PATFX1IO/INC
          INC       PATINVIO/INC             * patinvrf
          INC       PATMI1IO/INC             * patmi1af
          INC       PATTRNIO/INC             * pattranf
          INC       PATPGRIO/INC             * patpgraf     
          INC       PATFN1IO/INC             * patfundf     
          INC       PMSCNOIO/INC             * Credit note summary file
          INC       PMSFCIIO/INC             * Credit note file
          INC       PMSVX1IO/INC             * pmsvx1af
          INC       NHOSPDAY
          INC       RTIODAYS
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
          INC       PMSEDGIO/INC             * Effective DRG Dates
          INC       GETEFDRG

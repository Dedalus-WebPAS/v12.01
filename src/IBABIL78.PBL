.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBABIL78                                             *
.*                                                                            *
.*     FUNCTION:         MBS OPERATION REPORT                                 *
.*                                                                            *
.*     DATE WRITTEN:     28/05/1987                                           *
.*                                                                            *
.*     AUTHOR:           PAUL LO                                              *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*        V12.00.01 21/05/2025  Bella Turco    TSK 0955096                    *
.*                  Alphanumeric changes                                      *
.*        V11.01.01 17/02/2021  Tracey Nguyen  TSK 0888639                    *
.*                  FD changed - patmmbsf.PATMMBS1 from KEY10 to KEY11        *
.******************************************************************************
.*        V11.00.02 29/04/2020  J.Tan          TSK 0838262                    *
.*                  Fixed validating MBS item for the date                    *
.*        V11.00.01 04/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
.*                  Deleted temp file (PSTROL) after processing.              *
.*                  Removed PORT from temp filenames (FNAMEG, FNAMER, FNAMET).*
.******************************************************************************
.*        V10.04.01 23/04/2014  Davin         CAR 299953                      *
.*                  Added hospital id to report (when ibcnmhos=1)             *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.09.00  13/07/2007 Ian Farnell                                    *
.*                  Ported from V9.06 to V9.07                                *
.******************************************************************************
.*        V9.06.01  28/11/2006  Ramesh VK   CAR 126379                        *
.*                  Ported from V6.14 to V9.06 and Recompiled in V9.06        *
.*                  made a change to compiled in V9.06.                       *
.******************************************************************************
.*        V6.07.B01 02/02/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*        V6.06.01  01/11/1999  Greg Horvat                                   *
.*                  Recompiled for changes to PATIOICD                        *
.*        V6.05.03  08/12/1998 Jill Habasque                                  *
.*                  Removed CCENTRY                                           *
.*        V6.05.02  17/11/1998  J.Tan  SRF 629135                             *
.*                  Fixed printing admission and U/R number                   *
.*        V6.05.01  11/11/1998  Nicole Harrington                             *
.*                  Fixed date display                                        *
.*        V6.05.B01 01/07/1998  Davin  605                                    *
.*                  Fixed I*L on tempfile                                     *
.******************************************************************************
.* Release 6.01                                                               *
.*        V6.01.01  22/02/1994  Greg Horvat      SRF 127818                   *
.*                  Fixed so the temp file is erased when exiting the program *
.* Release 6.02                                                               *
.*        V6.02.01  19/09/1994  Gabrielle        SRF 130119                   *
.*                  Fixed calculating the operation average time, to          *
.*                  sum of all theatre times / number of operations           *
.*                  Changed "select field routine" to used standard "MAINQST" *
.*        V6.03.01  11/10/1995  Greg Horvat      SRF 611811                   *
.*                  Changed to use CALCDAYS instead of CALC when calculating  *
.*                  the difference between two dates                          *
.*        V6.03.02  13/10/1995  Greg Horvat      SRF 611811 (Rebound)         *
.*                  Changed NRBDAYS to be a FORM 4                            *
.*        V6.03.03  27/10/1995  Greg Horvat      SRF 612064 612205 612206     *
.*                  Recompiled for changes to NHOSPDAY                        *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATHSPFD/INC
          INC       PATICDFD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC
          INC       HL7BMTFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATMMBFD/INC
          INC       PATTRNFD/INC
.
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         1st Temporary Index file for report
.
GNDFILE   IFILE     SQL, FIXED=9, KEY="u1-8"
.
.Name     Type      Size    Physical    Start
.----     ----      ----    --------    -----
.KEY8     DIM        8         8          1        Admission Number
.End  of Record                           9
.
.         2nd Temporary Index file for report
.
TEMPFILE  IFILE     SQL, FIXED=113, KEY="u1-25"
.
.Name     Type      Size    Physical    Start
.----     ----      ----    --------    -----
.MMCODE   DIM        9         9          1
XDADMNO   DIM        8         8         10
.UNIQUEX  DIM        8         8         18
.PNAME22  DIM        22        22        26
.PURNO    FORM       8         5         48
.PSEX     DIM        1         1         53
.PSAGE    FORM       3         3         54
.DNAME22  DIM        22        22        57
.NBRDAYS  FORM       4         3         79
.PADATE   DIM        8         8         82
.PDDATE   DIM        8         8         90
.OPDATE   DIM        8         8         98
.THEATIM  FORM       4         3        106
.MBS1     FORM       1         2        109
.DAYCASE  FORM       1         2        111
.End  of Record                         113
+
.         *** WORK AREA ***
.
.         Program Flags
.
DAYFLAG   DIM       1
HEADFLG   FORM      "0"
PRIMFLG   DIM       1
PRNFLAG   FORM      1
MBS1FLG   DIM       1
SUMFLAG   DIM       1
.
.         Work Variables
.
CMDLINE   DIM       50
.CSECFEE   FORM      1
COUNT     FORM      8
.
FIELD     FORM      2
FIRST     DIM       1
FIVE5     FORM      "55"
FLINE     DIM       1
FNAMEG    DIM       8
FNAMER    DIM       8
FNAMET    DIM       8
.
ITMDESC   DIM       33                 * Item Code description
.
.
MBS1PAT   FORM      5
MBS1TIM   FORM      8
MBS1      FORM      1
MODE      DIM       1
.
NEWSUB    FORM      1
NODIS     DIM       1
NOOPR     DIM       1
.
PSTROL    FILE      ASCII, FIXED=256
REPTYPE   DIM       22
STATUS    FORM      1
.
.
UNIQUE    FORM      8
UNIQUEX   DIM       8
.
ZTIME     INIT      "00:00"
.
.         more Work Variables
.
DAYCASE   FORM      1          /*  Daycase indicator    */
NBRDAYS   FORM      4          /*  NHOSPDAY  VARIABLES  */
CALDAYS   FORM      4
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
JDAYS     FORM      3
JYEAR     FORM      2
LYEAR     FORM      "366"
OYEAR     FORM      "365"
TODATE    DIM       8
FROMDATE  DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
.
START3    DIM       3

XDIM6     DIM       6
YDIM6     DIM       6
.
START9    DIM       9
END9      DIM       9
.
XDIM9     DIM       9
YDIM9     DIM       9
.
FSTREC    INIT      "Start    "
LSTREC    INIT      "Finish   "
ALLREC    INIT      "All      "
.
PADATE    DIM       8
PDDATE    DIM       8
PFDATE    DIM       10
PTDATE    DIM       10
STARTD    DIM       8
ENDATE    DIM       8
OPDATE    DIM       8
OPRNUM    FORM      2
DISNUM    FORM      2
.
PNAME22   DIM       22
DNAME22   DIM       22
TYPDESC   DIM       25
.
GNDPAT    FORM      6
GNDTIME   FORM      8
GNDITIM   FORM      8
GNDIDC    FORM      8
GNDDDC    FORM      8
GNDPRO    FORM      6
GNDPROI   FORM      6
GNDDD     FORM      6
GNDID     FORM      6
GNDMBS    FORM      8
GNDIBD    FORM      8
GNDDAY    FORM      8
GNDLOS    FORM      5.2
GNDLOSI   FORM      5.2
GAVTHE    FORM      5.2
GAVTHEI   FORM      5.2
GAVTDD    FORM      5.2
GAVTID    FORM      5.2
.
RATIO     FORM      5.2
RATIOI    FORM      5.2
FORM52    FORM      5.2
THEATIM   FORM      4
STRTIME   FORM      4
ENDTIME   FORM      4
MINUTE    FORM      4
.
.PTCNI10D  DIM       8
SUBPAT    FORM      5
SUBPRO    FORM      5
MBS1PRO   FORM      5
SUBDAY    FORM      8
SUBLOS    FORM      5.2
SUBLOSI   FORM      5.2
TMPCAL    FORM      8.2
SINPAT    FORM      5
SUBTIME   FORM      8
SINTIME   FORM      8
DCDTIME   FORM      8
DCITIME   FORM      8
.
AVTHEA    FORM      5.2       * Av.Theatre time
AVTHEI    FORM      5.2       * Av.Indep. Theatre time
AVTDD     FORM      5.2       * Av.(daycase) Theatre time
AVTID     FORM      5.2       * Av.(Indep. Daycase) Theatre time
SUBDD     FORM      5         * No of day cases
SUBID     FORM      5         * No of Indep. Day cases
RATIODD   FORM      5.2       * Ratio (daycase) LOS to Theatre
RATIOID   FORM      5.2       * Ratio (Indep. daycase) LOS to Theatre
CODEDT    INIT      "DT"
.
DIM2A     DIM       2
DIM4      DIM       4
DIM9A     DIM       9
DIM40A    DIM       40
SAVCODE   DIM       9
SAVADM    DIM       8
.
.       Disease/operation variables
.
SAVMCD1   DIM       9
SAVMCD2   DIM       9
SAVMCD3   DIM       9
SAVMCD4   DIM       9
SAVMCD5   DIM       9
SAVMCD6   DIM       9
SAVMCD7   DIM       9
SAVMCD8   DIM       9
SAVMCD9   DIM       9
SAVMCD10  DIM       9
SAVMCD11  DIM       9
SAVMCD12  DIM       9
SAVMCD13  DIM       9
SAVMCD14  DIM       9
SAVMCD15  DIM       9
.
SAVDTY1   DIM       1
SAVDTY2   DIM       1
SAVDTY3   DIM       1
SAVDTY4   DIM       1
SAVDTY5   DIM       1
SAVDTY6   DIM       1
SAVDTY7   DIM       1
SAVDTY8   DIM       1
SAVDTY9   DIM       1
SAVDTY10  DIM       1
SAVDTY11  DIM       1
SAVDTY12  DIM       1
SAVDTY13  DIM       1
SAVDTY14  DIM       1
SAVDTY15  DIM       1
.
SAVDCD1   DIM       9
SAVDCD2   DIM       9
SAVDCD3   DIM       9
SAVDCD4   DIM       9
SAVDCD5   DIM       9
SAVDCD6   DIM       9
SAVDCD7   DIM       9
SAVDCD8   DIM       9
SAVDCD9   DIM       9
SAVDCD10  DIM       9
SAVDCD11  DIM       9
SAVDCD12  DIM       9
SAVDCD13  DIM       9
SAVDCD14  DIM       9
SAVDCD15  DIM       9
.
PRGID     INIT      "IBABIL78"
PRGNAM    INIT      "MBS OPERATION REPORT"
VERSION   INIT      "V12.01.00"
+
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1aff";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"paticddf";
          CALL      OPICD1
.
          DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmmbsf";
          OPEN      PATMMBS1,"patmmbsf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.          READ      CONTROLF,EIGHTY;*1,HLBMDGUP,*2,HLBMDGUA,*105,HLBMDGAD:
.                                    *106,HLBMDGDS,*107,HLBMDGTR,*108,HLBMDGRG
          READ      CONTROLF,TEN;*125,CSECFEE
.
.         MOVE      PORT TO DIM2A
.
.         CLEAR     FNAMER
.         APPEND    "PSTROL",FNAMER
.         APPEND    DIM2A,FNAMER
.         RESET     FNAMER
.         REP       " 0" IN FNAMER
.
.         CLEAR     FNAMET
.         APPEND    "PSTIDX",FNAMET
.         APPEND    DIM2A,FNAMET
.         RESET     FNAMET
.         REP       " 0" IN FNAMET
.
.         CLEAR     FNAMEG
.         APPEND    "GNDIDX",FNAMEG
.         APPEND    DIM2A,FNAMEG
.         RESET     FNAMEG
.         REP       " 0" IN FNAMEG
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEG
+
.
.          START OF PROGRAM
.
.          MAIN REPORT PARAMETERS
.
START0    DISPLAY   *P1:4,*EF:
                    *P1:4,"1. Starting Discharge Date       :":
                    *P1:5,"2. Ending   Discharge Date       :":
                    *P1:7,"3. From Operation Code           :":
                    *P1:8,"4. To   Operation Code           :":
                   *P1:10,"5. Doctor Type                   :":
                   *P1:12,"6. Primary Doctor Type Only (Y/N):":
                   *P1:14,"7. Summary Details Only     (Y/N):":
                   *P1:16,"8. Independent MBS Only     (Y/N):":
                   *P1:18,"9. Day Surgery Only         (Y/N):";
.
START     MOVE      ZERO,CPAGENO
          MOVE      ZERO,HEADFLG
          MOVE      "A",MODE
          MOVE      "60",CLNO
          CALL      ZEROS
          MOVE      ZERO,GNDPAT
          MOVE      ZERO,GNDTIME
          MOVE      ZERO,GNDITIM
          MOVE      ZERO,GNDIDC
          MOVE      ZERO,GNDDDC
          MOVE      ZERO,GNDDD
          MOVE      ZERO,GNDID
          MOVE      ZERO,GNDMBS
          MOVE      ZERO,GNDIBD
          MOVE      ZERO,GNDPRO
          MOVE      ZERO,GNDPROI
          MOVE      ZERO,GNDDAY
          MOVE      ZERO,GNDLOS
          MOVE      ZERO,GNDLOSI
          MOVE      SP8,SAVADM
.
.         Input Starting Discharge Date
.
KSTART    UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      THIRTY6,CCOL
          MOVE      FOUR,CVERT
          CALL      KEYDATE
.
          IF        OVRCD = 0
            GOTO      CHKDAT1
          ELSE
            MATCH     "C",MODE
            GOTO      ENDIT IF NOT EQUAL
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Input is mandatory in Change Mode **    ";
          CALL      HITENTER
          GOTO      KSTART
.
CHKDAT1   PACK      STARTD WITH CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PFDATE
          REP       " 0",STARTD
          RESET     STARTD
.
          MATCH     "C",MODE
          GOTO      KEND IF NOT EQUAL
          MATCH     STARTD,ENDATE
          GOTO      SFIELD IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Starting Date must be < or = Ending Date **    ";
          CALL      HITENTER
          GOTO      KSTART
.
.         Input Ending Discharge Date
.
KEND      UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      THIRTY6,CCOL
          MOVE      FIVE,CVERT
          CALL      KEYDATE
.
          COMPARE   ZERO,OVRCD
          GOTO      CHKDAT2 IF EQUAL
.
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          CALL      PACDATE
          DISPLAY   *P36:5,*V2LON,CPCDATE;
.
CHKDAT2   PACK      ENDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDATE
          RESET     ENDATE
.
          CALL      PACDATE
          MOVE      CPCDATE,PTDATE
.
          MATCH     STARTD,ENDATE
          GOTO      NEXTS IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Date must be > or = Starting Date **    ";
          CALL      HITENTER
          GOTO      KEND
.
NEXTS     MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
.
.         Input From Operation Code
.
KFOPR     KEYIN     *P36:7,*EL,*V2LON,START9;
          RESET     START9
          GOTO      NEXT3 IF NOT EOS
          MOVE      SP9,START9
.
KFOPR1    DISPLAY   *P36:7,*V2LON,FSTREC;
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KTOPR
.
NEXT3     ENDSET    START9
          APPEND    SP9,START9
          RESET     START9
.
          MATCH     SP9,START9
          GOTO      KFOPR1 IF EQUAL
          DISPLAY   *P36:7,*EL,*V2LON,START9;
.
          PACK      KEY17,START9,STARTD,SP70
          CALL      PATITMRD
          BRANCH    OVRCD OF INVCOD
          MOVE      IDESC,ITMDESC
          DISPLAY   *P48:7,ITMDESC;
.
          MATCH     "C",MODE
          GOTO      KTOPR IF NOT EQUAL
          MATCH     SP9,END9
          GOTO      SFIELD IF EQUAL
          MATCH     START9,END9
          GOTO      SFIELD IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** From Code must be < or = To Code **    ";
          CALL      HITENTER
          GOTO      KFOPR
.
INVCOD    DISPLAY   *P1:24,*EL,*B,*V2LON,"** Code does not exist **    ";
          CALL      HITENTER
          GOTO      KFOPR
.
.         Input To Operation Code
.
KTOPR     KEYIN     *P36:8,*EL,*V2LON,END9;
          RESET     END9
          GOTO      NEXT4 IF NOT EOS
          MOVE      SP9,END9
.
KTOPR1    DISPLAY   *P36:8,*V2LON,LSTREC;
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KDOCTYP
.
NEXT4     ENDSET    END9
          APPEND    SP9,END9
          RESET     END9
.
          MATCH     SP9,END9
          GOTO      KTOPR1 IF EQUAL
          DISPLAY   *P36:8,*EL,*V2LON,END9;
.
          PACK      KEY17,END9,STARTD,SP70
          CALL      PATITMRD
          BRANCH    OVRCD OF INVCODX
          MOVE      IDESC,ITMDESC
          DISPLAY   *P48:8,ITMDESC;
.
          MATCH     START9,END9
          GOTO      INVCOD1 IF LESS
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KDOCTYP
.
INVCOD1   DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Code must be > or = From Code **    ";
          CALL      HITENTER
          GOTO      KTOPR
.
INVCODX   DISPLAY   *P1:24,*EL,*B,*V2LON,"** Code does not exist **    ";
          CALL      HITENTER
          GOTO      KTOPR
.
.         Input Doctor Type
.
KDOCTYP   DISPLAY   *P36:10,*EL,*P55:10,"<",*V2LON,"ENTER",*HOFF,"> for All ";
          KEYIN     *P36:10,*V2LON,START3;
          RESET     START3
          GOTO      NEXT5 IF NOT EOS
          MOVE      SP3,START3
.
KDOCTYP1  DISPLAY   *P36:10,*EL,*V2LON,ALLREC;
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KPRIM
.
NEXT5     ENDSET    START3
          APPEND    SP3,START3
          RESET     START3
.
          MATCH     SP3,START3
          GOTO      KDOCTYP1 IF EQUAL
          DISPLAY   *P36:10,*EL,*V2LON,START3;
.
          PACK      KEY5 WITH CODEDT,START3
          CALL      RDCODE1
          BRANCH    OVRCD OF INVCOD2
.
          DISPLAY   *P48:10,TDESC;
.
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KPRIM
.
INVCOD2   DISPLAY   *P1:24,*EL,*B,*V2LON,"** Doctor Type does not exist **    ";
          CALL      HITENTER
          GOTO      KDOCTYP
.
.         Input Doctor's Primary Doctor Type Only or all Types
.
KPRIM     KEYIN     *P36:12,*EL,*V2LON,PRIMFLG;
          RESET     PRIMFLG
          GOTO      NXT01 IF NOT EOS
          MOVE      "N",PRIMFLG
          DISPLAY   *P36:12,*V2LON,PRIMFLG;
          GOTO      NXT03
.
NXT01     MATCH     "Y",PRIMFLG
          GOTO      NXT02 IF EQUAL
.
          MATCH     "N",PRIMFLG
          GOTO      NXT03 IF EQUAL
          BEEP
          GOTO      KPRIM
.
NXT02     MOVE      "PRIMARY DOCTOR TYPE ONLY",TYPDESC
          GOTO      NXT04
.
NXT03     MOVE      "ALL DOCTOR TYPES (2-5)",TYPDESC
.
NXT04     DISPLAY   *P48:12,TYPDESC;
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
.
.         Input Summary Details Only Indicator
.
KSUMM     KEYIN     *P36:14,*EL,*V2LON,SUMFLAG;
          RESET     SUMFLAG
          GOTO      NEXT7 IF NOT EOS
          MOVE      "N",SUMFLAG
          DISPLAY   *P36:14,*V2LON,SUMFLAG;
          GOTO      NEXT7A
.
NEXT7     MATCH     "Y",SUMFLAG
          GOTO      NEXT7A IF EQUAL
.
          MATCH     "N",SUMFLAG
          GOTO      NEXT7A IF EQUAL
          BEEP
          GOTO      KSUMM
.
NEXT7A    MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
.
.         Input Independent MBS Only Indicator
.
KMBS1     MOVE      SP30,REPTYPE
          KEYIN     *P36:16,*EL,*V2LON,MBS1FLG;
          RESET     MBS1FLG
          GOTO      NEXT8 IF NOT EOS
          MOVE      "N",MBS1FLG
          DISPLAY   *P36:16,*V2LON,MBS1FLG;
          GOTO      NEXT8A
.
NEXT8     MATCH     "Y",MBS1FLG
          GOTO      INDPOLY IF EQUAL
.
          MATCH     "N",MBS1FLG
          GOTO      NEXT8A IF EQUAL
          BEEP
          GOTO      KMBS1
.
INDPOLY   MOVE      "(Independent MBS Only)",REPTYPE
.
NEXT8A    MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
.
.         Input Day Surgery Only Indicator
.
KDAYC     KEYIN     *P36:18,*EL,*V2LON,DAYFLAG;
          RESET     DAYFLAG
          GOTO      NEXT9 IF NOT EOS
          MOVE      "N",DAYFLAG
          DISPLAY   *P36:18,*V2LON,DAYFLAG;
          GOTO      NEXT9A
.
NEXT9     MATCH     "Y",DAYFLAG
          GOTO      NEXT9A IF EQUAL
.
          MATCH     "N",DAYFLAG
          GOTO      NEXT9A IF EQUAL
          BEEP
          GOTO      KDAYC
.
NEXT9A    MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
.
KEYHOS    DISPLAY   *P36:18,*V2LON,DAYFLAG;
          CALL      KHOS0000
          BRANCH    EXIT,ENDIT
.
.         Select the Field to update
.
SFIELD    CALL      MAINQST
.......   KEYIN     *P1:24,*EL,"Select field (0 to continue) ? ",*V2LON,FIELD;
.
          MOVE      CCITEM,FIELD
.
          COMPARE   ZERO,FIELD               * Post
          GOTO      PROCESS IF EQUAL
.
          COMPARE   "-1",FIELD               * Cancel
          GOTO      ENDIT IF EQUAL
.
          MOVE      "C",MODE
          BRANCH    FIELD,KSTART,KEND,KFOPR,KTOPR,KDOCTYP,KPRIM,KSUMM:
                          KMBS1,KDAYC,KEYHOS
          DISPLAY   *P40:24,*EL,*B,*V2LON,"** Invalid field **",*W2;
          GOTO      SFIELD
.
.         Ok to Continue ?
.
...CONFIRM   CALL      CONTQST
...          BRANCH    CEXIT,PROCESS,SFIELD,CLRSCR
...          GOTO      CONFIRM
.
.         Start processing
.
PROCESS   CLOSE     TEMPFILE
          CALL      KILLIDX
          CALL      CREAIDX
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE TO UNIQUE
.
          OPEN      GNDFILE,FNAMEG
.
          DISPLAY   *P1:24,*EL,*V2LON,*P25:24,"** PROCESSING DISCHARGE FILE **":
                           *W2,*HOFF,*P1:24,*EL:
                    *P10:24,"Discharged Date :",*P44:24,"Operation Code :";
.
.         Read DISCHARGE file base on Start & End Dates
.
          PACK      KEY16 WITH STARTD,SP6
          CALL      RDSDSCH2
.
LOOP      CALL      RDKDSCH2
          BRANCH    OVRCD OF PRINTR
          UNPACK    DDATE WITH CCENT,CYEAR,CMON,CDAY
.
          MATCH     SP8,ENDATE
          GOTO      NEXT10 IF EQUAL
.
          MATCH     DDATE,ENDATE
          GOTO      PRINTR IF LESS
.
NEXT10    DISPLAY   *P28:24,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR;
          MOVE      DADMNO,KEY8
          CALL      RDMISS1                 * includes read of pmsvx1
          BRANCH    OVRCD OF LOOP
.
.         Multi-Hospital Processing
.
          COMPARE   ONE,IBCNMHOS
          GOTO      LOOP10 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      LOOP10 IF EQUAL
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LOOP IF NOT EQUAL
.
LOOP10    MATCH     "Y",DAYFLAG
          GOTO      CHKDOC IF NOT EQUAL
.
.         Day Surgery Only
.
          MATCH     ADATE,DDATE
          GOTO      LOOP IF NOT EQUAL
.
CHKDOC    MATCH     SP3,START3
          GOTO      NEXT14 IF EQUAL
.
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF LOOP
.
          MATCH     DRTYPE,START3
          GOTO      NEXT14 IF EQUAL
.
          MATCH     "Y",PRIMFLG
          GOTO      LOOP IF EQUAL
.
          MATCH     START3,DRTYPE2
          GOTO      NEXT14 IF EQUAL
.
          MATCH     START3,DRTYPE3
          GOTO      NEXT14 IF EQUAL
.
          MATCH     START3,DRTYPE4
          GOTO      NEXT14 IF EQUAL
.
          MATCH     START3,DRTYPE5
          GOTO      NEXT14 IF EQUAL
          GOTO      LOOP
.
NEXT14    MOVE      DURNO,KEY8
          CALL      RDMAST1
          MOVE      SP30,PNAME22
          MOVE      PGNAME,ANS
          PACK      PNAME22 WITH ANS,DOT,PSNAME
.
          MOVE      SP30,DNAME22
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NODOCA
          MOVE      DGNAME,ANS
          PACK      DNAME22 WITH ANS,DOT,DSNAME
.
NODOCA    MOVE      ADATE,FROMDATE
          MOVE      ADATE,PADATE
.
          PACK      DDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      DDATE,PDDATE
          MOVE      DDATE,TODATE
          CALL      NHOSPDAY
.
.         Determine if this is an independent MBS
.
          MOVE      TWO,MMRECN
          PACK      KEY11 WITH DADMNO,MMRECN
          CALL      RDMMBS1
          MOVE      OVRCD,MBS1
          BRANCH    OVRCD OF INITMLP
.
.         Not an independent MBS - check if we want it or not
.
          MATCH     "Y",MBS1FLG
          GOTO      LOOP IF EQUAL
.
.         Loop through the Medical Records MBS file
.
INITMLP   PACK      KEY11 WITH DADMNO,SP70
          CALL      RDSMMBS1
.
LOOP1     CALL      RDKMMBS1
          BRANCH    OVRCD OF LOOP
.
          MATCH     DADMNO,MMADMN
          GOTO      LOOP IF NOT EQUAL
.
.         Check if less than starting code wanted
.
          MATCH     SP9,START9
          GOTO      NEXT11 IF EQUAL
.
          MATCH     START9,MMCODE
          GOTO      LOOP1 IF LESS
.
.         Check if greater than ending code wanted
.
NEXT11    MATCH     SP9,END9
          GOTO      NEXT12 IF EQUAL
.
          MATCH     MMCODE,END9
          GOTO      LOOP1 IF LESS
.
.         Zero the time variables and check if there is a zero time (0:00)
.
NEXT12    MOVE      ZERO,THEATIM
          MOVE      ZERO,ENDTIME
          MOVE      ZERO,STRTIME
.
          MATCH     ZTIME,MMETIM
          GOTO      NEXT20 IF EQUAL
.
.         Calculate the end time of operation in minutes
.
          UNPACK    MMETIM WITH XHOUR,ANS,XMIN
          MOVE      XHOUR,ENDTIME
          MOVE      XMIN,MINUTE
.
          MULT      "60",ENDTIME
          ADD       MINUTE,ENDTIME
.
.         Check for a zero start time
.
NEXT20    MATCH     ZTIME,MMSTIM
          GOTO      NEXT30 IF EQUAL
.
          COMPARE   ZERO,ENDTIME
          GOTO      NEXT30 IF EQUAL
.
.         Calculate the start time of operation in minutes
.
          UNPACK    MMSTIM WITH XHOUR,ANS,XMIN
          MOVE      XHOUR,STRTIME
          MOVE      XMIN,MINUTE
.
          MULT      "60",STRTIME
          ADD       MINUTE,STRTIME
.
.         Calculate the total amount of time in theatre
.
          MOVE      ENDTIME,THEATIM
          SUB       STRTIME,THEATIM
.
          COMPARE   ZERO,THEATIM
          GOTO      NEXT30 IF NOT LESS
.
.         There are 1440 minutes in a day
.
          ADD       "1440",THEATIM
.
NEXT30    DISPLAY   *P61:24,*V2LON,MMCODE;
.
.         Check if this is a new admission
.
          MOVE      DADMNO,KEY8
          READ      GNDFILE,KEY8;KEY8
          GOTO      SAMEP IF NOT OVER
.
          ADD       NBRDAYS,GNDDAY
          WRITE     GNDFILE,KEY8;KEY8
.
          ADD       ONE,GNDPAT           * Grand total of patients
.
          COMPARE   ONE,MBS1
          GOTO      NEXT40 IF NOT EQUAL   
          ADD       ONE,GNDMBS           * Grand total of indep patients
          ADD       NBRDAYS,GNDIBD       * Grand total of indep.Bed Day
.
NEXT40    MATCH     ADATE,DDATE
          GOTO      SAMEP IF NOT EQUAL
          ADD       ONE,GNDDD            * Grand total of Daycases
.
          COMPARE   ONE,MBS1
          GOTO      SAMEP IF NOT EQUAL
          ADD       ONE,GNDID            * Grand total of Indep.Daycases
.
.         Check if the patient is a daycase
.
SAMEP     MOVE      ZERO,DAYCASE
          MATCH     ADATE,DDATE
          GOTO      SAME10 IF NOT EQUAL
.
          MOVE      ONE,DAYCASE           * Daycase
.
.         Write data to a temporary file
.
SAME10    PACK      KEY25 WITH MMCODE,UNIQUE,DADMNO
          WRITE     TEMPFILE,KEY25;KEY25,PNAME22,PURNO,PSEX,PSAGE:
                                   DNAME22,NBRDAYS,PADATE,PDDATE,MMDATE:
                                   THEATIM,MBS1,DAYCASE
          ADD       ONE TO UNIQUE
          GOTO      LOOP1
.
.         Generate report
.
PRINTR    DISPLAY   *P1:24,*EL,*V2LON,*P29:24,"** Generating Report **",*W2;
          CALL      HEADSET
.
          SUB       ONE,UNIQUE
          DISPLAY   *P1:23,*EF,"Total Records       : ",*V2LON,UNIQUE,*HOFF:
                    *P1:24,"Generating Record No. :",*P35:24,"Operation :":
                    *P60:24,"Admission :";
.
          MOVE      ONE,COUNT
          MOVE      ZERO,PRNFLAG
.
          MOVE      "Y",FIRST
          MOVE      SP30 TO KEY25
.
.         Loop over temporary file
.
          READ      TEMPFILE,KEY25;;
READLOP   READKS    TEMPFILE;MMCODE,UNIQUEX,XDADMNO,PNAME22,PURNO,PSEX:
                             PSAGE,DNAME22,NBRDAYS,PADATE,PDDATE,OPDATE:
                             THEATIM,MBS1,DAYCASE
          GOTO      ENDP IF OVER
          MOVE      UNIQUEX,UNIQUE
          MOVE      XDADMNO,DADMNO
.
          DISPLAY   *P23:24,*V2LON,COUNT,*P47:24,MMCODE,*P72:24,DADMNO;
.
          MATCH     "Y",FIRST
          GOTO      NOTFST IF NOT EQUAL
.
          MOVE      "N",FIRST
          MOVE      MMCODE,SAVCODE
          MOVE      DADMNO,SAVADM
          CALL      SETSUB
          CALL      HEAD
          MOVE      ONE,NEWSUB
          GOTO      SAMEH
.
.         Check for a change in Admission Number
.
NOTFST    MATCH     SAVADM,DADMNO
          GOTO      SAMEAD IF EQUAL
.
          MOVE      DADMNO,SAVADM
          MOVE      ZERO,PRNFLAG
.
.         Check for a change in operation
.
SAMEAD    MATCH     MMCODE,SAVCODE
          GOTO      SAMEH IF EQUAL
.
          CALL      PRTSUB
          MOVE      MMCODE,SAVCODE
          MOVE      DADMNO,SAVADM
.
          CALL      SETSUB
          MOVE      ONE,NEWSUB
.
.         Make sure only the first sub-operation code is counted for number of
.         patients and bed days
.
SAMEH     ADD       THEATIM,SUBTIME      * Theatre time
          ADD       THEATIM,GNDTIME      * Grand total of theatre time
.
.         Add theatre time to Independent MBS totals if MBS1 = 1
.
          COMPARE   ONE,MBS1
          GOTO      CPFLG IF NOT EQUAL
.
          ADD       NBRDAYS,MBS1TIM
          ADD       ONE,MBS1PAT
          ADD       THEATIM,SINTIME      * Indep.theatre time
          ADD       THEATIM,GNDITIM      * Grand total of indep. theatre time
.
CPFLG     COMPARE   ZERO,DAYCASE
          GOTO      CPFLG1 IF EQUAL
.
          ADD       THEATIM,DCDTIME      * Daycase theatre time
          ADD       THEATIM,GNDDDC       * Grand total of daycase theatre time
.
          COMPARE   ONE,MBS1
          GOTO      CPFLG1 IF NOT EQUAL
.
          ADD       THEATIM,DCITIME    * Daycase independent theatre time
          ADD       THEATIM,GNDIDC     * Grand total of daycase indep.thea time
.
CPFLG1    BRANCH    PRNFLAG OF NXTREC
.
.         Loop over MBS file to get get other operation codes
.
          PACK      KEY11 WITH DADMNO,SP70
          CALL      RDSMMBS1
.
CLP1      CALL      RDKMMBS1
          BRANCH    OVRCD OF CKHD
.
          MATCH     DADMNO,MMADMN
          GOTO      CKHD IF NOT EQUAL
.
          MATCH     MMCODE,SAVCODE
          GOTO      CLP1 IF NOT EQUAL
.
.         We now have the first sub-operation code for this operation code
.
          MATCH     MMDATE,OPDATE
          GOTO      CKHD IF NOT EQUAL  * Patient has already been counted
          ADD       ONE,SUBPAT         * No of patients for each operation code
          ADD       NBRDAYS,SUBDAY
.
          COMPARE   ONE,MBS1
          GOTO      CKHD IF NOT EQUAL
          ADD       ONE,SINPAT         * No of independent patients
.
.         Check for a day case
.
CKHD      COMPARE   ZERO,DAYCASE
          GOTO      CKHD1 IF EQUAL
          ADD       ONE,SUBDD            * No of day case
.
          COMPARE   ONE,MBS1
          GOTO      CKHD1 IF NOT EQUAL
          ADD       ONE,SUBID            * No of independent daycase
.
CKHD1     MOVE      SAVCODE,MMCODE       * Restore operation code
.
          CALL      PRINTD1
          MOVE      ONE,PRNFLAG
.
NXTREC    ADD       ONE,COUNT
          GOTO      READLOP
.
.         set sub-heading
.
SETSUB    MOVE      MMCODE,DIM9A
          MOVE      DIM9A,KEY9
          MOVE      "UNKNOWN CODE",IDESC
          MOVE      ZERO,IAMT
          PACK      KEY17,KEY9,PADATE,SP70
          CALL      PATITMRD
          MOVE      IDESC,DIM40A
          RETURN
+
.         OVER Condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOREC IF EQUAL
          CALL      PRTSUB
          COMPARE   FIFTY,CLNO
          GOTO      JUMPD IF LESS
          CALL      HEAD
.
JUMPD     CALL      PRTGND
.
JUMP5     PRINT     *N,*N,"***  End of Report  ***"
          DISPLAY   *P1:23,*EF,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
          CLOSE     TEMPFILE
.         CALL      KILLIDX
          GOTO      CLRSCR
.
NOREC     DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** No Report Generated **",*W2;
          CALL      KILLIDX
          GOTO      CLRSCR
.
.         PRINT ACTUAL DATA
.
PRINTD1   MOVE      "Y",FLINE
          PACK      KEY10,DADMNO,SP10
          MOVE      SP9,PTEDCODE
          MOVE      SP9,MMCODE
          MOVE      SP1,PTEDTYPE
          MOVE      "N",NODIS
          MOVE      "N",NOOPR
          MOVE      ZERO,OPRNUM
          MOVE      ZERO,DISNUM
          CALL      INTVAR
.
          PACK      KEY11,DADMNO,SP10
          CALL      RDSMMBS1
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
.
LOOPX     MATCH     "N",NOOPR
          GOTO      RDDIS IF NOT EQUAL
.
          CALL      RDKMMBS1
          BRANCH    OVRCD OF NOOPRS
.
          MATCH     DADMNO,MMADMN
          GOTO      NOOPRS IF NOT EQUAL
.
          MATCH     "N",NODIS
          GOTO      RDDIS IF EQUAL
          CALL      PRTDET
          GOTO      LOOPX
.
NOOPRS    COMPARE   ZERO,OPRNUM
          GOTO      NOOPR1 IF EQUAL
.
.         Get the other operation codes
.
          MOVE      OPRNUM,FORM2
          LOAD      MMCODE,FORM2,SAVMCD1,SAVMCD2,SAVMCD3,SAVMCD4,SAVMCD5:
                                 SAVMCD6,SAVMCD7,SAVMCD8,SAVMCD9,SAVMCD10:
                                 SAVMCD11,SAVMCD12,SAVMCD13,SAVMCD14,SAVMCD15
          SUB       ONE,OPRNUM
          GOTO      RDDIS
.
NOOPR1    MOVE      "Y",NOOPR
          MOVE      SP9,MMCODE
.
RDDIS     MATCH     "N",NODIS
          GOTO      CHKBOTH IF NOT EQUAL
.
          COMPARE   ZERO,DISNUM
          GOTO      LOOPY IF EQUAL
.
          MOVE      DISNUM,FORM2
          LOAD      PTEDTYPE,FORM2,SAVDTY1,SAVDTY2,SAVDTY3,SAVDTY4,SAVDTY5:
                                   SAVDTY6,SAVDTY7,SAVDTY8,SAVDTY9,SAVDTY10:
                                   SAVDTY11,SAVDTY12,SAVDTY13,SAVDTY14,SAVDTY15
.
          LOAD      PTEDCODE,FORM2,SAVDCD1,SAVDCD2,SAVDCD3,SAVDCD4,SAVDCD5:
                                   SAVDCD6,SAVDCD7,SAVDCD8,SAVDCD9,SAVDCD10:
                                   SAVDCD11,SAVDCD12,SAVDCD13,SAVDCD14,SAVDCD15
          SUB       ONE,DISNUM
          GOTO      LOOPY1
.
LOOPY     CALL      RKPTECD2
          BRANCH    OVRCD,NODISS
.
          MATCH     DADMNO,PTEDADMN
          GOTO      NODISS IF NOT EQUAL
.
LOOPY1    CALL      PRTDET
.
          MATCH     "N",NOOPR
          GOTO      LOOPX IF EQUAL
          GOTO      LOOPY
.
NODISS    MOVE      "Y",NODIS
          MOVE      SP9,PTEDCODE
          MOVE      SP1,PTEDTYPE
.
CHKBOTH   MATCH     "N",NOOPR
          GOTO      ENDPRT IF NOT EQUAL
          CALL      PRTDET
          GOTO      LOOPX
.
ENDPRT    COMPARE   FIFTY,CLNO
          GOTO      JUMPB IF LESS
.
          CALL      HLINE
          CALL      HEAD
          MOVE      ONE,NEWSUB
.
JUMPB     MATCH     "Y",FLINE
          CALL      LNONE IF EQUAL
.
          MATCH     "Y",SUMFLAG
          RETURN    IF EQUAL
.
          CALL      HLINE
          RETURN
+
.
PRTDET    COMPARE   FIFTY,CLNO
          GOTO      JUMPC IF LESS
          CALL      HLINE
          CALL      HEAD
          MOVE      ONE,NEWSUB
.
JUMPC     MATCH     "Y",FLINE
          GOTO      LNONE IF EQUAL
.
          MATCH     MMCODE,DIM9A
          GOTO      JUMPC1 IF NOT EQUAL
          ADD       ONE,SUBPRO
.
          COMPARE   ONE,MBS1
.         GOTO      JUMPCA IF NOT EQUAL
          GOTO      JUMPC1 IF NOT EQUAL
          ADD       ONE,MBS1PRO
.
.JUMPCA    MATCH     "Y",SUMFLAG
.          RETURN    IF EQUAL
.          GOTO      LNONE2
.
JUMPC1    MATCH     "Y",SUMFLAG
          RETURN    IF EQUAL
.
          COMPARE   FIFTY,CLNO
          CALL      HEAD IF NOT LESS
.
          COMPARE   ONE,NEWSUB
          CALL      SUBHEAD IF EQUAL
.
          PRINT     *1,"|",*26,"|",*35,"|",*44,"|",*48,"|",*54,"| ",MMCODE:
                    *65,"| ",PTEDTYPE,*69,PTEDCODE,*78,"|",*103,"|",*109,"|":
                    *120,"|",*131,"|"
          ADD       ONE,CLNO
          RETURN
+
.
LNONE     MATCH     MMCODE,DIM9A
          GOTO      LNONE1 IF NOT EQUAL
.
          ADD       ONE,SUBPRO
          COMPARE   ONE,MBS1
          GOTO      LNONE1A IF NOT EQUAL
.
          ADD       ONE,MBS1PRO      * Independent procedures
          GOTO      LNONE1A
.
.         Save the operation/diseases codes
.
LNONE1    ADD       ONE,OPRNUM
          STORE     MMCODE,OPRNUM,SAVMCD1,SAVMCD2,SAVMCD3,SAVMCD4,SAVMCD5:
                                  SAVMCD6,SAVMCD7,SAVMCD8,SAVMCD9,SAVMCD10:
                                  SAVMCD11,SAVMCD12,SAVMCD13,SAVMCD14,SAVMCD15
          ADD       ONE,DISNUM
.
          STORE     PTEDTYPE,DISNUM,SAVDTY1,SAVDTY2,SAVDTY3,SAVDTY4,SAVDTY5:
                                    SAVDTY6,SAVDTY7,SAVDTY8,SAVDTY9,SAVDTY10:
                                    SAVDTY11,SAVDTY12,SAVDTY13,SAVDTY14,SAVDTY15
.
          STORE     PTEDCODE,DISNUM,SAVDCD1,SAVDCD2,SAVDCD3,SAVDCD4,SAVDCD5:
                                    SAVDCD6,SAVDCD7,SAVDCD8,SAVDCD9,SAVDCD10:
                                    SAVDCD11,SAVDCD12,SAVDCD13,SAVDCD14,SAVDCD15
          RETURN
+
.
LNONE1A   MOVE      "N",FLINE
          MATCH     "Y",SUMFLAG
          RETURN    IF EQUAL
.
          MATCH     "Y",MBS1FLG
          GOTO      LNONE2 IF NOT EQUAL
.
          BRANCH    MBS1 OF LNONE2
          RETURN
.
LNONE2    COMPARE   FIFTY,CLNO
          CALL      HEAD IF NOT LESS
.
          COMPARE   ONE,NEWSUB
          CALL      SUBHEAD IF EQUAL
.
          PRINT     *1,"| ",PNAME22,*26,"|",PURNO,*35,"|",DADMNO:
                    *44,"| ",PSEX,*48,"| ",PSAGE,*54,"| ",MMCODE:
                    *65,"| ",PTEDTYPE,*69,PTEDCODE,*78,"| ",DNAME22;
.
          COMPARE   ZERO,DAYCASE
          GOTO      LNONE3 IF EQUAL
.
.         For a Day case patient, print the no of beddays as zero
.
          PRINT     *103,"|  DC";
          GOTO      LNONE4
.
LNONE3    PRINT     *103,"| ",NBRDAYS;
.
LNONE4    UNPACK    PADATE,XCENT,XYEAR,XMON,XDAY
          PRINT     *109,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
          UNPACK    PDDATE,XCENT,XYEAR,XMON,XDAY
          PRINT     *120,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,*131,"|"
          ADD       ONE,CLNO
          RETURN
+
.
.         HEADINGS FOR OUTPUT
.
HEAD      MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      HEAD132
          PRINT     *40,REPTYPE
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital       : ",PTHSNAME
          ENDIF
          MOVE      FOUR,CLNO
.
          COMPARE   ZERO,HEADFLG
          RETURN    IF NOT EQUAL
.
          MOVE      ONE,HEADFLG
          PRINT     *40,"Discharge Date : From  ",PFDATE," to  ",PTDATE,*N:
                    *40,"Operation Code : From  ",XDIM9,"  to  ",YDIM9,*N;
.
          MATCH     SP6,YDIM6
          GOTO      HEAD10 IF EQUAL
.
          PRINT     *40,"Doctor    Type : From  ",XDIM6:
                        "     to  ",YDIM6
          GOTO      HEAD20
.
HEAD10    PRINT     *40,"Doctor    Type : ",XDIM6
.
HEAD20    PRINT     *N;
          MOVE      EIGHT,CLNO
          RETURN
+
.
SUBHEAD   MATCH     "Y",SUMFLAG
          CALL      HLINE IF EQUAL
.
          COMPARE   FIFTY,CLNO
          GOTO      SUBHEAD1 IF LESS
          CALL      HEAD
.
SUBHEAD1  ADD       TWO,CLNO
          COMPARE   CSECFEE,SECLEV
          GOTO      NOFEE IF LESS
.
          PRINT     *N,*26,"Operation : ",DIM9A,*51,DIM40A:
                    *95,"MBS Fee : ",IAMT
          GOTO      OKCONT
.
NOFEE     PRINT     *N,*26,"Operation : ",DIM9A,*51,DIM40A
.
OKCONT    MOVE      ZERO,NEWSUB
.
          MATCH     "Y",SUMFLAG
          RETURN    IF EQUAL
.
          CALL      HLINE
          PRINT      *1,"| Patient Name":
                    *26,"| U/R No | Adm No |Sex| Age | Operat'ns":
                    *65,"| Diseases   | Attending Doctor":
                   *103,"| Bed | Admission| Discharge|",*N:
                     *1,"|",*26,"|",*35,"|",*44,"|",*48,"|",*54,"|":
                    *65,"|",*78,"|",*103,"|Days",*109,"|   Date":
                   *120,"|   Date",*131,"|"
.
          ADD       TWO,CLNO
          CALL      HLINE
          RETURN
+
.
HLINE     PRINT       *1,"*--------------------------------------------------":
                     *52,"---------------------------------------------------":
                    *103,"----------------------------*"
          ADD       ONE,CLNO
          RETURN
.
PRTSUB    COMPARE   ONE,NEWSUB
          CALL      SUBHEAD IF EQUAL
.
          MOVE      ZERO,SUBLOS
          MOVE      ZERO,SUBLOSI
.
          MOVE      SUBDAY,TMPCAL
          DIV       SUBPAT,TMPCAL
          MOVE      TMPCAL,SUBLOS          * Average LOS
.
          COMPARE   ZERO,MBS1PAT
          GOTO      NOMBS1 IF EQUAL
.
          MOVE      MBS1TIM,TMPCAL
          DIV       MBS1PAT,TMPCAL
          MOVE      TMPCAL,SUBLOSI         * Average Indep.LOS
          GOTO      CNTPSUB
.
NOMBS1    MATCH     "Y",MBS1FLG
          RETURN    IF EQUAL
.
CNTPSUB   MOVE      ZERO,AVTHEA
          MOVE      ZERO,RATIO
.
          COMPARE   ZERO,SUBTIME
          GOTO      PRTSUB1 IF EQUAL
.
          MOVE      SUBTIME,TMPCAL
          DIV       SUBPRO,TMPCAL
          MOVE      TMPCAL,AVTHEA       * Average theatre time
.
          MOVE      SUBLOS,FORM52
          MOVE      AVTHEA,RATIO
          DIV       FORM52,RATIO        * Ratio LOS to theatre
.
PRTSUB1   MOVE      ZERO,AVTHEI
          MOVE      ZERO,RATIOI
.
          COMPARE   ZERO,SINTIME
          GOTO      PRTSUB2 IF EQUAL
.
          MOVE      SINTIME,TMPCAL
          DIV       SINPAT,TMPCAL
          MOVE      TMPCAL,AVTHEI       * Average independent theatre time
.
          MOVE      SUBLOSI,FORM52
          MOVE      AVTHEI,RATIOI
          DIV       FORM52,RATIOI       * Ratio indep LOS to theatre
.
.         Calculate daycases
.
PRTSUB2   MOVE      ZERO,AVTDD
          MOVE      ZERO,AVTID
          MOVE      ZERO,RATIODD
          MOVE      ZERO,RATIOID
.
          COMPARE   ZERO,SUBDD
          GOTO      PRTSUB3 IF EQUAL
.
          MOVE      DCDTIME,TMPCAL
          DIV       SUBDD,TMPCAL      
          MOVE      TMPCAL,AVTDD        * Average daycase theatre time
.
          MOVE      ONE,FORM52          * Average LOS for daycase is always one
          MOVE      AVTDD,RATIODD
          DIV       FORM52,RATIODD      * Ratio daycase LOS to theatre
.
PRTSUB3   COMPARE   ZERO,SUBID
          GOTO      CNTPRTS IF EQUAL
.
          MOVE      DCITIME,TMPCAL
          DIV       SUBID,TMPCAL
          MOVE      TMPCAL,AVTID        * Average independent daycase theatre
.
          MOVE      ONE,FORM52          * Average LOS for daycase is always one
          MOVE      AVTID,RATIOID
          DIV       FORM52,RATIOID      * Ratio Indep.daycase LOS to theatre
.
CNTPRTS   COMPARE   FIFTY,CLNO
          GOTO      CNTPRTS1 IF LESS
          CALL      HEAD
          CALL      SUBHEAD
.
CNTPRTS1  PRINT     *1,"No. of Procedures  ",SUBPRO:
                    *29,"No. of Bed Days     ",SUBDAY:
                    *62,"Ave. L.O.S.       ",SUBLOS:
                    *93,"Ratio L.O.S. to Theatre        ",RATIO
          ADD       ONE,CLNO
.
          MATCH     "Y",MBS1FLG
          GOTO      PRTLN3 IF EQUAL
.
          PRINT     *1,"No. of Indep.Procs ",MBS1PRO:
                    *29,"No. of Indep.Bed Day",MBS1TIM:
                    *62,"Ave. Indep. L.O.S.",SUBLOSI:
                    *93,"Ratio Indep. L.O.S. to Theatre ",RATIOI
          ADD       ONE,CLNO
.
PRTLN3    PRINT     *1,"No. of Day Cases   ",SUBDD:
                    *62,"Ave. Theatre Time ",AVTHEA,"Min.":
                    *93,"Ratio Day Case L.O.S. to Thea. ",RATIODD:
                    *N,*1,"No. of Ind. DayCase",SUBID:
                    *62,"Ave. Ind.Thea.Time",AVTHEI,"Min.":
                    *93,"Ratio Indep. Day Case LOS/Thea.",RATIOID:
                    *N,*62,"Ave. Day Case Theatre Time       ",AVTDD,"Min.":
                    *N,*62,"Ave. Indep. Day Case Theatre Time",AVTID,"Min."
.
          ADD       SUBPRO,GNDPRO
          ADD       MBS1PRO,GNDPROI
          CALL      ZEROS
          MOVE      MMCODE,SAVCODE
          MOVE      DADMNO,SAVADM
          ADD       FOUR,CLNO
          RETURN
+
.
PRTGND    MOVE      ZERO,GAVTHE
          MOVE      ZERO,GAVTHEI
          MOVE      ZERO,GAVTDD
          MOVE      ZERO,GAVTID
          MOVE      ZERO,GNDLOS
          MOVE      ZERO,GNDLOSI
.
          MOVE      GNDTIME,TMPCAL
          DIV       GNDPRO,TMPCAL       * Divide by number of procedures
          MOVE      TMPCAL,GAVTHE       * Grand Average theatre time
.
          MOVE      GNDITIM,TMPCAL
          DIV       GNDMBS,TMPCAL
          MOVE      TMPCAL,GAVTHEI      * Grand Average Indep.theatre time
.
          MOVE      GNDDDC,TMPCAL
          DIV       GNDDD,TMPCAL
          MOVE      TMPCAL,GAVTDD       * Grand Ave. Daycase theatre time
.
          MOVE      GNDIDC,TMPCAL
          DIV       GNDID,TMPCAL
          MOVE      TMPCAL,GAVTID       * Grand Ave.Indep Daycase thea.time
.
          MOVE      GNDDAY,TMPCAL
          DIV       GNDPAT,TMPCAL
          MOVE      TMPCAL,GNDLOS       * Grand LOS
.
          MOVE      GNDIBD,TMPCAL
          DIV       GNDMBS,TMPCAL
          MOVE      TMPCAL,GNDLOSI      * Grand Indep.LOS
.
          PRINT     *N;
          ADD       ONE,CLNO
          CALL      HLINE
          COMPARE   FIFTY,CLNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *N,*N,"GRAND TOTALS":
                    *N,*1,"No. of Operations ",GNDPAT:
                    *29,"No. of Day Cases      ",GNDDD:
                    *62,"Total Bed Days     ",GNDDAY:
                    *93,"Ave. Theatre Time              ",GAVTHE:
                    *N,*1,"No. of Procedures ",GNDPRO:
                    *29,"No. of Indep.Day Cases",GNDID:
                    *62,"Ave. L.O.S.        ",GNDLOS:
                    *93,"Ave. Indep. Theatre Time       ",GAVTHEI:
                    *N,*1,"No. of Indep.Procs",GNDPROI:
                    *62,"Ave. Indep. L.O.S. ",GNDLOSI:
                    *93,"Ave. Day Case Theatre Time     ",GAVTDD,*N:
                    *93,"Ave. Indep.Day Case Thea. Time ",GAVTID
          RETURN
+
.
.         Deleting an Indexed file based on Port
.
KILLIDX   CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEG,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.
CREAIDX   PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 113 u1-25",015:
                               "isbuild ",FNAMEG," 9 u1-8"
          WEOF      PSTROL,SEQ
          CALL      ROLLF
          RETURN
+
.
ROLLF     CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     PSTROL,DELETE
          RETURN
+
.
CLRSCR    DISPLAY   *P36:4,*EL,*P36:5,*EL,*P36:7,*EL,*P36:8,*EL,*P36:10,*EL:
                    *P36:12,*EL,*P36:14,*EL,*P36:16,*EL,*P36:18,*EF;
          GOTO      START
.
.
HEADSET   MOVE      SP9,XDIM9
          MOVE      SP9,YDIM9
.
          MOVE      START9,XDIM9
          MOVE      END9,YDIM9
.
          MATCH     SP9,START9
          GOTO      CONT1 IF NOT EQUAL
          MOVE      FSTREC,XDIM9
.
CONT1     MATCH     SP9,END9
          GOTO      CONT2 IF NOT EQUAL
          MOVE      LSTREC,YDIM9
.
CONT2     MOVE      SP6,YDIM6
          MOVE      SP6,XDIM6
          MOVE      START3,XDIM6
.
          MATCH     SP3,START3
          RETURN    IF NOT EQUAL
          MOVE      FSTREC,XDIM6
          MOVE      LSTREC,YDIM6
          RETURN
+
.
ZEROS     MOVE      ZERO,SUBPAT
          MOVE      ZERO,SINPAT
          MOVE      ZERO,SUBDAY
          MOVE      ZERO,SUBLOS
          MOVE      ZERO,SUBPRO
          MOVE      ZERO,MBS1PRO
          MOVE      ZERO,SUBDD
          MOVE      ZERO,SUBID
          MOVE      ZERO,SUBTIME
          MOVE      ZERO,SINTIME
          MOVE      ZERO,DCDTIME
          MOVE      ZERO,DCITIME
          MOVE      ZERO,STRTIME
          MOVE      ZERO,ENDTIME
          MOVE      ZERO,AVTHEA
          MOVE      ZERO,AVTHEI
          MOVE      ZERO,AVTDD
          MOVE      ZERO,AVTID
          MOVE      ZERO,RATIO
          MOVE      ZERO,RATIOI
          MOVE      ZERO,RATIODD
          MOVE      ZERO,RATIOID
          MOVE      ZERO,PRNFLAG
          MOVE      ZERO,MBS1PAT
          MOVE      ZERO,MBS1TIM
          RETURN
+
.
.       Initialize operation/diseases variables
.
INTVAR    MOVE      ZERO,FORM2
.
INTV10    ADD       ONE,FORM2
          STORE     SP10,FORM2,SAVMCD1,SAVMCD2,SAVMCD3,SAVMCD4,SAVMCD5:
                               SAVMCD6,SAVMCD7,SAVMCD8,SAVMCD9,SAVMCD10:
                               SAVMCD11,SAVMCD12,SAVMCD13,SAVMCD14,SAVMCD15
.
          STORE     SP1,FORM2,SAVDTY1,SAVDTY2,SAVDTY3,SAVDTY4,SAVDTY5:
                              SAVDTY6,SAVDTY7,SAVDTY8,SAVDTY9,SAVDTY10:
                              SAVDTY11,SAVDTY12,SAVDTY13,SAVDTY14,SAVDTY15
.
          STORE     SP10,FORM2,SAVDCD1,SAVDCD2,SAVDCD3,SAVDCD4,SAVDCD5:
                               SAVDCD6,SAVDCD7,SAVDCD8,SAVDCD9,SAVDCD10:
                               SAVDCD11,SAVDCD12,SAVDCD13,SAVDCD14,SAVDCD15
          COMPARE   "15",FORM2
          GOTO      INTV10 IF LESS
          RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The Date Ranges                           *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:20,*EL,"10. Hospital Id                  : ";
          MOVE      THIRTY6,ECOL
          MOVE      "20",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P48:20,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.
ENDIT     CALL      KILLIDX
          CHAIN     PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS
          INC       NHOSPDAY
          INC       PATHSPKY
          INC       PATITMRD
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATHSPIO/INC
          INC       PATICDIO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       HL7BMTIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATMMBIO/INC
          INC       PATTRNIO/INC
.
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
          INC       TFILENAM
.

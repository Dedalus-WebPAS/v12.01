********************************************************************
.* System    :  OPERATING ROOMS                                               *
.* Program   :  IBAOPR53                                                      *
.* Desc      :  Personnel Log Report                                          *
.******************************************************************************
.* Date      :  02/01/1991                                                    *
.* Author    :  Justin Coates                                                 *
.* Function  :  Print the list of operations in which a given Doctor or Nurse *
.*                  has participated in                                       *
.* Mods      :                                                                *
.******************************************************************************
.* V11.03.02 19/04/2023  Thanh T  TSK 0909393                                 *
.*           Changed TEAM0000 to setup KEY22 with OPDAHOSP.                   *
.*           Changed REPT0000 to use RSOPDEA6 instead of RSOPDEA1.            *
.* V11.03.01 20/03/2023  PJ Le Febour   TSK 0909393                           *
.*           Amended KEY19 to KEY22 for theatre index changes                 *
.* V11.01.01 27/04/2021  Thanh T        TSK 0895165                           *
.*           Recompiled for OPRARDFD changes                                  *
.******************************************************************************
.*        V10.04.01 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added PMSVX1FD                                            *
.*        V9.12.01  05/11/2009  Ebon Clements CAR 207604                      *
.*                  Added call to RCVT0000 - determine time in recovery       
.*        V9.11.01  21/01/2009  Jill Habasque CAR 172297                      *
.*                  Mods to make OPDCNOOP or OPSGNOOP 1 if not entered        *
.*        V9.08.01  28/03/2007  Janet George  CAR 120359                      *
.*                  Fixed P * K error                                         *
.*        V9.03.01  07/08/2003  Jill Habasque CAR 41815                       *
.*                  Changed matches of opdastat                               *
.*        V9.02.02  11/03/2003  Tonii  CAR 37093                              *
.*                  'Age' and 'Time' not displaying correctly on the report   *
.*                  - Mods to call CALCAGE to get the current age not to use  *
.*                    the value stored in patma1af.psage                      *
.*                  - Mods to save TIMEB before calling DURN0000, as routine  *
.*                    will actually overwrite the value of TIMEB              *
.*        V9.02.01  12/02/2003  Tonii   SRS No: 36667                         *
.*                  Mods to use new tables if a record exists in oprsrgaf     *
.*                  Program to use OPCNFTIM and OPCNTTIM to determine which   *
.*                  operation times to calculate the duration                 *
.******************************************************************************
.*        V5.08.03  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  12/05/2000  Steve Downing                                 *
.*                  Fixed operation type description in breakdown             *
.*        V5.07.02  20/12/1999  Caleb Sharman  5.08 Mods                      *
.*                  Mods to breakdown the final report,                       *
.*                  the doctor option: summary of operating personnel         *
.*                  the nurse option : summary of operating nurse             *
.*        V5.07.01  24/02/1999  Jim Stathopoulos                              *
.*                  Added CKEYSUSP and CKEYORDR variables                     *
.*        V5.07.B01 21/01/1999  Nicole Harrington 507 rebound 091             *
.*                  Mods to display century on date                           *
.******************************************************************************
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*              V5.03.01  08/05/1996 Howellsy        5.04                      *
.*           :           Added 2 more nurse codes & types.                    *
.*              V5.01.01  21/01/1991  ROD                                     *
.*                        Converted to Release 5.01                           *
.*                        04/02/1991  ROD                                     *
.*                        Included Admission No. in heading                   *
.*                        07/02/1991  ROD                                     *
.*                        Fixed bug in calculation of No. of Operations       *
.*                        11/02/1991  ROD                                     *
.*                        Added No. of Operations in report (body)            *
.*              V5.01.02  05/03/1991  J.Tan            SRF 108015             *
.*                        Changes to OPRNURDS                                 *
.*              V5.01.03  02/07/1991  Justin Coates                           *
.*                        Fixed paging problems                               *
.*              V5.01.04  25/07/1991  ROD                                     *
.*                        Recompiled for OPRSESFD                             *
.*              V5.01.05  17/10/1991  Justin Coates                           *
.*                        Check bokrecfd & patprafd for patient details       *
.*              V5.01.06  27/02/1992  i chung          SRF 113325             *
.*                        Changed Operation Desc. to full length - Layout 2   *
.*              V5.01.07  19/08/92 J.Tan     SRF 116653                       *
.*                        Recompiled for OPRNURDS                             *
.*              V5.01.08  28/10/92 J.Tan     SRF 117938                       *
.*                        Mods not to include cancelled patients              *
.*        V5.01.09  24/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.******************************************************************************
.
.$$$$$
.         IBAOPR53        Personnel Log Report
.         --------        --------------------
.         INIT0000 - Initialization
.         OPTN0000 - get the report order (doctor or nurse)
.         KDOC0000 - enter doctor code / KNUR0000 - enter nurse code
.         KDAT0000 - enter the date range
.         REPT0000 - produce the report
.             PAGE0000 - prints page heading and table heading
.             TEAM0000 - determines if doctor/nurse is in operating team
.             TMDE0000 - use oprtsmaf to determine if doctor/nurse 
.                        is in operating team
.             PDET0000 - obtain and print details
.$$$$$
+
          INC       STD001FD                * standard Includes
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       PATCALAG/INC            * calculate age
          INC       BOKRC1FD/INC            * bookings file
          INC       OPRARDFD/INC
          INC       OPRCONFD/INC            * control file
          INC       OPRDEAFD/INC            * operation details file
          INC       OPRDEBFD/INC            * operation team file
          INC       OPRDECFD/INC            * operation usage file
          INC       OPRNURFD/INC            * nurses file
          INC       OPRSESFD/INC            * sessions file
          INC       OPRSRGFD/INC            * new operation details file
          INC       OPRTSMFD/INC            * new operation team file
.
          INC       PATCODFD/INC            * codes file
          INC       PATCONFD/INC
          INC       PATDO1FD/INC            * doctors file
          INC       PATMA1FD/INC            * master file
          INC       PATMI1FD/INC            * admissions file
          INC       PMSVX1FD/INC            * admissions file
          INC       PATPR1FD/INC            * pre-admissions file
+
.
. ------- program variables -------
.
BJDAY     FORM      3
CJDAY     FORM      3
PSAGETYP  DIM       6
PSAGECON  DIM       3
.
CMDLINE   DIM       50            * command line for 'execute' command
CODEDOCT  DIM       6             * doctor code
CODENURS  DIM       6             * nurse code
CURRDATE  DIM       8             * current date
.
DATESTRT  DIM       8             * starting date
DATESTOP  DIM       8             * ending date
DESC1     DIM       30            * op desc 1
DESC2     DIM       30            * op desc 2
DESC3     DIM       30            * op desc 3
DESCDOCT  DIM       40            * doctor desc
DESCNURS  DIM       40            * nurse desc
DIM3      DIM       3             * patdsdoc
DIM6      DIM       6             * the theatre for operation
DIM8      DIM       8             * operation date  dd/mm/yy
DIM10     DIM       10              
DIM10A    DIM       10            * starting date dd/mm/ccyy
DIM10B    DIM       10            * ending date   dd/mm/ccyy
DOCSULEV  DIM       15
DOCCODE   DIM       8
DURATION  FORM      4
.
FORM4B    FORM      4
.
NURSFLAG  FORM      1
OPERTOTL  FORM      4             * total count of operations
OPPERDC1  DIM       20            * operating/nursing personnel desc. - Layout 1
OPPERDC2  DIM       15            * operating/nursing personnel desc. - Layout 2
OPSRGFLG  FORM      1
OPTYPDC1  DIM       20            * operation type description - Layout 1
OPTYPDC2  DIM       15            * operation type description - Layout 2
PATNAME1  DIM       22            * patient name - Layout 1
PATNAME2  DIM       19            * patient name - Layout 2
PRTYP     DIM       15            * personnel type
SAVEUNIQ  DIM       10            * the operation unique number
SCRNFLAG  FORM      1             * ? performed flag
.
STIMEB    DIM       5
TIMEA     DIM       5             * operation start time
TIMEB     DIM       5             * operation end time
TIMEC     DIM       5             * anaesthetic start time
TIMEUSED  FORM      6             * total time used
.
TEMPFILE  IFILE     SQL,FIXED=26, KEY="U11-25,1-3"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
XTYPE     DIM       3        3     1   Operation type
XCOUNT    FORM      4        3     4   Counter
XTIME     FORM      6        4     7   Total time used
PERTY     DIM       15       15   11   Personnel Type
.                                 26   End of Record
.
. ------- program constants -------
.
INDEX     INIT      " 26 U11-25,1-3"
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
.
PRGID     INIT      "IBAOPR53"
PRGNAM    INIT      "Personnel Log Report"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * get which option
          BRANCH    EXIT,ML9999             * exit
          BRANCH    MOPTION,ML1100,ML1200
.
.         enter doctor code
.
ML1100    CALL      KDOC0000 
          BRANCH    EXIT,ML1000             * nothing entered
          GOTO      ML2000
.
.         enter nurse code
.
ML1200    CALL      KNUR0000
          BRANCH    EXIT,ML1000             * nothing entered
.
.         enter date range
.
ML2000    CALL      KDAT0000
          BRANCH    EXIT,ML3000             * continue
.
ML2100    BRANCH    MOPTION,ML1100,ML1200   * nothing entered
.
.         Ok to continue !
.
ML3000    CALL      CONTQST
          BRANCH    CEXIT,ML4000,ML2100,ML1000
.                         Yes    No     Cancel
.
.         produce the report
.
ML4000    CALL      REPT0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD                * display program header
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE           * current date
          DISPLAY   *P1:3,*EF               * clear the screen
          MOVE      ONE,CNEWDS              * new ? mark option
          MOVE      ONE,CDEFDTE             * once to accept default
          MOVE      ONE,CNOUNDLN            * no underlines
          MOVE      ZERO,CHIGHLT            * use highlighting
          MOVE      ZERO,NURSFLAG
.
          DISPLAY   *P50:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,FORTY2;*19,OPCNFOTM,*20,OPCNTOTM,*45,OPCNR53A:
                                    *47,OPCNANST,*235,OPCNFTIM,*237,OPCNTTIM
.
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"     * bookings file
.
          DISPLAY   *P58:24,"oprardaf"
          OPEN      OPRARDA1,"oprardaf"     
.
          DISPLAY   *P58:24,"oprdetaf"
          OPEN      OPRDETA6,"oprdetaf"     * operation details file
.
          DISPLAY   *P58:24,"oprdetbf"
          OPEN      OPRDETB1,"oprdetbf"     * operation team file
.
          DISPLAY   *P58:24,"oprdetcf"
          OPEN      OPRDETC1,"oprdetcf"     * operation usage file
.
          DISPLAY   *P58:24,"oprnuraf"
          OPEN      OPRNURA1,"oprnuraf"     * nurses file
          OPEN      OPRNURA2,"oprnuraf"     * nurses file
.
          DISPLAY   *P58:24,"oprsesaf"
          OPEN      OPRSESA1,"oprsesaf"     * sessions file
.
          DISPLAY   *P58:24,"oprsrgaf"
          OPEN      OPRSRGA1,"oprsrgaf"     * operation details
.
          DISPLAY   *P58:24,"oprtsmaf"
          OPEN      OPRTSMA1,"oprtsmaf"     * new team file
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"     * codes file
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"     * doctors file
          OPEN      PATDO1A2,"patdo1af"     * doctors file
.
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"     * master file
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"     * master extension file
.
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"     * admissions file
          OPEN      PMSVX1A1,"pmsvx1af"     * admissions file
.
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAME
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        Get which option to do report by                           *
.*        Returns : EXIT = 1      exit chosen                        *
.*                  MOPTION       option type                        *
.*********************************************************************
OPTN0000  HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*P1:5,ONE,*P1:6,TWO,*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Doctor":
                    *P3:6,"= Nurse":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*DV,UNDLN1,*P17:8,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9000 IF EQUAL       * exit selected
.
          BRANCH    MOPTION,OPTN2000,OPTN3000
          BEEP
          GOTO      OPTN1000
.
OPTN2000  DISPLAY   *P50:2,*V2LON,"- Doctor ";
          GOTO      OPTN8000
.
OPTN3000  DISPLAY   *P50:2,*V2LON,"- Nurse ";
.
OPTN8000  MOVE      FALSE,EXIT              * valid answer
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT               * exit selected
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  SCRN0000                    Called by : KDOC0000 *
.*        Redisplay Screen after a ? mark                   KNUR0000 *
.*********************************************************************
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*P1:5,ONE,*P1:6,TWO,*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Doctor":
                    *P3:6,"= Nurse":
                    *P1:8,"Select Option : ",*V2LON,MOPTION;
.
          BRANCH    MOPTION,SCRN1000,SCRN2000
.
.         by doctor code
.
SCRN1000  DISPLAY   *P1:10,"Doctor code   : ",*V2LON,CODEDOCT,*HOFF:
                    *P26:10,DESCDOCT;
          GOTO      SCRN9999
.
.         by nurse code
.
SCRN2000  DISPLAY   *P1:10,"Nurse code    : ",*V2LON,CODENURS,*HOFF:
                    *P26:10,DESCNURS;
.
SCRN9999  RETURN
+
.********************************************************************
.*                  KDOC0000                   Called by : ML1100   *
.*        Enter the doctor code                                     *
.*        Returns : EXIT  = 1     nothing entered                   *
.*                  CODEDOCT      doctor code                       *
.*                  DESCDOCT      doctor description                *
.********************************************************************
KDOC0000  PACK      DESCDOCT,SP20,SP20      * clear code
          MOVE      "10",CVERT              * row
          DISPLAY   *P1:10,"Doctor code   : ",*EF;
          MOVE      SP10,CODEDOCT
          MOVE      "17",ECOL
          MOVE      "10",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KDOC9000,KDOC9000
.
          MOVE      DCODE,CODEDOCT
.
KDOC2000  MOVE      CODEDOCT,KEY6           * key of entered value
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DESCDOCT
          GOTO      KDOC8000                * valid input
.
KDOC8000  MOVE      FALSE,EXIT              * valid input
KDOC8100  DISPLAY   *P26:10,DESCDOCT;
          GOTO      KDOC9999
.       
.         exit selected
.
KDOC9000  MOVE      ONE,EXIT
.
KDOC9999  RETURN
+
.********************************************************************
.*                  KNUR0000                   Called by : ML1200   *
.*        Enter the nurses code                                     *
.*        Returns : EXIT  = 1     nothing entered                   *
.*                  CODENURS      nurses code                       *
.*                  DESCNURS      nurses description                *
.********************************************************************
KNUR0000  MOVE      ONE,SCRNFLAG            * set ? as not performed
          MOVE      ONE,NURSFLAG
          PACK      DESCNURS,SP20,SP20      * clear code
          MOVE      "10",CVERT              * row
          DISPLAY   *P1:10,"Nurse code    : ",*EF;
.
KNUR1000  KEYIN     *P17:CVERT,*DV,UNDLN6,*P17:CVERT,*V2LON,CODENURS:
                    *P17:CVERT,*DV,CODENURS;
.
          ENDSET    CODENURS
          APPEND    SP6,CODENURS
          RESET     CODENURS
.
          MATCH     EXITCHAR,CODENURS 
          GOTO      KNUR9000 IF EQUAL       * exitchar entered
.
          MATCH     SP6,CODENURS 
          GOTO      KNUR9000 IF EQUAL       * nothing entered
.
          MATCH     QUEST,CODENURS
          GOTO      KNUR2000 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          CALL      OPRNURDS                * ? routine
          MOVE      TWENTY4,CVERT           * row for input
          MOVE      ZERO,SCRNFLAG           * set ? as performed
          DISPLAY   *P1:24,"Nurse code    : ",*EL;
          GOTO      KNUR1000
.
.         validate what was entered
.
KNUR2000  MOVE      CODENURS,KEY6           * key of entered value
          CALL      RDOPNUR1                * read nurse file
          BRANCH    OVRCD,KNUR7000          * invalid input
.
          MOVE      OPNRSNAM,PACSNAME
          MOVE      OPNRGNAM,PACGNAME
          MOVE      SP4,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DESCNURS
          GOTO      KNUR8000                * valid input
.
.         ERROR: invalid code entered
.
KNUR7000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Nurse code **    ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,KNUR1000          * ? not entered
.
          DISPLAY   *P1:24,"Nurse code    : ",*EL;
          GOTO      KNUR1000    
.
.         valid code entered
.
KNUR8000  MOVE      FALSE,EXIT              * valid input
          BRANCH    SCRNFLAG,KNUR8100       * ? not entered
.
          CALL      SCRN0000                * redisplay rest of screen
          GOTO      KNUR9999
.
KNUR8100  DISPLAY   *P26:10,DESCNURS;
          GOTO      KNUR9999
.
.         exit selected
.
KNUR9000  MOVE      ONE,EXIT
.
KNUR9999  RETURN
+
.*********************************************************************
.*                  KDAT0000                    Called by : ML2000   *
.*        Enter the date ranges                                      *
.*        Returns : EXIT = 0      exit chosen                        * 
.*                  DATESTRT      starting date                      *
.*                  DATESTOP      ending date                        *
.*********************************************************************
KDAT0000  DISPLAY   *P1:12,"Starting date : ",*EL:
                    *P1:14,"Ending date   : ",*EL;
.
.         enter starting date
.
KDAT1000  MOVE      "12",CVERT              * row
          MOVE      "17",CCOL               * column
          UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT9000          * nothing entered
.
          PACK      DATESTRT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATESTRT           * set up start date
          CALL      PACDATE
          MOVE      CPCDATE,DIM10A          * formatted start date
.
          MATCH     DATESTRT,CURRDATE
          GOTO      KDAT2000 IF NOT LESS    * date is <= current date
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Starting date must not be after current date **    ";
          CALL      HITENTER 
          GOTO      KDAT1000
.
.         enter ending date (default to starting date)
.
KDAT2000  MOVE      "14",CVERT              * row
          MOVE      "17",CCOL               * column
          UNPACK    DATESTRT,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT2000
          BRANCH    OVRCD,KDAT1000          * nothing entered
.
          PACK      DATESTOP,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATESTOP           * set up end date
          CALL      PACDATE
          MOVE      CPCDATE,DIM10B          * formatted end date
.
          MATCH     DATESTRT,DATESTOP
          GOTO      KDAT3000 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending date must be > or equal to Starting date **   ";
          CALL      HITENTER 
          GOTO      KDAT2000
.
.         check end date is before current date
.
KDAT3000  MATCH     DATESTOP,CURRDATE
          GOTO      KDAT8000 IF NOT LESS    * date is <= current date
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending date must not be after current date **    ";
          CALL      HITENTER 
          GOTO      KDAT2000
.
KDAT8000  MOVE      TRUE,EXIT               * valid dates
          GOTO      KDAT9999
.
KDAT9000  MOVE      FALSE,EXIT              * exit selected
.
KDAT9999  RETURN
+
.*********************************************************************
.*                  REPT0000                    Called by : ML4000   *
.*        Produce the report                                         *
.*********************************************************************
REPT0000  CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report",*HOFF:
                           *V2LON," **",*HOFF,*P45:24,"Date : ";
          MOVE      ZERO,CPAGENO            * first page
          MOVE      ZERO,TIMEUSED           * clear total time used
          MOVE      ZERO,OPERTOTL           * clear total operation counter
          CALL      CTMP0000                * create temp file
          CALL      PAGE0000                * print a new page header
.
          PACK      KEY26,DATESTRT,SP70     * start at start date
          CALL      RSOPDEA6                * positional read
.
.         get the next operation
.
REPT1000  CALL      RKOPDEA6                * read next record
          BRANCH    OVRCD,REPT8000          * finished range
.
          MATCH     OPDADATE,DATESTOP
          GOTO      REPT8000 IF LESS        * finished range
.
          COMPARE   THREE,OPDASTAT
          GOTO      REPT1000 IF EQUAL       * not include cancelled patients
.
          UNPACK    OPDADATE,XCENT,XYEAR,XMON,XDAY
          DISPLAY   *P52:24,*V2LON,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
          MOVE      OPDAUNIQ,SAVEUNIQ       * save the unique number
.
          PACK      KEY10,SAVEUNIQ
          CALL      RDOPARD1
          IF        OVRCD = 0
            MOVE      ONE,OPSRGFLG            * record exist in oprsrgaf
          ELSE
            MOVE      ZERO,OPSRGFLG
            GOTO      REPT4000
          ENDIF
.
.         check if record exist in oprsrgaf, use old table if no records
.
REPT2000  PACK      KEY11,SAVEUNIQ,SP1      * position at first item
          CALL      RSOPSRG1
REPT2500  CALL      RKOPSRG1
          BRANCH    OVRCD,REPT1000
.
          MATCH     SAVEUNIQ,OPSGUNID
          GOTO      REPT1000 IF NOT EQUAL
.
.         get team details in oprtsmaf
.
          CALL      TMDE0000
          BRANCH    EXIT,REPT2500
.
.         print the details
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS    * print a new page
.
          CALL      PDET0000                * print the details
          GOTO      REPT2500
.
REPT4000  PACK      KEY11,SAVEUNIQ,SP1      * start at first team
          CALL      RSOPDEC1                * position at start of usage file
.
.         get the next team for the operation from old table
.
REPT4500  CALL      RKOPDEC1                * get next team
          BRANCH    OVRCD,REPT1000          * end of file, get next operation
.
          MATCH     SAVEUNIQ,OPDCUNIQ
          GOTO      REPT1000 IF NOT EQUAL   * not the same operation
.
.         get the team details
.
          CALL      TEAM0000        
          BRANCH    EXIT,REPT4500           * entered doctor/nurse not in team
.
.         print the details
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS    * print a new page
.
          CALL      PDET0000                * print the details
          GOTO      REPT4500                * get next operation
.
.         finished report
.
REPT8000  CALL      SUMM0000                * finish up report
.
REPT9999  RETURN
+
.*********************************************************************
.*                  PAGE0000                    Called by : REPT0000 *
.*        Print the report page heading                              *
.*        Para's  : OPCNR53A      which report layout                * 
.*********************************************************************
PAGE0000  CALL      HEAD132
          PRINT     *N,*40,"Date Range : ",DIM10A," to ",DIM10B;
          BRANCH    MOPTION,PAGE1000,PAGE1200
.
PAGE1000  PRINT     *N,*40,"Doctor     : ",CODEDOCT,*64,DESCDOCT
          GOTO      PAGE2000
.
PAGE1200  PRINT     *N,*40,"Nurse      : ",CODENURS,*64,DESCNURS
.
PAGE2000  MOVE      "6",CLNO                * set line counter
.
.         print the required table heading
.
          BRANCH    OPCNR53A,PAGE4000,PAGE5000
          GOTO      PAGE9999
.
.         Report Layout 1 table heading
.
PAGE4000  PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
                    *N,"| Op. Date | Operation Description",*45,"| Time | ":
                       "Operation Type ",*75,"| Theatre|  U/R No. | ":
                       "Patient Name ",*120,"| Sex | Age |":
                    *N,"| Cln/Surg |",*45,"|No.Ops| Personnel Type",*75,"|":
                       *84,"|  Adm No. |",*120,"|     |     |":
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*"
          GOTO      PAGE6000
.
.         Report Layout 2 table heading
.
PAGE5000  PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
                    *N,"| Op. Date | Operation Description",*66,"| Time":
                       *72,"|Operation Type",*88,"|Th'tre| U/R No |":
                       " Patient Name",*124,"|Sex|Age|":
                    *N,"| Cln/Surg |",*66,"|",*72,"|Personnel Type":
                       *88,"|Anaes.| Adm No |",*124,"|   |   |":
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*"
.
PAGE6000  ADD       "5",CLNO                * add to line counter
.
PAGE9999  RETURN
+
.*********************************************************************
.*                  TMDE0000                    Called by : REPT2000 *
.*        get the team details for the operation from oprtsmaf       *
.*        Returns : EXIT = 1      invalid team details               *
.*********************************************************************
TMDE0000  BRANCH    MOPTION,TMDE0500,TMDE2000
.                            doctor / nurse
          GOTO      TMDE9000
.
.         get doctor team details
.
TMDE0500  PACK      KEY35,SAVEUNIQ,OPSGTMNO,SP30
          CALL      RSOPTSM1
TMDE1000  CALL      RKOPTSM1
          BRANCH    OVRCD,TMDE9000
.
          MATCH     SAVEUNIQ,OPTSUNID
          GOTO      TMDE9000 IF NOT EQUAL
.
          MATCH     OPSGTMNO,OPTSTMNO
          GOTO      TMDE9000 IF NOT EQUAL
.
          COMPARE   TWO,OPTSSIND            * nurse record
          GOTO      TMDE9000 IF EQUAL
.
          COMPARE   THREE,OPTSSIND          * other record
          GOTO      TMDE9000 IF EQUAL
.
          MATCH     CODEDOCT,OPTSSCDE       * same doctor?
          GOTO      TMDE1000 IF NOT EQUAL   * no, get next rec.
.
          MOVE      SP20,OPPERDC1
          MOVE      SP20,OPPERDC2
.
.         staff category code
.          
          MOVE      OPTSSTYP,DIM3
. 
          MATCH     SP3,DIM3
          GOTO      TMDE8000 IF EQUAL       * no type set up
.
          MOVE      SP20,TDESC      
          PACK      KEY5,ANSO,ANSP,DIM3   
          CALL      RDCODE1
          COMPARE   TWO,OPCNR53A          
          IF        @EQUAL                
            MOVE      TDESC,OPPERDC2
          ELSE
            MOVE      TDESC,OPPERDC1    
          ENDIF
.
.         doctor's supervision level
.
          MATCH     SP3,OPTSSLEV
          GOTO      TMDE8000 IF EQUAL       * no type set up
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSO,ANSS,OPTSSLEV
          CALL      RDCODE1
          MOVE      TDESC,DOCSULEV  
          GOTO      TMDE8000
.
.         get nurse team details
.
TMDE2000  PACK      KEY35,SAVEUNIQ,OPSGTMNO,SP1,TWO,CODENURS,SP30
          CALL      RSOPTSM1
TMDE2500  CALL      RKOPTSM1
          BRANCH    OVRCD,TMDE9000
.
          MATCH     SAVEUNIQ,OPTSUNID
          GOTO      TMDE9000 IF NOT EQUAL
.
          MATCH     OPSGTMNO,OPTSTMNO
          GOTO      TMDE9000 IF NOT EQUAL
.
          COMPARE   TWO,OPTSSIND            * nurse record
          GOTO      TMDE9000 IF NOT EQUAL
.
          MATCH     CODENURS,OPTSSCDE
          GOTO      TMDE9000 IF NOT EQUAL
.
.         staff category code
.
          MOVE      OPTSSTYP,DIM3
.
          MATCH     SP3,DIM3
          GOTO      TMDE8000 IF EQUAL       * no type set up
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSO,ANSN,DIM3
          CALL      RDCODE1
          COMPARE   TWO,OPCNR53A
          IF        @EQUAL
            MOVE      TDESC,OPPERDC2
          ELSE
            MOVE      TDESC,OPPERDC1
          ENDIF
.
TMDE8000  MOVE      ZERO,EXIT               * valid code  
          GOTO      TMDE9999
.
TMDE9000  MOVE      ONE,EXIT                * invalid code
.
TMDE9999  RETURN
+
.*********************************************************************
.*                  TEAM0000                    Called by : REPT4000 *
.*        get the team details for the operation oprdetbf            *
.*        Returns : EXIT = 1      invalid team details               *
.*********************************************************************
TEAM0000  PACK      KEY11,SAVEUNIQ,OPDCTEAM
          CALL      RDOPDEB1
          COMPARE   ZERO,OVRCD
          GOTO      TEAM5000 IF EQUAL       * valid team
.
.         team details don't exist, check if first team
.
          MATCH     "1",OPDCTEAM
          GOTO      TEAM9900 IF NOT EQUAL   * not team one so invalid
.
.         first team so use session file default team
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,TEAM9900          * invalid details
.
.         copy required oprsesfd values to oprdebfd values
.
          BRANCH    MOPTION,TEAM1000,TEAM1100
          GOTO      TEAM9900                * invalid
.
.         copy doctor details
.
TEAM1000  MOVE      OPSEDCC1,OPDBDCC1       * doctor code 1
          MOVE      OPSEDCC2,OPDBDCC2       * doctor code 2
          MOVE      OPSEDCC3,OPDBDCC3       * doctor code 3
          MOVE      OPSEDCC4,OPDBDCC4       * doctor code 4
          MOVE      OPSEDCC5,OPDBDCC5       * doctor code 5
          MOVE      OPSEDCC6,OPDBDCC6       * doctor code 6
          MOVE      OPSETYD1,OPDBTYD1       * doctor type 1
          MOVE      OPSETYD2,OPDBTYD2       * doctor type 2
          MOVE      OPSETYD3,OPDBTYD3       * doctor type 3
          MOVE      OPSETYD4,OPDBTYD4       * doctor type 4
          MOVE      OPSETYD5,OPDBTYD5       * doctor type 5
          MOVE      OPSETYD6,OPDBTYD6       * doctor type 6
          GOTO      TEAM5000
.
.         copy nurse details
.
TEAM1100  MOVE      OPSENUC1,OPDBNUC1       * nurse code 1
          MOVE      OPSENUC2,OPDBNUC2       * nurse code 2
          MOVE      OPSENUC3,OPDBNUC3       * nurse code 3
          MOVE      OPSENUC4,OPDBNUC4       * nurse code 4
          MOVE      OPSENUC5,OPDBNUC5       * nurse code 4
          MOVE      OPSENUC6,OPDBNUC6       * nurse code 4
          MOVE      OPSETYN1,OPDBTYN1       * nurse type 1
          MOVE      OPSETYN2,OPDBTYN2       * nurse type 2
          MOVE      OPSETYN3,OPDBTYN3       * nurse type 3
          MOVE      OPSETYN4,OPDBTYN4       * nurse type 4
          MOVE      OPSETYN5,OPDBTYN5       * nurse type 4
          MOVE      OPSETYN6,OPDBTYN6       * nurse type 4
.
.         have a valid team in oprdebfd values
.         check that entered doctor or nurse exists in team
.
TEAM5000  BRANCH    MOPTION,TEAM5100,TEAM6000
          GOTO      TEAM9900                * invalid
.
.         determine if entered doctor is in team
.
TEAM5100  MOVE      ONE,FORM1
.
TEAM5200  LOAD      DIM6,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3:
                               OPDBDCC4,OPDBDCC5,OPDBDCC6
.
          MATCH     CODEDOCT,DIM6
          GOTO      TEAM5300 IF EQUAL       * doctor found
.
          ADD       ONE,FORM1
          COMPARE   SEVEN,FORM1
          GOTO      TEAM9900 IF EQUAL       * invalid
          GOTO      TEAM5200                * try next code
.
.         have found required doctor, get their type
.
TEAM5300  LOAD      DIM3,FORM1,OPDBTYD1,OPDBTYD2,OPDBTYD3:
                               OPDBTYD4,OPDBTYD5,OPDBTYD6
.
          MOVE      SP20,OPPERDC1
          MOVE      SP20,OPPERDC2
          MOVE      SP20,DOCSULEV
          MATCH     SP3,DIM3
          GOTO      TEAM9800 IF EQUAL       * no type set up
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSO,ANSP,DIM3
          CALL      RDCODE1
          COMPARE   TWO,OPCNR53A
          GOTO      TEAM5310 IF EQUAL
          MOVE      TDESC,OPPERDC1
          GOTO      TEAM5400
.
TEAM5310  MOVE      TDESC,OPPERDC2
.
.         now get the doctor's supervision level
.
TEAM5400  MOVE      SP3,DIM3
          LOAD      DIM3,FORM1,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                               OPDBLVD5,OPDBLVD6
.
          MATCH     SP3,DIM3
          GOTO      TEAM9800 IF EQUAL       * no type set up
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSO,ANSS,DIM3
          CALL      RDCODE1
          MOVE      TDESC,DOCSULEV
          GOTO      TEAM9800
.
.         determine if entered nurse is in team
.
TEAM6000  MOVE      ONE,FORM1
.
TEAM6100  LOAD      DIM6,FORM1,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5:
                               OPDBNUC6
.
          MATCH     CODENURS,DIM6
          GOTO      TEAM6200 IF EQUAL       * nurse found
.
          ADD       ONE,FORM1
          COMPARE   SEVEN,FORM1
          GOTO      TEAM9900 IF EQUAL       * run out of codes
          GOTO      TEAM6100                * try next code
.
.         have found required nurse, get their type
.
TEAM6200  LOAD      DIM3,FORM1,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4,OPDBTYN5:
                               OPDBTYN6
.
          MOVE      SP20,OPPERDC1
          MOVE      SP20,OPPERDC2
          MATCH     SP3,DIM3
          GOTO      TEAM9800 IF EQUAL       * no type set up
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSO,ANSN,DIM3
          CALL      RDCODE1
          COMPARE   TWO,OPCNR53A
          GOTO      TEAM6210 IF EQUAL
          MOVE      TDESC,OPPERDC1
          GOTO      TEAM9800
.
TEAM6210  MOVE      TDESC,OPPERDC2
.
TEAM9800  MOVE      FALSE,EXIT              * valid details
          GOTO      TEAM9999
.
TEAM9900  MOVE      ONE,EXIT                * invalid team
.
TEAM9999  RETURN
+
.*********************************************************************
.*                  PDET0000                    Called by : xxxx0000 *
.*        Print the details about an operation                       *
.*********************************************************************
PDET0000  UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,DIM8             * operation date
          MOVE      CPCDATE,DIM10
.
          MOVE      OPDADES1,DESC1          * shorten operation descriptions
          MOVE      OPDADES2,DESC2
          MOVE      OPDADES3,DESC3
.
.         get start time, end time, anaesthetic start time
.
          IF        OPSRGFLG = 1
.
            CALL      RCVT0000              * Determine time in recovery
.
            LOAD      TIMEA,OPCNFTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                                      OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                                      OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                                      OPARTIDP,OPARTETC
.
            LOAD      TIMEB,OPCNTTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                                      OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                                      OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                                      OPARTIDP,OPARTETC
.
            MOVE      TIMEB,STIMEB
            CALL      DURN0000
            ADD       DURATION,TIMEUSED
          ELSE
            ADD       OPDCDURA,TIMEUSED       * add to total time used
            LOAD      TIMEA,OPCNFOTM,OPDCOTM1,OPDCOTM2,OPDCOTM3
            LOAD      TIMEB,OPCNTOTM,OPDCOTM1,OPDCOTM2,OPDCOTM3,OPDCOTM4
          ENDIF
          LOAD      TIMEC,OPCNANST,OPDAPRE1,OPDAPRE2,OPDAPRE3,OPDAPRE4,OPDAPRE5
.
.         get operation type description and update temp file
.
          MOVE      SP20,OPTYPDC1           * clear display descriptn - Layout 1
          MOVE      SP20,OPTYPDC2           * clear display descriptn - Layout 2
          MOVE      "zzz",KEY3              * default to zzz incase no/inv code
.
          MATCH     SP3,OPDATYPE
          GOTO      PDET1000 IF EQUAL       * no code to display
.
          MOVE      SP20,TDESC              * clear description
          PACK      KEY5,ANSO,ANSY,OPDATYPE
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,PDET1000          * invalid code
          COMPARE   TWO,OPCNR53A 
          IF        @EQUAL
            PACK      KEY18,OPPERDC2,OPDATYPE * type & pers. type are the key
          ELSE
            PACK      KEY18,OPPERDC1,OPDATYPE
          ENDIF
          COMPARE   TWO,OPCNR53A            * Report Layout 2 ?
          GOTO      PDET0500 IF EQUAL       * yes
          MOVE      TDESC,OPTYPDC1          * no
          GOTO      PDET1000
.
PDET0500  MOVE      TDESC,OPTYPDC2
.
.         maintain temp file
.
PDET1000  RESET     KEY18
          READ      TEMPFILE,KEY18;XTYPE,XCOUNT,XTIME,PERTY
          GOTO      PDET2000 IF OVER        * write a record
.
.         update temp file
.
          BRANCH    OPSRGFLG,PDET1500
.
          IF        OPDCNOOP=0
            MOVE      ONE,OPDCNOOP          * increment number of operations
          ENDIF
.
          ADD       OPDCNOOP,XCOUNT         * increment counter
          ADD       OPDCDURA,XTIME          * add to time
          UPDATE    TEMPFILE;XTYPE,XCOUNT,XTIME,PERTY
          GOTO      PDET3000
.
PDET1500  IF        OPSGNOOP=0
            MOVE      ONE,OPSGNOOP          * increment number of operations
          ENDIF
          ADD       OPSGNOOP,XCOUNT
          ADD       DURATION,XTIME
          UPDATE    TEMPFILE;XTYPE,XCOUNT,XTIME,PERTY
          GOTO      PDET3000
.
.         write a new record to temp file
.
PDET2000  BRANCH    OPSRGFLG,PDET2500
.
          IF        OPDCNOOP=0
            MOVE      ONE,OPDCNOOP          * increment number of operations
          ENDIF
.
          MOVE      OPDCNOOP,XCOUNT         * count set to one
          MOVE      OPDCDURA,XTIME          * the time
          UNPACK    KEY18,PERTY,XTYPE
          RESET     KEY18
          WRITE     TEMPFILE,KEY18;XTYPE,XCOUNT,XTIME,PERTY
          GOTO      PDET3000
.
PDET2500  IF        OPSGNOOP=0
            MOVE      ONE,OPSGNOOP          * increment number of operations
          ENDIF
.
          MOVE      OPSGNOOP,XCOUNT
          MOVE      DURATION,XTIME
          UNPACK    KEY18,PERTY,XTYPE
          RESET     KEY18
          WRITE     TEMPFILE,KEY18;XTYPE,XCOUNT,XTIME,PERTY
.
.         get the theatre code
.
PDET3000  MOVE      OPSETHET,DIM6           * default to session file theatre
          MATCH     SP6,OPDATHET
          GOTO      PDET4000 IF EQUAL       * use the session value
          MOVE      OPDATHET,DIM6           * the theatre
.
.         get U/R Number for patient name
.
PDET4000  MOVE      "No PMI details",PATNAME1
          MOVE      "No PMI details",PATNAME2
          MOVE      SP1,PSEX
          MOVE      ZERO,PSAGEYRS
.
          MOVE      OPDAADMN,KEY8
          CALL      RDMISS1                 * admission file
          COMPARE   ZERO,OVRCD
          GOTO      PDET4100 IF EQUAL       * have an admission record
.
          CALL      RDBKREC1                * try the booking file
          BRANCH    OVRCD,PDET5000          * invalid name
          MOVE      BKREURNO,AURNO          * set UR number
.
.         get PMI details
.
PDET4100  MOVE      AURNO,KEY8
          MATCH     ZEROUR,KEY8
          GOTO      PDET4200 IF EQUAL       * zero U/R, try pram file
.
          CALL      RDMAST1                 * master file
          BRANCH    OVRCD,PDET5000          * invalid details
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          CALL      CALCAGE                 * calculate patient age
          GOTO      PDET4300
.
PDET4200  MOVE      OPDAADMN,KEY8
          CALL      RDPRAM1                 * pre-admission file
          BRANCH    OVRCD,PDET5000          * no PMI details
.
PDET4300  MOVE      ONE,PACFRMT             * get patients name
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,ANS
          MOVE      ANS,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
.
          COMPARE   TWO,OPCNR53A            * Report Layout 2 ?
          GOTO      PDET4400 IF EQUAL       * yes
          MOVE      PACFNAME,PATNAME1       * no
          GOTO      PDET5000
.
PDET4400  MOVE      PACFNAME,PATNAME2
.
.         display the details
.
PDET5000  BRANCH    OPCNR53A,PDET6000,PDET7000
          GOTO      PDET9999
.
.         Report Layout 1
.
PDET6000  IF        OPSRGFLG = 1 
            PRINT     *1,"|",DIM10:                * line 1
                      *12,"| ",DESC1:
                      *45,"| ",DURATION:
                      *52,"| ",OPTYPDC1:
                      *75,"| ",DIM6:
                      *84,"| ",AURNO:
                      *95,"| ",PATNAME1:
                      *120,"|  ",PSEX:
                      *126,"| ",PSAGEYRS,*132,"|":
                      *N,"|  ",OPDACLIN:            * line 2
                      *12,"| ",DESC2:
                      *45,"| Op",OPSGNOOP:
                      *52,"| ",OPPERDC1:
                      *75,"|",*84,"| ",OPDAADMN:
                      *95,"| Case ",OPDACASE,"    Team ",OPSGTMNO:
                      *120,"|",*126,"|",*132,"|";
          ELSE
            PRINT     *1,"|",DIM10:                * line 1
                      *12,"| ",DESC1:
                      *45,"| ",OPDCDURA:
                      *52,"| ",OPTYPDC1:
                      *75,"| ",DIM6:
                      *84,"| ",AURNO:
                      *95,"| ",PATNAME1:
                      *120,"|  ",PSEX:
                      *126,"| ",PSAGEYRS,*132,"|":
                      *N,"|  ",OPDACLIN:            * line 2
                      *12,"| ",DESC2:
                      *45,"| Op",OPDCNOOP:
                      *52,"| ",OPPERDC1:
                      *75,"|",*84,"| ",OPDAADMN:
                      *95,"| Case ",OPDACASE,"    Team ",OPDCTEAM:
                      *120,"|",*126,"|",*132,"|"; 
          ENDIF
.
          ADD       TWO,CLNO                * add to line counter
          MATCH     SP30,DESC3
          GOTO      PDET9900 IF EQUAL       * no desc to display
.                   
          PRINT     *N,"| ",*12,"| ",DESC3,*45,"|",*52,"|",*75,"|",*84,"| ":
                    *95,"| ",*120,"|",*126,"|",*132,"|";
.
          ADD       ONE,CLNO                * add to line counter
          GOTO      PDET9900
. 
.         Report Layout 2
.
PDET7000  IF        OPSRGFLG = 1  
            PRINT     *1,"|",DIM10:                * line 1
                      *12,"|",DESC1:
                      *66,"| ",DURATION:
                      *72,"|",OPTYPDC2:
                      *88,"|",DIM6:
                      *95,"|",AURNO:
                      *104,"|",PATNAME2:
                      *124,"| ",PSEX:
                      *128,"|",PSAGEYRS,"|":
                      *N,"| ",OPDACLIN:            * line 2
                      *12,"|",DESC2:
                      *66,"|",TIMEA:
                      *72,"|",OPPERDC2:
                      *88,"|",TIMEC:
                      *95,"|",OPDAADMN:
                      *104,"|Case ",OPDACASE,"    Team ",OPSGTMNO:
                      *124,"|",*128,"|",*132,"|":
                      *N,"|":                     * line 3
                      *12,"|",DESC3:
                      *66,"|",STIMEB:
                      *72,"|",DOCSULEV,*88,"|",*95,"| ",*104,"|":
                      "Operations ",OPSGNOOP,*124,"|",*128,"|",*132,"|";
          ELSE
            PRINT     *1,"|",DIM10:                * line 1
                      *12,"|",DESC1:
                      *66,"| ",OPDCDURA:
                      *72,"|",OPTYPDC2:
                      *88,"|",DIM6:
                      *95,"|",AURNO:
                      *104,"|",PATNAME2:
                      *124,"| ",PSEX:
                      *128,"|",PSAGEYRS,"|":
                      *N,"| ",OPDACLIN:            * line 2
                      *12,"|",DESC2:
                      *66,"|",TIMEA:
                      *72,"|",OPPERDC2:
                      *88,"|",TIMEC:
                      *95,"|",OPDAADMN:
                      *104,"|Case ",OPDACASE,"    Team ",OPDCTEAM:
                      *124,"|",*128,"|",*132,"|":
                      *N,"|":                     * line 3
                      *12,"|",DESC3:
                      *66,"|",STIMEB:
                      *72,"|",DOCSULEV,*88,"|",*95,"| ",*104,"|":
                      "Operations ",OPDCNOOP,*124,"|",*128,"|",*132,"|";
          ENDIF
.
          ADD       THREE,CLNO              * add to line counter
.
PDET9900  PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*"
          ADD       ONE,CLNO                * add to line counter
.
          IF        OPDCNOOP=0
            MOVE      ONE,OPDCNOOP          * increment number of operations
          ENDIF
          IF        OPSGNOOP=0
            MOVE      ONE,OPSGNOOP          * increment number of operations
          ENDIF
.
          IF        OPSRGFLG = 1
            ADD       OPSGNOOP,OPERTOTL
          ELSE
            ADD       OPDCNOOP,OPERTOTL       * add on to operation total
          ENDIF
.
PDET9999  RETURN
+
.*********************************************************************
.*                  DURN0000                    Called by : NEWA0000 *
.*        Calculate a duration given two times                       *
.*        Para's  : TIMEA         the start time (HH:MM)             *
.*                  TIMEB         the end time   (HH:MM)             *
.*        Returns : DURATION      the duration (minutes)             *
.*********************************************************************
DURN0000  MOVE      ZERO,DURATION
          MATCH     TIMEB,TIMEA
          GOTO      DURN9999 IF EQUAL       * no duration
          GOTO      DURN1000 IF LESS        * times not over midnight
.
.         operation went over midnight so make end time larger
.         eg 22:00 to 01:00 becomes 22:00 to 25:00 which gives 3 hours duration
.
          UNPACK    TIMEB,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2
          ADD       "24",FORM2
          MOVE      FORM2,CHOUR
          PACK      TIMEB,CHOUR,ANS,CMIN
.
.         get end time as minutes
.
DURN1000  UNPACK    TIMEB,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM4A            * set up work variable
          MULT      "60",FORM4A             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ADD       FORM2,FORM4A            * the total minutes of end time
.
.         get start time as minutes
.
          UNPACK    TIMEA,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM4B            * set up work variable
          MULT      "60",FORM4B             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ADD       FORM2,FORM4B            * the total minutes of start time
.
.         duration = end minutes - start minutes
.
          SUB       FORM4B,FORM4A           * the duration
          MOVE      FORM4A,DURATION         * the duration
.
DURN9999  RETURN
+
.*********************************************************************
.*                  CTMP0000                    Called by : REPT0000 *
.*        Create the temp file which is used to count operation types*
.*********************************************************************
CTMP0000  CALL      DTMP0000                * delete temp file
.
          PACK      CMDLINE,ISBUILD,CFNAME,INDEX
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,CFNAME         * open temp file
.
CTMP9999  RETURN
+
.*********************************************************************
.*                  DTMP0000                    Called by : xxxx0000 *
.*        Delete the temp file which is used to count operation types*
.********************************************************************* 
DTMP0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO          * call overcond if doesnt exist
          OPEN      TEMPFILE,CFNAME         * open tempfile
          TRAPCLR   IO                      * clear the IO trap
          BRANCH    OVRCD,DTMP9999          * no file to delete
.
.         delete the temp file
.
          CLOSE     TEMPFILE                * close temp file
          PACK      CMDLINE,ISERASE,CFNAME
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
DTMP9999  RETURN
+
.*********************************************************************
.*                  SUMM0000                    Called by : REPT8000 *
.*        Finish off the report by looping through temp file         *
.********************************************************************* 
SUMM0000  MOVE      SP20,KEY18
          READ      TEMPFILE,KEY18;ANS
.
          COMPARE   "56",CLNO
          CALL      PAGE0000 IF NOT LESS    * print a new page
.
          PRINT     *N,*62,"Operations",*77,"Time Used";
          ADD       ONE,CLNO                * add to line counter
          COMPARE   "55",CLNO               * purpose for this check is to print
          GOTO      SUMM1000 IF LESS          at least one sub total line after
          MOVE      "54",CLNO                 the summary headings
.
.         loop through temp file
.
SUMM1000  READKS    TEMPFILE;XTYPE,XCOUNT,XTIME,PERTY
          GOTO      SUMM5000 IF OVER
.
          MOVE      "Unknown Type",TDESC
          PACK      KEY5,ANSO,ANSY,XTYPE
          CALL      RDCODE1
.
          COMPARE   "55",CLNO
          GOTO      SUMM2000 IF LESS
          CALL      PAGE0000
          PRINT     *N,*62,"Operations",*77,"Time Used";
          ADD       ONE,CLNO                * add to line counter
.
SUMM2000  MATCH     PERTY,PRTYP
          GOTO      SUMM3000 IF EQUAL
.
          IF        NURSFLAG = ZERO
            PACK      PTCDKEY2,ANSO,ANSP,PERTY,SP30
            MOVE      TDESC,OPTYPDC1
            CALL      RDCODE2
            CALL      RDKCODE2
            BRANCH    OVRCD,SUMM1000
.
            PRINT     *N,*N,*13,"OPERATING PERSONNEL CODE: ",ACODE,*44:
                      PERTY,*N,*39,OPTYPDC1,*60,":",*66,XCOUNT,*78,XTIME;
          ELSE
            PACK      PTCDKEY2,ANSO,ANSN,PERTY,SP30
            CALL      RDCODE2
            CALL      RDKCODE2
            BRANCH    OVRCD,SUMM1000
            PRINT     *N,*N,*17,"OPERATING NUSRE CODE: ",ACODE,*44:
                      PERTY,*N,*39,TDESC,*60,":",*66,XCOUNT,*78,XTIME;
          ENDIF
          ADD       ONE,CLNO
          MOVE      PERTY,PRTYP
          GOTO      SUMM1000

SUMM3000  PRINT     *N,*39,TDESC,*60,":",*66,XCOUNT,*78,XTIME;
          ADD       ONE,CLNO                * add to line counter
          GOTO      SUMM1000
.         
SUMM5000  PRINT     *N,*N,*44,"Operation Total : ",*66,OPERTOTL,*78,TIMEUSED:
                    *N,"Time = Operation Duration":
                    *N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*V2LON:
                    "** Report Generation Completed ** ";
          CALL      HITENTER
          CALL      DTMP0000                * delete temp file
.
SUMM9999  RETURN
+
.********************************************************************* 
.
          INC       STD001IO                * standard Includes
.
          INC       CALCAGE
          INC       OPRNURDS                * nurses file scan
          INC       PATDOCKY                * doctors file scan
          INC       OPRECOVT                * determine time in recovery
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       BOKRC1IO/INC            * bookings file
          INC       OPRARDIO/INC
          INC       OPRDEAIO/INC            * operation details file
          INC       OPRDEBIO/INC            * operation team file
          INC       OPRDECIO/INC            * operation usage file
          INC       OPRNURIO/INC            * nurses file
          INC       OPRSESIO/INC            * sessions file
          INC       OPRSRGIO/INC            * new operation details file
          INC       OPRTSMIO/INC            * new operation team file
. 
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       PATMA1IO/INC            * master file
          INC       PATMI1IO/INC            * admissions file
          INC       PMSVX1IO/INC            * admissions file
          INC       PATPR1IO/INC            * pre-admissions file
+

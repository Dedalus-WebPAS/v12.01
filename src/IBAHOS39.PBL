.******************************************************************************
.* System   : PATIENT MANAGEMENT                                              *
.* Program  : IBAHOS39                                                        *
.* Desc     : Midnight Cenus Report (N.Z. Only)                               *
.******************************************************************************
.* Date     : 25/08/1989                                                      *
.* Author   : K.Peak                                                          *
.* Mods.    :                                                                 *
.*        V12.00.01 30/05/2025 Jacob Jackson     TSK 0955096                  *
.*                  Alphanumeric changes                                      *
.*        V10.03.01 27/06/2013 Ebon Clements  CAR 287511                      *
.*                  Changes for WEXTN and NSEXTN length change from 4 to 20   *
.*        V10.02.01 25/06/2011 Steve Armstrong    CAR 240722                  *
.*                  Mods to cater for changes to PATLOCFD.                    *
.*                       - LSNAME & LGNAME extended to 40 chars.              *
.*                       - PTLCGNM2 added.                                    *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*        V9.04.01  07/10/2005  Mike Laming   CAR 77057                       *
.*                  Add test for In-active Wards (do not print)               *
.******************************************************************************
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B02 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*        V5.07.B01 12/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.******************************************************************************
.*        V5.01.02  08/11/1989  K.Peak           SRF 102951                   *
.*                  Fixed spelling of separate                                *
.*        V5.01.03  27/08/1990  D.Di.Paola       SRF 106232                   *
.*                  Fixed extraction of patient condition.                    *
.*                  Put in check for system parameter if patient condition    *
.*                  codes file driven or free format.                         *
.*        V5.01.121 27/11/1990  Glen Hobby                                    *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.122 21/05/1991  Steve O'Gorman                                *
.*                  Fixed errors from the new Compiler                        *
.*        V5.01.123 30/07/1992  i chung          SRF 116034                   *
.*                  Fixed not to print extra ward for Particular Ward opt.    *
.*                  Added "?" facility for particular ward/nursing stat. opts.*
.*        V5.01.124 05/04/1993  Steve O'Gorman                                *
.*                  Modified to allow for multi hospitals                     *
.*        V5.01.125 14/04/1993  ROD                                           *
.*                  Fixed printing of Hospital name                           *
.*        V5.01.126 11/06/93  J.Tan      SRF 121660                           *
.*                  Recompiled for PATWRDDS (check for active wards)          *
.*        V5.01.127 07/07/1993  i chung          SRF 123737                   *
.*                  Fixed inappropriate display of message "Ward in another   *
.*                  Hospital" for particular ward option                      *
.*        V5.01.128 08/10/93  Whirlpool          SRF 125064                   *
.*                  Fixed display of date in the heading so it is current     *
.*        V5.01.129 25/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.130 06/07/1994  Rob Leonard  SRF 440978                       *
.*                  Fixed Hospital Keyin                                      *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATLOCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATNOBFD/INC
          INC       PATNURFD/INC
          INC       PATWR1FD/INC
+
.**********************************************************************
.*                        VARIABLES                                   *
.**********************************************************************
AJDAY     FORM      3                  * julian admission day
.
CDESC     DIM       20
CJDAY     FORM      3                  * julian current day
.
DPSKIP    DIM       3                  * 0=sep page per ward, 1=no sep page
DHHMM     DIM       5
DOCNAME   DIM       22                 * doctorname title.initial.surname
DMSTAT    DIM       9
DSEX      DIM       6
DOCDESC   DIM       10                 * speciality description (doctor type)
.
DIM4      DIM       4
DIM10     DIM       10
DIM11     DIM       11
DIM16     DIM       16
.
GRDPAT    FORM      4                  * grand total of patients
GRDSTBY   FORM      4                  * grand total of standby patients
.
IBCNMHOS  FORM      1
.
PSKIP     FORM      1                  * 0=sep page per ward, 1=no sep page
PHDAYS    FORM      3
PNAME     DIM       27                 * psname.title.pgname
PATCOND   DIM       20                 * Patient condition description
.
RECFLG    FORM      1                  * Print totals flag
.
SAVNSTAT  DIM       3                  * Nursing station
SAVNDESC  DIM       30                 * Nursing station description
SAVNSIST  DIM       30                 * Sister in charge
SAVNEXTN  DIM       20                 * Nursing extension number
SAWARD    DIM       3                  * Ward
STBYFLG   FORM      1                  * Standby patient flag
SWARD     DIM       3                  * Saved ward
SBED      DIM       3                  * Saved bed 
.
TOTPAT    FORM      3                  * total patients
TOTSTBY   FORM      3                  * total standby patients
.
USRDESC   DIM       10                 * user define 7 description  (AUSR7)
WHOLWRD   FORM      1
+
.**********************************************************************
.*                        CONSTANTS                                   *
.**********************************************************************
CODECO    INIT      "CO"
CODEDT    INIT      "DT"
CODEM     INIT      "M "
CODEU7    INIT      "U7"
.
PRGID     INIT      "IBAHOS39"
PRGNAM    INIT      "Midnight Census"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
ML0000    CALL      INIT0000              * Intialization
.
ML1000    CALL      CLR0000               * Clear Variables
.
          CALL      HOSP0000              * Keyin Hospital Code
          BRANCH    EXIT,ML9000
          DISPLAY   *P20:3,PTHSNAME;
.
          CALL      OPTN0000              * Select Option
          BRANCH    EXIT,ML9000
          BRANCH    FORM1,ML2000,ML3000,ML4000,ML5000
.
ML2000    CALL      DOPT0000              * Display which option
          CALL      KWR0000               * Paticular Ward
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
ML3000    CALL      DOPT0000              * Display which option
          CALL      KNU0000               * Paticular Nursing Station
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
ML4000    CALL      DOPT0000              * Display which option
          CALL      ANS0000               * All Nursing Stations
          GOTO      ML6000
.
ML5000    CALL      DOPT0000              * Display which option
          CALL      AWR0000               * All Wards
.
ML6000    CALL      END0000               * End of Report
          GOTO      ML1000
.
.         ...The End
.
ML9000    CHAIN     PGM
+
.**********************************************************************
.*                         INIT0000                                   *
.*                       Initialization                               *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patlocaf";
          OPEN      PATLOCA1,"patlocaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patnursm";
          OPEN      PATNURS1,"patnursm"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN5;*188,CMATRN
          READ      CONTROLF,TEN9;*210,CPCFORM
.
INIT9999  RETURN
+
.**********************************************************************
.*                         CLR0000                                    *
.*                       Clear Variables                              *
.**********************************************************************
CLR0000   MOVE      ZERO,CPAGENO
          MOVE      ZERO,TOTPAT           * Total Patients
          MOVE      ZERO,TOTSTBY          * Total Standby patients
          MOVE      ZERO,GRDPAT           * Grand Patients Total
          MOVE      ZERO,GRDSTBY          * Grand Standby Patients Total
          MOVE      ZERO,RECFLG           * Print Totals Flag
          MOVE      ZERO,STBYFLG          * standby Flag
          MOVE      "60",CLNO
CLR9999   RETURN
+ 
.**********************************************************************
.*                             OPTN0000                               *
.*                        Main OPTioN Screen                          *
.**********************************************************************
OPTN0000  MOVE      FALSE,EXIT
          CALL      SCR0000
.
OPTN1000  KEYIN     *P17:10,*EL,*DV,UNDLN1,*P17:10,*V2LON,FORM1;
          COMPARE   ZERO,FORM1
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    FORM1,OPTN9999,OPTN9999,OPTN9999,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                             SCR0000                                *
.*                          SCReen options                            *
.**********************************************************************
SCR0000   HLINE     *G37,2,48,80
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                         *P1:5,*V2LON,ONE,*HOFF," = Particular Ward":
                         *P1:6,*V2LON,TWO,*HOFF," = Particular Nursing Station":
                       *P1:7,*V2LON,THREE,*HOFF," = All Nursing Stations":
                        *P1:8,*V2LON,FOUR,*HOFF," = All Wards":
                        *P1:10,"Select Option : ";
          RETURN
+
.********************************************************************** 
.*                              DOPT0000                              *
.*                        Display which option                        *
.**********************************************************************
DOPT0000  BRANCH    FORM1,DOPT1000,DOPT2000,DOPT3000,DOPT4000
.
DOPT1000  DISPLAY   *P48:2,*V2LON,"- Particular Ward ";
          GOTO      DOPT9999
.
DOPT2000  DISPLAY   *P48:2,*V2LON,"- Paticular Nursing Station ";
          GOTO      DOPT9999
.
DOPT3000  DISPLAY   *P48:2,*V2LON,"- All Nursing Station ";
          GOTO      DOPT9999
.
DOPT4000  DISPLAY   *P48:2,*V2LON,"- All Wards ";
.
DOPT9999  RETURN
+
.********************************************************************** 
.*                              KWR0000                               *
.*                           Paticular Ward                           *
.**********************************************************************
KWR0000   MOVE      FALSE,EXIT
          KEYIN     *P1:13,*EF,"Enter Ward Number : ",*P40:13,"( ",*V2LON,"?":
                    *HOFF," for Scan )",*P21:13,*V2LON,SAWARD,*EL;
          RESET     SAWARD
          GOTO      KWR9000 IF EOS
.
          ENDSET    SAWARD
          APPEND    SP3,SAWARD
          RESET     SAWARD
.
          MATCH     SP3,SAWARD
          GOTO      KWR9000 IF EQUAL
.
          CMATCH    QUEST,SAWARD
          GOTO      KWR1000 IF NOT EQUAL
          CALL      GETSCR00
          CALL      PATWRDDS
          CALL      PUTSCR00
          GOTO      KWR0000
.
KWR1000   PACK      KEY6 WITH SAWARD,SP6
          CALL      RDWARD1
          BRANCH    OVRCD OF KWR8000
.
          IF        IBCNMHOS=1
            MATCH     PTHSHOSP,PTWDHOSN     * Different Hospital ?
            IF        !@EQUAL
              DISPLAY   *P1:24,*EL,*B,*V2LON,"Ward in another Hospital.    ";
              CALL      HITENTER
              GOTO      KWR0000
            ENDIF
          ENDIF
.
          MOVE      ONE,PSKIP               * No page separation
          DISPLAY   *P30:13,*EL,WBDESC;
.
          COMPARE   ONE,WACTIVE
          GOTO      KWR1500 IF NOT EQUAL
          DISPLAY   *P1:22,*EL,*B,"This is an in-active Ward";
.
KWR1500   CALL      CONTQST                 * Ok to continue ?
          BRANCH    CEXIT,KWR2000,KWR0000,KWR9000
.
KWR2000   MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
          GOTO      KWR5000
.
KWR3000   CALL      RDKWARD1
          COMPARE   ZERO,OVRCD
          GOTO      KWR4000 IF EQUAL
          CALL      TOT0000                 * print totals
          GOTO      KWR9999
.
KWR4000   MATCH     WWARD,SAWARD
          GOTO      KWR5000 IF EQUAL
          CALL      TOT0000                 * print totals
          GOTO      KWR9999
.
KWR5000   MATCH     SP3,WBED
          GOTO      KWR7000 IF NOT EQUAL
.
          COMPARE   ONE,WINPUT              * Ward only ?
          GOTO      KWR3000 IF NOT EQUAL    * no
.
          PACK      KEY13 WITH WWARD,SP20
          CALL      RDSNOBE1
KWR6000   CALL      RDKNOBE1
          BRANCH    OVRCD OF KWR3000
.
          MATCH     WWARD,NBWARD
          GOTO      KWR3000 IF NOT EQUAL
          MOVE      NBADMNO,WADMNO
          MOVE      NBPLUR,WPLUR
          CALL      GDAT0000                * process record
          GOTO      KWR6000
.
KWR7000   CALL      GDAT0000                * process record
          GOTO      KWR3000
.
KWR8000   DISPLAY   *P1:24,*EL,*B,*V2LON,"Ward does not exist.    ";
          CALL      HITENTER
          GOTO      KWR0000
.
KWR9000   MOVE      TRUE,EXIT
.
KWR9999   RETURN
+
.********************************************************************** 
.*                              KNU0000                               *
.*                     Particular Nursing Station                     *
.**********************************************************************
KNU0000   MOVE      FALSE,EXIT
          KEYIN     *P1:13,*EF,"Enter Station Number : ",*P40:13,"( ",*V2LON:
                    "?",*HOFF," for Scan )",*P24:13,*V2LON,SAVNSTAT,*EL;
          RESET     SAVNSTAT
          GOTO      KNU9000 IF EOS
.
          ENDSET    SAVNSTAT
          APPEND    SP3,SAVNSTAT
          RESET     SAVNSTAT
.
          MATCH     SP3,SAVNSTAT
          GOTO      KNU9000 IF EQUAL
.
          CMATCH    QUEST,SAVNSTAT
          GOTO      KNU1000 IF NOT EQUAL
          CALL      PATNURDS
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          CALL      SCR0000
          DISPLAY   *P17:10,*V2LON,FORM1;
          GOTO      KNU0000
.
KNU1000   MOVE      SP3,NSTAT
          MOVE      SP3,NWARD
.
          PACK      KEY6 WITH SAVNSTAT,SP3
          CALL      RDNURS1
          BRANCH    OVRCD OF KNU8000
          DISPLAY   *P32:13,*EL,NSDESC;
          MOVE      NSDESC,SAVNDESC         * N/Station description
          MOVE      NSIST,SAVNSIST          * Sister in charge
          MOVE      NSEXTN,SAVNEXTN         * Ward extension
.
          CALL      SEP0000                 * separate page ?
.
          CALL      CONTQST
          BRANCH    CEXIT,KNU2000,KNU0000,KNU9000
.
KNU2000   MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
KNU3000   CALL      RDKNURS1
          COMPARE   ZERO,OVRCD
          GOTO      KNU3100 IF EQUAL
          CALL      TOT0000                 * print totals
          GOTO      KNU9999
.
KNU3100   MATCH     NSTAT,SAVNSTAT          * same Nursing Station ?
          GOTO      KNU4000 IF EQUAL        * yes
          CALL      TOT0000                 * no - print totals
          GOTO      KNU9999
.
KNU4000   PACK      KEY6 WITH NWARD,SP3
          CALL      RDWARD1
          BRANCH    OVRCD OF KNU3000
.
          BRANCH    WINPUT OF KNU6000
          CALL      RDKWARD1                * get the bed
          BRANCH    OVRCD OF KNU3000
          GOTO      KNU6000
.
KNU5000   CALL      RDKWARD1
          COMPARE   ZERO,OVRCD
          GOTO      KNU5100 IF EQUAL
          CALL      TOT0000                 * print totals
          GOTO      KNU3000
.
KNU5100   MATCH     WWARD,NWARD             * same Ward ?
          GOTO      KNU6000 IF EQUAL        * yes
          CALL      TOT0000                 * no - print totals
          GOTO      KNU3000
.
KNU6000   MATCH     SP3,WBED
          GOTO      KNU7000 IF NOT EQUAL
.
          COMPARE   ONE,WINPUT              * Ward only ?
          GOTO      KNU3000 IF NOT EQUAL    * no
.
          PACK      KEY13 WITH WWARD,SP20
          CALL      RDSNOBE1
KNU6100   CALL      RDKNOBE1
          BRANCH    OVRCD OF KNU5000
.
          MATCH     WWARD,NBWARD
          GOTO      KNU5000 IF NOT EQUAL
          MOVE      NBADMNO,WADMNO
          MOVE      NBPLUR,WPLUR
          CALL      GDAT0000                * process record
          GOTO      KNU6100
.
KNU7000   CALL      GDAT0000                * process record
          GOTO      KNU5000
.
KNU8000   DISPLAY   *P1:24,*EL,*B,*V2LON,"Station does not exist.    ";
          CALL      HITENTER
          GOTO      KNU0000
.
KNU9000   MOVE      TRUE,EXIT
.
KNU9999   RETURN
+
.********************************************************************** 
.*                              SEP0000                               *
.*                      Separate Page Required ?                      *
.**********************************************************************
SEP0000   KEYIN     *P1:16,*EL,"Would you like each Ward on a separate PAGE (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          RESET     ANS
          GOTO      SEP1000 IF EOS
          REP       UPPLOW,ANS
.
          MATCH     "Y",ANS
          GOTO      SEP2000 IF EQUAL
          MATCH     "N",ANS
          GOTO      SEP1000 IF EQUAL
          BEEP
          GOTO      SEP0000
.
SEP1000   MOVE      ONE,PSKIP               * No page separation
          DISPLAY   *P53:16,*EL,*V2LON,"No";
          GOTO      SEP9999
.
SEP2000   MOVE      ZERO,PSKIP              * Page separation    
          DISPLAY   *P53:16,*EL,*V2LON,"Yes";
.
SEP9999   RETURN
+
.********************************************************************** 
.*                              AWR0000                               *
.*                             All Wards                              *
.**********************************************************************
AWR0000   CALL      SEP0000               * Separate page ?
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
          MOVE      SP3,SAWARD
          MOVE      SP6,KEY6
          CALL      RDSWARD1
AWR0500   CALL      RDKWARD1                * read first ward record
          BRANCH    OVRCD,AWR9999
.
          MATCH     SP3,WBED
          IF        @EQUAL
            MOVE      WACTIVE,WHOLWRD
          ENDIF
          BRANCH    WHOLWRD,AWR0500         * bypass in-active wards
          BRANCH    WACTIVE,AWR0500         * bypass in-active beds 
.
          IF        IBCNMHOS=1
            MATCH     PTHSHOSP,PTWDHOSN     * Different Hospital
            GOTO      AWR1000 IF NOT EQUAL
          ENDIF
.
          BRANCH    WINPUT,AWR3000          * Ward only
          MOVE      WWARD,SAWARD            * save ward just read in
          GOTO      AWR3000
.
AWR1000   CALL      RDKWARD1                * read next ward record
          COMPARE   ZERO,OVRCD
          GOTO      AWR2000 IF EQUAL
          CALL      TOT0000                 * print totals
          GOTO      AWR9999
.
AWR2000   IF        IBCNMHOS=1
            MATCH     PTHSHOSP,PTWDHOSN     * Different Hospital ?
            GOTO      AWR1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP3,WBED
          IF        @EQUAL
            MOVE      WACTIVE,WHOLWRD
          ENDIF
          BRANCH    WHOLWRD,AWR1000         * bypass in-active wards
          BRANCH    WACTIVE,AWR1000         * bypass in-active beds
.
          MATCH     SP3,SAWARD
          GOTO      AWR2100 IF EQUAL
.
          MATCH     WWARD,SAWARD
          GOTO      AWR3000 IF EQUAL
          CALL      TOT0000                 * print totals
.
AWR2100   MOVE      WWARD,SAWARD
.
AWR3000   MATCH     SP3,WBED
          GOTO      AWR5000 IF NOT EQUAL
.
          MOVE      WACTIVE,WHOLWRD
          COMPARE   ONE,WINPUT              * Ward only ?
          GOTO      AWR1000 IF NOT EQUAL
.
          PACK      KEY13 WITH WWARD,SP10
          CALL      RDSNOBE1
AWR4000   CALL      RDKNOBE1
          BRANCH    OVRCD OF AWR1000
.
          MATCH     WWARD,NBWARD
          GOTO      AWR1000 IF NOT EQUAL
          MOVE      NBADMNO,WADMNO
          MOVE      NBPLUR,WPLUR
          CALL      GDAT0000                * process record
          GOTO      AWR4000
.
AWR5000   CALL      GDAT0000                * process record
          GOTO      AWR1000
.
AWR9999   RETURN
+
.********************************************************************** 
.*                              ANS0000                               *
.*                        All Nursing Stations                        *
.**********************************************************************
ANS0000   CALL      SEP0000                 * Separate page ?
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,"Station : ",*P20:24,"Ward :",*P40:24,"Bed :";
.
          MOVE      SP6,KEY6
          CALL      RDSNURS1
.
ANS1000   CALL      RDKNURS1
          COMPARE   ZERO,OVRCD
          GOTO      ANS2000 IF EQUAL
          CALL      TOT0000                 * print totals
          GOTO      ANS9999
.
ANS2000   MATCH     SP3,NWARD
          GOTO      ANS3000 IF NOT EQUAL
.
          DISPLAY   *P12:24,*V2LON,NSTAT;
          MOVE      NSTAT,SAVNSTAT          * Nursing Station
          MOVE      NSDESC,SAVNDESC         * N/Station description
          MOVE      NSIST,SAVNSIST          * Sister-in-Charge
          MOVE      NSEXTN,SAVNEXTN         * N/Station extension
          MOVE      "60",CLNO               * new page each Station
          GOTO      ANS1000
.
ANS3000   PACK      KEY6 WITH NWARD,SP3
          CALL      RDWARD1
          BRANCH    OVRCD OF ANS1000
.
          MATCH     SP3,WBED
          IF        @EQUAL
            MOVE      WACTIVE,WHOLWRD
          ENDIF
          BRANCH    WHOLWRD,ANS1000         * bypass in-active wards
          BRANCH    WACTIVE,ANS1000         * bypass in-active beds
.
          BRANCH    WINPUT OF ANS6000
          CALL      RDKWARD1
          BRANCH    OVRCD OF ANS1000
          GOTO      ANS6000
.
ANS4000   CALL      RDKWARD1
          COMPARE   ZERO,OVRCD
          GOTO      ANS5000 IF EQUAL
          CALL      TOT0000                 * print totals
          GOTO      ANS1000
.
ANS5000   MATCH     WWARD,NWARD
          GOTO      ANS6000 IF EQUAL
          CALL      TOT0000                 * print totals
          GOTO      ANS1000
.
ANS6000   MATCH     SP3,WBED
          GOTO      ANS8000 IF NOT EQUAL
.
          COMPARE   ONE,WINPUT
          GOTO      ANS1000 IF NOT EQUAL
.
          PACK      KEY13 WITH WWARD,SP20
          CALL      RDSNOBE1
ANS7000   CALL      RDKNOBE1
          BRANCH    OVRCD OF ANS4000
.
          MATCH     NBWARD,WWARD
          GOTO      ANS4000 IF NOT EQUAL
          MOVE      NBADMNO,WADMNO
          MOVE      NBPLUR,WPLUR
          CALL      GDAT0000                * process record
          GOTO      ANS7000
.
ANS8000   CALL      GDAT0000                * process record
          GOTO      ANS4000
.
ANS9999   RETURN
+
.********************************************************************** 
.*                              GDAT0000                              *
.*                          Process a Record                          *
.**********************************************************************
GDAT0000  MATCH     WWARD,SWARD
          GOTO      GDAT2000  IF EQUAL
.
          BRANCH    PSKIP OF GDAT1000
          COMPARE   ZERO,CPAGENO
          CALL      TOT0000 IF NOT EQUAL
          CALL      HEAD0000                * print headings
          GOTO      GDAT20000
.
GDAT1000  CALL      TOT0000                 * print totals
          COMPARE   "57",CLNO
          CALL      PAGE0000 IF LESS
.
.         Zero Admission Number indicates bed is empty
.
GDAT2000  COMPARE   "57",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MATCH     ZEROVISN,WADMNO
          GOTO      GDAT9000 IF EQUAL
          CALL      PRT0000                 * print data
.
.         Check for standby patients if bed is occupied
.
          MATCH     ZEROVISN,WSTBY
          GOTO      GDAT9999 IF EQUAL
.
          MOVE      WSTBY,WADMNO
          MOVE      ZERO,WPLUR
          MOVE      ONE,STBYFLG
          CALL      PRT0000                 * print data
          MOVE      ZERO,STBYFLG
          GOTO      GDAT9999
.
GDAT9000  CALL      PRT9000
.
GDAT9999  RETURN
.
.********************************************************************** 
.*                              TOT0000                               *
.*                           Print Totals                             *
.**********************************************************************
TOT0000   COMPARE   ZERO,CPAGENO
          GOTO      TOT1000 IF EQUAL
.
          COMPARE   ZERO,RECFLG             * print totals ?
          GOTO      TOT1000 IF EQUAL
.
          PRINT     *54,"Total Patients : ",TOTPAT:
                    *87,"Total Standby : ",TOTSTBY,*N
          ADD       TWO,CLNO
.
          ADD       TOTPAT,GRDPAT           * add for grand patient total
          ADD       TOTSTBY,GRDSTBY         * add for grand standby total
.
TOT1000   MOVE      ZERO,TOTPAT
          MOVE      ZERO,TOTSTBY
          MOVE      ZERO,RECFLG
          RETURN
+
.********************************************************************** 
.*                              GRD0000                               *
.*                       Print Grand Totals                           *
.**********************************************************************
GRD0000 COMPARE    "57",CLNO
        CALL       HEAD0000 IF NOT LESS
.
        PRINT     *N,*38,"GRAND TOTALS : ",*54,"Total Patients : ",GRDPAT:
                                           *87,"Total Standby : ",GRDSTBY:
                  *N,*38,"------------" 
        RETURN
+
.********************************************************************** 
.*                              NDAT0000                              *
.*                           No Data Available                        *
.**********************************************************************
NDAT0000  DISPLAY   *P1:24,*EL,*P25:24,*B,*V2LON:
                    "** No available data on file **",*W2;
          RETURN
+
.********************************************************************** 
.*                              END0000                               *
.*                           End of Report                            *
.**********************************************************************
END0000   COMPARE   ZERO,CPAGENO
          GOTO      END1000 IF NOT EQUAL
          CALL      NDAT0000               * no data available
          CALL      HEAD0000               * print headings            
          GOTO      END4000 
.
END1000   COMPARE   ZERO,RECFLG            * print Total Patients ?
          CALL      TOT0000 IF NOT EQUAL
.
          BRANCH    FORM1,END3000,END3000,END2000,END2000
.
END2000   CALL      GRD0000                * Grand Totals for "ALL" options only
.
END3000   DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "***  Census Generation Completed  ***",*W2;
.
END4000   PRINT     *N," ***  End of Report  ***"
.
END9999   RETURN              
+
.********************************************************************** 
.*                              PRT0000                               *
.*                             Print Data                             *
.**********************************************************************
PRT0000   COMPARE   ONE,CMATRN
          GOTO      PRT1000 IF NOT EQUAL
.
PRT1000   PACK      KEY9 WITH TWO,WADMNO  
          CALL      RDMISS2
          BRANCH    OVRCD OF PRT3500
          MOVE      SP10,USRDESC
          MATCH     SP3,AUSR7
          GOTO      PRT4000 IF EQUAL
.
          PACK      KEY5,CODEU7,AUSR7
          CALL      RDCODE1
          BRANCH    OVRCD,PRT4000
          MOVE      TDESC,USRDESC           * User Defined 7 description
          GOTO      PRT4000
.
.         Check if patient is on leave
.
PRT3500   PACK      KEY9 WITH FOUR,WADMNO 
          CALL      RDMISS2
          BRANCH    OVRCD OF PRT9000
.
PRT4000   MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF PRT9000
.
          MOVE      AURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,PRT9000
.
          MOVE      SP30,DOCNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF PRT4500
          MOVE      DTITL,DIM4
          MOVE      DGNAME,ANS
          MOVE      DSNAME,DIM16
          PACK      DOCNAME WITH DIM4,SP1,ANS,DOT,DIM16  * Dr`s name
.
PRT4500   MOVE      SP10,DOCDESC
          PACK      KEY5,CODEDT,DRTYPE
          CALL      RDCODE1
          BRANCH    OVRCD,PRT5000
          MOVE      TDESC,DOCDESC           * Dr. Type description (speciality)
.
PRT5000   MOVE      SP9,DMSTAT
          PACK      KEY5 WITH CODEM,PMSTAT
          CALL      RDCODE1
          BRANCH    OVRCD OF PRT6000
          MOVE      TDESC,DMSTAT
.
PRT6000   MOVE      SP3,ANS
          MOVE      PSNAME,DIM11
          MOVE      PGNAME,DIM10
          PACK      PNAME WITH DIM11,ANS,PTITL,ANS,DIM10    * Patient's name
.
PRT7000   MOVE      SP20,PATCOND
          PACK      KEY88,PTMASNAM,PMPXFNAM,AADMNO
          CALL      RDLOCA1
          BRANCH    OVRCD,PRT7700
          BRANCH    CPCFORM,PRT7200         * "1"= Codes File,"0"= Free Format
          MOVE      LPCONDD,PATCOND         * free format patient condition
          GOTO      PRT7300
.
PRT7200   MOVE      SP30,TDESC              * codes driven patient condition
          MATCH     SP3,LPCONDC
          GOTO      PRT7300 IF EQUAL
          PACK      KEY5,CODECO,LPCONDC
          CALL      RDCODE1
          BRANCH    OVRCD,PRT7300
          MOVE      TDESC,PATCOND
.
PRT7300   MOVE      ONE,RECFLG
          BRANCH    STBYFLG OF PRT8000
          GOTO      PRT7800
.
PRT7600   MOVE      SP30,TDESC
          MATCH     SP3,LPCONDC
          GOTO      PRT7700 IF EQUAL
          PACK      KEY5,CODECO,LPCONDC
          CALL      RDCODE1
          BRANCH    OVRCD,PRT7700
          MOVE      TDESC,PATCOND
.
PRT7700   MOVE      ONE,RECFLG
          BRANCH    STBYFLG OF PRT8000
.
PRT7800   ADD       ONE,TOTPAT              * Total patients
.
          PRINT     *1,"|",WWARD,WBED,*8,"|",AURNO,*17,"|",AADMNO,*26,"|",PNAME:
                    *54,"|",PREG,*58,"| ",PSEX,*62,"|",PSAGE,*66,"|",PATCOND:
                    *87,"|",DOCNAME,*110,"|",DOCDESC,*121,"|",USRDESC,*132,"|"
          GOTO      PRT8500
.
PRT8000   ADD       ONE,TOTSTBY             * Total standby patients
.
          PRINT     *1,"|",WWARD,WBED,*8,"|",AURNO,*17,"|",AADMNO,*26,"|",PNAME:
                    *54,"|",PREG,"| ",PSEX,"*",*62,"|",PSAGE,*66,"|",PATCOND:
                    *87,"|",DOCNAME,*110,"|",*121,"|",USRDESC,*132,"|"
.
PRT8500   CALL      PAGE0000 
          ADD       ONE,CLNO
          DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED;
          MOVE      WWARD,SWARD
          RETURN
.
.         Empty bed printout
.
PRT9000  PRINT     *1,"|",WWARD,WBED,*8,"|",*17,"|",*26,"|",*54,"|",*58,"|":
                   *62,"|",*66,"|",*87,"|",*110,"|",*121,"|",*132,"|"
.
         CALL      PAGE0000
         ADD       ONE,CLNO
         MOVE      WWARD,SWARD
         DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED;
         RETURN
+
.********************************************************************** 
.*                              PAGE0000                              *
.*                           Print Page Line                          *
.**********************************************************************
PAGE0000  PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.********************************************************************** 
.*                              HEAD0000                              *
.*                           Print Headings                           *
.**********************************************************************
HEAD0000  ADD       ONE,CPAGENO 
.
          CLOCK     TIME,CTIMEIS
          CALL      IBACLOCK
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          CALL      PACDATE
          REP       " 0",CPCDATE

          PRINT     *F,*1,"IBAHOS39",*45,CNAME,*116,"DATE : ",CPCDATE:
                    *N,*1,VERSION,*45,"***  MIDNIGHT CENSUS  ***";
.
          BRANCH    FORM1,HEAD1000,HEAD2000,HEAD3000,HEAD4000
.
HEAD1000  PRINT     *73,"( Particular Ward )";
          IF        IBCNMHOS=1
......      PRINT     *45,PTHSNAME
          ENDIF
          GOTO      HEAD5000
.
HEAD2000  PRINT     *73,"( Particular Nursing Station )";
          GOTO      HEAD5000
.
HEAD3000  PRINT     *73,"( All Nursing Station )";
          GOTO      HEAD5000
.
HEAD4000  PRINT     *73,"( All Wards )";
          IF        IBCNMHOS=1
......      PRINT     *45,PTHSNAME
          ENDIF
.
HEAD5000  PRINT     *118,"TIME : ",CTIMEIS,*N:
                    *118,"PAGE : ",*130,CPAGENO,*N
.
          MOVE      FOUR,CLNO 
          BRANCH    FORM1,HEAD7000,HEAD6000,HEAD6000,HEAD7000
          GOTO      HEAD7000
.
HEAD6000  PRINT     *1,"SISTER : ",SAVNSIST,*45,"STATION : ",SAVNSTAT:
                    *60,SAVNDESC,*95,"Extension : ",SAVNEXTN,*N
          ADD       TWO,CLNO
.
HEAD7000  CALL      PAGE0000
.
          PRINT     *1,"| Ward",*8,"|   U/R",*17,"|   Adm":
                    *26,"| Patient's Name",*54,"|Rel",*58,"|Sex",*62,"|Age":
                    *66,"| Patient's Condition",*87,"| Attending":
                    *110,"|Speciality",*121,"| Coding",*132,"|":
                    *N,"| /Bed",*8,"| Number",*17,"| Number",*26,"|",*54,"|   ":
                       "|M/F|   |",*87,"| Doctor",*110,"|",*121,"|",*132,"|"
          ADD       TWO,CLNO
          CALL      PAGE0000
          RETURN
+
.*********************************************************************
.*        HOSP0000                               Called by ML0000    *
.*        Get hospital code                                          *
.*********************************************************************
HOSP0000  IF        IBCNMHOS=1
            DISPLAY   *P1:3,*EF,*P1:4,"    Hospital    : ";
            MOVE      "4",EVERT
            MOVE      "19",ECOL
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
            OPEN      PATHSPA1,"pathspaf"
            CALL      PATHSPKY
            CLOSE     PATHSPA1
            MOVE      PTHSNAME,CNAME
          ENDIF
          RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       PATNURDS
          INC       PATWRDDS
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATLOCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATNOBIO/INC
          INC       PATNURIO/INC
          INC       PATWR1IO/INC
.
          INC       PATHSPKY
          INC       PATHSPIO/INC
+

.*******************************************************************************
.* System   : Waiting Lists                                                    *
.* Program  : IBAWAT11                                                         *
.* Desc     : Waiting Lists Position Enquiry                                   *
.*******************************************************************************
.* Date     : 13/02/89                                                         *
.* Author   : Eric Phan                                                        *
.* Mods     :                                                                  *
.*******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                          *
.*           Recompiled as WATTR1FD changes                                    *
.*******************************************************************************
.*        V10.05.01 05/01/2015  Ebon Clements     CAR 261630                   *
.*                  Removed port number from temp file name                    *
.*        V5.07.02  27/10/1999 J Habasque                                      *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes *
.*            V5.07.01 28/05/1999 Caleb Sharman SRF 645597                     *
.*                     Altered to display consultants code                     *
.*            V5.07.B02 12/01/1999  Nicole Harrington  507 rebounds - error 31 *
.*                     Lined up priority header and description                *
.*            V5.01.01 13/02/89  E.Phan  SRF 103515                            *
.*                     Adapted from IBAWAT24 V5.01.08.                         *
.*                     Only altered PRNT0000.                                  *
.*            V5.01.02 20/03/90  E.Phan  No SRF                                *
.*                     Fixed display routine                                   *
.*            V5.01.03 21/03/90  E.Phan  No SRF                                *
.*                     Added proc to 2nd key for I*D                           *
.*            V5.01.10 10/08/90  J. Coates                                     *
.*                     Added WTCNTPDS & fixed other minor problems             *
.*            V5.01.121 16/10/90 Graeme Williams                               *
.*                      Deleted the last print statement from the program      *
.*            V5.01.122 27/11/90 Glen Hobby                                    *
.*                      Recompiled for changes to                              *
.*                      PATMX1FD                                               *
.*        V5.01.123 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.04.01  25/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*******************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       TFILEVAR/INC
.
          INC       WATCONFD/INC
          INC       WATMESFD/INC
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WEBERRFD/INC
.
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
CODETP    INIT      "TP"
CODEWU    INIT      "WU"
.
OPTDESC1  INIT      "by Unit/Cons/Date "
OPTDESC2  INIT      "by Unit/Cons/Pr'ty/Date "
.
.
VALDCHRS  INIT      "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ"
.
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
SORT1     INIT      "U1-3,68-73,77-84,4-11,57-65,66-67"
SORT2     INIT      "U1-3,68-73,74-76,77-84,4-11,57-65,66-67"
.
. Name    Type   length     physical   start
. ----    ----   ------     --------   -----
TEMPUNIT  DIM       3         3          1      unit code
TEMPURNO  DIM       8         8          4      U/R number
TEMPNAME  DIM       45        45        12      patient name for print
TEMPPROC  DIM       9         9         57      procedure code
TEMPCNT   FORM      2         2         66      unique count
TEMPCONS  DIM       6         6         68      doctor code
NEWPRIO   DIM       3         3         74      priority
TEMPLDTE  DIM       8         8         77      wmdate1 ccyymmdd
TEMPDESC  DIM       33        33        85      procedure description
TEMPREAS  DIM       40        40       118      reason for treatment
TEMPDOB   DIM       6         6        158      Date of Birth (ddmmyy)
TEMPPTY   DIM       3         3        164      priority
TEMPFILL  DIM       11        11       167      filler
.
. -------                End of Record  178    -----
.
DTEMPCNT  DIM       2
.
CMDLINE   DIM       60
CODE      DIM       2
COMCOUNT  FORM      2
CONSDESC  DIM       20
CONSULT   DIM       6
CONTOT    FORM      6
.
DIM3      DIM       3
DIM8      DIM       8
DIM50     DIM       50
DIM55     DIM       55
.
FIRSTCH   FORM      1
FIRSTREC  FORM      1
FROMDATE  DIM       8
FRSTUNIT  FORM      1
.
INDEX     FORM      2
.
.
LINE      FORM      2
.
OPTNDESC  DIM       25
.
PTCNDCQS  FORM      1
.
RECCOUNT  FORM      4
.
SORTSTR   DIM       40
.
TEMPFILE  IFILE     SQL, FIXED=178, KEY=SORTSTR
.
TEMP55    DIM       55
TODATE    DIM       8
TODAY     DIM       8
.
UNIT      DIM       3
UNITDESC  DIM       20
.
DIM15A    DIM       15
DIM15B    DIM       15
DIM20     DIM       20
DOCCODE   DIM       6
NEXTUNIT  DIM       3
NEXTCONS  DIM       6
POSITION  FORM      3
.
PRGID     INIT      "IBAWAT11"
PRGNAM    INIT      "Waiting List Position Enquiry"
VERSION   INIT      "V12.01.00"
.
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000    CALL      INIT0000               * initialization
.
ML1000    CALL      CLR0000                * Clear Variables
.
          CALL      SCRN0000               * Main Option Screen
          BRANCH    EXIT,ML9999
.
ML1100    CALL      UNIT0000               * Get Unit Range
          BRANCH    EXIT,ML1000
.
          CALL      CONS0000               * Get Consultant Range
.
          CALL      DATE0000               * Get Date Range
.
          CALL      CONTQST                * Ok to Continue (Y/N/C)
          BRANCH    CEXIT,ML2000,ML1100,ML1000
.                          Yes    No     Cancel
.
ML2000    CALL      PROC0000               * Process Records in wattr1af
.
          CALL      PRNT0000               * Print Report
.
          GOTO      ML1000
.
ML9999    CHAIN     PGM
.
.**********************************************************************
.*                           INIT0000                                 *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P57:24,"Opening patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P65:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P65:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P65:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P65:24,"watmesaf"
          OPEN      WATMESA1,"watmesaf"
.
          DISPLAY   *P65:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P65:24,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P65:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,THIRTY7;*121,HREVPRIO,*122,WTCNTPUC,*185,WTCNTPDS
          CLOSE     CONTROLF
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAME
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
INIT9999  RETURN
.
.**********************************************************************
.*                           CLR0000                                  *
.**********************************************************************
.
CLR0000   PACK      UNITDESC,SP30,SP30
          PACK      CONSDESC,SP30,SP30
          MOVE      SP6,CONSULT
          MOVE      SP6,TEMPCONS
.
          RETURN
.
.**********************************************************************
.*                            SCRN0000                                *
.*                       Main Option Screen                           *
.**********************************************************************
.
SCRN0000  MOVE      FALSE,EXIT
          HLINE     *G37,2,55,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF:
                    " = Unit/Consultant/Date Sequence":
                    *P1:6,*V2LON," 2",*HOFF:
                    " = Unit/Consultant/Priority/Date Sequence":
                    *P1:8,"Select Item : ";
.
SCRN0100  KEYIN     *P15:8,*V2LON,MOPTION:
                    *P15:8,*DV,MOPTION
.
          COMPARE   ZERO,MOPTION
          GOTO      SCRN9000 IF EQUAL
          BRANCH    MOPTION,SCRN1000,SCRN1000
.
          BEEP
          GOTO      SCRN0100
.
SCRN1000  LOAD      OPTNDESC,MOPTION,OPTDESC1,OPTDESC2
          LOAD      SORTSTR,MOPTION,SORT1,SORT2
          STRIP     OPTNDESC
          DISPLAY   *P55:2,*V2LON,"- ",*+,OPTNDESC,SP1
          GOTO      SCRN9999
.
SCRN9000  MOVE      TRUE,EXIT
          CALL      KILL0000
.
SCRN9999  RETURN
+
.**************************************************************************
.*                  UNIT0000                                              *
.*        Keyin for what unit  (default = ALL)                            *
.**************************************************************************
.
UNIT0000  MOVE      FALSE,EXIT
          MOVE      SP3,UNIT
          DISPLAY   *P1:10,*EF,"Unit        : ",*EL
.
UNIT7000  KEYIN     *P15:10,*DV,UNDLN3:
                    *P15:10,*V2LON,UNIT:
                    *P15:10,*DV,UNIT
.
          ENDSET    UNIT
          APPEND    SP3,UNIT
          RESET     UNIT
.
          MATCH     SP3,UNIT
          GOTO      UNIT8000 IF EQUAL
.
          CMATCH    QUEST,UNIT
          GOTO      UNIT9500 IF EQUAL
.
          PACK      KEY5,CODEWU,UNIT
          CALL      RDCODE1
          BRANCH    OVRCD TO UNIT9000
          MOVE      TDESC,UNITDESC
.
          DISPLAY   *P30:10,*EL,UNITDESC;
.
          GOTO      UNIT9999
.
UNIT8000  MOVE      "All Units           ",UNITDESC
          DISPLAY   *P15:10,*V2LON,UNITDESC,*EL;
          GOTO      UNIT9999
.
UNIT9000  DISPLAY   *P1:24,*B,"Invalid Unit Code.  ",*EL;
          CALL      HITENTER
          GOTO      UNIT7000
.
UNIT9500  MOVE      CODEWU,CODE
          CALL      PATCODDS
          CALL      RDIS0000
          GOTO      UNIT0000
.
UNIT9999  RETURN
+
.**********************************************************************
.*                            CONS0000                                *
.*                    Keyin Consultant Routine                        *
.**********************************************************************
.
CONS0000  MOVE      SP6,CONSULT
          DISPLAY   *P1:11,"Consultant  : ",*EL;
          MOVE      "15",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,CONS8000,CONS0000
.
          MOVE      DCODE,CONSULT
.
          CALL      FCON0000               * Format Consultant's Name
.
          DISPLAY   *P15:11,*EL,DCODE:   
                    *P30:11,CONSDESC
          GOTO      CONS9999
.
CONS8000  MOVE      "All Consultants           ",CONSDESC
          DISPLAY   *P15:11,*V2LON,CONSDESC,*EL
          MOVE      SP10,CONSULT
          GOTO      CONS9999
.
CONS9999  RETURN
+
.******************************************************************************
.*                  DATE0000                                                  *
.*        Keyin from / to date                                                *
.*        date on list (=wmdate1) should be between these dates               *
.******************************************************************************
.
DATE0000  DISPLAY   *P1:13,*EF:
                    *P1:13,"From  Date  :":
                    *P1:14,"To    Date  :"
.
          UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
.
DATE7000  MOVE      TEN5,CCOL
          MOVE      TEN3,CVERT
.
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE7000
.
          MATCH     SP2,CDAY
          GOTO      DATE7000 IF EQUAL
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          MATCH     FROMDATE,TODAY
          GOTO      DATE8200 IF LESS
.
DATE7500  UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
.
          MOVE      TEN5,CCOL
          MOVE      TEN4,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
.
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE7500
.
          MATCH     SP2,CDAY
          GOTO      DATE7500 IF EQUAL
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      DATE8000 IF LESS
.
          MATCH     TODATE,TODAY
          GOTO      DATE8100 IF LESS
.
          GOTO      DATE9999
.
DATE8000  DISPLAY   *P1:24,*B,"From Date > To Date.  ",*EL;
          CALL      HITENTER
          GOTO      DATE7500
.
DATE8100  DISPLAY   *P1:24,*B,"To Date > Today's Date.  ",*EL;
          CALL      HITENTER
          GOTO      DATE7500
.
DATE8200  DISPLAY   *P1:24,*B,"From Date > Today's Date.  ",*EL;
          CALL      HITENTER
          GOTO      DATE0000
.
DATE9000  MOVE      TRUE,EXIT
.
DATE9999  RETURN
+
.**********************************************************************
.*                            RDIS0000                                *
.*                     Redisplay for ? Option                         *
.**********************************************************************
.
RDIS0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF:
                    " = Unit/Consultant/Date Sequence":
                    *P1:6,*V2LON," 2",*HOFF:
                    " = Unit/Consultant/Priority/Date Sequence":
                    *P1:8,"Select Item : ",*V2LON,MOPTION,*HOFF:
                    *P1:10,*EL,"Unit        : ":
                    *P15:10,*V2LON,UNIT,*HOFF,*P30:10,UNITDESC;
          RETURN
+
.**********************************************************************
.*                            CONT0000                                *
.*                  Ok to Continue (Y/N/C) Routine                    *
.**********************************************************************
.
CONT0000  MOVE      FALSE,EXIT
.
          CALL      CONTQST
.
          MOVE      ANS,FORM1
          BRANCH    FORM1,CONT1000,CONT1000,CONT1000
          BEEP
          GOTO      CONT0000
.
CONT1000  LOAD      EXIT,FORM1,ZERO,ONE,TWO
          RETURN
+
.**********************************************************************
.*                            FCON0000                                *
.*                     Format Consultant's Name                       *
.**********************************************************************
FCON0000  CLEAR     CONSDESC
          CALL      RDDOCT1                        * read doctor file
          BRANCH    OVRCD,FCON7000
.
          MOVE      DSNAME,PACSNAME         * set up surname
          MOVE      DGNAME,PACGNAME         * set up given name
          MOVE      DTITL,PACTITLE          * set up title
          MOVE      TWO,PACFRMT             * Surname, Title Given
          CALL      PACNAME                 * format the name
          PACK      CONSDESC,PACFNAME
          GOTO      FCON9999
.
.------ doctors name does not exist ------
.
FCON7000  PACK      CONSDESC,SP30,SP30
.
FCON9999  RETURN
+
.**********************************************************************
.*                            PROC0000                                *
.*                          Process Data                              *
.**********************************************************************
.
PROC0000  CALL      CREA0000               * Create Temp File
.
          MOVE      ZERO,RECCOUNT
.
          PACK      KEY27,FROMDATE,SP30
          CALL      RDSTREA3
.
PROC1000  CALL      RDKTREA3
          BRANCH    OVRCD,PROC9000
.
          DISPLAY   *P1:24,"Scanning ",*V2LON,WMURNO,*HOFF:
                    "   ",WTCNTPDS," ",*V2LON,WMCODE;
.
          PACK      KEY8,WMURNO            * UR number as key
          CALL      RDMAST1                * read master file
          BRANCH    OVRCD,PROC1000         * invalid UR number
.
          MATCH     WMDATE1,TODATE         * is it at end of date range ??
          GOTO      PROC9000 IF LESS       * yes
.
          COMPARE   ONE,WMSTAT             * Only Waiting List Patients
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP3,UNIT               * All Units Chosen
          GOTO      PROC2000 IF EQUAL
.
          MATCH     UNIT,WMUNIT
          GOTO      PROC1000 IF NOT EQUAL
.
PROC2000  MATCH     SP9,CONSULT            * All Consultants Chosen
          GOTO      PROC3000 IF EQUAL
.
          MATCH     CONSULT,WMDOCTOR
          GOTO      PROC1000 IF NOT EQUAL
.
PROC3000  CALL      FNAM0000               * Format Patient's Name
.
          MOVE      WMUNIT,TEMPUNIT
          MOVE      WMURNO,TEMPURNO
          MOVE      WMCODE,TEMPPROC
          MOVE      WMCNT,TEMPCNT
          MOVE      WMDOCTOR,TEMPCONS
          MOVE      WMPTY,TEMPPTY
          MOVE      WMPTY,NEWPRIO
          MOVE      WMDATE1,TEMPLDTE
          MOVE      WMDESC,TEMPDESC
          MOVE      WMREASON,TEMPREAS
          MOVE      PBDATE,TEMPDOB
          MOVE      SP30,TEMPFILL
.
          BRANCH    MOPTION,PROC3400,PROC3600
.
PROC3400  PACK      KEY36,TEMPUNIT,TEMPCONS,TEMPLDTE,TEMPURNO,TEMPPROC,DTEMPCNT
          GOTO      PROC3800
.
PROC3600  BRANCH    HREVPRIO TO PROC3605
          GOTO      PROC3700
.
PROC3605  CLEAR     NEWPRIO
          RESET     TEMPPTY
.
PROC3610  MOVE      TEMPPTY TO ANS
          RESET     VALDCHRS
          SCAN      ANS,VALDCHRS
          GOTO      PROC3650 IF NOT EQUAL
.
          MOVEFPTR  VALDCHRS,FORM2
          MOVE      THIRTY7,INDEX
          SUB       FORM2,INDEX
          RESET     VALDCHRS,INDEX
          MOVE      VALDCHRS,ANS
.
PROC3650  APPEND    ANS,NEWPRIO
          BUMP      TEMPPTY
          GOTO      PROC3610 IF NOT EOS
          RESET     NEWPRIO
.
PROC3700  PACK      KEY39,TEMPUNIT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPURNO:
                          TEMPPROC,DTEMPCNT
.
PROC3800  CALL      WRTEMP1
.
          ADD       ONE,RECCOUNT
.
          GOTO      PROC1000
.
PROC9000  COMPARE   ZERO,RECCOUNT
          GOTO      PROC9999 IF NOT EQUAL
.
          DISPLAY   *P1:24,*B,"No Records Found.  ",*EL;
          CALL      HITENTER
.
PROC9999  RETURN
.
.**********************************************************************
.*                            FNAM0000                                *
.*                   Format the Patient's Name                        *
.**********************************************************************
.
FNAM0000  PACK      TEMPNAME,SP30,SP20
          CLEAR     TEMPNAME
          MOVE      WMURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,FNAM9999
          MATCH     SP30,PSNAME
          GOTO      FNAM3000 IF EQUAL
          APPEND    PSNAME,TEMPNAME
          RESET     TEMPNAME,45
FNAM1000  CMATCH    SP1,TEMPNAME
          GOTO      FNAM2000 IF NOT EQUAL
          BUMP      TEMPNAME,-1
          GOTO      FNAM1000
FNAM2000  APPEND    COMMA,TEMPNAME
          APPEND    SP1,TEMPNAME
FNAM3000  APPEND    PGNAME,TEMPNAME
          RESET     TEMPNAME
FNAM9999  RETURN
.
.**********************************************************************
.*        CREA0000 - Create temp file                                 *
.**********************************************************************
.
CREA0000  CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    CFNAME,CMDLINE
          APPEND    " 178 ",CMDLINE
          APPEND    SORTSTR,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,CFNAME
          RETURN
.
.**********************************************************************
.*        KILL0000  - Remove temp file                                *
.**********************************************************************
.
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    CFNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
.*******************************************************************************
.*        PRNT0000 - Display Records in Temp File                              *
.*******************************************************************************
.
. ------- Initial phase - get 1st record -------
.
PRNT0000  UNPACK    SP30,NEXTUNIT,NEXTCONS   * Clear save variables
          PACK      KEY36,SP30,SP6           * Read all records (1st key)
          PACK      KEY39,SP30,SP9           * Read all records (2nd key)
          CALL      RSTEMP1                  * Position at start of file
          CALL      RKTEMP1                  * Read 1st record
          BRANCH    OVRCD,PRNT9999           * No records - error
          MOVE      TEMPUNIT,NEXTUNIT
          MOVE      TEMPCONS,NEXTCONS
.
. ------- Second phase - read all records for Unit/Consultant required -------
.
PRNT1000  CALL      DSHD0000                 * Disp this unit/cons's scrn header
          MOVE      SEVEN,CVERT              * Set row position for display
.
          PACK      KEY39,NEXTUNIT,NEXTCONS,SP30,SP9 * All recs for this
          PACK      KEY36,NEXTUNIT,NEXTCONS,SP30,SP6 * unit/consultant
          CALL      RSTEMP1
PRNT2000  CALL      RKTEMP1
          BRANCH    OVRCD,PRNT9000           * No more records - redisplay
.
          MATCH     NEXTUNIT,TEMPUNIT        * Same unit ?
          GOTO      PRNT9100 IF NOT EQUAL    * No - stop display
          MATCH     NEXTCONS,TEMPCONS        * Same consultant ?
          GOTO      PRNT9100 IF NOT EQUAL    * No - stop display
.
          ADD       ONE,CVERT                * One more line to display
          COMPARE   CVERT,TWENTY2            * Hit screen limit ?
          GOTO      PRNT9200 IF LESS         * Yes - stop display
.
. ------- Set up variables for display -------
.
PRNT3000  ADD       ONE,POSITION             * One more position down list
          MOVE      TEMPNAME,DIM20           * Patient name - DIM45 to DIM20
          MOVE      TEMPDESC,DIM15A          * Procedure description
          UNPACK    TEMPLDTE,CCENT,CYEAR,CMON,CDAY * Date on List
.
          MOVE      "* Invalid Priority *",TDESC
          MATCH     SP3,TEMPPTY              * Prevent category description
          GOTO      PRNT3100 IF EQUAL        * from being extracted.
          PACK      KEY5,CODETP,TEMPPTY
          CALL      RDCODE1
PRNT3100  MOVE      TDESC,DIM15B             * Priority desc - DIM20 to DIM15
.
          DISPLAY   *P1:CVERT,*V2LON,POSITION,DOT,*HOFF,*P6:CVERT,TEMPURNO:
                    *P15:CVERT,DIM20,*P36:CVERT,DIM15A,*P56:CVERT,DIM15B:
                    *P69:CVERT,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          GOTO      PRNT2000                 * Read next record
.
. ------- No more records -------
.
PRNT9000  MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"(",*V2LON,"E",*HOFF,")xit, ":
                    "(",*V2LON,"R",*HOFF,")edisplay : ",*V2LON,ANS;
          REP       "E1R2 01020",ANS
          MOVE      ZERO,FORM1
          MOVE      ANS,FORM1
          BRANCH    FORM1,PRNT9999,PRNT0000
          BEEP
          GOTO      PRNT9000
.
. ------- No more records for current consultant/unit -------
.
PRNT9100  MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"(",*V2LON,"E",*HOFF,")xit, ":
                    "(",*V2LON,"R",*HOFF,")edisplay, ":
                    "Next (",*V2LON,"U",*HOFF,")nit, ":
                    "Next (",*V2LON,"C",*HOFF,")onsultant : ",*V2LON,ANS;
          REP       "E1R2U3C4 010203040",ANS
          MOVE      ZERO,FORM1
          MOVE      ANS,FORM1
          BRANCH    FORM1,PRNT9999,PRNT0000,PRNT9400,PRNT9500
          BEEP
          GOTO      PRNT9100
.
. ------- Next screen for current unit/consultant -------
.
PRNT9200  MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"(",*V2LON,"E",*HOFF,")xit, ":
                    "(",*V2LON,"R",*HOFF,")edisplay, ":
                    "Next (",*V2LON,"S",*HOFF,")creen, ":
                    "Next (",*V2LON,"U",*HOFF,")nit, ":
                    "Next (",*V2LON,"C",*HOFF,")onsultant : ",*V2LON,ANS;
          REP       "E1R2S3U4C5 01020304050",ANS
          MOVE      ZERO,FORM1
          MOVE      ANS,FORM1
          BRANCH    FORM1,PRNT9999,PRNT0000,PRNT9300,PRNT9400,PRNT9500
          BEEP
          GOTO      PRNT9100
.
. ------- Next screen for the current consultant/unit -------
.
PRNT9300  MOVE      EIGHT,CVERT              * Set up top line of data display
          DISPLAY   *P1:CVERT,*EF;           * Clear screen from here onwards
          GOTO      PRNT3000                 * Display record
.
. ------- Search for next Unit -------
.
PRNT9400  MATCH     TEMPUNIT,NEXTUNIT        * Check for same unit here because
          GOTO      PRNT9600 IF NOT EQUAL    * we are always 1 record ahead
          CALL      RKTEMP1                  * Read next record
          BRANCH    OVRCD,PRNT9000           * No more records - only redisplay
          GOTO      PRNT9400                 * Check unit against saved value
.
. ------- Search for next Consultant -------
.
PRNT9500  MATCH     TEMPCONS,NEXTCONS        * Check for same cons here because
          GOTO      PRNT9600 IF NOT EQUAL    * we are always 1 record ahead
          CALL      RKTEMP1                  * Read next record
          BRANCH    OVRCD,PRNT9000           * No more records - only redisplay
          GOTO      PRNT9500                 * Check unit against saved value
.
. ------- New screen for new Unit/Consultant -------
.
PRNT9600  MOVE      TEMPUNIT,NEXTUNIT        * This is the unit required
          MOVE      TEMPCONS,NEXTCONS        * along with the consultant
          CALL      DSHD0000                 * Display new screen heading
          GOTO      PRNT3000                 * Display record
.
PRNT9999  RETURN
+
.*******************************************************************************
.*        DSHD0000 - Display Screen Header                                     *
.*        Requires : Current Unit in NEXTUNIT                                  *
.*                   Current Consultant in NEXTCONS                            *
.*******************************************************************************
.
DSHD0000  MOVE      "*** Invalid Unit ***",TDESC  * Default error description
          MATCH     SP3,NEXTUNIT                  * Prevent reading of
          GOTO      DSHD1000 IF EQUAL             * category record.
          PACK      KEY5,CODEWU,NEXTUNIT
          CALL      RDCODE1
DSHD1000  MOVE      TDESC,UNITDESC
.
          MOVE      "** Invalid Doctor **",CONSDESC    * Default error desc
          MATCH     SP6,NEXTCONS                       * Spaces invalid
          GOTO      DSHD2000 IF EQUAL                  * for doctor code
          PACK      KEY6,TEMPCONS
          CALL      RDDOCT1
          BRANCH    OVRCD,DSHD2000                     * No record - error
          CALL      FCON0000                           * Format doctor's name
.
DSHD2000  DISPLAY   *P1:3,*EF:
                    *P1:4,"Unit       : ",*V2LON,NEXTUNIT,*HOFF,*P21:4,UNITDESC:
                    *P1:5,"Consultant : ",*V2LON,NEXTCONS,*HOFF,*P21:5,CONSDESC:
                    *P1:7,*V2LON,*ULON,"Item",*P6:7,"  U/R   ":
                    *P15:7,"    Name            ",*P36:7,WTCNTPDS," Desc ":
                    *P52:7,"    Priority   ",*P68:7,"Date on List"
.
          MOVE      EIGHT,CVERT              * Data starts next line
          MOVE      ZERO,POSITION            * Reset position on list
.
DSHD9999  RETURN
.
.**********************************************************************
.*                   I/O ROUTINES FOR THE TEMP FILE                   *
.**********************************************************************
.
RATEMP1   MOVE      ZERO,OVRCD
          BRANCH    MOPTION,RATMP1,RATMP2
.
RATMP1    RESET     KEY36
          READ      TEMPFILE,KEY36;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RATMP2    RESET     KEY39
          READ      TEMPFILE,KEY39;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.-----------------------------------------
RSTEMP1   MOVE      ZERO,OVRCD
          BRANCH    MOPTION,RSTMP1,RSTMP2
.
RSTMP1    RESET     KEY36
          READ      TEMPFILE,KEY36;;
          RETURN
.
RSTMP2    RESET     KEY39
          READ      TEMPFILE,KEY39;;
          RETURN
.-----------------------------------------
RDTEMP1   MOVE      ZERO,OVRCD
          BRANCH    MOPTION,RDTMP1,RDTMP2
.
RDTMP1    RESET     KEY36
          READ      TEMPFILE,KEY36;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                                   DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                                   TEMPREAS,TEMPDOB,TEMPPTY,TEMPFILL
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPCNT,TEMPCNT
          RETURN
.
RDTMP2    RESET     KEY39
          READ      TEMPFILE,KEY39;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                                   DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                                   TEMPREAS,TEMPDOB,TEMPPTY,TEMPFILL
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPCNT,TEMPCNT
          RETURN
.-----------------------------------------
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                             DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                             TEMPREAS,TEMPDOB,TEMPPTY,TEMPFILL
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPCNT,TEMPCNT
          RETURN
.-----------------------------------------
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMPFILE;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                             DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                             TEMPREAS,TEMPDOB,TEMPPTY,TEMPFILL
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPCNT,TEMPCNT
          RETURN
.-----------------------------------------
WRTEMP1   BRANCH    MOPTION,WRTMP1,WRTMP2
.
WRTMP1    RESET     KEY36
          MOVE      TEMPCNT,DTEMPCNT
          WRITE     TEMPFILE,KEY36;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                                   DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                                   TEMPREAS,TEMPDOB,TEMPPTY,TEMPFILL
          RETURN
.
WRTMP2    RESET     KEY39
          MOVE      TEMPCNT,DTEMPCNT
          WRITE     TEMPFILE,KEY39;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                                   DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                                   TEMPREAS,TEMPDOB,TEMPPTY,TEMPFILL
          RETURN
.-----------------------------------------
DETEMP1   BRANCH    MOPTION,DETMP1,DETMP2
.
DETMP1    RESET     KEY36
          DELETE    TEMPFILE,KEY36
          RETURN
.
DETMP2    RESET     KEY39
          DELETE    TEMPFILE,KEY39
          RETURN
.
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATDOCKY
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       WATMESIO/INC
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WEBERRIO/INC

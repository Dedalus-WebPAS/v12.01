.*****************************************************************************
.* System    :   PATIENT                                                     *
.* Program   :   IBAHMS43                                                    *
.* Desc      :   Registered HCP Master Listing                               *
.*****************************************************************************
.* Date      :   22/03/93                                                    *
.* Author    :   Gabrielle Cameron                                           *
.* Function  :             This program provides a printed report for the    *
.*                         Registered HCP Master Listing                     *
.*                                                                           *
.* Mods : V9.02.01  10/02/2002 Ebon Clements                                 *
.*                  Ported from r5.09 and changed to use the HCP files       *
.*****************************************************************************
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       STDGENDF
          INC       PMSHCPFD/INC
          INC       PATCODFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
PRGID     INIT      "IBAHMS43"
PRGNAM    INIT      "Registered HCP Master Listing"
VERSION   INIT      "V12.01.00"

CKLINKED  FORM      1
DIM8ARRY  DIM       8[17]
DISPENCO  DIM       20                       * Display Ending Code
DISPENSN  DIM       31                       * Display Ending Surname
DISPSTCO  DIM       31                       * Display Starting Code
DISPSTSN  DIM       20                       * Display Starting Surname
.
ENDCODE   DIM       10                       * Ending Practice Code
ENDSNAM   DIM       20                       * Ending Surname
.
GSNAME    DIM       47                       * Formated Given name and Surname
.
KEYDESC   DIM       20                       * Key Description
.
RCOUNT    FORM      "0000000"                * Record Count
.
STARCODE  DIM       10                       * Starting Practice Code
STARSNAM  DIM       20                       * Starting Surname
.
TYPEDESC  DIM       7                        * Type Description
.
DISPNAME  DIM       25
PMHCP130  INIT      "Attending            "
PMHCP131  INIT      "Referring            "
PMHCP132  INIT      "Attending & Referring"
PMHCP133  INIT      "GP                   "
PMHCP134  INIT      "Attending & GP       "
PMHCP135  INIT      "Referrring & GP      "
PMHCP136  INIT      "Attending,Refer & GP "
PMHCP137  INIT      "Other                "
PMHCP138  INIT      "Other Attending      "
PMHCP139  INIT      "Other Referring      "
PMHCP140  INIT      "Other Attending & Referring"
.
zzzzz     INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
+
.**************************************************************************
.*                              ML0000                                    * 
.*                      Controlling Logic (Mainline code)                 * 
.**************************************************************************
ML0000    CALL      INIT0000                 * initialization and open files
.
ML0100    CALL      OPTN0000                 * select options
          BRANCH    EXIT,ML9999              * EXIT = 1 if 0 chosen in menu
          BRANCH    OPTION,ML1000,ML2000
.
. ---- report in HCP Code Sequence  ----
.
ML1000    CALL      CODE0000                 * read file in HCP code sequence 
          GOTO      ML0100                   * go to enter another option
.
. ---- report in Surname Sequence  ----
.
ML2000    CALL      SNAM0000                 * read file in Surname sequence 
          GOTO      ML0100                   * go to enter another option

ML9999    CHAIN     PGM                      * chain to previous program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.**************************************************************************** 
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening pmshcpaf"
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P56:24,"Opening pmshcpaf"
          OPEN      PMSHCPA2,"pmshcpaf"
.
          DISPLAY   *P56:24,"Opening patcodes"
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.*              OPTION = 1  Report in HCP Code Sequence                      *
.*              OPTION = 2  Report in Surname Sequence                      *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  KEYIN     *P1:4,*EF,*V2LON,"0",*HOFF," = Exit":         
                    *P1:5,*V2LON,"1",*HOFF," = HCP Code Sequence":
                    *P1:6,*V2LON,"2",*HOFF," = Surname Sequence":
                    *P1:8,"Select option : ",*V2LON,OPTION
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL        * exit

          BRANCH    OPTION,OPTN8000,OPTN8000 * valid option

          BEEP                               * invalid option
          GOTO      OPTN0000

OPTN8000  MOVE      FALSE,EXIT               * set EXIT = 0
          GOTO      OPTN9999
            
OPTN9000  MOVE      TRUE,EXIT                * set EXIT = 1
OPTN9999  RETURN
+
.****************************************************************************
.*                                CODE0000             Called by: ML0000    *
.*              OPTION = 1  Report in HCP Code Sequence                      *
.****************************************************************************
CODE0000  CLOCK     TIME,CTIMEIS             * Clock start time of Report
          MOVE      ZERO,CPAGENO             * Initialise Page No. to zero
          MOVE      ZERO,RCOUNT              * Initialise Record Count to zero
.
CODE0100  CALL      CRNG0000                 * Obtain Ranges
          BRANCH    EXIT,CODE9999
          CALL      CONTQST                  * Continue ?
          BRANCH    CEXIT,CODE1000,CODE0100,CODE9999
.
CODE1000  DISPLAY   *P1:24,*EL,"Printing Code : ";
          CALL      HEAD0000                 * Header details
          MOVE      STARCODE,KEY10           * Move key to start 
          CALL      RDPMHCP1                 * Positional Read 
          CALL      PRNT0000
.
CODE2000  REPEAT                             * Until CLNO >= 60
            CALL      RKPMHCP1               * Read next record
            BRANCH    OVRCD,CODE9000         * Branch if end of file
            MATCH     PMHCHCPC,ENDCODE 
            GOTO      CODE9000 IF LESS
            CALL      PRNT0000               * Print Record
            DISPLAY   *P17:24,*EL,PMHCHCPC
          UNTIL CLNO >= 60 
          CALL      HEAD0000
          GOTO      CODE2000                 * Repeat the loop
.
CODE9000  PRINT     *N,"*** Total number of records = ",RCOUNT," ***":
                    *N,"*** End of report ***"
          DISPLAY   *P1:24," Printing Completed. ";
          CALL      HITENTER
.
CODE9999  RETURN
+
.****************************************************************************
.*                                CRNG0000             Called by: CODE0000  *
.*                                                                          *
.*                   Obtain Code RaNGes for the HCP Codes                   *
.****************************************************************************
.
.---- Starting Practice Code ----
.
CRNG0000  KEYIN     *P3:11,*EF,"Starting HCP Code : ",*V2LON,STARCODE
.
CRNG0100  RESET     STARCODE                 * Nothing input - Default to Start
          IF @EOS
            MOVE      SP10,STARCODE           * Range = Start
            MOVE      "Start",DISPSTCO
            DISPLAY   *P23:11,*EL,*V2LON,DISPSTCO
          ELSE
.
            PACK      STARCODE,STARCODE,SP70
.
            PACK      KEY10,STARCODE         * Ensure Start Code exists
            CALL      RDPMHCP1
            IF        OVRCD = ONE
              BEEP
              GOTO      CRNG0000
            ELSE
              MOVE      PMHCGNAM,ANS
              DISPLAY   *P23:11,*EL,*V2LON,STARCODE:
                        *P32:11,*HOFF,ANS,DOT,PMHCSNAM
              PACK      DISPSTCO,STARCODE,SP1,ANS,DOT,PMHCSNAM
            ENDIF
          ENDIF

.
.---- Ending Practice Code -----
.
CRNG1000  KEYIN     *P3:12,"Ending HCP Code   : ",*V2LON,ENDCODE
.                                            
CRNG1100  RESET     ENDCODE                  * Nothing input - Default to End 
          IF @EOS
            MOVE      zzzzz,ENDCODE          * Range = Finish
            MOVE      "Finish",DISPENCO
            DISPLAY   *P23:12,*EL,*V2LON,DISPENCO
          ELSE  
.
            PACK      ENDCODE,ENDCODE,SP70
.
            PACK      KEY10,ENDCODE          * Ensure End Code exists
            CALL      RDPMHCP1
            IF        OVRCD = ONE
              BEEP
              GOTO      CRNG1000
            ELSE
              MOVE      PMHCGNAM,ANS
              DISPLAY   *P23:12,*EL,*V2LON,ENDCODE:
                        *P32:12,*HOFF,ANS,DOT,PMHCSNAM
              PACK      DISPENCO,ENDCODE,SP1,ANS,DOT,PMHCSNAM
            ENDIF
          ENDIF
.
. ---- Check end code against start code ----
.
CRNG2000  MATCH     STARCODE,ENDCODE
          IF @LESS
            DISPLAY   *P1:24,*B,"End range is less than Start range - ";
            CALL      HITENTER
            GOTO      CRNG0000
          ENDIF
CRNG9999  RETURN
+
.****************************************************************************
.*                                SNAM0000             Called by: ML0000    *
.*                                                                          *
.*              OPTION = 2  Report in Surname Sequence                      *
.****************************************************************************
SNAM0000  CLOCK     TIME,CTIMEIS             * Clock start time of Report
          MOVE      ZERO,CPAGENO             * Initialise Page No. to zero
          MOVE      ZERO,RCOUNT              * Initialise Record Count to zero
.
SNAM0100  CALL      SRNG0000                 * Obtain Ranges
          CALL      CONTQST                  * Continue ?
          BRANCH    CEXIT,SNAM1000,SNAM0100,SNAM9999 
.
SNAM1000  DISPLAY   *P1:24,*EL,"Printing Surname : ";
          PACK      KEY80,STARSNAM,SP70
          CALL      RSPMHCP2
.
SNAM2000  CALL      HEAD0000                 * Header details
          REPEAT
            CALL      RKPMHCP2               * Read next record
            BRANCH    OVRCD,SNAM9000         * Finish if End of file
            PACK      KEYDESC,PMHCSNAM,SP30
            MATCH     KEYDESC,ENDSNAM        * Check if end of range
            GOTO      SNAM9000 IF LESS       * Keydesc < EndDesc
            CALL      PRNT0000               * Print Record
            DISPLAY   *P20:24,*EL,PMHCSNAM
          UNTIL CLNO >= 60                  
          GOTO      SNAM2000                 * Repeat loop
.
SNAM9000  PRINT     *N,"*** Total number of records = ",RCOUNT," ***":
                    *N,"*** End of report ***"
          DISPLAY   *P1:24," Printing Completed. ";
          CALL      HITENTER
.
SNAM9999  RETURN
+
.****************************************************************************
.*                                SRNG0000           Called by: SNAM0000    *
.*             Obtain Surname RaNGes for the Surname Sequence               *
.****************************************************************************
.
. ---- Starting Surname Range ----
.
SRNG0000  MOVE      SP20,STARSNAM
          MOVE      SP20,ENDSNAM 
          KEYIN     *P3:11,*EF,"Starting Surname : ",*V2LON,STARSNAM
.
SRNG0100  RESET     STARSNAM 
          IF @EOS
            DISPLAY   *P22:11,*EL,*V2LON,"Start" * Range = Start
            MOVE      "Start",DISPSTSN           * Nothing Input default : Start
            MOVE      SP20,STARSNAM
          ELSE
            MOVE      STARSNAM,DISPSTSN
          ENDIF
.
. ---- Ending Surname Range ----
.
SRNG1000  KEYIN     *P3:12,"Ending Surname   : ",*V2LON,ENDSNAM
.
SRNG1100  RESET     ENDSNAM                   
          IF @EOS
            DISPLAY  *P22:12,*EL,*V2LON,"Finish" * Range = Finish
            MOVE      "Finish",DISPENSN          * Nothing input default to End 
            MOVE      zzzzz,ENDSNAM
          ELSE
            MOVE      ENDSNAM,DISPENSN           * Obtain with starting letter
          ENDIF
.
. ---- Check end Surname against start Surname ----
.
          MATCH     STARSNAM,ENDSNAM
          IF @LESS
            DISPLAY   *P1:24,*B,"End range is less than Start range - ";
            CALL      HITENTER
            GOTO      SRNG0000
          ENDIF
          PACK      STARSNAM,STARSNAM,SP20,SP20
          PACK      ENDSNAM,ENDSNAM,zzzzz     
SRNG9999  RETURN
+
.****************************************************************************
.*                                HEAD0000           Called by: CODE0000    *
.*                                                              SNAM0000    *
.*                   Calculate the header of each page                      *
.****************************************************************************
HEAD0000  CALL      HEAD132  

          IF        OPTION = 1
            MOVE      "HCP Code Sequence",CPHDROPT
            PRINT     *40,"Starting HCP Code : ",DISPSTCO
            PRINT     *40,"Ending HCP Code   : ",DISPENCO
          ELSE
            MOVE      "Surname Sequence",CPHDROPT
            PRINT     *40,"Starting Surname : ",DISPSTSN
            PRINT     *40,"Ending Surname   : ",DISPENSN
          ENDIF
.
          CALL      UND132
          PRINT     *1,"| ","HCP Code":
                    *17,"| ","HCP Name":
                    *69,"| ","Status":
                    *106,"| ","Speciality":
                    *132,"|"  
          PRINT     *1,"| ":
                    *17,"| ":
                    *69,"| ":
                    *106,"| ":
                    *132,"|"  

          CALL      UND132                   * Print line after header desc.
          ADD       "4",CLNO                 * Update Line Count
HEAD9999  RETURN
+
.****************************************************************************
.*                                PRNT0000             Called by: CODE0000  *
.*                         Print Record details                             *
.****************************************************************************
PRNT0000  CALL      VPRN0000                 * Setup up display vars            
.            
.--- Line 1 - HCP Code, Name, Address1, Type ---
.
          PRINT     *1,"| ",PMHCHCPC,*17,"| ",GSNAME,*69,"| ",DISPNAME:
                    *106,"| ",TDESC,*132,"|"  
.            
          CALL      UND132
          ADD       ONE,CLNO                * Four to Line Count
          ADD       ONE,RCOUNT               * One to Record Count
PRNT9999  RETURN

.*************************************************************************
.*                                VPRN0000           Called by: PRIN0000 *
.*                            setup Type of GP                           * 
.*************************************************************************
+
VPRN0000  MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME                  * Format Name
          MOVE      PACFNAME,GSNAME
.
          MOVE      PMHCP130,DISPNAME
          LOAD      DISPNAME,PMHCHCST,PMHCP131,PMHCP132,PMHCP133,PMHCP134:
                                      PMHCP135,PMHCP136,PMHCP137,PMHCP138:
                                      PMHCP139,PMHCP140
.
          MOVE      SP70,TDESC
          MATCH     SP70,PMHCSPC1
          GOTO      VPRN9999 IF EQUAL
.
          PACK      KEY5,ANSD,ANST,PMHCSPC1,SP70
          CALL      RDCODE1
          IF        OVRCD = 1
            MOVE      SP70,TDESC
          ENDIF
.
VPRN9999  RETURN
.
. IO INCLUDES
. -----------
          INC       STDGENCD
          INC       PMSHCPIO/INC
          INC       PATCODIO/INC

.******************************************************************************
.* System    : Patient                                                        *
.* Program   : IBAINP29                                                       *
.* Desc      : DRG/MDC Code Maintenance                                       *
.******************************************************************************
.* Date      : 12/01/95                                                       *
.* Author    : Paul Howells                                                   *
.* Modifications :                                                            *
.*       V11.05.01  26/03/2025  Davin            TSK 0956210                  *
.*                  Added DRG 12.0 to Menu at OPTN5000                        *
.*       V11.02.01  20/04/2022  Davin            TSK 0917793                  *
.*                  Added DRG 11.0 to Menu at OPTN5000                        *
.*       V10.14.03 06/05/2019  J.Tan          TSK 0857392                     *
.*                  Added validation to DRG Partition
.*       V10.14.02 26/03/2019  J.Tan          TSK 0857392                     *
.*                  Added DRG Partition to patdgwaf                           *
.*       V10.14.01  27/02/2019  Richa Phogat     TSK 0870439                  *
.*                  Added DRG 10.0 to Menu at OPTN5000                        *
.*       V10.12.01  21/03/2018  Tracey Nguyen    TSK 0852793                  *
.*                  Added DRG 9.0 to Menu at OPTN5000                         *
.*       V10.06.01  29/05/2015  Ebon Clements    CAR 317094                   *
.*                  Added DRG 8.0 to Menu at OPTN5000                         *
.*       V10.04.01  20/05/2014  Peter Vela       CAR 300038                   *
.*                  Changed input file to comma delimited                     *
.*                  Added update patdgwaf report                              *
.*       V10.03.02  03/05/2013  Davin            CAR 284669 HDP 2013 DRG 7.0  *
.*                  Added DRG 7.0 to Menu at OPTN5000                         *
.*       V10.03.01  28/06/2012  Mike Laming      CAR 267797 HDP 2012 DRG 6.2  *
.*                  Added DRG 6.2 to Menu at OPTN5000                         *
.*        V9.12.01  19/06/2009  Mike Laming      CAR 197814 HDP 2009 DRG 6.0  *
.*                  Added DRG 6.0 to Menu at OPTN5000                         *
.*        V9.08.01  04/05/2007  Mike Laming  CAR 137125 HDP 2007 DRG 5.2      *
.*                  Mods to PATDGWFD - Add field PTDWDRGV (add to Keys)       *
.*        V9.02.02  18/03/2003 J.Tan                                          *
.*                  Mods size of text file name for WEB front page            *
.*        V9.02.01  02/05/2002  Mike Laming  SRF 17030                        *
.*                  Correct "Regenerate" routine to use KEY19 (not KEY18)     *
.*        V5.08.B03 04/04/2000  Greg Horvat                                   *
.*                  Chanegd to keyin the clincal speciality code              *
.*        V5.08.B02 06/03/2000  Jill Habasque                                 *
.*                  Recompiled for STDKWSDF & STDKWSCD                        *
.*        V5.08.B01 14/02/2000  Greg Horvat                                   *
.*                  Copied from release 6.06, version V6.06.04                *
.*                                                                            *
.******************************************************************************
+
          INC       STDGENDF
          INC       STDKWSDF
.
          INC       PATCODFD/INC
          INC       PATCLSFD/INC
          INC       PATDGWFD/INC
          INC       PATKDRFD/INC
.
. ----- Old DRG File Definition -----
.
OLDFILE1  IFILE     SQL, FIXED=109, KEY="U1-4,85-87"                  *C-137125
.
. ----- New DRG File Definition -----
.
.                                                                     *C-137125
NEWFILE1  IFILE     SQL, FIXED=109, KEY="U1-4,85-87"
NEWFILE2  IFILE     SQL, FIXED=109, KEY="U5-8,1-4,85-87"
NEWFILE3  IFILE     SQL, FIXED=109, KEY="U9-68,1-4,85-87"
.
NEWFILE   INIT      " 109 UC1-4,85-87 UC5-8,1-4,85-87 UC9-68,1-4,85-87"
.
.Name      Type     Length   Physical   Start   Description
.----      ----     ------   --------   -----   -----------
.PTDWDRGC  DIM      4        4          1       DRG Number
.PTDWMDCC  DIM      4        4          5       MDC Number
.PTDWDESC  DIM      60       60         9       DRG Description
.PTDWLOWT  FORM     3.2      4          69      Low Trim
.PTDWHIGH  FORM     3.2      4          73      High Trim
.PTDWRWGT  FORM     3.6      6          77      Resource Weight
.PTDWCLSP  DIM      2        2          83      Clincal Speciality
.PTDWDRGV  DIM      3        3          85      DRG Version (format "051" = 5.1)
.PTDWWTEP  DIM      1        1          88      Warning to Enter a Prosthetic
.PTDWDRGT  DIM      3        3          89      DRG Partition (DRG Type) Cat Dt
.PTDWSPAR  DIM      17       17         92      Spare Variable
.
.End of Record                          109
.
. ----- DRG Textfile Definition -----
.
TXTFILE1  FILE      HL7, FIXED=99                                   *C-137125
.
.Name      Type     Length   Start   Description      Justification
.----      ----     ------   -----   -----------      -------------
TXTDRGN    DIM      4        1       DRG Number       Left
TXTMDCN    DIM      4        5       MDC Number       Left  (0 filled where req)
TXTDESC    DIM      60       9       DRG Description  Left
TXTLOWT    DIM      6        69      Low Trim         Right
TXTHIGH    DIM      6        75      High Trim        Right
TXTRWGT    DIM      10       81      Resource Weight  Right
TXTCLSP    DIM      2        91      Clinical Speclty Right
TXTDRGV    DIM      3        93      DRG Version (format = "051" is DRG 5.1)
TXTDRGT    DIM      3        96      DRG Partition    Cat Dt
TXTSPAR    DIM      1        99      Spare
.
.End of Record              100
.
. ----- Program Variables -----
.
CALLPOSN  FORM      1
CLSFLG    FORM      1                       * patclsaf flag
CMDLINE   DIM       80
CODE      DIM       2
.
DSCITEM   DIM       30
DISPCNT   FORM      2                       * Display screen counter
DISPOPTN  DIM       17                      * Display option
DRGC      DIM       4[15]
DRGCODE   DIM       4
DRGDESZ   DIM       3                       * DRG Version format n.n  *I-137125
DRGVERZ   DIM       3                       * DRG Version format 0nn  *I-137125
DV1       DIM       1                                                 *I-137125
DV2       DIM       1                                                 *I-137125
.
ERRNUMB   FORM      8
ERRDESC   DIM       70
.
FIRSTIME  FORM      1
LOADCNT   FORM      4                       * Load DRG counter
NEWFNAME  DIM       8                       * New filename
SVERT     FORM      2
.TXTFNAME  DIM       8                       * Text filename
TXTFNAME  DIM       80                      * Text filename
.
. ----- Procedure Variables -----
.
RECNUM    FORM      8
PTCNDGPV  DIM       3                                                 *I-137125
.
. ----- Program Constants -----
.
PIPE      INIT      "|"
DRG041    INIT      "041"
DRG042    INIT      "042"
DRG050    INIT      "050"
DRG051    INIT      "051"
DRG052    INIT      "052"
DRG060    INIT      "060"                                             *I-197814
DRG062    INIT      "062"                                             *I-267797
DRG070    INIT      "070"                                             *I-284669
DRG080    INIT      "080"
DRG090    INIT      "090"
DRG100    INIT      "100"
DRG110    INIT      "110"
DRG120    INIT      "120"
CHANGE    INIT      "Change           "
DELETE    INIT      "Delete           "
INSERT    INIT      "Insert           "
LOAD      INIT      "Load New DRG File"
INDEX     INIT      "Regenerating Index"
SP60      INIT      "                              ":
                    "                              "
VALDPASS  FORM      1
PRGID     INIT      "IBAINP29"
PRGNAM    INIT      "DRG/MDC Maintenance"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                 MAINLINE                                   *
.*                             Controlling Logic                              *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialization and open files
.
ML1000    CALL      OPTN0000                * Select options
          BRANCH    OPTION,ML2000,ML3000,ML4000,ML5000,ML6000
.                          Insert Change Delete Load    Regen
          GOTO      ML9999
.
. ----- Insert -----
.
ML2000    CALL      DSCR0000                * Display screen 
          CALL      IDRG0000                * Input DRG code
          BRANCH    EXIT,ML1000
.
          CALL      INST0000                * Insert mode
          BRANCH    EXIT,ML1000             * Exit
.
          CALL      CHGE0000                * Change mode
          BRANCH    EXIT,ML1000             * Exit
.
          CALL      POST0000                * Post
          GOTO      ML2000
.
. ----- Change -----
.
ML3000    CALL      DSCR0000                * Display screen 
          CALL      CDRG0000                * Input DRG code
          BRANCH    EXIT,ML1000
.
          CALL      CHGE0000                * Change mode
..          BRANCH    EXIT,ML1000             * Exit
          BRANCH    EXIT,ML3000
.
          CALL      POST0000                * Post
          GOTO      ML3000
.
. ----- Delete -----
.
ML4000    CALL      DSCR0000                * Display screen 
          CALL      CDRG0000                * Input DRG code
          BRANCH    EXIT,ML1000
.
          CALL      POST0000                * Post
          GOTO      ML4000
.
. ----- Load New DRG File -----
.
ML5000    CALL      OPEN0000                * Open the new DRG file
          CALL      KTFN0000                * Keyin textfile name
          BRANCH    EXIT,ML1000
.
          MOVE      ONE,VALDPASS            * Validation Pass Set to Yes
          CALL      LNDF0000                * Load new DRG file
.
          IF        ERRNUMB=0
            MOVE      ZERO,VALDPASS         * If no errors proceed
            OPEN      TXTFILE1,TXTFNAME       * Re position at the start
            CALL      LNDF0000              * Load new DRG file
          ENDIF

          CALL      KILL0000                * Delete any new DRG files
          GOTO      ML1000
.
. ----- Regenerating Index -----
.
ML6000    DISPLAY   *P1:24,*EL,"Recalibrating Key Word Index ..."
          MOVE      ZERO,RECNUM
          PACK      KEY19,SP70                                         *C-90201
          CALL      RSPTKD1
ML6100    CALL      RKPTKD1
          BRANCH    OVRCD,ML6200  
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY       *P40:24,*V2LON,RECNUM
          ENDIF
          PACK      KEY19,PTKDITM,PTKDKWD                              *C-90201
          CALL      DEPTKD1
          GOTO      ML6100  
.
ML6200    DISPLAY   *P1:24,*EL,"Creating Key Word Index ..."
          MOVE      ZERO,RECNUM
          PACK      KEY7,SP10                                         *C-137125
          CALL      RSPTDGW1
ML6300    CALL      RKPTDGW1
          BRANCH    OVRCD,ML1000
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY       *P40:24,*V2LON,RECNUM
          ENDIF
          PROC      PATKDRUP
          GOTO      ML6300  
.
ML9999    CHAIN     PGM                     * Chain back to program
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                              Initialization                                *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patdgwaf";
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATDGWA2,"patdgwaf"
          OPEN      PATDGWA3,"patdgwaf"
.
          DISPLAY   *P64:24,"patkdraf";
          OPEN      PATKDRA1,"patkdraf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patclsaf";
          MOVE      ZERO,CLSFLG             * patclsaf flag - no
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATCLSA1,"patclsaf"
          TRAPCLR   IO
          BRANCH    OVRCD,INIT1000
.
          PACK      KEY2,SP2
          CALL      RSPTCLS1                * Position on & read a clinical
          CALL      RKPTCLS1                  speciality file record
          BRANCH    OVRCD,INIT1000
.
          MOVE      ONE,CLSFLG              * patclsaf flag - yes
INIT1000  MOVE      ONE,CNEWDS
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000               Called by: ML0000   *
.*                         Get user options or exit                           *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          MOVE      SP3,DRGVERZ                                       *I-137125
          CALL      DISPHEAD
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Insert":
                    *P1:6,*V2LON,"2",*HOFF," = Change":
                    *P1:7,*V2LON,"3",*HOFF," = Delete":
                    *P1:8,*V2LON,"4",*HOFF," = Load New DRG File":
                    *P1:9,*V2LON,"5",*HOFF," = Regenerating DRG Keyword ":
                                           "Index":

                    *P1:11,"Select Option : ";
.
OPTN1000  KEYIN     *P17:11,*DV,UNDLN1,*P17:11,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL       * Exit
.
.                                                                     *C-137125
          BRANCH    OPTION,OPTN5000,OPTN5000,OPTN5000,OPTN5000,OPTN7000
.
          BEEP
          GOTO      OPTN1000
.
.******************************************** start of addition       *I-137125
OPTN5000  DISPLAY   *P1:13,"Select DRG Version : ";
.
          MOVE      "60",HLEF
          MOVE      "9",HTOP
          MOVE      "78",HRIG
          MOVE      "23",HBOT                                         *C-284669
.
          CALL      GETSCR00                     * save screen
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,*V2LON,HLEF,HTOP,HRIG,HBOT
.
          DISPLAY   *P62:10,*V2LON,"1.",*HOFF," DRG Ver 4.1 ":
                    *P62:11,*V2LON,"2.",*HOFF," DRG Ver 4.2 ":
                    *P62:12,*V2LON,"3.",*HOFF," DRG Ver 5.0 ":
                    *P62:13,*V2LON,"4.",*HOFF," DRG Ver 5.1 ":
                    *P62:14,*V2LON,"5.",*HOFF," DRG Ver 5.2 ":
                    *P62:15,*V2LON,"6.",*HOFF," DRG Ver 6.0 ":        *I-197814
                    *P62:16,*V2LON,"7.",*HOFF," DRG Ver 6.2 ":        *I-267797
                    *P62:17,*V2LON,"8.",*HOFF," DRG Ver 7.0 ":        *I-284669
                    *P62:18,*V2LON,"9.",*HOFF," DRG Ver 8.0 ":
                    *P61:19,*V2LON,"10.",*HOFF," DRG Ver 9.0 ":
                    *P61:20,*V2LON,"11.",*HOFF," DRG Ver 10.0":
                    *P61:21,*V2LON,"12.",*HOFF," DRG Ver 11.0":
                    *P61:22,*V2LON,"13.",*HOFF," DRG Ver 12.0";
.
OPTN6000  KEYIN     *P22:13,*DV,UNDLN1,*P22:13,*V2LON,D2;
          MATCH     SP2,D2
          IF        @EQUAL | @EOS
            CALL      PUTSCR00
            DISPLAY   *P1:13,*EL;
            GOTO      OPTN1000
          ENDIF
.
          TYPE      D2
          DISPLAY   *P22:13,"  ";
          GOTO      OPTN6000 IF NOT EQUAL
          CALL      PUTSCR00
.                                                                     *C-197814
          MOVE      D2,F2
          LOAD      DRGVERZ,F2,DRG041,DRG042,DRG050,DRG051,DRG052:
                            DRG060,DRG062,DRG070,DRG080,DRG090,DRG100,DRG110:
                            DRG120
          UNPACK    DRGVERZ,D1,DV1,DV2
          PACK      DRGDESZ,DV1,DOT,DV2
          DISPLAY   *P22:13,DRGDESZ
          GOTO      OPTN9000
.
.
OPTN7000  MOVE      "All",DRGVERZ
.
.********************************************   end of addition       *I-137125
.
OPTN9000  LOAD      DISPOPTN,OPTION,INSERT,CHANGE,DELETE,LOAD,INDEX
          SQUEEZE   DISPOPTN
.                                                                     *C-137125
          IF        OPTION = 4
            DISPLAY   *P49:2,*V2LON," - ",*+,DISPOPTN,SP1;
          ELSE
            DISPLAY   *P49:2,*V2LON," - ",*+,DISPOPTN," - DRG Ver ",DRGDESZ,SP2;
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 DSCR0000               Called by: ML0000   *
.*                           Display Screen Format                            *
.******************************************************************************
DSCR0000  DISPLAY   *P1:4,*EF,*P5:4,"DRG Code        : ": 
                    *P5:5,"DRG Version     : ":                       *I-137125
                    *P1:7,*V2LON," 1",*HOFF,". DRG Description : ":
                    *P1:8,*EL,*V2LON," 2",*HOFF,". MDC Code        : ":
                    *P1:9,*V2LON," 3",*HOFF,". Low Trim        : ":
                    *P1:10,*V2LON," 4",*HOFF,". High Trim       : ":
                    *P1:11,*V2LON," 5",*HOFF,". Resource Weight : ":
                    *P1:12,*V2LON," 6",*HOFF,". DRG Partition   : ";
.
          IF        CLSFLG=1
            DISPLAY   *P1:13,*V2LON," 7",*HOFF,". Clinical Spec.  : ";
          ENDIF
          CALL      CLRF0000                * Clear file variables
DSCR9999  RETURN     
+
.******************************************************************************
.*                                 CLRF0000               Called by: DSCR0000 *
.*                           Clear File Variables                  & KDRG0000 *
.******************************************************************************
CLRF0000  MOVE      SP5,PTDWDRGC
          MOVE      SP5,PTDWMDCC
          MOVE      SP60,PTDWDESC
          MOVE      ZERO,PTDWLOWT
          MOVE      ZERO,PTDWHIGH
          MOVE      ZERO,PTDWRWGT
          MOVE      SP2,PTDWCLSP
          MOVE      SP3,PTDWDRGV                                      *I-137125
          MOVE      SP70,PTDWWTEP
          MOVE      SP70,PTDWDRGT
          MOVE      SP30,PTDWSPAR
CLRF9999  RETURN
+
.******************************************************************************
.*                                 DISP0000               Called by: CDRG0000 *
.*                      Display Details For Chosen Code                       *
.******************************************************************************
DISP0000  DISPLAY   *P23:4,*V2LON,PTDWDRGC:
                    *P23:5,PTDWDRGV:                                  *I-137125
                    *P23:7,PTDWDESC:
                    *P23:8,PTDWMDCC:
                    *P23:9,PTDWLOWT:
                    *P23:10,PTDWHIGH:
                    *P23:11,PTDWRWGT:
                    *P23:12,PTDWDRGT;
          IF        CLSFLG=1
            PACK      KEY2,PTDWCLSP
            CALL      RDPTCLS1              * Read a clinical spec file rec
            IF        OVRCD<>1
              DISPLAY   *P23:13,*V2LON,PTDWCLSP,SP2,PTCIDESC;         *C-137125
            ENDIF
          ENDIF
DISP9999  RETURN
+
.******************************************************************************
.*                                 IDRG0000               Called by: ML0000   *
.*                            Input The DRG Code                              *
.******************************************************************************
IDRG0000  CALL      KDRG0000                * Keyin DRG code
          BRANCH    EXIT,IDRG9500,IDRG1000
          GOTO      IDRG9000
.
IDRG1000  DISPLAY   *P1:24,*EL,*B,"DRG Code Exists. ";
          CALL      HITENTER
          GOTO      IDRG0000
.
IDRG9000  MOVE      ZERO,EXIT
          GOTO      IDRG9999
.
IDRG9500  MOVE      ONE,EXIT
IDRG9999  RETURN     
+
.******************************************************************************
.*                                 CDRG0000               Called by: ML0000   *
.*                            Input The DRG Code                              *
.******************************************************************************
CDRG0000  CALL      KDRG0000                * Keyin DRG code
          BRANCH    EXIT,CDRG9500,CDRG1000,CDRG2000
.
CDRG1000  CALL      DISP0000                * Display details
          GOTO      CDRG9000
.
CDRG2000  DISPLAY   *P1:24,*EL,*B,"DRG Code Does Not Exist. ";
          CALL      HITENTER
          GOTO      CDRG0000
.
CDRG9000  MOVE      ZERO,EXIT
          GOTO      CDRG9999
.
CDRG9500  MOVE      ONE,EXIT
CDRG9999  RETURN     
+
.******************************************************************************
.*                                 KDRG0000               Called by: CDRG0000 *
.*                            Input The DRG Code                     IDRG0000 *
.*  Returns:  EXIT     = 1 Exit                                               *
.*                       2 Records exists                                     *
.*                       3 No record                                          *
.******************************************************************************
KDRG0000  CALL      CLRF0000                * Clear file variables
          KEYIN     *P23:4,*EL,*V2LON,DRGCODE
.
          SQUEEZE   DRGCODE
          ENDSET    DRGCODE
          APPEND    SP4,DRGCODE
          RESET     DRGCODE
.
          MATCH     SP4,DRGCODE
          GOTO      KDRG9500 IF EQUAL
.
          MATCH     EXITCHAR,DRGCODE
          GOTO      KDRG9500 IF EQUAL
.
          MOVE      ZERO,OVRCD
          MATCH     QUEST,DRGCODE
          IF        @EQUAL
            PROC      HPTKDA00 IF EQUAL
            SQUEEZE   PTDWDRGC
            PACK      PTDWDRGC,PTDWDRGC,SP4
            MOVE      PTDWDRGC,DRGCODE
          ENDIF
          BRANCH    OVRCD,KDRG0000
.
. ----- Validate DRG Code -----
.
          PACK      KEY7,DRGCODE,DRGVERZ,SP10                         *C-137125
          CALL      RDPTDGW1
          BRANCH    OVRCD,KDRG9000
.
          GOTO      KDRG8000
.
KDRG8000  MOVE      TWO,EXIT
          GOTO      KDRG9999
.
KDRG9000  MOVE      THREE,EXIT
          DISPLAY   *P23:5,*EL,*V2LON,DRGVERZ                         *I-137125
          MOVE      DRGVERZ,PTDWDRGV                                  *I-137125
          GOTO      KDRG9999
.
KDRG9500  MOVE      ONE,EXIT
KDRG9999  RETURN     
+
.******************************************************************************
.*                                 INST0000               Called by: ML0000   *
.*                             Input The Details                              *
.******************************************************************************
INST0000  CALL      KDES0000                * Keyin DRG desc
          BRANCH    EXIT,INST9999

          CALL      KMDC0000                * Keyin MDC code
          BRANCH    EXIT,INST9999
.
          CALL      KLOW0000                * Keyin low trim
          BRANCH    EXIT,INST9999
.
          CALL      KHIG0000                * Keyin high trim
          BRANCH    EXIT,INST9999
.
          CALL      KRES0000                * Keyin resource weight
          BRANCH    EXIT,INST9999
.
          CALL      KDTY0000                * Keyin DRG Type
          BRANCH    EXIT,INST9999
.
          IF        CLSFLG=1
            CALL      KCLS0000              * Keyin clinical speciality
            BRANCH    EXIT,INST9999
          ENDIF
.
INST9999  RETURN
+
.******************************************************************************
.*                                 CHGE0000               Called by: ML0000   *
.*                            Change The Details                              *
.******************************************************************************
CHGE0000  CALL      MAINQST
          IF        CLSFLG=1
            BRANCH    CCITEM,CHGE1000,CHGE2000,CHGE3000,CHGE4000,CHGE5000:
                             CHGE6000,CHGE7000
          ELSE
            BRANCH    CCITEM,CHGE1000,CHGE2000,CHGE3000,CHGE4000,CHGE5000:
                             CHGE6000
          ENDIF
.
          COMPARE   ZERO,CCITEM
          GOTO      CHGE9000 IF EQUAL       * Post
          GOTO      CHGE9500 IF LESS        * Cancel
.
          BEEP
          GOTO      CHGE0000
.
. ----- Change The Details -----
.
CHGE1000  CALL      KDES0000                * Keyin DRG desc
          BRANCH    EXIT,CHGE9999
          GOTO      CHGE0000
.
CHGE2000  CALL      KMDC0000                * Keyin MDC code
          BRANCH    EXIT,CHGE9999
          GOTO      CHGE0000
.
CHGE3000  CALL      KLOW0000                * Keyin low trim
          BRANCH    EXIT,CHGE9999
          GOTO      CHGE0000
.
CHGE4000  CALL      KHIG0000                * Keyin high trim
          BRANCH    EXIT,CHGE9999
          GOTO      CHGE0000
.
CHGE5000  CALL      KRES0000                * Keyin resource weight
          BRANCH    EXIT,CHGE9999
          GOTO      CHGE0000
.
CHGE6000  CALL      KDTY0000                * Keyin DRG Type
          BRANCH    EXIT,CHGE9999
          GOTO      CHGE0000
.
CHGE7000  CALL      KCLS0000                * Keyin clinical speciality
          BRANCH    EXIT,CHGE9999
          GOTO      CHGE0000
.
CHGE9000  MOVE      ZERO,EXIT
          GOTO      CHGE9999
.
CHGE9500  MOVE      ONE,EXIT
CHGE9999  RETURN
+
.******************************************************************************
.*                                 KDES0000               Called by: CHGE0000 *
.*                         Keyin The DRG Description               & INST0000 *
.******************************************************************************
KDES0000  KEYIN     *P23:7,*EL,*V2LON,*DV,PTDWDESC:                   *C-137125
                    *P23:7,*V2LON,*RV,PTDWDESC:                       *C-137125
                    *P23:7,*V2LON,*DV,PTDWDESC                        *C-137125
.
          ENDSET    PTDWDESC
          APPEND    SP60,PTDWDESC
          RESET     PTDWDESC
.
          MATCH     EXITCHAR,PTDWDESC
          GOTO      KDES9500 IF EQUAL
.
          MATCH     SP60,PTDWDESC
          GOTO      KDES0000 IF EQUAL
.
KDES9000  MOVE      ZERO,EXIT
          GOTO      KDES9999
.
KDES9500  MOVE      ONE,EXIT
KDES9999  RETURN     
+
.******************************************************************************
.*                                 KMDC0000               Called by: CHGE0000 *
.*                            Keyin The MDC Code                   & INST0000 *
.******************************************************************************
KMDC0000  DISPLAY   *P23:8,*EL,*V2LON,PTDWMDCC;                       *C-137125
          KEYIN     *P23:8,*V2LON,*RV,PTDWMDCC;                       *C-137125
.
          ENDSET    PTDWMDCC 
          APPEND    SP4,PTDWMDCC
          RESET     PTDWMDCC
.
          MATCH     EXITCHAR,PTDWMDCC
          GOTO      KMDC9500 IF EQUAL
.
          MATCH     SP4,PTDWMDCC
          GOTO      KMDC0000 IF EQUAL
.
          SQUEEZE   PTDWMDCC
          PACK      PTDWMDCC,PTDWMDCC,SP4
          UNPACK    PTDWMDCC,KEY2,KEY1
          REP       " 0",KEY2
          PACK      PTDWMDCC,KEY2,SP4        * Format (xx )
.
KMDC1000  DISPLAY   *P23:8,*EL,*V2LON,PTDWMDCC;                       *C-137125
.
KMDC9000  MOVE      ZERO,EXIT
          GOTO      KMDC9999
.
KMDC9500  MOVE      ONE,EXIT
KMDC9999  RETURN     
+
.******************************************************************************
.*                                 KLOW0000               Called by: CHGE0000 *
.*                            Keyin The Low Trim                   & INST0000 *
.******************************************************************************
KLOW0000  KEYIN     *P23:9,*EL,*V2LON,*DV,PTDWLOWT:                   *C-137125
                    *P23:9,*V2LON,*RV,PTDWLOWT:                       *C-137125
                    *P23:9,*V2LON,*DV,PTDWLOWT                        *C-137125
.
KLOW9000  MOVE      ZERO,EXIT
          GOTO      KLOW9999
.
KLOW9500  MOVE      ONE,EXIT
KLOW9999  RETURN     
+
.******************************************************************************
.*                                 KHIG0000               Called by: CHGE0000 *
.*                            Keyin The High Trim                  & INST0000 *
.******************************************************************************
KHIG0000  KEYIN     *P23:10,*EL,*V2LON,*DV,PTDWHIGH:                  *C-137125
                    *P23:10,*V2LON,*RV,PTDWHIGH:                      *C-137125
                    *P23:10,*V2LON,*DV,PTDWHIGH                       *C-137125
.
KHIG9000  MOVE      ZERO,EXIT
          GOTO      KHIG9999
.
KHIG9500  MOVE      ONE,EXIT
KHIG9999  RETURN     
+
.******************************************************************************
.*                                 KHIG0000               Called by: CHGE0000 *
.*                         Keyin The Resource Weight               & INST0000 *
.******************************************************************************
KRES0000  KEYIN     *P23:11,*EL,*V2LON,*DV,PTDWRWGT:                  *C-137125
                    *P23:11,*V2LON,*RV,PTDWRWGT:                      *C-137125
                    *P23:11,*V2LON,*DV,PTDWRWGT                       *C-137125
.
KRES9000  MOVE      ZERO,EXIT
          GOTO      KRES9999
.
KRES9500  MOVE      ONE,EXIT
KRES9999  RETURN
+
.******************************************************************************
.*                                 KDTY0000               Called by: CHGE0000 *
.*                       Keyin DRG Partition                       & INST0000 *
.******************************************************************************
KDTY0000  MOVE      "23",ECOL               * keyin position
          MOVE      SP3,ACODE
          MOVE      "Dt",CODE
          CALL      KEYINCOD
          BRANCH    EXIT,KDTY1000,KDTY9999  * Nothing,exit entered
.
KDTY1000  MOVE      ACODE,PTDWDRGT
          DISPLAY   *P30:12,*V2LON,TDESC;

KDTY9999  RETURN
+
.******************************************************************************
.*                                 KCLS0000               Called by: CHGE0000 *
.*                       Keyin The Clinical Speciality             & INST0000 *
.******************************************************************************
KCLS0000  MOVE      "23",ECOL
          MOVE      "13",EVERT                                        *C-137125
          MOVE      PTDWCLSP,CKYIDEF2
          MOVE      ZERO,CKYIMAND
          CALL      PATCLSKY                * Keyin a clinical speciality code
          BRANCH    EXIT,KCLS1000,KCLS9500
.
          MOVE      PTCICODE,PTDWCLSP
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,PTDWCLSP,SP2,PTCIDESC;
          GOTO      KCLS9000
.
KCLS1000  MOVE      SP2,PTDWCLSP
          DISPLAY   *PECOL:EVERT,*EL;
.
KCLS9000  MOVE      ZERO,EXIT
          GOTO      KCLS9999
.
KCLS9500  MOVE      ONE,EXIT
KCLS9999  RETURN
+
.******************************************************************************
.*                                 POST0000               Called by: ML0000   *
.*                             Post DRG Records                               *
.******************************************************************************
POST0000  BRANCH    OPTION,POST1000,POST2000,POST3000
          GOTO      POST9999
.
. ----- Write A Record -----
.
POST1000  MOVE      DRGCODE,PTDWDRGC
          MOVE      DRGVERZ,PTDWDRGV                                  *I-137125
          PACK      KEY7,PTDWDRGC,PTDWDRGV,SP10                       *C-137125
          CALL      WRPTDGW1
          PROC      PATKDRUP
          GOTO      POST9999
.
. ----- Update A Record -----
.
POST2000  PACK      KEY7,PTDWDRGC,PTDWDRGV,SP10                       *C-137125
          CALL      UPPTDGW1
          PROC      PATKDRUP
          GOTO      POST9999
.
. ----- Delete A Record -----
.
POST3000  CALL      DELQST
          IF        CEXIT = 1
            DISPLAY   *P1:24,*EL,*V2LON,"Deleting..."
            CALL      DEPTDGW1
          ENDIF
POST9999  RETURN
+
.******************************************************************************
.*                                 OPEN0000               Called by: ML0000   *
.*                           Open The New DRG File                            *
.******************************************************************************
OPEN0000  CLEAR     NEWFNAME
          APPEND    "newdgwaf",NEWFNAME
          RESET     NEWFNAME                * Setup new DRG filename
.
          CALL      KILL0000                * Delete any new DRG files
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFNAME,CMDLINE
          APPEND    NEWFILE,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID          * Create the new DRG file
          OPEN      NEWFILE1,NEWFNAME
          OPEN      NEWFILE2,NEWFNAME
          OPEN      NEWFILE3,NEWFNAME
OPEN9999  RETURN
+
.******************************************************************************
.*                                 KILL0000               Called by: ML0000   *
.*                          Delete The New DRG File                & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     NEWFILE1
          CLOSE     NEWFILE2
          CLOSE     NEWFILE3
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    NEWFNAME,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      NEWFILE1,NEWFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,KILL9999
.
          MOVE      SP10,KEY7                                         *C-137125
          CALL      RSNEW1
KILL1000  CALL      RKNEW1
          BRANCH    OVRCD,KILL9000
          PACK      KEY7,PTDWDRGC,PTDWDRGV,SP10
          CALL      DENEW1
          GOTO      KILL1000
.
KILL9000  CLOSE     NEWFILE1
KILL9999  RETURN
+
.******************************************************************************
.*                                 KTFN0000               Called by: ML0000   *
.*                            Keyin Textfile Name                             *
.******************************************************************************
KTFN0000  DISPLAY   *P1:13,*EL,"New DRG Textfile : ";
KTFN1000  KEYIN     *P20:13,*EL,*DV,UNDLN8,*P20:13,*V2LON,TXTFNAME;
.
          ENDSET    TXTFNAME
          APPEND    SP8,TXTFNAME
          RESET     TXTFNAME
.
          MATCH     SP8,TXTFNAME
          GOTO      KTFN9500 IF EQUAL       * Spaces entered
.
          CMATCH    EXITCHAR,TXTFNAME
          GOTO      KTFN9500 IF EQUAL       * Exitchar entered
.
          STRIP     TXTFNAME
          DISPLAY   *P20:13,*EL,*V2LON,*+,TXTFNAME,".txt"
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TXTFILE1,TXTFNAME
          TRAPCLR   IO
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Textfile ",*V2LON,*+,TXTFNAME,".txt":
                      *HOFF," doesn't exist. ";
            CALL      HITENTER
            GOTO      KTFN1000
          ENDIF
.
          CALL      RDTXT1                  * Read DRG textfile
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"DRG textfile is empty. ";
            CALL      HITENTER
            GOTO      KTFN1000
          ENDIF
          CLOSE     TXTFILE1
          OPEN      TXTFILE1,TXTFNAME       * Re position at the start
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,KTFN9000,KTFN1000,KTFN9500
.                         Yes      No       Cancel
.
KTFN9000  MOVE      ZERO,EXIT
          GOTO      KTFN9999
.
KTFN9500  MOVE      ONE,EXIT
KTFN9999  RETURN
+
.******************************************************************************
.*                                 LNDF0000               Called by: ML0000   *
.*                             Load New DRG File                              *
.******************************************************************************
LNDF0000  MOVE      ZERO,LOADCNT
          MOVE      ZERO,ERRNUMB
          MOVE      ZERO,CPAGENO
          MOVE      "99",CLNO               * initialise line count
          DISPLAY   *P1:24,*EL,"DRG : ",*P12:24,"MDC : ";
LNDF1000  CALL      RDTXT1                  * Read DRG textfile
          BRANCH    OVRCD,LNDF8000
.
          SQUEEZE   TXTDRGN
          PACK      TXTDRGN,TXTDRGN,SP4
          MATCH     SP4,TXTDRGN
          GOTO      LNDF1000 IF EQUAL       * Blank DRG code dont add
.
.******************************************** start of addition       *I-137125
          PACK      TXTDRGV,TXTDRGV,SP3
          IF        FIRSTIME = 1
            MOVE      ZERO,FIRSTIME
            MATCH     SP3,TXTDRGV
            IF        @EQUAL
              DISPLAY   *P1:23,"DRG Version is blank - file will be loaded":
                               " as ",DRGDESZ;
            ELSE
              MATCH     TXTDRGV,DRGVERZ
              GOTO      LNDF1100 IF EQUAL
              UNPACK    TXTDRGV,ANS,DIM1,KEY1
              PACK      KEY3,DIM1,DOT,KEY1
              DISPLAY   *P1:23,"DRG Version is ",KEY3," - file will be loaded":
                               " as",DRGDESZ;
            ENDIF
            CALL      CONTQST
            BRANCH    CEXIT,LNDF1100,LNDF9999,LNDF9999
LNDF1100    DISPLAY   *P1:23,*EL:
                      *P1:24,*EL,"DRG : ",*P12:24,"MDC : ";
          ENDIF
.
          MOVE      DRGVERZ,TXTDRGV
.********************************************   end of addition       *I-137125
.
           SQUEEZE   TXTMDCN
           PACK      TXTMDCN,TXTMDCN,SP4
.......... UNPACK    TXTMDCN,KEY2,KEY1
.......... MATCH     SP1,KEY1
.......... GOTO      LNDF2000 IF EQUAL       * Correct MDC code format
.
.......... UNPACK    TXTMDCN,KEY1,KEY2
.......... REP       " 0",KEY2
.......... PACK      TXTMDCN,KEY2,SP4        * Format (xx )
.
LNDF2000  CALL      CLRF0000                * Clear current file variables
          PACK      PTDWDRGC,TXTDRGN,SP70   * DRG code
          PACK      KEY7,PTDWDRGC,DRGVERZ,SP10                        *C-137125
          CALL      RDPTDGW1                * Read current DRG file
.
. ----- Set Up New DRG File Variables -----
.
          IF        OVRCD=1
            MOVE      DRGVERZ,PTDWDRGV      * set DRV version         *I-137125
            PACK      PTDWMDCC,TXTMDCN,SP70 * MDC code
          ELSE
            MATCH     SP70,TXTMDCN
            IF        !@EQUAL
              PACK      PTDWMDCC,TXTMDCN,SP70    * MDC code
            ENDIF
          ENDIF
.
          IF        OVRCD=1
            PACK      PTDWDESC,TXTDESC,SP70      * Use textfile DRG description
          ELSE
            MATCH     SP70,TXTDESC
            IF        !@EQUAL
              PACK      PTDWDESC,TXTDESC,SP70    * Use textfile DRG description
            ENDIF
          ENDIF
.
          IF        VALDPASS=1
            CALL      VALC0000              * validation only
            GOTO      LNDF1000
          ENDIF
.
          RJUSTIFY  TXTLOWT
          REP       " 0",TXTLOWT
          TYPE      TXTLOWT
          IF        @EQUAL
            MOVE      TXTLOWT,PTDWLOWT      * Use textfile low trim
          ENDIF
.
          RJUSTIFY  TXTHIGH
          REP       " 0",TXTHIGH
          TYPE      TXTHIGH
          IF        @EQUAL
            MOVE      TXTHIGH,PTDWHIGH      * Use textfile high trim
          ENDIF
.
          RJUSTIFY  TXTRWGT 
          REP       " 0",TXTRWGT
          TYPE      TXTRWGT
          IF        @EQUAL
            MOVE      TXTRWGT,PTDWRWGT      * Use textfile resource weight
          ENDIF
.
          PACK      PTDWDRGT,TXTDRGT,SP70   * DRG Partition    Cat Dt
          MOVE      "Dt",KEY2
          PACK      KEY5,KEY2,PTDWDRGT
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,PTDWDRGT         * Invalid Partition code
          ENDIF
.
.
          TYPE      TXTCLSP
          IF        @EQUAL
            PACK      KEY2,TXTCLSP,SP70
            CALL      RDPTCLS1                  * Read a clinical spec file rec
            IF        OVRCD<>1
              PACK      PTDWCLSP,TXTCLSP,SP70   * Use textfile clincal speciality
            ENDIF
          ENDIF
          MOVE      SP70,PTDWWTEP
.
. ----- Write A Record To The New DRG File -----
.
          PACK      KEY7,PTDWDRGC,PTDWDRGV,SP10                       *C-137125
          CALL      RANEW1                  * Read new DRG file with DRG code
          IF        OVRCD=1
            CALL      WRNEW1                * Write a new DRG code
          ELSE
            CALL      UPNEW1                * Update a new DRG code
          ENDIF
          ADD       ONE,LOADCNT             * Increment load DRG counter
          DISPLAY   *P7:24,*V2LON,PTDWDRGC,*P18:24,PTDWMDCC;
          GOTO      LNDF1000
.
. ----- Finished Loading New DRG File -----
.
LNDF8000  BRANCH    VALDPASS,LNDF9000
.
          DISPLAY   *P1:24,*V2LON,LOADCNT,*HOFF," DRG's Loaded Into DRG File. ";
          CALL      HITENTER
          COMPARE   ZERO,LOADCNT
          GOTO      LNDF9999 IF EQUAL       * No DRG's loaded
.
          CLOSE     TXTFILE1
.
. ----- Save Current DRG File In Case We Stuff Things Up -----
.
          DISPLAY   *P1:24,*EL,"Saving old DRG file ... ",*W1;
.         EXECUTE   "cp `ldat patdgwaf.dat` olddgwaf.dat",TASKID
.         EXECUTE   "cp `ldat patdgwaf.idx` olddgwaf.idx",TASKID
.
..........CALL      KOLD0000           * Remove records from old file
..........CALL      CCUR0000           * Copy Current to old file
.
.
          DISPLAY   *P1:24,*EL,"Moving new DRG file ... ",*W1;
.         EXECUTE   "mv newdgwaf.dat `ldat patdgwaf.dat`",TASKID
.         EXECUTE   "mv newdgwaf.idx `ldat patdgwaf.idx`",TASKID
.
.         CALL      KCUR0000           * Remove records from current file
          CALL      CNEW0000           * Copy New to Current
.
          CLOSE     NEWFILE1
          CLOSE     NEWFILE2
          CLOSE     NEWFILE3
          GOTO      LNDF9999
.
LNDF9000  COMPARE   ZERO,ERRNUMB
          GOTO      LNDF9999 IF EQUAL
.
          CALL      LINE0000                   * yes - print bottom line
          PRINT     *N,*1,"** Errors detected - file will not be uploaded **"
.
LNDF9999  RETURN
+
.******************************************************************************
.*        Validation
.******************************************************************************
VALC0000  PACK      PTDWDRGT,TXTDRGT,SP70   * DRG Partition    Cat Dt
          MATCH     SP70,PTDWDRGT
          GOTO      VALC9999 IF EQUAL
.
          MOVE      "Dt",KEY2
          PACK      KEY5,KEY2,PTDWDRGT
          CALL      RDCODE1
          BRANCH    OVRCD,VALC1000
.
          MATCH     "M",TCINDC1
          GOTO      VALC9999 IF EQUAL       * Medical
          MATCH     "I",TCINDC1
          GOTO      VALC9999 IF EQUAL       * Intervention
.
VALC1000  ADD       ONE,ERRNUMB
          CLEAR     ERRDESC
          APPEND    "Invalid DRG Partition (DRG Type)-",ERRDESC
          APPEND    " Code must have Indic1 = M or I",ERRDESC
          APPEND    SP70,ERRDESC
          RESET     ERRDESC
.
          CALL      PERR0000
.
VALC9999  RETURN
+
.******************************************************************************
.*        Copy Current DRG file to Old file                                   *
.******************************************************************************
CCUR0000  OPEN      OLDFILE1,"olddgwaf"
          PACK      KEY7,SP10                                         *C-137125
          CALL      RSPTDGW1
CCUR1000  CALL      RKPTDGW1
          BRANCH    OVRCD,CCUR9999
.
          PACK      KEY7,PTDWDRGC,PTDWDRGV                            *C-137125
          CALL      RAOLD1
          IF        OVRCD=1
            CALL      WROLD1          * write a current record to old file
          ELSE
            CALL      UPOLD1
          ENDIF
          GOTO      CCUR1000
CCUR9999  RETURN
+
.******************************************************************************
.*        Copy New DRG file to Current file                                   *
.******************************************************************************
CNEW0000  CALL      HEAD0000
.
          PACK      KEY7,SP10                                         *C-137125
          CALL      RSNEW1  
CNEW1000  CALL      RKNEW1
          BRANCH    OVRCD,CNEW9000
.
          PACK      KEY7,PTDWDRGC,PTDWDRGV                            *C-137125
          CALL      RAPTDGW1
          IF        OVRCD=1
            CALL      WRPTDGW1        * write a new record to current file
            CALL      PLNE0000
          ELSE
            CALL      UPPTDGW1
          ENDIF
          GOTO      CNEW1000
.
CNEW9000  CALL      LINE0000
.
CNEW9999  RETURN
+
.******************************************************************************
.*                                 KOLD0000                                   *
.*                            Kill old file                                   *
.******************************************************************************
KOLD0000  OPEN      OLDFILE1,"olddgwaf"
          MOVE      SP10,KEY7                                         *C-137125
          CALL      RSOLD1  
KOLD1000  CALL      RKOLD1  
          BRANCH    OVRCD,KOLD9999
          PACK      KEY7,PTDWDRGC,PTDWDRGV                            *C-137125
          CALL      DEOLD1
          GOTO      KOLD1000
KOLD9999  RETURN
+
.******************************************************************************
.*                                 KCUR0000                                   *
.*                            Kill current patdgwaf file                      *
.******************************************************************************
KCUR0000  MOVE      SP10,KEY7                                         *C-137125
          CALL      RSPTDGW1
KCUR1000  CALL      RKPTDGW1
          BRANCH    OVRCD,KCUR9999
          PACK      KEY7,PTDWDRGC,PTDWDRGV                            *C-137125
          CALL      DEPTDGW1
          GOTO      KCUR1000
KCUR9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by:          *
.*                           Print The Report Header                          *
.******************************************************************************
.
HEAD0000  CLOCK     TIME,CTIMEIS
          COMPARE   ZERO,ERRNUMB
          GOTO      HEAD1000 IF EQUAL
.
          MOVE      "- Errors on Upload",CPHDROPT
.
          CALL      HEAD132                 * Print the report header
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"DRG",*12,PIPE,*14,"MDC",*19,PIPE:
                    *21,"Error Description",*132,PIPE
.
          GOTO      HEAD3000
.
HEAD1000  MOVE      "- Added to patdgwaf",CPHDROPT
.
          CALL      HEAD132                 * Print the report header
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"DRG",*12,PIPE,*14,"MDC",*19,PIPE:
                    *21,"Description",*91,PIPE,*93,"DRG Version",*132,PIPE
.
HEAD3000  CALL      LINE0000                     * draw line across page
.
          MOVE      SEVEN,CLNO                   * initialise line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      Draw line across page                               *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
.******************************************************************************
.*                                  PERR0000              Called by:          *
.*                           Print Error messages                             *
.******************************************************************************
PERR0000  COMPARE   "99",CLNO
          CALL      HEAD0000 IF EQUAL            * first page
.
          COMPARE   CLNO,SIXTY3                  * page full ?
          IF        @LESS
            CALL      LINE0000                   * yes - print bottom line
            CALL      HEAD0000                   * print header
          ENDIF
.
          PRINT     *1,PIPE,*3,PTDWDRGC,*12,PIPE,*14,PTDWMDCC,*19,PIPE:
                    *21,ERRDESC,*132,PIPE
          ADD       ONE,CLNO                     * increment page line count
.
PERR9999  RETURN
+
.*****************************************************************************
.*                              PLNE0000           Called by:                *
.*                        Print a detail line                                *
.*****************************************************************************
.
PLNE0000  COMPARE   CLNO,SIXTY3                  * page full ?
          IF        @LESS
            CALL      LINE0000                   * yes - print bottom line
            CALL      HEAD0000                   * print header
          ENDIF
.
PLNE1000  PRINT     *1,PIPE,*3,PTDWDRGC,*12,PIPE,*14,PTDWMDCC,*19,PIPE:
                    *21,PTDWDESC,*91,PIPE,*93,PTDWDRGV,*132,PIPE
          ADD       ONE,CLNO                     * increment page line count
.
PLNE9999  RETURN
+
.******************************************************************************
.*                          I/O Routines For Tempfiles                        *
.******************************************************************************
.                                           * start of changes        *C-137125
RAOLD1    MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      OLDFILE1,KEY7;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSOLD1    RESET     KEY7
          READ      OLDFILE1,KEY7;;
          RETURN  
.
RKOLD1    MOVE      ZERO,OVRCD
          READKS    OLDFILE1;PTDWDRGC,PTDWMDCC,PTDWDESC,PTDWLOWT,PTDWHIGH:
                            PTDWRWGT,PTDWCLSP,PTDWDRGV,PTDWSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
DEOLD1    MOVE      ZERO,OVRCD
          RESET     KEY7
          DELETE    OLDFILE1,KEY7
          RETURN
.
UPOLD1    UPDATE    OLDFILE1;PTDWDRGC,PTDWMDCC,PTDWDESC,PTDWLOWT,PTDWHIGH:
                             PTDWRWGT,PTDWCLSP,PTDWDRGV,PTDWSPAR
          RETURN
.
WROLD1    RESET     KEY7
          WRITE     OLDFILE1,KEY7;PTDWDRGC,PTDWMDCC,PTDWDESC,PTDWLOWT,PTDWHIGH:
                                  PTDWRWGT,PTDWCLSP,PTDWDRGV,PTDWSPAR
          RETURN
+
.******************************************************************************
.*                          I/O Routines For Tempfiles                        *
.******************************************************************************
RANEW1    MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      NEWFILE1,KEY7;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
UPNEW1    UPDATE    NEWFILE1;PTDWDRGC,PTDWMDCC,PTDWDESC,PTDWLOWT,PTDWHIGH:
                             PTDWRWGT,PTDWCLSP,PTDWDRGV,PTDWWTEP,PTDWDRGT:
                             PTDWSPAR
          RETURN
.
WRNEW1    RESET     KEY7
          WRITE     NEWFILE1,KEY7;PTDWDRGC,PTDWMDCC,PTDWDESC,PTDWLOWT,PTDWHIGH:
                                  PTDWRWGT,PTDWCLSP,PTDWDRGV,PTDWWTEP,PTDWDRGT:
                                  PTDWSPAR
          RETURN
.
RSNEW1    RESET     KEY7
          READ      NEWFILE1,KEY7;;
          RETURN  
.
RKNEW1    MOVE      ZERO,OVRCD
          READKS    NEWFILE1;PTDWDRGC,PTDWMDCC,PTDWDESC,PTDWLOWT,PTDWHIGH:
                            PTDWRWGT,PTDWCLSP,PTDWDRGV,PTDWWTEP,PTDWDRGT:
                            PTDWSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
DENEW1    MOVE      ZERO,OVRCD
          RESET     KEY7
          DELETE    NEWFILE1,KEY7
          RETURN
.
RDTXT1    MOVE      ZERO,OVRCD
          READ      TXTFILE1,SEQ;TXTDRGN,TXTMDCN,TXTDESC,TXTLOWT,TXTHIGH:
                                 TXTRWGT,TXTCLSP,TXTDRGV,TXTDRGT,TXTSPAR
          GOTO      OVERCOND IF OVER
          RETURN

+
.==============================================================================
.
          INC       STDGENCD
          INC       STDKWSCD
          INC       PATDSCOD
.
          INC       PATCLSKY
          INC       PATDSDGW
          INC       PATKDRPR
          INC       PATKDRKY
          INC       KEYINCOD
.
          INC       PATCODIO/INC
          INC       PATCLSIO/INC
          INC       PATDGWIO/INC
          INC       PATKDRIO/INC
+
.


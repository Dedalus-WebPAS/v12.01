. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBABIL36                                            *
. *                                                                           *
. *     FUNCTION:         Transferred Deposit Audit report                    *
. *                                                                           *
. *     DATE WRITTEN:     14/12/2021                                          *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
. *---------------------------------------------------------------------------*
. *        V12.02.00 14/12/2021  J.Tan          TSK 0309320                   *
. *                  Created Transferred Deposit Audit report                 *
. *****************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
          INC       PATDHEAD/INC
          INC       PATCALAG/INC
          INC       WEBCONFD/INC
.
          INC       PATCONFD/INC            * Control file
          INC       NZHISIFD/INC
          INC       OUTPREFD/INC
          INC       BOKRC1FD/INC            * Booking file
          INC       PATMA1FD/INC            * Master file
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC           * visit extention file
          INC       PMSPX2FD/INC
          INC       PATPR1FD/INC            * Pre admission file
          INC       PATGSRFD/INC            * Surname file
.
          INC       PATCODFD/INC
          INC       COMAUDFD/INC
          INC       RCPREGFD/INC
          INC       PRIPRAFD/INC
          INC       PMSHCPFD/INC
          INC       PRIALSFD/INC                 * Medical Prac.all user access
          INC       PRISECFD/INC                 * Medical Prac.security access
.
.
.               WORK AREA
.
CURRDATE  DIM       8
ENDDATE   DIM       8
FRMDATE   DIM       8
NMPNUMB   DIM       20
USERID    DIM       10
.
BJDAY     FORM      3
CJDAY     FORM      3
.
AEVISIT   INIT      "AEPAT"
OTVISIT   INIT      "OTPAT"
IPVISIT   INIT      "INPAT"
PPVISIT   INIT      "PPRAC"
.
DOCFNAME  DIM       40
DISPNAME  DIM       60
FMTGNAME  DIM       35
FMTTITLE  DIM       10
FMTSNAME  DIM       35
NAME35    DIM       35
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
MDPRACFR  DIM       17
MDPRACTO  DIM       17
SERDOCFR  DIM       17
SERDOCTO  DIM       17
DEPTYPFR  DIM       15
DEPTYPTO  DIM       15
VISTYPFR  DIM       5
VISTYPTO  DIM       5
REGISTFR  DIM       15
REGISTTO  DIM       15
.
PRGID     INIT      "IBABIL36"
PRGNAM    INIT      "Transferred Deposit Audit Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                              MAIN0000                                      *
.*                  Controlling Logic (Mainline code)                         *
.******************************************************************************
MAIN0000  CALL      INIT0000            * Initialisation and open files
.
MAIN1000  CALL      OPTN0000            * Select options
          BRANCH    EXIT,MAIN9999       * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN2000:   * Proceed to process
                            MAIN3000:   * Proceed to process
                            MAIN2000    * Proceed to process
.
          GOTO      MAIN1000            * invalid selection; get again
.
MAIN2000  CALL      KDAT0000            * keyin range of date
          BRANCH    EXIT,MAIN1000
          GOTO      MAIN4000
.
MAIN3000  CALL      KYUR0000            * keyin U/R Number
          BRANCH    EXIT,MAIN1000
          GOTO      MAIN4000
.
MAIN4000  CALL      GUSR0000            * Keyin user id
          CALL      CONTQST             * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:     * Yes
                          MAIN2000:     * No; prompt options again
                          MAIN1000      * Cancel
.
MAIN5000  CALL      PROC0000            * Processing
          GOTO      MAIN1000
.
MAIN9000  DISPLAY   *P1:24,"Transferred Deposit Audit file doesn't exit.",*W2;
.
MAIN9999  CHAIN     PGM                 * Chain back to program
+
.******************************************************************************
.*                                INIT0000             Called by: MAIN0000    *
.*                  Display headings and open files                           *
.******************************************************************************
INIT0000  CALL      DISPHEAD                      * display heading
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"rcpregaf";
          OPEN      RCPREGA1,"rcpregaf"
.
          DISPLAY   *P64:24,"pripracf";
          OPEN      PRIPRAC1,"pripracf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"comaudaf";
          OPEN      COMAUDA3,"comaudaf"
          OPEN      COMAUDA4,"comaudaf"
.
          OPEN      PRIALSA1,"prialsaf"
          OPEN      PRISECA1,"prisecaf"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                OPTN0000             Called by: MAIN0000    *
.*                  Get user options or exit                                  *
.*                                                                            *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                    *
.*                        TRUE  (1)  Exit option selected                     *
.*              COPTION = 0 Exit                                              *
.*                        1 Run Report                                        *
.******************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = By Date Range":
                    *P1:6,*V2LON,TWO,*HOFF," = By U/R Number"
.
OPTN1000  KEYIN     *P1:9,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:9,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL             * exit
.
          BRANCH    COPTION,OPTN9000,OPTN9000
.
.         BEEP
.         GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.         Processing
.******************************************************************************
PROC0000 DISPLAY   *P1:24,*EL,*P20:24,"Receipt No :":
                    *P50:24,"Scanning :";
.
          CALL      HEAD0000
          BRANCH    COPTION,PROC1000,PROC2000
          GOTO      PROC9999
.
.         Option By Date range
.
PROC1000  PACK      KEY41,FRMDATE,SP70
          CALL      RSCMAUD4
PROC1100  CALL      RKCMAUD4
          BRANCH    OVRCD OF PROC9999
.
          DISPLAY   *P62:24,CMAURECN;
          MATCH     CMAUTDAT,ENDDATE           * check the end date
          GOTO      PROC9999 IF LESS
          GOTO      PROC8000
.
.         Option By U/R number
.
PROC2000  PACK      KEY41,PURNO,SP70
          CALL      RSCMAUD3
PROC2100  CALL      RKCMAUD3
          BRANCH    OVRCD OF PROC9999
.
          DISPLAY   *P62:24,CMAURECN;
          MATCH     PURNO,CMAUURNO             * Same U/R ?
          GOTO      PROC9999 IF NOT EQUAL
.
.         check the medical practice user access
.
PROC8000  PACK      KEY10,USERID,SP70
          CALL      RDPRALS1
          COMPARE   ZERO,OVRCD
          GOTO      PROC8200 IF EQUAL       * User has access to all Med.Prac.
.
          MATCH     SP70,CMAUFPRA
          IF        !@EQUAL
            PACK      KEY16,CMAUFPRA,USERID,SP70
            CALL      RDPRSEC1
            BRANCH    OVRCD,PROC8300          * no access
          ENDIF
          MATCH     SP70,CMAUTPRA
          IF        !@EQUAL
            PACK      KEY16,CMAUTPRA,USERID,SP70
            CALL      RDPRSEC1
            BRANCH    OVRCD,PROC8300          * no access
          ENDIF
.
PROC8200  DISPLAY   *P35:24,CMAURECN;
          CALL      PDAT0000                   * Print data
.
PROC8300  BRANCH    COPTION,PROC1100,PROC2100
.
PROC9000  CALL      PEND0000                   * Print end of report
PROC9999  RETURN
+
.******************************************************************************
.         Clear variables
.******************************************************************************
CVAR0000  MOVE     SP70,MDPRACFR
          MOVE     SP70,MDPRACTO
          MOVE     SP70,SERDOCFR
          MOVE     SP70,SERDOCTO
          MOVE     SP70,DEPTYPFR
          MOVE     SP70,DEPTYPTO
          MOVE     SP70,VISTYPFR
          MOVE     SP70,VISTYPTO
          MOVE     SP70,REGISTFR
          MOVE     SP70,REGISTTO
CVAR9999  RETURN
+
.******************************************************************************
.         Set variables
.******************************************************************************
SVAR0000  MATCH     SP70,CMAUFPRA
          IF        !@EQUAL
            PACK      KEY6,CMAUFPRA             * Medical practice from
            CALL      RDPRPR1
            IF        OVRCD=0
              PACK      MDPRACFR,PRPRDESC,SP70
            ENDIF
          ENDIF
.
          MATCH     SP70,CMAUTPRA
          IF        !@EQUAL
            PACK      KEY6,CMAUTPRA             * Medical practice to
            CALL      RDPRPR1
            IF        OVRCD=0
              PACK      MDPRACTO,PRPRDESC,SP70
            ENDIF
          ENDIF
.
          MATCH     SP70,CMAUFSDR
          IF        !@EQUAL
            PACK      KEY10,CMAUFSDR,SP70       * Service doctor from
            CALL      RDPMHCP1
            IF        OVRCD=0
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
              PACK      SERDOCFR,DOCFNAME,SP70
            ELSE
              CLEAR     SERDOCFR
              APPEND    "Invalid Doctor - ",SERDOCFR
              APPEND    CMAUFSDR,SERDOCFR
              RESET     SERDOCFR
            ENDIF
          ENDIF
.
          MATCH     SP70,CMAUTSDR
          IF        !@EQUAL
            PACK      KEY10,CMAUTSDR,SP70      * Service doctor to
            CALL      RDPMHCP1
            IF        OVRCD=0
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
              PACK      SERDOCTO,DOCFNAME,SP70
            ELSE
              CLEAR     SERDOCTO
              APPEND    "Invalid Doctor - ",SERDOCTO
              APPEND    CMAUTSDR,SERDOCTO
              RESET     SERDOCTO
            ENDIF
          ENDIF
.
          PACK     KEY5,ANSD,ANSP,CMAUFDTY    * Deposit type from
          CALL     RDCODE1
          IF       OVRCD=0
            PACK     DEPTYPFR,TDESC
          ENDIF
.
          PACK     KEY5,ANSD,ANSP,CMAUTDTY    * Deposit type to
          CALL     RDCODE1
          IF       OVRCD=0
            PACK     DEPTYPTO,TDESC
          ENDIF
.
          MOVE     ZERO,F1
          MOVE     CMAUFVTY,F1                * Visit type
          LOAD     VISTYPFR,F1,AEVISIT,OTVISIT,IPVISIT,PPVISIT
          MOVE     CMAUTVTY,F1
          LOAD     VISTYPTO,F1,AEVISIT,OTVISIT,IPVISIT,PPVISIT
.
          MOVE     CMAUFREG,KEY3              * Register from
          CALL     RDREGA1
          IF       OVRCD=0
            PACK     REGISTFR,REGDESC,SP70
          ENDIF
.
          MOVE     CMAUTREG,KEY3              * Register to
          CALL     RDREGA1
          IF       OVRCD=0
            PACK     REGISTTO,REGDESC,SP70
          ENDIF
.
SVAR9999  RETURN
+
.******************************************************************************
.         Print record 
.******************************************************************************
PDAT0000  CALL      CVAR0000               * Clear Variables
          CALL      SVAR0000               * Set Variables
.
          UNPACK    CMAUTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          COMPARE   FIFTY5,CLNO                  * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          BRANCH    COPTION,PDAT2000,PDAT4000
          GOTO      PDAT9999
.
.         Print By date range option
.
PDAT2000   PRINT    *1,"|",CMAURECN:
                    *14,"|",CMAUFADM:
                    *24,"|",MDPRACFR:
                    *42,"|",SERDOCFR:
                    *60,"|",DEPTYPFR:
                    *76,"|",VISTYPFR:
                    *84,"|",REGISTFR:
                    *101,"|",CMAUCUID:
                    *112,"|",CPCDATE," ",CMAUTTIM:
                    *132,"|"
.
          PRINT     *1,"|":
                    *14,"|",CMAUTADM:
                    *24,"|",MDPRACTO:
                    *42,"|",SERDOCTO:
                    *60,"|",DEPTYPTO:
                    *76,"|",VISTYPTO:
                    *84,"|",REGISTTO:
                    *101,"|":
                    *112,"|":
                    *132,"|"
.
          GOTO      PDAT9000
.
.         Print By U/R number option
.
PDAT4000  PRINT     *1,"|",CMAUURNO,*10,"|",CMAURECN:
                    *24,"|",CMAUFADM;
.
          UNPACK    MDPRACFR,KEY15
          PRINT     *34,"|",KEY15;
.
          UNPACK    SERDOCFR,KEY15
          PRINT     *50,"|",KEY15;
.
          UNPACK    DEPTYPFR,KEY13
          PRINT     *66,"|",KEY13:
                    *80,"|",VISTYPFR;
.
          UNPACK    REGISTFR,KEY12
          PRINT     *88,"|",KEY12:
                    *101,"|",CMAUCUID:
                    *112,"|",CPCDATE," ",CMAUTTIM:
                    *132,"|"
.
          PRINT     *1,"|",*10,"|":
                    *24,"|",CMAUTADM;
.
          UNPACK    MDPRACTO,KEY15
          PRINT     *34,"|",KEY15;
.
          UNPACK    SERDOCTO,KEY15
          PRINT     *50,"|",KEY15;
.
          UNPACK    DEPTYPTO,KEY13
          PRINT     *66,"|",KEY13:
                    *80,"|",VISTYPTO;
.
          UNPACK    REGISTTO,KEY12
          PRINT     *88,"|",KEY12:
                    *101,"|":
                    *112,"|":
                    *132,"|"
.
PDAT9000  CALL      UND132
          ADD       TWO,CLNO
.
PDAT9999  RETURN
+
.******************************************************************************
.         Header
.******************************************************************************
HEAD0000  BRANCH    COPTION,HEAD1000,HEAD2000
          GOTO      HEAD9999
.
HEAD1000  MOVE      "- By Date Range",CPHDROPT
.
          CALL      HEAD132
          UNPACK    FRMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *37,"From Date : ",CPCDATE;
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *63,"to : ",CPCDATE
.
          CALL      UND132
          PRINT     *1,"|Receipt No.":
                    *14,"|Visit No.":
                    *24,"|Medical Prac.":
                    *42,"|Service Doctor":
                    *60,"|Deposit Type":
		    *76,"|Vis.Typ":
                    *84,"|Register":
                    *101,"|User Id":
                    *112,"|Date/Time":
                    *132,"|"
.
          PRINT     *1,"|":
                    *14,"|From/To":
                    *24,"|From/To":
                    *42,"|From/To":
                    *60,"|From/To":
                    *76,"|From/To":
                    *84,"|From/To":
                    *101,"|":
                    *112,"|":
                    *132,"|"
          GOTO      HEAD9000
.
HEAD2000  MOVE      "- By U/R number",CPHDROPT
          CALL      HEAD132
          CALL      UND132
          PRINT     *1,"|U/R No.",*10,"|Receipt No.":
                    *24,"|Visit No.":
                    *34,"|Medical Prac.":
                    *50,"|Service Doctor":
                    *66,"|Deposit Typ":
                    *80,"|Vis.Typ":
                    *88,"|Register":
                    *101,"|User Id":
                    *112,"|Date/Time":
                    *132,"|"
.
          PRINT     *1,"|",*10,"|":
                    *24,"|From/To":
                    *34,"|From/To":
                    *50,"|From/To":
                    *66,"|From/To":
                    *80,"|From/To":
                    *88,"|From/To":
                    *101,"|":
                    *112,"|":
                    *132,"|"
.
HEAD9000  CALL      UND132
          ADD       FOUR,CLNO
.
HEAD9999  RETURN
+
.******************************************************************************
.         Print end of report   
.******************************************************************************
PEND0000  PRINT     *N,"***  End of Report  ***",*N
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **";
.
PEND9999  RETURN
+
.******************************************************************************
.         Keyin from and to date
.******************************************************************************
KDAT0000  UNPACK    SP70,FRMDATE,ENDDATE
          KEYIN     *P1:4,*EF:
                    *P1:6,"  From Date : ":
                    *P1:8,"  To   Date : "
.
.         From date
.
KDAT1000  UNPACK    SP70 WITH CCENT,CYEAR,CMON,CDAY
          MOVE      "6",CVERT
          MOVE      "24",CCOL
          CALL      KEYDATE                 * Keyin from date
          BRANCH    OVRCD,KDAT9000
.
          MATCH     SP2,CDAY
          GOTO      KDAT9000 IF EQUAL
.
          DISPLAY   *P24:6,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON:
                    *HOFF,SLASH,*V2LON,CCENT,CYEAR
.
          PACK      FRMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FRMDATE
.
          MATCH     FRMDATE,CURRDATE
          GOTO      KDAT8000 IF LESS
.
.         End date
.
KDAT2000  UNPACK    SP70 WITH CCENT,CYEAR,CMON,CDAY
          MOVE      "8",CVERT
          MOVE      "24",CCOL
          CALL      KEYDATE
.
          DISPLAY   *P24:8,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON:
                    *HOFF,SLASH,*V2LON,CCENT,CYEAR
.
          PACK      ENDDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          MOVE      ZERO,EXIT
.
          MATCH     ENDDATE,CURRDATE
          GOTO      KDAT8200 IF LESS
.
          MATCH     FRMDATE,ENDDATE
          GOTO      KDAT9999 IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                    "To Date **",*W2;
          GOTO      KDAT1000
.
KDAT8000  DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                    "current Date **",*W2;
          GOTO      KDAT1000
.
KDAT8200  DISPLAY   *P1:24,*B,*EL,*V2LON,"** To Date must be <= ":
                    "to current Date **",*W2;
          GOTO      KDAT2000
.
KDAT9000  MOVE      ONE,EXIT
KDAT9999  RETURN
+
.******************************************************************************
.*                                  KYUR0000              Called by: ML0000   *
.*                              Keyin The U/R No                              *
.******************************************************************************
KYUR0000  DISPLAY   *P1:3,*EF,*P1:4,"U/R Number : "
          MOVE      "4",CVERT
          MOVE      "14",CCOL
          CALL      KURN                    * Keyin the U/R number
          BRANCH    EXIT,KYUR9500
          BRANCH    OVRCD,KYUR1000
          GOTO      KYUR9000
.
KYUR1000  DISPLAY   *P1:24,*EL,*B,"U/R Number does not exist.  ";
          CALL      HITENTER
          GOTO      KYUR0000
.
KYUR9000  MOVE      ZERO,EXIT
          GOTO      KYUR9999
.
KYUR9500  MOVE      ONE,EXIT
KYUR9999  RETURN
+
.*******************************************************************************
.*                                  GUSR0000               Called by: MAIN0000 *
.*                               Get Web User Id                               *
.*******************************************************************************
GUSR0000  KEYIN     *P1:16,*EF,"Web User Id  : ",*V2LON,USERID
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      GUSR0000 IF EQUAL
GUSR9999  RETURN
+
.******************************************************************************
.*                       Routines                                             *
.******************************************************************************
GETSVAR   RETURN
+
.******************************************************************************
.         IO includes
.******************************************************************************
          INC       STD001IO
          INC       PATCOMN1
          INC       PATSNDX                 * Change surname into sound ex
          INC       PATSNX2
          INC       CLPATMAS  
          INC       CALCAGE
          INC       PATDOCCD
          INC       NAMSTRCD
.
          INC       OUTPREIO/INC
          INC       BOKRC1IO/INC            * Booking file
          INC       PMSVX1IO/INC           * visit extention file
          INC       PMSPX2IO/INC
          INC       PATGSRIO/INC            * Surname file
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC            * Pre admission file
          INC       NZCOMPIO/INC
.
          INC       PATCODIO/INC
          INC       COMAUDIO/INC
          INC       RCPREGIO/INC
          INC       PRIPRAIO/INC
          INC       PMSHCPIO/INC
          INC       PRIALSIO/INC                 * Medical Prac.all user access
          INC       PRISECIO/INC                 * Medical Prac.security access
.

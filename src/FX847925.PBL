.******************************************************************************
.*   Program      : FX847925                                                  *
.*   Description  : Move OPDAURNO to OPHIURNO for incorrect records.          *
.*                                                                            *
.*   Author       : Ania P                    TSK 847925                      *
.*   Date         : 22/01/2018                                                *
.*   Modifications:                                                           *
.*                                                                            *
.*       V10.12.02  29/01/2018  Ania P                                        *
.*                  Added report option 3                                     *
.******************************************************************************
.
          INC       STD001FD
          INC       OPRHISFD/INC
          INC       OPRDEAFD/INC
.
.******************************************************************************
.                   VARIABLES/CONSTANTS                                       *
.******************************************************************************
.
.         Variables
.
RECNUM    FORM      8
DIM34     DIM       34
NEXTUCNT  DIM       4
SAVEURNO  DIM       8
SAVEEVNT  DIM       8
SAVEUNIQ  DIM       10
SAVEEDAT  DIM       8
SAVEUCNT  DIM       4
.
.         Constants
.
.
.
.         File Definitions
.
DEBUGTXT  FILE      ASCII,FIXED=256
.
.
.******************************************************************************
.                   PROGRAM CONSTANTS                                         *
.******************************************************************************
.
PRGNAM    INIT      "Replace OPHIURNO with OPDAURNO for Incorrect Records"
PRGID     INIT      "FX847925"
VERSION   INIT      "V12.01.00"
.
.******************************************************************************
.*                  MAIN0000                                                  *
.*                  Program Mainline                                          *
.******************************************************************************
.
MAIN0000  CALL      INIT0000                      
          BRANCH    EXIT,MAIN9999                
.
          CALL      OPTN0000                                        * get option
          BRANCH    COPTION,MAIN1000,MAIN1000,MAIN1000              * run fixit
          GOTO      MAIN9999                      
.
MAIN1000  CALL      CONTQST                                         * continue ?
          BRANCH    CEXIT,MAIN7000:                                 * yes
                          MAIN0000:                                 * no
                          MAIN9999                                  * cancel
. 
MAIN7000  IF        COPTION=THREE
            CALL      OREP0000                                      * run report
          ELSE
            CALL      OFIX0000                                      * run fix
          ENDIF
.
MAIN9999  CLOSE     DEBUGTXT
          CHAIN     PGM
+
.*******************************************************************************
.*                  INIT0000                                                   *
.*                  Display heading and open DB files                          *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          OPEN      OPRHISA1,"oprhisaf"
          OPEN      OPRDETA3,"oprdetaf"
          PREP      DEBUGTXT,"FX847925.txt"
.
INIT9999  RETURN
+
.*******************************************************************************
.*                  OPTN0000 - Get user options or exit                        *
.*                                                                             *
.*                  Returns: EXIT = FALSE (0)  Ok to proceed                   *
.*                                  TRUE  (1)  Exit option selected            *
.*                  COPTION - option selected                                  *
.*******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:4,*V2LON,ONE,*HOFF,". Fix Theatre History UR Number.":
                    *P1:5,*V2LON,TWO,*HOFF,". Fix Theatre History - Debug Mode":
                    *P1:6,*V2LON,THREE,*HOFF,". Run Report Only"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                           * exit selected ?
          GOTO      OPTN9500 IF EQUAL                      * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000,OPTN9000     * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*******************************************************************************
.*                  OFIX0000                                                   *
.*                  Replace ophiurno with opdaurno for affected rows           *     
.*******************************************************************************
.
OFIX0000  DISPLAY   *P1:20,*EL,"Record No. :";
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" ************************ Starting Fix *************************** "
          ENDIF
.
          PACK      KEY38,SP70
          CALL      RSOPHIS1
OFIX1000  CALL      RKOPHIS1
          BRANCH    OVRCD,OFIX9000
.
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" A. Read New His KEY38:",KEY38
            WRITE     DEBUGTXT,SEQ;"    Record: ",OPHIURNO,OPHIEVNT,OPHIUNIQ,OPHIEDAT,DOPHIUCN 
          ENDIF
.
          PACK      KEY10,OPHIUNIQ
          CALL      RDOPDEA3
          COMPARE   ONE,OVRCD
          IF        @EQUAL
            IF        COPTION=TWO
              WRITE     DEBUGTXT,SEQ;" ************************ No oprdet/Ignored ************************ "
            ENDIF
            GOTO      OFIX1000
          ENDIF
.
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" B. Read New Det KEY10:",KEY10
            WRITE     DEBUGTXT,SEQ;"    OPDAURNO:[",OPDAURNO,"] OPDAUNIQ:[",OPDAUNIQ,"]"
          ENDIF
.
          MATCH     OPHIURNO,OPDAURNO
          IF        @EQUAL
            IF        COPTION=TWO
              WRITE     DEBUGTXT,SEQ;" ************************ Match/Ignored ************************ "
            ENDIF
            GOTO      OFIX1000
          ENDIF
.
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" C. URs not equal","[",OPHIURNO,"]",DIM1,"[",OPDAURNO,"]" 
          ENDIF
.
.         Records don't match. Need to update OPRHISAF
.
          ADD       ONE,RECNUM
.
          IF        (RECNUM%1000) = 0 | (RECNUM%1000) = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.         Use UR from oprdetaf to check whether an update on oprhisaf will cause
.         an I*U duplicate record error. If error, find row with highest count &
.         add one to counter so that a new record can be written. Otherwise just
.         do a normal update on the record.
.
          PACK      SAVEURNO,OPHIURNO
          PACK      SAVEEVNT,OPHIEVNT
          PACK      SAVEUNIQ,OPHIUNIQ
          PACK      SAVEEDAT,OPHIEDAT
          PACK      SAVEUCNT,OPHIUCNT
.
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" D. Saved:   ",SAVEURNO,SAVEEVNT,SAVEUNIQ,SAVEEDAT,SAVEUCNT
          ENDIF
          PACK      KEY38,OPDAURNO,SAVEEVNT,SAVEUNIQ,SAVEEDAT,SAVEUCNT
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" E. KEY38:   ",KEY38
          ENDIF
          CALL      RDOPHIS1
.
. 1.      If OVRCD=1 then do a write, otherwise do an update
.
          IF        OVRCD=1
            IF        COPTION=TWO
              WRITE     DEBUGTXT,SEQ;" 1. New key not found in oprhisaf - Attempting Update"
            ENDIF
            PACK      KEY38,SAVEURNO,SAVEEVNT,SAVEUNIQ,SAVEEDAT,SAVEUCNT
            IF        COPTION=TWO
              WRITE     DEBUGTXT,SEQ;" 2. Reading old oprhisaf record KEY38:",KEY38
            ENDIF
            CALL      RDOPHIS1
            MOVE      OPDAURNO,OPHIURNO
            IF        COPTION=TWO
              WRITE     DEBUGTXT,SEQ;" 3. UPOPHIS1:",OPHIURNO,OPHIEVNT,OPHIUNIQ,OPHIEDAT,OPHIUCNT
            ENDIF
            CALL      UPOPHIS1
            PACK      KEY38,SAVEURNO,SAVEEVNT,SAVEUNIQ,SAVEEDAT,SAVEUCNT
            IF        COPTION=TWO
              WRITE     DEBUGTXT,SEQ;" 4. RSOPHIS1 KEY38:",KEY38
            ENDIF
            CALL      RSOPHIS1
            IF        COPTION=TWO
              WRITE     DEBUGTXT,SEQ;" ************************ Completed Update ************************ "
            ENDIF
            GOTO      OFIX1000
          ENDIF
. OR
. 2.      Correction will cause duplicate key so find next OPHIUCNT to make
.         this record's key/index unique
.
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;"    New Key already exists in oprhisaf - calculating new OPHIUCNT"
          ENDIF
          PACK      KEY38,OPDAURNO,SAVEEVNT,SAVEUNIQ,SAVEEDAT,Z70
          PACK      KEY34,KEY38
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" 5. KEY34:   ",KEY34," KEY38:",KEY38
          ENDIF
          CALL      RSOPHIS1
          CALL      RPOPHIS1
          PACK      DIM34,OPHIURNO,OPHIEVNT,OPHIUNIQ,OPHIEDAT 
          MATCH     KEY34,DIM34                                        * Same record variables?
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" 6. DIM34:   ",DIM34
          ENDIF
          IF        @EQUAL
            ADD       ONE,OPHIUCNT
          ELSE
            MOVE      ONE,OPHIUCNT
          ENDIF
          MOVE      OPHIUCNT,NEXTUCNT
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" 7. NEXTUCNT:",NEXTUCNT
          ENDIF
.
.         Write the new oprhisaf record using the correct UR & Count
.          
          PACK      KEY38,SAVEURNO,SAVEEVNT,SAVEUNIQ,SAVEEDAT,SAVEUCNT
          CALL      RDOPHIS1
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" 8. KEY38:   ",KEY38
          ENDIF
          MOVE      OPDAURNO,OPHIURNO
          MOVE      NEXTUCNT,OPHIUCNT
          CALL      WROPHIS1
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" 9. WROPHIS1:",OPHIURNO,OPHIEVNT,OPHIUNIQ,OPHIEDAT,OPHIUCNT
          ENDIF
.
.         Clean up incorrect oprhisaf record
.
          PACK      KEY38,SAVEURNO,SAVEEVNT,SAVEUNIQ,SAVEEDAT,SAVEUCNT
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;"10. Deleting:",KEY38
          ENDIF
          CALL      RDOPHIS1
          CALL      DEOPHIS1
          PACK      KEY38,SAVEURNO,SAVEEVNT,SAVEUNIQ,SAVEEDAT,SAVEUCNT
          CALL      RSOPHIS1
          IF        COPTION=TWO
            WRITE     DEBUGTXT,SEQ;" ************************ Completed Write ************************ "
          ENDIF
          GOTO      OFIX1000
.         
OFIX9000  DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,RECNUM,": Update completed.  ";
          CALL      HITENTER
.
OFIX9999  RETURN
+
.*******************************************************************************
.*                  OREP0000                                                   *
.*                  Run report to output broken records                        *
.*******************************************************************************
.
OREP0000  DISPLAY   *P1:20,*EL,"Record No. :";
          WRITE     DEBUGTXT,SEQ;"The rows below contain index1 keys to broken oprhisaf records."
          WRITE     DEBUGTXT,SEQ;""
.
          PACK      KEY38,SP70
          CALL      RSOPHIS1
OREP1000  CALL      RKOPHIS1
          BRANCH    OVRCD,OREP9000
.
          PACK      KEY10,OPHIUNIQ
          CALL      RDOPDEA3
          COMPARE   ONE,OVRCD
          IF        @EQUAL
            WRITE     DEBUGTXT,SEQ;"[",OPHIURNO,OPHIEVNT,OPHIUNIQ,OPHIEDAT,DOPHIUCN,"]":
                                   " - No matching OPRDETAF.OPDAUNIQ record. (Type 1)"
            GOTO      OREP1000
          ENDIF
.
          MATCH     OPHIURNO,OPDAURNO
          IF        !@EQUAL
            WRITE     DEBUGTXT,SEQ;"[",OPHIURNO,OPHIEVNT,OPHIUNIQ,OPHIEDAT,DOPHIUCN,"]":
                                   " - OPRDETAF.OPDAURNO ",OPDAURNO," doesn't match. (Type 2)"
            ADD       ONE,RECNUM
          ENDIF
.
          GOTO      OREP1000
.
OREP9000  WRITE     DEBUGTXT,SEQ;""
          WRITE     DEBUGTXT,SEQ;"Found ",RECNUM," OPRHISAF records with bad ":
                                 "OPHIURNO values. (Type 2)"
OREP9999  DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,RECNUM,": Report completed. ";
          CALL      HITENTER
          RETURN
.*******************************************************************************
.*        I/O Routines                                                         *
.*******************************************************************************
.
          INC       STD001IO
          INC       OPRHISIO/INC
          INC       OPRDEAIO/INC
.

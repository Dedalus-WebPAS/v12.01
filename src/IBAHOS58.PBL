. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAHOS58                                            *
. *                                                                           *
. *     FUNCTION:         MORTALITY INDEX REPORT                              *
. *                                                                           *
. *     DATE WRITTEN:     22/06/88                                            *
. *                                                                           *
. *     AUTHOR:           KARIN PEAK    (I.B.A)                               *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
.******************************************************************************
.* V12.00.01 16/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V10.04.02 17/06/2014 Steve Armstrong  CAR 301639                    *
.*                  Moved call to TFILENAM for FNAMET to start of program.    *
.*        V10.04.01 14/04/2014 Ebon Clements CAR 298721                       *
.*                  Added multi hospital functionality                        *
.******************************************************************************
.*        V10.03.01 27/12/2012 Patrick Adair CAR 261630                       *
.*                  Removed port number from temp file name                   *
.******************************************************************************
.*        V10.02.01 23/03/2011  Mike Laming   CAR 240107                      *
.*                  Remove PTCNUCEP PATMRDFD/IO and PATMROFD/IO               *
.*                  Mods for Key changes in PATECDFD & PATECOFD - file change *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*        V9.12.01  24/09/2009  Sandra Barcham   206742                       *
.*                  Change PTCNDRSM for DD from 2 to allow for 0 as well      *
.******************************************************************************
.*        V9.09.01  06/03/2008  Sandra Barcham   152103                       *
.*                  Fix printing of operations                                *
.******************************************************************************
.*        V9.04.01  30/08/2005  Mike Laming   CAR 63051                       *
.*                  Change report field "Pat Cat." to "Discharge Ward"        *
.******************************************************************************
.*        V9.03.01  04/12/2003  Sylvek Litewka SRF 45729                      *
.*                  Recompiled for changes made to PATECDFD and PATECOFD.     *
.******************************************************************************
.*        V9.02.03  13/09/2002  Jill Habasque  SRF 22005                      *
.*                  Fixed separation mode                                     *
.*        V9.02.02  30/08/2002  Jill Habasque  SRF 17573                      *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V9.02.01  13.11.2001 Dean Cramer                                    *
.*                  Make so gets correct seperation mode                      *
.******************************************************************************
.*        V5.08.03  10.10.2000 Sandra Barcham                                 *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 15/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.******************************************************************************
.*        V5.07.B02 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 13/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
.*                        V4.01 19/01/89 J.Tan   (I.B.A.)  *
. *                       Initialize totals and fixed Autop*
. *                       SRF 4567                         *
. *                       V5.01 20/03/89 J.Tan             *
. *                             Fixed PATIOMDI             *
. *                       V5.02 04/04/89 K.Peak            *
. *                             Autopsy Indicator SRF 4959 *
. *                       V5.03 18/05/89 S.O'Gorman        *
. *                             Allow for 8 Digit U.R's &  *
. *                             Admission Number           *
. *                             Include STD001             *
. *                             Use KEYDDATE Routine       *
. *                       V5.04 27/05/89 J.Tan             *
. *                             Fixed Mother Adm. to form8 *
. *                             (PATMI1FD).     SRF 100750 *
. *                       V5.05 30/05/89 J.Tan             *
. *                             Fixed the temporary index  *
. *                             SRF 100794                 *
. *                       V5.06 06/06/89 J.Tan             *
. *                             Mods Locking master file   *
. *                             (PATIOMAS)     SRF 100809  *
. *                    V5.01.01 05/07/89 K.Peak            *
. *                             Changed Adm & U/R to 8 chrs*
. *                   V5.01.121 27/11/90 Glen Hobby        *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *                   V5.01.122 13.01.94 S.Barcham                            *
. *                            Remove FIFTY INIT                              *
. *                   V5.01.123 29/06/94  Rob Leonard  SRF 440937             *
. *                             Add ability to use UK medical records disease *
. *                             and operation files (ICD9 and OPCS4 coding).  *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATECOFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.         WORK AREA
.
.--------------------------------------------------------------
.Name     Type   Length   Physical   Start
.----     ----   ------   --------   -----
.PSNAME   DIM       20        20        1
DUNIQUE   DIM       8         8        21
XDADMNO   DIM       8         8        29
ACDATE    DIM       8         8        37
.DURNO    DIM       8         8        45
.PGNAME   DIM      25        25        53
.ACLSS    DIM       3         3        78
.PSAGE    FORM      3         3        81
.PSEX     DIM       1         1        84
.PTEDTYPE DIM       1         1        85
.PTEDCODE DIM       9         9        86
.PTEOCODE DIM       9         9        95
.ADOCTA   DIM       6         6       104
.PTITL    DIM       4         4       110
TOTDAY    FORM      4         3       114
FIRST     FORM      1         2       117
AUTFLG    DIM       1         1       119
DWARD     DIM       3         3       120                              *I-63051
DBED      DIM       3         3       123                              *I-63051
.
.  END OF RECORD                           126                         *C-63051
.
.  redefine form fields for key
.  ----------------------------
.Name     Type     Length
.----     ----     ------
.DADMNO   DIM       8
UNIQUE    FORM      8
.
.--------------------------------------------------------------
.                          CONSTANTS
.--------------------------------------------------------------
.
CODED     INIT      "D "
CODEDD    INIT      "DD"
DUN       INIT      "UNK"
SP65      INIT      "                              ":
                    "                                   "
ZED10     INIT      "zzzzzzzzzz"
.
PSTROL    FILE      ASCII, FIXED=256
.
.                                                           * was 120  *C-63051
TEMPFILE  IFILE     SQL, FIXED=126, KEY="U37-44,1-20,53-77,29-36,21-28"
.
.--------------------------------------------------------------
.                          VARIABLES
.--------------------------------------------------------------
.
ADMSSN    DIM       8
ADTIM     DIM       5
.
CALDAYS   FORM      4
CMDLINE   DIM       50
HOSPCODE  DIM       3
HOSPDESC  DIM       50
.
DALERG    DIM       3
DANS      DIM       3
DATET     DIM       8
DDSTAT    DIM       19
DHOSP     DIM       3
DILLN     DIM       3
DIM5      DIM       5
DIM16     DIM       16
DIM17     DIM       17
DIMDAY    DIM       2
DIMMONTH  DIM       2
DIMYEAR   DIM       2
DSTIM     DIM       5
.
FDATE     DIM       8
FNAMET    DIM       8
FROMDATE  DIM       8        * FROM DATE IN (CCYYMMDD) FORMAT
.
JYEAR     FORM      2
.
.
LNO       FORM      2
.
NAUTOPY   FORM      8
NBRDAYS   FORM      4
NDSCH     FORM      8
NDEAD     FORM      8
.
PAGENO    FORM      3
PERCDED   FORM      8.2
PERCOPY   FORM      8.2
.
RTDAYS    FORM      4
SEC       FORM      2
SKEY30    DIM       30
STATUS    FORM      1
OPCND     FORM      1
.
TODATE    DIM       8        * TO DATE IN (CCYYMMDD) FORMAT
.
WDATE1    DIM       8
WDATE2    DIM       8
.
XDAY1     DIM       2
XDAY2     DIM       2
XDAY3     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XMON3     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XYEAR3    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
XCENT3    DIM       2
.
PRGID     INIT      "IBAHOS58"
PRGNAM    INIT      "Mortality Index Report"
VERSION   INIT      "V12.01.00"
.
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          DISPLAY   *P56:24,"Opening patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMET
.
          MOVE      PORT TO DIM2
.
.        START OF PROGRAM
.
START     MOVE      ZERO,PAGENO
          MOVE      " ",ANS
.
          HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Date Sequence"
.
          KEYIN     *P1:7,"Option : ",*EL,*V2LON,OPTION
.
          BRANCH    OPTION OF KDATE
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BEEP
          GOTO      START
.
NODSCH    DISPLAY   *P20:24,*EL,*V2LON,"** No Discharges on File **",*W2;
          GOTO      START
.
KDATE     DISPLAY   *P51:2,*V2LON,"- Date Sequence "
.
KDDATE    DISPLAY   *P1:4,*EF,"Keyin From Date : ":
                    *P1:6,"Keyin To   Date : "
.
. ----- Keyin the From Date  -----
.
          MOVE      SP2,CDAY
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      SP2,CCENT
          MOVE      TWENTY,CCOL
          MOVE      FOUR,CVERT
          MOVE      FIFTY,ECOL
          MOVE      FOUR,EVERT
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      START IF EQUAL
.
          PACK      FDATE,CCENT,CYEAR,CMON,CDAY
          UNPACK    FDATE,XCENT1,XYEAR1,XMON1,XDAY1
KADAT2
.
. ----- Keyin the To Date  -----
.
          MOVE      TWENTY,CCOL
          MOVE      SIX,CVERT
          MOVE      FIFTY,ECOL
          MOVE      SIX,EVERT
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYDATE
.
ENDAD2    PACK      DATET,CCENT,CYEAR,CMON,CDAY
          UNPACK    DATET,XCENT2,XYEAR2,XMON2,XDAY2
.
CNTCHK    MATCH     FDATE TO DATET * 'FROM' DATE MUST BE
          GOTO      REKEY IF NOT LESS           * >= 'TO' DATE
.
INVDATG   DISPLAY   *P20:24,*EL,*B,*V2LON:
                    "** From Date Must be Less Than To Date **":
                    *W2,*P20:24,*EL;
          GOTO      KDDATE
.
.        CHECK IF DATES ARE OK OR NOT
.
REKEY     CALL      KHOS0000                     * Keyin hospital ID
          BRANCH    EXIT,KDDATE
.
          KEYIN     *P1:24,*EF," Date ok (":
                    *V2LON,"Y",*HOFF,*DV,SLASH:
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
.
          MATCH     "N" TO ANS
          GOTO      KDDATE IF EQUAL
.
          MATCH     "Y" TO ANS
          GOTO      DEATH IF EQUAL
.
          BEEP
          GOTO      REKEY
.
INVMASTD  DISPLAY   *P20:23,*B,*EL:
                    "** Discharge ",*V2LON,DADMNO,*HOFF:
                    " has an invalid U/R ":
                    *V2LON,DURNO,*HOFF," ***",*W2
.
          PRINT     "** Discharge ",DADMNO," has an invalid U/R ":
                    DURNO," ... Please Contact I.B.A **"
.
          GOTO      NEXTD
.
INVADMN   DISPLAY   "** Discharge ",DADMNO," has an no Admission Information ":
                    " ... Please Contact I.B.A **"
.
          GOTO      NEXTD
.
DEATH     CALL      KILLIDX
          CALL      CREAIDX
          BRANCH    OPCND OF ENDP
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE TO UNIQUE
          MOVE      ZERO,NDSCH
          MOVE      ZERO,NDEAD
          MOVE      ZERO,NAUTOPY
.
          PACK      KEY16,FDATE,SP20
.
          CALL      RDSDSCH2
          MOVE      "60",LNO
.
.                 DISPLAY ADMISSIONS IN RANGE
.
          DISPLAY   *P1:24,*EL,*P20:24,"Admission Number : "
.
NEXTD     CALL      RDKDSCH2
          BRANCH    OVRCD OF SORTD
.
          MOVE      SP1,AUTFLG
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
.
          PACK      ACDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0" IN ACDATE
.
          MATCH     ACDATE,DATET
          GOTO      SORTD IF LESS
.
          PACK      KEY8,DADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,NEXTD
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSPCODE
            IF        !@EQUAL
              MATCH     HOSPCODE,PMVXMHOS   * Check hospital ID
              GOTO      NEXTD IF NOT EQUAL
            ENDIF
          ENDIF

.
          DISPLAY   *P39:24,*EL,*V2LON,DADMNO
.
          MOVE      DURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMASTD
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF INVADMN
.
          ADD       ONE,NDSCH                 * no of discharges
.
.       Check for discharge status
.
          COMPARE   ONE,PTCNDRSM
          GOTO      NEXTD1 IF NOT EQUAL
.
          PACK      KEY5 WITH CODED,DSTAT
          CALL      RDCODE1
          BRANCH    OVRCD OF NEXTD
          GOTO      NEXTD2
.
NEXTD1    PACK      KEY5,ANSD,ANSD,DDEST
          CALL      RDCODE1
          BRANCH    OVRCD OF NEXTD
.
NEXTD2    CMATCH    "D",TCINDC1
          GOTO      NEXTD IF NOT EQUAL
          ADD       ONE,NDEAD                 * no of deaths
.
.       Check with the autopsy indicator
.
          COMPARE   ONE,PAUTOPY
          GOTO      NOAUTOP IF NOT EQUAL
.
          ADD       ONE,NAUTOPY               * Autopsy
          MOVE      "A",AUTFLG
.
NOAUTOP   UNPACK    ADATE,XCENT3,XYEAR3,XMON3,XDAY3
.
          PACK      FROMDATE,XCENT3,XYEAR3,XMON3,XDAY3
          REP       " 0",FROMDATE
.
          MOVE      ACDATE,TODATE
          REP       " 0",TODATE
.
          CALL      RTIODAYS
          MOVE      NBRDAYS,TOTDAY
.
          CALL      DWARD000                * get Discharge Ward       *I-63051
.
          MOVE      ONE,FIRST
.
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECO2
.
NEXTO     CALL      RKPTECO2
          MOVE      PTEOADMN,ADMSSN
          BRANCH    OVRCD OF NOOPR
.
          MATCH     AADMNO,ADMSSN
          GOTO      NOOPR IF NOT EQUAL
.
          GOTO      NEXTDIS
.
NOOPR     MOVE      SP9,PTEOCODE
.
NEXTDIS   CALL      RKPTECD2
          MOVE      PTEDADMN,ADMSSN
          BRANCH    OVRCD OF NODIS
.
          MATCH     AADMNO,ADMSSN
          GOTO      NODIS IF NOT EQUAL
.
          GOTO      WRTMP
.
NODIS     MOVE      SP9,PTEDCODE
          MOVE      SP1,PTEDTYPE
          BRANCH    FIRST OF WRTMP
.
          MATCH     SP9,PTEOCODE
          GOTO      NEXTD IF EQUAL
.
WRTMP     MOVE      UNIQUE,DUNIQUE
          MOVE      DADMNO,XDADMNO
.
.
          PACK      KEY67 WITH ACDATE,PSNAME,PGNAME,XDADMNO,DUNIQUE
          WRITE     TEMPFILE,KEY67;PSNAME,DUNIQUE,XDADMNO,ACDATE:
                    DURNO,PGNAME,ACLSS,PSAGE,PSEX:
                    PTEDTYPE,PTEDCODE,PTEOCODE,ADOCTA,PTITL,TOTDAY:
                    FIRST,AUTFLG,DWARD,DBED                            *C-63051
          ADD       ONE TO UNIQUE
          MOVE      ZERO,FIRST
.
          GOTO      NEXTO
.
NOOPEN    DISPLAY   *P1:24,*B,*EL,"** TEMPORARY STATISTICS  FILE ":
                    FNAMET," NOT FOUND **",*W2;
          RETURN
.
SORTD     DISPLAY   *P1:24,*EL,*P20:24,"** PROCESSING STATISTICS **",*W;
.
          CLOSE     TEMPFILE
.
SORTFI    TRAP      NOOPEN IF IO
          OPEN      TEMPFILE,FNAMET
          TRAPCLR   IO
.
          PACK      KEY67,SP30,SP30,SP10
          READ      TEMPFILE,KEY67;;
          DISPLAY   *P1:24,*EL,*P20:24,"Patient Name :"
.
READLOP   READKS    TEMPFILE;PSNAME,DUNIQUE,XDADMNO,ACDATE:
                    DURNO,PGNAME,ACLSS,PSAGE,PSEX:
                    PTEDTYPE,PTEDCODE,PTEOCODE,ADOCTA,PTITL,TOTDAY,FIRST:
                    AUTFLG,DWARD,DBED                                  *C-63051
          GOTO      ENDP IF OVER
.
          MOVE      DUNIQUE,UNIQUE
          MOVE      XDADMNO,DADMNO
          UNPACK    ACDATE,XCENT,XYEAR,XMON,XDAY
.
          DISPLAY   *P39:24,*EL,*V2LON,PSNAME
          CALL      DISPDA
          GOTO      READLOP
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,PAGENO
          GOTO      NODSCH IF EQUAL
.
          CALL      PAGEND
.
          MOVE      NDEAD,PERCDED
          MULT      "100",PERCDED
          DIV       NDSCH,PERCDED
.
          MOVE      NAUTOPY,PERCOPY
          MULT      "100",PERCOPY
          DIV       NDEAD,PERCOPY
.
          PRINT     *49,"No of Dis. Patients : ",NDSCH:
                    *90,"No of Deaths  : ",NDEAD,*N:
                    *49,"% Death             : ",PERCDED:
                    *90,"No of Autopsy : ",NAUTOPY,*N:
                    *49,"% of Autopsy/Death  : ",PERCOPY
.
          PRINT     *N,"  ***  END OF REPORT  ***"
.
          DISPLAY   *P20:24,*EL,*V2LON:
                    "** Report Generation Completed **",*W2;
.
          CALL      KILLIDX
          GOTO      START
.
.     PRINT ACTUAL DATA
.
DISPDA    COMPARE   "55",LNO
          CALL      HEAD IF NOT LESS
.
          MOVE      PGNAME,DIM16
          MOVE      DTIME,DIM5
          MOVE      DIM5,DSTIM
          MOVE      ATIME,DIM5
          MOVE      DIM5,ADTIM
.
          BRANCH    FIRST OF FPRINT
.
          PRINT     *1,"!",*45,"! ":
                    *54,"!  ",*60,"! ",*66,"! ",PTEDTYPE,PTEDCODE:
                    *79,"! ",PTEOCODE,*91,"! ",*100,"! ":
                    *106,"! ":
                    *118,"! ",*127,"! ",*132,"!"
.
          ADD       ONE,LNO
          GOTO      DISPEND
.
FPRINT    PRINT     *1,"!",PSNAME,SP1,PTITL,SP1,DIM16,*45,"!",DURNO:
                    *54,"!  ",PSEX,*60,"! ",PSAGE,*66,"! ",PTEDTYPE,PTEDCODE:
                    *79,"! ",PTEOCODE,*91,"! ",ADOCTA,*100,"! ",TOTDAY:
                    *106,"!",XDAY,"/",XMON,"/",XCENT,XYEAR,AUTFLG:
                    *118,"!",DADMNO,*127,"! ",DWARD,*132,"!"           *C-63051
.
          ADD       ONE,LNO
.
DISPEND   RETURN
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,LNO
          RETURN
.
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,PAGENO
.
          COMPARE   ONE,PAGENO
          CALL      PAGEND IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          MOVE      "(DATE - SEQUENCE)",CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *40,"From Date : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    *65,"To Date : ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2
.
          IF        IBCNMHOS=1
            PRINT    *40,"Hospital : ",HOSPDESC
          ENDIF
.
          CALL      PAGEND
.
          PRINT     *1,"!",*45,"!",*54,"!",*60,"!",*66,"! Disease":
                    *79,"! Operation":
                    *91,"! Dr.",*100,"! No. ",*106,"! Death",*118,"!":
                    *127,"!Dsch",*132,"!",*N:                          *C-63051
                    *1,"!Patients Name",*45,"! U/R No ":
                    *54,"! Sex ",*60,"! Age ",*66,"! Codes ":
                    *79,"! Codes ",*91,"! Code ",*100,"! Days":
                    *106,"! Date ",*118,"! Adm No ":
                    *127,"!Ward",*132,"!"                              *C-63051
.
          CALL      PAGEND
.
          MOVE      "8",LNO
          RETURN
.
.       Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          DISPLAY   *P1:11,*EF,*P1:11
          RETURN
.
CREAIDX   CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
.                                                           * was 120  *C-63051
          APPEND    " 126 u37-44,1-20,53-77,29-36,21-28",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ZERO,OPCND
          TRAP      NOINDZ IF IO
          OPEN      TEMPFILE,FNAMET
          TRAPCLR   IO
          RETURN
.
NOINDZ    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMET," Not Found **",*W4;
          TRAPCLR   IO
          RETURN
.
.******************************************** start of addition        *I-63051
.******************************************************************************
.                                   DWARD000
.                     Get the Discharge Ward / Bed
.******************************************************************************
DWARD000  UNPACK    SP10,DWARD,DBED
          PACK      KEY30,DAADMNO,ZED10,SP30
          CALL      RDSTRAN2
.
DWARD100  CALL      RDPTRAN2
          BRANCH    OVRCD,DWARD999
.
          MATCH     DAADMNO,DTADMN
          GOTO      DWARD999 IF NOT EQUAL
.
          MATCH     "D",TMOVE
          GOTO      DWARD999 IF NOT EQUAL
.
          MOVE      TWARD,DWARD
          MOVE      TBED,DBED
.
DWARD999  RETURN
.********************************************   end of addition        *I-63051
.
+
.*******************************************************************************
.       Keyin hospital ID
.*******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
          MOVE       SP70,HOSPCODE
          MOVE       SP70,HOSPDESC
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          DISPLAY    *P1:8,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       EIGHT,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS1000,KHOS9000
          MOVE       PTHSHOSP,HOSPCODE
          MOVE       PTHSNAME,HOSPDESC
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS1000  MOVE       "All Hospitals",PTHSNAME
          MOVE       PTHSNAME,HOSPDESC
          MOVE       SP70,HOSPCODE
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
.
+
          INC       STD001IO
          INC       TFILENAM                     * Generate Temp File Name
          INC       PATHSPKY
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       RTIODAYS
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
ENDIT     CALL      KILLIDX
          CHAIN     PGM
          STOP

.*****************************************************************************
.*    Program      : IBAMEH56                                                *
.*    Description  : Leave Report                                            *
.*                                                                           *
.*    Author       : ROD    (13/12/91)                                       *
.*    Function     : MH patients who are currently on holiday or trial leave *
.*    Modifications:                                                         *
.*       V12.00.01  22/05/2025  Bella Turco                 TSK 0955096      *
.*                  Alphanumeric changes                                     *
.*       V10.05.01  01/01/2015  Ania P                      CAR 261630       *
.*                  Removed use of PORT for temp file naming                 *
.*       V10.01.01  10/02/2011  Jill Parkinson CAR 233088                    *
.*                  Added pmsvx1af                                           *
.*        V9.02.00  26/10/2002  Lina Vo     SRF 22733                        *
.*                  Ported to V9                                             *
.*****************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                *
.*                  Changed Health Fund variables to be 8 chars              *
.*       V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                  Fixed global recompile bugs                              *
.*****************************************************************************
.*        V5.07.01  12/09/1999  Steve Downing                                *
.*                  Redundant code removed from end date calculations        *
.*       V5.07.B02  14/01/1999  Jim Stathopoulos                             *
.*                  Date fixed for Report                                    *
.*        V5.07.00  27/10/1998  Davin                                        *
.*                  Mods for 507                                             *
.*****************************************************************************
.*        V5.04.02  10/01/1997  Steve Armstrong   MEH changes                *
.*                  Mods to cater for CMH MHVISTAT statuses                  *
.*        V5.04.01  07/11/1996  Howellsy                                     *
.*                  Added Review Time & Legal Status Time.                   *
.*****************************************************************************
.*                   V5.01.002  13/02/92  ROD                                *
.*                              After Gen report return to menu options.     *
.*                   V5.01.003  24/07/92  ROD                                *
.*                              Use latest legal status not PTMXLS           *
.*****************************************************************************
.
          INC       STD001FD
          INC       MEHVI1FD/INC
          INC       MEHLEGFD/INC
          INC       MEHCONFD/INC
          INC       PATTRNFD/INC
          INC       PATCONFD/INC
          INC       PATONLFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
BJDAY     FORM      3
CJDAY     FORM      3
CMDLINE   DIM       80
CNTYR     DIM       4
CURRDATE  DIM       8
DAYS      INIT      " Days"
DSLVED    DIM       8
DSREVD    DIM       8
ENDDATE   DIM       8
FCNTYR    FORM      4
MONTHS    INIT      " Months"
NDAYS     FORM      2
NOPATS    FORM      4
REPNO     FORM      1
REPSECT   DIM       38
REPS1     INIT      "Leave Review Due in "
REPS2     INIT      "On Trial Leave Longer than "
REPS3     INIT      "On Holiday Leave Longer than "
REPS1L    DIM       38
REPS2L    DIM       38
REPS3L    DIM       38
SAVOMON   FORM      2
STRTCENT  FORM      2
STRTDAYS  FORM      3
STRTLEAP  FORM      1
STRTYR    FORM      2
TMPLVD    DIM       8
YEARS     INIT      " Years"
Z30       INIT      "ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ"
.
. Temp file vars
.
MEHTMPA1  IFILE     SQL, FIXED=70, KEY="U1-8,9-34,35-42"
XXREVD    DIM       8        1     Review date
XXNAME    DIM       26       9     Name
XXURNO    DIM       8       35     U/R No.
XXLSC     DIM       3       43     Legal status code
XXLVED    DIM       8       46     Leave date
XXADOCT   DIM       16      54     Attending doctor
.             rec length    70
.
PRGID     INIT      "IBAMEH56"
PRGNAM    INIT      "Leave Report"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code   (1 of the great all time mainlines)           *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
          CALL      CRET0000                      * create temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      NDAY0000                      * Get Days til next review
.
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000    * Yes, No, Cancel
.
ML5000    CALL      RPRT0000                      * Print Report
          CALL      CRET0000                      * erase temp file
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"mehlegaf"
          OPEN      MEHLEGA1,"mehlegaf"
.
          DISPLAY   *P60:24,"mehvi1af"
          OPEN      MEHVI1A2,"mehvi1af"
.
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P60:24,"patonlvf"
          OPEN      PATONLV1,"patonlvf"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          CALL      TFILENAM
.
          READ      CONTROLF,SIXTY9;*75,MHCNMAXH,MHCNMAXT
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      REPS2L,REPS2,MHCNMAXT,YEARS
          PACK      REPS3L,REPS3,MHCNMAXH,MONTHS
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Review date"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* NDAY0000  :  Get Number of days until next review                        *
.****************************************************************************
NDAY0000  DISPLAY   *P1:9,"Number of days until the next review : "
.
NDAY1000  KEYIN     *P1:24,*EL,*P40:9,*DV,UNDLN2:
                    *P40:9,*V2LON,NDAYS:
                    *P40:9,*DV,NDAYS
.
          IF        NDAYS < 0
                      BEEP
                      GOTO    NDAY1000
          ENDIF
.
          CALL      CDAT0000                      * convert to a date
.
          PACK      REPS1L,REPS1,NDAYS,DAYS
.
NDAY9999  RETURN
+
.****************************************************************************
.* CDAT0000  :  Calculate an end date given a start date and duration       *
.****************************************************************************
CDAT0000  CALL      IBACLOCK                * convert start date to julian days
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CDAY,DD                 * set up start day
          MOVE      CMON,MM                 *              month
          MOVE      CYEAR,YY                *              year
          MOVE      CCENT,CC                *              century
.
          CALL      DMYCON                  * get julian day
          MOVE      JULDAY,STRTDAYS         * get current julian day
          MOVE      JULYR,STRTYR            * get current julian year
          MOVE      JULCC,STRTCENT          * get current julian century
          MOVE      LEAPYR,STRTLEAP         * get leap year flag (1=leap)
.
.         add the number of days
.
          ADD       NDAYS,STRTDAYS          * get the total days for the year
.
.         convert back to calender date
.
          MOVE      STRTDAYS,JWKDAY         * work days
          MOVE      JULYR,JWKYER            * work year
          MOVE      JULCC,JWKCC             * work century
          CALL      JULCON
.
.         set up end date
.
CDAT5000  PACK      ENDDATE,CC,YY,MM,DD
          REP       " 0",ENDDATE
.
CDAT9999  RETURN
+
.****************************************************************************
.*  LOAD0000  :  Load data into temp file                                   *
.****************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading ... [        ]";
.
          PACK      KEY9,SP10
          CALL      RSMHVIS2
LOAD0500  CALL      RKMHVIS2
          BRANCH    OVRCD,LOAD9999
.
          COMPARE   MHVISTAT,SIX                 * ignore CMH records
          GOTO      LOAD0500 IF LESS
.
. check if on holiday or trial leave depending on report number
.
          IF   REPNO = 1
            IF        ! ((MHVISTAT=3) | (MHVISTAT=4))
              GOTO      LOAD0500                    * ignore
            ENDIF
          ENDIF
.
          IF   REPNO = 2
            IF       ! (MHVISTAT=3)
              GOTO      LOAD0500                    * ignore
            ENDIF
          ENDIF
.       
          IF   REPNO = 3
            IF       ! (MHVISTAT=4)
              GOTO      LOAD0500                    * ignore
            ENDIF
          ENDIF
.
          CALL      IDAT0000                      * init temp vars
.
. get Admission and Master details
.
          PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD0500
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD0500
.
          PACK      KEY14,AWARD,ABED,AADMNO       * get next leave review date
          CALL      RDONLV1
          BRANCH    OVRCD,LOAD0500
          MOVE      OERDATE,XXREVD                * review date
.
          IF   REPNO = 1
            MATCH     ENDDATE,OERDATE             * within given no. of days?
            GOTO      LOAD1000 IF LESS 
            GOTO      LOAD1000 IF EQUAL
            GOTO      LOAD0500
          ENDIF
.
. we have a valid record so set up data for temp file
.
LOAD1000  MOVE      PURNO,XXURNO
.
          STRIP     PSNAME                        * patients name
          PACK      XXNAME,PSNAME,COMMA,SP1,PGNAME
.
. now get latest legal status
.
          MOVE      SP3,XXLSC
          PACK      KEY21,MHVIADMN,Z30
          CALL      RSMHLEG1
          CALL      RPMHLEG1                      * read last rec
          BRANCH    OVRCD,LOAD1200
.
          MATCH     MHLEADMN,MHVIADMN             * same admission no?
          GOTO      LOAD1200 IF NOT EQUAL
.
          MOVE      MHLESTAT,XXLSC                * legal status
.
LOAD1200  MOVE      OERDATE,XXLVED                * expected return date
.
.      get attending doctor
.
          MOVE      SP20,XXADOCT
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1
          IF        OVRCD=0
                      PACK    XXADOCT,DSNAME,SP20
                      STRIP   XXADOCT
                      ENDSET  XXADOCT
                      APPEND  SP1,XXADOCT
                      MOVE    DGNAME,ANS
                      APPEND  ANS,XXADOCT
                      RESET   XXADOCT
          ENDIF
.
          CALL      LVDT0000                      * get leave date
.
. if on rep no. 2 then only get pats on trial leave for more than MHCNMAXT years
.
          IF        REPNO = 2
            MOVE      XXLVED,TMPLVD               * temp leave date
            UNPACK    TMPLVD,CCENT,CYEAR,CMON,CDAY
            PACK      CNTYR,CCENT,CYEAR           * join century & year
            MOVE      CNTYR,FCNTYR                * move to form field
            ADD       MHCNMAXT,FCNTYR             * add max yrs to leave cnt/yr
            UNPACK    FCNTYR,CCENT,CYEAR
            PACK      TMPLVD,CCENT,CYEAR,CMON,CDAY
            REP       " 0",TMPLVD
.
            MATCH     TMPLVD,CURRDATE             * >= current date
            GOTO      LOAD0500 IF LESS
          ENDIF
.
. if on rep no. 3 then only get pats on hol leave for more than MHCNMAXH months
.
          IF        REPNO = 3
            MOVE      XXLVED,TMPLVD               * temp leave date
            UNPACK    TMPLVD,CCENT,CYEAR,CMON,CDAY
            ASSIGN    (MHCNMAXH/TEN2),FORM1
.        get new year
            PACK      CNTYR,CCENT,CYEAR
            MOVE      CNTYR,FCNTYR
            ADD       FORM1,FCNTYR
.        get new month
            MOVE      CMON,SAVOMON                * save old month
            ASSIGN    MHCNMAXH/TEN2,FORM1
            ASSIGN    (MHCNMAXH - (FORM1*TEN2) + SAVOMON),IMON
            IF        IMON > 12
                        ADD     ONE,FCNTYR
                        ASSIGN  (IMON-TEN2),IMON
            ENDIF
            MOVE      IMON,CMON
            UNPACK    FCNTYR,CCENT,CYEAR
.        set up end date
            PACK      TMPLVD,CCENT,CYEAR,CMON,CDAY
            REP       " 0",TMPLVD
.
            MATCH     TMPLVD,CURRDATE             * >= current date
            GOTO      LOAD0500 IF LESS
          ENDIF
.
. write to temp file
.
LOAD2000  PACK      KEY42,XXREVD,XXNAME,XXURNO
          CALL      WRTEMP1
          DISPLAY   *P43:24,XXURNO
          GOTO      LOAD0500
.
LOAD9999  RETURN
+
.**************************************************************************
.*  LVDT0000  :  Get Leave date                                           *
.**************************************************************************
LVDT0000  MOVE      SP8,XXLVED
          PACK      KEY30,MHVIADMN,Z30
          CALL      RDSTRAN2
LVDT1500  CALL      RDPTRAN2
          BRANCH    OVRCD,LVDT9999
.
          MATCH     TADMN,MHVIADMN                * same admission no.
          GOTO      LVDT9999 IF NOT EQUAL
.
          MATCH     ANSL,TMOVE                    * Leave record?
          GOTO      LVDT1500 IF NOT EQUAL
.
          PACK      XXLVED,TDATE
          REP       " 0",XXLVED
.
LVDT9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      ONE,REPNO                     * start at report no. 1
.
        WHILE     REPNO < 4
.
          CALL      LOAD0000                      * Load data into temp file
.
          CALL      HEAD80                        * Print 3 line header
          CALL      PTHD0000                      * print table headings
          MOVE      ZERO,NOPATS
          MOVE      TEN,CLNO                      * line counter
.
. Now loop thru temp file and print data
.
          PACK      KEY42,SP30,SP20
          CALL      RSTEMP1
RPRT1000  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT7000                * no more records
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT2000 IF LESS
.
. set up for new page
.
          CALL      HEAD80 
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
RPRT2000  UNPACK    XXREVD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,DSREVD                 * display var (Review Date)
.
          UNPACK    XXLVED,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,DSLVED                 * display var (Leave Date)
.
. print details
.
          PRINT     *1,"|",XXURNO,*11,"| ",XXNAME,"| ",XXLSC," |",DSLVED:
                    "|",DSREVD,"|",XXADOCT,"|"
.
          ADD       ONE,CLNO
          ADD       ONE,NOPATS
          GOTO      RPRT1000
.
. we have finished printing patient data so print end of report details
.
RPRT7000  CALL      UND80
          PRINT     *1,"Number of patients = ",NOPATS
          CALL      CRET0000                      * clear temp file
          ADD       ONE,REPNO                     * increment report number
.
        DO   .{end while}
.
          PRINT     *N,"*** End of Report ***"
.
RPRT9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Headings                                     *
.**************************************************************************
PTHD0000  LOAD      REPSECT,REPNO,REPS1L,REPS2L,REPS3L
.
          PRINT     *1,REPSECT
.
          CALL      UND80
          PRINT     *1,"|",*11,"|",*39,"|Legal|   On   | Review |",*80,"|":
                    *N,*1,"| U/R No. | Patient Name",*39,"|Stat.| Leave  |":
                    "  Date  |Attending Doctor|"
          CALL      UND80
.
PTHD9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  initialise vars                                         *
.*************************************************************************
IDAT0000  UNPACK    SP70,XXREVD,XXNAME,XXURNO,XXLSC,XXLVED
          UNPACK    SP70,XXADOCT
.
IDAT9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 70 U1-8,9-34,35-42",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,TFILNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RSTEMP1   RESET     KEY42
          READ      MEHTMPA1,KEY42;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;XXREVD,XXNAME,XXURNO,XXLSC,XXLVED,XXADOCT
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY42
          WRITE     MEHTMPA1,KEY42;XXREVD,XXNAME,XXURNO,XXLSC,XXLVED,XXADOCT
          RETURN
.
.
          INC       STD001IO
          INC       MEHVI1IO/INC
          INC       MEHLEGIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATONLIO/INC
          INC       PATDO1IO/INC
          INC       PATTRNIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

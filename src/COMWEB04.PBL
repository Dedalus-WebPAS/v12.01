.------------------------------------------------------------------------------
. Program       : COMWEB04
.
. Function      : PABX Billing Interface Maintenance
.
.                 Report 1: Charge file maintenance
.                 Report 2: Extension file maintenance
.
. Modifications :
.      V12.00.01  30/05/2025 Jacob Jackson   TSK 0955096                  
.                 Alphanumeric changes                                      
.      V11.03.01  19/12/2022  Bella Turco    TSK 0921957
.                 Added NOMORE00 - Display "No more records" at end of list
.                 Added PTCNNRTD - Displays number of rows set by parameter 
.                 value
.      V10.04.01  31.03.2014  B.G.Ackland  CAR 299363
.                 Replace META Tag Redirect with Javascript in Common RDIREC00
.                 Routine
.------------------------------------------------------------------------------
.      V10.03.01  16/07/2013  Jill Parkinson CAR 287923
.                 Added include of PATCONFD
.------------------------------------------------------------------------------
.      V9.05.01   09/03/2006  Ebon Clements CAR 78533
.                 Mods for PATCODTM - Hospital specific category codes
.      V9.05.B01  30/01/2006  Ebon Clements CAR 93186
.                 Mods for REDIRECT length change. Recompiled for IBAWEBDF
.      v9.03.00   20/05/2004  Davin  CAR 49527
.                 Created server
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
. FD Includes
.------------------------------------------------------------------------------
.
          INC       IBAWEBDF
          INC       WEBDATDF
          INC       RSHWEBDF
.
          INC       PATCONFD/INC
          INC       IBACHGFD/INC
          INC       IBAEXTFD/INC
          INC       IBAEXTTD/INC
          INC       IBAPBXFD/INC
          INC       DEBDBTFD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
.
          INC       CALDURNV/INC
.
.-----------------------------------------------------------------------------
. Variable Declarations
.-----------------------------------------------------------------------------
.
. CGI Parameters
. **************
.
NEXTPAGE  FORM      1         * Nextpage parameter
UPDTTYPE  DIM       1         * Update Type (A)dd,(U)pdate,(D)elete,Blank=disp
STARTKEY  DIM       4         * Starting Key for search
.
CHTYP001  DIM       3
CHTYP002  FORM      3.2
.
FLTREXTN  DIM       4        * Search Filter - Extension Number
FLTRADMN  DIM       8        * Search Filter - Admission Number
FLTRDEBT  DIM       8        * Search Filter - Debtor Code
FLTRFRDT  DIM       8        * Search Filter - From Date
FLTRTODT  DIM       8        * Search Filter - To Date
FLTRBILL  FORM      1        * Search Filter - Billed Indicator
PATNNAME  DIM       30       * Patient Name
.
.
. Program Variables
. *****************
ADMINNUM  DIM       8        * Admission number as numeric field
BILLSTAT  DIM       5 
CHARGE    FORM      3.2
CHRGDESC  DIM       20       * Charge Type Description
DSPSDATE  DIM       10
ETTIME    DIM       6
EXTDATE8  DIM       8
EXTNNUMB  DIM       8
EXTYPDSC  DIM       30       * Extension Type Description
MAXROWS   FORM      3        * Maximum number of rows returned by Search
PHTYPDSC  DIM       20       * Phone Type Description
SAVEXTN   DIM       4
TOTLROWS  FORM      3        * Total Number of Rows
.
.
.
. Program Constants
. *****************
.
.
+
.-----------------------------------------------------------------------------
.
PRGID     INIT      "COMWEB04"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "PABX Billing Interface Maintenance"
+
.------------------------------------------------------------------------------
.
          CALL      WEBINT00                * Initialise Fifo Etc.
          CALL      INIT0000                * Open Files
.
MAIN1000  CALL      WEBRED00                * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO          * End Server Process if Option=999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00                * Output File & Write HTML Pg Header
          GOTO      MAIN1000
.
MAIN1100  CALL      CHGFIL00                * Charge File Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPG000                * next page
          GOTO      MAIN1000
.
MAIN1200  CALL      EXTFIL00                * Extension File Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPG000                * next page
          GOTO      MAIN1000
.
MAIN1300  CALL      REPENQ00                * Call Repository Enquiry
          BRANCH    EXIT,MAIN1000
          CALL      NXTPG000                * next page
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
+
.------------------------------------------------------------------------------
.  Next Page URL
.------------------------------------------------------------------------------
.
NXTPG000  IF        NEXTPAGE=0
            CALL      WINCLS00              * Refresh parent and close window
          ENDIF

          IF        NEXTPAGE=1
            CALL      RSAVED00              * Redirect URL
          ENDIF
.
          IF        NEXTPAGE=2
            CALL      GOBACK00              * Go Back 1 html page
          ENDIF
.
          IF        NEXTPAGE=3
            CALL      DAH20000              * Go Back 2 html pages
          ENDIF
.
NXTPG999  RETURN
.------------------------------------------------------------------------------
.  Goes back 1 page
.------------------------------------------------------------------------------
.
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"

          CALL      CLOSHTML
.
GOBACK99  RETURN
+
.------------------------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------------------------
INIT0000  MOVE      "100",MAXROWS
.
          OPEN      IBACHGA1,"ibachgaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      IBAEXTA1,"ibaextaf"
          OPEN      IBAEXTA2,"ibaextaf"
          OPEN      DEBDBTA1,"debdbtaf"
          OPEN      IBAPBXA1,"ibapabxf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATWR1A1,"patwr1af"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
+
.------------------------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,STARTKEY
.
          MOVE      Z70,CHTYP001
          MOVE      ZERO,CHTYP002
.
          MOVE      SP70,FLTREXTN
          MOVE      SP70,FLTRADMN
          MOVE      SP70,FLTRDEBT
          MOVE      SP70,FLTRFRDT
          MOVE      SP70,FLTRTODT
          MOVE      ZERO,FLTRBILL
          MOVE      SP70,PATNNAME
.
          CALL      CPEXT000           * Clear PABX Extension file CGI vars
.
.
CLRPAR99  RETURN
+
.------------------------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------------------------
.
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fltrextn=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FLTREXTN
            PACK      FLTREXTN,FLTREXTN,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fltradmn=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FLTRADMN
            PACK      FLTRADMN,FLTRADMN,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fltrdebt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FLTRDEBT
            PACK      FLTRDEBT,FLTRDEBT,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fltrfrdt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FLTRFRDT
            PACK      FLTRFRDT,FLTRFRDT,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fltrtodt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FLTRTODT
            PACK      FLTRTODT,FLTRTODT,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fltrbill=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FLTRBILL
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "patnname=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PATNNAME
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "chtyp",QRYNAME
          IF        @EQUAL
            CALL      STCHG000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "ibext",QRYNAME
          IF        @EQUAL
            CALL      SPEXT000         * IBAEXTFD
            GOTO      SETPAR99 
          ENDIF
.
SETPAR99  RETURN
+
.-------------------------------------------------------------------------------
.   Set Parameters for Charge Type Table
.-------------------------------------------------------------------------------
STCHG000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STCHG001,STCHG002
.
          MOVE      ONE,EXIT
          GOTO      STCHG999
.
STCHG001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,CHTYP001
          PACK      CHTYP001,CHTYP001,SP70
          GOTO      STCHG999
STCHG002  MOVE      QRYDATA,CHTYP002
          GOTO      STCHG999
.
STCHG999  RETURN
+
.------------------------------------------------------------------------------
. Charge File Maintenance
.------------------------------------------------------------------------------
CHGFIL00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE            * Display
          GOTO      CHGFIL40 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * add
          GOTO      CHGFIL10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * update
          GOTO      CHGFIL20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * delete
          GOTO      CHGFIL30 IF EQUAL
.
          GOTO      CHGFIL90
.
.
CHGFIL10  RESET     CHTYP001                * add formaction
          PACK      KEY3,CHTYP001,SP70
          CALL      RDIBCHG1
          COMPARE   ZERO,OVRCD
          GOTO      CHGFIL92 IF EQUAL
.
          CALL      CHGSAV00                * Moves cgis into file variables
.
          PACK      KEY3,CHTYP001,SP70
          CALL      WRIBCHG1                * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      CHGFIL99
.
CHGFIL20  PACK      KEY3,CHTYP001,SP70      * Update Formaction
          CALL      RDIBCHG1
          BRANCH    OVRCD,CHGFIL91
.
          CALL      CHGSAV00                * Moves cgis into file variables
.
          CALL      UPIBCHG1                * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      CHGFIL99
.
CHGFIL30  PACK      KEY3,CHTYP001,SP70      * Delete Formaction
          CALL      RDIBCHG1
          BRANCH    OVRCD,CHGFIL91
          PACK      KEY3,IBCHCHRG
          CALL      DEIBCHG1                * delete charge type
.
          MOVE      ZERO,EXIT
          GOTO      CHGFIL99
.
.         ************ DISPLAY FORMACTION **********
.
CHGFIL40  PACK      KEY3,CHTYP001,SP70
          CALL      RDIBCHG1
          IF        OVRCD=1
            CALL      CLRCHG00              * Clear charge type file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "PABX Charge File Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,CHGFIL99
.
          CALL      WEBBOD00                * Process Web Body
.
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      CHGFIL99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
CHGFIL90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHGFIL99
.
CHGFIL91  MOVE      "Charge Type does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHGFIL99
.
CHGFIL92  MOVE      "Charge Type exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHGFIL99
.
CHGFIL99  RETURN
+
.------------------------------------------------------------
. Moves charge type cgi variables into file variables
.------------------------------------------------------------
CHGSAV00  MATCH     CHTYP001,Z70
          IF        !@EQUAL
            MOVE      CHTYP001,KEY3
            SQUEEZE   KEY3
            PACK      IBCHCHRG,KEY3,SP70
          ENDIF
.
          MOVE      CHTYP002,IBCHRATE
.
CHGSAV99  RETURN
+
.-----------------------------------------------------------
. Clear charge type file variables
.-----------------------------------------------------------
CLRCHG00  PACK      IBCHCHRG,SP70
          MOVE      ZERO,IBCHRATE
          PACK      IBCHSPAR,SP70
CLRCHG99  RETURN
+
.------------------------------------------------------------------------------
. Extension File Maintenance
.------------------------------------------------------------------------------
EXTFIL00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE            * Display
          GOTO      EXTFIL40 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      EXTFIL10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      EXTFIL20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      EXTFIL30 IF EQUAL
.
          GOTO      EXTFIL90
.
.
EXTFIL10  RESET     IBEXT001                * Add formaction
          PACK      KEY4,IBEXT001,SP70
          CALL      RDIBEXT1
          COMPARE   ZERO,OVRCD
          GOTO      EXTFIL92 IF EQUAL
.
          CALL      MVEXT000                * Moves CGIs to file variables
.
          PACK      KEY4,IBEXT001,SP70
          CALL      WRIBEXT1                * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      EXTFIL99
.
EXTFIL20  PACK      KEY4,IBEXT001,SP70      * Update Formaction
          CALL      RDIBEXT1
          BRANCH    OVRCD,EXTFIL91
.
          CALL      MVEXT000                * Moves CGIs to file variables
.
          CALL      UPIBEXT1                * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      EXTFIL99
.
EXTFIL30  PACK      KEY4,IBEXT001,SP70      * Delete Formaction
          CALL      RDIBEXT1
          BRANCH    OVRCD,EXTFIL91
          PACK      KEY4,IBEXEXTN,SP70
          CALL      DEIBEXT1                * Delete Extension Number from file
.
          MOVE      ZERO,EXIT
          GOTO      EXTFIL99
.
.         ************ DISPLAY FORMACTION **********
.
EXTFIL40  PACK      KEY4,IBEXT001,SP70
          CALL      RDIBEXT1
          IF        OVRCD=1
            CALL      CLEXT000              * Clear Extension file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "PABX Extension File Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,EXTFIL99
.
          CALL      WEBBOD00                * Process Web Body
.
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EXTFIL99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
EXTFIL90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EXTFIL99
.
EXTFIL91  MOVE      "Extension Number does not exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EXTFIL99
.
EXTFIL92  MOVE      "Extension Number already exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EXTFIL99
.
EXTFIL99  RETURN
+
.------------------------------------------------------------------------------
. Call Repository Enquiry
.------------------------------------------------------------------------------
REPENQ00  CLEAR     WEBTITLE
          MOVE      "Call Repository Enquiry",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,REPENQ99
.
          CALL      WEBBOD00                * Process Web Body
.
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      REPENQ99
.
REPENQ99  RETURN
+
.------------------------------------------------------------------------------
. Process Fields called from WEBBOD00
.------------------------------------------------------------------------------
.
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30
.
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      TABL0000                * Table Displays
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22
          GOTO      PROFLD99
.
PROFLD21  CALL      TABL0000                * Table Displays
          GOTO      PROFLD99
.
PROFLD22  CALL      IBEX0000                * PABX Extension File Display
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31
          GOTO      PROFLD99
.
PROFLD31  CALL      SRCH0000                * Search Items Display
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------
. Table Outputs (Visit,etc)
.------------------------------------------------------------
TABL0000  BRANCH    FLDITMNO,TABL0100,TABL0200,TABL0300,TABL0400,TABL0500:
                             TABL0600,TABL0700
          GOTO      TABL9999
.
TABL0100  CALL      CHGTYP00                 * Charge Types by Type
          GOTO      TABL9999
.
TABL0200  CALL      PREVGR00                 * Previous group
          GOTO      TABL9999
.
TABL0300  CALL      NEXTGR00                 * Next group
          GOTO      TABL9999
.
TABL0400  WRITE     HTMLFILE;IBCHCHRG;       * call charge type
          GOTO      TABL9999
.
TABL0500  WRITE     HTMLFILE;IBCHRATE;       * call rate
          GOTO      TABL9999
.
TABL0600  MOVE      "CY",CATEGORY            * charge type description
          MOVE      IBCHCHRG,CODE
          CALL      CODITM00
          GOTO      TABL9999
.
TABL0700  CALL      EXTTBL00                * Extension File Table
          GOTO      TABL9999
.
TABL9999  RETURN
+
.------------------------------------------------------------------------------
. Search Items Display (Values for Search filters, Search results, etc)
.------------------------------------------------------------------------------
SRCH0000  BRANCH    FLDITMNO,SRCH0100,SRCH0200,SRCH0300,SRCH0400,SRCH0500:
                             SRCH0600,SRCH0700,SRCH0800,SRCH0900,SRCH1000:
                             SRCH1100
          GOTO      SRCH9999
.
SRCH0100  WRITE     HTMLFILE;FLTREXTN;      * Filter value - Extension number
          GOTO      SRCH9999
.
SRCH0200  CALL      EXTSEL00                * Extension Numbers Selection List
          GOTO      SRCH9999
.
SRCH0300  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,SRCH0310
.
          WRITE     HTMLFILE;FLTRADMN;      * Filter value - Admission number
          GOTO      SRCH9999
.
SRCH0310  WRITE     HTMLFILE;PATNNAME;      * Patient Name - Admission Desc
          GOTO      SRCH9999
.
SRCH0400  WRITE     HTMLFILE;FLTRDEBT;      * Filter value - Debtor number
          GOTO      SRCH9999
.
SRCH0500  MATCH     SP70,FLTRDEBT           * Debtor Desc
          GOTO      SRCH9999 IF EQUAL
          PACK      KEY8,FLTRDEBT,SP70
          CALL      RDDBDB1
          IF        OVRCD=0
            WRITE     HTMLFILE;DBDBNA1;
          ENDIF
          GOTO      SRCH9999
.
SRCH0600  WRITE     HTMLFILE;FLTRFRDT;      * Filter - From Date (CCYYMMDD)
          GOTO      SRCH9999
.
SRCH0700  WRITE     HTMLFILE;FLTRTODT;      * Filter - To Date (CCYYMMDD)
          GOTO      SRCH9999
.
SRCH0800  MATCH     SP70,FLTRFRDT
          GOTO      SRCH9999 IF EQUAL
          UNPACK    FLTRFRDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2                 * Filter - From Date (DD MMM CCYY)
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      SRCH9999
.
SRCH0900  MATCH     SP70,FLTRTODT
          GOTO      SRCH9999 IF EQUAL
          UNPACK    FLTRTODT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2                 * Filter - To Date (DD MMM CCYY)
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      SRCH9999
.
SRCH1000  WRITE     HTMLFILE;FLTRBILL;      * Filter - Billed Indicator
          GOTO      SRCH9999
.
SRCH1100  CALL      DISRES00                * Display Search results
          GOTO      SRCH9999
.
SRCH9999  RETURN
+
.------------------------------------------------------------------------------
. Display Search Results - Based on selected Search criteria
.------------------------------------------------------------------------------
DISRES00  MATCH     SP70,FLTRADMN           * Is Admission filter blank?
          IF        !@EQUAL
            MOVE      ZERO,TOTLROWS
            CALL      DADM0000              * No, Show Admission Search Results
            GOTO      DISRES99
          ENDIF
.
          MATCH     SP70,FLTRDEBT           * Is Debtor filter blank?
          IF        !@EQUAL
            MOVE      ZERO,TOTLROWS
            CALL      DDEB0000              * No, Show Debtor Search Results
            GOTO      DISRES99
          ENDIF
.
          MATCH     SP70,FLTREXTN           * Is Extension filter blank?
          IF        !@EQUAL
            MOVE      ZERO,TOTLROWS
            MOVE      FLTREXTN,EXTNNUMB
            CALL      DEXT0000              * No, Show Extension Search Results
            GOTO      DISRES99
          ENDIF
.
DISRES99  RETURN
+
.------------------------------------------------------------------------------
. Display Calls based on Admission Number
.
. get range of date/times patient was at a particular extension number. Then
. get all calls made between these date/times. Then get next range of date/time
. in case patient has changed ward/bed and has a new extension number. Do all
. this up until discharge.
.------------------------------------------------------------------------------
DADM0000  MOVE      FLTRADMN,ADMINNUM
          PACK      KEY30,ADMINNUM,SP30
          CALL      RDSTRAN2
DADM1000  CALL      RDKTRAN2
          BRANCH    OVRCD,DADM9999
.
          MATCH     ADMINNUM,TADMN
          GOTO      DADM9999 IF NOT EQUAL
.
          PACK      KEY6,TWARD,TBED              * get extension number
          CALL      RDWARD1
          BRANCH    OVRCD,DADM9000               * error!
.
          MATCH     SP4,WEXTN                    * ignore if no extension
          GOTO      DADM1000 IF EQUAL
.
          PACK      KEY4,WEXTN                   * make sure extension exists
          CALL      RDIBEXT1
          BRANCH    OVRCD,DADM1000
.
          MATCH     SP70,FLTREXTN                * Is Extension filter blank?
          IF        !@EQUAL                      
            MATCH     FLTREXTN,WEXTN             * No, does Extn match filter?
            GOTO      DADM1000 IF NOT EQUAL      * No, get next record (Extn)
          ENDIF
.
          MOVE      TDATE,STDATE                 * save start date
          MOVE      TTIME,STTIME                 * save start time
          MOVE      WEXTN,SAVEXTN                * save extension number
.
. now check for a change in ward bed (extension number)
.
DADM1500  CALL      RDKTRAN2
          BRANCH    OVRCD,DADM9999
.
          MATCH     ADMINNUM,TADMN
          GOTO      DADM9999 IF NOT EQUAL
.
          MOVE      TDATE,ETDATE                 * save end date
          MOVE      TTIME,ETTIME                 * save end time
.
          MATCH     ANSD,TMOVE                   * discharge record?
          GOTO      DADM3500 IF EQUAL            * finished if discharged
.
          PACK      KEY6,TWARD,TBED              * get extension number
          CALL      RDWARD1
          BRANCH    OVRCD,DADM9000               * error!
.
. if no extension then continue anyway, because gcal0000 will do nothing
.
          MATCH     SP4,WEXTN                    * ignore if no extension
          GOTO      DADM3000 IF EQUAL
.
          PACK      KEY4,WEXTN                   * make sure extension exists
          CALL      RDIBEXT1
          BRANCH    OVRCD,DADM9100
.
          MATCH     SP70,FLTREXTN                * Is Extension filter blank?
          IF        !@EQUAL                      
            MATCH     FLTREXTN,WEXTN             * No, does Extn match filter?
            GOTO      DADM1000 IF NOT EQUAL      * No, get next record (Extn)
          ENDIF
.
          MATCH     WEXTN,SAVEXTN                * still same extension?
          GOTO      DADM1500 IF EQUAL            * get next transfer record
.
. we have the start and end dates and times so get calls for this range
.
DADM3000  CALL      GCAL0000                     * get calls between these dats
          IF        TOTLROWS>=MAXROWS
            GOTO      DADM9999
          ENDIF
          MOVE      ETDATE,STDATE                * new strt date = old end date
          MOVE      ETTIME,STTIME                * new strt time = old end time
          MOVE      WEXTN,SAVEXTN                * new extension
          GOTO      DADM1500
.
DADM3500  CALL      GCAL0000                     * get calls between these dats
.
DADM9000  
...          DISPLAY   *P1:24,*EL,"ERROR:  Ward/Bed Missing ",*V2LON,TWARD:
...                    *HOFF,"/",*V2LON,TBED,*HOFF,".  ";
...          CALL      HITENTER
          GOTO      DADM9999
.
DADM9100  
...          DISPLAY   *P1:24,*EL,"ERROR:  Extension Missing ",*V2LON,WEXTN:
...                    *HOFF,".  ";
...          CALL      HITENTER
          GOTO      DADM9999
.
DADM9999  RETURN
+
.------------------------------------------------------------------------------
. GCAL0000  :  get calls for a patient between 2 given dates/times   
.              Parameters: STDATE = start date, STTIME = start time  
.                          ETDATE = end date  , ETTIME = end time    
.                          SAVEXTN = Extension Number                
.-----------------------------------------------------------------------------
GCAL0000  PACK      KEY20,SAVEXTN,SP20
          CALL      RSIBPBX1
GCAL2000  CALL      RKIBPBX1
          BRANCH    OVRCD,GCAL9999
.
          MATCH     SAVEXTN,IBPBEXTN
          GOTO      GCAL9999 IF NOT EQUAL
.
. now check if call in range
.
          UNPACK    IBPBSDAT,EXTDATE8
.
          MATCH     EXTDATE8,ETDATE              * end trans date < ext date?
          GOTO      GCAL9999 IF LESS
.
.  now check Start date/time
.
          MATCH     STDATE,EXTDATE8              * ext date < start trans date?
          GOTO      GCAL2000 IF LESS
.
          MATCH     STDATE,EXTDATE8
          IF        @EQUAL
            MATCH     STTIME,IBPBSTIM            * time in range?
            GOTO      GCAL2000 IF LESS           * not in range
          ENDIF
.
.    no check End date/time
.
          MATCH     EXTDATE8,ETDATE              * date in pats stay range
          GOTO      GCAL9999 IF LESS
.
          MATCH     EXTDATE8,ETDATE              * date in pats stay range
          IF        @EQUAL
            MATCH     IBPBSTIM,ETTIME
            GOTO      GCAL5000 IF NOT LESS
            GOTO      GCAL2000
          ENDIF
.
. we have a call made in date/time range for this extension
.
GCAL5000  ADD       ONE,TOTLROWS
          CALL      DSPROW00                     * Display Call Details
.
          IF        TOTLROWS>=MAXROWS
            WRITE     HTMLFILE;"alert('Too Many Calls to Display.');";
            GOTO      DEXT9999
          ENDIF
.
          GOTO      GCAL2000
.
GCAL9999  RETURN
+
.------------------------------------------------------------------------------
. Display Calls based on Debtor Number
.------------------------------------------------------------------------------
DDEB0000  PACK      KEY12,FLTRDEBT,SP70          * Find Extension Numbers for a
          CALL      RSIBEXT2                     * given Debtor Code.
DDEB0100  CALL      RKIBEXT2
          BRANCH    OVRCD,DDEB9999
.
          MATCH     FLTRDEBT,IBEXDEBT            * Same Debtor?
          GOTO      DDEB9999 IF NOT EQUAL        * No, finished processing
.
          MATCH     SP70,FLTREXTN                * Is Extension Filter blank?
          GOTO      DDEB0500 IF EQUAL            * Yes, get calls for this Extn
. 
          MATCH     IBEXEXTN,FLTREXTN            * Is this Extn same as Filter?
          GOTO      DDEB0100 IF NOT EQUAL        * No, get next Extn for Debtor
.          
.         Find all calls for this Extension number
.
DDEB0500  MOVE      IBEXEXTN,EXTNNUMB
          CALL      DEXT0000
          IF        TOTLROWS>=MAXROWS
            GOTO      DDEB9999
          ENDIF
.
          GOTO      DDEB0100                     * Get next Extn for Debtor
.
DDEB9999  RETURN
+
.------------------------------------------------------------------------------
. Display Calls based on Extension Number
.------------------------------------------------------------------------------
DEXT0000  PACK      KEY20,EXTNNUMB,FLTRFRDT,SP70
          CALL      RSIBPBX1
DEXT0100  CALL      RKIBPBX1
          BRANCH    OVRCD,DEXT9999
.
          MATCH     EXTNNUMB,IBPBEXTN            * Same Extension Number?
          GOTO      DEXT9999 IF NOT EQUAL        * No, finished processing
. 
          MATCH     SP70,FLTRTODT                * Is To Date Filter blank?
          GOTO      DEXT0500 IF EQUAL            * Yes, skip To Date check
.
          MATCH     IBPBEDAT,FLTRTODT            * Is To_Date < Call_End_Date  
          GOTO      DEXT0100 IF LESS             * Yes, out of range, skip 
.
DEXT0500  COMPARE   "0",FLTRBILL                 * Display All calls?
          GOTO      DEXT1000 IF EQUAL            * Yes, skip check for Billed 
.
          COMPARE   "2",FLTRBILL                 * Display Billed calls only?
          IF        @EQUAL
            MATCH     SP70,IBPBBILD              * Yes, Is Billed date blank?
            GOTO      DEXT0100 IF EQUAL          * Yes, skip this record
          ELSE
            MATCH     SP70,IBPBBILD              * No, Is Billed date blank?
            GOTO      DEXT0100 IF NOT EQUAL      * No, skip this record
          ENDIF
.
DEXT1000  ADD       ONE,TOTLROWS
          CALL      DSPROW00                     * Display Call Data
.
          IF        TOTLROWS>=MAXROWS
            WRITE     HTMLFILE;"alert('Too Many Calls to Display.');";
            GOTO      DEXT9999
          ENDIF
.
          GOTO      DEXT0100
.
DEXT9999  RETURN
+
.------------------------------------------------------------------------------
. Format Call data for display                                
.------------------------------------------------------------------------------
FRMT0000  UNPACK    IBPBSDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSPSDATE
.
          PACK      KEY4,IBPBEXTN,SP70
          CALL      RDIBEXT1
.
          MOVE      ZERO,IBCHRATE
          PACK      KEY3,IBEXCHRG,SP70
          CALL      RDIBCHG1                        * get rate
          ASSIGN    (IBCHRATE*IBPBPULS),CHARGE
.
          MOVE      IBPBSTIM,TIMESTRT
          MOVE      IBPBETIM,TIMESTOP
          CALL      CALLDURN                        * get duration of call
.
          MATCH     SP70,IBPBBILD
          IF        @EQUAL
            MOVE      "No",BILLSTAT
          ELSE
            MOVE      "Yes",BILLSTAT
          ENDIF
.
FRMT9999  RETURN
+
.------------------------------------------------------------------------------
. Display Single row of Call related Data
.------------------------------------------------------------------------------
DSPROW00  CALL      FRMT0000                * Format Data for Display 
          WRITE     HTMLFILE;"t.addRow(#"",IBPBEXTN,"#",":
                             "#"",IBPBSDAT,"#",":
                             "#"",IBPBSTIM,"#",":
                             "#"",CALLDURN,"#",":
                             "#"",IBPBPHNO,"#",":
                             "#"",CHARGE,"#",":
                             "#"",BILLSTAT,"#");"
.
DSPROW99  RETURN
+
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",STARTKEY
          REP       "'~",STARTKEY
.
          WRITE     HTMLFILE;"comweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY;
.
          REP       "+ ",STARTKEY
          REP       "~'",STARTKEY
.
NEXTGR99  RETURN
+
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",STARTKEY
          REP       "'~",STARTKEY
.
          WRITE     HTMLFILE;"comweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY;
.
          REP       "+ ",STARTKEY
          REP       "~'",STARTKEY
.
PREVGR99  RETURN
+
.------------------------------------------------------------
. Table of Charge Types by Type
.------------------------------------------------------------
CHGTYP00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      CTHED000              * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY3,STARTKEY,SP70
          CALL      RSIBCHG1
          CALL      RPIBCHG1
.
CHGTYP10  CALL      RKIBCHG1
          BRANCH    OVRCD,CHGTYP90
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      CHGTYP10
          ENDIF
.
          PACK      KEY5,ANSC,ANSY,IBCHCHRG
          CALL      RDCODE1
          IF        OVRCD=1
            PACK      TDESC,SP70
          ENDIF
.
          CALL      CTROW000                * Display Charge Type Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CHGTYP10 IF NOT EQUAL
          GOTO      CHGTYP99
.
CHGTYP90  CALL      NOMORE00      * Display No More Records Message
.
CHGTYP99  RETURN
+
.------------------------------------------------------------
. Charge Type Maintenance (TABLE HEADING)
.------------------------------------------------------------
CTHED000  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell width=20%>Charge Type":
                             "</td>":
                             "<td class=HeadingCell width=20%>Description":
                             "</td>":
                             "<td class=HeadingCell width=60%>Rate ($/Pulse)":
                             "</td>":
                             "</tr>"
CTHED999  RETURN
+
.------------------------------------------------------------
. Charge Type Maintenance (TABLE ROW)
.------------------------------------------------------------
CTROW000  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail01":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&chtyp001=",IBCHCHRG,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             IBCHCHRG,"</td>":
                             "<td>",TDESC,"</td>":
                             "<td>",IBCHRATE,"</td>":
                             "</tr>"
CTROW999  RETURN
+
.------------------------------------------------------------
. Table of Extension Numbers
.------------------------------------------------------------
EXTTBL00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      EXHED000              * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY4,STARTKEY,SP70
          CALL      RSIBEXT1
          CALL      RPIBEXT1
.
EXTTBL10  CALL      RKIBEXT1
          BRANCH    OVRCD,EXTTBL90
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      EXTTBL10
          ENDIF
.
          MOVE      SP70,CHRGDESC
          PACK      KEY5,ANSC,ANSY,IBEXCHRG
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,CHRGDESC
          ENDIF
.
          MOVE      SP70,EXTYPDSC
          COMPARE   "1",IBEXETYP
          IF        @EQUAL
            MOVE      "Ward/Bed",EXTYPDSC
            GOTO      EXTTBL20
          ENDIF
.
          COMPARE   "2",IBEXETYP
          IF        @EQUAL
            MOVE      "Tenant",EXTYPDSC
            GOTO      EXTTBL20
          ENDIF
.
          COMPARE   "3",IBEXETYP
          IF        @EQUAL
            MOVE      "Accounts Receivable (Doctor)",EXTYPDSC
            GOTO      EXTTBL20
          ENDIF
.
          COMPARE   "4",IBEXETYP
          IF        @EQUAL
            MOVE      "Word Only (No charging)",EXTYPDSC
            GOTO      EXTTBL20
          ENDIF
.
EXTTBL20  MOVE      SP70,PHTYPDSC
          PACK      KEY5,ANSX,ANSV,IBEXPTYP
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,PHTYPDSC
          ENDIF
.
          CALL      EXROW000                * Display Extension Table Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      EXTTBL10 IF NOT EQUAL
          GOTO      EXTTBL99
.
EXTTBL90  CALL      NOMORE00       * Display No More Records Message
.
EXTTBL99  RETURN
+
.------------------------------------------------------------
. Extension File Table (TABLE HEADING)
.------------------------------------------------------------
EXHED000  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell width=20%>Extension Number":
                             "</td>":
                             "<td class=HeadingCell width=20%>Description":
                             "</td>":
                             "<td class=HeadingCell width=20%>Charge Type":
                             "</td>":
                             "<td class=HeadingCell width=20%>Extension Type":
                             "</td>":
                             "<td class=HeadingCell width=20%>Phone Type":
                             "</td>":
                             "</tr>"
EXHED999  RETURN
+
.------------------------------------------------------------
. Extension File Table (TABLE ROW)
.------------------------------------------------------------
EXROW000  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ibext001=",IBEXEXTN,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             IBEXEXTN,"</td>":
                             "<td>",IBEXDESC,"</td>":
                             "<td>",CHRGDESC,"</td>":
                             "<td>",EXTYPDSC,"</td>":
                             "<td>",PHTYPDSC,"</td>":
                             "</tr>"
EXROW999  RETURN
+
.------------------------------------------------------------------------------
. Extension Numbers Selection List
.------------------------------------------------------------------------------
EXTSEL00  PACK      KEY4,SP70
          CALL      RSIBEXT1
EXTSEL10  CALL      RKIBEXT1
          BRANCH    OVRCD,EXTSEL99
.
          WRITE     HTMLFILE;"<option value=",IBEXEXTN,">":
                               "(",IBEXEXTN,") ",IBEXDESC,"</option>";
.
          GOTO      EXTSEL10
.
EXTSEL99  RETURN
+
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
+
.------------------------------------------------------------------------------
. I/O and Subroutine Includes
.------------------------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       DAYOFWEK
          INC       RSHWEBCD
          INC       WEBDATCD
.
          INC       IBACHGIO/INC
          INC       IBAEXTIO/INC
          INC       IBAPBXIO/INC
          INC       DEBDBTIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
.
          INC       IBAEXTTM
          INC       CALLDURN
+
.------------------------------------------------------------------------------
. End Of Program
.------------------------------------------------------------------------------

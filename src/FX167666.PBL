.*****************************************************************************
.* System    :   Allied Health System                                        *
.* Program   :   FX167666                                                    *
.* Desc      :   Fixit for A/H Funding Source                                *
.*****************************************************************************
.* Date      :   02/05/2008                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will update allrefaf.alrecomp                  *
.* Mods      :                                                               *
.*         V10.00.01 22/04/2010  Steve Armstrong   CAR 220289                *
.*                   Removed fix for allhdtaf.alhdrfnd.                      *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
.
.
PRGID     INIT      "FX167666"
PRGNAM    INIT      "Allied Health Funding Fixit"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*    Loop through the allrefaf table looking for records                    *
.*    where the funding status (alrecomp & alhdfnds) is filled with tilda's. *
.*    If matching records are found, update the fields with spaces.          *
.*****************************************************************************
.
.         Fix allrefaf
.
PROC0000  MOVE      ZERO,COUNTER                 * initialise counter
          DISPLAY   *P1:24,*EL,"Updating allrefaf: ";
.
          MOVE      SP8,KEY8
          CALL      RSALREF1                     * position at start of file
PROC0500  CALL      RKALREF1                     * read next record
          BRANCH    OVRCD,PROC5000               * eof - finished
.
          IF        (COUNTER%1000)=0 | COUNTER = 0
            DISPLAY   *P20:24,*EL,*V2LON,COUNTER
          ENDIF
.
          MATCH     "~~~",ALRECOMP               * tilda's in funding status ?
          GOTO      PROC0500 IF NOT EQUAL        * no - get next record
.
          ADD       ONE,COUNTER                  * increment counter
          MOVE      SP3,ALRECOMP                 * replace tilda's with spaces
          CALL      UPALREF1                     * update referral record
          GOTO      PROC0500
.
PROC5000  DISPLAY   *P1:24,*EL,*V2LON,COUNTER,*HOFF," allrefaf records ":
                    "updated.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLREFIO/INC

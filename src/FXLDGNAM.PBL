.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   FXLDGNAM                                                  *
.* Desc      :   Populate Patient First & Second Given Name Fields         *
.***************************************************************************
.* Date      :   20/11/2008                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will populate the patient first & second     *
.*           :   given name fields in pmspx2af                             *
.*           :                                                             *
.* Mods      :  V10.00.01 16/07/2010 Davin           CAR 232500            *
.*           :            Added option 2 to clear pmpxfnam & pmpxsnam      *
.*           :   V9.11.00 20/11/2008 Ebon Clements    CAR 183976           *
.*           :            Created fix                                      *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECORDNM  FORM      8
.
PRGID     INIT      "FXLDGNAM"
PRGNAM    INIT      "Populate Patient First & Second Given Name Fields"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    IF        COPTION=2
            CALL      CLNM0000             * clear pmpxfnam/pmpxsnam
            GOTO      ML9999
          ENDIF
          CALL      PROC0000               * process            
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run FIXIT to populate names":
                    *P1:6,*V2LON,TWO,*HOFF,". Clear pmpxfnam & pmpxsnam"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,RECORDNM
          PACK      KEY8,SP70
          CALL      RDSMAST1
PROC1000  CALL      RDKMAST1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,RECORDNM
.
          IF        (RECORDNM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECORDNM
          ENDIF
.
          MATCH     SP70,PGNAME             * blank full given name ?
          GOTO      PROC1000 IF EQUAL       * yes - ignore
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC1000
.
          MATCH     SP70,PMPXFNAM           * blank first given name ?
          GOTO      PROC1000 IF NOT EQUAL   * no - ignore record
.
          MOVE      SP70,PMPXFNAM
          MOVE      SP70,PMPXSNAM
.
          SCAN      SP1,PGNAME              * No space so only one name
          IF        !@EQUAL
            MOVE      PGNAME,PMPXFNAM       * Save first name
            GOTO      PROC1500
          ENDIF
.
          MOVEFPTR  PGNAME,F2               * Save length of first name
          RESET     PGNAME
          SETLPTR   PGNAME,F2               * Set LL to length of first name
          MOVE      PGNAME,PMPXFNAM         * Save first name
.
          SETLPTR   PGNAME,25               * Set LL back to 25
          RESET     PGNAME
          BUMP      PGNAME,F2               * Bump to the space between names
.
PROC1100  MATCH     SP1,PGNAME              * Remove any spaces from start
          GOTO      PROC1400 IF NOT EQUAL   * of second names
.
          BUMP      PGNAME,1                * Bump 1 character
          GOTO      PROC1400 IF EOS
.
          GOTO      PROC1100
          
PROC1400  MOVE      PGNAME,PMPXSNAM         * Save second names
.
PROC1500  PACK      PMPXFNAM,PMPXFNAM,SP70
          PACK      PMPXSNAM,PMPXSNAM,SP70
.
          CALL      UPPMPX21
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
.**************************************************************************
.*                               CLNM0000              Called by: ML0000  *
.*                      Clear pmpxfnam & pmpxsnam                         *
.**************************************************************************
CLNM0000  PACK      KEY8,SP70
          CALL      RSPMPX21
CLNM1000  CALL      RKPMPX21
          BRANCH    OVRCD,CLNM9999
.
          MOVE      SP70,PMPXFNAM
          MOVE      SP70,PMPXSNAM
          CALL      UPPMPX21
          GOTO      CLNM1000
.
CLNM9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC

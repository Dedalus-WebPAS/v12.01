+
.**********************************************************************
.* System   : Operating Room System                                   *
.* Program  : IBAOPR84                                                *
.* Desc     : Nurse File Master List                                  *
.**********************************************************************
.* Date     :  11/12/89                                               *
.* Author   :  K.Peak                                                 *
.* Mods.    :                                                         *
.*             V10.04.01  07/07/2014  J.Tan     CAR 261630            *
.*                        Removed port number from temp file name     *
.*             V5.07.01   06/12/1999  J Habasque SRF 636249           *
.*                        Fixed "No records Selected" error message   *
.*             V5.07.B01  Nicole Harrington 507 rebound 88            *
.*                        Mods to use DISPHEAD                        *
.**********************************************************************
.*             30/03/90  Bill Dew 
.              delete extra line print on headings and fix codes file not
.              to print category headings
.   
.              15/10/90 Peter Eddey (whirlpool)
.              changed branch on " ok to continue " routine
.              updated ? routine to current standards
.*             V5.01.02 05/03/91 J.Tan        SRF 108015              *
.*             Changes to OPRNURDS                                    *
.*             V5.01.03 19/08/92 J.Tan     SRF 116653                 *
.*             Recompiled for OPRNURDS                                *
.**********************************************************************
.
.$$$$$
.    Operating Room System Nurse File Master List (IBAOPR84 )
.    --------------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPRNURA1
.               PATCODE1
.               CONTROLF
.
.    - Clear Input Variables
.
.    - Display Screen Format
.
.    - Input Range 
.      If "?" entered, call OPRNURDS routine
.
.    - Read through appropriate file
.
.    - Print Report
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRNURFD/INC           * Nurse File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       PATCODFD/INC           * patient codes file
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
CODEQ1    INIT      "Q1"
CODEQ2    INIT      "Q2"
CODEQ3    INIT      "Q3"
CODEQ4    INIT      "Q4"
CODEQ5    INIT      "Q5"
.
STATUS    DIM       10
STATUS1   INIT      "Active    "
STATUS2   INIT      "Non Active"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CODE      DIM       5
CMDLINE   DIM       50
.
DIM15     DIM       15              * used in OPRNURDS
.
ECODE     DIM       6               * to nurse code
ESNAM     DIM       30              * to nurse code surname
ESNAM1    DIM       30              * to nurse code surname
ECDESC    DIM       30              * to nurse code description
.
FINISH    INIT      "FINISH                        "
.
.
LNO       FORM      2
.
NDESC1    DIM       10              * user define category description
NDESC2    DIM       10
NDESC3    DIM       10
NDESC4    DIM       10
NDESC5    DIM       10
NURSCODE  DIM       6
.
PAGENO    FORM      3
.
STATIND   DIM       1
START     INIT      "START                         "
STARTFIN  FORM      1
SCODE     DIM       6               * from nurse code
SSNAM     DIM       30              * from nurse code surname
SSNAM1    DIM       30              * from nurse code surname
SCDESC    DIM       30              * from nurse code description
SP66      INIT      "                                                  ":
                    "                "
SORTSTR   DIM       50
SORT1     INIT      "u1-6,84-91"
SORT2     INIT      "u7-36,84-91"
.
.
USERDES1  DIM       10              * user define code description
USERDES2  DIM       10
USERDES3  DIM       10
USERDES4  DIM       10
USERDES5  DIM       10
UVERT1    FORM      2
UNIQUE    FORM      8
.
VAR       FORM      2
.
OPRTMP    DIM       8
OPRTMPXX  IFILE SQL, FIXED=92, KEY=SORTSTR
.
.----------------------------------------------------------
.         Temporary File Layout Variables
.
.Name     Type      Length      Physical      Start
.----     ----      ------      --------      -----
.OPNRCODE DIM            6             6          1
.OPNRSNAM DIM           30            30          7
.OPNRGNAM DIM           30            30         37
.OPNRUSR1 DIM            3             3         67
.OPNRUSR2 DIM            3             3         70
.OPNRUSR3 DIM            3             3         73
.OPNRUSR4 DIM            3             3         76
.OPNRUSR5 DIM            3             3         79
.OPNRSTAT FORM           1             2         82
DUNIQUE   DIM            8             8         84
.
.End of Record                                   92
.----------------------------------------------------------
.
PRGID     INIT      "IBAOPR84"
PRGNAM    INIT      "Nurse Master Listing"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
          CALL      INIT0000               * initialization and open files
.
.MLQQ      SPLOPEN   "data"
ML0100    CALL      CLR0000                * clear input variables
.
          CALL      OPTN0000               * select option         
          BRANCH    EXIT,ML9999
.
          CALL      DOPT0000               * display option selected
.
ML0200
          CALL      RNGE0000               * select range
          CALL      CONT0000               * ok to continue
          BRANCH    EXIT,ML1000,ML0200,ML0100
.
ML1000    CALL      CREA0000               * create temporary file
          MOVE      ONE,UNIQUE
.
          BRANCH    OPTION,ML2000,ML3000
.
ML2000    CALL      CODE0000               * sort data by code sequence
          GOTO      ML4000
.
ML3000    CALL      SNAM0000               * sort data by surname sequence
.
ML4000    CALL      PRNT0000               * print report
          CALL      END0000                * end of report
          GOTO      ML0100
.
ML9999    CLOSE     OPRTMPXX
          CALL      KILL0000               * remove temporary file
.MLQQQ     SPLCLOSE  "data"
          CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CNEWDS
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"oprnuraf";
          OPEN      OPRNURA1,"oprnuraf"
.
          DISPLAY   *P64:24,"oprnuraf";
          OPEN      OPRNURA2,"oprnuraf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OPRTMP
.
          READ      CONTROLF,FORTY;*213,OPCNUNU1,OPCNUNM1,OPCNUNU2,OPCNUNM2:
                                        OPCNUNU3,OPCNUNM3,OPCNUNU4,OPCNUNM4:
                                        OPCNUNU5,OPCNUNM5
INIT9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000   MOVE      ZERO,PAGENO
          MOVE      "60",LNO
.
. ----- Clear the Nurse File Variables -----
.
          MOVE      SP6,OPNRCODE
          PACK      OPNRSNAM,SP20,SP20
          PACK      OPNRGNAM,SP20,SP20
          UNPACK    SP30,OPNRUSR1,OPNRUSR2,OPNRUSR3,OPNRUSR4,OPNRUSR5
.
          RETURN
+
.**********************************************************************
.*                             OPTN0000                               *
.*                          Select Option                             *
.**********************************************************************
.
OPTN0000  MOVE      FALSE,EXIT
.
          HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P2:5,*V2LON,ONE,*HOFF,". Nurse Code Sequence":
                    *P2:6,*V2LON,TWO,*HOFF,". Nurse Surname Sequence"
.
OPTN1000  DISPLAY   *P1:8,*EL,"Option : "
          KEYIN     *P10:8,*V2LON,OPTION
.
          BRANCH    OPTION,OPTN9999,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
OPTN9999  RETURN
+
.**********************************************************************
.*                             DOPT0000                               *
.*                          Display Option                            *
.**********************************************************************
.
DOPT0000  BRANCH    OPTION,DOPT1000,DOPT2000
.
DOPT1000  DISPLAY   *P50:2,*V2LON,"- Nurse Code Sequence "
          MOVE      SORT1,SORTSTR
          GOTO      DOPT9999
.
DOPT2000  DISPLAY   *P50:2,*V2LON,"- Nurse Surname Sequence "
          MOVE      SORT2,SORTSTR
.
DOPT9999  RETURN
+
.**********************************************************************
.*                             RNGE0000                               *
.*                          Select Range                              *
.**********************************************************************
.
RNGE0000  MOVE      SP6,SCODE
          MOVE      SP6,ECODE
          MOVE      SP30,SSNAM
          MOVE      SP30,ESNAM
          MOVE      SP30,SSNAM1
          MOVE      SP30,ESNAM1
          MOVE      SP30,SCDESC
          MOVE      SP30,ECDESC
.
          BRANCH    OPTION,RNGE1000,RNGE4000
.
.         Find Nurse Code Range
.
RNGE1000  DISPLAY   *P1:9,*EF:
                    *P1:10,"Nurse Code From : ":
                    *P1:11,"Nurse Code To   : "
.
RNGE1100  KEYIN     *P19:10,*EL,*V2LON,SCODE
.
          ENDSET    SCODE
          APPEND    SP6,SCODE
          RESET     SCODE
.
          MATCH     QUEST,SCODE
          GOTO      RNGE1200 IF NOT EQUAL
          MOVE      SP6,SCODE
          CALL      OPRNURDS
          MOVE      ZERO,STARTFIN
          CALL      NURQ0000
          GOTO      RNGE2300
.
RNGE1200  MATCH     SP6,SCODE
          GOTO      RNGE1500 IF NOT EQUAL
          MOVE      START,SCDESC                * begin at start of nurse codes
          DISPLAY   *P19:10,*EL,*V2LON,START
          GOTO      RNGE2300
.
RNGE1500  MOVE      SP20,SCDESC
          MOVE      SCODE,KEY6
          CALL      RDOPNUR1
          BRANCH    OVRCD,RNGE2000
          MOVE      OPNRGNAM,ANS
          PACK      SCDESC,ANS,DOT,OPNRSNAM        * nurse surname
.
RNGE1700  DISPLAY   *P30:10,SCDESC
          GOTO      RNGE2300
.
RNGE2000  DISPLAY   *P1:24,*EL,"Code Does Not Exist - Hit <",*V2LON,"ENTER":
                    *HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL
          GOTO      RNGE0000
.
RNGE2300  KEYIN     *P19:11,*EL,*V2LON,ECODE
.
          ENDSET    ECODE
          APPEND    SP6,ECODE
          RESET     ECODE
.
          MATCH     QUEST,ECODE
          GOTO      RNGE2400 IF NOT EQUAL
          MOVE      SP6,ECODE
          CALL      OPRNURDS
          MOVE      ONE,STARTFIN
          CALL      NURQ0000
          GOTO      RNGE9999
.
RNGE2400  MATCH     SP6,ECODE
          GOTO      RNGE2500 IF NOT EQUAL
          MOVE      FINISH,ECDESC                * finish at end of nurse codes
          DISPLAY   *P19:11,*EL,*V2LON,FINISH
          GOTO      RNGE2700
.
RNGE2500  MOVE      SP20,ECDESC
          MOVE      ECODE,KEY6
          CALL      RDOPNUR1
          BRANCH    OVRCD,RNGE3000
          MOVE      OPNRGNAM,ANS
          PACK      ECDESC,ANS,DOT,OPNRSNAM        * nurse surname
.
          DISPLAY   *P30:11,ECDESC
RNGE2700 
          MATCH     SP6,ECODE
          GOTO      RNGE9999 IF EQUAL
.
          MATCH     SCODE,ECODE
          GOTO      RNGE3500 IF LESS
          GOTO      RNGE9999
.
RNGE3000  DISPLAY   *P1:24,*EL,"Code Does Not Exist - Hit <",*V2LON,"ENTER":
                    *HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL
          GOTO      RNGE2300
.
RNGE3500  DISPLAY   *P1:24,*EL,"To Code must be >= to From Code - Hit <":
                    *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL
          GOTO      RNGE0000
.
.         Find Nurse Surname Range
.
RNGE4000  KEYIN     *P1:9,*EF:
                    *P1:10,"Surname From : ":
                    *P1:11,"Surname To   : ":
                    *P16:10,*EL,*V2LON,SSNAM
.
          RESET     SSNAM
.
          MATCH     SP30,SSNAM
          GOTO      RNGE4500 IF NOT EQUAL
          MOVE      START,SSNAM                * begin at start of nurse s/nam
.
RNGE4500  DISPLAY   *P16:10,*V2LON,SSNAM
          MOVE      SSNAM,SSNAM1
.
          MATCH     START,SSNAM
          GOTO      RNGE4700 IF NOT EQUAL
          MOVE      SP30,SSNAM
.
RNGE4700  KEYIN     *P16:11,*EL,*V2LON,ESNAM
.
          RESET     ESNAM
.
          MATCH     SP30,ESNAM
          GOTO      RNGE5000 IF NOT EQUAL
          MOVE      FINISH,ESNAM                 * finish at end of nurse s/nam
.
RNGE5000  DISPLAY   *P16:11,*V2LON,ESNAM
          MOVE      ESNAM,ESNAM1
.
          MATCH     SP30,ESNAM
          GOTO      RNGE5200 IF EQUAL
.
          MATCH     SSNAM,ESNAM
          GOTO      RNGE5500 IF LESS
.
RNGE5200  MATCH     FINISH,ESNAM
          GOTO      RNGE9999 IF NOT EQUAL
          MOVE      SP30,ESNAM
          GOTO      RNGE9999
.
RNGE5500  DISPLAY   *P1:24,*EL,"To Name must be >= to From Name - Hit <":
                    *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL
          GOTO      RNGE0000
.
RNGE9999  RETURN
+
.*********************************************************************
.*                            NURQ0000                               *
.*              handles a question mark keyin  on nurse codes        *
.*********************************************************************
.
NURQ0000
          MOVE      SP6,NURSCODE
.
          DISPLAY   *P1:24,*EL,"Nursing code : "
          KEYIN     *P16:24,*DV,UNDLN6:
                    *P16:24,*V2LON,NURSCODE:
                    *P16:24,*DV,NURSCODE
.
          RESET     NURSCODE
          GOTO      NURQ2000 IF EOS
.
          PACK      NURSCODE,NURSCODE,SP6
          MATCH     SP6,NURSCODE
          GOTO      NURQ3000 IF NOT EQUAL
NURQ2000
          BRANCH    STARTFIN,NURQ2200
          MOVE      START,SCDESC                * begin at start of nurse codes
          MOVE      SP6,SCODE
          GOTO      NURQ6000
NURQ2200
          MOVE      FINISH,ECDESC 
          MOVE      "zzzzzz",ECODE
          GOTO      NURQ6000
.
NURQ3000  
          BRANCH    STARTFIN,NURQ4000
          MOVE      SP20,SCDESC
          GOTO      NURQ5000
NURQ4000
          MOVE      SP20,ECDESC
NURQ5000
          MOVE      NURSCODE,KEY6
          CALL      RDOPNUR1
          BRANCH    OVRCD,NURQ7000
          MOVE      OPNRGNAM,ANS
          BRANCH    STARTFIN,NURQ5500
          PACK      SCDESC,ANS,DOT,OPNRSNAM        * nurse surname
          MOVE      NURSCODE,SCODE
          CALL      RDSP0000
          DISPLAY   *P19:10,*EL,*V2LON,SCODE
          DISPLAY   *P30:10,*EL,SCDESC
          GOTO      NURQ9999 
NURQ5500
          MOVE      NURSCODE,ECODE
          MATCH     SCODE,ECODE
          GOTO      NURQ8000 IF LESS
.
          PACK      ECDESC,ANS,DOT,OPNRSNAM        * nurse surname
          MOVE      NURSCODE,ECODE
          CALL      RDSP0000
          DISPLAY   *P19:11,*EL,*V2LON,ECODE
          DISPLAY   *P30:11,*EL,ECDESC
          GOTO      NURQ9999 
.
NURQ6000  
          CALL      RDSP0000
          GOTO      NURQ9999
.
NURQ7000  
          BEEP
          DISPLAY   *P1:24,*EL,"Code Does Not Exist. ";
          CALL      HITENTER
          GOTO      NURQ0000
.
NURQ8000  
          BEEP
          DISPLAY   *P1:24,*EL,"To code must be >= from code. ";
          CALL      HITENTER
          GOTO      NURQ0000
.
NURQ9999  RETURN
.**********************************************************************
.*                             CONT0000                               *
.*                          Ok to Continue                            *
.**********************************************************************
.
CONT0000  MOVE      FALSE,EXIT
.
          CALL      CONTQST
          MOVE      ANS,FORM1
          BRANCH    FORM1,CONT9999,CONT9000,CONT9500
.
CONT9000  MOVE      TWO,EXIT
          GOTO      CONT9999
.
CONT9500  MOVE      THREE,EXIT
CONT9999  RETURN
+
.**********************************************************************
.*                             CODE0000                               *
.*                     Sort Data by Code Sequence                     *
.**********************************************************************
.
CODE0000  DISPLAY   *P1:24,*EL,*P20:24,"Nurse Code : "
.
          PACK      KEY6,SCODE
          MATCH     SP6,KEY6
          GOTO      CODE1500 IF NOT EQUAL
.
          CALL      RSOPNUR1
CODE1000  CALL      RKOPNUR1
          BRANCH    OVRCD,CODE9000
          GOTO      CODE20000
.
CODE1500  CALL      RDOPNUR1
          BRANCH    OVRCD,CODE9000
.
CODE2000  MATCH     SP6,ECODE
          GOTO      CODE3000 IF EQUAL
.
          MATCH     ECODE,OPNRCODE
          GOTO      CODE9000 IF NOT LESS
.
CODE3000  DISPLAY   *P33:24,*V2LON,OPNRCODE
.
          PACK      KEY14,OPNRCODE,DUNIQUE
          CALL      WRTMP1
          GOTO      CODE1000
.
CODE9000  MATCH     SP6,ECODE
          GOTO      CODE9999 IF EQUAL
          PACK      KEY14,OPNRCODE,DUNIQUE
          CALL      WRTMP1
CODE9999  RETURN
+
.**********************************************************************
.*                             SNAM0000                               *
.*                     Sort Data by Surname Sequence                  *
.**********************************************************************
.
SNAM0000  DISPLAY   *P1:24,*EL,*P20:24,"Surname : "
.
          PACK      KEY66,SSNAM,SP20,SP20
.
          CALL      RSOPNUR2
SNAM1000  CALL      RKOPNUR2
          BRANCH    OVRCD,SNAM9999
.
SNAM2000  MATCH     SP30,ESNAM
          GOTO      SNAM3000 IF EQUAL
.
          MATCH     OPNRSNAM,ESNAM
          GOTO      SNAM3000 IF EQUAL
          GOTO      SNAM9999 IF LESS 
.
SNAM3000  DISPLAY   *P31:24,*V2LON,OPNRSNAM
.
          PACK      KEY38,OPNRSNAM,DUNIQUE
          CALL      WRTMP1
          GOTO      SNAM1000
.
SNAM9999  RETURN
+
.**************************************************************************
.*                         PRNT0000                                       *
.*                        Print Data                                      *
.**************************************************************************
.
PRNT0000  BRANCH    OPTION,PRNT1000,PRNT2000
.
PRNT1000  MOVE      SP20,KEY14
          GOTO      PRNT3000
.
PRNT2000  PACK      KEY38,SP20,SP20
.
PRNT3000  CALL      RDSTMP1
PRNT3500  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT9999
.
          COMPARE   "50",LNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      STATUS1,STATUS
          LOAD      STATUS,OPNRSTAT,STATUS2
          MOVE      OPNRGNAM,KEY25
          PRINT     *1,"|",OPNRCODE,*8,"|",OPNRSNAM,*39,"|",KEY25:
                    *65,"|",STATUS,"|";
.
          MOVE      "77",CCOL
.
PRNT4000  
          MOVE      SP10,USERDES1
          COMPARE   ONE,OPCNUNU1                    * "0" = No, "1" = Yes 
          GOTO      PRNT4500 IF NOT EQUAL           * if No, get next user def
.
          MATCH     SP3,OPNRUSR1
          GOTO      PRNT4200 IF EQUAL
          PACK      KEY5,CODEQ1,OPNRUSR1
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT4200
          MOVE      TDESC,USERDES1
.
PRNT4200  ADD       ONE,CCOL
          PRINT     *CCOL,USERDES1;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
PRNT4500  
          MOVE      SP10,USERDES2
          COMPARE   ONE,OPCNUNU2                    * "0" = No, "1" = Yes 
          GOTO      PRNT5000 IF NOT EQUAL           * if No, get next user def
.
          MATCH     SP3,OPNRUSR2
          GOTO      PRNT4700 IF EQUAL
          PACK      KEY5,CODEQ2,OPNRUSR2
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT4700
          MOVE      TDESC,USERDES2
.
PRNT4700  ADD       ONE,CCOL
          PRINT     *CCOL,USERDES2;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
PRNT5000  
          MOVE      SP10,USERDES3
          COMPARE   ONE,OPCNUNU3                    * "0" = No, "1" = Yes 
          GOTO      PRNT5500 IF NOT EQUAL           * if No, get next user def
.
          MATCH     SP3,OPNRUSR3
          GOTO      PRNT5200 IF EQUAL
          PACK      KEY5,CODEQ3,OPNRUSR3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT5200
          MOVE      TDESC,USERDES3
.
PRNT5200  ADD       ONE,CCOL
          PRINT     *CCOL,USERDES3;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
PRNT5500  
          MOVE      SP10,USERDES4
          COMPARE   ONE,OPCNUNU4                    * "0" = No, "1" = Yes 
          GOTO      PRNT6000 IF NOT EQUAL           * if No, get next user def
.
          MATCH     SP3,OPNRUSR4
          GOTO      PRNT5700 IF EQUAL
          PACK      KEY5,CODEQ4,OPNRUSR4
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT5700
          MOVE      TDESC,USERDES4
.
PRNT5700  ADD       ONE,CCOL
          PRINT     *CCOL,USERDES4;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
PRNT6000  
          MOVE      SP10,USERDES5
          COMPARE   ONE,OPCNUNU5                    * "0" = No, "1" = Yes 
          GOTO      PRNT6500 IF NOT EQUAL           * if No, get next user def
.
          MATCH     SP3,OPNRUSR5
          GOTO      PRNT6200 IF EQUAL
          PACK      KEY5,CODEQ5,OPNRUSR5
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT6200
          MOVE      TDESC,USERDES5
.
PRNT6200  ADD       ONE,CCOL
          PRINT     *CCOL,USERDES5;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
PRNT6500  ADD       ONE,LNO
          PRINT     ""
          GOTO      PRNT3500
.
PRNT9999  RETURN
+
.**************************************************************************
.*                         END0000                                        *
.*                      End of Report                                     *
.**************************************************************************
.
END0000   COMPARE   ZERO,CPAGENO
          GOTO      END1000 IF NOT EQUAL
.
.         No Data Found
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** No Record Selected **",*W
          CALL      HEAD0000
          PRINT     *N,*N,"** END OF REPORT **"
          GOTO      END9999
.
END1000
          CALL      PGE0000
          PRINT     *N,*N,"** END OF REPORT **"
          DISPLAY   *P1:24,*EL,*V2LON:
                    "** Report Generation Completed ** ";
          CALL      HITENTER
.
END9999   DISPLAY   *P1:14,*EF
          CLOSE     OPRTMPXX
          CALL      KILL0000               * remove temporary file
          RETURN
+
.**************************************************************************
.*                         HEAD0000                                       *
.*                     Print Heading                                      *
.**************************************************************************
.
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      PGE0000 IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          BRANCH    OPTION,HEAD0500,HEAD1000
.
HEAD0500  PRINT     *40,"Code From : ",SCODE,SP3,SCDESC,*N:
                    *40,"Code To   : ",ECODE,SP3,ECDESC,*N
          GOTO      HEAD1500
.
HEAD1000  PRINT     *40,"Surname From : ",SSNAM1,*N:
                    *40,"Surname To   : ",ESNAM1,*N
.
HEAD1500  CALL      PGE0000
.
          PRINT     *1,"|Nurse",*8,"|",*39,"|":
                    *65,"|",*76,"|";
.
          MOVE      "77",CCOL
.
          COMPARE   ONE,OPCNUNU1                    * "0" = No, "1" = Yes 
          GOTO      HEAD2000 IF NOT EQUAL           * if No, get next user def
          ADD       TEN1,CCOL
          PRINT     *CCOL,"|";
.
HEAD2000  COMPARE   ONE,OPCNUNU2                    * "0" = No, "1" = Yes 
          GOTO      HEAD2500 IF NOT EQUAL           * if No, get next user def
          ADD       TEN1,CCOL
          PRINT     *CCOL,"|";
.
HEAD2500  COMPARE   ONE,OPCNUNU3                    * "0" = No, "1" = Yes 
          GOTO      HEAD3000 IF NOT EQUAL           * if No, get next user def
          ADD       TEN1,CCOL
          PRINT     *CCOL,"|";
.
HEAD3000  COMPARE   ONE,OPCNUNU4                    * "0" = No, "1" = Yes 
          GOTO      HEAD3500 IF NOT EQUAL           * if No, get next user def
          ADD       TEN1,CCOL
          PRINT     *CCOL,"|";
.
HEAD3500  COMPARE   ONE,OPCNUNU5                    * "0" = No, "1" = Yes 
          GOTO      HEAD4000 IF NOT EQUAL           * if No, get next user def
          ADD       TEN1,CCOL
          PRINT     *CCOL,"|";
.
HEAD4000  PRINT     *N,*1,"|Code",*8,"|Surname",*39,"|Given Name(s)":
                    *65,"|  Status  |";
.
          MOVE      "77",CCOL
.
          COMPARE   ONE,OPCNUNU1                    * "0" = No, "1" = Yes 
          GOTO      HEAD4500 IF NOT EQUAL           * if No, get next user def
.
          MOVE      SP10,NDESC1
          PACK      KEY5,CODEQ1,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD4200
          MOVE      TDESC,NDESC1
.
HEAD4200  ADD       ONE,CCOL
          PRINT     *CCOL,NDESC1;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD4500  COMPARE   ONE,OPCNUNU2                    * "0" = No, "1" = Yes 
          GOTO      HEAD5500 IF NOT EQUAL           * if No, get next user def
.
          MOVE      SP10,NDESC2
          PACK      KEY5,CODEQ2,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD5000
          MOVE      TDESC,NDESC2
.
HEAD5000  ADD       ONE,CCOL
          PRINT     *CCOL,NDESC2;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD5500  COMPARE   ONE,OPCNUNU3                    * "0" = No, "1" = Yes 
          GOTO      HEAD6000 IF NOT EQUAL           * if No, get next user def
.
          MOVE      SP10,NDESC3
          PACK      KEY5,CODEQ3,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD5700
          MOVE      TDESC,NDESC3
.
HEAD5700  ADD       ONE,CCOL
          PRINT     *CCOL,NDESC3;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD6000  COMPARE   ONE,OPCNUNU4                    * "0" = No, "1" = Yes 
          GOTO      HEAD6500 IF NOT EQUAL           * if No, get next user def
.
          MOVE      SP10,NDESC4
          PACK      KEY5,CODEQ4,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD6200
          MOVE      TDESC,NDESC4
.
HEAD6200  ADD       ONE,CCOL
          PRINT     *CCOL,NDESC4;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD6500  COMPARE   ONE,OPCNUNU5                    * "0" = No, "1" = Yes 
          GOTO      HEAD7000 IF NOT EQUAL           * if No, get next user def
.
          MOVE      SP10,NDESC5
          PACK      KEY5,CODEQ5,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD6700
          MOVE      TDESC,NDESC5
.
HEAD6700  ADD       ONE,CCOL
          PRINT     *CCOL,NDESC5;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD7000  PRINT     *N;
          CALL      PGE0000
          MOVE      TEN1,LNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                             PGE0000                                *
.*                     Print Page Separator                           *
.**********************************************************************
.
PGE0000   PRINT     "*---------------------------------------":
                    "-------------------------------------";
.
          MOVE      "78",CCOL
.
          COMPARE   ONE,OPCNUNU1                    * "0" = No, "1" = Yes 
          GOTO      PGE1000 IF NOT EQUAL            * if No, get next user def
.
          PRINT     *CCOL,"-----------";
          ADD       TEN1,CCOL
.
PGE1000   COMPARE   ONE,OPCNUNU2                    * "0" = No, "1" = Yes 
          GOTO      PGE1500 IF NOT EQUAL            * if No, get next user def
.
          PRINT     *CCOL,"-----------";
          ADD       TEN1,CCOL
.
PGE1500   COMPARE   ONE,OPCNUNU3                    * "0" = No, "1" = Yes 
          GOTO      PGE2000 IF NOT EQUAL            * if No, get next user def
.
          PRINT     *CCOL,"-----------";
          ADD       TEN1,CCOL
.
PGE2000   COMPARE   ONE,OPCNUNU4                    * "0" = No, "1" = Yes 
          GOTO      PGE2500 IF NOT EQUAL            * if No, get next user def
.
          PRINT     *CCOL,"-----------";
          ADD       TEN1,CCOL
.
PGE2500   COMPARE   ONE,OPCNUNU5                    * "0" = No, "1" = Yes 
          GOTO      PGE3000 IF NOT EQUAL            * if No, get next user def
.
          PRINT     *CCOL,"----------*";
          ADD       TEN1,CCOL
.
PGE3000   ADD       ONE,LNO
          PRINT     ""                * finish the semi-colon print
          RETURN
+
.**********************************************************************
.*                             RDSP0000                               *
.*                        Redisplay Screen                            *
.**********************************************************************
.
RDSP0000  HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P2:5,*V2LON,ONE,*HOFF,". Nurse Code Sequence":
                    *P2:6,*V2LON,TWO,*HOFF,". Nurse Surname Sequence":
                    *P1:8,*EL,"Option : ",*P10:8,*V2LON,OPTION,*HOFF
          MATCH     SP6,SCODE
          GOTO      RDSP2222 IF EQUAL
          DISPLAY   *P1:10,"Nurse Code From : ":
                    *P19:10,*EL,*V2LON,SCODE,*HOFF,*P30:10,SCDESC:
                    *P1:11,"Nurse Code To   : "
          GOTO      RDSP3000
RDSP2222
          DISPLAY   *P1:10,"Nurse Code From : ",*V2LON,START,*HOFF:
                    *P1:11,"Nurse Code To   : "
RDSP3000
          MATCH     "zzzzzz",ECODE
          GOTO      RDSP4000 IF EQUAL
          DISPLAY   *P19:11,*EL,*V2LON,ECODE,*HOFF,*P30:11,ECDESC
          GOTO      RDSP9999
RDSP4000
          COMPARE   ZERO,STARTFIN
          GOTO      RDSP9999 IF EQUAL
          DISPLAY   *P19:11,*V2LON,FINISH,*HOFF
.
RDSP9999  RETURN
+
.**********************************************************************
.*                             CREA0000                               *
.*                     Create Temporary File                          *
.**********************************************************************
.
CREA0000  CLOSE     OPRTMPXX
          CALL      KILL0000               * remove temporary file
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OPRTMP,CMDLINE
          APPEND    " 92 ",CMDLINE
          APPEND    SORTSTR,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      OPRTMPXX,OPRTMP
          RETURN
+
.**********************************************************************
.*                             KILL0000                               *
.*                     Remove Temporary File                          *
.**********************************************************************
.
KILL0000  PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OPRTMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.********************************************************************** 
.*              Temporary File Layouts                                *
.**********************************************************************
.
RDSTMP1   MOVE      ZERO,OVRCD
          BRANCH    OPTION,RSTMP1,RSTMP2
.
RSTMP1    RESET     KEY14
          READ      OPRTMPXX,KEY14;;
          RETURN
.
RSTMP2    RESET     KEY38
          READ      OPRTMPXX,KEY38;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    OPRTMPXX;OPNRCODE,OPNRSNAM,OPNRGNAM,OPNRUSR1:
                                   OPNRUSR2,OPNRUSR3,OPNRUSR4,OPNRUSR5:
                                   OPNRSTAT,DUNIQUE
          MOVE      DUNIQUE,UNIQUE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMP1    MOVE      UNIQUE,DUNIQUE
          BRANCH    OPTION,WTMP1,WTMP2
.
WTMP1     RESET     KEY14
          WRITE     OPRTMPXX,KEY14;OPNRCODE,OPNRSNAM,OPNRGNAM,OPNRUSR1:
                                   OPNRUSR2,OPNRUSR3,OPNRUSR4,OPNRUSR5:
                                   OPNRSTAT,DUNIQUE
          ADD       ONE,UNIQUE
          GOTO      OVERCOND IF OVER
          RETURN
.
WTMP2     RESET     KEY38
          WRITE     OPRTMPXX,KEY38;OPNRCODE,OPNRSNAM,OPNRGNAM,OPNRUSR1:
                                   OPNRUSR2,OPNRUSR3,OPNRUSR4,OPNRUSR5:
                                   OPNRSTAT,DUNIQUE
          ADD       ONE,UNIQUE
          MOVE      UNIQUE,DUNIQUE
          GOTO      OVERCOND IF OVER
          RETURN
.
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
         INC       STD001IO
         INC       TFILENAM                * Generate Temp File Name
         INC       IBASEQIO/INC            * Sequential Numbers File
         INC       WEBERRIO/INC            * Web Server Error Logging
.
         INC       OPRNURDS                 * nurse codes file routine 
         INC       PATCODKY                * patient codes file routine
         INC       PATCODIO/INC            * patient i/o for codes
         INC       OPRNURIO/INC            * nurse i/o
.
.
.

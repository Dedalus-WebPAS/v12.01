.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   IBARSH13                                                  *
.* Desc      :   Schedule program to process doctors inactive dates        *
.***************************************************************************
.* Date      :   24/03/2003                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will process various functions that relate   *
.*           :   to the doctors active date                                *
.*           :                                                             *
.* Mods      :                                                             *
.*           :   V9.03.00 19/04/2004 Ebon Clements    CAR 18703            *
.*           :            Created program                                  *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
. 
          INC       PATDO1FD/INC
.
. LOCAL VARIABLE DEFINITION
. ----
.
CURRDATE  DIM       8
DRAW      INIT      "*-------------------------------------------------------":
                    "----------*"
DISPDAT1  DIM       11
DIM40     DIM       40
UPDATFLG  FORM      1            * Update record or just print report
.
PRGID     INIT      "IBARSH13"
PRGNAM    INIT      "Global Maintenance"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000                * initialisation and open files
.
ML0100    CALL      OPTN0000                * select options
          BRANCH    EXIT,ML9999             * EXIT = 1 if 0 chosen in menu
.
          BRANCH    OPTION,ML1100
.
          GOTO      ML0100
.
ML1100    CALL      EXPR0000                * Inactivate expired doctor codes
          BRANCH    EXIT,ML0100
          GOTO      ML9999
.
ML9999    CHAIN     PGM                     * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATDO1A1,"patdo1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Inactivate expired doctor codes";
.
OPTN0500  KEYIN     *P1:22,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:22,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.**************************************************************************
.*                               EXPR0000              Called by: ML4000  *
.* Inactivate expired doctor codes                                        *
.**************************************************************************
.
EXPR0000  CALL      UPDT0000                    * Print report only
.
          CALL      CONTQST                     * ok to continue
          BRANCH    CEXIT,EXPR5000,EXPR9999,EXPR9999    * 1=Yes, 2=No, 3=Cancel
.
EXPR5000  CALL      PEXP0000                    * Update/Print expired codes
.
EXPR9999  RETURN
.
.------------------------------------------------------------
. Update records or print report only
.------------------------------------------------------------
UPDT0000  DISPLAY   *P1:14,*EF
          KEYIN     *P1:14,*EL,"Print Report Only":
                    " (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ZERO,UPDATFLG
            GOTO      UPDT9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ONE,UPDATFLG
            GOTO      UPDT9999
          ENDIF
.
          BEEP
          GOTO      UPDT0000
.
UPDT9999  RETURN
.
.*******************************************************************************
.*                                  PEXP0000                                   *
.* Print/Expired doctor codes                            called by : MAINLINE  *
.*******************************************************************************
PEXP0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON:
                    "Printing Expired doctor Codes.  Please wait ... ";
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ZERO,CPAGENO                 * initialize page no.
          CALL      HEAD0000                     * print headings
.
          PACK      KEY6,SP70
          CALL      RDSDOCT1                     * position read PATVADFD file
PEXP1100  CALL      RDKDOCT1
          BRANCH    OVRCD,PEXP9000               * end-of-file
.
          COMPARE   ONE,DRSTAT                   * Skip if not active
          GOTO      PEXP1100 IF EQUAL
.
          MATCH     SP70,PTDOEDAT                * Skip if no end date
          GOTO      PEXP1100 IF EQUAL
.     
          MATCH     PTDOEDAT,CURRDATE            * Check if record has expired
          GOTO      PEXP1100 IF LESS
.
          MATCH     "1",PTDOAUPD                 * Skip if Auto Update End Date
          GOTO      PEXP1100 IF NOT EQUAL        * is set to no
.
          IF        UPDATFLG=1
            MOVE      ONE,DRSTAT                 * Set to Inactive
            CALL      UPDOCT1                    * Update Transfer Source
          ENDIF
.
.         print detail line
.
          COMPARE   "55",CLNO                    * page overflow ?
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      DTITL,PACTITLE
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM40
.
          MOVE      SP70,DISPDAT1
          MATCH     SP70,PTDOEDAT
          IF        !@EQUAL
            UNPACK    PTDOEDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,DISPDAT1
          ENDIF
.
          PRINT     *13,"| ",DCODE,*22,"|",DIM40,*67,"|":
                    DISPDAT1,"|"
.
          ADD       ONE,CLNO                     * increase current line no by 1
          GOTO      PEXP1100
.
.         End of Range/End of File
.
PEXP9000  PRINT     *13,DRAW,*N,*N,*13,"***  End of Report  ***"
.
PEXP9999  RETURN
.
.*******************************************************************************
.*                                  HEAD0000                                   *
.*        routine headings                               called by : PRNT0000  *
.*******************************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          GOTO      HEAD1000 IF EQUAL
          PRINT     *13,DRAW
.
HEAD1000  CLOCK     TIME,CTIMEIS
          CALL      HEAD80
.
          PRINT     *20,"Inactivate Expired Doctor Codes"
          IF        UPDATFLG=0
            PRINT     *20,"Print Report Only : Yes"
          ELSE
            PRINT     *20,"Print Report Only : No "
          ENDIF
          ADD       TWO,CLNO
.
          PRINT     *N,*13,DRAW:
                    *N,*13,"| Doctor",*22,"| Name ":
                       *67,"| End Date  |":
                    *N,*13,DRAW
P
.
          ADD       FOUR,CLNO                    * increase current line no by 4
.
HEAD9999  RETURN
.
          INC       STD001IO
.
          INC       PATDO1IO/INC

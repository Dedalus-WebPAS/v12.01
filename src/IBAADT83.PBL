.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAADT83                                             *
.*                                                                            *
.*     FUNCTION:         HEALTH FUND WARD INCOME REPORT FOR COMMUNITY BILLING *
.*                                                                            *
.*     DATE WRITTEN:     12/09/2013                                           *
.*                                                                            *
.*     AUTHOR:           J.TAN                                                *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*----------------------------------------------------------------------------*
.* V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                            * 
.*           Added alpanumeric visit number generation -ADFN1000              *
.* V11.05.01 24/02/2025  J.Tan            TSK 0946490                         *
.*           Recompiled for PATCATBI - Changes for MV CWO Payment Model       *
.* V11.04.08 25/10/2024 J.Tan             TSK 0951578                         *
.*           Recompiled for ABFPTINV-Added Min.to roundup ICU Hrs(ABF)        *
.* V11.04.07 23/09/2024  J.Tan          TSK 0949848                           *
.*           Recompiled for ABFPTINV - DRG List used in for COVID Adjustor    *
.* V11.04.06 11/09/2024  J.Tan          TSK 0947315                           *
.*           Recompiled for ABFPTINV - fixed Total Unqualified                *
.* V11.04.05 07/08/2024  J.Tan            TSK 0939432                         *
.*           Recompiled for ISBITEMS - zero Item quantity (PTCNISBZ)          *
.* V11.04.04 01/08/2024  J.Tan          TSK 0947804                           *
.*           Recompiled for ABFMHINV - Changes for Discharges after 1/7/2023  *
.* V11.04.03 17/07/2024  J.Tan          TSK 0947315                           *
.*           Recompiled for ABFPTINV (LOS ICU adjusted & Newborn)             *
.* V11.04.02 14/03/2024  J.Tan          TSK 0910994                           *
.*           Recompiled for CMXMATRX                                          *
.* V11.04.01 29/01/2024  Jacob Jackson  TSK 0919335                           *
.*           Add HF History includes and new variable for GETTFEES            *
.* V11.02.02 29/06/2022 J.Tan           TSK 0920712                           *
.*           Recompiled for ABFAECCL - I*D on emrvcdaf file                   *
.* V11.02.01 03/03/2022  J.Tan           TSK 0902652                          *
.*           Recompiled for OUTCATBI/AAECATBI - Pati.Invoice new functionality*
.* V11.01.08 17/11/2021  J.Tan          TSK 0913621                           *
.*           Recompiled for ABFAEINV - Mod for Multiple NEP Values            *
.* V11.01.07 28/10/2021 J.Tan           TSK 0913056                           *
.*           Recompiled for ABFAECCL-ADMFLAG and CCAR0000(Clinical Care)      *
.* V11.01.06 29/09/2021  J.Tan          TSK 0901515                           *
.*           Recompiled for CMXMATRX - Added Primary Proc.to Matrix           *
.* V11.01.05 14/09/2021  J.Tan          TSK 0906222                           *
.*           Recompiled for ABFPTINV - Mod for Multiple NEP Values            *
.* V11.01.04 09/08/2021  J.Tan           TSK 0905305                          *
.*           Recompiled for AAECATBI - AE Care class for 2021/2022(ABFAECCL)  *
.* V11.01.03 07/07/2021  J.Tan           TSK 0908588                          *
.*           Recompiled for PATCATBI -Exclude Unqualified days for Casepayment*
.* V11.01.02 28/04/2021  Thanh T         TSK 0895165                          *
.*           Recompiled for OPRARDFD changes                                  *
.*           07/04/2021  Tracey Nguyen   TSK 0901515                          *
.*           Recompiled for PMSCMTFD - Added new fields                       *
.*           a) 1 File type for ICD procedure (PMCCIPF)                       *
.*           b) 3 new CMBS Field Type (PMCCFTS4/PMCCFT5C/PMCCFT6C)            *
.*           c) 3 new CMBS Codes From (PMCCPC4F,PMCCSC5F,PMCCTC6F)            *
.*           d) 3 new CMBS Codes To   (PMCCPC4T/PMCCSC5T,PMCCTC6T)            *
.*           27/04/2021  J.Tan           TSK 0863287                          *
.*           Recompiled for PATCATBI - Patient Invoice new functionality      *
.* V11.01.01 10/03/2021  Tracey Nguyen   TSK 0901515                          *
.*           Recompiled for PMSCMTFD - Added Primary ICD Procedure 1-5        *
.*           (PMCCICP1-5)                                                     *
.*----------------------------------------------------------------------------*
.*        V11.00.04 10/11/2020  J.Tan           TSK 0899562                   *
.*                  Recompiled for CMXMATRX - setup date for reading MBS item *
.*        V11.00.03 29/09/2020  J.Tan          TSK 0896829                    *
.*                  Recompiled for CMXMATRX - use National(PTPGNDRG)DRG       *
.*        V11.00.02 22/09/2020  J.Tan           TSK 0895330                   *
.*                  Recompiled for CMXMATRX - reposition read of Matrix table *
.*        V11.00.01 19/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.15.01 11/10/2019  Peter Vela       TSK 0882906                  *
.*                  Recompiled for OUTCATBI - Fixed I*C for OUTBB1 ABF        *
.*        V10.14.01 09/09/2019  J.Tan           TSK 0857392                   *
.*                  Recompiled for ABF invoice                                *
.*        V10.13.03 11/01/2019  Peter Vela     TSK 0867807                    *
.*                  Recompiled for OUTCATBI                                   *
.*        V10.13.02 26/10/2018  Thanh T        TSK 0859743                    *
.*                  Added BULKBILL field and removed OUTHEDFD/OUTHEDIO,       *
.*                  OUTSESFD/OUTSESIO for OUTCATBI changes                    *
.*        V10.13.01 25/10/2018  Thanh T        TSK 0859743                    *
.*                  Added OUTHEDFD/OUTHEDIO, OUTSESFD/OUTSESIO for OUTCATBI   *
.*                  changes                                                   *
.*----------------------------------------------------------------------------*
.*        V10.12.01 18/07/2018  J.Tan          TSK 0859911                    *
.*                  Recompiled for PATCATBI - Fixed No Payday Add on charge   *
.*        V10.11.01 07/09/2017  J.Tan         TSK 0837479                     *
.*                  Recompiled for CMXMATRX - Consumption type items          *
.*        V10.10.02 13/06/2017  J.Tan          TSK 0839991                    *
.*                  Recompiled for PATDBTYP (PATCMSTP)                        *
.*        V10.10.01 25/01/2017  J.Tan          TSK 0832407                    *
.*                  Recompiled for PATCATBI- multi bed trans on sameday for MV*
.*                  04/04/2017  J.Tan         TSK 0297904                     *
.*                  Recompiled for PATCMSTP - Non-Accumulative inlier ICU/CCU *
.*        V10.08.08 06/10/2016  J.Tan         TSK 0826824                     *
.*                  Recompiled for CALCMVHR - MV Hours                        *
.*        V10.08.07 21/09/2016  J.Tan         TSK 0826370                     *
.*                  Recompiled for CMXMATRX - Chgd MCHGARR MCHGAMT array to F3*
.*        V10.08.06 26/08/2016  Don Nguyen    TSK 0312570                     *
.*                  Recompiled for CMXMATRX - Added check for PTCNDGED=1      *
.*                  Added PMSEDGFD/IO and GETEFDRG                            *
.*        V10.08.05 17/08/2016  J.Tan         TSK 0808745                     *
.*                  Recompiled for PATCATBI - for NPDAYFLG=1                  *
.*        V10.08.04 20/07/2016  J.Tan         TSK 0819914                     *
.*                  Recompiled for PATCATBI - Non ICU MV Hours                *
.*        V10.08.03 15/07/2016  J.Tan         TSK 0819914                     *
.*                  Recompiled for PATCATBI - Calculating non ICU MV hours    *
.*        V10.08.02 20/06/2016  J.Tan         TSK 0820816                     *
.*                  Recompiled for PATCATBI - Fixed No payday for Daycase     *
.*        V10.08.01 02/06/2016  J.Tan         TSK 0816651                     *
.*                  Recompiled for PATCATBI -Continuous Bill.to chk for Matrix*
.*        V10.07.02 04/03/2016  J.Tan         CAR 290448                      *
.*                  Recompiled for PATCATBI - Mods for Addon with No Pay DAy  *
.*        V10.07.01 18/11/2015  J.Tan         CAR 303942                      *
.*                  Added new payment types                                   *
.*        V10.06.02 15/07/2015  J.Tan          CAR 319342                     *
.*                  Recompiled for CMXMATRX - Chg 'Disc.UDF' to 'Care Class'  *
.*        V10.06.01 14/07/2015  Peter Vela     CAR 318700                     *
.*                  Recompiled for PRBILCMN - Validate for deleted VISMDT     *
.*                  record                                                    *
.*        V10.05.01  14/01/2015 J.Tan           CAR 309722                    *
.*                   Fixed breakdown column for patient with NO pmspbraf rec. *
.*        V10.04.07  03/07/2014 J.Tan           CAR 268860                    *
.*                   Recompiled for PATCATBI - ICD10 Suggested Classification *
.*        V10.04.06 19/06/2014 J.Tan           CAR 302452                     *
.*                  Recompiled for PATDINVS - Changed MCHGARR/MCHGAMT         *
.*        V10.04.05 16/06/2014 Steve Armstrong  CAR 301639                    *
.*                  Added call to TFILENAM for FNAMEU to replace use of       *
.*                  hard-coded "tmpideaf".                                    *
.*                  Added call to KILL0000 on exit of program.                *
.*                  Renamed TMPADT83 with FNAMEV.                             *
.*                  Renamed FNAMEX in local code with FNAMEU as FNAMEX is     *
.*                  already used for a different temp file in PATCATBI.       *
.*                  Moved calls to TEMPFILE out of CREA0000 into INIT0000     *
.*                  (FNAMEI, FNAMET, FNAMEU & FNAMEV).                        *
.*        V10.04.04 11/06/2014 Sandra Barcham CAR 301639                      *
.*                  Fixed all calls to TFILENAM                               *
.*        V10.04.03 11/06/2014 Sandra Barcham CAR 301639                      *
.*                  Moved call to TFILENAM to INIT0000                        *
.*        V10.04.02 28/04/2014 J.Tan  CAR 298297                              *
.*                  Fixed default payment for split invoice                   *
.*        V10.04.01 13/03/2014 J.Tan  CAR 297139                              *
.*                  Mods to update to be invoiced items                       *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCOMM/INC
          INC       PABXVARS/INC
.
          INC       PMSPYRFD/INC
          INC       PMSPBRFD/INC
.
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEDTRFD/INC
          INC       IBASECFD/INC
          INC       IBAPASFD/INC
          INC       IBAGEDFD/INC
          INC       IBAGSTFD/INC
          INC       IBACONFD/INC
          INC       IBATMDFD/INC
          INC       IBATMHFD/INC
          INC       NHIMASFD/INC
.
          INC       MLTCFNFD/INC
          INC       NZPBFEFD/INC
          INC       NZPCFNFD/INC
          INC       NZPCMTFD/INC
          INC       NZPEXTFD/INC
          INC       NZPRDTFD/INC
          INC       NZPFIGFD/INC
          INC       NZPFINFD/INC
          INC       NZPIBRFD/INC
          INC       NZPMCHFD/INC
          INC       NZPMXCFD/INC
          INC       NZPPFEFD/INC
          INC       NZPRBRFD/INC
          INC       NZPRFNFD/INC
          INC       NZPPICFD/INC
          INC       NZPSPRFD/INC
          INC       NZPTFEFD/INC
          INC       NZPRVBFD/INC
          INC       NZPIVBFD/INC
          INC       OPRARDFD/INC
          INC       OPRDEAFD/INC
          INC       OPRSESFD/INC
.
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
          INC       PATELGFD/INC
          INC       PATBFEFD/INC
          INC       PATCFAFD/INC
          INC       PMSCIDFD/INC
          INC       PMSCCDFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATCTFFD/INC
          INC       PATDHEAD/INC
          INC       PATDINVS/INC
          INC       PATDGWFD/INC
          INC       PATDO1FD/INC
          INC       PATDRGFD/INC
          INC       PATDSCFD/INC
          INC       PATDTRFD/INC
          INC       PATECDFD/INC
          INC       PATECOFD/INC
          INC       PATFINFD/INC
          INC       PATFMOFD/INC
          INC       PATFX1FD/INC
          INC       PATFN1FD/INC
          INC       PATFN2FD/INC
          INC       PATGTUFD/INC
          INC       PATHSPFD/INC
          INC       PATHTRFD/INC
          INC       EMRHTRFD/INC
          INC       OUTHTRFD/INC
          INC       PATIBLFD/INC
          INC       PATICUFD/INC
          INC       PATIN1FD/INC
          INC       PATINMFD/INC
          INC       PATINVFD/INC
          INC       PATIOVFD/INC
          INC       PATIPEFD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC
          INC       PATMCHFD/INC
          INC       PATMI1FD/INC
          INC       PATOVBFD/INC
          INC       PATPGRFD/INC
          INC       PATRE1FD/INC
          INC       PATRBLFD/INC
          INC       PATRATFD/INC
          INC       PATRCAUD/INC
          INC       PATRFNFD/INC
          INC       PATSELDT/INC
          INC       PATSRBFD/INC
          INC       PATTFEFD/INC
          INC       PATTRNFD/INC
          INC       PATVENFD/INC
          INC       PATVISFD/INC
          INC       PATWC1FD/INC
          INC       PATWMAFD/INC
          INC       PATWR1FD/INC
          INC       PATWVEFD/INC
          INC       PMSHATFD/INC
          INC       PMSEVTFD/INC
          INC       PMSCNOFD/INC
          INC       PMSHCPFD/INC
          INC       PMSSINFD/INC
          INC       PATCMTFD/INC
          INC       PMSCMTFD/INC
          INC       PMSSGAFD/INC
          INC       PMSMTIFD/INC
          INC       PMSMPRFD/INC
          INC       PMSVX1FD/INC
          INC       PATRGMFD/INC
          INC       PMSREGFD/INC
.
          INC       PATCMXFD/INC
          INC       PATAFEFD/INC
          INC       PATASDFD/INC
          INC       PATHDFFD/INC
          INC       PATHLFFD/INC
          INC       PATLDFFD/INC
          INC       PATLSDFD/INC
          INC       PRIITMFD/INC
.
          INC       EMRSITFD/INC
          INC       EMRHISFD/INC
          INC       EMRVISFD/INC
          INC       COMDEPFD/INC
          INC       RCPBNKFD/INC
          INC       RCPDTEFD/INC
          INC       RCPBDTFD/INC
          INC       RCPCONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       PMSEDGFD/INC       * Effective DRG Dates
          INC       PATDDHFD/INC
.
.Data File Definition
.--------------------
PMSIDEA1   IFILE    SQL, FIXED=276, KEY="U1-8,9-11,12-19,20-25"
PMSIDEA2   IFILE    SQL, FIXED=276, KEY="U1-8,12-19,90-90,9-11,20-25"
PMSIDEA3   IFILE    SQL, FIXED=276, KEY="U1-8,12-19,26-26,27-34,9-11,20-25"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
PMIDADMN  DIM       8      8        1     Admission Number
PMIDCLTY  DIM       3      3        9     Claim Type (Cat CL) - Payer
PMIDINVN  DIM       8      8        12    Invoice No (blank if not invoiced)
PMIDTRNO  DIM       6      6        20    Transaction No.
PMIDRTYP  DIM       1      1        26    Record Type 1=Accomodation,
.                                         2=Theatre Item  3=Misc.Item
PMIDFDAT  DIM       8      8        27    From Date (ccyymmdd) only for Acc.
PMIDTDAT  DIM       8      8        35    To Date (cccyymmdd) only for Accom.
PMIDNDAY  FORM      4      3        43    Days of Accommodation
PMIDDESC  DIM       35     35       46    Accommodation Description
PMIDITEM  DIM       9      9        81    Item Code
DPMIDDEF  DIM       1      1        90    Default 0=No,1=Yes
PMIDREGM  DIM       10     10       91    Regime Code
PMIDAMNT  FORM      12.2   8        101   Amount
PMIDGSTM  FORM      12.2   8        109   GST Amount
PMIDSPLT  DIM       1      1        117   Split Invoice
PMIDCNID  DIM       6      6        118   Contract ID
PMIDCUID  DIM       10     10       124   WEB User Created (websecaf)
PMIDCDAT  DIM       8      8        134   Date Record Created (ccyymmdd)
PMIDCTIM  DIM       8      8        142   Time Record Created (hh:mm:ss)
PMIDUUID  DIM       10     10       150   WEB User Updated (websecaf)
PMIDUDAT  DIM       8      8        160   Date Record Updated (ccyymmdd)
PMIDUTIM  DIM       8      8        168   Time Record Updated (hh:mm:ss)
PMIDITDT  DIM       8      8        176   Invoice todate (ccyymmdd)
PMIDSPAR  DIM       92     92       184   Spare
.
.End of Record                      276
.
.Redefine Key Variables
.----------------------
PMIDDEFT  FORM      1
.
. ******  VARIABLES DEFINITIONS  ******
.
ADNAME    DIM       9 
AVPAT     FORM      12.2
ASDATE    DIM       8
.
ADAY      DIM       2
AMON      DIM       2
AYER      DIM       2
ACEN      DIM       2
ACCDTE    DIM       8     * accommodation date used in AAECATBI
AECNFTYP  FORM      1
AECNCHRS  FORM      1
.
COUNTER   FORM      6
CDYSDAYS  FORM      6                       * CALCDAYS number of days
CDYSFDTE  DIM       8                       * CALCDAYS from date
CDYSTDTE  DIM       8                       * CALCDAYS to date
CDYSYEAR  FORM      2                       * CALCDAYS year
CERTINDC  FORM      1
BULKBILL  DIM       1
CODE      DIM       2
CMDLINE   DIM       120
CMDLIN1   DIM       70
CMDLIN2   DIM       16
CURSESS   DIM       18
CURRDATE  DIM       8
CUTDAY    DIM       2
CUTMON    DIM       2
CUTYER    DIM       2
CUTCEN    DIM       2
CALDAYS   FORM      3
CATCR     INIT      "cr"
CODECL    INIT      "CL"
CODEDP    INIT      "DP"
CODEA     INIT      "A "
CODEBT    INIT      "BT"
.
DEFPFLAG  FORM      1
DB        INIT      "DB"
DDDAY     DIM       2
DDMON     DIM       2
DDYER     DIM       2
DDCEN     DIM       2
DIM1A     DIM       1
*DIM3      DIM       3
DIM4      DIM       4
DIM5A     DIM     5
DIM9      DIM       9
DIM11     DIM       11
DIM13     DIM       13
DIM14     DIM     14
DIM18     DIM       18
DIM18A    DIM       18
DIM19A    DIM       19
DIM24A    DIM       24
DIM30     DIM       30
DOCTCODE  DIM       6
DOCFNAME  DIM       50
.
EFTDATE   DIM       8         * Effective date (ccyymmdd)
EMCNGINV  DIM       1
EMCNDPOC  DIM       1
EMCNOPOC  DIM       15
.
FORM3A    FORM      3
FIVE9     FORM      "59"
FMTGNAME  DIM       35
FMTTITLE  DIM       10
FMTSNAME  DIM       35
.
HOSPID    DIM       3
HOSPNAME  DIM       50
HINVDES   DIM       30
HESYST    FORM      1
HFHIFUND  FORM      2
.
JYEAR     FORM      2
KEYFLAG   FORM      1
KEY13A    DIM       13
.
MBSFLAG   FORM      1
PASS      FORM      1
OPCNCONS  DIM       1
OPOCFLAG  FORM      1
OPNFLAG   FORM      "0"      * Used in stepdown
OPRDATE   DIM       8
OTCNCFEE  FORM      1
OTCNFTYP  FORM      1
LOSDAYS   FORM      5
.
RECCOUNT  FORM      5
SAVKEY26  DIM       26
SAVAMNT   FORM      12.2
SAVALLW   FORM      12.2
SAVBEND   FORM      3
SAVDISC   FORM      12.2
SAVEXTRA  FORM      12.2
SP12      INIT      "            "
SP14      INIT      "              "
SUROPT    FORM      1
SERVDOCT  DIM       10
.
TOTFEES   FORM      12.2
TFEEIND   FORM      1
THCFLG    FORM      2
THREED    INIT      "3"
TTBILL    FORM      12.2
TTDAYS    FORM      8
TTNETT    FORM      12.2
UNKGST    FORM      1
WDATE1    DIM       8
WDATE2    DIM       8
WBSEUID   DIM       10
ZERO4     INIT      "0000"
.
DISCONLY  FORM      1     * discharged only 1=no 0=yes
.
.         Web Variables (only the include files use these variables)
.
DAYFORMT  DIM       4
NJAN      INIT      "January"
NFEB      INIT      "February"
NMAR      INIT      "March"
NAPR      INIT      "April"
NMAY      INIT      "May"
NJUN      INIT      "June"
NJUL      INIT      "July"
NAUG      INIT      "August"
NSEP      INIT      "September"
NOCT      INIT      "October"
NNOV      INIT      "November"
NDEC      INIT      "December"
.
HTMLFILE  FIFO      ASCII, FIXED=250
MTHNAM3   DIM       3
MTHNAM3A  DIM       3

DAYCASE   FORM      1
.
FNAMER    DIM       8
FNAMEI    DIM       8
FNAMET    DIM       8
FNAMEU    DIM       8
FNAMEV    DIM       8
.
PATNUM    FORM      6
PATNAM    DIM       14
.
PNAM1     DIM       1
PNAM2     DIM       1
PHEADFL   FORM      1
.
SRBDAY    DIM       4
SRYEAR    FORM      4
SEC       FORM      2
SHTABLE   DIM       8
STATUS    FORM      1
.
TDAYNO    FORM      4
.
UNIQUE    FORM      8
WDATE     DIM       8
WSTAT     FORM      1
.
XBED      DIM       3
XDATE1    DIM       8                       * Yesterday date DDMMYY
XMON2     DIM       2
XDAY2     DIM       2
XWARD     DIM       3
XYEAR2    DIM       2
XCENT2    DIM       2
YTDATE    DIM       8
HOSNAME   DIM       50
HOSCODE   DIM       3
.
CFPAYCOD  DIM       3        * 3M Codefinder PAY Code
.
. ******  Temporary index file for report  ******
.
TEMPFILE  IFILE     SQL, FIXED=109, KEY="u1-28"
UKEY1     INIT      " 109 U1-28"
.
.Name     Type      Size    Physical    Start
.----     ----      ----    --------    -----
.XWARD    DIM        3         3          1
.XBED     DIM        3         3          4
.PATNAM   DIM        14        14         7
XUNIQUE   DIM        8         8         21
.PGNAME   DIM        25        25        29
.PSEX     DIM        1         1         54
.PSAGE    FORM       3         3         55
.ADOCTA   DIM        6         6         58
.AFUNDH   DIM        6         6         64
.AFNDTB   DIM        8         8         70
.ADATE    DIM        8         8         78
.ACLAIM   DIM        3         3         86
.ATYPE    DIM        3         3         89
.DAYCASE  FORM       1         2         92
.TDAYNO   FORM       4         3         94
.STRBTYP  DIM        3         3         97
XAADMNO   DIM        8         8        100
INVCFLAG  DIM        1         1        108
.
.End of Record                          109
.
. ******  Temporary index file for report  ******
.
TMSRFILE  IFILE     SQL, FIXED=28, KEY="u1-9"
SKEY1     INIT      " 28 U1-9"
.
.Name     Type      Size    Physical    Start
.----     ----      ----    --------    -----
SITEMC    DIM        9         9          1
SIAMNT    FORM       12.2      8         10
SSPARE    DIM        10        10        18
.End of Record                           28
.
XXSRFILE  IFILE     SQL, FIXED=24, KEY="u1-9,10-17"
XKEY1     INIT      " 24 U1-9,10-17"
.
.Name     Type      Size    Physical    Start
.----     ----      ----    --------    -----
XITEMC    DIM        9         9          1
XVISTN    DIM        8         8         10
XSPARE    DIM        6         6         18
.End of Record                           24
.
. ******  CONSTANTS  ******
.
ASK1      INIT      "*"
ASK2      INIT      "**"
ASK3      INIT      "***"
ASK4      INIT      "****"
CODEPY    INIT      "Py"
.
.------------------------------------------------------------------------
PRGID     INIT      "IBAADT83"
PRGNAM    INIT      "HEALTH FUND WARD INCOME REPORT - COMMUNITY BILLING"
VERSION   INIT      "V12.01.00"
.------------------------------------------------------------------------
+
.****************************************************************************
.*                            MAIN0000                                      *
.*                      Controlling Logic (Mainline code)                   *
.****************************************************************************
MAIN0000  CALL      INIT0000
          MOVE      ZERO,OPTION
          MOVE      ZERO,PHEADFL 
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Ward/Bed Sequence";
.
MAIN1000  KEYIN     *P1:8,*EF,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    OPTION,MAIN1200
          BEEP
          GOTO      MAIN0000
.
MAIN1200  CALL      KHOS0000            * keyin hospital
          BRANCH    EXIT,MAIN0000
.
          CALL      CONTQST          * Ok to continue ?
          BRANCH    CEXIT,MAIN3000,MAIN1200,MAIN9999
          BEEP
          GOTO      MAIN1200
.
MAIN3000  CALL      STPR0000         * Start processing
          CALL      PRPT0000         * Report
          GOTO      MAIN1000
.
MAIN9999  CALL      KILL0000         * remove temp files
          CHAIN     PGM
          STOP
+
.****************************************************************************
.         Display screen header and open files
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patbfeef";
          OPEN      PATBFEE1,"patbfeef"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
          OPEN      PATDTRA3,"patdtraf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
          OPEN      PMSMTIA2,"pmsmtiaf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pmspbraf"
          OPEN      PMSPBRA1,"pmspbraf"
          OPEN      PMSPBRA4,"pmspbraf"
          OPEN      PATRGMA1,"patrgmaf"
          OPEN      PMSREGA1,"pmsregaf"
          OPEN      PMSPYRA2,"pmspyraf"
.
.         OPEN      MLTCFNA1,"mltcfnaf"
.         OPEN      MLTCFNA2,"mltcfnaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATMCHG1,"patmchgf"
          OPEN      PATVISA1,"patvisaf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*217,CAUDQ
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,EIGHTY8;*233,PTCNADES,*248,PTCNISBZ
.
          PACK      CURRDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
...  used in ABFAECCL
          CALL      TFILENAM                * Get file name calculating Exponent
          MOVE      TFILNAME,FNAMEN
.
...  used in PATCATBI CRMV0000
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEX
.
...  used in PATCATBI PTMP0000
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEZ
.
...  used in PATCATBI PT2P0000
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEY
.
...  used in PABXBILL
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PBXFNAME
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PBXFNAM2
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEV
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEI
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEU
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMET
.
INIT9999  RETURN
+
.****************************************************************************
.            PROCESS START
.****************************************************************************
STPR0000  CALL      CREA0000
          CLOCK     TIME,CTIMEIS
          CALL      SETY0000           * Set up yesterday date
.
          MOVE      ONE,UNIQUE
          MOVE      SP8,DDATE
          MOVE      ZERO,DAYCASE
          MOVE      ZERO,CPAGENO
          MOVE      " ",ANS
.
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Processing Current Admissions **",*HOFF:
                    *P1:24,*EL,*P10:24,"Current Admission No :":
                               *P50:24,"Scanning :";
          MOVE      "2",WSTAT
          CALL      ADFN0000
.
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Processing On-Leave Admissions **",*HOFF:
                    *P1:24,*EL,*P10:24,"On-leave Admission No :":
                               *P50:24,"Scanning :";
          MOVE      "4",WSTAT
          CALL      ADFN0000
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "** Processing Discharge Admissions **",*HOFF:
                    *P1:24,*EL,*P10:24,"Discharged Admission No :":
                               *P50:24,"Scanning :";
.
          MOVE      "3",WSTAT
          MOVE      "000000" TO DDATE
.
.         Get the patients discharged from yesterday
.
          PACK      KEY16 WITH YTDATE,SP8
          CALL      RDSDSCH2
STPR1000  CALL      RDKDSCH2
          BRANCH    OVRCD OF STPR9999
.
          DISPLAY   *P62:24,DADMNO;
          MOVE      ZERO,DAYCASE
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE            * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,DADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      STPR1000 IF NOT EQUAL
              ELSE
                GOTO      STPR1000
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF STPR9999
.
.         Check for the Admission Date
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF STPR1000
          CALL      RDPMVX11
          BRANCH    OVRCD OF STPR1000
.
          MATCH     DDATE,YTDATE
          GOTO      STPR2000 IF NOT EQUAL
.
          MATCH     ADATE,YTDATE            * patient was discharged yesterday
          GOTO      STPR1000 IF NOT EQUAL
.
.         MOVE      ONE,DAYCASE             * It is a daycase - Patient
          GOTO      STPR3000                * discharged & admitted yesterday
.
.         Not discharged yesterday, so it must be today
.
STPR2000  UNPACK    ADATE WITH XCENT,XYEAR,XMON,XDAY
          PACK      XDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",XDATE
.
          MATCH     CURRDATE,XDATE          * Check if the patient was admitted
          GOTO      STPR1000 IF NOT LESS    * before today
.
STPR3000  CALL      CTRN0000                * Check transfer records
          GOTO      STPR1000
.
STPR9999  RETURN
+
.****************************************************************************
.             READ ADMISSIONS FILE
.****************************************************************************
ADFN0000  PACK      KEY9 WITH WSTAT,SP8
          CALL      RDSMISS2
.
ADFN1000  CALL      RDKMISS2
          BRANCH    OVRCD,ADFN9999
.
          COMPARE   WSTAT,ASTAT
          GOTO      ADFN9999 IF NOT EQUAL
.
.   MATCH     "4638280",AADMNO
.   GOTO      ADFN1100  IF EQUAL
.   MATCH     "4639398",AADMNO
.   GOTO      ADFN1000  IF NOT EQUAL
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF ADFN1000
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE            * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,AADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      ADFN1000 IF NOT EQUAL
              ELSE
                GOTO      ADFN1000
              ENDIF
            ENDIF
          ENDIF

          MOVE      AADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD OF ADFN1000
.
          DISPLAY   *P62:24,AADMNO;
          UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      WDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",WDATE
.
          MATCH     CURRDATE,WDATE
          GOTO      ADFN1000 IF NOT LESS
          MOVE      ZERO,DAYCASE
          CALL      CTRN0000                  * Check transfer records
          GOTO      ADFN1000
.
ADFN9999  RETURN
+
.****************************************************************************
.         Subroutine to process transfer for the patient
.****************************************************************************
CTRN0000  MOVE      AADMNO,IPADMNO
          MOVE      IPADMNO,DIPADMNO
          PACK      KEY22,DIPADMNO,SP70
          CALL      RSPMPBR1
          CALL      RKPMPBR1
          BRANCH    OVRCD,CTRN1000
          MATCH     DIPADMNO,PMPBADMN          * Same admission?
          GOTO      CTRN2000 IF EQUAL
.
CTRN1000  CALL      APBR0000                   * Add payers for this admission
.
CTRN2000  CALL      AREG0000                   * Add items of regimes
          CALL      CPBR0000                   * workout payers breakdown
.
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
.
          MOVE      SP10,STATYPE
          MOVE      SP10,STRBTYP
          MOVE      SP10,FROMDATE
          MOVE      AWARD,STWARD
          MOVE      ABED,STBED
.
CTRN3000  PACK      KEY30 WITH AADMNO,CURRDATE,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD OF CTRN4000
.
          MATCH     TADMN,AADMNO
          GOTO      CTRN4000 IF NOT EQUAL
.
.         Get yesterday ward/bed
.
          MOVE      TWARD,STWARD
          MOVE      TBED,STBED
          UNPACK    TDATE,XCENT,XYEAR,XMON,XDAY
.
          MOVE      TATYPE,STATYPE
          MOVE      TRBTYP,STRBTYP
.
CTRN4000  MOVE      CURRDATE,TDATE           * Use current date
.
          MOVE      ONE,TDAYNO
.
          BRANCH    DAYCASE OF CTRN8000
.
.         If not a daycase then day number is calculated from Adate to 
.         the current/discharged date
.
          MOVE      CURRDATE,TODATE           * Use current date
          REP       " 0",TODATE
.
          REP       " 0",DDATE
          MATCH     "000000",DDATE
          IF        !@EQUAL
            MOVE      DDATE,TODATE
            REP       " 0",TODATE
          ENDIF
          MOVE      ADATE,FROMDATE          * Cal. total number of days in hosp.
          REP       " 0",FROMDATE
          CALL      NHOSPDAY
          MOVE      NBRDAYS,TDAYNO
.
CTRN8000  MOVE      STWARD,XWARD
          MOVE      STBED,XBED
.
          MOVE      SP20,PATNAM
          MOVE      PSNAME,PATNAM
          PACK      KEY28 WITH XWARD,XBED,PATNAM,UNIQUE
.
          PACK      ASDATE,CCC,CYY,CMM,CDD        * Current date
          CALL      PATAGED                       * Get the current age
.
.         Updating the temp file
.
          CALL      WRTEMP
          ADD       ONE,UNIQUE
CTRN9999  RETURN
+
.****************************************************************************
.         Add payers for this admission
.****************************************************************************
APBR0000  MOVE      SP70,PTRGREGM
          PACK      KEY10,DIPADMNO,SP70   * get regime code
          CALL      RSPTRGM1
          CALL      RKPTRGM1
          BRANCH    OVRCD,APBR9999
          MATCH     DIPADMNO,PTRGADMN
          GOTO      APBR9999 IF NOT EQUAL
          MATCH     SP70,PTRGREGM
          GOTO      APBR9999 IF EQUAL
.
.         Get payers for this admission
.
          MOVE      ZERO,DEFPFLAG          * Default payer flag
          PACK      KEY19,AURNO,SP70
          CALL      RSPMPYR2
APBR1000  CALL      RKPMPYR2
          BRANCH    OVRCD,APBR9999
.
          MATCH     AURNO,PMPYURNO
          GOTO      APBR9999 IF NOT EQUAL
.
.         if Payer claim type equals to Patient Claim type, the it must be a
.         Default payer
.
          MATCH     ACLAIM,PMPYCLTY
          IF        @EQUAL
            MATCH     "1",PMPYDEFT
            GOTO      APBR1000 IF NOT EQUAL      * Not a default payer
          ENDIF
.
          MATCH     "1",PMPYDEFT
          IF        @EQUAL
            BRANCH    DEFPFLAG,APBR1000          * Default payer exists already
            MOVE      ONE,DEFPFLAG
          ENDIF
.
.         Payers with effective for the Visit
.
          MATCH     PMPYEFDT,ALACDTE
          GOTO      APBR1000 IF LESS
.
          MATCH     SP70,PMPYETDT
          GOTO      APBR2000 IF EQUAL            * Blank Effective to date
.
          MATCH     ALACDTE,PMPYETDT
          GOTO      APBR1000 IF LESS
.
.         Check if breakdown exist for the payer
.
APBR2000  MOVE      "  0",PMPBICNT
          PACK      KEY22,AADMNO,PMPYCLTY,PMPYEFDT,PMPBICNT,SP70
          CALL      RDPMPBR1
          COMPARE   ZERO,OVRCD
          GOTO      APBR4000 IF EQUAL
.
          CALL      CLPMSPBR
          UNPACK    KEY22,PMPBADMN,PMPBCLTY,PMPBEFDT,PMPBICNT
          MOVE      "ACCOMCHRG",PMPBITEM
          MOVE      PMPYDEFT,PMPBDEFT
          MOVE      PMPYETDT,PMPBETDT      * Effective to Date
          MOVE      PTRGREGM,PMPBREGM
          MOVE      ZERO,PMPBMAXB
          MOVE      ZERO,PMPBMAXD
          MOVE      WBSEUID,PMPBCUID
          CALL      IBACLOCK
          PACK      PMPBCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMPBCDAT
          MOVE      CTIMEIS,PMPBCTIM
.
          CALL      WRPMPBR1
.
APBR4000  PACK      KEY13,PTRGREGM,SP1,SP1,ZERO,SP70
          CALL      RDPMREG1
          BRANCH    OVRCD,APBR1000        * next payer
.
.         Loop through all Misc.charges of the Regime
.
APBR6000  CALL      RKPMREG1
          BRANCH    OVRCD,APBR1000
.
          MATCH     PMREREGM,PTRGREGM      * Same regiem ?
          GOTO      APBR1000 IF NOT EQUAL
.
          MATCH     "1",PMREITYP
          GOTO      APBR6000 IF NOT EQUAL  * Not Misc.item
          MATCH     "0",PMREACTV
          GOTO      APBR6000 IF NOT EQUAL  * Not active
.
          CALL      CIBR0000               * Check if item exist
          BRANCH    EXIT,APBR6000
.
          CALL      CLPMSPBR
.
          MOVE      AADMNO,PMPBADMN
          MOVE      PMPYCLTY,PMPBCLTY
          MOVE      PMPYEFDT,PMPBEFDT
.
          MOVE      FORM3,PMPBICNT         * Item Count
          MOVE      PMREITMN,PMPBITEM      * Item code
          MOVE      PMPYDEFT,PMPBDEFT
          MOVE      PMPYETDT,PMPBETDT      * Effective to Date
          MOVE      PTRGREGM,PMPBREGM
          MOVE      ZERO,PMPBMAXB
          MOVE      ZERO,PMPBMAXD
          MOVE      WBSEUID,PMPBCUID
.
          CALL      WRPMPBR1
          GOTO      APBR6000
.
APBR9999  RETURN
+
.------------------------------------------------------------
.         Check if item in the Payer breakdown already
.------------------------------------------------------------
CIBR0000  MOVE      ZERO,EXIT
          PACK      KEY22,AADMNO,PMPYCLTY,PMPYEFDT,SP70
          CALL      RSPMPBR1
CIBR1000  CALL      RKPMPBR1
          BRANCH    OVRCD,CIBR2000
.
          PACK      KEY19,PMPBADMN,PMPBCLTY,PMPBEFDT,SP70
          MATCH     KEY19,KEY22
          GOTO      CIBR2000 IF NOT EQUAL
          MATCH     "  0",PMPBICNT
          GOTO      CIBR1000 IF EQUAL      * Accommodation record
.
          MATCH     PMREITMN,PMPBITEM
          GOTO      CIBR1000 IF NOT EQUAL
.
          MOVE      ONE,EXIT
          GOTO      CIBR9999
.
CIBR2000  MOVE      ZERO,FORM3
          PACK      KEY22,AADMNO,PMPYCLTY,PMPYEFDT,Z70
          CALL      RSPMPBR1
          CALL      RPPMPBR1
          BRANCH    OVRCD,CIBR8000
.
          PACK      KEY19,PMPBADMN,PMPBCLTY,PMPBEFDT,SP70
          MATCH     KEY19,KEY22
          GOTO      CIBR8000 IF NOT EQUAL
          MOVE      PMPBICNT,FORM3
.
CIBR8000  ADD       ONE,FORM3
          MOVE      ZERO,EXIT
CIBR9999  RETURN
+
.****************************************************************************
.         Workout payers breakdown
.****************************************************************************
CPBR0000  MOVE      ZERO,ISBLFLAG
          OPEN      PMSIDEA2,"pmsideaf"
          CALL      IMID0000       * Check if invoice raised as at midnight
.
.         If invoice has been raised as as mid night, we won't have to
.         calculate the tobe invoiced to get the daily charge
.
          MATCH     "1",INVCFLAG
          GOTO      CPBR9000 IF EQUAL   * Invoice raised
.
          MOVE      ALACDTE,FROMDATE
          MOVE      CURRDATE,TODATE
          REP       " 0",TODATE
          IF        ASTAT=3
            CALL      RDDSCH1
            IF        OVRCD=0
              MOVE      DDATE,TODATE
            ENDIF
          ENDIF
          MOVE      ZERO,LOSDAYS
          DAYSEP    FROMDATE,TODATE,LOSDAYS * LOS include leaves
.
          CLOSE     PMSIDEA2
.
          OPEN      PMSIDEA1,FNAMET
          OPEN      PMSIDEA2,FNAMET
          OPEN      PMSIDEA3,FNAMET
.
.         Calculate to be invoiced amount as at Mid night
.
          MOVE      ZERO,DEFPAYCL
          PACK      KEY26,DAADMNO,SP70
          CALL      RSPMPBR4
CPBR1000  CALL      RKPMPBR4
          BRANCH    OVRCD,CPBR6000
.
          MATCH     DAADMNO,PMPBADMN       * Same admission no?
          GOTO      CPBR6000 IF NOT EQUAL
          COMPARE   ZERO,PMPBMAXB
          GOTO      CPBR1000 IF EQUAL      * No maximum billing for Accom.
.
          MATCH     "  0",PMPBICNT
          GOTO      CPBR2000 IF NOT EQUAL  * not accommodation record
.
.         Accommodation from a payer
.
          PACK      SAVKEY26,PMPBADMN,PMPBICNT,PMPBDEFT,PMPBCNTR:
                             PMPBCLTY,PMPBEFDT,SP70
          MOVE      PMPBCLTY,SAVACLM
          CALL      FACM0000               * Accom.start/end date for this payer
.
          MOVE      SAVKEY26,KEY26
          CALL      RDPMPBR4               * Reposition
.
          MOVE      ONE,ISBLFLAG
          MOVE      SEVEN,PROGFLAG         * a payer 'tobe invoiced' accom.chrge
          CALL      ISAC0000
.
          GOTO      CPBR1000               * next payer
.
CPBR2000  CALL      UPIT0000               * Updating Items for the payer
          GOTO      CPBR1000               * next payer
.
CPBR6000  IF        ISBLFLAG=0
            CALL      FACM0000             * Accom.start/end date for this payer
          ENDIF
          CALL      CDEF0000               * Check default payer
          CLOSE     PMSIDEA1
          CLOSE     PMSIDEA3
.
CPBR9000  CLOSE     PMSIDEA1
          CLOSE     PMSIDEA2
          CLOSE     PMSIDEA3
.
CPBR9999  RETURN
+
.****************************************************************************
.         Check if invoice raised as at midnight
.****************************************************************************
IMID0000  MOVE      "0",INVCFLAG
          PACK      KEY26,DAADMNO,Z70
          CALL      RSPMIDE2
IMID1000  CALL      RPPMIDE2
          BRANCH    OVRCD,IMID9999
.
          MATCH     DAADMNO,PMIDADMN
          GOTO      IMID9999 IF NOT EQUAL  * different admission
.
          MATCH     SP70,PMIDINVN
          GOTO      IMID9999 IF EQUAL      * tobe invoiced
.
          MATCH     "1",PMIDRTYP
          GOTO      IMID1000 IF NOT EQUAL  * Not accommodation
.
.         found an invoice, check range of invoice
.
          MATCH     PMIDFDAT,YTDATE
          GOTO      IMID6000 IF EQUAL
          GOTO      IMID1000 IF LESS       * invoiced already
.
          MATCH     YTDATE,PMIDTDAT
          GOTO      IMID1000 IF LESS
.
IMID6000  MOVE      "1",INVCFLAG           * invoice raised already
.
IMID9999  RETURN
+
.****************************************************************************
.         Updating Items for the payer
.****************************************************************************
UPIT0000  MOVE      ZERO,ENDFLAG
          MOVE      "3",PMMIRTYP
          PACK      KEY15,DAADMNO,PMMIRTYP,SP70
          CALL      RSPMMTI2
UPIT3000  CALL      RKPMMTI2
          BRANCH    OVRCD,UPIT9999
.
          MATCH     DAADMNO,PMMIVISN         * Same visit number?
          GOTO      UPIT9999 IF NOT EQUAL
          MATCH     "3",PMMIRTYP
          GOTO      UPIT9999 IF NOT EQUAL
.
          MATCH     "1",PMMIINCT             * Item from Regime?
          GOTO      UPIT3000 IF NOT EQUAL
.
          MATCH     PMMIITEM,PMPBITEM
          GOTO      UPIT3000 IF NOT EQUAL
.
UPIT4000  MOVE      PMMIAMTP,SAVAMNT
          ADD       PMMIRBAT,SAVAMNT        * Unit price
.
          MOVE      LOSDAYS,PMMISERV
          MOVE      PMPBMAXB,FORM12P2
          DIV       PMPBMAXD,FORM12P2
          MOVE      FORM12P2,FORM6          * no of items for max pay
          IF        FORM6<PMMISERV
            MOVE      FORM6,PMMISERV
          ENDIF
          IF        PMMISERV=0
            MOVE       ONE,PMMISERV
          ENDIF
.
          MOVE      SAVAMNT,LINETOT         * charge per day
          IF        SAVAMNT > PMPBMAXD
            MOVE      PMPBMAXD,LINETOT      * Max day charge
          ENDIF
          MULT      PMMISERV,LINETOT
          MOVE      LINETOT,FORM12P2
          IF        FORM12P2 > PMPBMAXB
            MOVE      PMPBMAXB,FORM12P2  * Max bill
          ENDIF
          MOVE      FORM12P2,LINETOT
          MOVE      ZERO,LINEGST
.
          MOVE      "3",SRECTYPE              * Item record
          MOVE      SP70,PMIDINVN
          CALL      WTDE0000
.
UPIT9999  RETURN
+
.****************************************************************************
.         Workout the 'tobe' invoiced Accom.for default payer
.****************************************************************************
CDEF0000  MOVE      SP70,DEFPAYER
          MOVE      ZERO,DEFPAYCL
          MOVE      ZERO,TOTDAYS
.
          CALL      GDPY0000          * get Def.payer to pay reminder of inv.
          MOVE      DEFPAYER,PMPBCLTY    * Default payer
          MOVE      "1",PMPBDEFT
.
.         Check for any reminder Accom.invoice amt to be paid by Default payer
.
          MOVE      ALACDTE,SFRDATE   * Accom.start date
          MOVE      ONE,PROGFLAG
          CALL      ISAC0000          * Get Contract & inv.total from all payers
          COMPARE   ZERO,ACCMTOT
          GOTO      CDEF0800 IF EQUAL
.
          CALL      REIN00000
          BRANCH    EXIT,CDEF1000     * Payer(s) paid all accommodation
.
CDEF0800  MOVE      ONE,DEFPAYCL
          MOVE      SEVEN,PROGFLAG
          MOVE      DEFPAYER,PMPBCLTY    * Default payer
          MOVE      "1",PMPBDEFT
          CALL      ISAC0000
.
.         Check for items
.
CDEF1000  MOVE      "3",PMMIRTYP
          PACK      KEY15,DAADMNO,PMMIRTYP,SP70
          CALL      RSPMMTI2
CDEF2000  CALL      RKPMMTI2
          BRANCH    OVRCD,CDEF9999
.
          MATCH     DAADMNO,PMMIVISN         * Same visit number?
          GOTO      CDEF9999 IF NOT EQUAL
          MATCH     "3",PMMIRTYP
          GOTO      CDEF9999 IF NOT EQUAL
.
          MATCH     "1",PMMIINCT
          GOTO      CDEF2000 IF NOT EQUAL     * Not from Community Billing
.
          MOVE      PMMIAMTP,PMMIAMTT
          ADD       PMMIRBAT,PMMIAMTT         * Unit price
          MULT      LOSDAYS,PMMIAMTT
.
          MOVE      ZERO,FORM6A
.
.         Check payers for this item   
.
          MOVE      PMMIITEM,KEY9
          MOVE      SP70,PMIDINVN
          MOVE      "0",DPMIDDEF
          PACK      KEY26,DAADMNO,PMIDINVN,DPMIDDEF,SP70
          CALL      RSPMIDE2
CDEF2200  CALL      RKPMIDE2
          BRANCH    OVRCD,CDEF6000
.
          PACK      KEY17,PMIDADMN,PMIDINVN,DPMIDDEF,SP70
          MATCH     KEY17,KEY26
          GOTO      CDEF6000 IF NOT EQUAL
.
          MATCH     "3",PMIDRTYP
          GOTO      CDEF2200 IF NOT EQUAL     * Not a misc.item record
          MATCH     PMMIITEM,PMIDITEM
          GOTO      CDEF2200 IF NOT EQUAL
.
.         Found a payer
.
          SUB       PMIDAMNT,PMMIAMTT
          COMPARE   ZERO,PMMIAMTT
          GOTO      CDEF2000 IF EQUAL      * payers pays all item amount
.
.         Check if the payer paying all the unit price
.
          MOVE      ZERO,LINEGST
          COMPARE   PMIDNDAY,PMMISERV
          GOTO      CDEF4000 IF NOT EQUAL
.
.         Allocate the reminder of item amount to Default payer
.
          MOVE      PMMIAMTT,LINETOT
          MOVE      ZERO,LINEGST
          MOVE      LOSDAYS,PMMISERV
          GOTO      CDEF8000
.
.         Payer pays part of the unit item amount
.
CDEF4000  MOVE      PMIDAMNT,FORM12F2
          DIV       PMIDNDAY,FORM12F2         * Unit price payer pays
.
          MOVE      PMMIAMTP,FORM12P2
          ADD       PMMIRBAT,FORM12P2         * Actual Unit price
          COMPARE   FORM12P2,FORM12F2
          GOTO      CDEF5000 IF EQUAL         * payer pays the full unit price
.
          SUB       FORM12F2,FORM12P2        * unit price amt tobe paid by payer
          MOVE      PMIDNDAY,PMMISERV
.
          MOVE      FORM12P2,LINETOT
          MULT      PMIDNDAY,LINETOT
          MOVE      "3",SRECTYPE             * Item record
          MOVE      SP70,PMIDINVN
          CALL      WTDE0000
          MOVE      LOSDAYS,PMMISERV
.
CDEF5000  MOVE      PMIDTDAT,PMMITDAT
          SUB       PMIDNDAY,PMMISERV       * no of items not paid by payer
          COMPARE   PMMISERV,ZERO
          GOTO      CDEF2000 IF NOT LESS
.
CDEF6000  MOVE      PMMIAMTP,FORM12P2
          ADD       PMMIRBAT,FORM12P2         * Actual Unit price
          MOVE      FORM12P2,LINETOT
          MULT      PMMISERV,LINETOT
.
CDEF8000  MOVE      "3",SRECTYPE              * Item record
          MOVE      SP70,PMIDINVN
          CALL      WTDE0000
.
          GOTO      CDEF2000
.
CDEF9999  RETURN
+
.*******************************************************************************
.         Get default payer who will pay Accommodation
.*******************************************************************************
GDPY0000  PACK      KEY26,DAADMNO,SP70
          CALL      RSPMPBR4
GDPY2000  CALL      RKPMPBR4
          BRANCH    OVRCD,GDPY9000
.
          MATCH     DAADMNO,PMPBADMN
          GOTO      GDPY9000 IF NOT EQUAL
.
          MATCH     "1",PMPBDEFT
          GOTO      GDPY2000 IF NOT EQUAL * Not default
.
          MOVE      PMPBCLTY,DEFPAYER     * Default payer
          GOTO      GDPY9999
.
GDPY9000  MOVE      ACLAIM,DEFPAYER       * Default to Admission claim code
.
GDPY9999  RETURN
+
.*******************************************************************************
.         Check for any reminder Accom.invoice amt to be paid by Default payer
.*******************************************************************************
REIN0000  MOVE      ZERO,FORM12P2
          MOVE      SP70,PMIDINVN
          MOVE      ONE,PMIDRTYP
          PACK      KEY34,DAADMNO,PMIDINVN,PMIDRTYP,SP70
          CALL      RSPMIDE3
REIN1000  CALL      RKPMIDE3
          BRANCH    OVRCD,REIN3000
.
          MATCH     DAADMNO,PMIDADMN
          GOTO      REIN3000 IF NOT EQUAL
          MATCH     SP70,PMIDINVN
          GOTO      REIN3000 IF NOT EQUAL
          MATCH     "1",PMIDRTYP
          GOTO      REIN3000 IF NOT EQUAL     * Not Accommodation
.
          ADD       PMIDAMNT,FORM12P2
          GOTO      REIN1000
.
REIN3000  COMPARE   ACCMTOT,FORM12P2          * Check with Total billing
          GOTO      REIN9000 IF NOT LESS      * payer paying all Accomm.
.
          MOVE      ZERO,EXIT                 * there is still reminder inv.amt
          GOTO      REIN9999
.
REIN9000  MOVE      ONE,EXIT                  * Payer paid all Accom.charges
.
REIN9999  RETURN
+

.****************************************************************************
.         Check if there is a payer for this items
.****************************************************************************
CIPY0000  PACK      KEY26,DAADMNO,SP70
          CALL      RSPMIDE2
CIPY1000  CALL      RKPMIDE2
          BRANCH    OVRCD,CIPY9999
.
          MATCH     DAADMNO,PMIDADMN
          GOTO      CIPY9999 IF NOT EQUAL
.
          MATCH     SP70,PMIDINVN
          GOTO      CIPY9999 IF NOT EQUAL
.
          MATCH     "3",PMIDRTYP             * Misc.Item
          GOTO      CIPY1000 IF NOT EQUAL
.
          MATCH     PMMIITEM,PMIDITEM
          GOTO      CIPY1000 IF NOT EQUAL
.
          SUB       PMIDAMNT,LINETOT
          GOTO      CIPY1000
.
CIPY9999  RETURN
+
.****************************************************************************
.             PRINT REPORT
.****************************************************************************
PRPT0000  DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report",*HOFF:
                     *V2LON," **",*HOFF,*P45:24,"Patient  :";
.
          MOVE      ZERO,PATNUM
          MOVE      SP3,SAVWARD
          MOVE      "60",CLNO
.
          PACK      KEY28 WITH SP20,SP10
          CALL      RDSTEMP
PRPT1000  CALL      RDKTEMP
          BRANCH    OVRCD OF PRPT9000
.
          MOVE      XAADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PRPT1000
.
          DISPLAY   *P57:24,*V2LON,PATNAM;
.
          ADD       ONE,PATNUM
.
          MATCH     SAVWARD,XWARD
          GOTO      PRPT5000 IF EQUAL
.
          COMPARE   "60",CLNO
          GOTO      PRPT4000 IF EQUAL
          ADD       ONE,CLNO               * this is to avoid double underlining
          COMPARE   "57",CLNO                at the bottom of the page
          GOTO      PRPT3000 IF LESS
          CALL      HEAD0000
          GOTO      PRPT4000
.
PRPT3000  IF        OPTION = 1 
            CALL      PRTLN000
          ELSE
            CALL      PRTLN000
            PACK      KEY6,XWARD,SP3
            CALL      RDWARD1 
            PRINT     *1,"|"," WARD : ",XWARD,*16,WBDESC,*132,"|"
            CALL      PRTLN000
            ADD       TWO,CLNO
          ENDIF
.
PRPT4000  MOVE      XWARD,SAVWARD
.
PRPT5000  COMPARE   "57",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.         Get the bed type
.
          MOVE      SP8,DIM8
          PACK      KEY5,CODEBT,STRBTYP
          CALL      RDCODE1
          BRANCH    OVRCD,PRPT6000
          MOVE      TDESC,DIM8
.
.         Get the bed type code from the ward file
.
          PACK      WEFRBT,SP70
          PACK      KEY6,XWARD,XBED,SP70
          CALL      RDWARD1
.
.         Get the patient initial given name
.
PRPT6000  MOVE      SP1,PNAM1
          MOVE      PGNAME,PNAM1
.
.         Get the attending doctor name
.
          MOVE      SP20,ADNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF PRPT6200
.
          MOVE      DGNAME,PNAM2
          MOVE      DSNAME,ADNAME
.
PRPT6200  MOVE      SP10,KEY8
          PACK      KEY5 WITH CODEBT,STRBTYP
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,KEY8
          ENDIF
.
          PRINT     *1,"|",XWARD,SLASH,XBED,*9,"|",KEY8:
                    *18,"|",AADMNO," |",PNAM1,DOT,PATNAM,"| ",PSEX," |":
                    PSAGE,"|";
.
          MATCH     SP20,ADNAME
          IF        !@EQUAL
            PRINT     PNAM2,DOT,ADNAME;
          ENDIF
.
.         Loop through Invoice breakdown file
.
          CALL      PIVB0000                    * Print invoice breakdown
          GOTO      PRPT1000
.
.         Report ended
.
PRPT9000  COMPARE   "47",CLNO
          GOTO      PRPT9200 IF LESS
          CALL      HEAD0000
          GOTO      PRPT9400
.
PRPT9200  CALL      PRTLN000
.
PRPT9400  
          PRINT     *N,*N,*5,"*------------------------":
                    *30,"----------------------------------------":
                        "----------------------------------------*":
                    *N,*5,"|Overnights Patients":
                    *30,"Number":
                    *59,PATNUM:
                    *110,"|"
.
          CALL      TTBR0000             * Print total breakdown
.
          PRINT     *5,"|",*110,"|"
.
          PRINT     *5,"|":
                    *30,"Total Fees",*50,TOTFEES,*110,"|"
.
          MOVE      ZERO,AVPAT
          MOVE      TOTFEES,AVPAT
          DIV       PATNUM,AVPAT
.
          PRINT     *5,"|":
                    *30,"Overall Average",*50,AVPAT,*110,"|":
                    *N,*5,"*------------------------":
                    *30,"----------------------------------------":
                        "----------------------------------------*":
                    *N,*N
.
          PRINT     *N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P25:24,*V2LON:
                    "** Report Generation Completed **";
          CALL      KILL0000         * remove temp files
PRPT9999  RETURN
+
.****************************************************************************
.         Print totals of each breakdown
.****************************************************************************
TTBR0000  MOVE      ZERO,TOTFEES
          MOVE      SP70,KEY9
          CALL      RSTMSR1
TTBR1000  CALL      RKTMSR1
          BRANCH    OVRCD,TTBR9999
.
          PRINT     *5,"|";
.
          MATCH     "ACCOM",SITEMC
          IF        @EQUAL
            PRINT     *30,"Accom.Rates";
          ELSE
            READ      CONTROLF,TWENTY;*193,PTCNDCLM
            PACK      KEY29,PTCNDCLM,SP6,SP3,SITEMC,CURRDATE,SP70
            CALL      PATMCHRD
            IF        EXIT=1
              PRINT     *30,SITEMC;
            ELSE
              PACK      KEY20,MDESC
              PRINT     *30,KEY20;
            ENDIF
          ENDIF
.
          MOVE      ZERO,AVPAT
          MOVE      SIAMNT,AVPAT
.
          CALL      NPAT0000          * Get no of patients for the item
          DIV       F5,AVPAT
          ADD       SIAMNT,TOTFEES
.
          PRINT     *50,SIAMNT:
                    *65,"  Average",*75,AVPAT,*110,"|"
          GOTO      TTBR1000
.
TTBR9999  RETURN
+
.****************************************************************************
.         Get no of patients fo the item
.****************************************************************************
NPAT0000  MOVE      ZERO,F5
          PACK      KEY17,SITEMC,SP70
          CALL      RSXXSR1
NPAT1000  CALL      RKXXSR1
          BRANCH    OVRCD,NPAT9999
.
          MATCH     SITEMC,XITEMC       * Same item?
          GOTO      NPAT9999 IF NOT EQUAL
.
          ADD       ONE,F5
          GOTO      NPAT1000
.
NPAT9999  RETURN
+
.****************************************************************************
.         Print invoice breakdown
.****************************************************************************
PIVB0000  MOVE      ZERO,COUNTER
          MOVE      ZERO,FORM6P2
          MOVE      "  0",PMPBICNT
          MOVE      ZERO,ENDFLAG
.
          CLOSE     PMSIDEA1
          MATCH     "1",INVCFLAG
          IF        @EQUAL
            OPEN      PMSIDEA1,"pmsideaf"
          ELSE
            OPEN      PMSIDEA1,FNAMET
          ENDIF
.
          PACK      KEY26,XAADMNO,PMPBICNT,SP70
          CALL      RSPMPBR4
PIVB1000  CALL      RKPMPBR4
          BRANCH    OVRCD,PIVB6000
.
          MATCH     PMPBADMN,XAADMNO
          GOTO      PIVB6000 IF NOT EQUAL
.
          COMPARE   ZERO,PMPBMAXD
          GOTO      PIVB1000 IF EQUAL     * No daily charge
.
          MATCH     "  0",PMPBICNT        * Accommodation record
          GOTO      PIVB4000 IF NOT EQUAL
.
.         Accommodation
.
          MATCH     "1",PMPBDEFT
          IF        @EQUAL
            MOVE      ONE,ENDFLAG         * Found a default payer
          ENDIF
.
          PACK      KEY12,PMPBCLTY,SP70
          PACK      KEY5,ANSC,ANSL,PMPBCLTY
          CALL      RDCODE1
          IF        OVRCD=0
            PACK      KEY12,TDESC
          ENDIF
.
          IF        COUNTER<>0
            PRINT     *1,"|",*9,"|":
                      *18,"|",*28,"|",*45,"| ",*49,"|":
                      *53,"|";
          ENDIF

          PRINT     *65,"|",KEY12;
.
          IF        COUNTER=0
            UNPACK    ADATE WITH XCENT,XYEAR,XMON,XDAY
            PRINT     *81,"|",XDAY,SLASH,XMON,SLASH,XYEAR;
            PRINT     *92,"| ",ATYPE;
          ELSE
            PRINT     *81,"|":
                      *92,"|";
          ENDIF
.
          MOVE      "1",KEY1
          CALL      GFEE0000                * Get room rate
          MOVE      FORM12P2,FORM6P2
          PRINT     *99,"| ACCOM";
          IF        COUNTER=0
            PRINT     *115,"| ",TDAYNO;
          ELSE
            PRINT     *115,"|";
          ENDIF
          PRINT     *122,"|",FORM6P2,*132,"|"
          ADD       ONE,CLNO
.
          MOVE      "ACCOM    ",SITEMC
          CALL      USRT0000
.
          ADD       ONE,COUNTER
          GOTO      PIVB1000
.
.         Items
.
PIVB4000  
.         CALL      GITM0000                * Get item amount
.         BRANCH    EXIT,PIVB1000
.
          MOVE      "3",KEY1
          CALL      GFEE0000                * Get item rate
.
          PACK      KEY12,PMPBCLTY,SP70
          PACK      KEY5,ANSC,ANSL,PMPBCLTY
          CALL      RDCODE1
          IF        OVRCD=0
            PACK      KEY12,TDESC
          ENDIF
          IF        COUNTER<>0
            PRINT     *1,"|",*9,"|":
                      *18,"|",*28,"|",*45,"| ",*49,"|":
                      *53,"|";
          ENDIF
.
          MOVE      FORM12P2,FORM6P2
          PRINT     *65,"|",KEY12;
.
          IF        COUNTER=0
            UNPACK    ADATE WITH XCENT,XYEAR,XMON,XDAY
            PRINT     *81,"|",XDAY,SLASH,XMON,SLASH,XYEAR;
            PRINT     *92,"| ",ATYPE;
          ELSE
            PRINT     *81,"|":
                      *92,"| ";
          ENDIF
.
          PRINT     *99,"| ",PMPBITEM;
.
          IF        COUNTER=0
            PRINT     *115,"| ",TDAYNO;
          ELSE
            PRINT     *115,"|";
          ENDIF
.
          PRINT     *122,"|",FORM6P2,*132,"|"
          ADD       ONE,CLNO
.
          MOVE      PMPBITEM,SITEMC
          CALL      USRT0000
.
          ADD       ONE,COUNTER
          GOTO      PIVB1000
.
.         Print default payer
.
PIVB6000  BRANCH    ENDFLAG,PIVB9200
.
          MOVE      XAADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PIVB9000
.
          MOVE      ACLAIM,PMPBCLTY        * patient claim type
.
          CALL      GDFE0000               * Get default rate
.
PIVB9000  IF        COUNTER=0
            PRINT     *65,"|":
                      *81,"|":
                      *92,"| ":
                      *99,"| ":
                      *115,"| ":
                      *122,"|",*132,"|"
            ADD       ONE,CLNO
          ENDIF
.
PIVB9200  CLOSE     PMSIDEA1
PIVB9999  RETURN
+
.****************************************************************************
.         Get Room/item rate
.****************************************************************************
GFEE0000  MOVE      ZERO,FORM12P2
          PACK      KEY25,XAADMNO,PMPBCLTY,SP70
          CALL      RSPMIDE1
GFEE1000  CALL      RKPMIDE1
          BRANCH    OVRCD,GFEE9999
.
          MATCH     XAADMNO,PMIDADMN
          GOTO      GFEE9999 IF NOT EQUAL       * Different admission
          MATCH     PMPBCLTY,PMIDCLTY
          GOTO      GFEE9999 IF NOT EQUAL
.
          MATCH     PMIDRTYP,KEY1
          GOTO      GFEE1000 IF NOT EQUAL       * Not an accommodation record
.
          MATCH     "3",KEY1
          IF        @EQUAL
            MATCH     PMPBITEM,PMIDITEM
            GOTO      GFEE1000 IF NOT EQUAL
          ENDIF
.
          MATCH     PMIDFDAT,YTDATE
          GOTO      GFEE1000 IF LESS
          MATCH     YTDATE,PMIDTDAT
          GOTO      GFEE1000 IF LESS
.
          MOVE      PMIDAMNT,FORM12P2
          DIV       PMIDNDAY,FORM12P2
.
GFEE9999  RETURN
+
.****************************************************************************
.         Get item amount
.****************************************************************************
GITM0000  MOVE      ONE,EXIT
          MOVE      ZERO,FORM12P2
          MOVE      "3",DTRECTYP
          PACK      KEY18,PMPBADMN,DTRECTYP,SP70
          CALL      RDSDTRN3
GITM1000  CALL      RDKDTRN3
          BRANCH    OVRCD,GITM2000
.
          MATCH     PMPBADMN,DTADMNO
          GOTO      GITM2000 IF NOT EQUAL
          MATCH     "3",DTRECTYP
          GOTO      GITM2000 IF NOT EQUAL
.
          COMPARE   ONE,PTDTCCU              * Item from Regime?
          GOTO      GITM1000 IF NOT EQUAL
          MATCH     PMPBITEM,TITEMNO
          GOTO      GITM1000 IF NOT EQUAL
.
          PACK      KEY8,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY
          REP       " 0",KEY8
          MOVE      KEY8,ISBLDATE
          ADD       TSERVS,ISBLDATE
          MOVE      ISBLDATE,DIM8
.
          MATCH     KEY8,YTDATE
          GOTO      GITM1000 IF LESS
          MATCH     YTDATE,DIM8
          GOTO      GITM1000 IF LESS
.
          MOVE      TPATAMTP,FORM12P2
          ADD       TREBATE,FORM12P2
.
          MOVE      ZERO,EXIT
          GOTO      GITM9999
.
.         Try to be invoiced
.
GITM2000  PACK      KEY15,PMPBADMN,PMMIRTYP,SP70
          CALL      RSPMMTI2
GITM3000  CALL      RKPMMTI2
          BRANCH    OVRCD,GITM9999
.
          MATCH     PMPBADMN,PMMIVISN        * Same visit number?
          GOTO      GITM9999 IF NOT EQUAL
          MATCH     "3",PMMIRTYP
          GOTO      GITM9999 IF NOT EQUAL
.
          MATCH     "1",PMMIINCT             * Item from Regime?
          GOTO      GITM3000 IF NOT EQUAL
          MATCH     PMPBITEM,PMMIITEM
          GOTO      GITM3000 IF NOT EQUAL
.
          MOVE      PMMITDAT,ISBLDATE
          ADD       PMMISERV,ISBLDATE
          MOVE      ISBLDATE,KEY8
.
          MATCH     PMMITDAT,YTDATE
          GOTO      GITM3000 IF LESS
          MATCH     YTDATE,KEY8
          GOTO      GITM3000 IF LESS
.
          MOVE      PMMIAMTP,FORM12P2
          ADD       PMMIRBAT,FORM12P2
          MOVE      ZERO,EXIT
.
GITM9999  RETURN
+
.****************************************************************************
.         Get Patient Default payer daily charge from tempfile if not invoiced
.****************************************************************************
GDFE0000  PACK      KEY25,XAADMNO,SP70
          CALL      RSPMIDE1
GDFE1000  CALL      RKPMIDE1
          BRANCH    OVRCD,GDFE9999
.
          MATCH     XAADMNO,PMIDADMN
          GOTO      GDFE9999 IF NOT EQUAL       * Different admission
          COMPARE   ONE,PMIDDEFT
          GOTO      GDFE1000 IF NOT EQUAL       * Not a default payer
.
          MATCH     ALACDTE,PMIDFDAT
          IF        @EQUAL
            MATCH     PMIDFDAT,YTDATE
            GOTO      GDFE1000 IF LESS
          ELSE
            MATCH     PMIDFDAT,YTDATE
            GOTO      GDFE1000 IF EQUAL
            GOTO      GDFE1000 IF LESS
          ENDIF
          MATCH     YTDATE,PMIDTDAT
          GOTO      GDFE1000 IF LESS
.
          MOVE      PMIDAMNT,FORM12P2
          DIV       PMIDNDAY,FORM12P2
.
          MATCH     "1",PMIDRTYP
          IF        @EQUAL
            MOVE      "ACCOM   ",PMIDITEM
          ENDIF
.
          IF        COUNTER<>0
            PRINT     *1,"|",*9,"|":
                      *18,"|",*28,"|",*45,"| ",*49,"|":
                      *53,"|";
          ENDIF
.
          MOVE      FORM12P2,FORM6P2
          PRINT     *65,"|",ACLAIM,"(SELF)";
.
          IF        COUNTER=0
            UNPACK    ADATE WITH XCENT,XYEAR,XMON,XDAY
            PRINT     *81,"|",XDAY,SLASH,XMON,SLASH,XYEAR:
                      *92,"| ",ATYPE;
          ELSE
            PRINT     *81,"|":
                      *92,"| ";
          ENDIF
.
          PRINT     *99,"| ",PMIDITEM:
                    *115,"|":
                    *122,"|",FORM6P2,*132,"|"
          ADD       ONE,CLNO
.
          MOVE      PMIDITEM,SITEMC
          CALL      USRT0000
.
          ADD       ONE,COUNTER
          GOTO      GDFE1000
.
GDFE9999  RETURN
+
.****************************************************************************
.         Next Line 
.****************************************************************************
NLIN0000  COMPARE   ZERO,COUNTER
          GOTO      NLIN9999 IF EQUAL
.
          PRINT     *1,"|",*9,"|":
                    *18,"|",*28,"|",*45,"| ",*49,"|":
                    *53,"|":
                    *65,"|":
                    *81,"|":
                    *92,"| ";
NLIN9999  RETURN
+
.****************************************************************************
.             HEADING
.****************************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      PRTLN000 IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          MOVE      "(Ward/Bed Sequence)",CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
          ADD       ONE,CLNO
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital Id :",PTHSNAME
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *N,*40,"Patients at Midnight ":
                       XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2,*N
.
          CALL      PRTLN000
.
          PRINT    *1,"| Ward/ ",*9,"|    Bed",*18,"|   Adm.":
                   *28,"|":
		   *45,"|",*49,"|",*53,"|":
		   *65,"|Claim":
                   *81,"| Adm.":
                   *92,"|Adm.":
                   *99,"|":
                   *115,"|No. of",*122,"|   Day ",*132,"|":
                *N,*1,"|   Bed ",*9,"|    Type",*18,"|  Number":
                   *28,"|Patient Name":
                   *45,"|Sex|Age| Att. Doct.",*65,"|Type":
                   *81,"| Date":
                   *92,"|Type":
                   *99,"|Breakdown":
                   *115,"| Days",*122,"|   Rate",*132,"|"
.
          CALL      PRTLN000
          MOVE      NINE,CLNO
          RETURN
+
.****************************************************************************
.         Print a line
.****************************************************************************
PRTLN000  PRINT     *1,"*-------------------------------":
                    *33,"-----------------------------------------------":
                    *80,"----------------------------------------------------*"
          RETURN
+
.****************************************************************************
.             Set up yesterday date
.****************************************************************************
SETY0000  MOVE      CDD,DD
          MOVE      CMM,MM
          MOVE      CYY,YY
          MOVE      CCC,CC
          CALL      DMYCON
.
          SUB       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      YTDATE WITH CC,YY,MM,DD
          REP       " 0",YTDATE
          UNPACK    YTDATE WITH XCENT2,XYEAR2,XMON2,XDAY2
SETY9999  RETURN
+
.****************************************************************************
.         Create an indexed file based on port
.****************************************************************************
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,FNAMEV,UKEY1
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          OPEN      TEMPFILE,FNAMEV              * open temp index file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,FNAMEI,SKEY1
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          OPEN      TMSRFILE,FNAMEI              * open temp index file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,FNAMEU,XKEY1
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          OPEN      XXSRFILE,FNAMEU              * open temp index file
.
          CLEAR     CMDLINE
          APPEND    ISBUILD,CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 276 U1-8,9-11,12-19,20-25",CMDLINE
          APPEND    " U1-8,12-19,90-90,9-11,20-25",CMDLINE
          APPEND    " U1-8,12-19,26-26,27-34,9-11,20-25",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000                                    *
.*               close and erase the temporary file                         *
.****************************************************************************
KILL0000  CLOSE     TEMPFILE                     * close file
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,FNAMEV         * set file erase command
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLOSE     TMSRFILE                     * close file
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,FNAMEI         * set file erase command
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLOSE     XXSRFILE                     * close file
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,FNAMEU         * set file erase command
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLOSE     PMSIDEA1
          CLOSE     PMSIDEA3
.
          CLEAR     CMDLINE
          APPEND    ERASE,CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999  RETURN
+
.****************************************************************************
.         Updating Breakdown sort file
.****************************************************************************
USRT0000  COMPARE   ZERO,FORM12P2
          GOTO      USRT9999 IF EQUAL
.
          PACK      KEY17,SITEMC,XAADMNO,SP70
          CALL      RDXXSR1
          IF        OVRCD=1
            MOVE      SP70,XSPARE
            CALL      WRXXSR1
          ENDIF
.
          PACK      KEY9,SITEMC,SP70
          CALL      RDTMSR1
          IF        OVRCD=0
            ADD       FORM12P2,SIAMNT
            CALL      UPTMSR1
          ELSE
            MOVE      FORM12P2,SIAMNT
            CALL      WRTMSR1
          ENDIF
USRT9999  RETURN
+
.****************************************************************************
.           I/O routines for the Temp Index file
.****************************************************************************
WRTMSR1   PACK      SSPARE,SP70
          RESET     KEY9
          WRITE     TMSRFILE,KEY9;SITEMC,SIAMNT,SSPARE
          RETURN
.
RKTMSR1   MOVE      ZERO,OVRCD
          READKS    TMSRFILE;SITEMC,SIAMNT,SSPARE
          GOTO      OVERCOND IF OVER
          MOVE      XAADMNO,AADMNO
          RETURN
.
RSTMSR1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TMSRFILE,KEY9;;
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMSR1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TMSRFILE,KEY9;SITEMC,SIAMNT,SSPARE
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMSR1   MOVE      ZERO,OVRCD
          UPDATE    TMSRFILE;SITEMC,SIAMNT,SSPARE
          RETURN
.
.****************************************************************************
WRXXSR1   PACK      XSPARE,SP70
          RESET     KEY17
          WRITE     XXSRFILE,KEY17;KEY17,XSPARE
          RETURN
.
RDXXSR1   MOVE      ZERO,OVRCD
          RESET     KEY17
          READ      XXSRFILE,KEY17;XITEMC,XVISTN,XSPARE
          GOTO      OVERCOND IF OVER
          RETURN
.
RKXXSR1   MOVE      ZERO,OVRCD
          READKS    XXSRFILE;XITEMC,XVISTN,SSPARE
          GOTO      OVERCOND IF OVER
          MOVE      XAADMNO,AADMNO
          RETURN
.
RSXXSR1   MOVE      ZERO,OVRCD
          RESET     KEY17
          READ      XXSRFILE,KEY17;;
          GOTO      OVERCOND IF OVER
          RETURN
.
.****************************************************************************
.
WRTEMP    RESET     KEY28
          MOVE      AADMNO,XAADMNO
          MOVE      UNIQUE,XUNIQUE
          WRITE     TEMPFILE,KEY28;XWARD,XBED,PATNAM,XUNIQUE,PGNAME,PSEX,PSAGE:
                                   ADOCTA,AFUNDH,AFNDTB,ADATE,ACLAIM:
                                   ATYPE,DAYCASE:
                                   TDAYNO,STRBTYP,XAADMNO,INVCFLAG
          RETURN
.
RDKTEMP   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XWARD,XBED,PATNAM,XUNIQUE,PGNAME,PSEX,PSAGE:
                             ADOCTA,AFUNDH,AFNDTB,ADATE,ACLAIM:
                             ATYPE,DAYCASE:
                             TDAYNO,STRBTYP,XAADMNO,INVCFLAG
          GOTO      OVERCOND IF OVER
          MOVE      XAADMNO,AADMNO
          RETURN
.
RDSTEMP   MOVE      ZERO,OVRCD
          RESET     KEY28
          READ      TEMPFILE,KEY28;;
          GOTO      OVERCOND IF OVER
          RETURN
+
.------------------------------------------------------------------------------
.         KHOS0000
.         Enter The Hospital
.------------------------------------------------------------------------------
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:21,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TWENTY1,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:21,*V2LON,PTHSNAME;
          PACK      HOSNAME,PTHSNAME,SP70
          PACK      HOSCODE,PTHSHOSP,SP70
.
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.----------------------------------------------------------------------
.         Get the Start and End Accommodation date for this payer
.----------------------------------------------------------------------
FACM0000  MOVE      CURRDATE,BACKDATE
          MOVE      ZERO,ACDAYCS             * Assume not a day case
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      ONE,ACDAYCS            * This is a day case
          ENDIF
.
          MOVE      ZERO,FORM5A
          MOVE      ALACDTE,SFRDATE          * Default Accommodation fromdate
          MOVE      "  0",PMPBICNT
          PACK      KEY26,DAADMNO,PMPBICNT,SP70
          CALL      RSPMPBR4
FACM1000  CALL      RKPMPBR4
          BRANCH    OVRCD,FACM1200
.
          MATCH     PMPBADMN,DAADMNO
          GOTO      FACM1200 IF NOT EQUAL
.
          MATCH     "  0",PMPBICNT        * Accommodation record
          GOTO      FACM2000 IF NOT EQUAL
.
          MATCH     PMPBCLTY,SAVACLM
          GOTO      FACM1200 IF EQUAL
.
.         Workout how many days of this payer is paying
.
          MOVE      PMPBMAXB,FORM12P2
          DIV       PMPBMAXD,FORM12P2
.
          ADD       FORM12P2,FORM5A
          GOTO      FACM1000
.
FACM1200  COMPARE   ZERO,FORM5A
          GOTO      FACM2000 IF EQUAL
.
          MOVE      ALACDTE,ISBLDATE
          ADD       FORM5A,ISBLDATE
          MOVE      ISBLDATE,SFRDATE
.
          UNPACK    SFRDATE INTO XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,FDAY
          MOVE      XMON,FMON
          MOVE      XYEAR,FYEAR
          MOVE      XCENT,FCENT
.
.         If there is no discharge date, use the current date as end date
.
FACM2000  COMPARE   THREE,ASTAT
          GOTO      FACM5000 IF NOT EQUAL

          MATCH     SP8,DDATE
          GOTO      FACM5000 IF EQUAL
.
          MATCH     ADATE,DDATE
          IF        @EQUAL
           MOVE      FDAY,TDAY
           MOVE      FMON,TMON
           MOVE      FYEAR,TYEAR
           MOVE      FCENT,TCENT
           GOTO      FACM9999
          ENDIF
.
.         For day cases ( or no accommodation range ) use from-date as to-date
.
          MATCH     CURRDATE,DDATE
          IF        @LESS
            UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
            MOVE      XDAY,TDAY
            MOVE      XMON,TMON
            MOVE      XYEAR,TYEAR
            MOVE      XCENT,TCENT
          ENDIF
          GOTO      FACM9999
.
.         Use the keyin date
.
FACM5000  UNPACK    CURRDATE,XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,TDAY
          MOVE      XMON,TMON
          MOVE      XYEAR,TYEAR
          MOVE      XCENT,TCENT
          GOTO      FACM9999
.
FACM9999  RETURN
+
.*******************************************************************************
.         Write to temporary Invoice Payer details
.*******************************************************************************
WTDE0000  MOVE      PMIDINVN,KEY8
          MOVE      ONE,F6
          PACK      KEY25,DAADMNO,PMPBCLTY,PMIDINVN,Z70
          CALL      RSPMIDE1
          CALL      RPPMIDE1
          BRANCH    OVRCD,WTDE7000
.
          PACK      KEY19,PMIDADMN,PMIDCLTY,PMIDINVN,SP70
          MATCH     KEY19,KEY25
          GOTO      WTDE7000 IF NOT EQUAL
          MOVE      PMIDTRNO,F6
.
WTDE6000  ADD       ONE,F6
WTDE7000  MOVE      KEY8,PMIDINVN
          MOVE      F6,PMIDTRNO
          PACK      KEY25,DAADMNO,PMPBCLTY,PMIDINVN,PMIDTRNO,SP70
          CALL      RAPMIDE1
          COMPARE   ZERO,OVRCD
          GOTO      WTDE6000 IF EQUAL
.
          CALL      CLPMSIDE                   * Clear variable
.
          MOVE      KEY8,PMIDINVN
          MOVE      DAADMNO,PMIDADMN
          MOVE      PMPBCLTY,PMIDCLTY
          MOVE      F6,PMIDTRNO
          MOVE      SRECTYPE,PMIDRTYP
          MATCH     "1",SRECTYPE
          IF        @EQUAL
            PACK      PMIDFDAT,FCENT1,FYEAR1,FMON1,FDAY1
            REP       " 0",PMIDFDAT
            PACK      PMIDTDAT,TCENT1,TYEAR1,TMON1,TDAY1
            REP       " 0",PMIDTDAT
            PACK      PMIDDESC,TDDESC,SP70
            MOVE      SP70,PMIDITEM
            MOVE      NODAYS,PMIDNDAY
            MOVE      SAVCONTR,PMIDCNID
          ELSE
            MOVE      PMMITDAT,PMIDFDAT
            REP       " 0",PMIDFDAT
            PACK      PMIDDESC,PMMIDESC,SP70
            MOVE      PMMIITEM,PMIDITEM
            MOVE      PMMISERV,PMIDNDAY     * Quantity
            IF        PRTIFLAG=1
              MOVE      "1",PMIDSPLT        * split invoice for item record
            ENDIF
            MOVE      PMIDFDAT,ISBLDATE
            ADD       PMIDNDAY,ISBLDATE
            MOVE      ISBLDATE,PMIDTDAT
.
          ENDIF
.
          MOVE      LINETOT,PMIDAMNT
          MOVE      LINEGST,PMIDGSTM
          MOVE      PMPBDEFT,PMIDDEFT
          MOVE      SP70,PMIDREGM
.
          PACK      PMIDCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMIDCDAT           * date created
          CLOCK     TIME,PMIDCTIM           * time created
          MOVE      WBSEUID,PMIDCUID         * web user id
          UNPACK    SP70,PMIDUUID,PMIDUDAT,PMIDUTIM
          PACK      PMIDSPAR,SP70,SP70
.
          CALL      WRPMIDE1
          GOTO      WTDE9999
.
WTDE9999  RETURN
+
.==============================================================================
.         I/O for tempfile
.==============================================================================
.
RSPMIDE1  RESET     KEY25
          READ      PMSIDEA1,KEY25;;
          RETURN
.
RAPMIDE1  RESET     KEY25
          MOVE      ZERO,OVRCD
          READ      PMSIDEA1,KEY25;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDPMIDE1  RESET     KEY25
          MOVE      ZERO,OVRCD
          READ      PMSIDEA1,KEY25;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPMIDDEF,PMIDDEFT
          RETURN
.
RKPMIDE1  MOVE      ZERO,OVRCD
          READKS    PMSIDEA1;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPMIDDEF,PMIDDEFT
          RETURN
.
RPPMIDE1  MOVE      ZERO,OVRCD
          READKP    PMSIDEA1;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPMIDDEF,PMIDDEFT
          RETURN
.
WRPMIDE1  RESET     KEY25
          MOVE      PMIDDEFT,DPMIDDEF
          WRITE     PMSIDEA1,KEY25;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          RETURN
.
RSPMIDE2  RESET     KEY26
          READ      PMSIDEA2,KEY26;;
          RETURN
.
RKPMIDE2  MOVE      ZERO,OVRCD
          READKS    PMSIDEA2;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPMIDDEF,PMIDDEFT
          RETURN
.
RPPMIDE2  MOVE      ZERO,OVRCD
          READKP    PMSIDEA2;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPMIDDEF,PMIDDEFT
          RETURN
.
RSPMIDE3  RESET     KEY34
          READ      PMSIDEA3,KEY34;;
          RETURN
.
RAPMIDE3  RESET     KEY34
          MOVE      ZERO,OVRCD
          READ      PMSIDEA3,KEY34;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDPMIDE3  RESET     KEY34
          MOVE      ZERO,OVRCD
          READ      PMSIDEA3,KEY34;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPMIDDEF,PMIDDEFT
          RETURN
.
RKPMIDE3  MOVE      ZERO,OVRCD
          READKS    PMSIDEA3;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPMIDDEF,PMIDDEFT
          RETURN
.
RPPMIDE3  MOVE      ZERO,OVRCD
          READKP    PMSIDEA3;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPMIDDEF,PMIDDEFT
          RETURN
.
UPPMIDE3  MOVE      ZERO,OVRCD
          MOVE      PMIDDEFT,DPMIDDEF
          UPDATE    PMSIDEA3;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          RETURN
.
WRPMIDE3  RESET     KEY34
          MOVE      PMIDDEFT,DPMIDDEF
          WRITE     PMSIDEA3,KEY34;PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,PMIDRTYP:
                        PMIDFDAT,PMIDTDAT,PMIDNDAY,PMIDDESC,PMIDITEM,DPMIDDEF:
                        PMIDREGM,PMIDAMNT,PMIDGSTM,PMIDSPLT,PMIDCNID,PMIDCUID:
                        PMIDCDAT,PMIDCTIM,PMIDUUID,PMIDUDAT,PMIDUTIM,PMIDSPAR
          RETURN
.
DEPMIDE3  RESET     KEY34
          DELETE    PMSIDEA3,KEY34
          RETURN
.
          INC       STD001IO
          INC       AAECATBI
          INC       AAEDE1IO/INC
          INC       AAEDTRIO/INC
          INC       CALCAGE
          INC       CLNZPPIC
          INC       IBAGEDIO/INC
          INC       IBAGSTIO/INC
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
          INC       IBATMDIO/INC
          INC       NHIMASIO/INC
          INC       MLTCFNIO/INC

          INC       NZPBFEIO/INC
          INC       NZPCFNIO/INC
          INC       NZPCMTIO/INC
          INC       NZPEXTIO/INC
          INC       NZPRDTIO/INC
          INC       NZPFIGIO/INC
          INC       NZPIBRIO/INC
          INC       NZPMCHIO/INC
          INC       NZPMXCIO/INC
          INC       NZPPFEIO/INC
          INC       NZPPICIO/INC
          INC       NZPRBRIO/INC
          INC       NZPRFNIO/INC
          INC       NZPFINIO/INC
          INC       NZPSPRIO/INC
          INC       NZPTFEIO/INC
          INC       NZPIVBIO/INC
          INC       NZPRVBIO/INC
.
          INC       OPRDEAIO/INC
          INC       OPRSESIO/INC
          INC       OUTCATBI
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       PATCATBI
          INC       PATCODDS
          INC       CHKCMXPT                * Check if casemix & get epis type
          INC       PATCMSTP
          INC       PATGNCMX
          INC       CMGETTHR
          INC       CMXMATRX
          INC       NHOSPDAY
          INC       CALCDAYS
          INC       PABXBILL
          INC       PATCOMN3
          INC       PATIVMES
          INC       CLPATDTR                * Clear DTR file variables
          INC       PATHSPKY
          INC       PRBILCMN
          INC       GETBFEES
          INC       GETTFEES
          INC       GETCNEFF
          INC       PATMCHRD
          INC       PATAGED
          INC       CLPMSIDE
          INC       ISBITEMS
          INC       CLPMSPBR
.
          INC       PMSPYRIO/INC
          INC       PMSPBRIO/INC
          INC       PATAFEIO/INC       * for casemix
          INC       PATASDIO/INC
          INC       PATCMXIO/INC
          INC       PATHDFIO/INC
          INC       PATHLFIO/INC
          INC       PATLDFIO/INC
          INC       PATLSDIO/INC
          INC       PRIITMIO/INC
.
          INC       PATELGIO/INC
          INC       PATBFEIO/INC
          INC       PATTFEIO/INC       *theatre fee
          INC       PATCODIO/INC
          INC       PATCFAIO/INC
          INC       PMSCIDIO/INC
          INC       PMSCCDIO/INC
          INC       PATCTFIO/INC
          INC       PATDGWIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATDTRIO/INC
          INC       PATDRGIO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATGTUIO/INC
          INC       PATRE1IO/INC
          INC       PATHSPIO/INC
          INC       PATHTRIO/INC
          INC       EMRHTRIO/INC
          INC       OUTHTRIO/INC
          INC       PATFINIO/INC
          INC       PATFMOIO/INC
          INC       PATFX1IO/INC
          INC       PATFN1IO/INC
          INC       PATFN2IO/INC
          INC       PATIBLIO/INC
          INC       PATICUIO/INC
          INC       PATIN1IO/INC
          INC       PATINMIO/INC
          INC       PATINVIO/INC
          INC       PATIOVIO/INC
          INC       PATIPEIO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       PATMCHIO/INC
          INC       PATMI1IO/INC
          INC       PATOVBIO/INC
          INC       PATPGRIO/INC
          INC       PATRBLIO/INC
          INC       PATRCAIO/INC
          INC       PATRATIO/INC
          INC       PATSRBIO/INC
          INC       PATTRNIO/INC
          INC       PATVENIO/INC
          INC       PATVISIO/INC
          INC       PATWC1IO/INC
          INC       PATWMAIO/INC
          INC       PATWR1IO/INC
          INC       PATWVEIO/INC
          INC       PMSHATIO/INC
          INC       PMSEVTIO/INC
          INC       PMSCNOIO/INC
          INC       PMSHCPIO/INC
          INC       PMSSINIO/INC
          INC       PATCMTIO/INC
          INC       PMSCMTIO/INC
          INC       PMSSGAIO/INC
          INC       PMSMTIIO/INC
          INC       PMSMPRIO/INC
          INC       PMSVX1IO/INC
          INC       PATRGMIO/INC
          INC       PMSREGIO/INC
          INC       RCPBDTIO/INC
          INC       OPRARDIO/INC
          INC       PATSELFN
          INC       PATITMRD
.
          INC       EMRSITIO/INC
          INC       EMRHISIO/INC
          INC       EMRVISIO/INC
          INC       COMDEPIO/INC
          INC       RCPBNKIO/INC
          INC       RCPDTEIO/INC
          INC       STEPDOWN
          INC       VALCMXFN
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
          INC       PMSEDGIO/INC       * Effective DRG Dates
          INC       PATDDHIO/INC
          INC       GETEFDRG
.
WRITETRN
DISPMORE
GETTHR
PATCALF
TAIL
TAIL18
INITIVAR
PRTEOL
PROCMISC
PRTCLMS
NXTTRAN1
WEBERR00
PRTINVTL
SINVT000
DOCNM000
ISUG0000
PRLN0000
PROACC00
PEOL0000
ENDPRT00
IPRH0000
CKTL0000
PRTIPROV
ENDPRT    RETURN
.
+

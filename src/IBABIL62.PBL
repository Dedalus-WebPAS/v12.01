. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBABIL62                                            *
. *                                                                           *
. *     FUNCTION:         ECLIPSE - NON EXTRACTED CLAIMS                      *
. *                                                                           *
. *     DATE WRITTEN:     27/04/2009                                          *
. *                                                                           *
. *     AUTHOR:           Peter Vela                                          *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *       V12.00.01  23/05/2025 PJ Le Febour  TSK 0955096 AlphaNum visitnum   *
. *                  replaced COMPARE ZERO,PINVADM to MATCH ZEROVISN,PINVADM  *
. *                           COMPARE ATNUMB,PINVADM to MATCH ATNUMB,PINVADM  *
. *                           COMPARE OTNUMB,PINVADM to MATCH OTNUMB,PINVADM  *
. *                           COMPARE TADMNO,PINVADM to MATCH TADMNO,PINVADM  *
. *       V10.13.01  06/12/2013 J.Tan         TSK 0859743                     *
. *                  Mod for OTRECTYP=6 for Theatre item                      *
. *        V9.12.02  18/08/2009 Peter Vela    CAR 203723                      *
. *                  Added validations for PTFXECLP.patfx1af                  *
. *                  Eclipse Participant Health Fund Extension Table          *
. *        V9.12.01  27/04/2009 Peter Vela    CAR 183976                      *
. *                  Created report program                                   *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       RSHWEBDF
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEDTRFD/INC
.
          INC       EMRVISFD/INC
.
          INC       IBACONFD/INC
          INC       IBASECFD/INC
          INC       IBAPASFD/INC
.
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
.
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDRGFD/INC
          INC       PATDTRFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATHSPFD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPARFD/INC
          INC       PATPCPFD/INC
          INC       PATSELDT/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PMSECTFD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLES
. ---------------
AFIRST    DIM       6
ALAST     DIM       6
.
CDESC     DIM       20
.CFEETYP   FORM      1
CODE      DIM       2
CSTRTYR   DIM       8                            * for PATSTRYR
CRNTOTAL  FORM      12.2
.
DIM8      DIM       8
DIM30     DIM       30
.
ECLKEY3   DIM       3
ERRFLAG   FORM      1
.
FILTOPTN  FORM      1
FIRST     DIM       8
FORM7P2   FORM      7.2
FORM8P2   FORM      8.2
FORM12P2  FORM      12.2
FROMDATE  DIM       8
.
IDATET    DIM       8
.
LAST      DIM       8
.
ITEMGST   FORM      12.2
PRTFLAG   FORM      1
PATSTCUR  DIM       4                            * for PATSTRYR
PINVBED   FORM      6
PINVADJM  FORM      12.2
PINVACCM  FORM      12.2
PINVGSTM  FORM      12.2
PINVMISC  FORM      12.2
PINVTHET  FORM      12.2
PINVTOTL  FORM      12.2
PNAME     DIM       15
PPAYCASH  FORM      12.2
PPAYJRNL  FORM      12.2
PPAYTOTL  FORM      12.2
.
SFIRST    FORM      8
STKEY     DIM       8
STMAST    DIM       2
.
TMPCLMCD  DIM       3
TMPHLFND  DIM       6
TMPACLMD  FORM      12.2
TMPOUTSA  FORM      12.2
TMPHLFTO  FORM      12.2
TMPHLFTP  FORM      12.2
TMPOUTST  FORM      12.2
.
TINVACCM  FORM      12.2
TINVBED   FORM      6
TINVMISC  FORM      12.2
TINVTHET  FORM      12.2
TINVTOTL  FORM      12.2
TINVADJM  FORM      12.2
TINVGSTM  FORM      12.2
TODATE    DIM       8
TOTONLY   FORM      1
TPAYCASH  FORM      12.2
TPAYJRNL  FORM      12.2
TPAYTOTL  FORM      12.2
.COTHSFN   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
FIVE9     FORM      "59"
.
.
PRGID     INIT      "IBABIL62" 
PRGNAM    INIT      "Eclipse - Non Extracted Claims"
VERSION   INIT      "V12.01.00"
+
.
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR2,"patinvrf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA2,"patdrgaf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"aaedtref";
          OPEN      AAEDTRE1,"aaedtref"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patfx1af";
          OPEN      PATFX1A1,"patfx1af"
.
          DISPLAY   *P64:24,"patparaf";
          OPEN      PATPARA1,"patparaf"
.
          DISPLAY   *P64:24,"patpcpaf";
          OPEN      PATPCPA1,"patpcpaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
. 
          DISPLAY   *P64:24,"pmsectaf"
          OPEN      PMSECTA2,"pmsectaf"
.
          dISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
          READ      CONTROLF,TEN3;*194,CVETINS
+
.
.        START OF PROGRAM
.
.           MAIN        MENU
.
.             SELECT OPTION
.
START   MOVE       ZERO,CPAGENO
        MOVE       SP30,DIM30
.
.       Key in date range
.
KDATER  MOVE       "- INVOICE DATE SEQUENCE ***",DIM30
        DISPLAY    *P51:2,*V2LON,"- Invoice Date Sequence ",*HOFF:
                   *P1:4,*EF:
                   *P1:6,"Starting Date :"
        MOVE       SIX,CVERT
.
        CALL       PATSTRYR                      * get start of financial year
.
        UNPACK     CSTRTYR,XCENT,XYEAR,XMON,XDAY
        MOVE       XDAY,IDAY
        MOVE       XMON,IMON
        MOVE       XYEAR,IYEAR
        MOVE       XCENT,ICENT
.
        CALL       KEYDTE
        PACK       FROMDATE WITH ICENT,IYEAR,IMON,IDAY
        REP        " 0",FROMDATE
.
        DISPLAY    *P1:8,"  Ending Date :"
        MOVE       EIGHT,CVERT
KDATE2  MOVE       CDD,IDAY
        MOVE       CMM,IMON
        MOVE       CYY,IYEAR
        MOVE       CCC,ICENT
        CALL       KEYDTE
        PACK       TODATE WITH ICENT,IYEAR,IMON,IDAY
        REP        " 0",TODATE
.
        MATCH      FROMDATE,TODATE
        GOTO       MASTN2 IF NOT LESS
.
        DISPLAY    *P1:24,*EL,*B,"End Date must be >= Start Date.  ";
        CALL       HITENTER
        GOTO       KDATE2
.
NOMAST  DISPLAY    *P1:24,*EL,*B,"No Patients on File. ";
        CALL       HITENTER
        GOTO       START
.
MASTN2
MASTN2A COMPARE    ONE,IBCNMHOS
        GOTO       MASTN2B IF NOT EQUAL
.
        DISPLAY    *P1:12,*EL,"Hospital Id : ";
        MOVE       TEN5,ECOL
        MOVE       "12",EVERT
        MOVE       SP3,CKYIDEF3
        MOVE       ZERO,CKYIMAND           * Not Mandatory
        OPEN       PATHSPA1,"pathspaf"
.
        CALL       PATHSPKY
        BRANCH     EXIT,MASTN2B,START
        GOTO       MASTN2C 
.
MASTN2B MOVE       "All Hospitals",PTHSNAME
        MOVE       SP10,PTHSHOSP
.
MASTN2C CALL       CONTQST
        BRANCH     CEXIT,MASTN3:                 * yes
                         KDATER:                 * no
                         ENDIT                   * cancel
.
MASTN3  MOVE       ONE,ERRFLAG
        MOVE       ZERO,TINVBED
        MOVE       ZERO,TINVACCM
        MOVE       ZERO,TINVTHET
        MOVE       ZERO,TINVMISC
        MOVE       ZERO,TINVTOTL
        MOVE       ZERO,TINVADJM
        MOVE       ZERO,TINVGSTM
        MOVE       ZERO,TPAYCASH
        MOVE       ZERO,TPAYJRNL
        MOVE       ZERO,TPAYTOTL
        MOVE       ZERO,CRNTOTAL
        MOVE       ZERO,TMPHLFTO
        MOVE       ZERO,TMPHLFTP
        MOVE       ZERO,TMPOUTST
        MOVE       ONE,PRTFLAG
.
STDAT   PACK       KEY16 WITH FROMDATE,SP8
        CALL       RDSINV2
.
STCONT  MOVE       ZERO,CPAGENO
        MOVE       ZERO,CNOUNDLN
        MOVE       "60",CLNO
        CALL       IBACLOCK
        DISPLAY    *P1:24,*EL,"Invoice ## :",*P40:24,"Date :";
.
NEXTN   
.
RDDAT   CALL        RDKINV2
        BRANCH      OVRCD OF ENDP
.
        MATCH       PINVDTE,TODATE
        GOTO        ENDP IF LESS
.
NEXTN2  DISPLAY     *P70:24,PINVNO;
.
CNTI    MOVE        ZERO,PINVBED
        MOVE        ZERO,PINVACCM
        MOVE        ZERO,PINVGSTM
        MOVE        ZERO,PINVADJM
        MOVE        ZERO,PINVTHET
        MOVE        ZERO,PINVMISC
        MOVE        ZERO,PINVTOTL
        MOVE        ZERO,PPAYCASH
        MOVE        ZERO,PPAYJRNL
        MOVE        ZERO,PPAYTOTL
        MOVE        ZERO,ITEMGST
        MOVE        SP70,TMPHLFND
        MOVE        SP70,TMPCLMCD
        MOVE        ZERO,TMPACLMD
        MOVE        ZERO,TMPOUTSA
.
        MOVE        PINVUR,PURNO
        MOVE        PURNO,KEY8
        CALL        RDMAST1
.
        IF          IBCNMHOS=1
          MATCH       SP10,PTHSHOSP
          IF          !@EQUAL
            PACK        KEY8,PINVADM,SP8
            CALL        RDPMVX11
            BRANCH      OVRCD,NEXTN
            MATCH       PMVXMHOS,PTHSHOSP
            GOTO        NEXTN IF NOT EQUAL
          ENDIF
        ENDIF
.
        CALL        DISPDA
        BRANCH      FILTOPTN,NEXTN
.
        UNPACK      PINVDTE WITH XCENT,XYEAR,XMON,XDAY
        DISPLAY     *P13:24,*V2LON,PINVNO:
                    *P47:24,XDAY,"/",XMON,"/",XYEAR;
.
        MATCH       ZEROVISN,PINVADM
        GOTO        NEXTN IF EQUAL
.
.       Check Dtran totals against invoice data
.
        IF          IBCNUGST=0 
          MOVE        PINVAMT,FORM12P2
          SUB         PTINGSTA,FORM12P2       * don't include GST
          COMPARE     PINVTOTL,FORM12P2
          CALL        INVERR IF NOT EQUAL
        ELSE
          MOVE        PINVTOTL,FORM12P2
          COMPARE     PINVTOTL,PINVAMT
          CALL        INVERR IF NOT EQUAL
          ADD         PTINGSTJ,PINVJOUR
        ENDIF
.
        MULT        SEQ,PINVJOUR
        COMPARE     PPAYJRNL,PINVJOUR
        CALL        JRNLERR IF NOT EQUAL
.
        ADD         PINVHFDA,PINVPATA
        ADD         PINVOTHA,PINVPATA
.
        MULT        SEQ,PINVPATA
        COMPARE     PPAYCASH,PINVPATA
        CALL        CASHERR IF NOT EQUAL
.
        GOTO        NEXTN
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     
         COMPARE      ZERO,CPAGENO
         GOTO         NOMAST IF EQUAL
.
PRNTTOT  COMPARE    "57",CLNO
         CALL       HEAD IF NOT LESS
.
         COMPARE    "9",CLNO
         CALL       PAGEND IF NOT EQUAL
.
PRNT200  PRINT      *32,TMPHLFTO,*48,TINVTOTL,*64,TMPHLFTP,*80,TPAYCASH:
                    *96,TMPOUTST
         IF         CFEETYP=9
           PRINT      *112,TINVADJM;
         ENDIF
.
.        PRINT      *56,TINVACCM,*74,TINVMISC,*92,TPAYCASH,*110,TPAYTOTL
.        IF         CFEETYP=9
.          PRINT      *51,TINVADJM;
.        ENDIF
.        PRINT      *66,TINVTHET,*83,TINVTOTL,*101,TPAYJRNL,*118,CRNTOTAL
.
.
PRNT400  PRINT      *N,*N,"*** END OF REPORT ***"
.
.        Check if any errors were found
.
         BRANCH       ERRFLAG OF ENDREP
.
         PRINT        *N,"*** Errors Found in Report. ":
                      "Contact I.B.A Immediately. ":
                      "REPORT MAY BE INACCURATE ***"
.
ENDREP   DISPLAY      *P1:24,*EL,*EL,*V2LON:
                      "** Report Generation Completed **",*W;
         GOTO         ENDIT
+
.
.
.     PRINT ACTUAL DATA 
.
DISPDA  MOVE       ZERO,FILTOPTN
        MATCH      ZEROVISN,PINVADM
        GOTO       NEXT8 IF EQUAL
.
        MOVE       PGNAME,ANS
        PACK       PNAME WITH ANS,DOT,PSNAME
.
        PACK       KEY22 WITH PINVADM,PINVNO,SP6
.
.       Loop through the appropriate DTRAN for this invoice
.
        BRANCH     PINVSYS OF AAEDTR,OUTDTR,INPDTR,OTHDTR,NEXT9
        GOTO       INPDTR
.
NEXTT   BRANCH     PINVSYS OF ANXTT,ONXTT,INXTT,ONXTT,NEXT9
        GOTO       INXTT
.
.       A + E invoice
.
AAEDTR  CALL       CHKEMR00
        BRANCH     EXIT,NEXT8
.
        CALL       RDSDTRE1
.
        MOVE       PINVADM,ADANUMB
        MOVE       ADANUMB,KEY8
        CALL       RDDETA1
        MOVE       ADACLASS,ATYPE
.
ANXTT   CALL       RDKDTRE1
        BRANCH     OVRCD OF ENDT
.
        MATCH      PINVNO,ATINVNO
        GOTO       ENDT IF NOT EQUAL
.
        MATCH      ATNUMB,PINVADM
        GOTO       ENDT IF NOT EQUAL
.
        MOVE       ATPATAMT,TPATAMTT
        MOVE       ZERO,PTDTGSTM
        IF         IBCNUGST=2 | IBCNUGST=3
          MOVE       ATDTGSTM,PTDTGSTM    * GST for AAE
        ENDIF
        MOVE       ZERO,PTDTGSTL
        BRANCH     ATRECTYP OF MISC,PYMNT,JRNAL,PYMNT,MISC
        GOTO       ANXTT
.
.       Other financial invoice
.
OTHDTR  GOTO       NEXT8
.
.       MOVE       SP6,PVISITE
.       MOVE       PINVADM,PVIBILL
.       MOVE       PVIBILL,KEY8
.       CALL       RDVISA1
.
.       MOVE       "out",OSTFILE
.       CLOSE      OUTDTRO1
.       PACK       CFNAME,OSTFILE,FILDTRO1
.       OPEN       OUTDTRO1,CFNAME
.
.       PACK       KEY22 WITH SP2,KEY20
.       CALL       RDSDTRO1
.       GOTO       ONXTT
.
.       Outpatient invoice
.
OUTDTR  MOVE       SP6,PVISITE
        MOVE       PINVADM,OBAOUTNO
        MOVE       OBAOUTNO,KEY8
        CALL       RDVISA1
.
        MOVE       "out",OSTFILE
        MOVE       PVISITE,KEY6
        CALL       RDSITA1
.
        PACK       CFNAME,OSTFILE,FILBB1A1
        CALL       OPOTBB11
.
        CLOSE      OUTDTRO1
        PACK       CFNAME,OSTFILE,FILDTRO1
        OPEN       OUTDTRO1,CFNAME
.
        MOVE       OBAOUTNO,KEY8
        CALL       RDBOKB1
        MOVE       OBACLASS,ATYPE
.
        CLOSE      OUTBB1A1
.
        CALL       CHKOUT00 
        BRANCH     EXIT,NEXT8
.
        CALL       RDSDTRO1
ONXTT   CALL       RDKDTRO1
        BRANCH     OVRCD OF ENDT
.
        MATCH      PINVNO,OTINVNO
        GOTO       ENDT IF NOT EQUAL
.
        MATCH      OTNUMB,PINVADM
        GOTO       ENDT IF NOT EQUAL
.
        MOVE       OTPATAMT,TPATAMTT
        IF         IBCNUGST=2 | IBCNUGST=3
          MOVE       OTDTGSTM,PTDTGSTM          * GST for OUT
        ENDIF
        MOVE       ZERO,PTDTGSTL
        BRANCH     OTRECTYP OF MISC,PYMNT,JRNAL,MISC,ADJM,THET
        GOTO       ONXTT
.
.       In-patient invoice
.
INPDTR  MOVE       PINVADM,KEY8
        CALL       RDMISS1
.
        CALL       CHKINP00
        BRANCH     EXIT,NEXT8
.
        CALL       RDSDTRN1
INXTT   CALL       RDKDTRN1
        BRANCH     OVRCD OF ENDT
.
        MATCH      PINVNO,TREF
        GOTO       ENDT IF NOT EQUAL
.
        MATCH      TADMNO,PINVADM
        GOTO       ENDT IF NOT EQUAL
.
        BRANCH     TRECTYPE OF ACCM,THET,MISC,MISC,PYMNT,JRNAL,ADJM,THET
        GOTO       INXTT
.
ADJM    ADD        TPATAMTT,PINVADJM
        ADD        TPATAMTT,PINVTOTL
        GOTO       NEXTT
.
MISC    ADD        TPATAMTT,PINVMISC
        ADD        TPATAMTT,PINVTOTL
        IF         IBCNUGST=2
          ADD        PTDTGSTM,PINVMISC
          ADD        PTDTGSTM,PINVTOTL
        ENDIF
        IF         IBCNUGST=3
          ADD        PTDTGSTM,PINVMISC
          ADD        PTDTGSTM,ITEMGST
        ENDIF
        GOTO       NEXTT
.
ACCM    ADD        TPATAMTT,PINVACCM
        ADD        TPATAMTT,PINVTOTL
        COMPARE    THREE,IBCNUGST
        GOTO       LUMPS IF EQUAL        * GST Amt from invoice total
.
        IF         IBCNUGST=2
          ADD        PTDTGSTM,PINVACCM
          ADD        PTDTGSTM,PINVTOTL
        ENDIF
.
.       If the accomodation amount is no zero, then add in bed days,
.       otherwise check transfer file to see if this a zero rate charge
.       or an on-leave record.
.
        COMPARE    ZERO TO TPATAMTT
        GOTO       BEDS IF NOT EQUAL
.
        PACK       IDATET WITH TTCENT,TTYEAR,TTMONTH,TTDAY    * Accom. date to
        REP        " 0",IDATET
.
.       Check the first record of the accommodation to-date. If this
.       on-leave, then this record will be a "R" record or "L" record.
.
        PACK       KEY30 WITH TADMNO,IDATET,SP20
        CALL       RDSTRAN2
        CALL       RDKTRAN2
        BRANCH     OVRCD OF BEDS
.
        CMATCH     "R",TMOVE
        GOTO       LUMPS IF EQUAL
.
        PACK       IDATET WITH TTCENT,TTYEAR,TTMONTH,TTDAY    * Accom. date to
        REP        " 0",IDATET
.
BEDS    ADD        TNODAYS,PINVBED
.
LUMPS   MOVE       PTDTLSPT,FORM12P2
        ADD        PTDTLSRB,FORM12P2      * Total lumpsum
        IF         IBCNUGST=2
          ADD        PTDTGSTL,FORM12P2    * GST of lumpsum amount
        ENDIF
.
        ADD        FORM12P2,PINVACCM
        ADD        FORM12P2,PINVTOTL
        GOTO       NEXTT
.
THET    ADD        TPATAMTT,PINVTHET
        ADD        TPATAMTT,PINVTOTL
        IF         IBCNUGST=2
          ADD        PTDTGSTM,PINVTHET
          ADD        PTDTGSTM,PINVTOTL
        ENDIF
        IF         IBCNUGST=3
          ADD        PTDTGSTM,PINVTHET
          ADD        PTDTGSTM,ITEMGST
        ENDIF
        GOTO       NEXTT
.
GVSUB   SUB        TPATAMTG,PINVACCM
        SUB        TPATAMTG,PINVTOTL
        GOTO       NEXTT
.
PYMNT   ADD        TPATAMTT,PPAYCASH
        ADD        TPATAMTT,PPAYTOTL
        GOTO       NEXTT
.
JRNAL   ADD        TPATAMTT,PPAYJRNL
        ADD        TPATAMTT,PPAYTOTL
        IF         IBCNUGST=2 | IBCNUGST=3
          ADD        PTDTGSTM,PPAYJRNL      * GST Journal
          ADD        PTDTGSTM,PPAYTOTL      * GST Journal
        ENDIF
        GOTO       NEXTT
.
ENDT    IF         IBCNUGST=3
          MOVE       PTINGSTA,PTDTGSTM
          SUB        ITEMGST,PTDTGSTM
          ADD        PTDTGSTM,PINVACCM
          ADD        PTINGSTA,PINVTOTL
        ENDIF
.
        MULTIPLY   SEQ,PPAYCASH
        MULTIPLY   SEQ,PPAYJRNL
        MULTIPLY   SEQ,PPAYTOTL
.
        ASSIGN     (PINVAMT+PINVPATA+PINVHFDA+PINVOTHA+PINVJOUR),TMPACLMD
.
        IF         IBCNUGST = 2
          ADD        PTINGSTJ,TMPACLMD
        ENDIF
.
.       MOVE       PINVTOTL,TMPOUTSA
.       SUB        PINVHFDA,TMPOUTSA
.       SUB        PPAYCASH,TMPOUTSA
.
        ASSIGN    (PINVAMT+PINVPATA+PINVHFDA+PINVOTHA+PINVJOUR+PTINCRTT),TMPOUTSA       
        IF        IBCNUGST=2
          ADD       PTINGSTJ,TMPOUTSA
        ENDIF
.
        COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
        UNPACK     PINVDTE WITH XCENT,XYEAR,XMON,XDAY
.
        PRINT      *1,PINVNO,*10,XDAY,"/",XMON,"/",XYEAR,*19,TMPHLFND:
                   *24,TMPCLMCD,*32,TMPACLMD,*48,PINVTOTL,*64,PINVHFDA:
                   *80,PPAYCASH,*96,TMPOUTSA
.
PRTFULL IF         CFEETYP=9
          PRINT      *112,PINVADJM;
        ENDIF
        ADD        ONE,CLNO
.
ACCTOTS ADD        PINVBED,TINVBED
        ADD        PINVACCM,TINVACCM
        ADD        PINVTOTL,TINVTOTL
        ADD        PINVMISC,TINVMISC
        ADD        PINVTHET,TINVTHET
        ADD        PPAYCASH,TPAYCASH
        ADD        PPAYJRNL,TPAYJRNL
        ADD        PPAYTOTL,TPAYTOTL
        ADD        PTINCRTT,CRNTOTAL
        ADD        PINVADJM,TINVADJM
        ADD        PINVGSTM,TINVGSTM
        ADD        TMPACLMD,TMPHLFTO
        ADD        PINVHFDA,TMPHLFTP
        ADD        TMPOUTSA,TMPOUTST
        GOTO       NEXT9
.
NEXT8   MOVE       ONE,FILTOPTN
NEXT9   RETURN
.
.       A cancelled invoice
.
CANCLI  
.
        COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
.
        PRINT      *1,PINVNO,*10,"-- CANCELLED --"
        ADD        ONE,CLNO
CANRET  RETURN
+
.
.       Errors found in data. Indicates a difference between Dtran file
.       and the invoice file
.
INVERR  COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
        PRINT      *1,"***** Invoice total for invoice ",PINVNO:
                   " may be incorrect. Invoice says $",PINVAMT:
                   ", Debtors Transaction says $",FORM12P2
        ADD        ONE,CLNO
        MOVE       ZERO,ERRFLAG
        RETURN
.
JRNLERR COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
        PRINT      *1,"***** Journal total for invoice ",PINVNO:
                   " may be incorrect. Invoice says $",PINVJOUR:
                   ", Debtors Transaction says $",PPAYJRNL
        ADD        ONE,CLNO
        MOVE       ZERO,ERRFLAG
        RETURN
.
CASHERR COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
        PRINT      *1,"*****    Cash total for invoice ",PINVNO:
                   " may be incorrect. Invoice says $",PINVPATA:
                   ", Debtors Transaction says $",PPAYCASH
        ADD        ONE,CLNO
        MOVE       ZERO,ERRFLAG
        RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "----------------------------------------------":
                    "------------------------------------------":
                    "--------------------------------------------"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      CALL      HEAD132
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSHOSP,"    ",PTHSNAME
          ENDIF
.
HDDAT     UNPACK    FROMDATE WITH XCENT,XYEAR,XMON,XDAY
          PRINT     *40,"STARTING DATE : ",XDAY,"/",XMON,"/",XCENT,XYEAR
          UNPACK    TODATE WITH XCENT,XYEAR,XMON,XDAY
          PRINT     *40,"  ENDING DATE : ",XDAY,"/",XMON,"/",XCENT,XYEAR,*N
.
HDCONT    PRINT    *1,"Invoice",*10,"Invoice",*19,"Hlth",*24,"Claim":
                   *38,"Hlth Fund",*56,"Invoice",*70,"Hlth Fund",*84,"Patient Pay":
                   *100,"Outstanding";
.
          IF       CFEETYP=9
            PRINT    *112,"Adj.";
          ENDIF
.
          PRINT    *N,"Number",*10,"Date",*19,"Fund",*24,"Code",*41,"Amount",*58,"Total":
                   *70,"Pay Total",*90,"Total",*105,"Amount"
.
          IF       CFEETYP=9
            PRINT    *112,"Total";
          ENDIF
.
.         PRINT    *63,"   Total",*73,"   Total":
.                  *81,"   Total",*90,"   Total",*98,"!":
.                  *99,"   Total",*108,"   Total":
.                  *117,"   Total",*126,"   Note"
.
          CALL      PAGEND
          MOVE      TEN1,CLNO
          IF        IBCNMHOS=1
            ADD       ONE,CLNO
          ENDIF
          RETURN
+
.
.         Subroutine to key in a valid date
.
KEYDTE
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      "17",CCOL          * set up position
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
.
          RETURN
+
ENDIT     CHAIN      PGM
          STOP
.
.-------------------------------------------------------------------------------
. Check emergency invoices           
.-------------------------------------------------------------------------------
CHKEMR00  MOVE      ZERO,EXIT
.
          PACK      KEY8,PINVADM,SP70
          CALL      RDEMVIS1
          BRANCH    OVRCD,CHKEMR90
.
          MOVE      EMVIFUND,TMPHLFND
          MOVE      EMVICOMP,TMPHLFND
.
          MATCH     SP70,EMVICOMP
          GOTO      CHKEMR10 IF EQUAL
          GOTO      CHKEMR10 IF EOS
.
          PACK      KEY5,ANSC,ANSL,EMVICOMP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKEMR10
.
          MOVE      ZERO,FORM1
          WHILE     FORM1 < 5
            ADD       ONE,FORM1
            LOAD      ANS,FORM1,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
            MATCH     ANSV,ANS                   * DVA code ?
            GOTO      CHKEMR50 IF EQUAL          * yes
          DO
.
.         MATCH     EMVICOMP,CVETINS
.         GOTO      CHKEMR50 IF EQUAL
.
CHKEMR10  MATCH     SP70,EMVIFUND
          GOTO      CHKEMR90 IF EQUAL
          GOTO      CHKEMR90 IF EOS
.
          PACK      KEY6,EMVIFUND,SP70
          CALL      RDFUND1
          BRANCH    OVRCD,CHKEMR90
.
          PACK      KEY6,EMVIFUND,SP70
          CALL      RDPTFX11
          BRANCH    OVRCD,CHKEMR90
.
          COMPARE   THREE,PTFXEXTR
          GOTO      CHKEMR90 IF NOT EQUAL
.
          MOVE      SP70,ECLKEY3
          MATCH     SP70,PTFXECLP
          IF        @EQUAL | @EOS
            MATCH     SP70,PTHFHGRP
            GOTO      CHKEMR90 IF EQUAL
            GOTO      CHKEMR90 IF EOS
            MOVE      PTHFHGRP,ECLKEY3
          ELSE
            MOVE      PTFXECLP,ECLKEY3
          ENDIF
.
          PACK      KEY3,ECLKEY3,SP70
          CALL      RDPTPAR1
          BRANCH    OVRCD,CHKEMR90
.
          PACK      KEY9,ECLKEY3,SP70
          CALL      RSPTPCP1
CHKEMR30  CALL      RKPTPCP1
          BRANCH    OVRCD,CHKEMR90
.
          MATCH     PTPPCODE,ECLKEY3
          GOTO      CHKEMR90 IF NOT EQUAL
.
          MATCH     "IHC",PTPPCPID
          GOTO      CHKEMR90 IF NOT EQUAL
.
CHKEMR50  PACK      KEY16,PINVNO,Z70
          CALL      RSPMECT2
          CALL      RPPMECT2
          BRANCH    OVRCD,CHKEMR99
.
          MATCH     PINVNO,PMECINVN
          GOTO      CHKEMR99 IF NOT EQUAL
.
CHKEMR90  MOVE      ONE,EXIT
CHKEMR99  RETURN
.
.-------------------------------------------------------------------------------
. Check outpatient invoices       
.-------------------------------------------------------------------------------
CHKOUT00  MOVE      ZERO,EXIT
.
          MOVE      OTBBFUND,TMPHLFND
          MOVE      OBACOMP,TMPHLFND
.
          MATCH     SP70,OBACOMP
          GOTO      CHKOUT10 IF EQUAL
          GOTO      CHKOUT10 IF EOS
.
          PACK      KEY5,ANSC,ANSL,OBACOMP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKOUT10
.
          MOVE      ZERO,FORM1
          WHILE     FORM1 < 5
            ADD       ONE,FORM1
            LOAD      ANS,FORM1,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
            MATCH     ANSV,ANS                   * DVA code ?
            GOTO      CHKOUT50 IF EQUAL          * yes
          DO
.
.         MATCH     OBACOMP,CVETINS
.         GOTO      CHKOUT50 IF EQUAL
.
CHKOUT10  MATCH     SP70,OTBBFUND
          GOTO      CHKOUT90 IF EQUAL
          GOTO      CHKOUT90 IF EOS
.
          PACK      KEY6,OTBBFUND,SP70
          CALL      RDFUND1
          BRANCH    OVRCD,CHKOUT90
.
          PACK      KEY6,OTBBFUND,SP70
          CALL      RDPTFX11
          BRANCH    OVRCD,CHKOUT90
.
          COMPARE   THREE,PTFXEXTR
          GOTO      CHKOUT90 IF NOT EQUAL
.
          MOVE      SP70,ECLKEY3
          MATCH     SP70,PTFXECLP
          IF        @EQUAL | @EOS
            MATCH     SP70,PTHFHGRP
            GOTO      CHKOUT90 IF EQUAL
            GOTO      CHKOUT90 IF EOS
            MOVE      PTHFHGRP,ECLKEY3
          ELSE
            MOVE      PTFXECLP,ECLKEY3
          ENDIF
.
          PACK      KEY3,ECLKEY3,SP70
          CALL      RDPTPAR1
          BRANCH    OVRCD,CHKOUT90
.
          PACK      KEY9,ECLKEY3,SP70
          CALL      RSPTPCP1
CHKOUT30  CALL      RKPTPCP1
          BRANCH    OVRCD,CHKOUT90
.
          MATCH     PTPPCODE,ECLKEY3
          GOTO      CHKOUT90 IF NOT EQUAL
.
          MATCH     "IHC",PTPPCPID
          GOTO      CHKOUT30 IF NOT EQUAL
.
CHKOUT50  PACK      KEY16,PINVNO,Z70
          CALL      RSPMECT2
          CALL      RPPMECT2
          BRANCH    OVRCD,CHKEMR99
.
          MATCH     PINVNO,PMECINVN
          GOTO      CHKOUT99 IF NOT EQUAL 
.
CHKOUT90  MOVE      ONE,EXIT
CHKOUT99  RETURN
.-------------------------------------------------------------------------------
. Check inpatient invoices
.-------------------------------------------------------------------------------
CHKINP00  MOVE      ZERO,EXIT
.
          MOVE      AFUNDH,TMPHLFND
          MOVE      ACLAIM,TMPCLMCD
.
          MATCH     SP70,ACLAIM
          GOTO      CHKINP10 IF EQUAL
          GOTO      CHKINP10 IF EOS
.
          PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKINP10
.
          MOVE      ZERO,FORM1
          WHILE     FORM1 < 5
            ADD       ONE,FORM1
            LOAD      ANS,FORM1,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
            MATCH     ANSV,ANS                   * DVA code ?
            GOTO      CHKINP50 IF EQUAL          * yes
          DO
.
.         MATCH     ACLAIM,CVETINS
.         GOTO      CHKINP50 IF EQUAL
.
CHKINP10  MATCH     SP70,AFUNDH
          GOTO      CHKINP90 IF EQUAL
          GOTO      CHKINP90 IF EOS
.
          PACK      KEY6,AFUNDH,SP70
          CALL      RDFUND1
          BRANCH    OVRCD,CHKINP90
.
          PACK      KEY6,AFUNDH,SP70
          CALL      RDPTFX11
          BRANCH    OVRCD,CHKINP90
.
          COMPARE   THREE,PTFXEXTR
          GOTO      CHKINP90 IF NOT EQUAL
.
          MOVE      SP70,ECLKEY3
          MATCH     SP70,PTFXECLP
          IF        @EQUAL | @EOS
            MATCH     SP70,PTHFHGRP
            GOTO      CHKINP90 IF EQUAL
            GOTO      CHKINP90 IF EOS
            MOVE      PTHFHGRP,ECLKEY3
          ELSE
            MOVE      PTFXECLP,ECLKEY3
          ENDIF
.
          PACK      KEY3,ECLKEY3,SP70
          CALL      RDPTPAR1
          BRANCH    OVRCD,CHKINP90
.
          PACK      KEY9,ECLKEY3,SP70
          CALL      RSPTPCP1
CHKINP30  CALL      RKPTPCP1
          BRANCH    OVRCD,CHKINP90
.
          MATCH     PTPPCODE,ECLKEY3
          GOTO      CHKINP90 IF NOT EQUAL
.
          MATCH     "IHC",PTPPCPID
          GOTO      CHKINP30 IF NOT EQUAL
.
CHKINP50  PACK      KEY16,PINVNO,Z70
          CALL      RSPMECT2
          CALL      RPPMECT2
          BRANCH    OVRCD,CHKINP99
.
          MATCH     PINVNO,PMECINVN
          GOTO      CHKINP99 IF NOT EQUAL
.
CHKINP90  MOVE      ONE,EXIT
CHKINP99  RETURN
.
.-------------------------------------------------------------------------------
.
          INC       PATCODDS
          INC       PATSELFN
          INC       PATSTRYR
          INC       PATHSPKY
.
          INC       AAEDE1IO/INC
          INC       AAEDTRIO/INC
          INC       EMRVISIO/INC
          INC       IBASECIO/INC
          INC       IBAPASIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       PATCODIO/INC
          INC       PATDRGIO/INC
          INC       PATDTRIO/INC
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATHSPIO/INC
          INC       PATINVIO/INC
          INC       PATPARIO/INC
          INC       PATPCPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PMSECTIO/INC
          INC       PMSVX1IO/INC
.
          INC       STD001IO

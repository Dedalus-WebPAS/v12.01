.******************************************************************************
.* System     : Hospital Records                                              *
.* Program    : IBAHOS78                                                      *
.* Desc       : Records Culling Report                                        *
.******************************************************************************
.* Date       : 13/11/2003                                                    *
.* Author     : Mike Laming                                                   *
.* Modifications :                                                            *
.*                                                                            *
.******************************************************************************
.* V12.00.01  20/05/2025 Thanh T        TSK 0962204                           *
.*            Changed to print Mr Filing Code to appear on the 1st line of    *
.*            Filing sequence with location option (4)                        * 
.* V11.05.01  06/02/2025 Thanh T        TSK 0946997                           *
.*            Added 2 new options:                                            *
.*            UR Sequence with Location                                       *  
.*            Filing Sequence with Location                                   *  
.******************************************************************************
.* V10.15.01  18/10/2019 Tracey Nguyen  TSK 0876818                           *
.*            Added patient DOB column                                        *
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved call to TFILENAM to INIT0000                              *
.* V10.03.01  27/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*                                                                            *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       AAEDI1FD/INC
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATBAYFD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATVISFD/INC
          INC       MRTMASFD/INC 
          INC       MRTLOCFD/INC 
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.*********************************************************************
.         Temporary File Definition
.*********************************************************************
.
.         Filename : hos78axx.dat          (xx = port id)
.
SEQTEMP1  IFILE SQL, FIXED=100, KEY="U1-20"                      *(KEY20)
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TMPSEQ    DIM       20          20          1       Report final sequence
TMPURNO   DIM       8           8          21       Patient U/R number
TMPADTE   DIM       8           8          29       Admission/Visit Date
TMPDDTE   DIM       8           8          37       Discharge Date
TMPAAE    FORM      1           1          45       A&E Records
TMPOUT    FORM      1           1          46       Outpatient Records
TMPINP    FORM      1           1          47       Inpatient Records
TMPBAY    DIM       3           3          48       MR Bay Code
TMPTDF    DIM       11          11         51       Terminal Digit Filing Code
TMPSPRE   DIM       39          39         62       Spare
.
.End of Record                            100
.
.*********************************************************************
.*                           GLOBAL VARIABLES                        *
.*********************************************************************
CMDLINE   DIM       80
DD1       DIM       2
DD2       DIM       2
DD3       DIM       2
DD4       DIM       2
DIM10A    DIM       10
DIM10B    DIM       10
.
FORM5     FORM      5
FORM8     FORM      8
FROMDTE   DIM       8
FROMDTTX  DIM       10
FROMURNO  DIM       8
FROMURNF  FORM      8
FROMURTX  DIM       8
.
PAGENO    FORM      5
PREVURNO  DIM       8
PREVURNF  FORM      8
PRTSUMM   FORM      1
RECNO     FORM      5
RECINP    FORM      5
RECOUT    FORM      5
RECAAE    FORM      5
XXXNO     FORM      5
.
SAVURNO   DIM       8
SAVADTE   DIM       8
SAVDDTE   DIM       8
SAVAAE    FORM      1
SAVOUT    FORM      1
SAVINP    FORM      1
SAVBAY    DIM       3
SAVTDF    DIM       11
SUMMTEXT  DIM       30
.
TEMPFILE  DIM       8
TOOODTE   DIM       8
TOOODTTX  DIM       10
TOOOURNO  DIM       8
TOOOURNF  FORM      8
TOOOURTX  DIM       8
.
XPSNAME   DIM       15
XPGNAME   DIM       20
HOMLOCDS  DIM       20
HOMLOCD4  DIM       4
CURLOCDS  DIM       20
+
.*********************************************************************
.*                                 CONSTANTS                         *
.*********************************************************************
END       INIT      "End       "
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
UKEY2     INIT      " 100 U1-20"
.
START     INIT      "Start     "
SLSH      INIT      "/"
.
PRGID     INIT      "IBAHOS78"
PRGNAM    INIT      "RECORDS CULLING REPORT"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
ML0000    CALL      INIT0000                * display header and open files
.
ML1000    CALL      MENU0000                * select report or summary    
          BRANCH    EXIT,ML9000             * EXIT = 1 if 0 chosen in menu
.
ML2000    CALL      RANG0000                * input report range/s         
          BRANCH    EXIT,ML1000
.
          CALL      TEMP0000                * prepare Temp file
.
ML3000    CALL      EXTR0000                * extract data to temp file
          BRANCH    EXIT,ML1000
.
ML4000    MOVE      "99",CLNO               * Force page overflow
          BRANCH    MOPTION,ML5000:         * Option 1 - UR sequence
                            ML5000:         * Option 2 - UR sequence loc
                            ML5000:         * Option 3 - Filing sequence
                            ML5000:         * Option 4 - Filing sequence loc
                            ML6000          * Option 5 - Totals
          GOTO      ML1000
.
ML5000    IF        MOPTION = 2
            CALL      PRNTL000              * print report with location Opt 2
          ELSE
            IF        MOPTION = 4         
              CALL      PRNTN000            * print report with location Opt 4
            ELSE
              CALL      PRNT0000            * print report opt 1, 3
            ENDIF
          ENDIF 
.
ML6000    CALL      SUMM0000                * print the report totals
          GOTO      ML1000
.
ML9000    CALL      KILL0000                * delete temporary file
.
ML9999    CHAIN     PGM                     * chain back to program
.
+
.******************************************************************************
.*                              INIT0000                                      *
.*                    display header and open files                           *
.******************************************************************************
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P64:24,"aaedi1af";
          OPEN      AAEDI1A1,"aaedi1af"
.
          DISPLAY   *P64:24,"patbaysf";
          OPEN      PATBAYS2,"patbaysf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          OPEN      MRTMASA3,"mrtmasaf"
          OPEN      MRTLOCA4,"mrtlocaf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
.
          MOVE      ZERO,PAGENO             * initialize page no
          PACK      TMPSPRE,SP70,SP20
.
          CALL     IBACLOCK
.
INIT9999  RETURN
+
.******************************************************************************
.*                              MENU0000                                      *
.*                      select report or summary                              *
.******************************************************************************
MENU0000  MOVE      ZERO,EXIT    
          MOVE      ZERO,PRTSUMM
          UNPACK    SP30,FROMURNO,FROMURTX,FROMDTE,FROMDTTX
          UNPACK    SP30,TOOOURNO,TOOOURTX,TOOODTE,TOOODTTX
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Culling Report - UR sequence":
                    *P1:6,*V2LON,"2",*HOFF," = Culling Report - UR sequence":
                                           " with location":
                    *P1:7,*V2LON,"3",*HOFF," = Culling Report - Filing seq.":
                    *P1:8,*V2LON,"4",*HOFF," = Culling Report - Filing seq.":
                                           " with location":
                    *P1:9,*V2LON,"5",*HOFF," = Culling Report Summary":
                    *P1:11,"Select Option : "
.
MENU1000  KEYIN     *P17:11,*DV,UNDLN1:
                    *P17:11,*V2LON,MOPTION
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL       * exit
.
          BRANCH    MOPTION,MENU9999,MENU9999,MENU9999,MENU9999,MENU9999
          BEEP
          GOTO      MENU1000
.
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.******************************************************************************
.*                              RANG0000                                      *
.*                     input report range/s screen                            *
.******************************************************************************
RANG0000  MOVE      ZERO,EXIT
.
          DISPLAY   *P1:4,*EF,"U/R No. - From : ",FROMURTX:
                    *P1:5,*EL,"        - To   : ",TOOOURTX:
                    *P1:7,*EL,"Dates   - From : ",FROMDTTX:
                    *P1:8,*EL,"        - To   : ",TOOODTTX;
.
          MATCH     FROMURTX,START
          IF        @EQUAL
            MOVE      SP8,FROMURNO
          ENDIF
          MATCH     TOOOURTX,END
          IF        @EQUAL
            MOVE      SP8,TOOOURNO
          ENDIF
.
          MATCH     START,FROMDTTX
          IF        @EQUAL
            MOVE      SP8,FROMDTE
          ENDIF
          MATCH     END,TOOODTTX
          IF        @EQUAL
            MOVE      SP8,TOOODTE
          ENDIF
.
.                                           * keyin "From U/R"
RANG1000  KEYIN     *P18:4,*EL,*V2LON,*JR,FROMURNF;
          MOVE      FROMURNF,FROMURNO
          MOVE      FROMURNF,FROMURTX
          COMPARE   ZERO,FROMURNF
          IF        @EQUAL | @EOS
            MOVE      SP8,FROMURNO
            MOVE      START,FROMURTX        * "Start"
            DISPLAY   *P18:4,*EL,FROMURTX
          ENDIF
.
.                                           * keyin "To U/R"
RANG2000  KEYIN     *P18:5,*EL,*V2LON,*JR,TOOOURNF;
          MOVE      TOOOURNF,TOOOURNO
          MOVE      TOOOURNF,TOOOURTX
          COMPARE   ZERO,TOOOURNF
          IF        @EQUAL | @EOS
            MOVE      "99999999",TOOOURNO
            MOVE      TOOOURNO,TOOOURNF
            MOVE      END,TOOOURTX          * "End   "
            DISPLAY   *P18:5,*EL,TOOOURTX
          ENDIF
.
.                                           * keyin "From Date"
RANG3000  MOVE      SEVEN,CVERT
          MOVE      TEN8,CCOL
          UNPACK    FROMDTE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10A,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
          DISPLAY   *P18:7,*EL,DIM10A  
          MOVE      ZERO,CHIGHLT
          MOVE      ONE,CCANLDTE
.
          CALL      KEYDATE                 * keyin the "From Date"
          BRANCH    OVRCD,RANG3500
          PACK      FROMDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FROMDTTX
          GOTO      RANG4000
.
RANG3500  MOVE      START,FROMDTTX
          DISPLAY   *P18:7,*EL,FROMDTTX
          MOVE      SP8,FROMDTE
.
.                                           * keyin "To Date"
RANG4000  MOVE      EIGHT,CVERT
          MOVE      TEN8,CCOL
          REP      "~ ",TOOODTE
          UNPACK    TOOODTE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10B,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
          DISPLAY   *P18:8,*EL,DIM10B
          MOVE      ZERO,CHIGHLT
          MOVE      ONE,CCANLDTE
.         
          CALL      KEYDATE                 * keyin the "To Date"
          BRANCH    OVRCD,RANG4500
          PACK      TOOODTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,TOOODTTX
.
          REP       " 0",TOOODTE
          GOTO      RANG5000
.
RANG4500  MOVE      END,TOOODTTX
          DISPLAY   *P18:8,*EL,TOOODTTX
          PACK      TOOODTE,CCC,CYY,CMM,CDD
          REP       " 0",TOOODTE
.
.
RANG5000  MOVE      ZERO,EXIT
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,RANG9999,RANG1000,RANG9000
          GOTO      RANG5000
.
RANG9000  MOVE      ONE,EXIT
.
RANG9999  RETURN
+
.******************************************************************************
.*                              TEMP0000                                      *
.*                        prepare Temp file                                   *
.******************************************************************************
TEMP0000  CALL      KILL0000                * remove existing file
          BRANCH    EXIT,TEMP5000           * its an Oracle D/B
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY2
          EXECUTE   CMDLINE,TASKID          * create temporary index file
.
TEMP5000  OPEN      SEQTEMP1,TEMPFILE       * open temp index file
.
TEMP9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by: CREA0000  *
.*               close and erase the temporary file               ML0000    *
.****************************************************************************
.
KILL0000  MOVE      ZERO,EXIT
          CLOSE     SEQTEMP1                * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE  * set file erase command
          EXECUTE   CMDLINE,TASKID          * erase temp file
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO          * Check if file still exists
          OPEN      SEQTEMP1,TEMPFILE
          BRANCH    OVRCD,KILL9999          * File does not exist
.
.*                                          * its an Oracle D/B - delete records
.
KILL1000  DISPLAY   *P1:24,*EL,"** Deleting tempfile records ***";
          MOVE      ZERO,FORM5
.
KILL2000  PACK      KEY20,SP20              * start at the first record
          CALL      RSTEMX1
          CALL      RKTEMX1
          BRANCH    OVRCD,KILL9000          * file now empty
.
          PACK      KEY20,TMPSEQ,SP20
          CALL      DETEMX1
          ADD       ONE,FORM5
          DISPLAY   *P40:24,*EL,FORM4," deleted";
          GOTO      KILL2000
.
KILL9000  MOVE      ONE,EXIT
.
KILL9999  RETURN
+
.******************************************************************************
.*                              EXTR0000                                      *
.*                        Extract Visits in UR Range                          *
.******************************************************************************
EXTR0000  MOVE      ZERO,EXIT
          MOVE      ZERO,RECNO
          MOVE      ZERO,RECAAE
          MOVE      ZERO,RECINP
          MOVE      ZERO,RECOUT
          MOVE      ZERO,XXXNO
          MOVE      ZERO,PREVURNO
          CALL      CLRT0000                * cleat temp file data fields
          PACK      KEY24,FROMURNO,SP25
          CALL      RDSVISA2
.
EXTR1000  CALL      RDKVISA2                * read next visit record
          BRANCH    OVRCD,EXTR9000
.
          ADD       ONE,XXXNO
          ASSIGN    (XXXNO%60),FORM5
          COMPARE   ONE,FORM5
          CALL      TOTS0000 IF EQUAL       * display record counts
.
          MATCH     PVIURNO,TOOOURNO        * within U/R range ?
          GOTO      EXTR9000 IF LESS
.
          MOVE      PREVURNO,PREVURNF
          IF        PREVURNF <> 0
            MATCH     PVIURNO,PREVURNO      * same U/R ?
            GOTO      EXTR5000 IF NOT EQUAL * no - write the previous one
          ENDIF
. 
.                                           * process the next visit
EXTR2000  MOVE      PVIURNO,PREVURNO        * save this UR No.
.
          BRANCH    PVITYPE,EXTR2100:       * A&E Event/Visit
                            EXTR2200:       * Outpatient Visit
                            EXTR2300        * Inpatient Admission
.
          GOTO      EXTR1000                * bypass financial records
.
.                                           * A&E Event / Visit
.
EXTR2100  CALL      STOR0000                * store visit data into Temp fields
          PACK      KEY8,DPVIBILL,SP8
          CALL      RDDISA1                 * read AAE discharge
          BRANCH    OVRCD,EXTR1000          * no discharge
.
          MOVE      ADSDATE,TMPDDTE
          GOTO      EXTR1000                * return to read next record
.
.                                           * Outpatient Visit
.
EXTR2200  CALL      STOR0000                * store visit data into Temp fields
          GOTO      EXTR1000                * return to read next record
.
.                                           * Inpatient Admission
.
EXTR2300  MATCH     "00000000",PVIDATE      * check if a pre-admission
          GOTO      EXTR1000 IF EQUAL       * bypass pre-admissions
.
          CALL      STOR0000                * store visit data into Temp fields
          PACK      KEY8,DPVIBILL,SP8
          CALL      RDMISS1                 * read the admission
          BRANCH    OVRCD,EXTR1000
.
          IF        ASTAT = 1 | ASTAT = 2 | ASTAT = 4
            GOTO      EXTR2350
          ENDIF
          IF        ASTAT = 3
            CALL      RDDSCH1               * read Discharge record
            BRANCH    OVRCD,EXTR2350
            MOVE      DDATE,TMPDDTE
          ENDIF
          GOTO      EXTR1000                * return to read next record
.
EXTR2350  PACK      TMPDDTE,CCC,CYY,CMM,CDD
          REP       " 0",TMPDDTE
          GOTO      EXTR1000                * return to read next record
.
.          
.                                           * write temp record
EXTR5000  MOVE      ZERO,EXIT
.                                           * check if a record has been set up
          MATCH     SP20,TMPSEQ
          GOTO      EXTR6000 IF EQUAL
.                                           * check if within date range
          MATCH     FROMDTE,TMPDDTE
          GOTO      EXTR6000 IF LESS        * visit date less than FROM date
.
          MATCH     TOOODTE,TMPDDTE
          GOTO      EXTR6000 IF NOT LESS    * TO date less than Visit/Disch Date
.
          PACK      KEY20,TMPSEQ,SP20
          CALL      RDTEMX1
          IF        OVRCD = 0
.                                           * should never happen
            MOVE      SAVURNO,TMPURNO
            MOVE      SAVADTE,TMPADTE
            MOVE      SAVDDTE,TMPDDTE
            MOVE      SAVAAE,TMPAAE
            MOVE      SAVOUT,TMPOUT
            MOVE      SAVINP,TMPINP
            MOVE      SAVBAY,TMPBAY
            MOVE      SAVTDF,TMPTDF
            CALL      UPTEMX1
          ELSE
            CALL      WRTEMX1
            ADD       ONE,RECNO
          ENDIF
.
          ADD       TMPAAE,RECAAE
          ADD       TMPINP,RECINP
          ADD       TMPOUT,RECOUT
.
EXTR6000  CALL      CLRT0000
          GOTO      EXTR2000
.
.
EXTR9000  IF        RECNO = 0
            DISPLAY   *P1:24,"No records found - ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ELSE
            CALL      TOTS0000              * display record count
          ENDIF
.
EXTR9999  RETURN
+
.******************************************************************************
.*                              CLRT0000                                      *
.*                    clear temp file data fields                             *
.******************************************************************************
CLRT0000  MOVE      ZERO,EXIT
.                                           * store the data
          MOVE      SP20,TMPSEQ
          MOVE      SP8,TMPURNO
          MOVE      SP8,TMPADTE
          MOVE      SP8,TMPDDTE
          MOVE      ZERO,TMPAAE
          MOVE      ZERO,TMPOUT
          MOVE      ZERO,TMPINP
          MOVE      SP3,TMPBAY
          MOVE      SP20,TMPTDF
          PACK      TMPSPRE,SP70
.
CLRT9999  RETURN
+
.******************************************************************************
.*                              STOR0000                                      *
.*                    store visit data into Temp fields                       *
.******************************************************************************
STOR0000  MOVE      ZERO,EXIT
.                                           * store the data
          MOVE      PVIURNO,TMPURNO
.                                           * develop report sequence
          PACK      TMPSEQ,TMPURNO,SP20
          IF        MOPTION = 3 | MOPTION = 4
            UNPACK    TMPURNO,DD1,DD2,DD3,DD4
            CALL      GBAY0000              * get MR Filing Bay
            PACK      TMPTDF,DD4,SP1,DD3,SP1,DD2,SP1,DD1
            PACK      TMPSEQ,TMPBAY,TMPTDF,SP20
          ENDIF
.
          MOVE      PVIDATE,TMPADTE
          MATCH     PVIDATE,TMPDDTE
          IF        @LESS
            MOVE      PVIDATE,TMPDDTE
          ENDIF
.
          STORE     ONE,PVITYPE,TMPAAE,TMPOUT,TMPINP
.
.                                           * save the data just in case
          MOVE      TMPURNO,SAVURNO
          MOVE      TMPADTE,SAVADTE
          MOVE      TMPDDTE,SAVDDTE
          MOVE      TMPAAE,SAVAAE
          MOVE      TMPOUT,SAVOUT
          MOVE      TMPINP,SAVINP
          MOVE      TMPBAY,SAVBAY
          MOVE      TMPTDF,SAVTDF
.
STOR9999  RETURN
+
.******************************************************************************
.*                              GBAY0000                                      *
.*                    Get the Filing Bay for given UR                         *
.******************************************************************************
GBAY0000  MOVE      SP3,TMPBAY
.
.         Get the last two digits of the U/R number
.
          REP       " 0",DD4
          REP       " 0",DD3
          MATCH     SP2,DD2
          GOTO      GBAY1000 IF EQUAL
          REP       " 0",DD2
GBAY1000  MATCH     SP2,DD1
          GOTO      GBAY2000 IF EQUAL
          REP       " 0",DD1
.
.         Check for a record in the bay code file
.
GBAY2000  PACK      KEY5,DD4,SP3
          CALL      RDSBAYS2
          CALL      RDKBAYS2
          BRANCH    OVRCD,GBAY9999
          MATCH     DD4,BAYUR
          GOTO      GBAY9999 IF NOT EQUAL
.
          PACK      TMPBAY,BAYCODE,SP3
.
GBAY9999  RETURN
+
.******************************************************************************
.*                              TOTS0000                                      *
.*                    Display running totals                                  *
.******************************************************************************
TOTS0000  MOVE      ZERO,EXIT
.
          DISPLAY   *P1:22,*EF,"Scanning - U/R : ",PVIURNO;
          DISPLAY   *P1:18,*EL,"U/Rs found     : ",RECNO;
          IF        RECINP > 0
            DISPLAY   *P1:20,*EL,"    Inpatients : ",RECINP;
          ENDIF
          IF        RECOUT > 0
            DISPLAY   *P27:20,*EL,"  Outpatients : ",RECOUT;
          ENDIF
          IF        RECAAE > 0
            DISPLAY   *P53:20,*EL,"  A&E : ",RECAAE;
          ENDIF
.
TOTS9999  RETURN
+
.******************************************************************************
.*                              SUMM0000                                      *
.*                        Print the Summary Report                            *
.******************************************************************************
SUMM0000  MOVE      "SUMMARY",SUMMTEXT
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
SUMM1000  PRINT     *1,"|",*132,"|",*N:
                    *1,"|",*3," Total U/Rs found - ",RECNO,*132,"|",*N:
                    *1,"|",*3,"       Inpatients - ",RECINP:
                    *32,"A&E Patients - ",RECAAE: 
                    *56,"Outpatients  - ",RECOUT,*132,"|",*N,*1,"|",*132,"|"
.
SUMM9000  CALL      UND132
          PRINT     *1,"End of Report"
.
SUMM9999  RETURN
+
.-----------------------------------------------------------------------------
.* Print the detail report with location for option 1 and 3
.-----------------------------------------------------------------------------
PRNT0000  MOVE      "99",CLNO               * Force page overflow
          MOVE      ZERO,XXXNO
          MOVE      SP30,SUMMTEXT
          IF        MOPTION = 3
            MOVE      "Terminal Digit Filing seq.",SUMMTEXT
          ENDIF
.
          DISPLAY   *P1:23,*EF,*P1:24,"Printing Report"
.
          MOVE      SP20,KEY20
          CALL      RSTEMX1                 * position on first record
.
PRNT1000  CALL      RKTEMX1
          BRANCH    OVRCD,PRNT9000          * end of file
.
          MOVE      TMPURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD = 1
            MOVE      SP30,PSNAME
            MOVE      SP30,PGNAME
          ENDIF
.
          COMPARE   "59",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          ADD       ONE,XXXNO
          ASSIGN    (XXXNO%10),FORM5
          IF        FORM5 = 1
            DISPLAY   *P19:24,XXXNO;        * display record counts
          ENDIF
.
PRNT2000  UNPACK    TMPDDTE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10A,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
          REP       " 0",DIM10A
.
          MATCH     SP70,PBDATE
          IF        @EQUAL | @EOS
            MOVE      SP70,DIM10B
          ELSE
            UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10B,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
            REP       " 0",DIM10B
          ENDIF
.
          PRINT     *1,"|",*3,TMPURNO:
                    *13,"|",*15,PSNAME,*37,PGNAME:
                    *64,"|",*65,DIM10B:
                    *75,"|",*77,DIM10A:
                    *88,"| ";
.
          IF        TMPINP = 1
            PRINT     *90,"INP";
          ENDIF
          IF        TMPAAE = 1
            PRINT     *96,"A&E";
          ENDIF
          IF        TMPOUT = 1
            PRINT     *102,"OUT";
          ENDIF
.
          IF        MOPTION = 3 
            PRINT     *110,"| ",TMPBAY,*116,"| ",TMPTDF;
          ENDIF
.
          PRINT     *132,"|"
          ADD       ONE,CLNO
          GOTO      PRNT1000
.
PRNT9000  CALL      UND132
.
PRNT9999  RETURN
+
.****************************************************************************
.*                              HEAD0000               Called by: PRNT0000  *
.*                      print the report headings                 ML0000    *
.****************************************************************************
HEAD0000  CALL      HEAD132                 * print the page header
.
          PRINT     *1,"U/R  - From : ",FROMURTX,*27,"to : ",TOOOURTX,*N:
                    *1,"Date - From : ",FROMDTTX,*27,"to : ",TOOODTTX:
                    *46,SUMMTEXT
.
          CALL      UND132
.
          PRINT     *1,"|   U/R No":
                    *13,"| Patient Surname       Given Names":
                    *64,"|Pat. DOB  |Last Contact| Visit Types ";

.
          IF        MOPTION = 3
            PRINT     *110,"| BAY | MR Filing Code";
          ENDIF
.
          PRINT     *132,"|"
.
          CALL      UND132
.
          MOVE      EIGHT,CLNO
.
HEAD9999  RETURN
+
.-----------------------------------------------------------------------------
.* Print the detail report with location for option 2 and 4
.-----------------------------------------------------------------------------
PRNTL000  MOVE      "99",CLNO               * Force page overflow
          MOVE      ZERO,XXXNO
          MOVE      SP30,SUMMTEXT
          IF        MOPTION = 4
            MOVE      "Terminal Digit Filing seq.",SUMMTEXT
          ENDIF
.
          DISPLAY   *P1:23,*EF,*P1:24,"Printing Report"
.
          MOVE      SP70,KEY20
          CALL      RSTEMX1                 * position on first record
.
PRNTL100  CALL      RKTEMX1
          BRANCH    OVRCD,PRNTL900          * end of file
.
          MOVE      TMPURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD = 1
            MOVE      SP70,PSNAME
            MOVE      SP70,PGNAME
          ENDIF
.
          COMPARE   "59",CLNO
          CALL      HEADL000 IF NOT LESS
.
          ADD       ONE,XXXNO
          ASSIGN    (XXXNO%10),FORM5
          IF        FORM5 = 1
            DISPLAY   *P19:24,XXXNO;        * display record counts
          ENDIF
.
PRNTL200  MOVE      PSNAME,XPSNAME
          MOVE      PGNAME,XPGNAME
.
          MATCH     SP70,PBDATE
          IF        @EQUAL | @EOS
            MOVE      SP70,DIM10B
          ELSE
            UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10B,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
            REP       " 0",DIM10B
          ENDIF
.
          PACK      KEY20,TMPURNO,SP70
          CALL      RSMRMAS3
PRNTL500  CALL      RKMRMAS3                    * Reads MRT master file
          BRANCH    OVRCD,PRNTL100
.
          MATCH     MRMAURNO,TMPURNO
          GOTO      PRNTL100 IF NOT EQUAL
.
          MATCH     SP70,MRMAEPDT
          IF        @EQUAL | @EOS
            MOVE      SP70,DIM10A
          ELSE
            UNPACK    MRMAEPDT,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10A,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
            REP       " 0",DIM10A
          ENDIF
.
          MOVE      SP70,HOMLOCDS
          MATCH     SP70,MRMAHLOC
          IF        !@EQUAL 
            PACK      KEY7,MRMAHLOC,MRMAOHOS,SP70
            CALL      RDMRLOC4
            IF        OVRCD = 0
              MOVE      MRLODESC,HOMLOCDS
            ENDIF
          ENDIF
.
          MOVE      SP70,CURLOCDS
          MATCH     SP70,MRMACLOC
          IF        !@EQUAL 
            PACK      KEY7,MRMACLOC,MRMACHOS,SP70
            CALL      RDMRLOC4
            IF        OVRCD = 0
              MOVE      MRLODESC,CURLOCDS
            ENDIF 
          ENDIF
.
          PRINT     *1,"|",*2,TMPURNO:
                    *11,"|",*13,DMRMAVOL:
                    *15,"|",*16,XPSNAME:
                    *35,XPGNAME:
                    *56,"|",*57,DIM10B:
                    *67,"|",*69,DIM10A:
                    *80,"|",*82,MRMADOTY:
                    *89,"|",*90,HOMLOCDS:
                    *110,"|",*111,CURLOCDS:
                    *132,"|"
.
          ADD       ONE,CLNO
.
          IF        MOPTION = 4 
            PRINT     *1,"|",*110,"|",*111,TMPTDF:
                      *132,"|"
            ADD       ONE,CLNO
          ENDIF
.
          GOTO      PRNTL500
.
PRNTL900  CALL      UND132
.
PRNTL999  RETURN
+
.-----------------------------------------------------------------------------
.* Print the detail report with location for option 4
.-----------------------------------------------------------------------------
PRNTN000  MOVE      "99",CLNO               * Force page overflow
          MOVE      ZERO,XXXNO
          MOVE      SP70,SUMMTEXT
          MOVE      "Terminal Digit Filing seq.",SUMMTEXT
.
          DISPLAY   *P1:23,*EF,*P1:24,"Printing Report"
.
          MOVE      SP70,KEY20
          CALL      RSTEMX1                 * position on first record
.
PRNTN100  CALL      RKTEMX1
          BRANCH    OVRCD,PRNTN900          * end of file
.
          MOVE      TMPURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD = 1
            MOVE      SP70,PSNAME
            MOVE      SP70,PGNAME
          ENDIF
.
          COMPARE   "59",CLNO
          CALL      HEADN000 IF NOT LESS
.
          ADD       ONE,XXXNO
          ASSIGN    (XXXNO%10),FORM5
          IF        FORM5 = 1
            DISPLAY   *P19:24,XXXNO;        * display record counts
          ENDIF
.
PRNTN200  MOVE      PSNAME,XPSNAME
          MOVE      PGNAME,XPGNAME
.
          MATCH     SP70,PBDATE
          IF        @EQUAL | @EOS
            MOVE      SP70,DIM10B
          ELSE
            UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10B,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
            REP       " 0",DIM10B
          ENDIF
.
          PACK      KEY20,TMPURNO,SP70
          CALL      RSMRMAS3
PRNTN500  CALL      RKMRMAS3                    * Reads MRT master file
          BRANCH    OVRCD,PRNTN100
.
          MATCH     MRMAURNO,TMPURNO
          GOTO      PRNTN100 IF NOT EQUAL
.
          MATCH     SP70,MRMAEPDT
          IF        @EQUAL | @EOS
            MOVE      SP70,DIM10A
          ELSE
            UNPACK    MRMAEPDT,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10A,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
            REP       " 0",DIM10A
          ENDIF
.
          MOVE      SP70,HOMLOCD4
          MATCH     SP70,MRMAHLOC
          IF        !@EQUAL
            PACK      KEY7,MRMAHLOC,MRMAOHOS,SP70
            CALL      RDMRLOC4
            IF        OVRCD = 0
              MOVE      MRLODESC,HOMLOCD4
            ENDIF
          ENDIF
.
          MOVE      SP70,CURLOCDS
          MATCH     SP70,MRMACLOC
          IF        !@EQUAL
            PACK      KEY7,MRMACLOC,MRMACHOS,SP70
            CALL      RDMRLOC4
            IF        OVRCD = 0
              MOVE      MRLODESC,CURLOCDS
            ENDIF
          ENDIF
.
          PRINT     *1,"|",*2,TMPURNO:
                    *11,"|",*13,DMRMAVOL:
                    *15,"|",*16,XPSNAME:
                    *35,XPGNAME:
                    *56,"|",*57,DIM10B:
                    *67,"|",*69,DIM10A:
                    *80,"|",*82,MRMADOTY:
                    *89,"|",*90,HOMLOCD4:
                    *95,"|",*96,CURLOCDS:
                    *117,"|",*119,TMPTDF:
                    *132,"|"
.
          ADD       ONE,CLNO
.
          GOTO      PRNTN500
.
PRNTN900  CALL      UND132
.
PRNTN999  RETURN
+
.-----------------------------------------------------------------------------
. Print the report headings with location for Option 2
.-----------------------------------------------------------------------------
HEADL000  CALL      HEAD132                 * print the page header
.
          PRINT     *1,"U/R  - From : ",FROMURTX,*27,"to : ",TOOOURTX,*N:
                    *1,"Date - From : ",FROMDTTX,*27,"to : ",TOOODTTX:
                    *46,SUMMTEXT
.
          CALL      UND132
.
          PRINT     *1,"|  U/R No":
                    *11,"|Vol": 
                    *15,"|Patient Surname    Given Names":
                    *56,"|Pat. DOB  ":
                    *67,"|Last Contact":
                    *80,"|Doc Type":
                    *89,"|Home Location":
                    *110,"|Current Location":
                    *132,"|"
.
          MOVE      EIGHT,CLNO
.
          CALL      UND132
.
          MOVE      EIGHT,CLNO
.
HEADL999  RETURN
+
.-----------------------------------------------------------------------------
. Print the report headings with location for Option 4
.-----------------------------------------------------------------------------
HEADN000  CALL      HEAD132                 * print the page header
.
          PRINT     *1,"U/R  - From : ",FROMURTX,*27,"to : ",TOOOURTX,*N:
                    *1,"Date - From : ",FROMDTTX,*27,"to : ",TOOODTTX:
                    *46,SUMMTEXT
.
          CALL      UND132
.
          PRINT     *1,"|  U/R No":
                    *11,"|Vol":
                    *15,"|Patient Surname    Given Names":
                    *56,"|Pat. DOB  ":
                    *67,"|Last Contact":
                    *80,"|Doc Type":
                    *89,"|Home":
                    *95,"|Current Location":
                    *117,"|Filing Code":
                    *132,"|"
.
          MOVE      EIGHT,CLNO
.
          CALL      UND132
.
          MOVE      EIGHT,CLNO
.
HEADN999  RETURN

+
.******************************************************************************
          INC       STD001IO
          INC       TFILENAM                     * Generate Temp File Name
          INC       AAEDI1IO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATBAYIO/INC
          INC       PATVISIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDSCIO/INC
          INC       MRTMASIO/INC
          INC       MRTLOCIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
.

.****************************************************************************
.*      I/O ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
.
.         Index 1 Routines
.
RATEMX1   MOVE      ZERO,OVRCD
          READ      SEQTEMP1,KEY20;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMX1   READ      SEQTEMP1,KEY20;;
          RETURN
.
RDTEMX1   MOVE      ZERO,OVRCD
          READ      SEQTEMP1,KEY20;TMPSEQ,TMPURNO,TMPADTE,TMPDDTE,TMPAAE:
                                  TMPOUT,TMPINP,TMPBAY,TMPTDF,TMPSPRE
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMX1   MOVE      ZERO,OVRCD
          READKS    SEQTEMP1;TMPSEQ,TMPURNO,TMPADTE,TMPDDTE,TMPAAE:
                                  TMPOUT,TMPINP,TMPBAY,TMPTDF,TMPSPRE
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMX1   MOVE      ZERO,OVRCD
          READKP    SEQTEMP1;TMPSEQ,TMPURNO,TMPADTE,TMPDDTE,TMPAAE:
                                  TMPOUT,TMPINP,TMPBAY,TMPTDF,TMPSPRE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMX1   WRITE     SEQTEMP1,KEY20;TMPSEQ,TMPURNO,TMPADTE,TMPDDTE,TMPAAE:
                                  TMPOUT,TMPINP,TMPBAY,TMPTDF,TMPSPRE
          RETURN
.
UPTEMX1   MOVE      ZERO,OVRCD
          UPDATE    SEQTEMP1;TMPSEQ,TMPURNO,TMPADTE,TMPDDTE,TMPAAE:
                                  TMPOUT,TMPINP,TMPBAY,TMPTDF,TMPSPRE
          RETURN
.
DETEMX1   DELETE    SEQTEMP1,KEY20
          RETURN

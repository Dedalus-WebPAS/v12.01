.-------------------------------------------------------
. Modifications :
.
. C2.00.01 09.09.1999  Sandra Barcham
.          Change ITEMNO to XITEMNO for PATITMFD          
.-------------------------------------------------------
.
.         V6.00.01  Disable Copy to the same screen.
.         V6.00.04  Improved Performance for Batch Screens by Storing the number
.                   on the screen in the first key field for that batch
.         V6.00.05  24/11/1992    Justin Coates
.                   implement new variable for the program id for the item file
.         V6.00.06  06/04/93 J.Tan   SRF 121615
.                   Mods to check for screen sizes in copying
.         V6.00.07  23/04/93 B.G.Ackland       
.                   Allow Selection Maintenance when editing Selection or WP
.                   field. Fix Selection Delete on Field Delete.
.                   Default all YORN keyins to No.
.         C1.00.01  10/11/1995 Matt              
.                   Changed to allow a display item to be painted more than 
.                   once. This is to allow painting of multiple labels.
.         C1.00.02  01/02/1996    Justin Coates  (SUHT mods)
.                   Only allow a field to be painted twice if it is a display
.                   only field (all label fields WILL be display only).
.                   Fix bug where an item can be painted as a selection twice.
.         C1.00.03  07/02/1996    Justin Coates  (SUHT mods)
.                   Fix bug where if an item has no selection, then the next 
.                   selection on file will be posted for the current item
.         C1.00.04  15.08.1996    B.G.Ackland               
.                   Restore feature where an item can be painted as a 
.                   selection twice.
.
          INC       SYSSTDDF
          INC       PAINTDDF
          INC       SCRPRGFD/INC  * Program Master File
          INC       SCRMASFD/INC  * Screen Master File
          INC       SCRSBGFD/INC  * Screen Background
          INC       SCRFLDFD/INC  * Screen Fields
          INC       SCRSLTFD/INC  * Screen Selections
          INC       SCRPSTFD/INC  * Screen Positions
.
SELKEY13  DIM       13
COLUMN    FORM      3
VAR127    DIM       127
SHELL     INIT      "sh"
FUNCNAME  INIT      "scrtfc"
MAKETFUN  DIM       80
MAKEDEC   DIM       80
SCRTDCA1  IFILE     SQL, FIXED=9, KEY="U1-8"
DECNAME   INIT      "scrtdc"
DECFNAME  DIM       8
INDEX     INIT      " 9 U1-8"
.
SAVPID    DIM       8
FSTPID    DIM       8
LSTPID    DIM       8
PRGIDF    DIM       8         * program id for field file
SCRIDF    DIM       2         * screen  id for field file
.
COPYTPID  DIM       8
COPYTSID  DIM       2
.
COPYFPID  DIM       8
COPYFSID  DIM       2
COPYFTOP  FORM      2
COPYFLEF  FORM      3
COPYFBOT  FORM      2
COPYFRIG  FORM      3
.
SAVKEY12  DIM       12
SAVKEY13  DIM       13
SAVKEY20  DIM       20
.
FIELDNAM  DIM       8
KEYNAME   DIM       8
RACALLID  DIM       8
RDCALLID  DIM       8
RKCALLID  DIM       8
RSCALLID  DIM       8
RPCALLID  DIM       8
.
RA        INIT      "RA"
RS        INIT      "RS"
RK        INIT      "RK"
RP        INIT      "RP"
RDA       INIT      "RDA"
RDS       INIT      "RDS"
RDK       INIT      "RDK"
RDP       INIT      "RDP"
RD        INIT      "RD"
.
SCRNTYP   DIM       20
SCRNTYP0  INIT      "Normal Screen    "
SCRNTYP1  INIT      "Virtual Sub Screen"
.
SCRNSEX   DIM       20
SCRNSEX0  INIT      "Restart Screen "
SCRNSEX1  INIT      "Exit Screen "
.
FLAGSP70  FORM      1
OLDPID    DIM       8
NEWPID    DIM       8
.
KEYTYPE   DIM       10
KEYTYPE0  INIT      "Modify"
KEYTYPE1  INIT      "Insert"
KEYTYPE2  INIT      "Either"
SAVKEY15  DIM       15
.
FIELDNO   FORM      5
FORM5     FORM      5
.
INDENT    FORM      3
INITFOR2  INIT      "FORM2"
INITVAR   INIT      "VAR"
INITL006  INIT      "L006"
INITL010  INIT      "L010"
INITL020  INIT      "L020"
BRATYPE   FORM      1   * Branch Type 0=Display 1=Select
ENDLABEL  DIM       8
DEFLABEL  DIM       8
BRALABEL  DIM       5
.
KEYEXIT   FORM      1
SELEXIT   FORM      1
DISEXIT   FORM      1
.
LABELNO3  FORM      3
.
CPLAB     DIM       4
DLLAB     DIM       4
ADLAB     DIM       4
GPLAB     DIM       4
WPLAB     DIM       4
SCLAB     DIM       4
SMLAB     DIM       4
DKLAB     DIM       4
DFLAB     DIM       4
KYLAB     DIM       4
SFLAB     DIM       4
CFLAB     DIM       4
NPLAB     DIM       4
PPLAB     DIM       4
LABELCP   INIT      "CP"
LABELDL   INIT      "DL"
LABELAD   INIT      "AD"
LABELGP   INIT      "GP"
LABELWP   INIT      "WP"
LABELSC   INIT      "SC"
LABELSM   INIT      "SM"
LABELSF   INIT      "SF"
LABELKY   INIT      "KY"
LABELDK   INIT      "DK"
LABELDF   INIT      "DF"
LABELCF   INIT      "CF"
LABELNP   INIT      "NP"
LABELPP   INIT      "PP"
.
LOWUP     INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
SCFDTMP1  FILE      ASCII, FIXED=80
FILENAME  DIM       8
COMMAND   DIM       80
FUNTYPE   DIM       20
FUNTYPE1  INIT      "Manual       "
FUNTYPE2  INIT      "Write/Update "
FUNTYPE3  INIT      "Delete       "
FUNTYPE4  INIT      "Generated    "
.
DTYPE     DIM       4
DTYPE0    INIT      "DIM "
DTYPE1    INIT      "FORM"
FLDFLAG   FORM      1
KEYFLAG   FORM      1
KTYPNAM    DIM       10
KTYPNAM0   INIT      "Normal  "
KTYPNAM1   INIT      "*JR     "
KTYPNAM2   INIT      "*JR,*ZF "
KTYPNAM3   INIT      "0=No    "
KTYPNAM4   INIT      "0=Yes   "
KTYPNAM5   INIT      "Date    "
KTYPNAM6   INIT      "Null    "
FDINC      INIT      "FD/INC"
KYINC      INIT      "KY"
IOINC      INIT      "IO/INC"
VI         INIT      "vi "
TXT        INIT      ".txt"
.
ACCEPT    FORM      1
LOCYESNO  DIM       3
KTYPDES   DIM       20
EXITDES   DIM       20
.
. Save Variables
.----------------
SSCPSPID   DIM       8      8        1     Program                            
SSCPSSID   DIM       2      2        9     Screen                             
SSCPSITM   DIM       5      5        18    Field Number                       
SSCPSROW   DIM       2      2        23    Row                                
SSCPSCOL   DIM       3      3        25    Column                             
SSCPSMAN   FORM      1      2        28    Mandatory 0=No                     
SSCPSLEN   FORM      3      3        30    Display Length                     
SSCPSATT   DIM       1      1        33    Display Attributes                 
SSCPSNOR   FORM      2      1        33    Display Attributes                 
SSCPSMTY   DIM       5      1        33    Display Attributes                 
SSCPSFMT   DIM       25     1        33    Display Attributes                 
.
. Save Item Variables 
.---------------------
SSCFLPID   DIM       8      8        1     Program ID                         
SSCFLSID   DIM       2      2        9     Screen Id                          
SSCFLFNO   DIM       5      5        11    Field Number                       
SSCFLDES   DIM       35     35       16    Description                        
SSCFLTYP   FORM      1      2        51    Type 0.Key 1.Sel 2.Dis 3.Fun 4.Scr 
SSCFLFLD   DIM       8      8        53    Field                              
SSCFLCAL   DIM       8      8        61    Call Routine                       
SSCFLWPF   DIM       8      8        69    Word Processor File                
SSCFLFSC   DIM       2      2        77    Screen to Call                     
SSCFLDEC   FORM      1      2        79    Declare Field in Program 0=No      
SSCFLMIN   FORM      3      3        81    Minimum Screen Length              
SSCFLMAX   FORM      3      3        84    Maximum Screen Length              
SSCFLFLN   FORM      3.1    4        87    Field Length                       
SSCFLFTY   FORM      1      2        91    Field Type 0=Dim                   
SSCFLKTY   FORM      1      2        93    Keyin Type 0-6                     
SSCFLMAN   FORM      1      2        95    Mandatory 0=No                     
SSCFLFEX   FORM      1      2        97    Function Exit Type                 
SSCFLDAF   DIM       5      5        99    Display After Field                
SSCFLMUL   FORM      1      2        99    Display After Field                
SSCFLMTY   DIM       5      2        99    Display After Field                
SSCFLKEY   FORM      1      2        99    Display After Field                
SSCFLSPA   DIM       33     33       104   Spare                              
.--------------------------------------------------------------------
AUD       INIT      "AUD"
KEYLEN    FORM      4
KEYFILE   DIM       4
PPACKKEY  INIT      "          PACK      KEY"
SEGMENT   DIM       4
PACKKEY   DIM       80    * Validation Pack Key
SAV80     DIM       80 
.
FIRST     FORM      1
COUNT     FORM      4
AUDFLAG   FORM      1
LOCFLAG   FORM      1
.
DSEC      DIM       5
DPOS      DIM       3
MAKETMP   DIM       80
KILLTMP   DIM       80
TEMPFILE  DIM       8
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
TEMPNAME  INIT      "systmp"
KEYDEFN   INIT      " 9 U1-3,4-6,7-8"
INCLFILE  FILE      ASCII, FIXED=80
FD        INIT      "FD"
IO        INIT      "IO"
KY        INIT      "KY"
PRNAME     DIM       12
PBLANK    INIT      "."
PSELA     INIT      "SELA"
LABELNO   FORM      4
PDISPLAY  INIT      "          DISPLAY    "
PDSPACE   INIT      "                     "
PRETURN   INIT      "          RETURN"
PCALL     INIT      "          CALL      "
UUCALLID  DIM       8
RLCALLID  DIM       8
UPCALLID  DIM       8
WRCALLID  DIM       8
DECALLID  DIM       8
AWCALLID  DIM       8
ADCALLID  DIM       8
UU        INIT      "UU"
RL        INIT      "RL"
AW        INIT      "AW"
AD        INIT      "AD"
WR        INIT      "WR"
UP        INIT      "UP"
DE        INIT      "DE"
.
OPENFILE  DIM       8
PHYSFILE  DIM       8
.
MOVEFLAG  FORM      1
SAVCCOL   FORM      3
SAVCVERT  FORM      2
DIM80     DIM       80
DIM8      DIM       8
DIM5      DIM       5
DFP1      DIM       3
DFP2      DIM       1
DOTSLASH  INIT      "./"
.
CHAR      DIM       1
.
.
CNUMOPT   FORM      2
.
FLDACC    FORM      1                  * accept mode (1=on)
X80       INIT      "xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx":
                    "xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx"
.
FCNT      FORM      2
LCNT      FORM      2
LASTTYP   FORM      1
.
PBL       INIT      ".PBL"
INC       INIT      ".INC"
COMPILER  DIM       20
.
PRGNAM    INIT      "Dynamic Control System Screen Manager"
PRGID     INIT      "IBASCR13"
VERSION   INIT      "V12.01.00"
.
ML0000    DISPLAY   *P1:1,*EF:
                    *P15:1,*V2LON,"Dynamic Screen Manager Version : ",VERSION
          HLINE     *G37,2,1,80
          DISPLAY   *P34:10,*BLINKON,*V2LON,"Loading Program"
          OPEN      SCRPRGA1,"scrprgaf"
          OPEN      SCRMASA1,"scrmasaf"
          OPEN      SCRFLDA1,"scrfldaf"
          OPEN      SCRFLDA2,"scrfldaf"
          OPEN      SCRSBGA1,"scrsbgaf"
          OPEN      SCRPSTA1,"scrpstaf"
          OPEN      SCRPSTA2,"scrpstaf"
          OPEN      SCRSLTA1,"scrsltaf"
          OPEN      SCRSLTA2,"scrsltaf"
.
          CALL      GLOAD000     * Load Graphic Characters
.
          MOVE      "3",MINLINE
          MOVE      "24",MAXLINE
          MOVE      "1",MINCOL
          MOVE      "80",MAXCOL
.
MAIN1000  CALL      GPRG0000          * Get Program to Work On
          BRANCH    EXIT,MAIN9999
.
MAIN1500  CALL      SOPT0000          * Get Screen to Work On
          BRANCH    EXIT,MAIN2000
          GOTO      MAIN1500
          
MAIN2000  CALL      UUSCPR1           * Unlock Program
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM
.
.-------------------------------------------
. Get Program to Work On and Lock It
.-------------------------------------------
GPRG0000  DISPLAY   *P1:1,*EF:
                    *P25:1,*V2LON,"Dynamic Screen Manager"
          HLINE     *G37,2,1,80
          MOVE      ONE,ACCEPT
          CALL      DPPR0000
          MOVE      ZERO,EXIT
          MOVE      FOUR,CVERT
          MOVE      "20",CCOL
          MOVE      ZERO,CKEYTYP
          MOVE      "6",HTOP
          MOVE      "24",HBOT
          MOVE      "40",HLEF
          MOVE      "80",HRIG
          CALL      KSCPRA00
          BRANCH    EXIT,GPRG9000,GPRG9000
.
          CALL      LPRG0000
          BRANCH    EXIT,GPRG0000
.
          CALL      DPPR0000
          MOVE      ZERO,EXIT
          GOTO      GPRG9999
.
GPRG9000  MOVE      ONE,EXIT
GPRG9999  RETURN
.----------------------------------------
. LPRG - Lock Program to be Updated
.----------------------------------------
LPRG0000  PACK      KEY8,SCPRPID,SP70
          CALL      RLSCPR1
          BRANCH    OVRCD,LPRG9000,LPRG8000
          MOVE      ZERO,EXIT
          GOTO      LPRG9999
.
LPRG8000  KEYIN     *P1:24,*EL,"Record Locked - (":
                    *V2LON,"R",*HOFF,")etry or (":
                    *V2LON,"E",*HOFF,")xit ? ",ANS
          REP       UPPLOW,ANS
          MATCH     ANSR,ANS
          GOTO      LPRG0000 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      LPRG0000 IF NOT EQUAL
.
LPRG9000  MOVE      ONE,EXIT
.
LPRG9999  RETURN
.----------------------------------------
. UPRG - Unlock Program
.----------------------------------------
ULOC0000  CALL      UUSCPR1
ULOC9999  RETURN
.----------------------------------------
. Display Program Details
.----------------------------------------
DPPR0000  DISPLAY   *P1:4,*EF,"    Program ID   : "
          BRANCH    ACCEPT,DPPR9999
          DISPLAY   *P20:04,*V2LON,*EL,SCPRPID,SP1,*HOFF,SCPRNAM
DPPR9999  RETURN
.------------------------------------
. Screen Options
.------------------------------------
SOPT0000  CALL      GSCR0000
          BRANCH    EXIT,SOPT9999
.
SOPT0010  DISPLAY   *P1:1,*EF,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"M",*HOFF,"odify, ":
                    *V2LON,"B",*HOFF,"ackground, ":
                    *V2LON,"F",*HOFF,"ields, ":
                    *V2LON,"C",*HOFF,"opy, ":
                    *V2LON,"R",*HOFF,"emove"
          HLINE     *G37,2,1,80
.
SOPT0100  MOVE      ZERO,ACCEPT
          CALL      DSMA0000
.
          KEYIN     *P1:24,*EL,*EOFF,*NOEDIT,*+,ANS
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      SOPT9000 IF EQUAL
          MATCH     ANSM,ANS
          GOTO      SOPT1000 IF EQUAL
          MATCH     ANSB,ANS
          GOTO      SOPT3000 IF EQUAL
          MATCH     ANSF,ANS
          GOTO      SOPT4000 IF EQUAL
          MATCH     ANSC,ANS
          GOTO      SOPT5000 IF EQUAL
          MATCH     ANSR,ANS
          GOTO      SOPT6000 IF EQUAL
SOPT0110
          BEEP
          GOTO      SOPT0100
.
. Modify Details
.---------------
SOPT1000  MATCH     SP2,SCMAFSC
          GOTO      SOPT1010 IF EQUAL       * cant modify this screen
          MATCH     SCMASID,SCMAFSC
          GOTO      SOPT1010 IF EQUAL       * cant modify this screen
SOPT1005
          CALL      SSMA0000
          BRANCH    EXIT,SOPT9000
          GOTO      SOPT0010
.
SOPT1010  DISPLAY   *P1:24,*B,"Cant modify this screen's details - ",*EL;
          CALL      HITENTER
          MATCH     "*",ANS
          IF        @EQUAL
            MATCH     "1426",PASSCODE
            GOTO      SOPT1005 IF EQUAL
          ENDIF
          GOTO      SOPT0010
.
. Paint Screen Background
.------------------------
SOPT3000  CALL      RLOAD000
          CALL      PAINT000
          GOTO      SOPT0010
.
. Edit Fields
.------------
SOPT4000  CALL      RLOAD000  * Display Background
          CALL      PTFLD000  * Edit Fields
          CALL      BSCAN000  * Update Multiple Field Display Count
          GOTO      SOPT0010
.
. Copy
.------------
SOPT5000  CALL      SCOPY000
          GOTO      SOPT0010
.
. Remove
.------------
SOPT6000  CALL      SREMV000
          GOTO      SOPT0010
.
SOPT9000  MOVE      ZERO,EXIT
SOPT9999  RETURN
.-------------------------------------------
. Get Screen to Work On 
.-------------------------------------------
GSCR0000  DISPLAY   *P1:1,*EF:
                    *P25:1,*V2LON,"Dynamic Screen Manager"
          HLINE     *G37,2,1,80
          MOVE      ONE,ACCEPT
          CALL      DSMA0000   * Display Details
          MOVE      ZERO,EXIT
          MOVE      FIVE,CVERT
          MOVE      "20",CCOL
          MOVE      TWO,CKEYTYP             * insert/change
          MOVE      "6",HTOP
          MOVE      "24",HBOT
          MOVE      "40",HLEF
          MOVE      "80",HRIG
          MOVE      SCPRPID,SCMAPID
          CALL      KSCMAA00
          BRANCH    EXIT,GSCR9000,GSCR9000
.
GSCR1000  PACK      KEY10,SCMAPID,SCMASID
          CALL      RDSCMA1
          BRANCH    OVRCD,GSCR5000          * insert mode
.
          MATCH     SP2,SCMAFSC
          IF        !@EQUAL
            MOVE      SCMAFSC,SCRIDF
          ELSE
            MOVE      SCMASID,SCRIDF
          ENDIF
.
          MATCH     SP8,SCMAFPR
          IF        !@EQUAL
            MOVE      SCMAFPR,PRGIDF          * set program id
            MOVE      "01",SCRIDF             * set screen id to one
            MOVE      "01",SCMAFSC            * set screen id to one
            CALL      UPSCMA1                 * update file with screen 01
          ELSE
            MOVE      SCMAPID,PRGIDF
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      GSCR9999
.
GSCR5000  MOVE      ONE,ACCEPT
          CALL      CSMA0000                * clear variables
          MOVE      SCMAPID,PRGIDF          * set field file program id
          CALL      SSMA0000                * change details
          BRANCH    EXIT,GSCR0000
          MOVE      ZERO,EXIT
          GOTO      GSCR9999
.
GSCR9000  MOVE      ONE,EXIT
          GOTO      GSCR9999
.
GSCR9999  RETURN
+
CSMA0000  MOVE      SP70,SCMADES
          MOVE      SP70,SCMACBF
          MOVE      SP70,SCMACAF
          MOVE      ZERO,SCMATYP
          MOVE      ZERO,SCMASEX
          MOVE      SP70,SCMAFSC
          MOVE      ZERO,SCMACLR
          MOVE      SP70,SCMAFPR
          MOVE      "08",SCMATOP
          MOVE      "23",SCMABOT
          MOVE      "01",SCMALEF
          MOVE      "80",SCMARIG
          RETURN 
.----------------------
. Copy a Screen
.---------------------
SCOPY000  
          MOVE      SCPRPID,COPYTPID
          MOVE      SCMASID,COPYTSID
.
          MOVE      SCMATOP,COPYFTOP
          MOVE      SCMALEF,COPYFLEF
          MOVE      SCMABOT,COPYFBOT
          MOVE      SCMARIG,COPYFRIG
.
SCOPY001  
          DISPLAY   *P1:1,*EL,"Copy from Program/Screen : "
          MOVE      ONE,CVERT
          MOVE      "28",CCOL
          CALL      KSCPRA00
          BRANCH    EXIT,SCOPY900,SCOPY900
          MOVE      ONE,CVERT
          MOVE      "37",CCOL
          MOVE      SCPRPID,SCMAPID
          CALL      KSCMAA00
          BRANCH    EXIT,SCOPY900,SCOPY900
          MATCH     COPYTPID,SCMAPID
          IF        @EQUAL
            MATCH     COPYTSID,SCMASID
            IF        @EQUAL
              BEEP
              GOTO    SCOPY001
            ENDIF
          ENDIF
.
.         Check the screen sizes
.
          COMPARE   COPYFTOP,SCMATOP
          GOTO      SCOPY005 IF LESS
          COMPARE   COPYFLEF,SCMALEF
          GOTO      SCOPY005 IF LESS
          COMPARE   COPYFBOT,SCMABOT
          GOTO      SCOPY005 IF LESS
          COMPARE   COPYFRIG,SCMARIG
          GOTO      SCOPY005 IF LESS
          GOTO      SCOPY010
.
SCOPY005  DISPLAY   *P1:24,*B,*EL,"Incompatible screen sizes - ",*HOFF;
          CALL      HITENTER
          GOTO      SCOPY001
.
SCOPY010  DISPLAY   *P1:1,*EL,"Copy ",*V2LON,"A",*HOFF,"ll, ":
                    *V2LON,"B",*HOFF,"ackground, ":
                    *V2LON,"P",*HOFF,"ositions, ":
                    *V2LON,"S",*HOFF,"elections"
.
SCOPY020  KEYIN     *P1:24,*EOFF,*NOEDIT,*+,ANS
          REP       UPPLOW,ANS
          MATCH     ANSA,ANS
          GOTO      SCOPY100 IF EQUAL
          MATCH     ANSB,ANS
          GOTO      SCOPY200 IF EQUAL
          MATCH     ANSP,ANS
          GOTO      SCOPY300 IF EQUAL
          MATCH     ANSS,ANS
          GOTO      SCOPY400 IF EQUAL
          BEEP
          GOTO      SCOPY020
.
.
. Copy All
.--------------
SCOPY100  
          DISPLAY   *P1:1,*EL,"Copy All from Program/Screen : ":
                    SCMAPID,"/",SCMASID
          DISPLAY   *P59:1,"Continue (Y/N) ? "
          MOVE      ONE,CVERT
          MOVE      "76",CCOL
          MOVE      ANSN,DEFYESNO   * Default Y
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      SCOPY900 IF EQUAL
.
          CALL      CPPST000 * Copy Edit Positions
          CALL      CPSBG000 * Copy Background
          CALL      CPSLT000 * Copy Selections
          GOTO      SCOPY900
.
. Copy Background
.--------------
SCOPY200  
          DISPLAY   *P1:1,*EL,"Copy Background From Program/Screen : ":
                    SCMAPID,"/",SCMASID
          DISPLAY   *P59:1,"Continue (Y/N) ? "
          MOVE      ONE,CVERT
          MOVE      "76",CCOL
          MOVE      ANSN,DEFYESNO   * Default Y
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      SCOPY900 IF EQUAL
.
          CALL      CPSBG000 * Copy Background
          GOTO      SCOPY900
.
. Copy Positions
.---------------
SCOPY300  
          DISPLAY   *P1:1,*EL,"Copy Positions From Program/Screen : ":
                    SCMAPID,"/",SCMASID
          DISPLAY   *P59:1,"Continue (Y/N) ? "
          MOVE      ONE,CVERT
          MOVE      "76",CCOL
          MOVE      ANSN,DEFYESNO   * Default Y
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      SCOPY900 IF EQUAL
.
          CALL      CPPST000 * Copy Positions
          GOTO      SCOPY900
.
. Copy Selections
.---------------
SCOPY400  
          DISPLAY   *P1:1,*EL,"Copy Selections From Program/Screen : ":
                    SCMAPID,"/",SCMASID
          DISPLAY   *P59:1,"Continue (Y/N) ? "
          MOVE      ONE,CVERT
          MOVE      "76",CCOL
          MOVE      ANSN,DEFYESNO   * Default Y
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      SCOPY900 IF EQUAL
.
          CALL      CPSLT000 * Copy Selections
          GOTO      SCOPY900
.
SCOPY900  MOVE      COPYTPID,KEY8
          CALL      RDSCPR1
          PACK      KEY10,COPYTPID,COPYTSID
          CALL      RDSCMA1
          RETURN
.
. Copy Field Screen Positions
.----------------------------
CPPST000  
          MOVE      SCPRPID,COPYFPID
          MOVE      SCMASID,COPYFSID
          MOVE      COPYTPID,SCPRPID
          MOVE      COPYTSID,SCMASID
          CALL      RMPST000              
          MOVE      COPYFPID,SCPRPID
          MOVE      COPYFSID,SCMASID
          PACK      KEY15,SCPRPID,SCMASID,SP70
          CALL      RSSCPS1
CPPST100  CALL      RKSCPS1
          BRANCH    OVRCD,CPPST999
          MATCH     SCPRPID,SCPSPID
          GOTO      CPPST999 IF NOT EQUAL
          MATCH     SCMASID,SCPSSID
          GOTO      CPPST999 IF NOT EQUAL
          PACK      SAVKEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          PACK      KEY15,COPYTPID,COPYTSID,SCPSROW,SCPSCOL
          UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      RASCPS1
          IF        OVRCD=0
            CALL      UPSCPS1
          ELSE
            CALL      WRSCPS1
          ENDIF
          MOVE      SAVKEY15,KEY15
          CALL      RDSCPS1
          GOTO      CPPST100
.
CPPST999  RETURN
.
. Copy Screen Background
.------------------------
CPSBG000  
          MOVE      SCPRPID,COPYFPID
          MOVE      SCMASID,COPYFSID
          MOVE      COPYTPID,SCPRPID
          MOVE      COPYTSID,SCMASID
          CALL      RMSBG000
          MOVE      COPYFPID,SCPRPID
          MOVE      COPYFSID,SCMASID
          PACK      KEY12,SCPRPID,SCMASID,SP70
          CALL      RSSCSB1
CPSBG100  CALL      RKSCSB1
          BRANCH    OVRCD,CPSBG999
          MATCH     SCPRPID,SCSBPID
          GOTO      CPSBG999 IF NOT EQUAL
          MATCH     SCMASID,SCSBSID
          GOTO      CPSBG999 IF NOT EQUAL
          PACK      SAVKEY12,SCSBPID,SCSBSID,SCSBLIN
          PACK      KEY12,COPYTPID,COPYTSID,SCSBLIN
          UNPACK    KEY12,SCSBPID,SCSBSID,SCSBLIN
          CALL      RASCSB1
          IF        OVRCD=0
            CALL      UPSCSB1
          ELSE
            CALL      WRSCSB1
          ENDIF
          MOVE      SAVKEY12,KEY12
          CALL      RDSCSB1
          GOTO      CPSBG100
CPSBG999  RETURN
.
. Copy Selections
.-----------------
CPSLT000  
          MOVE      SCPRPID,COPYFPID
          MOVE      SCMASID,COPYFSID
          MOVE      COPYTPID,SCPRPID
          MOVE      COPYTSID,SCMASID
          CALL      RMSLT000
          MOVE      COPYFPID,SCPRPID
          MOVE      COPYFSID,SCMASID
          PACK      KEY13,SCPRPID,SCMASID,SP70
          CALL      RSSCSL1
CPSLT100  CALL      RKSCSL1
          BRANCH    OVRCD,CPSLT999
          MATCH     SCPRPID,SCSLPID
          GOTO      CPSLT999 IF NOT EQUAL
          MATCH     SCMASID,SCSLSID
          GOTO      CPSLT999 IF NOT EQUAL
          PACK      SAVKEY13,SCSLPID,SCSLSID,SCSLSEL
          PACK      KEY13,COPYTPID,COPYTSID,SCSLSEL
          UNPACK    KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      RASCSL1
          IF        OVRCD=0
            CALL      UPSCSL1
          ELSE
            CALL      WRSCSL1
          ENDIF
          MOVE      SAVKEY13,KEY13
          CALL      RDSCSL1
          GOTO      CPSLT100
CPSLT999  RETURN
.---------------------------
. Remove Part of a Screen
.--------------------------
SREMV000  DISPLAY   *P1:1,*EL,"Remove ",*V2LON,"A",*HOFF,"ll, ":
                    *V2LON,"B",*HOFF,"ackground, ":
                    *V2LON,"S",*HOFF,"elections, ":
                    *V2LON,"P",*HOFF,"ositions"
.
SREMV020  KEYIN     *P1:24,*EOFF,*NOEDIT,*+,ANS
          REP       UPPLOW,ANS
          MATCH     ANSA,ANS
          GOTO      SREMV100 IF EQUAL
          MATCH     ANSB,ANS
          GOTO      SREMV200 IF EQUAL
          MATCH     ANSS,ANS
          GOTO      SREMV300 IF EQUAL
          MATCH     ANSP,ANS
          GOTO      SREMV400 IF EQUAL
          BEEP
          GOTO      SREMV020
.
. Remove All
.--------------
SREMV100  
          DISPLAY   *P1:1,*EL,"Remove All from Program/Screen : ":
                    SCMAPID,"/",SCMASID
          DISPLAY   *P59:1,"Continue (Y/N) ? "
          MOVE      ONE,CVERT
          MOVE      "76",CCOL
          MOVE      ANSN,DEFYESNO   * Default Y
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      SREMV900 IF EQUAL
.
          CALL      RMPST000 * Remove Edit Positions
          CALL      RMSBG000 * Remove Background
          CALL      RMSLT000 * Remove Selections
          GOTO      SREMV900
.
. Remove Background
.--------------
SREMV200  
          DISPLAY   *P1:1,*EL,"Remove Background From Program/Screen : ":
                    SCMAPID,"/",SCMASID
          DISPLAY   *P59:1,"Continue (Y/N) ? "
          MOVE      ONE,CVERT
          MOVE      "76",CCOL
          MOVE      ANSN,DEFYESNO   * Default Y
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      SREMV900 IF EQUAL
.
          CALL      RMSBG000 * Remove Background
          GOTO      SREMV900
.
. Remove Selections
.--------------
SREMV300  
          DISPLAY   *P1:1,*EL,"Remove Selections From Program/Screen : ":
                    SCMAPID,"/",SCMASID
          DISPLAY   *P59:1,"Continue (Y/N) ? "
          MOVE      ONE,CVERT
          MOVE      "76",CCOL
          MOVE      ANSN,DEFYESNO   * Default Y
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      SREMV900 IF EQUAL
.
          CALL      RMSLT000 * Remove Background
          GOTO      SREMV900
.
. Remove Position
.--------------
SREMV400  
          DISPLAY   *P1:1,*EL,"Remove Item Positions From Program/Screen : ":
                    SCMAPID,"/",SCMASID
          DISPLAY   *P59:1,"Continue (Y/N) ? "
          MOVE      ONE,CVERT
          MOVE      "76",CCOL
          MOVE      ANSN,DEFYESNO   * Default Y
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      SREMV900 IF EQUAL
.
          CALL      RMPST000 * Remove Background
          GOTO      SREMV900
.
SREMV900  RETURN
.
. Remove Field Screen Positions
.----------------------------
RMPST000  PACK      KEY15,SCPRPID,SCMASID,SP70
          CALL      RSSCPS1
          CALL      RKSCPS1
          BRANCH    OVRCD,RMPST999
          MATCH     SCPRPID,SCPSPID
          GOTO      RMPST999 IF NOT EQUAL
          MATCH     SCMASID,SCPSSID
          GOTO      RMPST999 IF NOT EQUAL
          PACK      KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      DESCPS1
          GOTO      RMPST000
RMPST999  RETURN
.
. Remove Screen Background
.------------------------
RMSBG000  PACK      KEY12,SCPRPID,SCMASID,SP70
          CALL      RSSCSB1
          CALL      RKSCSB1
          BRANCH    OVRCD,RMSBG999
          MATCH     SCPRPID,SCSBPID
          GOTO      RMSBG999 IF NOT EQUAL
          MATCH     SCMASID,SCSBSID
          GOTO      RMSBG999 IF NOT EQUAL
          PACK      KEY12,SCSBPID,SCSBSID,SCSBLIN
          CALL      DESCSB1
          GOTO      RMSBG000
RMSBG999  RETURN
.
. Remove Selections
.-----------------
RMSLT000  PACK      KEY13,SCPRPID,SCMASID,SP70
          CALL      RSSCSL1
          CALL      RKSCSL1
          BRANCH    OVRCD,RMSLT999
          MATCH     SCPRPID,SCSLPID
          GOTO      RMSLT999 IF NOT EQUAL
          MATCH     SCMASID,SCSLSID
          GOTO      RMSLT999 IF NOT EQUAL
          PACK      KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      DESCSL1
          GOTO      RMSLT000
RMSLT999  RETURN
.------------------------------------
. Paint a Screen
.------------------------------------
PAINT000  MOVE      SCMATOP,MINLINE
          MOVE      SCMALEF,MINCOL
          MOVE      SCMABOT,MAXLINE
          MOVE      SCMARIG,MAXCOL
.
          MOVE      MINCOL,CCOL
PAINT060  MOVE      MINLINE,CVERT
          MOVE      ONE,SCRNSAVE
PAINT090  DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"S",*HOFF,"ave, ":
                    *V2LON,"C",*HOFF,"ancel, ":
                    *V2LON,"V",*HOFF,"ideo, ":
                    *V2LON,"E",*HOFF,"dit, ":
                    *V2LON,"G",*HOFF,"raphic, ":
                    *V2LON,"M",*HOFF,"ove"
          HLINE     *G37,2,1,80
.
PAINT100  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS;
          CALL      KMOVE000
          BRANCH    EXIT,PAINT100
          REP       UPPLOW,ANS
.
          MATCH     ANSE,ANS
          GOTO      PAINT200 IF EQUAL
.
          MATCH     ANSV,ANS
          GOTO      PAINT300 IF EQUAL
.
          MATCH     ANSG,ANS
          GOTO      PAINT500 IF EQUAL
.
          MATCH     ANSM,ANS
          GOTO      PAINT600 IF EQUAL
.
          MATCH     ANSS,ANS
          GOTO      PAINT700 IF EQUAL
.
          MATCH     ANSX,ANS
          GOTO      PAINT800 IF EQUAL
.
          MATCH     ANSC,ANS
          GOTO      PAINT900 IF EQUAL
          GOTO      PAINT100
.
. Edit part of the Screen
.------------------------
PAINT200  CALL      EDIT0000
          GOTO      PAINT090
.
. Set Video Attributes
.---------------------
PAINT300  CALL      VIDEO000
          GOTO      PAINT090
.
. Graphic
.------------
PAINT500  CALL      GRAPH000
          GOTO      PAINT090
.
. Move   
.------------
PAINT600  CALL      MOVE0000
          GOTO      PAINT090
.
. Save Screen
.-------------
PAINT700  CALL      SAVE0000
          DISPLAY   *P13:1,"Saved - ";
          CALL      HITENT01
          GOTO      PAINT090
.
. Exit - Disable if screen modified and not saved
.-------------------------------------------------
PAINT800  BRANCH    SCRNSAVE,PAINT999
          KEYIN     *P1:1,*EL:
                    *P1:1,"Screen Background must be Saved First - ";
          CALL      HITENT01 
          GOTO      PAINT090
.
. Cancel - Reload Screen
.-----------------------
PAINT900  CALL      RLOAD000
          GOTO      PAINT090
.
PAINT999  RETURN
.
.----------------------------------------
. Edit Screen
.----------------------------------------
EDIT0000  MOVE      ZERO,SCRNSAVE
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"Edit, <",*V2LON,"Enter",*HOFF,"> to Exit"
EDIT0100  GETVAR    CDATA,CATTR,CCOL,CVERT
          KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS;
          CALL      KMOVE000
          BRANCH    EXIT,EDIT0100
          RESET     ANS
          GOTO      EDIT0900 IF EOS
          MOVE      ANS,CDATA
          PUTVAR    CDATA,CATTR,CCOL,CVERT
          COMPARE   MAXCOL,CCOL
          GOTO      EDIT0100 IF EQUAL
          ADD       ONE,CCOL
          GOTO      EDIT0100
EDIT0900
          RETURN
.----------------------------------------
. Video Edit
.----------------------------------------
VIDEO000  MOVE      ZERO,SCRNSAVE
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"H",*HOFF,"ighlight, ":
                    *V2LON,"R",*HOFF,"everse, ":
                    *V2LON,"B",*HOFF,"link, ":
                    *V2LON,"U",*HOFF,"nderline "
          MOVE      ZERO,FBLINK
          MOVE      ZERO,FV2LON
          MOVE      ZERO,FHON
          MOVE      ZERO,FULON
VIDEO100  GETVAR    CDATA,CATTR,CCOL,CVERT
          CALL      PUTCH000
          KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS;
          CALL      KMOVE000
          BRANCH    EXIT,VIDEO100
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      VIDEO900 IF EQUAL
          REP       "0z1z2z3z4z5z6z7z8z9z",ANS
          REP       "B1U2H3R4",ANS
          TYPE      ANS
          GOTO      VIDEO100 IF NOT EQUAL
          MOVE      ANS,F1
          LOAD      FLAG,F1,FBLINK,FULON,FV2LON,FHON
          BRANCH    FLAG,VIDEO200
          STORE     ONE,F1,FBLINK,FULON,FV2LON,FHON
          GOTO      VIDEO100
VIDEO200  STORE     ZERO,F1,FBLINK,FULON,FV2LON,FHON
          GOTO      VIDEO100
.
VIDEO900  
          RETURN
.------------------------------------------
. Save Screen to ID
.------------------------------------------
SAVE0000  MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,*V2LON,SCPRPID,*HOFF,"/",*V2LON,SCMASID
          MOVE      SCPRPID,SCSBPID
          MOVE      SCMASID,SCSBSID
          ASSIGN    MINLINE-1,LINE
.
SAVE1000  ADD       ONE,LINE
.
          PACK      SCSBDAT,SP70,SP70
          PACK      SCSBATT,SP70,SP70
          GETVAR    SCSBDAT,SCSBATT,1,LINE
.
          MOVE      LINE,SCSBLIN
          PACK      KEY12,SCSBPID,SCSBSID,SCSBLIN
          CALL      DESCSB1
          MATCH     SP80,SCSBDAT
          GOTO      SAVE1100 IF EQUAL
          CALL      WRSCSB1
.
SAVE1100  COMPARE   MAXLINE,LINE
          GOTO      SAVE1000 IF NOT EQUAL
          MOVE      ONE,SCRNSAVE
.
SAVE9999  MOVE      SVERT,CVERT
          MOVE      SCOL,CCOL
          RETURN
.----------------------------------------
. Reload a Screen Display
.----------------------------------------
RLOAD000  DISPLAY   *P1:1,*EL,*P1:3,*EF
.
          MOVE      SCMATOP,MINLINE
          MOVE      SCMALEF,MINCOL
          MOVE      SCMABOT,MAXLINE
          MOVE      SCMARIG,MAXCOL
.
          PACK      KEY12,SCPRPID,SCMASID,SP70
          CALL      RSSCSB1
RLOAD100  CALL      RKSCSB1
          BRANCH    OVRCD,RLOAD999
          MATCH     SCSBPID,SCPRPID
          GOTO      RLOAD999 IF NOT EQUAL
          MATCH     SCSBSID,SCMASID
          GOTO      RLOAD999 IF NOT EQUAL
          MOVE      SCSBLIN,LINE
          PUTVAR    SCSBDAT,SCSBATT,1,LINE
          GOTO      RLOAD100
.
RLOAD999  MOVE      ONE,SCRNSAVE
          RETURN
.
.----------------------------------------
. Graphic Edit
.----------------------------------------
GRAPH000  MOVE      ZERO,SCRNSAVE
          MOVE      ONE,GCHARNUM
          MOVE      ZERO,LINETYPE
          MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
GRAPH090  DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"E",*HOFF,"rase Line, ":
                    *V2LON,"S",*HOFF,"ingle Line, ":
                    *V2LON,"D",*HOFF,"ouble Line, ":
                    *V2LON,"C",*HOFF,"haracter Select"
GRAPH100  GETVAR    CDATA,CATTR,CCOL,CVERT
          KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS;
          CALL      GMOVE000
          BRANCH    EXIT,GRAPH100
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      GRAPH900 IF EQUAL
          MATCH     ANSS,ANS
          GOTO      GRAPH200 IF EQUAL
          MATCH     ANSD,ANS
          GOTO      GRAPH300 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      GRAPH400 IF EQUAL
          MATCH     ANSC,ANS
          GOTO      GRAPH500 IF EQUAL
          GOTO      GRAPH100
GRAPH200  MOVE      ONE,LINETYPE
          GOTO      GRAPH100
GRAPH300  MOVE      TWO,LINETYPE
          GOTO      GRAPH100
GRAPH400  MOVE      ZERO,LINETYPE
          GOTO      GRAPH100
GRAPH500  DISPLAY   *P1:1,*EL
          KEYIN     *P1:1,"Character Number : ",GCHARNUM
          IF        GCHARNUM>45|GCHARNUM<1
              BEEP
              GOTO    GRAPH500
          ENDIF
          MOVE      THREE,LINETYPE
          GOTO      GRAPH090
.
GRAPH900  MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          RETURN
.----------------------------------
. Move Display Area of the Screen
.----------------------------------
MOVE0000  CALL      MARK0000
          MOVE      ZERO,SCRNSAVE
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"Move to the new Position, e":
                    *V2LON,"X",*HOFF,"it "
MOVE1000  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS;
          CALL      KMOVE000
          BRANCH    EXIT,MOVE1000
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      MOVE1000 IF NOT EQUAL
          MOVE      SCOL,COLOFF
          SUB       CCOL,COLOFF
          MOVE      SVERT,VERTOFF
          SUB       CVERT,VERTOFF
          SUSPEND
          CALL      PUTSCR000
          ASSIGN    MINLINE-ONE,CVERT
          WHILE     CVERT<MAXLINE
            ADD       ONE,CVERT
            ASSIGN    MINCOL-ONE,CCOL
            WHILE     CCOL<MAXCOL
              ADD       ONE,CCOL
              IF        FARRAY[CCOL][CVERT]=1
                ASSIGN    CCOL-COLOFF,COLUMN
                ASSIGN    CVERT-VERTOFF,VERT
              IF        !(COLUMN<MINCOL|VERT<MINLINE|COLUMN>MAXCOL|VERT>MAXLINE)
                  PUTVAR    CARRAY[CCOL][CVERT],AARRAY[CCOL][CVERT],COLUMN,VERT
                ENDIF
              ENDIF
            DO
          DO
          FLUSH
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
MOVE9999  RETURN
.----------------------------------
. Mark area to Move 
.----------------------------------
MARK0000  MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL          
          MOVE      MINCOL,HLEF
          MOVE      MAXCOL,HRIG
          MOVE      MINLINE,HTOP
          MOVE      MAXLINE,HBOT
          CALL      CLEAR000
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"Mark Area to Move, e":
                    *V2LON,"X",*HOFF,"it "
MARK0900  
          BRANCH    FARRAY[CCOL][CVERT],MARK0950
          MOVE      ONE,FARRAY[CCOL][CVERT]
          GETVAR    CARRAY[CCOL][CVERT],AARRAY[CCOL][CVERT],CCOL,CVERT
          MOVE      AARRAY[CCOL][CVERT],CATTR
          AND       ATTGRAPH,CATTR
          OR        ATTHON,CATTR
          OR        ATTV2LON,CATTR
          PUTVAR    CARRAY[CCOL][CVERT],CATTR,CCOL,CVERT
          GOTO      MARK1000
.
MARK0950  MOVE      ZERO,FARRAY[CCOL][CVERT]
          PUTVAR    CARRAY[CCOL][CVERT],AARRAY[CCOL][CVERT],CCOL,CVERT
.    
MARK1000  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS;
          CALL      KMOVE000
          BRANCH    EXIT,MARK0900
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      MARK1000 IF NOT EQUAL
          SUSPEND
          ASSIGN    MINLINE-ONE,CVERT
          WHILE     CVERT<MAXLINE
            ADD       ONE,CVERT
            ASSIGN    MINCOL-ONE,CCOL
            WHILE     CCOL<MAXCOL
              ADD       ONE,CCOL
              IF        FARRAY[CCOL][CVERT]=1 
                DISPLAY   *PCCOL:CVERT,SP1
              ENDIF
            DO
          DO
          CALL      GETSCR00
          ASSIGN    MINLINE-ONE,CVERT
          WHILE     CVERT<MAXLINE
            ADD       ONE,CVERT
            ASSIGN    MINCOL-ONE,CCOL
            WHILE     CCOL<MAXCOL
              ADD       ONE,CCOL
              IF        FARRAY[CCOL][CVERT]=1 
                MOVE      AARRAY[CCOL][CVERT],CATTR
                AND       ATTGRAPH,CATTR
                OR        ATTHON,CATTR
                OR        ATTV2LON,CATTR
                PUTVAR    CARRAY[CCOL][CVERT],CATTR,CCOL,CVERT
              ENDIF
            DO
          DO
          FLUSH
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          RETURN
.
CLEAR000
          MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          ASSIGN    MINLINE-ONE,CVERT
          WHILE     CVERT<MAXLINE
            ADD       ONE,CVERT
            ASSIGN    MINCOL-ONE,CCOL
            WHILE     CCOL<MAXCOL
              ADD       ONE,CCOL
              MOVE      ZERO,FARRAY[CCOL][CVERT]
              MOVE      SP1,CARRAY[CCOL][CVERT]
              MOVE      SP1,AARRAY[CCOL][CVERT]
            DO
          DO
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          RETURN
.--------------------------------------------
. Display Fields over Backgraoud Screen 
.--------------------------------------------
DPFLD000  MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL
          PACK      KEY15,SCPRPID,SCMASID,SP70
          CALL      RSSCPS1
DPFLD100  CALL      RKSCPS1
          BRANCH    OVRCD,DPFLD999
          MATCH     SCPRPID,SCPSPID
          GOTO      DPFLD999 IF NOT EQUAL
          MATCH     SCMASID,SCPSSID
          GOTO      DPFLD999 IF NOT EQUAL
. 
          CALL      DPITM000
.
          GOTO      DPFLD100
.
DPFLD999  MOVE      SVERT,CVERT
          MOVE      SCOL,CCOL
          RETURN
.
.-------------------------------------------
. Display a Field Item of the Screen Painter
.-------------------------------------------
DPITM000  
          MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          BRANCH    MOVEFLAG,DPITM010
          IF        SCFLTYP=5
            GOTO      DPITM100
          ENDIF
DPITM010  MOVE      SCPSROW,CVERT
          MOVE      ONE,LLCNT
          WHILE     LLCNT<=SCPSLEN
            MOVE      SCPSCOL,CCOL
            ADD       LLCNT,CCOL
            SUB       ONE,CCOL
            PUTVAR    ANSX,SCPSATT,CCOL,CVERT
            ADD       ONE,LLCNT
          DO
          GOTO      DPITM999
.
DPITM100  MOVE      SCPSROW,CTOP
          MOVE      SCPSCOL,CLEF 
          MOVE      SCPSLEN,LLCNT
          MOVE      SCPSNOR,CVERT
          ASSIGN    CLEF+LLCNT+1,CRIG
          ASSIGN    CTOP+CVERT+1,CBOT
          MOVE      SCPSCOL,CCOL 
          BOX       16,CLEF,CTOP,CRIG,CBOT
          ADD       ONE,CLEF
          SUB       "12",CRIG
          DISPLAY   *PCLEF:CBOT,*HON,SP1,SP10:
                    *PCRIG:CBOT,SP5
          ADD       SIX,CRIG
          DISPLAY   *PCRIG:CBOT,*HON,SP5
.
DPITM999
          MOVE      SVERT,CVERT
          MOVE      SCOL,CCOL
          RETURN
.--------------------------------------------------------------
. Paint Fields on Screen
.--------------------------------------------------------------
PTFLD000  MOVE      SCMATOP,MINLINE
          MOVE      SCMALEF,MINCOL
          MOVE      SCMABOT,MAXLINE
          MOVE      SCMARIG,MAXCOL
          CALL      DPFLD000 * Display Current Fields
.
PTFLD090  DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"E",*HOFF,"dit, ":
                    *V2LON,"S",*HOFF,"elections, ":
                    *V2LON,"V",*HOFF,"ideo, ":
                    *V2LON,"M",*HOFF,"ove, ":
                    *V2LON,"R",*HOFF,"edisplay"
.
PTFLD100  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS;
          CALL      KMOVE000
          BRANCH    EXIT,PTFLD100
          REP       UPPLOW,ANS
.
          MATCH     ANSE,ANS
          GOTO      PTFLD200 IF EQUAL
.
          MATCH     ANSV,ANS
          GOTO      PTFLD300 IF EQUAL
.
          MATCH     ANSR,ANS
          GOTO      PTFLD400 IF EQUAL
.
          MATCH     ANSM,ANS
          GOTO      PTFLD500 IF EQUAL
.
          MATCH     ANSS,ANS
          GOTO      PTFLD850 IF EQUAL
.
          MATCH     ANSX,ANS
          GOTO      PTFLD900 IF EQUAL
          GOTO      PTFLD100
.
. Edit part of the Screen
.------------------------
PTFLD200  MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          CALL      FEDIT000
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          GOTO      PTFLD090
.
. Set Video Attributes
.---------------------
PTFLD300  MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          CALL      FVIDE000
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          GOTO      PTFLD090
.
. Redisplay Screen
.---------------------
PTFLD400  MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          CALL      RLOAD000
          CALL      DPFLD000
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          GOTO      PTFLD090
.
. Move A Field
.--------------
PTFLD500  CALL      FMOV0000
          GOTO      PTFLD090
.
. Selections
.-----------
PTFLD850  MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          CALL      SEDIT000
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          GOTO      PTFLD090
.
PTFLD900  RETURN
.----------------------------------
. Selection Edit 
.----------------------------------
SEDIT000  MOVE      "13",HTOP
          MOVE      "1",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          CALL      GETSCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
SEDIT001  DISPLAY   *P1:14,*EF
          HLINE     *G37,14,1,80
          DISPLAY   *P4:15,*V2LON,*ULON," Selection Maintenance ",*HOFF:
                    *P1:17,"    Selection           :":
                    *P1:19,*V2LON," 1.",*HOFF," Item                :":
                    *P1:20,*V2LON," 2.",*HOFF," Description         :":
                    *P1:21,*V2LON," 3.",*HOFF," Security Level      :":
                    *P1:22,*V2LON," 4.",*HOFF," Position Row/Column :"
          MOVE      "27",CCOL
          MOVE      "17",CVERT
          MOVE      TWO,CKEYTYP
          MOVE      SCPRPID,SCSLPID
          MOVE      SCMASID,SCSLSID
          CALL      KSCSLA00
          BRANCH    EXIT,SEDIT900,SEDIT900
          PACK      KEY13,SCPRPID,SCMASID,SCSLSEL
          CALL      RDSCSL1
          IF        OVRCD=0
            MOVE      ZERO,ACCEPT
            PACK      KEY15,PRGIDF,SCRIDF,SCSLITM
            CALL      RDSCFL1
            MOVE      SCSLSEC,F2
            MOVE      F2,F1
            DISPLAY   *P27:19,*V2LON,SCSLITM,SP1,*HOFF,SCFLDES:
                      *P27:20,*V2LON,SCSLDES:
                      *P27:21,*V2LON,F1
            IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=5
              DISPLAY   *P27:22,*V2LON,SCSLROW,*HOFF,"/",*V2LON,SCSLCOL
            ELSE
              DISPLAY   *P27:22,*V2LON,"N/A"
            ENDIF
          ELSE
            MOVE      ONE,ACCEPT
            MOVE      SCPRPID,SCSLPID
            MOVE      SCMASID,SCSLSID
            MOVE      SP70,SCSLITM
            MOVE      SP70,SCSLDES
            MOVE      SP70,SCSLROW
            MOVE      SP70,SCSLCOL
            MOVE      " 0",SCSLSEC
          ENDIF
          MOVE      ZERO,CCITEM
.
SEDIT010  ADD       ONE,CCITEM
          BRANCH    ACCEPT,SEDIT020
.
          CALL      MDELQST
          COMPARE   ZERO,CCITEM
          GOTO      SEDIT800 IF EQUAL
.
          COMPARE   SEQ,CCITEM
          GOTO      SEDIT001 IF EQUAL
.
          COMPARE   "-2",CCITEM
          GOTO      SEDIT850 IF EQUAL
.
SEDIT020  BRANCH    CCITEM,SEDIT110,SEDIT120,SEDIT130,SEDIT140
          BRANCH    ACCEPT,SEDIT030
          BEEP
SEDIT030  MOVE      ZERO,ACCEPT
          GOTO      SEDIT010

.
. Option    1
.
SEDIT110  MOVE      "27",CCOL
          MOVE      "19",CVERT
          MOVE      ZERO,CKEYTYP
          MOVE      SP8,SCFLFNO
          MOVE      SP70,SCFLDES
          MOVE      PRGIDF,SCFLPID
          MOVE      SCRIDF,SCFLSID
          DISPLAY   *PCCOL:CVERT,*V2LON,SCFLFNO,SP1,*HOFF,SCFLDES
          CALL      KSCFLA00
          BRANCH    EXIT,SEDIT110,SEDIT001
.
. Invalid if field is a normal key field
. Invalid if field is a multi item and is not the key field
.
          IF        SCFLTYP=0
            IF        SCFLMUL=0
              DISPLAY   *P1:24,*B,*EL,"Key Field Item - ";
              CALL      HITENTER
              GOTO      SEDIT110
            ENDIF
          ELSE
            IF       SCFLMUL=1
              DISPLAY   *P1:24,*B,*EL:
                        "You may only select the multiple key field - ";
              CALL      HITENTER
              GOTO      SEDIT110
            ENDIF
          ENDIF
.
. Invalid for display or prompt type fields
.
          IF        SCFLTYP=2|SCFLTYP=6
            DISPLAY   *P1:24,*B,*EL,"Display Only Item - ";
            CALL      HITENTER
            GOTO      SEDIT110
          ENDIF
.
. get the position on the screen of the field
.
          PACK      KEY20,SCPRPID,SCMASID,SCFLFNO,SP70
          CALL      RSSCPS2
          CALL      RKSCPS2
          BRANCH    OVRCD,SEDIT116
          MATCH     SCPRPID,SCPSPID
          GOTO      SEDIT116 IF NOT EQUAL
          MATCH     SCMASID,SCPSSID
          GOTO      SEDIT116 IF NOT EQUAL
          MATCH     SCFLFNO,SCPSITM
          GOTO      SEDIT116 IF NOT EQUAL
          MOVE      SCPSROW,SCSLROW
          MOVE      SCPSCOL,SCSLCOL
          DISPLAY   *P27:22,*V2LON,SCSLROW,*HOFF,"/",*V2LON,SCSLCOL
          GOTO      SEDIT117

SEDIT116  IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=5|SCFLTYP=7
            DISPLAY   *P27:22,*EL,*V2LON,"N/A"
          ELSE 
            DISPLAY   *P1:24,*EL,*B,"Item Not on Screen - ";
            CALL      HITENTER
            GOTO      SEDIT110
          ENDIF
.
SEDIT117  MOVE      SCFLFNO,SCSLITM
          DISPLAY   *PCCOL:CVERT,*V2LON,SCFLFNO,SP1,*HOFF,SCFLDES
          IF        ACCEPT=1
            MOVE      SCFLDES,SCSLDES
          ENDIF
.
. check if the item selected is already a selection
.
          PACK      KEY18,SCPRPID,SCMASID,SCFLFNO,SCSLSEL
..          PROC      CKIAS000                * check if already a selection
..          BRANCH    EXIT,SEDIT110           * already a selection
          GOTO      SEDIT010
.
. Option    2
.
SEDIT120  MOVE      "27",CCOL
          MOVE      "20",CVERT
          KEYIN     *PCCOL:CVERT,*RV,*V2LON,SCSLDES:
                    *PCCOL:CVERT,*DV,*V2LON,SCSLDES
          GOTO      SEDIT010
.
.
. Option    3
.
SEDIT130  MOVE      "27",CCOL
          MOVE      "21",CVERT
          MATCH     SP70,SCSLSEC
          IF        @EQUAL
            MOVE      " 0",SCSLSEC
          ENDIF
          MOVE      SCSLSEC,F2
          MOVE      F2,F1
          KEYIN     *PCCOL:CVERT,*RV,*V2LON,F1:
                    *PCCOL:CVERT,*DV,*V2LON,F1
          MOVE      F1,F2
          MOVE      F2,SCSLSEC
          GOTO      SEDIT010
.
. Option    4
.
SEDIT140  MOVE      "27",CCOL
          MOVE      "22",CVERT
          BRANCH    ACCEPT,SEDIT010
          IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=7
            DISPLAY   *P27:22,*V2LON,"N/A"
            GOTO      SEDIT010
          ENDIF
          CALL      RKSCPS2
          BRANCH    OVRCD,SEDIT145
          MATCH     SCPRPID,SCPSPID
          GOTO      SEDIT145 IF NOT EQUAL
          MATCH     SCMASID,SCPSSID
          GOTO      SEDIT145 IF NOT EQUAL
          MATCH     SCSLITM,SCPSITM
          GOTO      SEDIT145 IF NOT EQUAL
          MOVE      SCPSROW,SCSLROW
          MOVE      SCPSCOL,SCSLCOL
          DISPLAY   *P27:22,*V2LON,SCSLROW,*HOFF,"/",*V2LON,SCSLCOL
          GOTO      SEDIT010
.
SEDIT145  PACK      KEY20,SCPRPID,SCMASID,SCSLITM,SP70
          CALL      RSSCPS2
          CALL      RKSCPS2
          BRANCH    OVRCD,SEDIT148
          MATCH     SCPRPID,SCPSPID
          GOTO      SEDIT148 IF NOT EQUAL
          MATCH     SCMASID,SCPSSID
          GOTO      SEDIT148 IF NOT EQUAL
          MATCH     SCSLITM,SCPSITM
          GOTO      SEDIT148 IF NOT EQUAL
          MOVE      SCPSROW,SCSLROW
          MOVE      SCPSCOL,SCSLCOL
          DISPLAY   *P27:22,*V2LON,SCSLROW,*HOFF,"/",*V2LON,SCSLCOL
          GOTO      SEDIT010

SEDIT148  IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=5|SCFLTYP=7
            DISPLAY   *P27:22,*EL,*V2LON,"N/A"
          ENDIF
          GOTO      SEDIT010
.
SEDIT800  
          PACK      KEY13,SCPRPID,SCMASID,SCSLSEL
          UNPACK    KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      RASCSL1
          IF        OVRCD=1
            CALL      WRSCSL1
          ELSE
            CALL      UPSCSL1
          ENDIF
          GOTO      SEDIT001
.
SEDIT850
          PACK      KEY13,SCPRPID,SCMASID,SCSLSEL
          CALL      RASCSL1
          IF        OVRCD=0
            CALL      DESCSL1
          ENDIF
          GOTO      SEDIT001
.
SEDIT900 
          CALL      PUTSCR00
          RETURN
.----------------------------------------------
. Field Edit
.----------------------------------------------
FEDIT000  
          MOVE      SP70,SELKEY13
          PACK      KEY15,SCPRPID,SCMASID,CVERT,CCOL
          CALL      RDSCPS1
          IF        OVRCD=1
            MOVE      ONE,ACCEPT
            GETVAR    CDATA,SCPSATT,1,1
            UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
            MOVE      ZERO,SCPSLEN
            MOVE      SP70,SCPSMTY
            MOVE      SP70,SCPSFMT
            MOVE      SP70,SCPSITM
            MOVE      ZERO,SCPSMAN
            MOVE      ZERO,SCPSNOR
          ELSE
            MOVE      ZERO,ACCEPT
            CALL      UDIS0000                 * Undisplay Screen Item
          ENDIF
.
          CALL      SCRF0000                 * Edit Screen Display
          CALL      SELF0000                 * Modify Field Details
          BRANCH    EXIT,FEDIT900,FEDIT600,FEDIT500
.                   EXIT 0=Post,1=Cancel,2=Delete,3=Repeat
.
. Save Field
.------------
.
          IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=7
            GOTO      FEDIT900
          ELSE
            PACK      KEY15,SCPRPID,SCMASID,SCPSROW,SCPSCOL
            UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
            CALL      RASCPS1
            IF        OVRCD=0
              CALL      UPSCPS1
            ELSE
              CALL      WRSCPS1
            ENDIF
          ENDIF
.        
. Delete the previous selection
. -----------------------------
.
          MATCH     SP70,SELKEY13   * Delete Any Previous Selection
          IF        !@EQUAL
            MOVE      SELKEY13,KEY13
            CALL      RASCSL1
            IF        OVRCD=0
              CALL      DESCSL1
            ENDIF
          ENDIF
.
. Write the new selection
. -----------------------
.
          IF        !(SCFLTYP=1|SCFLTYP=5)
            GOTO      FEDIT900
          ENDIF
          MATCH     SP70,SCSLSEL
          GOTO      FEDIT900 IF EQUAL   * No Selection for Field
.
          PACK      KEY20,SCPRPID,SCMASID,SCPSROW,SCPSCOL,SCPSITM
          UNPACK    KEY20,SCSLPID,SCSLSID,SCSLROW,SCSLCOL,SCSLITM
          MOVE      SCFLDES,SCSLDES
          PACK      KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      RASCSL1
          IF        OVRCD=0
            CALL      UPSCSL1
          ELSE
            CALL      WRSCSL1
          ENDIF
          GOTO      FEDIT900
.
. Repeat Field
.----------------
FEDIT500
          IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=7
            GOTO      FEDIT900
          ELSE
            PACK      KEY15,SCPRPID,SCMASID,SCPSROW,SCPSCOL
            UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
            CALL      RASCPS1
            IF        OVRCD=0
              CALL      UPSCPS1
            ELSE
              CALL      WRSCPS1
            ENDIF
          ENDIF
          CALL      PUTSCR00
          PACK      KEY15,SCPRPID,SCMASID,SCPSROW,SCPSCOL
          CALL      RDSCPS1
          IF        OVRCD=0
            CALL      DPITM000
          ENDIF
          MOVE      SCPSROW,CVERT
.
FEDIT550  KEYIN     *P1:1,*EL,"Number of Line for Repeat : ",*V2LON,F2
          IF        F2+CVERT>24
            DISPLAY   *P1:1,*B,*EL,"That Many would not Fit on the Screen - ";
            CALL      HITENT01
            GOTO      FEDIT550
          ENDIF
FEDIT560  IF        F2=0
            GOTO      FEDIT999
          ENDIF
          SUB       ONE,F2
          ADD       ONE,CVERT
          PACK      KEY15,SCPRPID,SCMASID,CVERT,SCPSCOL
          UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      RASCPS1
          IF        OVRCD=0
            CALL      UPSCPS1
          ELSE
            CALL      WRSCPS1
          ENDIF
          CALL      DPITM000
          GOTO      FEDIT560
.
. Delete Field
.---------------
FEDIT600  PACK      KEY15,SCPRPID,SCMASID,SCPSROW,SCPSCOL
          CALL      RASCPS1
          IF        OVRCD=0
            CALL      DESCPS1
          ENDIF
          PACK      KEY18,SCPRPID,SCMASID,SCPSITM,SP70
          CALL      RSSCSL2
FEDIT610  CALL      RKSCSL2
          BRANCH    OVRCD,FEDIT699
          MATCH     SCPRPID,SCSLPID
          GOTO      FEDIT699 IF NOT EQUAL
          MATCH     SCMASID,SCSLSID
          GOTO      FEDIT699 IF NOT EQUAL
          MATCH     SCPSITM,SCSLITM
          GOTO      FEDIT699 IF NOT EQUAL
          MATCH     SCPSROW,SCSLROW
          GOTO      FEDIT610 IF NOT EQUAL
          MATCH     SCPSCOL,SCSLCOL
          GOTO      FEDIT610 IF NOT EQUAL
          PACK      KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      DESCSL1
          GOTO      FEDIT610
.
FEDIT699  CALL      PUTSCR00
          GOTO      FEDIT999
.
. Cancel
.-------------
FEDIT900  CALL      PUTSCR00
          PACK      KEY15,SCPRPID,SCMASID,SCPSROW,SCPSCOL
          CALL      RDSCPS1
          IF        OVRCD=0
            CALL      DPITM000
          ENDIF
.
FEDIT999  RETURN
.-----------------------------------------
. Item Screen Display
.-----------------------------------------
SCRF0000  MOVE      "1",HLEF
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          MOVE      "1",HTOP
          CALL      GETSCR00
          MOVE      ZERO,HLEF
          MOVE      ZERO,HRIG
          MOVE      ZERO,HBOT
          MOVE      ZERO,HTOP
.
          DISPLAY   *P1:13,*EF
          HLINE     *G37,13,1,80
          DISPLAY   *P4:14,*V2LON,*ULON," Screen Position - ":
                    CCOL,"/",CVERT,SP1,*HOFF:
                    *P2:16,*V2LON,"1. ",*HOFF,"Item Number     : ":
                    *P2:17,*V2LON,"   ",*HOFF,"Field Type      : ":
                    *P2:18,*V2LON,"2. ",*HOFF,"Display Length  : ":
                    *P2:19,*V2LON,"3. ",*HOFF,"Mandatory (Y/N) : "
.
          BRANCH    ACCEPT,SCRF9999
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          CALL      GTYPE000
          DISPLAY   *P23:16,*V2LON,SCPSITM,*HOFF,SP1,SCFLDES
          DISPLAY   *P23:17,*V2LON,KTYPDES
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2|SCFLTYP=5
            DISPLAY   *P45:17,"Min/Max=",*V2LON,SCFLMIN,*HOFF,"/",*V2LON,SCFLMAX
          ENDIF
          DISPLAY   *P23:18,*V2LON,SCPSLEN
          MOVE      NO,YESNO
          LOAD      YESNO,SCPSMAN,YES
          DISPLAY   *P23:19,*V2LON,YESNO  
.
          IF        SCFLTYP=2|SCFLTYP=6
            MOVE      SCFLMAN,SCPSMAN
            DISPLAY   *P23:19,*V2LON,"N/A"
          ENDIF
          IF        SCFLTYP=5
            DISPLAY   *P2:20,*V2LON,"4. ",*HOFF,"Number of Lines : "
            DISPLAY   *P23:20,*V2LON,SCPSNOR
          ENDIF
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2
            DISPLAY   *P2:20,*V2LON,"4. ",*HOFF,"Format String   : "
            IF        SCFLFTY=1
              DISPLAY   *P23:20,*V2LON,SCPSFMT
            ELSE
              MOVE      SP70,SCPSFMT
              DISPLAY   *P23:20,*V2LON,"N/A"
            ENDIF
          ENDIF
          IF        SCFLTYP=1|SCFLTYP=5
            CALL      SCRFSL00
          ENDIF
.
SCRF9999  RETURN
.------------------------------------------------------------------------------
. Get ans Display Selection Data for field types 1 (selection) & 5 (WP) only
.------------------------------------------------------------------------------
SCRFSL00  DISPLAY   *P2:21,*V2LON,"5. ",*HOFF,"Selection       : "
          DISPLAY   *P2:22,*V2LON,"6. ",*HOFF,"Security Level  : "
          PACK      SELKEY13,SP70
          PACK      KEY18,SCPSPID,SCPSSID,SCPSITM,SP70
          CALL      RSSCSL2
SCRFSL10  CALL      RKSCSL2
          BRANCH    OVRCD,SCRFSL90
          PACK      KEY15,SCSLPID,SCSLSID,SCSLITM,SP70
          MATCH     KEY15,KEY18
          GOTO      SCRFSL90 IF NOT EQUAL
.
          DISPLAY   *P23:21,*V2LON,SCSLSEL
          MOVE      SCSLSEC,F2
          MOVE      F2,F1
          DISPLAY   *P23:22,*V2LON,F1
          PACK      SELKEY13,SCSLPID,SCSLSID,SCSLSEL
          GOTO      SCRFSL99
.
SCRFSL90  MOVE      SP3,SCSLSEL
          MOVE      ZERO,SCSLSEC
.
SCRFSL99  RETURN
.----------------------------------------
. FVIDE Edit
.----------------------------------------
FVIDE000  
          PACK      KEY15,SCPRPID,SCMASID,CVERT,CCOL
          CALL      RDSCPS1
          BRANCH    OVRCD,FVIDE900
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          BRANCH    OVRCD,FVIDE900
          IF        SCFLTYP=5
            DISPLAY   *P1:1,*B,*EL,"Function Not Available for WP Field - ";
            CALL      HITENT01
            GOTO      FVIDE999
          ENDIF  
          PACK      KEY15,SCPRPID,SCMASID,CVERT,CCOL
.
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"H",*HOFF,"ighlight, ":
                    *V2LON,"R",*HOFF,"everse, ":
                    *V2LON,"B",*HOFF,"link, ":
                    *V2LON,"U",*HOFF,"nderline "
          MOVE      ZERO,FBLINK
          MOVE      ZERO,FV2LON
          MOVE      ZERO,FHON
          MOVE      ZERO,FULON
          GETVAR    CDATA,CATTR,CCOL,CVERT

FVIDE100  
          PACK      KEY15,SCPRPID,SCMASID,CVERT,CCOL
          CALL      RDSCPS1
          IF        OVRCD=0
            PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
            CALL      RDSCFL1
            IF        OVRCD=0
              IF        !(SCFLTYP=5)
                CALL      PUTCH000
                GETVAR    CDATA,CATTR,CCOL,CVERT
                MOVE      CATTR,SCPSATT
                CALL      UPSCPS1
                CALL      DPITM000
              ENDIF
            ENDIF
          ENDIF
.
FVIDE110  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS;
          CALL      KMOVE000
          BRANCH    EXIT,FVIDE100
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      FVIDE999 IF EQUAL
          REP       "0z1z2z3z4z5z6z7z8z9z",ANS
          REP       "B1U2H3R4",ANS
          TYPE      ANS
          GOTO      FVIDE110 IF NOT EQUAL
          MOVE      ANS,F1
          LOAD      FLAG,F1,FBLINK,FULON,FV2LON,FHON
          BRANCH    FLAG,FVIDE200
          STORE     ONE,F1,FBLINK,FULON,FV2LON,FHON
          GOTO      FVIDE100
FVIDE200  STORE     ZERO,F1,FBLINK,FULON,FV2LON,FHON
          GOTO      FVIDE100
.
FVIDE900  DISPLAY   *P1:1,*B,*EL,"Not on a Field - ";
          CALL      HITENT01
          GOTO      FVIDE999
FVIDE999  RETURN
.-------------------------------------------
. Routine to display blanks for an item
. ie undisplay it
.-------------------------------------------
UDIS0000  
          MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          IF        SCFLTYP=5
            GOTO      UDIS0100
          ENDIF
.
          MOVE      SCPSROW,CVERT
          GETVAR    CDATA,CATTR,1,1
          MOVE      ONE,LLCNT
          WHILE     LLCNT<=SCPSLEN
            MOVE      SCPSCOL,CCOL
            ADD       LLCNT,CCOL
            SUB       ONE,CCOL
            PUTVAR    SP1,CATTR,CCOL,CVERT
            ADD       ONE,LLCNT
          DO
          GOTO      UDIS9999
.
UDIS0100  MOVE      SCPSROW,CTOP
          MOVE      SCPSCOL,CLEF 
          MOVE      SCPSLEN,LLCNT
          MOVE      SCPSNOR,CVERT
          ASSIGN    CLEF+LLCNT+1,CRIG
          ASSIGN    CTOP+CVERT+1,CBOT
          MOVE      SCPSCOL,CCOL 
          BOX       0,CLEF,CTOP,CRIG,CBOT
UDIS9999  
          MOVE      SVERT,CVERT
          MOVE      SCOL,CCOL
          RETURN
.
.-------------------------------------------------
. SELF - Select Field
.-------------------------------------------------
SELF0000  MOVE      ZERO,CCITEM
          MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
.
.---- determine option to execute ----
.
SELF0100  ADD       ONE,CCITEM
          BRANCH    ACCEPT,SELF0500
          CALL      IDELQST
.
          COMPARE   ZERO,CCITEM
          GOTO      SELF9000 IF EQUAL
.
          COMPARE   SEQ,CCITEM
          GOTO      SELF9500 IF EQUAL
.
          COMPARE   "-2",CCITEM
          GOTO      SELF9600 IF EQUAL
          COMPARE   "-3",CCITEM
          GOTO      SELF9700 IF EQUAL
.
SELF0500  BRANCH    CCITEM,SELF1100,SELF1200,SELF1300,SELF1400,SELF1500:
                           SELF1600
.
          BRANCH    ACCEPT,SELF0900
          BEEP
.
SELF0900  MOVE      ZERO,ACCEPT
          GOTO      SELF0100
.
. Option    1
.
SELF1100
          BRANCH    ACCEPT,SELF1110
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          BRANCH    OVRCD,SELF1110
          COMPARE   ZERO,SCFLMAN
          GOTO      SELF1110 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,"Mandatory Field - ";
          CALL      HITENTER
          GOTO      SELF0100
.  
SELF1110  MOVE      "023",CCOL
          MOVE      "16",CVERT
          MOVE      PRGIDF,SCFLPID
          MOVE      SCRIDF,SCFLSID
          MOVE      SCFLFNO,SSCFLFNO
          MOVE      "40",HLEF
          MOVE      "80",HRIG
          MOVE      "1",HTOP
          MOVE      "21",HBOT
          MOVE      ZERO,CKEYTYP
.
          CALL      KSCFLA00
          BRANCH    EXIT,SELF9999,SELF9999
          IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=7
            DISPLAY   *P1:24,*B,*EL,"Invalid Field Type for Display - ";
            CALL      HITENTER
            GOTO      SELF1110
          ENDIF
.
          CALL      SPST0000     * Save Position Variables
          BRANCH    SCFLMUL,SELF1120
.
.         see if the item is already painted
.
          PACK      KEY20,SCPRPID,SCMASID,SCFLFNO,SP70
          CALL      RSSCPS2
          CALL      RKSCPS2
          BRANCH    OVRCD,SELF1190
          MATCH     SCPRPID,SCPSPID
          GOTO      SELF1190 IF NOT EQUAL
          MATCH     SCMASID,SCPSSID
          GOTO      SELF1190 IF NOT EQUAL
          MATCH     SCFLFNO,SCPSITM
          GOTO      SELF1190 IF NOT EQUAL
.
          COMPARE   TWO,SCFLTYP
          GOTO      SELF1190 IF EQUAL       * display only field allowed twice
.
          DISPLAY   *P1:24,*B,*EL,"Field Already On Screen - ";
          CALL      HITENTER
          CALL      RPST0000     * Restore Position Variables
          GOTO      SELF1110
.
. Multiple Item and not key field must be preceded by the key field
. for its Group
.
SELF1120  IF        SCFLTYP=0
            GOTO      SELF1190
          ENDIF
          PACK      KEY15,SCPRPID,SCMASID,SCPSROW,SCPSCOL
          CALL      RSSCPS1
SELF1130  CALL      RPSCPS1
          BRANCH    OVRCD,SELF1180
          MATCH     SCPRPID,SCPSPID
          GOTO      SELF1180 IF NOT EQUAL
          MATCH     SCMASID,SCPSSID
          GOTO      SELF1180 IF NOT EQUAL
          MATCH     SSCPSROW,SCPSROW
          GOTO      SELF1180 IF NOT EQUAL
          CALL      ISAVE000
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          BRANCH    OVRCD,SELF1140
          COMPARE   ZERO,SCFLTYP
          GOTO      SELF1140 IF NOT EQUAL
          MATCH     SCFLMTY,SSCFLMTY
          GOTO      SELF1150 IF EQUAL
SELF1140  CALL      IREST000
          GOTO      SELF1130
SELF1150  CALL      IREST000
          GOTO      SELF1190
.
SELF1180  DISPLAY   *P1:24,*B,*EL:
                    "Multiple Field must be preceded by its Key Field - ";
          CALL      HITENTER
          CALL      RPST0000     * Restore Position Variables
          GOTO      SELF1110
.
SELF1190  CALL      RPST0000     * Restore Position Variables
          MOVE      SCFLFNO,SCPSITM
          MOVE      SCFLMTY,SCPSMTY
          DISPLAY   *PCCOL:CVERT,*V2LON,SCPSITM,*HOFF,SP1,SCFLDES
.
          CALL      GTYPE000
          DISPLAY   *P23:17,*V2LON,KTYPDES
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2|SCFLTYP=5
            DISPLAY   *P45:17,"Min/Max=",*V2LON,SCFLMIN,*HOFF,"/",*V2LON,SCFLMAX
          ENDIF
          IF        SCFLMAX=SCFLMIN
            MOVE      SCFLMAX,SCPSLEN
            DISPLAY   *P23:18,*V2LON,SCPSLEN
            ADD       ONE,CCITEM
            IF        SCFLMAN=1
              ADD       ONE,CCITEM
            ENDIF
          ENDIF
          IF        SCFLMAN=1
            MOVE      SCFLMAN,SCPSMAN
            DISPLAY   *P23:19,*V2LON,YES
          ENDIF
          IF        SCFLTYP=2|SCFLTYP=6
            MOVE      SCFLMAN,SCPSMAN
            DISPLAY   *P23:19,*V2LON,"N/A"
          ENDIF
          MOVE      SCFLFNO,SCPSITM
          MOVE      SCFLMTY,SCPSMTY
          DISPLAY   *P1:20,*EL,*P1:21,*EL,*P1:22,*EL
          IF        SCFLTYP=5
            DISPLAY   *P2:20,*V2LON,"4. ",*HOFF,"Number of Lines : "
          ENDIF
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2
            DISPLAY   *P2:20,*V2LON,"4. ",*HOFF,"Format String   : "
            IF        !(SCFLFTY=1)
              DISPLAY   *P23:20,*V2LON,"N/A"
            ENDIF
          ENDIF
.
          IF        SCFLTYP=1|SCFLTYP=5
            CALL      SCRFSL00
          ENDIF
          GOTO      SELF0100
.
. Option    2
.
SELF1200  MOVE      "023",CCOL
          MOVE      "18",CVERT
          IF        SCFLMAX=SCFLMIN
            MOVE      SCFLMAX,SCPSLEN
            DISPLAY   *P1:24,*B,*EL,"Length Fixed - ";
            CALL      HITENTER
            GOTO      SELF0100
          ENDIF
.
          KEYIN     *PCCOL:CVERT,*V2LON,SCPSLEN:
                    *PCCOL:CVERT,*DV,*V2LON,SCPSLEN:
                    *PCCOL:CVERT
.
          IF        SCPSLEN>SCFLMAX
            DISPLAY   *P1:24,*B,*EL,"Maximum Length = ",SCFLMAX," - ";
            CALL      HITENTER
            GOTO      SELF1200
          ENDIF
          IF        SCPSLEN<SCFLMIN
            DISPLAY   *P1:24,*B,*EL,"Minmum Length = ",SCFLMIN," - ";
            CALL      HITENTER
            GOTO      SELF1200
          ENDIF
          MOVE      SCPSCOL,CCOL 
          ADD       SCPSLEN,CCOL 
          IF        CCOL-1>80
            DISPLAY   *P1:24,*B,*EL,"This would not fit on the Screen - ";
            CALL      HITENTER
            GOTO      SELF1200
          ENDIF
.
          GOTO      SELF0100
.
. Option    3
.
SELF1300  MOVE      "023",CCOL
          MOVE      "19",CVERT
          IF        SCFLTYP=2
            MOVE      SCFLMAN,SCPSMAN
            BEEP
            GOTO      SELF0100
          ENDIF
          IF        SCFLMAN=1
            MOVE      SCFLMAN,SCPSMAN
            DISPLAY   *P1:24,*B,*EL,"Field Must be Mandatory - ";
            CALL      HITENTER
            GOTO      SELF0100
          ENDIF
          MOVE      ANSN,DEFYESNO   * Default Y
          CALL      YORN0000
          MOVE      FYESNO,SCPSMAN
          DISPLAY   *PCCOL:CVERT,*V2LON,YESNO
          GOTO      SELF0100
.
. Option    4
.
SELF1400  MOVE      "023",CCOL
          MOVE      "20",CVERT
          IF        SCFLTYP=5
            KEYIN     *PCCOL:CVERT,*V2LON,SCPSNOR:
                      *PCCOL:CVERT,*DV,*V2LON,SCPSNOR:
                      *PCCOL:CVERT
            MOVE      SCPSROW,CVERT
            ADD       SCPSNOR,CVERT
            IF        CVERT+1>24
              DISPLAY   *P1:24,*B,*EL,"This would not fit on the Screen - ";
              CALL      HITENTER
              GOTO      SELF1400
            ENDIF
            MOVE      SP70,SCPSFMT
            GOTO      SELF0100
          ENDIF
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2
            MOVE      ZERO,SCPSNOR
            IF        SCFLFTY=1
              KEYIN     *PCCOL:CVERT,*V2LON,SCPSFMT:
                        *PCCOL:CVERT,*DV,*V2LON,SCPSFMT:
                        *PCCOL:CVERT
              GOTO      SELF0100
            ENDIF
          ENDIF
          MOVE      SP70,SCPSFMT
          MOVE      ZERO,SCPSNOR
          GOTO      SELF0100
.
. Option 5 ( Selection )
.
SELF1500  IF        !(SCFLTYP=1|SCFLTYP=5)
            IF        ACCEPT=0
              BEEP
            ENDIF
            GOTO      SELF0100
          ENDIF
.
          MOVE      "023",CCOL
          MOVE      "21",CVERT
          MOVE      SP70,SCSLSEL
          PACK      KEY13,SCPRPID,SCMASID,SCSLSEL
          KEYIN     *PCCOL:CVERT,*V2LON,*JR,*RV,SCSLSEL:
                    *PCCOL:CVERT,*DV,SCSLSEL
          ENDSET    SCSLSEL
          APPEND    SP70,SCSLSEL
          RESET     SCSLSEL
          MATCH     SP70,SCSLSEL
          IF        !@EQUAL
            CALL      VALSEL00
            BRANCH    EXIT,SELF1500,SELF9500
          ENDIF
          GOTO      SELF0100
.
. Option 6 ( Security Level )
.
SELF1600  IF        SCFLTYP=1|SCFLTYP=5
            MOVE      "023",CCOL
            MOVE      "22",CVERT
            MATCH     SP70,SCSLSEL
            IF        !@EQUAL
              MOVE      SCSLSEC,F2
              MOVE      F2,F1
              KEYIN     *PCCOL:CVERT,*V2LON,*RV,F1:
                        *PCCOL:CVERT,*DV,F1
              MOVE      F1,F2
              MOVE      F2,SCSLSEC
            ENDIF
          ELSE
            IF        ACCEPT=0
              BEEP
            ENDIF
          ENDIF
          GOTO      SELF0100
.
.---- exit points ----
.
SELF9000  MOVE      ZERO,EXIT
          GOTO      SELF9999
.
SELF9500  MOVE      ONE,EXIT
          GOTO      SELF9999
.
SELF9600
          MOVE      TWO,EXIT
          COMPARE   ZERO,SCFLMAN
          GOTO      SELF9999 IF EQUAL
          COMPARE   ONE,SCFLMUL
          GOTO      SELF9999 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,"IBA Mandatory Field. Deletion Denied - ";
          CALL      HITENTER
          GOTO      SELF0100
SELF9700  
          MOVE      THREE,EXIT
          COMPARE   ONE,SCFLMUL
          GOTO      SELF9999 IF EQUAL
          COMPARE   TWO,SCFLTYP
          GOTO      SELF9999 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,"Field Not Repeatable - ";
          CALL      HITENTER
          GOTO      SELF0100
.
SELF9999  MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          RETURN
.------------------------------------------------------------
. Validate Selection for Field 
.------------------------------------------------------------
VALSEL00  ENDSET    SCSLSEL 
          CMATCH    EXITCHAR,SCSLSEL 
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      VALSEL99
          ENDIF
.
          CMATCH    QUEST,SCSLSEL 
          IF        @EQUAL
            CALL      SPST0000    * Save Position Details
            MOVE      CCITEM,SCCITEM
            CALL      HSCSLA00
            MOVE      SCCITEM,CCITEM
            CALL      RPST0000    * Restore Position Details
.
            ENDSET    SCSLSEL 
            APPEND    SP70,SCSLSEL 
            RESET     SCSLSEL 
.
            MATCH     SP70,SCSLSEL 
            IF        @EQUAL
              MOVE      ONE,EXIT
              GOTO      VALSEL99
            ENDIF
            DISPLAY   *PCCOL:CVERT,*V2LON,SCSLSEL    
          ENDIF
          RESET     SCSLSEL 
.
          MOVE      ZERO,EXIT
          PACK      KEY13,SCPRPID,SCMASID,SCSLSEL
          CALL      RDSCSL1
          BRANCH    OVRCD,VALSEL99
          MATCH     SCSLITM,SCPSITM
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Selection Exists For Another Item - ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
.           
VALSEL99  RETURN
.--------------------------------
. Save Position Variables
.--------------------------------
SPST0000  MOVE      SCPSPID,SSCPSPID
          MOVE      SCPSSID,SSCPSSID
          MOVE      SCPSROW,SSCPSROW
          MOVE      SCPSCOL,SSCPSCOL
          MOVE      SCPSITM,SSCPSITM
          MOVE      SCPSMAN,SSCPSMAN
          MOVE      SCPSLEN,SSCPSLEN
          MOVE      SCPSATT,SSCPSATT
          MOVE      SCPSNOR,SSCPSNOR
          MOVE      SCPSFMT,SSCPSFMT
          MOVE      SCPSMTY,SSCPSMTY
          RETURN
.--------------------------------
. Restore Position Variables
.--------------------------------
RPST0000  MOVE      SSCPSPID,SCPSPID
          MOVE      SSCPSSID,SCPSSID
          MOVE      SSCPSROW,SCPSROW
          MOVE      SSCPSCOL,SCPSCOL
          MOVE      SSCPSITM,SCPSITM
          MOVE      SSCPSMAN,SCPSMAN
          MOVE      SSCPSLEN,SCPSLEN
          MOVE      SSCPSATT,SCPSATT
          MOVE      SSCPSNOR,SCPSNOR
          MOVE      SSCPSFMT,SCPSFMT
          MOVE      SSCPSMTY,SCPSMTY
          RETURN
.
.          PACK      KEY18,SCPSPID,SCPSSID,SCPSSEL,SP70
.-------------------------------------------------------
. FMOV Item
.-------------------------------------------------------
FMOV0000  DISPLAY   *P1:1,*EL,"e",*V2LON,"X",*HOFF,"it"
          MOVE      ONE,MOVEFLAG
          MOVE      CVERT,DIMCVERT
          MOVE      CCOL,DIMCCOL
          PACK      KEY15,SCPRPID,SCMASID,CVERT,CCOL,SP70
          CALL      RDSCPS1
          BRANCH    OVRCD,FMOV9000
          PACK      KEY15,SCPRPID,SCMASID,CVERT,CCOL
          CALL      DESCPS1
.
          CALL      UDIS0000
.
          DISPLAY   *PCCOL:CVERT,*HON,ANSX
          MOVE      SCPSATT,SSCPSATT
          GETVAR    CDATA,SCPSATT,CCOL,CVERT
          DISPLAY   *PCCOL:CVERT,SP1
          MOVE      MINCOL,HLEF
          MOVE      MAXCOL,HRIG
          MOVE      MINLINE,HTOP
          MOVE      MAXLINE,HBOT
          GETVAR    CDATA80,CATTR80,1,CVERT
.
FMOV1000  CALL      DPITM000
          KEYIN     *PCCOL:CVERT,*HOFF,*EOFF,*NOEDIT,*+,ANS
          PUTVAR    CDATA80,CATTR80,1,CVERT
          CALL      KMOVE000
          COMPARE   ZERO,EXIT
          GOTO      FMOV1100 IF EQUAL
          GETVAR    CDATA80,CATTR80,1,CVERT
          MOVE      CCOL,SCPSCOL
          MOVE      CVERT,SCPSROW
          GOTO      FMOV1000
.
FMOV1100  REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      FMOV1000 IF NOT EQUAL
.
          PACK      KEY15,SCPRPID,SCMASID,CVERT,CCOL
          CALL      RASCPS1
          BRANCH    OVRCD,FMOV2000
          DISPLAY   *P1:24,*B,*EL,"Item At This Position - ";
          CALL      HITENTER
          GOTO      FMOV1000
.
FMOV2000  MOVE      SSCPSATT,SCPSATT
          PACK      KEY15,SCPRPID,SCMASID,CVERT,CCOL
          UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      WRSCPS1
          PACK      KEY18,SCPRPID,SCMASID,SCPSITM,SP70
          CALL      RSSCSL2
FMOV2010  CALL      RKSCSL2
          BRANCH    OVRCD,FMOV2100
          MATCH     SCSLPID,SCPRPID
          GOTO      FMOV2100 IF NOT EQUAL
          MATCH     SCSLSID,SCMASID
          GOTO      FMOV2100 IF NOT EQUAL
          MATCH     SCSLITM,SCPSITM
          GOTO      FMOV2100 IF NOT EQUAL
          MATCH     SCSLROW,DIMCVERT
          GOTO      FMOV2010 IF NOT EQUAL
          MATCH     SCSLCOL,DIMCCOL 
          GOTO      FMOV2010 IF NOT EQUAL
          PACK      KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      RDSCSL1
          IF        OVRCD=0
            MOVE      SCPSROW,SCSLROW
            MOVE      SCPSCOL,SCSLCOL
            CALL      UPSCSL1
          ENDIF
          GOTO      FMOV2010
.
FMOV2100  
          MOVE      ZERO,MOVEFLAG
          CALL      DPITM000
          GOTO      FMOV9999
.
FMOV9000  DISPLAY   *P1:1,*B,*EL,"Not on a Field - ";
          CALL      HITENT01
          GOTO      FMOV9999
.
FMOV9999 
          MOVE      ZERO,MOVEFLAG
          RETURN
.******************************************************************************
.         Subroutine to SELECT ITEM, POST, DELETE or CANCEL
.------------------------------------------------------------------------------
MDELQST   MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF,")ost, (":
                    *V2LON,"C",*HOFF,")ancel, (":
                    *V2LON,"D",*HOFF,")elete ? ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      MDELQST IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  C",CCITEMD
          GOTO      MDELQST1 IF EQUAL
.
          MATCH     "  P",CCITEMD
          GOTO      MDELQST2 IF EQUAL
.
          MATCH     "  D",CCITEMD
          GOTO      MDELQST3 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      MDELQSTE IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      MDELQSTE IF LESS
.
          RETURN
.
MDELQSTE  BEEP
          GOTO      MDELQST
.
MDELQST1  MOVE      "-1",CCITEM
          RETURN
.
MDELQST2  MOVE      "0",CCITEM
          RETURN
.
MDELQST3  MOVE      "-2",CCITEM
          RETURN
.
.******************************************************************************
.         Subroutine to SELECT ITEM, POST, DELETE or CANCEL
.------------------------------------------------------------------------------
IDELQST   MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF,")ost, (":
                    *V2LON,"C",*HOFF,")ancel, (":
                    *V2LON,"R",*HOFF,")epeat, (":
                    *V2LON,"D",*HOFF,")elete ? ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      IDELQST IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  C",CCITEMD
          GOTO      IDELQST1 IF EQUAL
.
          MATCH     "  P",CCITEMD
          GOTO      IDELQST2 IF EQUAL
.
          MATCH     "  D",CCITEMD
          GOTO      IDELQST3 IF EQUAL
.
          MATCH     "  R",CCITEMD
          GOTO      IDELQST4 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      IDELQSTE IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      IDELQSTE IF LESS
.
          RETURN
.
IDELQSTE  BEEP
          GOTO      IDELQST
.
IDELQST1  MOVE      "-1",CCITEM
          RETURN
.
IDELQST2  MOVE      "0",CCITEM
          RETURN
.
IDELQST3  MOVE      "-2",CCITEM
          RETURN
.
IDELQST4  MOVE      "-3",CCITEM
          RETURN
.
MVPID000  MOVE     SCPRPID,SCPRPID
          RETURN
.
.
GKTYP000  MOVE     KEYTYPE0,KEYTYPE
          LOAD     KEYTYPE,SCFLKEY,KEYTYPE1,KEYTYPE2
          RETURN  
.
KTYPE000  DISPLAY  *P1:24,*EL,"Type : ",*V2LON,"0.",*HOFF,"Key, ":
                                        *V2LON,"1.",*HOFF,"Sel., ":
                                        *V2LON,"2.",*HOFF,"Dis., ":
                                        *V2LON,"3.",*HOFF,"Fun., ":
                                        *V2LON,"4.",*HOFF,"Scr., ":
                                        *V2LON,"5.",*HOFF,"WP, ":
                                        *V2LON,"6.",*HOFF,"Prompt, ":
                                        *V2LON,"7.",*HOFF,"Gen.Fun.";
          KEYIN    *PCCOL:CVERT,*V2LON,SCFLTYP:
                   *PCCOL:CVERT,*DV,SCFLTYP
          ADD      ONE,SCFLTYP
          BRANCH   SCFLTYP,KTYPE900,KTYPE900,KTYPE900,KTYPE900,KTYPE900:
                           KTYPE900,KTYPE900,KTYPE900
          BEEP
          GOTO     KTYPE000
.
KTYPE900  SUB      ONE,SCFLTYP
          RETURN

GTYPE000  BRANCH   SCFLTYP,GTYPE100,GTYPE200,GTYPE300,GTYPE400,GTYPE500:
                           GTYPE600,GTYPE700
          MOVE     "Key            ",KTYPDES
          GOTO     GTYPE999
GTYPE100  MOVE     "Selection      ",KTYPDES
          GOTO     GTYPE999
GTYPE200  MOVE     "Display        ",KTYPDES
          GOTO     GTYPE999
GTYPE300  MOVE     "Function       ",KTYPDES
          GOTO     GTYPE999
GTYPE400  MOVE     "Screen         ",KTYPDES
          GOTO     GTYPE999
GTYPE500  MOVE     "Word Processor ",KTYPDES
          GOTO     GTYPE999
GTYPE600  MOVE     "Prompt         ",KTYPDES
          GOTO     GTYPE999
GTYPE700  MOVE     "Generated Funct",KTYPDES
          GOTO     GTYPE999
GTYPE999  RETURN

GEXIT000  BRANCH   SCFLFEX,GEXIT100,GEXIT200,GEXIT300
          MOVE     "Exit Screen    ",EXITDES
          GOTO     GEXIT999
GEXIT100  MOVE     "Return Prompt  ",EXITDES
          GOTO     GEXIT999
GEXIT200  MOVE     "Restart Screen ",EXITDES
          GOTO     GEXIT999
GEXIT300  MOVE     "Restore Screen ",EXITDES
          GOTO     GEXIT999
GEXIT999  RETURN
.
KEXIT000  DISPLAY  *P1:24,*EL,"Type : ",*V2LON,"0.",*HOFF," Exit, ":
                                        *V2LON,"1.",*HOFF," Prompt, ":
                                        *V2LON,"2.",*HOFF," Restart, ":
                                        *V2LON,"3.",*HOFF," Restore";
          KEYIN    *PCCOL:CVERT,*V2LON,SCFLFEX:
                   *PCCOL:CVERT,*DV,SCFLFEX
          ADD      ONE,SCFLFEX
          BRANCH   SCFLFEX,KEXIT900,KEXIT900,KEXIT900,KEXIT900
          BEEP
          GOTO     KEXIT000
KEXIT900  SUB      ONE,SCFLFEX
          RETURN
.
HITENT01  KEYIN     "Hit <",*V2LON,"ENTER",*HOFF,"> to continue ":
                    *EOFF,ANS,*P1:01,*EL;
          RETURN
ISAVE000  MOVE      SCFLPID,SSCFLPID
          MOVE      SCFLSID,SSCFLSID
          MOVE      SCFLFNO,SSCFLFNO
          MOVE      SCFLDES,SSCFLDES
          MOVE      SCFLTYP,SSCFLTYP
          MOVE      SCFLFLD,SSCFLFLD
          MOVE      SCFLCAL,SSCFLCAL
          MOVE      SCFLWPF,SSCFLWPF
          MOVE      SCFLFSC,SSCFLFSC
          MOVE      SCFLDEC,SSCFLDEC
          MOVE      SCFLMIN,SSCFLMIN
          MOVE      SCFLMAX,SSCFLMAX
          MOVE      SCFLFLN,SSCFLFLN
          MOVE      SCFLFTY,SSCFLFTY
          MOVE      SCFLKTY,SSCFLKTY
          MOVE      SCFLMAN,SSCFLMAN
          MOVE      SCFLFEX,SSCFLFEX
          MOVE      SCFLDAF,SSCFLDAF
          MOVE      SCFLSPA,SSCFLSPA
          MOVE      SCFLMUL,SSCFLMUL
          MOVE      SCFLMTY,SSCFLMTY
          MOVE      SCFLKEY,SSCFLKEY
          RETURN
          
IREST000  MOVE      SSCFLPID,SCFLPID
          MOVE      SSCFLSID,SCFLSID
          MOVE      SSCFLFNO,SCFLFNO
          MOVE      SSCFLDES,SCFLDES
          MOVE      SSCFLTYP,SCFLTYP
          MOVE      SSCFLFLD,SCFLFLD
          MOVE      SSCFLCAL,SCFLCAL
          MOVE      SSCFLWPF,SCFLWPF
          MOVE      SSCFLFSC,SCFLFSC
          MOVE      SSCFLDEC,SCFLDEC
          MOVE      SSCFLMIN,SCFLMIN
          MOVE      SSCFLMAX,SCFLMAX
          MOVE      SSCFLFLN,SCFLFLN
          MOVE      SSCFLFTY,SCFLFTY
          MOVE      SSCFLKTY,SCFLKTY
          MOVE      SSCFLMAN,SCFLMAN
          MOVE      SSCFLFEX,SCFLFEX
          MOVE      SSCFLDAF,SCFLDAF
          MOVE      SSCFLSPA,SCFLSPA
          MOVE      SSCFLMUL,SCFLMUL
          MOVE      SSCFLMTY,SCFLMTY
          MOVE      SSCFLKEY,SCFLKEY
          RETURN
.         
.---------------------------------- 
. Display Screen Details
.----------------------------------
DSMA0000  DISPLAY   *P1:4,*EF,"    Program ID   : ":
                    *V2LON,*EF,SCPRPID,SP1,*HOFF,SCPRNAM:
                    *P1:5,*EF,"    Screen  ID   : ":
                    *P1:07,*EF,*V2LON," 1.",*HOFF," Description  : ":
                    *P1:08,*EF,*V2LON," 2.",*HOFF," Field file Id: "
          DISPLAY   *P20:05,*V2LON,*EL,SCMASID
          BRANCH    ACCEPT,DSMA9999
.
          DISPLAY   *P20:07,*V2LON,*EL,SCMADES:
                    *P20:08,*V2LON,SCMAFSC
          MOVE      SCMAFSC,SCRIDF
.
DSMA9999  RETURN
.----------------------------------------
. Modify Screen Details
.----------------------------------------
SSMA0000  MOVE      ZERO,CCITEM
SSMA0100  ADD       ONE,CCITEM
          BRANCH    ACCEPT,SSMA0500
          CALL      MDELQST
.
          COMPARE   ZERO,CCITEM
          GOTO      SSMA9000 IF EQUAL
.
          COMPARE   SEQ,CCITEM
          GOTO      SSMA9500 IF EQUAL
.
          COMPARE   "-2",CCITEM
          GOTO      SSMA9400 IF EQUAL
.
SSMA0500  BRANCH    CCITEM,SSMA1100,SSMA1200
.
          BRANCH    ACCEPT,SSMA0900
          BEEP
.
SSMA0900  MOVE      ZERO,ACCEPT
          GOTO      SSMA0100
.
. Screen Description
.
SSMA1100  MOVE      "07",CVERT
          MOVE      "20",CCOL
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,SCMADES:
                    *PCCOL:CVERT,*DV,*V2LON,SCMADES:
                    *PCCOL:CVERT
          ENDSET    SCMADES
          APPEND    SP70,SCMADES
          RESET     SCMADES
.
          MATCH     SP70,SCMADES
          GOTO      SSMA9500 IF EQUAL
          CMATCH    EXITCHAR,SCMADES
          GOTO      SSMA9500 IF EQUAL
          GOTO      SSMA0100
.
. field file screen id
.
SSMA1200  KEYIN     *P20:08,*DV,UNDLN2:
                    *P20:08,*V2LON,SCMAFSC
          PACK      SCMAFSC,SCMAFSC,SP2
          MATCH     SP2,SCMAFSC
          GOTO      SSMA1210 IF EQUAL       * valid screen id
.
          PACK      KEY10,SCMAPID,SCMAFSC
          CALL      RASCMA1
          COMPARE   ZERO,OVRCD
          GOTO      SSMA1210 IF EQUAL       * valid screen id
.
          MATCH     SCMAFSC,SCMASID
          GOTO      SSMA1210 IF EQUAL       * matches current screen id
.
          DISPLAY   *P1:24,*B,*EL,"Invalid Screen Id.  ",*EL;
          CALL      HITENTER
          GOTO      SSMA1200
.
SSMA1210  DISPLAY   *P20:08,*V2LON,SCMAFSC
          MOVE      SCMAFSC,SCRIDF
          GOTO      SSMA0100
.
SSMA9000  CALL      WSCR0000     * Write or Update
          MOVE      ZERO,EXIT
          GOTO      SSMA9999
.
SSMA9400  CALL      DSCR0000     * Delete
          MOVE      ONE,EXIT
          GOTO      SSMA9999
.
SSMA9500  PACK      KEY10,SCPRPID,SCMASID
          CALL      RDSCMA1
          IF        OVRCD=0
            MOVE      ZERO,EXIT
            CALL      DSMA0000
          ELSE
            MOVE      ONE,EXIT
          ENDIF
          GOTO      SSMA9999
.
SSMA9999  RETURN
.-------------------------------------------------
. WSCR - Write to Files
.-------------------------------------------------
WSCR0000  PACK      KEY10,SCMAPID,SCMASID,SP70
          CALL      RASCMA1
          IF        OVRCD=0
            CALL      UPSCMA1
          ELSE
            CALL      WRSCMA1
          ENDIF
WSCR9999  RETURN
.-------------------------------------------------
. DSCR - Delete Record
. ************* Expand for all Files *************
.-------------------------------------------------
DSCR0000  PACK      KEY10,SCMAPID,SCMASID,SP70
          CALL      RASCMA1
          IF        OVRCD=0
            CALL      DESCMA1
          ENDIF
DSCR9999  RETURN
.------------------------------------------------------------
. BSCAN - Scan Screen for Multiple Line Fields and Update
.         Number of lines on screen to the key field.
.------------------------------------------------------------
BSCAN000   PACK     KEY15,SCPRPID,SCMASID,SP70
           PACK     KEY10,SCPRPID,SCMASID,SP70
           CALL     RSSCFL1
BSCAN100
           CALL     RKSCFL1              * Scan for Multiple(Batch) Key Fields
           BRANCH   OVRCD,BSCAN999
           PACK     KEY15,SCFLPID,SCFLSID,SP70
           MATCH    KEY10,KEY15          * Check for Change in Screen
           GOTO     BSCAN999 IF NOT EQUAL
           COMPARE  ZERO,SCFLTYP         * Check for Key Field
           GOTO     BSCAN100 IF NOT EQUAL
           COMPARE  ONE,SCFLMUL          * Check Multiple
           GOTO     BSCAN100 IF NOT EQUAL
.
. Scan for First Item on the Screen
.
           PACK     KEY15,SCFLPID,SCFLSID,SCFLFNO,SP70
           PACK     KEY20,SCFLPID,SCFLSID,SCFLFNO,SP70
           CALL     RSSCPS2
           CALL     RKSCPS2
           BRANCH   OVRCD,BSCAN100    * No Items On Screen Continue Scan
           PACK     KEY20,SCPSPID,SCPSSID,SCPSITM,SP70
           MATCH    KEY15,KEY20       * No Items On Screen Continue Scan
           GOTO     BSCAN100 IF NOT EQUAL
.
. Save Key to First Item
.
           PACK     SAVKEY20,SCPSPID,SCPSSID,SCPSITM,SCPSROW,SCPSCOL
.
. Count Number of Key Items On Screen
.
           PACK     KEY15,SCFLPID,SCFLSID,SCFLFNO,SP70
           PACK     KEY20,SCFLPID,SCFLSID,SCFLFNO,SP70
           MOVE     ZERO,SECTOR
           CALL     RSSCPS2
BSCAN200
           CALL     RKSCPS2
           BRANCH   OVRCD,BSCAN300    * No Items On Screen Continue Scan
           PACK     KEY20,SCPSPID,SCPSSID,SCPSITM,SP70
           MATCH    KEY15,KEY20       * No Items On Screen Continue Scan
           GOTO     BSCAN300 IF NOT EQUAL
           ADD      ONE,SECTOR
           GOTO     BSCAN200
.
. Update First Item With Number Counted on Screen
.
BSCAN300  MOVE      SAVKEY20,KEY20
          CALL      RDSCPS2
          IF        OVRCD=0
            MOVE      SECTOR,SCPSNOR
            CALL      UPSCPS2
          ENDIF
          GOTO     BSCAN100
.
BSCAN999  RETURN
.
          INC       SYSSTDCD
          INC       PAINTDCD
          INC       SCRFLDIO/INC  * Screen Fields
          INC       SCRFLDKY    
          INC       SCRMASIO/INC  * Screen Master File
          INC       SCRMASKY    
          INC       SCRPRGIO/INC  * Program Master File
          INC       SCRPRGKY     
          INC       SCRPSTIO/INC  * Screen Positions
          INC       SCRSBGIO/INC  * Screen Background
          INC       SCRSLTIO/INC  * Screen Selections
          INC       SCRSLTKY    
.
.
.*********************************************************************
.         see if the item is already a selection
.         Para's  : KEY18   (program/screen/item/selection)
.*********************************************************************
          DEFPROC   CKIAS000
.
          INC       SCRSLTFD/INC
.
SAVKEY15  DIM       15
CURRSEL   DIM       3
.
          UNPACK    KEY18,SAVKEY15,KEY3
          MOVE      KEY3,CURRSEL
          OPEN      SCRSLTA2,"scrsltaf"
          PACK      KEY18,SAVKEY15,SP3
          CALL      RSSCSL2
CKIAS100  CALL      RKSCSL2
          BRANCH    OVRCD,CKIAS900
          PACK      KEY15,SCSLPID,SCSLSID,SCSLITM
          MATCH     SAVKEY15,KEY15
          GOTO      CKIAS900 IF NOT EQUAL   * field is not a selction
.
          MATCH     CURRSEL,SCSLSEL
          GOTO      CKIAS100 IF EQUAL       * this is the current selection
.
          DISPLAY   *P1:24,*B,"Item is already a selection (",SCSLSEL,") - ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      CKIAS999
.
CKIAS900  MOVE      ZERO,EXIT
          GOTO      CKIAS999
.
          INC       IBAOVRIO/INC
          INC       SCRSLTIO/INC
.
CKIAS999  ENDPROC
+

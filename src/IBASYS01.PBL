. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBASYS01                         *
. *                                                        *
. *     FUNCTION:         PROGRAM FILE SECURITY            *
. *                                                        *
. *     MODIFICATIONS:                                     *
. *                                                        *
. *           W2.00       28.04.87 E. PHAN  (I.B.A.)       *
. *                       Adapted this program from        *
. *                       IBABIL99 Ver 2.04                *
. *                                                        *
. *           W2.01       22.06.87 E. PHAN  (I.B.A.)       *
. *                       When "?" option selected, screen *
. *                       redisplayed did not include      *
. *                       progname. Fixed.                 *
. *           W2.02       03/08/87 M.HAYSE  (I.B.A.)       *
. *                       NEW COMMON/INC ADDED             *
. *           W2.03       05/08/87 M.HAYSE  (I.B.A.)       *
. *                       PAPER TYPE DIDNT CHECK CODES FILE*
. *           W2.04       10/09/87 G.Williams (I.B.A)      *
. *                       Don't zero-fill Paper Types      *
. *                                                        *
. *           V5.01       23.02.89 B.G.Ackland(I.B.A)      *
. *                       Add Load Programs Option         *
. *                                                        *
. *           V5.02       18.06.89 B.G.Ackland(I.B.A)      *
. *                       Fix bug in print at 415          *
. *                                                        *
. *           V6.00       19/08/91 Steve O'Gorman          *
. *                       Added Program Alias Name         *
. *                                                        *
. *                * A T T E N T I O N *                   *
. *                - - - - - - - - - - -                   *
. *          This   program   is  the   property  of       *
. *          Information Builders Australia  Pty Ltd       *
. *          and must not be utilised,  modified  or       *
. *          copied in any manner whatsoever without       *
. *          the  express   written   permission  of       *
. *          Information Builders Australia Pty Ltd.       *
. *                                                        *
. **********************************************************
.
          INC       STD001FD
.
          INC       IBASECFD/INC
.
.        EXTRA   VARIABLES
.
SECURTXT  FILE   ASCII, FIXED=50
FILENAME  DIM       8
CHG       FORM      1
SEC       FORM      2
FIELD     FORM      2
.
CODE      DIM       2
CODEPT    INIT      "PT"
.
DIM41     DIM       41
DIM50     DIM       50
.
MODE      DIM       1
MODE1     DIM       1
.
PRGALIAS  DIM       8
.
PRGID     INIT      "IBASYS01"
PRGNAM    INIT      "Program File Security"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          DISPLAY   *ES,PRGID:
                    *P56:24,"Opening ibasecuf";
          OPEN      IBASECU1,"ibasecuf"
          OPEN      IBASECU2,"ibasecuf"
.
          DISPLAY   *P64:24,"ibacodef";
          OPEN      IBACODE1,"ibacodef"
.
          CALL      DISPHEAD
.
START     DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert":
                    *P1:6,*V2LON,TWO,*HOFF," = Change":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete":
                    *P1:8,*V2LON,FOUR,*HOFF," = Load Security  ":
                    *P1:9,*V2LON,FIVE,*HOFF," = List  "
.
START0    KEYIN     *P1:11,*EF,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF INSERT,CHANGE,DELETE,LOAD,LIST
          DISPLAY   *P35:10,*EL,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      START0
+
.
.       ADD A PROGRAM SECURITY
.
INSERT    CALL      DISPI
          CALL      DISP1
          MOVE      ANSI,MODE1
          GOTO      KPROG
.
.       LOAD A PROGRAM SECURITY
.
LOAD      KEYIN     *P1:13,*EL,"Enter Security File to Load : ",*V2LON,FILENAME
          ENDSET    FILENAME
          APPEND    SP8,FILENAME
          RESET     FILENAME
          MATCH     SP8,FILENAME
          GOTO      START IF EQUAL
.
          TRAP      OVERCOND IF IO
          OPEN      SECURTXT,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,NOFILE
.
          DISPLAY   *P30:24,"Loading :"
          CALL      LOADFILE
          GOTO      START
.
NOFILE    KEYIN     *P1:24,"File Not Found. Tap Enter.",ANS,*P1:24,*EL
          GOTO      LOAD
       
.
.       CHANGE A PROGRAM SECURITY
.
CHANGE    CALL      DISPC
          CALL      DISP1
          MOVE      ANSC,MODE1
          GOTO      KPROG
RDOPGM    CALL      DISPDA
          GOTO      SFIELD
+
.
.               DELETE A PROGRAM SECURITY
.
DELETE    CALL      DISPD
          CALL      DISP1
          MOVE      ANSD,MODE1
          GOTO      KPROG
.
DELCS     CALL      DISPDA
.
REKEY1    KEYIN     *P1:24,*EL,"Correct data (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
          MATCH     ANSN,ANS
          GOTO      DELETE IF EQUAL
.
          MATCH     ANSY,ANS
          GOTO      REKEY8 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*P50:24,*B,*V2LON:
                    "** You must answer (Y/N) **",*W2;
          GOTO      REKEY1
.
REKEY8    KEYIN     *P1:24,*EL,"Are you sure ":
                    "you want to delete this data (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          MATCH     ANSY,ANS
          GOTO      PROGSDEL IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      REPEAT IF EQUAL
.
          DISPLAY   *P1:24,*EL,*P50:24,*B,*V2LON:
                    "** You must answer (Y/N) **",*W2;
          GOTO      REKEY8
.
PROGSDEL  CALL      DESECU1
          DISPLAY   *P1:24,*EL,*P30:24,*V2LON,"*** DELETED ***",*W2;
          GOTO      REPEAT
+
.
.             INPUT PROGRAM NAME
.
KPROG     MOVE      SP8,PSECNME
          CALL      DISPCL
.
          KEYIN     *P30:5,*EL,*DV,UNDLN8,*P30:5,*V2LON,PSECNME:
                    *P30:5,*DV,PSECNME
.
          RESET     PSECNME
          GOTO      START IF EOS
.
          ENDSET    PSECNME
          APPEND    SP3,PSECNME
          RESET     PSECNME
.
          MOVE      MODE1,MODE
.
          MOVE      PSECNME,KEY8
          CALL      RDSECU1
          BRANCH    OVRCD OF ENDNO
.
          MATCH     ANSC,MODE
          GOTO      RDOPGM IF EQUAL
.
          MATCH     ANSD,MODE
          GOTO      DELCS IF EQUAL
.
          DISPLAY   *P41:5,*B,*V2LON:
                    "*** Program Name already exists *** ",*W2
          GOTO      KPROG
.
ENDNO     MATCH     ANSI,MODE
          GOTO      KDESC IF EQUAL
.
          DISPLAY   *P41:5,*B,*V2LON:
                    "*** Program Name does not exist *** ",*W2
          GOTO      KPROG
+
.
.             INPUT  PROGRAM DESCRIPTION
.
KDESC     PACK      PSECDESC WITH UNDLN,UNDLN
          DISPLAY   *P30:7,*EL,PSECDESC
          MOVE      SP30,PSECDESC
          KEYIN     *P30:7,*V2LON,PSECDESC,*P30:7,*DV,PSECDESC
          MOVE      ONE,CHG
.
          ENDSET    PSECDESC
          APPEND    SP30,PSECDESC
          RESET     PSECDESC
          MATCH     ANSC,MODE
          GOTO      SFIELD IF EQUAL
+
.
.             INPUT SECURITY LEVEL
.
KSECLEV   MOVE      ZERO,PSECLEV
          KEYIN     *P30:8,*EL,*V2LON,PSECLEV
          DISPLAY   *P30:8,*EL,*V2LON,PSECLEV
          MOVE      ONE,CHG
          MATCH     ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.             INPUT SECURITY CHARGE
.
KPSECPW   PACK      PSECPW WITH UNDLN,UNDLN
          DISPLAY   *P30:9,*EL,PSECPW
          MOVE      SP30,PSECPW
          KEYIN     *P30:9,*V2LON,PSECPW,*P30:9,*DV,PSECPW
          MOVE      ONE,CHG
          MATCH     ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.             INPUT PAPER TYPE
.
KPAPER    MOVE      ZERO,PPAPER
          DISPLAY   *P30:10,*EL,UNDLN2
          MOVE      SP3,DIM2
          DISPLAY   *P1:24,*EL,"Options :  0 - No Paper , 1 - Plain Paper":
                               " , 2 - Special Stationary";
          KEYIN     *P30:10,*V2LON,*JR,DIM2
          TYPE      DIM2
          GOTO      INVPAPR IF NOT EQUAL
          MOVE      DIM2,FORM2
          ADD       ONE,FORM2
          BRANCH    FORM2,NOPAPER,PLPAPER,SPPAPER
          GOTO      INVPAPR
.
NOPAPER   MOVE      "No Paper            ",PRXDESC
          GOTO      OKPAPER
.
PLPAPER   MOVE      "Plain Paper         ",PRXDESC
          GOTO      OKPAPER
.
SPPAPER   MOVE      "Special Stationary  ",PRXDESC
.
OKPAPER   MOVE      DIM2,PPAPER
          DISPLAY   *P30:10,*EL,*V2LON,PPAPER,*P40:10,PRXDESC
          MOVE      ONE,CHG
          MATCH     ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
          GOTO      KALIAS
.
INVPAPR   DISPLAY   *P40:10,*EL,*B,*V2LON:
                    "*** Invalid Paper Type *** ",*W2
          GOTO      KPAPER
.
KALIAS    MOVE      SP8,PRGALIAS
          DISPLAY   *P30:11,*EL,UNDLN8
.
          KEYIN     *P30:11,*EL,*V2LON,PRGALIAS
.
          ENDSET    PRGALIAS
          APPEND    SP8,PRGALIAS
          RESET     PRGALIAS
.
          DISPLAY   *P30:11,*EL,*V2LON,PRGALIAS
          MOVE      ONE,CHG
.
          MATCH     SP8,PRGALIAS
          GOTO      ALIASOK IF EQUAL
.
          PACK      KEY16,PRGALIAS,SP30
          CALL      RDSSECU2
          CALL      RDKSECU9                * RDKSECU9 is a read k the same as
.                                           * RDKSECU2 but dosen't change var's
          BRANCH    OVRCD,ALIASOK
.
          MATCH     PRGALIAS,PSEALIAS
          GOTO      ALIASOK IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Program Alias Already Exists - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      KALIAS
.
ALIASOK   GOTO      SFIELD
.
.
.                       SELECT THE FIELD TO UPDATE
.
SFIELD    KEYIN     *P1:24,*EL,"Select field (0 to post) ? ",*V2LON,FIELD;
          COMPARE   ZERO,FIELD
          GOTO      POST1 IF EQUAL
          MOVE      ANSC,MODE
.
RTRCH     BRANCH    FIELD OF KDESC,KSECLEV,KPSECPW,KPAPER,KALIAS
.
INVFLD    DISPLAY   *P40:24,*EL,*B,*V2LON:
                    "*** Invalid Field *** ",*W2;
          GOTO      SFIELD
.
POST1     KEYIN     *P1:24,*EL,"Ok to post (":
                    *V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSC,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          CMATCH    ANSN,ANS
          GOTO      SFIELD IF EQUAL
.
          CMATCH    ANSC,ANS
          GOTO      REPEAT IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      POSTCNT IF EQUAL
          BEEP
          GOTO      POST1
.
POSTCNT   MATCH     ANSC,MODE1
          GOTO      SECUSUP IF EQUAL
.
          MATCH     ANSI,MODE1
          GOTO      SECUSWR IF EQUAL
.
          GOTO      POST1
.
.
.       CHECK CHG INDICATOR TO SEE IF ANYTHING WAS CHANGED
.
SECUSUP   COMPARE   ZERO,CHG
          GOTO      REPEAT IF EQUAL
.
          MOVE      PRGALIAS,PSEALIAS
.
          CALL      UPSECU1
          GOTO      ENDPST
.
SECUSWR   MOVE      PSECNME,KEY8
          MOVE      PRGALIAS,PSEALIAS
.
          CALL      WRSECU1
.
ENDPST    DISPLAY   *P1:24,*EL,*P30:24,*EL,*V2LON:
                    "*** Data successfully posted *** ",*W2;
          GOTO      REPEAT
+
.
.       PROCEDURE TO CLEAN MENU
.
DISPCL    DISPLAY   *P30:5,*EL,*P30:7,*EL,*P30:8,*EL,*P30:9,*EL,*P30:10,*EL:
                    *P30:11,*EF;
          RETURN
.
.       MENUS FOR EACH MODE
.
DISPI     DISPLAY   *P42:2,*EL,*V2LON,"- Insert Mode"
          RETURN
.
DISPC     DISPLAY   *P42:2,*EL,*V2LON,"- Change Mode"
          RETURN
.
DISPD     DISPLAY   *P42:2,*EL,*V2LON,"- Delete Mode"
          RETURN
.
.      MAIN MENU
.
DISP1     DISPLAY   *P1:4,*EF:
                    *P1:5,"    Program Name          :":
                    *P1:7," 1. Program Description   :":
                    *P1:8," 2. Security Level        :":
                    *P1:9," 3. Password              :":
                    *P1:10," 4. Paper Type            :":
                    *P1:11," 5. Program Alias Name    :"
.
          RETURN
+
.
.          SUBROUTINE TO DISPLAY DATA ONTO THE SCREEN
.
DISPDA    PACK      KEY5 WITH CODEPT,PPAPER,SP3
          CALL      RDICOD1
          MOVE      PPAPER,FORM2
          ADD       ONE,FORM2
          BRANCH    FORM2,DISPNOP,DISPPLP,DISPSPP
          GOTO      DISPSPP
.
DISPNOP   MOVE      "No Paper            ",PRXDESC
          GOTO      DISPOK
.
DISPPLP   MOVE      "Plain Paper         ",PRXDESC
          GOTO      DISPOK
.
DISPSPP   MOVE      "Special Stationary  ",PRXDESC
.
DISPOK    DISPLAY   *P30:5,*V2LON,*EL,PSECNME:
                    *P30:7,PSECDESC,*P30:8,*EL,PSECLEV:
                    *P30:9,*EL,PSECPW:
                    *P30:10,*EL,PPAPER,*P40:10,PRXDESC:
                    *P30:11,*EL,PSEALIAS
          RETURN
.
+
.
.          PRINT PROGRAM SECURITY FILE
.
LIST      DISPLAY   *P1:24,*EL,*P30:24,*V2LON:
                    "*** Generating List ***",*W2:
                    *P20:24,*HOFF,*EL,"Program Name :"
          MOVE      ZERO,CPAGENO
          MOVE      SP8,KEY8
          CALL      RDSSECU1
CONT      CALL      RDKSECU1
          BRANCH    OVRCD OF FINISH
          DISPLAY   *P36:24,*V2LON,PSECNME
          COMPARE   ZERO,CPAGENO
          CALL      HEAD IF EQUAL
          COMPARE   "55",CLNO
          GOTO      NEXTL IF LESS
          CALL      HLINE
          CALL      HEAD
NEXTL     CALL      DETAIL
          GOTO      CONT
.
.          FINALISE
.
FINISH    COMPARE   ZERO,CPAGENO
          GOTO      NOPROG IF EQUAL
          CALL      HLINE
          PRINT     *N,*N,*35,"*** END OF REPORT ***"
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      START0
.
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
          PRINT     *F,PRGID,*35,CNAME,*91,"DATE - ",CDATE:
                    *N,VERSION,*35,PRGNAM:
                    *91,"PAGE - ",*103,CPAGENO,*N,*N
          CALL      HLINE
          PRINT     *19,"! Program   ! Program",*66,"! Security  !":
                    *79," Password  ! Paper !"
          PRINT     *19,"! Name      ! Description",*66,"! Level":
                    *78,"!",*90,"! Type  ",*98,"! Alias   !"
          CALL      HLINE
          MOVE      "8",CLNO
          RETURN
.
.
HLINE     PRINT     *19,"*-----------------------------------------":
                    "-----------------------------------------------*"
          RETURN
.
.
DETAIL    PRINT     *19,"!",*21,PSECNME,*31,"!",*33,PSECDESC,*66,"!":
                    *71,PSECLEV,*78,"!",*80,PSECPW,*90,"!   ",PPAPER:
                    *98,"! ",PSEALIAS,"!"
          ADD       ONE,CLNO
          RETURN
.
.
NOPROG    DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "*** FILE IS EMPTY ***",*W2,*P1:24,*EL;
          GOTO      START0
.--------------------------
. load security file
.--------------------------
LOADFILE  READ      SECURTXT,SEQ;PSECNME,PSECDESC,PSECLEV,PSECPW,PPAPER
          GOTO      ENDLOAD IF OVER
          DISPLAY   *P40:24,PSECNME
          MOVE      PSECNME,KEY8
          CALL      RDSECU1
          BRANCH    OVRCD,LOADOK
          GOTO      LOADFILE
LOADOK    CALL      WRSECU1
          GOTO      LOADFILE
ENDLOAD   RETURN
.
          INC       STD001IO
          INC       IBADSCOD
.
          INC       IBASECIO/INC
.
ENDIT     CHAIN     PGM
          STOP
.
.************************************************************************ 
.*                            RDKSECU9                                  *
.* This read next record was introduced to enable the value of PSEALIAS *
.* for the next record to be checked without distroying the value of    *
.* the other variables in the file.                                     *
.************************************************************************ 
.
RDKSECU9 MOVE      ZERO,OVRCD
         READKS    IBASECU2;DIM50:
                            PSEALIAS,DIM41
         GOTO      OVERCOND IF OVER
         RETURN  
.
.

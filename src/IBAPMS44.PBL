.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   IBAPMS44                                                  *
.* Desc      :   Eligibility Report                                        *
.***************************************************************************
.* Date      :   20/02/2009                                                *
.* Author    :   Jill Habasque                                             *
.*                                                                         *
.* Modifications                                                           *
.* V11.00.01 08/09/2020  Peter Vela       TSK 0895969                      *
.*                       Added WebServices OECW                            *
.* V10.14.01 20/03/2019  J.Tan            TSK 0857392                      *
.*                       Changed RCP Transaction count from DIM3 to DIM5   *
.* V10.03.02  15/06/2014 Jill Parkinson CAR 301639                         *
.*                       Added call to KILL000 after PROC0000              *
.* V10.03.01  02/01/2013 Patrick Adair                              CAR 261630 *
.*                       Removed port number from temp file name               *
.* V9.12.01     23/07/2009 Peter Vela     CAR 201110                       *
.*              Display Response Excess Payments (pmseehaf) on Report      *
.* V9.11.01     20/02/2009 Jill Habasque  CAR 153759                       *
.*              Created program                                            *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATELGFD/INC
          INC       PATHSPFD/INC
          INC       PATFN1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATWR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PMSEEHFD/INC
          INC       RCPBNKFD/INC
          INC       RCPDTEFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       WEBEEHFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ADMISFLG  FORM      1
ADMISSNO  DIM       8
BOOKING   DIM       21
BOOKFLAG  FORM      1
CMDLINE   DIM       127
CURRDATE  DIM       8
DIM4      DIM       4
DIM7      DIM       7
DIM9      DIM       9
DIM10     DIM       10
EFUNDH    DIM       6
FORM12P2  FORM      12.2
FROMDATE  DIM       8
OECWINDC  FORM      1
HOSPCODE  DIM       3
PRINTFLG  FORM      1
REPTYPE   DIM       1
SAVHFUND  DIM       6
TMPFILNM  DIM       8
TODATE    DIM       8
.
TEMPFILE  IFILE     SQL, FIXED=83, KEY="U1-8"
TEMPFIL2  IFILE     SQL, FIXED=83, KEY="U17-22,1-8"
.
.Name     Type   Length   Physical   Start   Description
.----     ----   ------   --------   -----   -----------
TMPADMNO  DIM       8        8         1     Admission No
TMPADATE  DIM       8        8         9     Expected Adm date
TMPHFUND  DIM       6        6         17    Health Fund  
TMPCLAIM  DIM       3        3         23    Claim Code   
TMPCCOMP  DIM       1        1         26    Check complete
TMPEXCLS  DIM       1        1         27    Exclusion
TMPQUOTC  DIM       1        1         28    Quote complete
TMPQUOTA  DIM       1        1         29    Quote accepted
TMPCPAYP  DIM       6        6         30    Copayment P
TMPCPAYS  DIM       6        6         36    Copayment S
TMPCPAYT  DIM       6        6         42    Copayment T
TMPEXCES  DIM       6        6         48    Excess
TMPPAYRC  DIM       6        6         54    Payment received
TMPSTATS  DIM       15       15        60    Status
TMPURNUM  DIM       8        8         75    UR Number
.
. End of Record                        83
.
PRGID     INIT      "IBAPMS44"
PRGNAM    INIT      "Expected Admissions Eligibility Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
          CALL      KILL0000
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      CONTROLF,"controlf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATELGA1,"patelgaf"
          OPEN      PATFN1A1,"patfn1af" 
          OPEN      PATMA1A1,"patma1af" 
          OPEN      PATMX1A1,"patmx1af" 
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSPX2A1,"pmspx2af" 
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSEEHA1,"pmseehaf"
          OPEN      RCPDTEA6,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD     * current date
          REP       " 0",CURRDATE
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND29;*61,PTCNWSIN,*71,PTCNOECW,*72,PTCNOECE
.
          MOVE      ONE,OECWINDC
          MATCH     "1",PTCNOECW
          IF         @EQUAL
.
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      WEBEEHA1,"webeehaf"
            TRAPCLR   IO
            IF        OVRCD=1
              MOVE      ZERO,OECWINDC
            ENDIF
.
          ENDIF
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          MOVE      ZERO,EXIT                * Keyin From Date
          MOVE      TWENTY1,CCOL             * Set Up Column
          MOVE      TEN,CVERT                * Set Up Veritcal
          CALL      FDAT0000                 * Keyin From Date 
          BRANCH    EXIT,OPTN0500
.                                            * Keyin To Date
          MOVE      TWENTY1,CCOL             * Set Up Column
          MOVE      TEN1,CVERT               * Set Up Vertical
          CALL      TDAT0000                 * Keyin To Date
          BRANCH    EXIT,OPTN0500
.
          CALL      VDAT0000                 * Fromdate after Todate ?
          BRANCH    EXIT,OPTN0500
.
          IF         IBCNMHOS=1
            MOVE       "15",ECOL
            MOVE       "14",EVERT
            MOVE       SP3,CKYIDEF3
            MOVE       ONE,CKYIMAND           * Mandatory for Multi hospital
            OPEN       PATHSPA1,"pathspaf"
.
            CALL       PATHSPKY
            BRANCH     EXIT,OPTN9000,OPTN9000
            PACK       HOSPCODE,KEY3
            DISPLAY   *P15:14,*V2LON,PTHSNAME
          ENDIF
          KEYIN     *P1:10,*EL,"Report Type 0=Bookings 1=Preadmits 2=Both  : ":
                    *DV,UNDLN1,*P47:10,*V2LON,REPTYPE
          GOTO      OPTN9000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               FDAT0000            Called by: OPTN0000  *
.*                           input From Date                              *
.**************************************************************************
FDAT0000  CALL      IDAT0000                 * Initialize Date Variable
          CALL      KEYDATE                  * Keyin Date
          BRANCH    OVRCD,FDAT9900           * No input, exit
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          GOTO      FDAT9999
.
FDAT9900  MOVE      "1",EXIT                * Exit
.
FDAT9999  RETURN
+
.**************************************************************************
.*                               TDAT0000            Called by: OPTN0000  *
.*                           input To Date                                *
.**************************************************************************
TDAT0000  CALL      IDAT0000                 * Initialize Date Variable
          CALL      KEYDATE                  * Keyin Date
          BRANCH    OVRCD,TDAT9900           * No input, return to fromdate
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
.
          GOTO      TDAT9999
.
TDAT9900  MOVE      "1",EXIT                 * Exit
.
TDAT9999  RETURN
+
.**************************************************************************
.*                                 VDAT0000           Called by: FDAT0000 *
.*                         Validate Date Variables               TDAT0000 *
.*    Returns:  EXIT    = FALSE (0)  Dates Valid continue processing      *
.*                        TRUE  (1)  Dates NOT valid exit                 *
.**************************************************************************
VDAT0000  MATCH     FROMDATE,TODATE             * Fromdate after Todate ?
          GOTO      VDAT9900 IF LESS
.
          GOTO      VDAT9999
.
VDAT9900  DISPLAY   *P1:24,*EL,*B,"From date must be before To date. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
VDAT9999  RETURN
+
.**************************************************************************
.*                                 IDAT0000           Called by: FDAT0000 *
.*                         Initialize Date Variables             TDAT0000 *
.**************************************************************************
IDAT0000  MOVE      ZERO,CCANLDTE
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      ZERO,EXIT
.
IDAT9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      CTMP0000      * Create Temp file 
          CALL      TOPB0000      * write patients to tempfile
          CALL      PRINT000      * print report
.
PROC9999  RETURN
+
.------------------------------------------------------------
.Table of Patient Bookings with Expected Discharge
.------------------------------------------------------------
TOPB0000  PACK      EFUNDH,SP70
          MATCH     "1",REPTYPE
          GOTO      TOPB5000 IF EQUAL
.
          PACK      KEY16,FROMDATE,SP70
          CALL      RSBKREC4                             *Read booking file
TOPB1000  CALL      RKBKREC4
          BRANCH    OVRCD,TOPB5000
.
          MATCH     BKREEDAT,TODATE
          GOTO      TOPB5000 IF LESS
.
          IF        BKRESTAT>0
            GOTO      TOPB1000
          ENDIF
.
          PACK      KEY8,DBKREBOO,SP70
          CALL      RDBKRX11
          BRANCH    OVRCD,TOPB1000
.
          PACK      KEY8,BKREURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,TOPB1000
.
          IF        IBCNMHOS<>1
            GOTO      TOPB1500
          ENDIF
.
          MATCH     SP70,PTHSHOSP
          GOTO      TOPB1500 IF EQUAL
.
          MATCH     SP70,BKREWARD
          GOTO      TOPB1200 IF EQUAL
.
          PACK      KEY6,BKREWARD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,TOPB1000
.
          MATCH     PTHSHOSP,PTWDHOSN
          GOTO      TOPB1000 IF NOT EQUAL
          GOTO      TOPB1500
.
TOPB1200  PACK      KEY6,BKRXPOWD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,TOPB1000
.
          MATCH     HOSPCODE,PTWDHOSN
          GOTO      TOPB1000 IF NOT EQUAL
.
TOPB1500  PACK      EFUNDH,PFUNDH,SP70
          CALL      BKTABC00
          GOTO      TOPB1000
.
. ------ output pre-admitted patients ------
.
TOPB5000  MATCH     "0",REPTYPE
          GOTO      TOPB9000 IF EQUAL
.
          PACK      KEY16,FROMDATE,SP70
          CALL      RDSMISS3
TOPB5500  CALL      RDKMISS3
          BRANCH    OVRCD,TOPB9000
.
          MOVE      ONE,ADMISFLG
          MATCH     ADATE,TODATE
          GOTO      TOPB9000 IF LESS
.
          COMPARE   "1",ASTAT                 * pre-admissions only
          GOTO      TOPB5500 IF NOT EQUAL
.
          MOVE      ZERO,BOOKFLAG             * Set booking flag to no
          MOVE      AADMNO,BKREBOOK
          MOVE      AURNO,BKREURNO
          MOVE      SP70,BKREADAT
          PACK      KEY8,AADMNO,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,TOPB6000
.
TOPB5700  COMPARE   TWO,BKRESTAT
          IF        !@EQUAL
            MOVE      ONE,BOOKFLAG           * Patient has a booking
          ENDIF
.
TOPB6000  PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          IF        OVRCD=1
            MOVE      SP70,PMVXAPWD
            MOVE      SP70,PMVXPSTY
            MOVE      SP70,PMVXFRMS
          ENDIF
.
          IF        IBCNMHOS<>1
            GOTO      TOPB7000
          ENDIF
.
          MATCH     SP70,PTHSHOSP
          GOTO      TOPB7000 IF EQUAL
.
          MATCH     PTHSHOSP,PMVXMHOS
          GOTO      TOPB5500 IF NOT EQUAL
.
TOPB7000  MOVE      AADMNO,BKREBOOK
          MOVE      AURNO,BKREURNO
          MOVE      ADATE,BKREEDAT
          MOVE      ACLAIM,BKRECLMT
          PACK      EFUNDH,AFUNDH,SP70
.
          MATCH     SP70,EFUNDH
          IF        @EQUAL
            PACK      KEY8,BKREURNO,SP70
            CALL      RDMAST1
            BRANCH    OVRCD,TOPB5500
            PACK      EFUNDH,PFUNDH
          ENDIF
.
          CALL      BKTABC00
          GOTO      TOPB5500
.
TOPB9000
TOPB9999  RETURN
+
BKTABC00  PACK     ADMISSNO,BKREBOOK,SP70
          COMPARE  ONE,ADMISFLG
          GOTO     BKTABC10 IF EQUAL
.
          IF       BKRESTAT = 0
            MOVE     "Booked",BOOKING
            MATCH    SP70,BKREWARD
            IF       !@EQUAL
              MOVE     BKREWARD,AWARD
            ELSE
              MOVE     BKRXPOWD,AWARD
            ENDIF
          ENDIF
          GOTO     BKTABC15
.
BKTABC10 COMPARE   ONE,BOOKFLAG
          IF        @EQUAL
            MOVE     "Pre-Adm/Booked",BOOKING
          ELSE
            MOVE     "Pre-Admitted",BOOKING
          ENDIF
.
BKTABC15  PACK      KEY8,BKREBOOK,SP70
          CALL      RDTFIL1
          IF        OVRCD<>1
            GOTO      BKTABC99
          ENDIF
.
          PACK      TMPADMNO,BKREBOOK,SP70
          PACK      TMPADATE,BKREEDAT,SP70
          PACK      TMPHFUND,EFUNDH,SP70
          PACK      TMPCLAIM,BKRECLMT,SP70
.
          MOVE      TMPADMNO,KEY8
          CALL      RDPTELG1
          IF        OVRCD=1
            CALL      CLRELG00               * Clear eligibility fields
          ENDIF
.
.***********************************************
.
          MOVE      TMPADMNO,KEY20
          LJUSTIFY  KEY20
.
          MATCH     "1",PTCNWSIN
          GOTO      BKTABC50 IF NOT EQUAL
.
          MATCH     "1",PTCNOECW
          GOTO      BKTABC50 IF NOT EQUAL

.
          COMPARE   ONE,OECWINDC
          GOTO      BKTABC50 IF NOT EQUAL
.
          PACK      KEY23,KEY20,Z70
          CALL      RSWBEEH1
          CALL      RPWBEEH1
          IF        OVRCD=1
            CALL      CLWBEH00
          ENDIF
.
          MATCH     KEY20,WBEHARID
          IF        !@EQUAL
            CALL      CLWBEH00
          ENDIF
.
          PACK      TMPCCOMP,PTELCMPL,SP70
          PACK      TMPEXCLS,PTELXCLS,SP70
          PACK      TMPQUOTC,PTELCMPL,SP70
          PACK      TMPQUOTA,PTELQACC,SP70
          UNPACK    PTELPSHA,DIM9,TMPCPAYS
          UNPACK    PTELPTHE,DIM9,TMPCPAYT
.
          MATCH     SP70,WBEHCPAM
          IF        !@EQUAL & !@EOS
            MOVE      SP70,DIM9
            MOVE      WBEHCPAM,DIM9
            RJUSTIFY  DIM9
            UNPACK    DIM9,DIM7,DIM2
            PACK      DIM10,DIM7,DOT,DIM2
            UNPACK    DIM10,DIM4,TMPCPAYP
          ELSE
            UNPACK    PTELPPRI,DIM9,TMPCPAYP
          ENDIF
.
          MATCH     SP70,WBEHEXAM
          IF        !@EQUAL & !@EOS
            MOVE      SP70,DIM9
            MOVE      WBEHEXAM,DIM9
            RJUSTIFY  DIM9
            UNPACK    DIM9,DIM7,DIM2
            PACK      DIM10,DIM7,DOT,DIM2
            UNPACK    DIM10,DIM4,TMPEXCES
          ELSE
            UNPACK    PTELXCSS,DIM9,TMPEXCES
          ENDIF
.
          GOTO      BKTABC80           
.
BKTABC50  MATCH     "1",PTCNOECE
          GOTO      BKTABC80 IF EQUAL
.
          PACK      KEY23,KEY20,Z70
          CALL      RSPMEEH1
          CALL      RPPMEEH1
          IF        OVRCD=1
            CALL      CLPMEH00
          ENDIF
.
          MATCH     KEY20,PMHEARID
          IF        !@EQUAL
            CALL      CLPMEH00
          ENDIF
.
          PACK      TMPCCOMP,PTELCMPL,SP70
          PACK      TMPEXCLS,PTELXCLS,SP70
          PACK      TMPQUOTC,PTELCMPL,SP70
          PACK      TMPQUOTA,PTELQACC,SP70
          UNPACK    PTELPSHA,DIM9,TMPCPAYS
          UNPACK    PTELPTHE,DIM9,TMPCPAYT
.
          MATCH     SP70,PMHECPAM
          IF        !@EQUAL & !@EOS
            MOVE      SP70,DIM9
            MOVE      PMHECPAM,DIM9
            RJUSTIFY  DIM9
            UNPACK    DIM9,DIM7,DIM2
            PACK      DIM10,DIM7,DOT,DIM2
            UNPACK    DIM10,DIM4,TMPCPAYP
          ELSE
            UNPACK    PTELPPRI,DIM9,TMPCPAYP
          ENDIF
.
          MATCH     SP70,PMHEEXAM
          IF        !@EQUAL & !@EOS
            MOVE      SP70,DIM9
            MOVE      PMHEEXAM,DIM9
            RJUSTIFY  DIM9
            UNPACK    DIM9,DIM7,DIM2
            PACK      DIM10,DIM7,DOT,DIM2
            UNPACK    DIM10,DIM4,TMPEXCES
          ELSE
            UNPACK    PTELXCSS,DIM9,TMPEXCES
          ENDIF
.
.*******************************************************
.
BKTABC80  PACK      TMPSTATS,BOOKING,SP70
          PACK      TMPURNUM,BKREURNO,SP70
          CALL      GPAY0000
.
          PACK      KEY8,TMPADMNO,SP70
          CALL      WRTFIL1 
.
BKTABC99  RETURN
+
.------------------------------------------------------------
.Print details
.------------------------------------------------------------
PRINT000  CALL      HEAD132
.
          MOVE      ZERO,PRINTFLG
          PACK      SAVHFUND,SP70
          PACK      KEY14,SP70
          CALL      RSTFIL2
PRINT100  CALL      RKTFIL2
          BRANCH    OVRCD,PRINT900
.
          IF        PRINTFLG=0
            GOTO      PRINT150
          ENDIF
.
          MATCH     SAVHFUND,TMPHFUND
          GOTO      PRINT200 IF EQUAL
.
PRINT150  PACK      SAVHFUND,TMPHFUND,SP70
          PACK      KEY14,SAVHFUND,ZERO,ZERO,ZERO,ZERO,SP70
          CALL      RDFUND1
          IF        OVRCD=1
            MOVE      SAVHFUND,HFNAME
          ENDIF
.
          MATCH     SP70,HFNAME
          IF        !@EQUAL
            CALL      UND132
          ENDIF
. 
          PRINT     *10,"Health Fund : ",HFNAME
          CALL      UND132
          PRINT     *1,"|Name",*30,"|UR Number",*40,"|Exp Adm Date":
                    *53,"|Claim Code",*64,"|Check":
                    *70,"|Quote",*76,"|Quote":
                    *82,"|Excl",*87,"|Co-payment",*98,"|Excess":
                    *105,"|Payment",*113,"|Status",*132,"|"
.
          PRINT     *1,"|",*30,"|",*40,"|":
                    *53,"|",*64,"|Comp":
                    *70,"|Comp",*76,"|Accep":
                    *82,"|",*87,"|",*98,"|":
                    *105,"|",*113,"|",*132,"|"
.
          CALL      UND132
.
PRINT200  PACK      KEY8,TMPURNUM,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PRINT250
.
          MOVE      SP70,KEY28
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format the patients name
          PACK      KEY28,PACFNAME
.
          UNPACK    TMPADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,"|",KEY28,*30,"|",TMPURNUM,*40,"|",CPCDATE:
                    *53,"|",TMPCLAIM,*64,"|";
          MATCH     "1",TMPCCOMP
          IF        @EQUAL
            PRINT     "Yes";
          ENDIF 
          PRINT     *70,"|";
          MATCH     "1",TMPQUOTC
          IF        @EQUAL
            PRINT     "Yes";
          ENDIF 
          PRINT     *76,"|";
          MATCH     "1",TMPQUOTA
          IF        @EQUAL
            PRINT     "Yes";
          ENDIF 
          PRINT     *87,"|",TMPCPAYP:
                    *98,"|",TMPEXCES:
                    *105,"|",TMPPAYRC,*113,"|",TMPSTATS,*132,"|"
.
          MATCH     SP70,TMPCPAYS
          IF        !@EQUAL
            PRINT     *1,"|",*30,"|",*40,"|":
                      *53,"|",*64,"|":
                      *70,"|",*76,"|":
                      *82,"|",*87,"|",TMPCPAYS,*98,"|":
                      *105,"|",*113,"|",*132,"|"
          ENDIF
.
          MATCH     SP70,TMPCPAYT
          IF        !@EQUAL
            PRINT     *1,"|",*30,"|",*40,"|":
                      *53,"|",*64,"|":
                      *70,"|",*76,"|":
                      *82,"|",*87,"|",TMPCPAYT,*98,"|":
                      *105,"|",*113,"|",*132,"|"
          ENDIF
.
PRINT250  PACK      SAVHFUND,TMPHFUND
          MOVE      ONE,PRINTFLG
          GOTO      PRINT100
.
PRINT900  CALL      UND132
          PRINT     *1,"**** End of Report ****"
.
PRINT999  RETURN
+
.----------------------------------------------------------------------
.         Get payment recieved
.----------------------------------------------------------------------
GPAY0000  MOVE      ZERO,FORM12P2
          PACK      KEY25,TMPADMNO,SP70
          CALL      RSRCDTE6
GPAY1000  CALL      RKRCDTE6
          BRANCH    OVRCD,GPAY9999
.
          MATCH     TMPADMNO,RCDTVIST
          GOTO      GPAY9999 IF NOT EQUAL
.
          PACK      KEY12,RCDTRECN,SP70
          CALL      RDRCBNK1
          BRANCH    OVRCD,GPAY2000
.
          MATCH     "1",RCBNSTAT
          GOTO      GPAY1000 IF EQUAL            * Already cancelled
.
GPAY2000  ADD       RCDTAMNT,FORM12P2
          GOTO      GPAY1000
.
GPAY9999  PACK      TMPPAYRC,FORM12P2
          RETURN
.==============================================================================
.                Tempfile I/O
.==============================================================================
RSTFIL1   RESET     KEY8
          READ      TEMPFILE,KEY8;;
          RETURN
.
RKTFIL1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TMPADMNO,TMPADATE,TMPHFUND,TMPCLAIM:
                             TMPCCOMP,TMPEXCLS,TMPQUOTC,TMPQUOTA:
                             TMPCPAYP,TMPCPAYS,TMPCPAYT,TMPEXCES:
                             TMPPAYRC,TMPSTATS,TMPURNUM
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTFIL1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMPFILE,KEY8;TMPADMNO,TMPADATE,TMPHFUND,TMPCLAIM:
                             TMPCCOMP,TMPEXCLS,TMPQUOTC,TMPQUOTA:
                             TMPCPAYP,TMPCPAYS,TMPCPAYT,TMPEXCES:
                             TMPPAYRC,TMPSTATS,TMPURNUM
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTFIL1   RESET     KEY8
          WRITE     TEMPFILE,KEY8;TMPADMNO,TMPADATE,TMPHFUND,TMPCLAIM:
                             TMPCCOMP,TMPEXCLS,TMPQUOTC,TMPQUOTA:
                             TMPCPAYP,TMPCPAYS,TMPCPAYT,TMPEXCES:
                             TMPPAYRC,TMPSTATS,TMPURNUM
          RETURN
.
RSTFIL2   RESET     KEY14
          READ      TEMPFIL2,KEY14;;
          RETURN
.
RKTFIL2   MOVE      ZERO,OVRCD
          READKS    TEMPFIL2;TMPADMNO,TMPADATE,TMPHFUND,TMPCLAIM:
                             TMPCCOMP,TMPEXCLS,TMPQUOTC,TMPQUOTA:
                             TMPCPAYP,TMPCPAYS,TMPCPAYT,TMPEXCES:
                             TMPPAYRC,TMPSTATS,TMPURNUM
          GOTO      OVERCOND IF OVER
          RETURN
.
.-----------------------------------------------------------
.         Clear eligibility fields
.-----------------------------------------------------------
CLRELG00  CALL      CLPATELG
.
CLRELG99  RETURN
+
.******************************************************************************
.*                             CTMP0000                 Called by:            *
.*                        create temporary file                               *
.******************************************************************************
CTMP0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TMPFILNM
.
          CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          APPEND    " 83 U1-8 U17-22,1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TMPFILNM
          OPEN      TEMPFIL2,TMPFILNM
.
CTMP9999  RETURN
+
.******************************************************************************
.*                             KILL0000                  Called by:           *
.*                   erase temporary file                                     *
.******************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLOSE     TEMPFIL2
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.------------------------------------------------------------
.   Clear pmseehaf
.------------------------------------------------------------
CLPMEH00  PACK      PMHEARID,SP70
          PACK      PMHERPTC,SP70
          PACK      PMHECTID,SP70
          PACK      PMHEBENL,SP70
          PACK      PMHECFAC,SP70
          PACK      PMHECPAM,SP70
          PACK      PMHECPDR,SP70
          PACK      PMHECPDI,SP70
          PACK      PMHECPFN,SP70
          PACK      PMHECPMN,SP70
          PACK      PMHECPRN,SP70
          PACK      PMHEEXAM,SP70
          PACK      PMHEEADI,SP70
          PACK      PMHEEXBA,SP70
          PACK      PMHEEXDI,SP70
          PACK      PMHEFSTA,SP70
          PACK      PMHEFRID,SP70
          PACK      PMHEFSCD,SP70
          PACK      PMHEMSCD,SP70
          PACK      PMHEPEAI,SP70
          PACK      PMHEPSCD,SP70
          PACK      PMHETNAM,SP70
          PACK      PMHETBDI,SP70
          PACK      PMHETBSC,SP70
          PACK      PMHEVCID,SP70
          PACK      PMHEUDTE,SP70
          PACK      PMHEUTIM,SP70
          PACK      PMHESPAR,SP70
.
CLPMEH99  RETURN
+
.------------------------------------------------------------
.   Clear webeehaf
.------------------------------------------------------------
CLWBEH00  PACK      WBEHARID,SP70
          PACK      WBEHRPTC,SP70
          PACK      WBEHCTID,SP70
          PACK      WBEHBENL,SP70
          PACK      WBEHCFAC,SP70
          PACK      WBEHCPAM,SP70
          PACK      WBEHCPDR,SP70
          PACK      WBEHCPDI,SP70
          PACK      WBEHCPFN,SP70
          PACK      WBEHCPMN,SP70
          PACK      WBEHCPRN,SP70
          PACK      WBEHEXAM,SP70
          PACK      WBEHEADI,SP70
          PACK      WBEHEXBA,SP70
          PACK      WBEHEXDI,SP70
          PACK      WBEHFSTA,SP70
          PACK      WBEHFRID,SP70
          PACK      WBEHFSCD,SP70
          PACK      WBEHMSCD,SP70
          PACK      WBEHPEAI,SP70
          PACK      WBEHPSCD,SP70
          PACK      WBEHTNAM,SP70
          PACK      WBEHTBDI,SP70
          PACK      WBEHTBSC,SP70
          PACK      WBEHVCID,SP70
          PACK      WBEHUDTE,SP70
          PACK      WBEHUTIM,SP70
          PACK      WBEHSPAR,SP70
.
CLWBEH99  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CLPATELG
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATHSPKY
          INC       PATFN1IO/INC
          INC       PATELGIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSPX2IO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PMSVX1IO/INC
          INC       PMSEEHIO/INC
          INC       RCPBNKIO/INC
          INC       RCPDTEIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
          INC       WEBEEHIO/INC

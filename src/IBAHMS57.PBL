.*****************************************************************************
.*    Program      : IBAHMS57                                                *
.*    Description  : Shortfall of Clinical data report                       *
.*                                                                           *
.*    Author       : ROD    (11/03/93)                                       *
.*    Modifications:                                                         *
.*        V12.00.01 30/05/2025 Jacob Jackson    TSK 0955096                  *
.*                  Alphanumeric changes                                     *
.*        V10.04.01 12/06/2014 Steve Armstrong  CAR 301639                   *
.*                  Moved calls to TFILENAM into INIT0000 and added call to  *
.*                  DTPD0000 on exit of program.                             *
.*****************************************************************************
.*        V10.03.01 24/12/2012 Patrick Adair   CAR 261630                    *
.*                  Removed port number from temp file name                  *
.*****************************************************************************
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V9.03.00  28/10/2003  Steve Armstrong    CAR 44635                 *
.*                  Ported from r5.06.                                       *
.*****************************************************************************
.*        V5.02.01  21/12/94  Graeme Williams     SRF 441174                 *
.*                  Altered the way the program did it's page breaking       *
.*****************************************************************************
.*        V5.01.05  26/04/94  Paul Howells     SRF 440767                    *
.*                  Look at FCE's for discharged patients only               *
.*        V5.01.04  31/01/94  ROD       SRF 440588                           *
.*                  Change to use FCE's not just discharges                  *
.*        V5.01.003 30/03/93  ROD                                            *
.*                  Fix error in writing episode temp records                *
.*        V5.01.002 25/03/1993  Steve Armstrong                              *
.*                  Mods for episode in PATCEDFD                             *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATDAYFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PATWR1FD/INC
          INC       PATTRNFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. Temp file (1) vars
.
HMSTMPA1  IFILE     SQL, FIXED=22, KEY="U1-3,4-6,7-12"
XXHOSP    DIM       3        1       * hospital
XXSPEC    DIM       3        4       * specialty
XXCONS    DIM       6        7       * consultant
XXFINEP   FORM      4       13       * finished episodes
XXCODEP   FORM      4       16       * coded episodes
XXUNCEP   FORM      4       19       * uncoded episodes
.        End of record      22
.
TEMPD     IFILE     SQL, FIXED=9, KEY="U1-8"
KEYD      INIT      " 9 U1-8"
.
.
. LOCAL VARIABLES
. ---------------
.
CFNAMED   DIM       8
CMDLINE   DIM       80
CODE      DIM       2
CONSDESC  DIM       35
CRDYDATE  DIM       8
.
DIM3      DIM       3
DOCCODE   DIM       6
.
EKYSPEC   DIM       3
ENDCODE   DIM       6
ENDESCOD  DIM       6
ENDESTYP  DIM       6
.
FDDATE    DIM       10
FNAME     DIM       8
FORM5     FORM      5
FROMDATE  DIM       8
.
GRNCOD    FORM      6
GRNFIN    FORM      6
GRNUNC    FORM      6
.
HOSPNAME  DIM       30
.
KYHOSP    DIM       3
.
SAVBED    DIM       3
SAVEPNO   FORM      2
SAVHOSP   DIM       3
SAVSPEC   DIM       3
SHRTFALL  FORM      3.2
SKYSPEC   DIM       3
SPECDESC  DIM       20
STDCODE   DIM       6
STDESCOD  DIM       6
STDESTYP  DIM       6
.
TDDATE    DIM       10
TODATE    DIM       8
TOTCOD    FORM      5
TOTFIN    FORM      5
TOTSHRT   FORM      3.2
TOTUNC    FORM      5
.
YEARFILE  DIM       4         * current file year
YEARSTOP  FORM      4         * ending   year    ccyy
.
.
. PROGRAM CONSTANTS
. -----------------
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
TOTUNDLN  INIT      "*----------------------------------------------------*"
.
.
PRGID     INIT      "IBAHMS57"
PRGNAM    INIT      "Shortfall of Clinical Data Report"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
.
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
          CALL      PROC0000                      * keyin search fields
          BRANCH    EXIT,ML1000                   * Cancel or EXITCHAR
.
          CALL      CRET0000                      * create/erase temp file
          CALL      EXTR0000
          BRANCH    EXIT,ML1000                   * no data found
.
          CALL      RPRT0000                      * Print Report
          GOTO      ML1000
.
ML9999    CALL      KILL0000                      * remove temp file
          CALL      DTPD0000                      * remove temp file
          CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"patecdaf"
          OPEN      PATECDA1,"patecdaf"
.
          DISPLAY   *P60:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P60:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P60:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P1:24,*EL;
.
          MOVE      ONE,CNEWDS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,CFNAMED
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
.
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print report"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          IF        MOPTION = 0
            MOVE      ONE,EXIT
            GOTO      MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* PROC0000  :  Keyin Hospital, Specialty, Consultant, Date ranges          *
.****************************************************************************
.
PROC0000  DISPLAY   *P1:10,*EF;
.
          CALL      HOSP0000                      * keyin Hospital code
          BRANCH    EXIT,PROC9000                 * EXITCHAR
.
          CALL      SPEC0000                      * keyin specialty's
          BRANCH    EXIT,PROC9000                 * EXITCHAR
.
          CALL      CONS0000                      * keyin consultants
          BRANCH    EXIT,PROC9000                 * EXITCHAR
.
          CALL      KDAT0000                      * keyin FCE date range
.
. ask Continue?
.
          CALL      CONTQST
          BRANCH    CEXIT,PROC8000,PROC0000,PROC9000
.
PROC8000  MOVE      ZERO,EXIT                     * everything cool!
          GOTO      PROC9999
.
PROC9000  MOVE      ONE,EXIT                      * EXITCHAR or Cancel
.
PROC9999  RETURN
+
.****************************************************************************
.* HOSP0000  :  Keyin Hospital code                                         *
.****************************************************************************
.
HOSP0000  DISPLAY   *P1:10,*EL,"Hospital            : "
          MOVE      "23",CCOL
          MOVE      "10",CVERT
          MOVE      CCOL,ECOL
          MOVE      CVERT,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
.
          CALL      PATHSPKY
          BRANCH    EXIT,HOSP2000,HOSP9000        * nothing or EXITCHAR
.
          MOVE      KEY3,KYHOSP                   * valid input
          MOVE      PTHSNAME,HOSPNAME
          DISPLAY   *P28:10,PTHSNAME
          GOTO      HOSP9999
.
HOSP2000  MOVE      SP3,KYHOSP                    * nothing input
          DISPLAY   *P23:10,*V2LON,"All"
          MOVE      "All",HOSPNAME
          MOVE      ZERO,EXIT
          GOTO      HOSP9999
.
HOSP9000  MOVE      ONE,EXIT                      * EXITCHAR input
.
HOSP9999  RETURN
+
.****************************************************************************
.*  SPEC0000  :  Keyin range of Specialty's                                 *
.****************************************************************************
.
SPEC0000  DISPLAY   *P1:12,*EL,"Specialty      from : ":
                    *P1:13,*EL,"               to   : "
.
          MOVE      "23",ECOL                     * set up parameters
          MOVE      "12",EVERT
          MOVE      "A ",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
.
          CALL      PATCODKY
          BRANCH    EXIT,SPEC2000,SPEC9000        * nothing, EXITCHAR input
.
          MOVE      ACODE,SKYSPEC                 * start specialty
          MOVE      ACODE,STDESTYP                * start specialty print
          DISPLAY   *P28:12,TDESC                 * display description
          GOTO      SPEC5000
.
. nothing input for start specialty
.
SPEC2000  MOVE      SP3,SKYSPEC                   * start doctor type
          MOVE      "Start",STDESTYP              * print var
          DISPLAY   *P23:12,*V2LON,"Start"
.
. Keyin Ending Specialty
.
SPEC5000  MOVE      "23",ECOL                     * set up parameters
          MOVE      "13",EVERT
          MOVE      "A ",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
.
          CALL      PATCODKY
          BRANCH    EXIT,SPEC7000,SPEC9000        * nothing, EXITCHAR input
.
          MOVE      ACODE,EKYSPEC                 * end specialty
.
          MATCH     SKYSPEC,EKYSPEC               * valid range?
          GOTO      SPEC6000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      SPEC0000
.
SPEC6000  MOVE      ACODE,ENDESTYP                * end specialty print
          DISPLAY   *P28:13,TDESC                 * display description
          MOVE      ZERO,EXIT
          GOTO      SPEC9999
.
. nothing input for end doctor type
.
SPEC7000  MOVE      "zzz",EKYSPEC                 * end specialty
          MOVE      "Finish",ENDESTYP             * print var
          DISPLAY   *P23:13,*V2LON,"Finish"
          MOVE      ZERO,EXIT
          GOTO      SPEC9999
.
SPEC9000  MOVE      ONE,EXIT                      * EXITCHAR
.
SPEC9999  RETURN
+
.****************************************************************************
.*  CONS0000  :  Keyin range of Doctor codes                                *
.****************************************************************************
.
CONS0000  DISPLAY   *P1:15,*EL,"Consultant     from : ":
                    *P1:16,*EL,"               to   : "
.
          MOVE      "23",ECOL                     * set up parameters
          MOVE      "15",EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
.
          CALL      PATDOCKY
          BRANCH    EXIT,CONS2000,CONS9000        * nothing, EXITCHAR input
.
          MOVE      DOCCODE,STDCODE               * start doctor code
          MOVE      DOCCODE,STDESCOD              * start doctor code (print)
          MOVE      DOCCODE,KEY6
          CALL      FDNM0000                      * format doctor name
          DISPLAY   *P31:15,PACFNAME              * display Doctor name
          GOTO      CONS5000
.
. nothing input for start doctor code
.
CONS2000  MOVE      SP6,STDCODE                   * start doctor code
          MOVE      "Start",STDESCOD              * print var
          DISPLAY   *P23:15,*V2LON,"Start"
.
. Keyin Ending Doctor code
.
CONS5000  MOVE      "23",ECOL                     * set up parameters
          MOVE      "16",EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
.
          CALL      PATDOCKY
          BRANCH    EXIT,CONS7000,CONS9000        * nothing, EXITCHAR input
.
          MOVE      DOCCODE,ENDCODE               * end doctor type
.
          MATCH     STDCODE,ENDCODE               * valid range?
          GOTO      CONS6000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      CONS0000
.
CONS6000  MOVE      DOCCODE,ENDESCOD              * end doctor code (print)
          CALL      FDNM0000                      * format doctor name
          DISPLAY   *P31:16,PACFNAME              * display Doctor name
          MOVE      ZERO,EXIT
          GOTO      CONS9999
.
. nothing input for end doctor code
.
CONS7000  MOVE      "zzzzzz",ENDCODE              * end doctor code
          MOVE      "Finish",ENDESCOD             * print var
          DISPLAY   *P23:16,*V2LON,"Finish"
          MOVE      ZERO,EXIT
          GOTO      CONS9999
.
CONS9000  MOVE      ONE,EXIT                      * EXITCHAR
.
CONS9999  RETURN
+
.**************************************************************************
.*  KDAT0000      Keyin a date range                                      *
.**************************************************************************
.
KDAT0000  DISPLAY   *P1:18,*EL,"          date from : ":
                    *P1:19,*EL,"               to   : "
.
. keyin from date
.
          UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "23",CCOL
          MOVE      "18",CVERT
          MOVE      ONE,CCENTRY
          MOVE      ZERO,CDEFDTE
          MOVE      "00000000",FROMDATE
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT1000                * anything input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          PACK      FDDATE,CDAY,SLASH,CMON,SLASH,CYEAR,CCENT    * display var
          REP       " 0",FDDATE 
          GOTO      KDAT5000
.
KDAT3000  DISPLAY   *P23:18,*V2LON,"Start"         * no date was input
          MOVE      "Start",FDDATE                * display var
.
. keyin to date
.
KDAT5000  UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "23",CCOL
          MOVE      "19",CVERT
          MOVE      ONE,CCENTRY
          MOVE      ZERO,CDEFDTE
          MOVE      "99999999",TODATE
.
KDAT6000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT6000
          BRANCH    OVRCD,KDAT6000                * anything input?
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          PACK      TDDATE,CDAY,SLASH,CMON,SLASH,CYEAR,CCENT    * display var
          REP       " 0",TDDATE 
          GOTO      KDAT7000
.
KDAT6500  DISPLAY   *P23:19,*V2LON,"Finish"        * no date was input
          MOVE      "Finish",TDDATE               * display var
          MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
. check if dates entered are valid (range)
.
KDAT7000  REP       " 0",FROMDATE
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT9000 IF LESS
.
          MOVE      ZERO,EXIT
          GOTO      KDAT9999
. 
KDAT9000  DISPLAY   *P1:24,*EL,*B,"Invalid date range.  ";
          CALL      HITENTER
          GOTO      KDAT0000
.
KDAT9999  RETURN
+
.*********************************************************************
.*        Create a temp file                                         *
.*********************************************************************
.
CTPD0000  CALL      DTPD0000                * delete temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMED,KEYD,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPD,CFNAMED           * open the temp file
CTPD9999  RETURN
+
.*********************************************************************
.*        Delete temp file D                                         *
.*********************************************************************
.
DTPD0000  CLOSE     TEMPD                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMED * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
DTPD9999  RETURN
+
.*********************************************************************
.         get all people in hospital for date range
.         Para's  : YEARSTOP      ending year 
.*********************************************************************
.
MKTD0000  CALL      CTPD0000                * create temp file A
          MOVE      SP8,CRDYDATE            * clear display date
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          PACK      YEARFILE,CCENT,CYEAR      * get ccyy of start date
          PACK      KEY12,CMON,CDAY,SP20
.
. ------- open the required day file -------
.
MKTD1000  PACK      CFNAME,FILDAYA1,YEARFILE
          REP       " 0",CFNAME             * starting year file name
          DISPLAY   *P1:24,"Scanning : ",*V2LON,CFNAME,*EL;
          CLOSE     PATDAYA1
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,MKTD5000          * file for year not there
.
. ------- loop over the required day file -------
.
          CALL      RSPTDAY1
MKTD2000  CALL      RKPTDAY1
          BRANCH    OVRCD,MKTD5000          * end of current file
.
          PACK      KEY8,YEARFILE,PTDYDATE
          REP       " 0",KEY8
          UNPACK    KEY8,KEY2,KEY6
.
          MATCH     FROMDATE,KEY8
          GOTO      MKTD2000 IF LESS        * file date is too low
.
          MATCH     KEY8,TODATE
          GOTO      MKTD5000 IF LESS        * file date is too high
.
          MATCH     PTDYDATE,CRDYDATE
          IF        !@EQUAL
            DISPLAY   *P25:24,PTDYDATE
          ENDIF     !@EQUAL
          MOVE      PTDYDATE,CRDYDATE       * set current date
.
          MOVE      PTDYADMN,KEY8
          READ      TEMPD,KEY8;ANS
          GOTO      MKTD2000 IF NOT OVER    * on file already
.
          WRITE     TEMPD,KEY8;KEY8
          GOTO      MKTD2000
.
.         end of current day file, check following years one if in range
.
MKTD5000  MOVE      YEARFILE,FORM4          * get current year of file
          MOVE      FORM4,FORM5             * copy to larger variable
          ADD       ONE,FORM5               * get to next year
          MOVE      FORM5,FORM4             * get to form 4
          MOVE      FORM4,YEARFILE          * set new file year name
          MOVE      SP20,KEY12              * set start key
          MOVE      SP8,CRDYDATE            * clear display date
.
          COMPARE   FORM4,YEARSTOP
          GOTO      MKTD1000 IF NOT LESS    * finished the date range
.
MKTD9999  RETURN
+
.***************************************************************************
.*  EXTR0000  :  Load data into temp file                                  *
.***************************************************************************
.
EXTR0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading data... [                     ]"
.
. --- get patient using discharge date range ---
.
          PACK      KEY16,FROMDATE,SP20
          CALL      RDSDSCH2
EXTR1000  CALL      RDKDSCH2
          BRANCH    OVRCD,EXTR9000
.
          MATCH     DDATE,TODATE                  * in range?
          GOTO      EXTR9000 IF LESS              * no
.
. ******* loop over the tran file to get FCE *******
.
          MOVE      SP6,STADOCT
          MOVE      SP3,STATYPE
          MOVE      SP3,STWARD
.
          PACK      KEY30,DADMNO,SP30
          CALL      RDSTRAN2
EXTR2000  CALL      RDKTRAN2
          BRANCH    OVRCD,EXTR1000
.
          MATCH     TADMN,DADMNO                  * still same patient?
          GOTO      EXTR1000 IF NOT EQUAL
.
          DISPLAY   *P47:24,TADMN,TDATE,PTTREPNO
.
          MATCH     ANSA,TMOVE
          GOTO      EXTR2900 IF EQUAL             * ignore admission record
.
          MATCH     ANSD,TMOVE
          GOTO      EXTR4900 IF EQUAL       * discharged so FCE
.
          MATCH     TADOCT,STADOCT
          GOTO      EXTR5000 IF NOT EQUAL   * different attending doctor
.
.         save the attending doctor to see if a FCE on next change record
.
EXTR2900  MOVE      TADOCT,STADOCT
          MOVE      TATYPE,STATYPE
          MOVE      TWARD,STWARD
          MOVE      TBED,SAVBED
          MOVE      PTTREPNO,SAVEPNO
          GOTO      EXTR2000
.
EXTR4900  MOVE      TDATE,DDATE             * set the discharge date
.
. ******* have a finished consultant episode *******
.
EXTR5000  MATCH     SKYSPEC,STATYPE                * specialty in start range?
          GOTO      EXTR2900 IF LESS
.
          MATCH     STATYPE,EKYSPEC                * specialty in end range?
          GOTO      EXTR2900 IF LESS
.
          MATCH     STDCODE,STADOCT                * consultant in start range?
          GOTO      EXTR2900 IF LESS
.
          MATCH     STADOCT,ENDCODE                * consultant in end range?
          GOTO      EXTR2900 IF LESS
.
. --- get hospital for this ward ---
.
          PACK      KEY6,STWARD,SAVBED
          CALL      RDWARD1
          BRANCH    OVRCD,EXTR2900
.
          MATCH     SP3,KYHOSP                    * All Hospitals?
          IF        !@EQUAL
            MATCH     KYHOSP,PTWDHOSN             * same hospital?
            GOTO      EXTR2900 IF NOT EQUAL
          ENDIF
.
. check if coding has been completed
.
          MOVE      SP8,PTEDDTCD                  * assume coding not completed
          MOVE      "  1",KEY3
          PACK      KEY13,TADMN,SAVEPNO,KEY3,SP20                     *C-240107
          CALL      RDPTECD1
.
. set up variables
.
          MOVE      PTWDHOSN,XXHOSP
          MOVE      STATYPE,XXSPEC
          MOVE      STADOCT,XXCONS
.
. write or update temp file
.
          PACK      KEY12,XXHOSP,XXSPEC,XXCONS
          CALL      RDTEMP1
          IF        OVRCD=0
            MATCH     SP8,PTEDDTCD                * coding completed date blank?
            IF        @EQUAL
              ADD       ONE,XXUNCEP               * uncoded episodes
            ELSE
              ADD       ONE,XXCODEP               * coded episodes
            ENDIF
            ADD       ONE,XXFINEP                 * finished episodes
            CALL      UPTEMP1
          ELSE
            MATCH     SP8,PTEDDTCD                * coding completed date blank?
            IF        @EQUAL
              MOVE      ONE,XXUNCEP               * uncoded episodes
              MOVE      ZERO,XXCODEP
            ELSE
              MOVE      ONE,XXCODEP               * coded episodes
              MOVE      ZERO,XXUNCEP
            ENDIF
            MOVE      ONE,XXFINEP                 * finished episodes
            CALL      WRTEMP1
          ENDIF
          GOTO      EXTR2900
.
EXTR9000  CALL      ANYD0000                      * any data found?
          BRANCH    EXIT,EXTR9999                 * no
          DISPLAY   *P1:24,*EL
          MOVE      ZERO,EXIT
.
EXTR9999  RETURN
+
.****************************************************************************
.* ANYD0000  :  Check if any data was found                                 *
.****************************************************************************
.
ANYD0000  PACK      KEY12,SP20
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,ANYD9000
.
          MOVE      ZERO,EXIT                     * data found
          GOTO      ANYD9999
.
ANYD9000  DISPLAY   *P1:24,*EL,*B,"No data found.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
ANYD9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
.
RPRT0000  DISPLAY   *P30:24,"Printing...";
.
          CALL      IBACLOCK                      * initialise a few things
          CLOCK     TIME,CTIMEIS                  * report start time
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      SP3,SAVHOSP
          MOVE      SP3,SAVSPEC
          MOVE      ZERO,TOTFIN                   * initialise totals
          MOVE      ZERO,TOTCOD
          MOVE      ZERO,TOTUNC
          MOVE      ZERO,GRNFIN
          MOVE      ZERO,GRNCOD
          MOVE      ZERO,GRNUNC
.
.         Get the first hospital, and print the 1st page heading
.
          MOVE      SP3,XXHOSP
          PACK      KEY12,SP20
          CALL      RSTEMP1
          CALL      RKTEMP1
.
          MOVE      XXHOSP,SAVHOSP                * 1st hospital code
          CALL      PAGE0000                      * print 1st page header
          CALL      PTHD0000                      * print 1st table headings
.         
          PACK      KEY12,SP20
          CALL      RSTEMP1
RPRT0250  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT8000                * no more records
.
. check if we need to print a new hospital
.
          MATCH     XXHOSP,SAVHOSP
          IF        !@EQUAL
            CALL      STOT0000                    * print spec totals
            MOVE      XXHOSP,SAVHOSP              * new hospital code
            CALL      PTHD0000                    * print table headings
            MOVE      SP3,SAVSPEC                 * force speciality heading
          ENDIF
.
          CALL      FDAT0000                      * format data for printing
.
.         Check for a change in speciality
.
          MATCH     XXSPEC,SAVSPEC                * same specialty?
          IF        @EQUAL
            IF        CLNO >= 55
              CALL      PAGE0000
              CALL      PTHD0000
              PRINT     *1,"|  ",XXSPEC,SP2,SPECDESC;
            ELSE
              PRINT     *1,"|";
            ENDIF
          ELSE
            MATCH     SP3,SAVSPEC                 * 1st time thru printing?
            IF        !@EQUAL
              CALL      STOT0000                  * print spec totals
              IF        CLNO >= 55
                CALL      PAGE0000
                CALL      PTHD0000
              ELSE
                CALL      UND132
              ENDIF
            ENDIF
            PRINT     *1,"|  ",XXSPEC,SP2,SPECDESC;
          ENDIF
.
          ADD       XXFINEP,TOTFIN              * increment total finish epi.
          ADD       XXCODEP,TOTCOD              * increment total coded epi.
          ADD       XXUNCEP,TOTUNC              * increment total uncoded epi.
          ADD       XXFINEP,GRNFIN              * increment GRAND finish epi.
          ADD       XXCODEP,GRNCOD              * increment GRAND coded epi.
          ADD       XXUNCEP,GRNUNC              * increment GRAND uncoded epi.
.
          PRINT     *31,"|  ",XXCONS,SP2,CONSDESC:
                    *79,"|",*84,XXFINEP,*92,"|",*97,XXCODEP,*105,"|",*110:
                    XXUNCEP,*118,"|",*123,SHRTFALL,*132,"|"
          ADD       ONE,CLNO                      * increment line counter
.
          MOVE      XXSPEC,SAVSPEC                * current speciality
          GOTO      RPRT0250
.
. we have finished printing patient data so print end of report details
.
. print sub totals for last specialty
.
RPRT8000  CALL      STOT0000
.
. print GRAND totals
.
          ASSIGN    ((GRNUNC/GRNFIN)*100),TOTSHRT     * get % total
          CALL      UND132
          PRINT     *1,"|  G R A N D    T O T A L S ",*79,"|",*82,GRNFIN:
                    *92,"|",*95,GRNCOD,*105,"|",*108:
                    GRNUNC,*118,"|",*123,TOTSHRT,*132,"|"
          CALL      UND132
          PRINT     *N,"*** End of Report ***"
.
RPRT9999  RETURN
+
.***************************************************************************
.*  STOT0000  :  Print the speciality totals                               *
.***************************************************************************
.
STOT0000  CALL      UND132
          ASSIGN    ((TOTUNC/TOTFIN)*100),TOTSHRT     * get % total
          PRINT     *79,"|",*83,TOTFIN,*92,"|",*96,TOTCOD,*105,"|",*109:
                    TOTUNC,*118,"|",*123,TOTSHRT,*132,"|",*N:
                    *79,TOTUNDLN,*N
          ADD       THREE,CLNO
.
          MOVE      ZERO,TOTFIN
          MOVE      ZERO,TOTCOD
          MOVE      ZERO,TOTUNC
STOT9999  RETURN
+
.***************************************************************************
.*  PAGE0000  :  Set up new page                                           *
.***************************************************************************
.
PAGE0000  CALL      HEAD132
.
          PRINT        *40,"Hospital   : ",KYHOSP,SP2,HOSPNAME:
                    *N,*40,"Specialty  : ",STDESTYP,"   - ",ENDESTYP:
                    *N,*40,"Consultant : ",STDESCOD,"   - ",ENDESCOD:
                    *N,*40,"Date Range : ",FDDATE," - ",TDDATE,*N
.
          ADD       FIVE,CLNO
.
PAGE9999  RETURN
+
.***************************************************************************
.*  FDNM0000  :  Format name for printing                                  *
.***************************************************************************
.
FDNM0000  MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
FDNM9999  RETURN
+
.***************************************************************************
.*  FDAT0000  :  Format data for printing                                  *
.***************************************************************************
.
FDAT0000  MOVE      "Unknown code",TDESC
          PACK      KEY5,ANSA,SP1,XXSPEC
          CALL      RDCODE1
          MOVE      TDESC,SPECDESC
.
          MOVE      "Unknown code",CONSDESC
          PACK      KEY6,XXCONS
          CALL      RDDOCT1
          IF        OVRCD=0
            CALL      FDNM0000                    * format name
            MOVE      PACFNAME,CONSDESC
          ENDIF
.
          ASSIGN    ((XXUNCEP/XXFINEP)*100),SHRTFALL       * get short fall (%)
.
FDAT9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
.
PTHD0000  COMPARE   "50",CLNO                     * enough room on this page?
          CALL      PAGE0000 IF NOT LESS          * no, print new heading
.
          MOVE      "Unknown",PTHSNAME            * print hospital on heading
          MOVE      SAVHOSP,KEY3
          CALL      RDPTHSP1
          PRINT     *1,"Hospital : ",SAVHOSP,SP2,PTHSNAME:
                    *1,"Hospital : ",SAVHOSP,SP2,PTHSNAME
.
          CALL      UND132
          PRINT     *1,"|",*31,"|",*79,"|  Finished  |   Coded",*105,"|  ":
                    "Uncoded   |   Short",*132,"|",*N:
                    *1,"|  Specialty",*31,"|  Consultant",*79,"|  Episodes":
                    *92,"|  Episodes  |  Episodes  |   Fall (%)",*132,"|"
          CALL      UND132
.
          ADD       THREE,CLNO
.
PTHD9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
.
KILL0000  CLOSE     HMSTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
.
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 22 U1-3,4-6,7-12",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      HMSTMPA1,FNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
.
RSTEMP1   RESET     KEY12
          READ      HMSTMPA1,KEY12;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      HMSTMPA1,KEY12;XXHOSP,XXSPEC,XXCONS,XXFINEP,XXCODEP,XXUNCEP
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    HMSTMPA1;XXHOSP,XXSPEC,XXCONS,XXFINEP,XXCODEP,XXUNCEP
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    HMSTMPA1;XXHOSP,XXSPEC,XXCONS,XXFINEP,XXCODEP,XXUNCEP
          RETURN
.
WRTEMP1   RESET     KEY12
          WRITE     HMSTMPA1,KEY12;XXHOSP,XXSPEC,XXCONS,XXFINEP,XXCODEP,XXUNCEP
          RETURN
.
.*****************************************************************************
.*        I/O Includes                                                       *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       PATCODKY
          INC       PATDOCKY
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATDAYIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATHSPIO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

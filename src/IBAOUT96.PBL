. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAOUT96                                            *
. *                                                                           *
. *     FUNCTION:         AUDIT REPORT                                        *
. *                                                                           *
. *     DATE WRITTEN:     21/06/88                                            *
. *                                                                           *
. *     AUTHOR:           MALCOLM HAYSE                                       *
. *                                                                           *
. * Modifications:                                                            *
. *                                                                           *
. * V10.13.01 05/12/2018  Don Nguyen        TSK 0838558                       *
. *           Deleted temp file (OUTAUDXX) on program exit                    *
. * V10.12.01 20/03/2018  Richa Phogat      TSK 0261630                       *
. *           Removed multiple instances of PORT                              *
. *****************************************************************************
. * V9.03.01  11/09/2003  Sandra Barcham     43010                            *
. *           Removed site prefix from open of outpreaf & outphoaf            *
. *****************************************************************************
. * V9.02.01  03/02/2003  Tracey Nguyen  SRF 22709                            *
. *           Changed open OUTAUBB1 and OUTAUDBA to use site prefixes         *
. * V9.02.00  Ported from r5.10                                               *
. *****************************************************************************
. * V5.04.01  21/04/1997  howellsy   RHA                                      *
. *           Added OTBBRANK,OBOUSR4-8                                        *
. *****************************************************************************
. * V5.03.02  05/12/1995    Justin Coates  (SUHT mods)                        *
. *           changed obarsflg to obapxray                                    *
. *****************************************************************************
. *     MODIFICATIONS:    V5.01 15/05/89 J.Tan   (I.B.A)                      *
. *                             Add option to clear audits                    *
. *                    V5.01.01 16/07/89 K.Peak  (I.B.A)                      *
. *                             Changed Adm & U/R to 8 chrs                   *
. *                    V5.01.02 03/10/89  L.P.   (I.B.A)                      *
. *                             Recompiled with new                           *
. *                             OUTAUXFD.INC  SRF102514                       *
. *                    V5.01.03 04/10/89  K.Peak (I.B.A)                      *
. *                             Replaced capital to lower                     *
. *                             SRF 102601                                    *
. *                    V5.01.04 06/10/89  K.Peak (I.B.A)                      *
. *                             Added search_dpath for                        *
. *                             alpha ports  SRF 102601                       *
. *                    V5.01.05 26/10/89  Steve O'Gorman                      *
. *                             Reset FILENAM after extract                   *
. *                             of port number  SRF 102801                    *
. *                    V5.01.15 28/06/90  Paul Duncan                         *
. *                             Allowed for printing of                       *
. *                             audits a and b in new                         *
. *                             format.         SRF105033                     *
. *                    V5.01.16 10/07/90 Paul Duncan                          *
. *                             fixed I*M when All chosen                     *
. *                             asks "do you want to clear"                   *
. *                             every time now - not just                     *
. *                             first                                         *
. *                    V5.01.17 12/07/90 Paul Duncan                          *
. *                             recomp for OUTGENSS audits                    *
. *        V5.01.18  24.02.92 Neeriem "I Hate Support" Dye                    *
. *                  Reformat print to look beautiful for                     *
. *                  new fields (outbb1af)                                    *
. *        V5.01.19  08/03/93 Whirlpool                                       *
. *                  added new outosfaf file and                              *
. *                  recompiled for OUTGENSS                                  *
. *        V5.01.20  23.03.92 Neeriem "I Hate Support" Dye                    *
. *                  Fix print                                                *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       OUTAUXFD/INC
          INC       OUTCONFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTOSFFD/INC
          INC       OUTSITFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.             WORK AREA
.
AUDFLG    FORM      1
CLEARFLG  FORM      1
CNOPORT   FORM      2
CMDLINE   DIM       50
.
DIM3      DIM       3
DIM13     DIM       13
DIM15     DIM       15
DIM27     DIM       27
.
DANSX     DIM       13
.
ENDOPT    FORM      1
.
IBCNMHOS  FORM      1
.
ONAME     DIM       30
OPERCODE  DIM       4
.
OAUDA     INIT      "outaua"
OAUDB     INIT      "outaub"
OAUDC     INIT      "outauc"
OAUDD     INIT      "outaud"
OAUDE     INIT      "outaue"
OAUDF     INIT      "outauf"
OAUDG     INIT      "outaug"
OAUDH     INIT      "outauh"
OAUDI     INIT      "outaui"
OAUDJ     INIT      "outauj"
OAUDK     INIT      "outauk"
OAUDL     INIT      "outaul"
OAUDM     INIT      "outaum"
OAUDN     INIT      "outaun"
OAUDO     INIT      "outauo"
OAUDP     INIT      "outaup"
OAUDQ     INIT      "outauq"
OAUDR     INIT      "outaur"
OAUDS     INIT      "outaus"
OAUDT     INIT      "outaut"
OAUDU     INIT      "outauu"
OAUDV     INIT      "outauv"
OAUDW     INIT      "outauw"
OAUDX     INIT      "outaux"
OAUDY     INIT      "outauy"
OAUDZ     INIT      "outauz"
.
PDESC     DIM       19
PDESCA    INIT      "O/P Bookings       "
PDESCB    INIT      "O/P Booking Details"
PDESCC    INIT      "Audit C (Not used) "
PDESCD    INIT      "Audit D (Not Used) "
PDESCE    INIT      "Audit E (Not used) "
PDESCF    INIT      "Audit F (Not used) "
PDESCG    INIT      "Audit G (Not used) "
PDESCH    INIT      "Audit H (Not used) "
PDESCI    INIT      "O/P Clinic ID's    "
PDESCJ    INIT      "Audit J (Not used) "
PDESCK    INIT      "Audit K (Not used) "
PDESCL    INIT      "Audit L (Not used) "
PDESCM    INIT      "O/P Master         "
PDESCN    INIT      "O/P Non-attendees  "
PDESCO    INIT      "Audit O (Not used) "
PDESCP    INIT      "O/P Public Holiday "
PDESCQ    INIT      "Audit Q (Not used) "
PDESCR    INIT      "Audit R (Not Used) "
PDESCS    INIT      "Audit S (Not used) "
PDESCT    INIT      "O/P Clinic Types   "
PDESCU    INIT      "Audit U (Not used) "
PDESCV    INIT      "Audit V (Not used) "
PDESCW    INIT      "Audit W (Not Used) "
PDESCX    INIT      "Audit X (Not used) "
PDESCY    INIT      "Audit Y (Not used) "
PDESCZ    INIT      "Audit Z (Not used) "
PRDATE    DIM       8
.
SEC       FORM      1
SEC1      FORM      "00001"
.
TOTADD    FORM      4
TOTCHA    FORM      4
TOTDEL    FORM      4
.
INDEX     FORM      2
INPFILE   FILE      ASCII, FIXED=256
.
FIELD     FORM      2
FILES     FILE      ASCII, FIXED=256
FILETYP   DIM       6
FNAMEP    DIM       8
FILENAM   DIM       40
.
PRGID     INIT      "IBAOUT96"
PRGNAM    INIT      "Audit Reports"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN;*138,CNOPORT
.
          MOVE      UID,OSTFILE
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEP   
          RESET     FNAMEP
.
.         MAIN      MENU
.
START     DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print Audit Reports"
.
BEGIN     KEYIN     *P1:7,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF START0
.
          BEEP
          GOTO      BEGIN
.
START0    
          READ      CONTROLF,THIRTY1;*51,HOAUDA,HOAUDB,HOAUDC,HOAUDD,HOAUDE:
                                         HOAUDF,HOAUDG,HOAUDH,HOAUDI,HOAUDJ:
                                         HOAUDK,HOAUDL,HOAUDM,HOAUDN,HOAUDO:
                                         HOAUDP,HOAUDQ,HOAUDR,HOAUDS,HOAUDT:
                                         HOAUDU,HOAUDV,HOAUDW,HOAUDX,HOAUDY:
                                         HOAUDZ
.
          DISPLAY   *P1:3,*EF:
                    *P1:4," 1. ",PDESCA," :":
                    *P1:5," 2. ",PDESCB," :":
                    *P1:6," 3. ",PDESCC," :":
                    *P1:7," 4. ",PDESCD," :":
                    *P1:8," 5. ",PDESCE," :":
                    *P1:9," 6. ",PDESCF," :":
                    *P1:10," 7. ",PDESCG," :":
                    *P1:11," 8. ",PDESCH," :":
                    *P1:12," 9. ",PDESCI," :":
                    *P1:13,"10. ",PDESCJ," :":
                    *P1:14,"11. ",PDESCK," :":
                    *P1:15,"12. ",PDESCL," :":
                    *P1:16,"13. ",PDESCM," :":
                    *P41:4,"14. ",PDESCN,":":
                    *P41:5,"15. ",PDESCO,":":
                    *P41:6,"16. ",PDESCP,":":
                    *P41:7,"17. ",PDESCQ,":":
                    *P41:8,"18. ",PDESCR,":":
                    *P41:9,"19. ",PDESCS,":":
                    *P41:10,"20. ",PDESCT,":":
                    *P41:11,"21. ",PDESCU,":":
                    *P41:12,"22. ",PDESCV,":":
                    *P41:13,"23. ",PDESCW,":":
                    *P41:14,"24. ",PDESCX,":":
                    *P41:15,"25. ",PDESCY,":":
                    *P41:16,"26. ",PDESCZ,":"
.
          MOVE      ZERO,INDEX
          MOVE      THREE,CVERT
          MOVE      "27",CCOL
          MOVE      ZERO,CLEARFLG
.
AUDLOOP1  ADD       ONE,INDEX
          ADD       ONE,CVERT
          COMPARE   "27",INDEX
          GOTO      START1 IF EQUAL
.
          COMPARE   "17",CVERT
          GOTO      AUDPRNT1 IF LESS
.
          MOVE      FOUR,CVERT
          MOVE      "66",CCOL
.
AUDPRNT1  LOAD      FIELD USING INDEX OF HOAUDA,HOAUDB,HOAUDC,HOAUDD,HOAUDE:
                                         HOAUDF,HOAUDG,HOAUDH,HOAUDI,HOAUDJ:
                                         HOAUDK,HOAUDL,HOAUDM,HOAUDN,HOAUDO:
                                         HOAUDP,HOAUDQ,HOAUDR,HOAUDS,HOAUDT:
                                         HOAUDU,HOAUDV,HOAUDW,HOAUDX,HOAUDY:
                                         HOAUDZ
          MOVE      DYES,DANSX
          LOAD      DANSX USING FIELD OF DNO
          DISPLAY   *PCCOL:CVERT,*V2LON,DANSX;
.
          LOAD      FILETYP USING INDEX OF OAUDA,OAUDB,OAUDC,OAUDD,OAUDE:
                                           OAUDF,OAUDG,OAUDH,OAUDI,OAUDJ:
                                           OAUDK,OAUDL,OAUDM,OAUDN,OAUDO:
                                           OAUDP,OAUDQ,OAUDR,OAUDS,OAUDT:
                                           OAUDU,OAUDV,OAUDW,OAUDX,OAUDY:
                                           OAUDZ
          GOTO      AUDLOOP1
.
START1    
START10   KEYIN     *P1:24,*EL," Select Audit Report (":
                    *V2LON,"1-26",*HOFF,") or (":
                    *V2LON,*DV,ANSA,*HOFF,")ll ? ",*V2LON,DIM2;
.
          RESET     DIM2
          GOTO      START IF EOS
.
          TYPE      DIM2
          GOTO      SINGAUDT IF EQUAL
.
          CMATCH    ANSA,DIM2
          GOTO      ALLAUDIT IF EQUAL
.
INVFLD    BEEP
          GOTO      START10
.
INVAVL    DISPLAY   *P50:24,*B,*V2LON,"*** Not Available ***",*W2;
          GOTO      START1
.
NODATA    DISPLAY   *P50:24,*B,*V2LON,"*** Empty Audit ***",*W2;
          GOTO      START1
.
SINGAUDT  MOVE      ONE,OPTION        **** DUMMY SET
          MOVE      ZERO,CPAGENO
          DISPLAY   *P1:24,*EL,*P1:23,"Audit : ",*P20:23,"Terminal :";
          MOVE      DIM2,INDEX
          COMPARE   ZERO,INDEX
          GOTO      INVFLD IF EQUAL
          GOTO      INVFLD IF LESS
.
          COMPARE   INDEX,TWENTY6
          GOTO      INVFLD IF LESS
.
          CALL      GETAUD
          BRANCH    AUDFLG OF INVAVL
.
          BRANCH    INDEX,NEWAUDIT,NEWAUDIT
          GOTO      SEARCH
.
NEWAUDIT  CALL      AUDT0000                 * print new audit format(a or b)
          GOTO      START
.
SEARCH    CLEAR     CMDLINE
          APPEND    "search_dpath '",CMDLINE
          APPEND    FILETYP,CMDLINE
          APPEND    "*.txt' > ",CMDLINE
          APPEND    FNAMEP,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          GOTO      TERMINAL
.
ALLAUDIT  MOVE      TWO,OPTION
          DISPLAY   *P1:24,*EL,*P1:23,"Audit : ",*P20:23,"Terminal :";
.
          MOVE      ZERO,INDEX
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,CLEARFLG
.
NEXTALL   ADD       ONE,INDEX
          COMPARE   "27",INDEX
          GOTO      ENDAUDIT IF EQUAL
.
          CALL      GETAUD
          BRANCH    AUDFLG OF NEXTALL
.
          BRANCH    INDEX,NEWAUD,NEWAUD
          GOTO      OLDAUD
.
NEWAUD    CALL      AUDT0000              * print new type of audits
          GOTO      NEXTALL
.
OLDAUD    CLEAR     CMDLINE
          APPEND    "search_dpath '",CMDLINE
          APPEND    FILETYP,CMDLINE
          APPEND    "*.txt' > ",CMDLINE
          APPEND    FNAMEP,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
TERMINAL  MOVE      SP1,AUMODE
          DISPLAY   *P10:23,SP10,*P10:23,*V2LON,AUTYPE;
          MOVE      ZERO,FORM2
          MOVE      "60",CLNO    *** NEW PAGE FOR EACH AUDIT TYPE
.
          OPEN      FILES,FNAMEP
.
NEXTTERM  READ      FILES,SEQ;FILENAM
          GOTO      ENDTERM IF OVER
          DISPLAY   *P35:23,*EL,FILENAM;
          TRAP      REPAT1 IF IO
          OPEN      INPFILE,FILENAM
          TRAPCLR   IO
.
          ENDSET    FILENAM
.
NXTT1     CMATCH    ".",FILENAM
          GOTO      NXTT2 IF EQUAL
          BUMP      FILENAM BY -1
          GOTO      NXTT1 IF NOT EOS
.
NXTT2     BUMP      FILENAM BY -2
          MOVE      FILENAM,DIM2
          RESET     FILENAM
.
          MOVE      DIM2,AUPORT
          CALL      PRNTAUDX
          BRANCH    OVRCD OF NEXTTERM,NEXTTERM
.
          DISPLAY   *P35:23,*EL,*V2LON,FILENAM;
.
          LOAD      PDESC USING INDEX FROM PDESCA,PDESCB,PDESCC,PDESCD,PDESCE:
                                           PDESCF,PDESCG,PDESCH,PDESCI,PDESCJ:
                                           PDESCK,PDESCL,PDESCM,PDESCN,PDESCO:
                                           PDESCP,PDESCQ,PDESCR,PDESCS,PDESCT:
                                           PDESCU,PDESCV,PDESCW,PDESCX,PDESCY:
                                           PDESCZ
.
          CALL      PRNTDA      **** PRINTS ALL OUT DATA FOR THIS TERMINAL
          CLOSE     OUTAUDXX
          GOTO      NEXTTERM
.
REPAT1    NORETURN
          GOTO      NEXTTERM
.
ENDTERM   BRANCH    OPTION OF ENDAUDIT,NEXTALL
.
.      WE HAVE AN AUDIT AND IT IS OPEN
.
PRNTDA    MOVE      ZERO,AUNEXT
.
NEXTA     ADD       ONE,AUNEXT
.
          COMPARE   AUNEXT,AUORIG
          RETURN    IF EQUAL
.
          CALL      READAUDX
.
          CALL      DISPDA
          GOTO      NEXTA
.
.           PRINTING HAS FINISHED
.
ENDAUDIT  COMPARE   ZERO,CPAGENO
          GOTO      FREEAUD IF EQUAL
.
          BRANCH    CLEARFLG,FREEAUD,ZEROAUD * already asked this queasion
.
          KEYIN     *P1:24,*EL,"Do you want the audit files cleared (":
                           *V2LON,"Y",*HOFF,*DV,SLASH:
                           *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          MATCH     "Y",ANS
          GOTO      ZEROAUD IF EQUAL
.
          MATCH     "N",ANS
          GOTO      FREEAUD IF EQUAL
.
          BEEP
          GOTO      ENDAUDIT
.
ZEROAUD   MOVE      ONE,ENDOPT
          GOTO      ENDFILES
.
FREEAUD   MOVE      TWO,ENDOPT
.
.         Loop over the files, and either delete them, or free them
.
ENDFILES  DISPLAY   *P1:23,*EL,*P1:24,*EL,"Input File :";
.
          BRANCH    OPTION OF SINGFREE,ALLFREE
.
SINGFREE  MOVE      DIM2,INDEX
          CALL      GETAUD
          BRANCH    AUDFLG OF INVAVL
          GOTO      FREEFILE
.
ALLFREE   MOVE      TWO,INDEX
.
NXTFILE   ADD       ONE,INDEX
          COMPARE   "27",INDEX
          GOTO      ENDP IF EQUAL
.
          CALL      GETAUD
          BRANCH    AUDFLG OF NXTFILE
.
.         Loop through each port
.
FREEFILE  MOVE      SP1,AUMODE
          MOVE      ZERO,FORM2
.
          OPEN      FILES,FNAMEP
NXTPORT   READ      FILES,SEQ;FILENAM
          GOTO      CHKOPTS IF OVER
          TRAP      REPAT2 IF IO
          OPEN      INPFILE,FILENAM
          TRAPCLR   IO
.
          ENDSET    FILENAM
NXTT3     CMATCH    ".",FILENAM
          GOTO      NXTT4 IF EQUAL
          BUMP      FILENAM BY -1
          GOTO      NXTT3 IF NOT EOS
NXTT4     BUMP      FILENAM BY -2
          MOVE      FILENAM,DIM2
.
          MOVE      DIM2,AUPORT
          PACK      AUFILE WITH AUOUTAU,AUTYPE,AUPORT
          REP       " 0",AUFILE
.
          DISPLAY   *P14:24,*V2LON,AUFILE
.
          MOVE      ZERO,OVRCD
          TRAP      ENDTRAP IF IO
          OPEN      OUTAUDXX,AUFILE
          TRAPCLR   IO
.
          READ      OUTAUDXX,ZERO;AUNEXT,AUFLAG
          COMPARE   TWO,AUFLAG
          GOTO      NXTPORT IF NOT EQUAL
.
          BRANCH    ENDOPT OF AUDDEL,AUDFREE
.
.         Remove the audit file
.
AUDDEL    
          CLOSE     OUTAUDXX
          PREP      OUTAUDXX,AUFILE
          CLOSE     OUTAUDXX,DELETE
          DISPLAY   *P30:24,"** Cleared **";
          GOTO      NXTPORT
.
.         Free the audit file
.
AUDFREE   MOVE      ZERO,AUFLAG
          WRITE     OUTAUDXX,ZERO;AUNEXT,AUFLAG
          CLOSE     OUTAUDXX
          GOTO      NXTPORT
.
REPAT2    NORETURN
          GOTO      NXTPORT
.
CHKOPTS   BRANCH    OPTION OF ENDP,NXTFILE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NODATA IF EQUAL
.
          PRINT     *N,"  ***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"** Generation Completed **",*W2;
          GOTO      START
.
ENDTRAP   NORETURN
          GOTO      NXTPORT
.
.         Get the relevant audit file
.
GETAUD    MOVE      ZERO,AUDFLG
          LOAD      FIELD USING INDEX OF HOAUDA,HOAUDB,HOAUDC,HOAUDD,HOAUDE:
                                         HOAUDF,HOAUDG,HOAUDH,HOAUDI,HOAUDJ:
                                         HOAUDK,HOAUDL,HOAUDM,HOAUDN,HOAUDO:
                                         HOAUDP,HOAUDQ,HOAUDR,HOAUDS,HOAUDT:
                                         HOAUDU,HOAUDV,HOAUDW,HOAUDX,HOAUDY:
                                         HOAUDZ
.
          LOAD      FILETYP USING INDEX OF OAUDA,OAUDB,OAUDC,OAUDD,OAUDE:
                                           OAUDF,OAUDG,OAUDH,OAUDI,OAUDJ:
                                           OAUDK,OAUDL,OAUDM,OAUDN,OAUDO:
                                           OAUDP,OAUDQ,OAUDR,OAUDS,OAUDT:
                                           OAUDU,OAUDV,OAUDW,OAUDX,OAUDY:
                                           OAUDZ
.      1 = NO
.
          BRANCH    FIELD OF NOAUDIT
.
          LOAD      AUTYPE USING INDEX OF ANSA,ANSB,ANSC,ANSD,ANSE:
                                          ANSF,ANSG,ANSH,ANSI,ANSJ:
                                          ANSK,ANSL,ANSM,ANSN,ANSO:
                                          ANSP,ANSQ,ANSR,ANSS,ANST:
                                          ANSU,ANSV,ANSW,ANSX,ANSY,ANSZ
.
          REP     "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz",AUTYPE
.
          RETURN
.
NOAUDIT   MOVE      ONE,AUDFLG
          RETURN
.
.
.     PRINT ACTUAL DATA
.
DISPDA    COMPARE   FIFTY5,CLNO
          CALL      HEAD IF NOT LESS
.
          CMATCH    ANSI,AUMODE
          GOTO      DISPDA1 IF EQUAL
.
          CMATCH    ANSC,AUMODE
          GOTO      DISPDA2 IF EQUAL
.
          CMATCH    ANSD,AUMODE
          GOTO      DISPDA3 IF EQUAL
.
          PRINT     *1,AUDATE,*10,AUTIME,*19,AUPASSCD,*24,SP1,AUPORT:
                    *29,AUITEM,*51,AUDESC,*72,"Unknown",*82,AUCONBEF
          ADD       ONE,CLNO
          RETURN
.
DISPDA1   PRINT     *1,AUDATE,*10,AUTIME,*19,AUPASSCD,*24,SP1,AUPORT:
                    *29,AUITEM,*51,AUDESC,*72,"Inserted",*82,AUCONBEF
          ADD       ONE,CLNO
          RETURN
.
DISPDA2   PRINT     *1,AUDATE,*10,AUTIME,*19,AUPASSCD,*24,SP1,AUPORT:
                    *29,AUITEM,*51,AUDESC,*72,"Chgd From",*82,AUCONBEF:
                    *N,*72,"Chgd To",*82,AUCONAFT
          ADD       TWO,CLNO
          RETURN
.
DISPDA3   PRINT     *1,AUDATE,*10,AUTIME,*19,AUPASSCD,*24,SP1,AUPORT:
                    *29,AUITEM,*51,AUDESC,*72,"Deleted",*82,AUCONBEF
          ADD       ONE,CLNO
          RETURN
.
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
.
          CLOCK     TIME,CTIMEIS
          PRINT     *F,"IBAOUT96",*35,CNAME,*118,"DATE : ",CDATE:
                    *N,VERSION,*35,"AUDIT REPORT FOR - ",PDESC:
                    *118,"TIME : ",CTIMEIS:
                    *N,*118,"PAGE : ",SP5,CPAGENO:
                    *N,"  Date",*10,"  Time":
                    *19,"User",*24,"Port":
                    *29,"Record",*51,"Field":
                    *72,"Operation",*82,"Data":
                    *N,"--------",*10,"--------":
                    *19,"----",*24,"----":
                    *29,"--------------------":
                    *51,"--------------------":
                    *72,"---------":
                    *82,"--------------------------------------------------"
.
          MOVE      FIVE,CLNO
          RETURN
.
PAGEND    PRINT     *1,"-----------------------------------":
                    "---------------------------------------------":
                    "-------------------------------"
          ADD       ONE,CLNO
          RETURN
.
.************************************************************************
.*                         AUDT0000                                     *
.*   Print the new audits - a = bookings, b = booking details           *
.*                   called by : NEWAUDIT                               *
.************************************************************************
AUDT0000  BRANCH    CLEARFLG,AUDT0500,AUDT0500 * first time thru - flg will = 0
          KEYIN     *P1:24,*EL,"Do you want the audit files cleared (":
                           *V2LON,"Y",*HOFF,*DV,SLASH:
                           *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          MOVE      ONE,CNOUNDLN
          MOVE      ZERO,CLEARFLG
          MOVE      ZERO,CPAGENO
          REP       "N1Y2",ANS              * no = 1 , yes = 2
          MOVE      ANS,CLEARFLG
          COMPARE   ZERO,CLEARFLG           * invalid entry
          GOTO      AUDT0000 IF EQUAL
.
AUDT0500  BRANCH    INDEX,AUDT5000          * go down for audit a
.
          CLOCK     TIME,CTIMEIS            * set up head132 variables
          MOVE      "AUDIT REPORT FOR ",PRGNAM
          MOVE      " O/P booking details",CPHDROPT
          CALL      HEAD0000
          DISPLAY   *P10:23,SP10,*P10:23,*V2LON,AUTYPE;
.
          PACK      CFNAME,UID,FILAUBB1 
          OPEN      OUTAUBB1,CFNAME
          MOVE      SP20,KEY19
          CALL      ASOTBOB                 * position at start of file
.
AUDT1000  CALL      AKOTBOB                 * read next record
          BRANCH    OVRCD,AUDT9000
.
          COMPARE   "60",CLNO               * time for a new page
          CALL      HEAD0000 IF NOT LESS
. 
          MATCH     IDDD,OTBBSITE           * right site?
          GOTO      AUDT1000 IF NOT EQUAL   * no go back
.
          CALL      PRNB0000                * print one record
          MOVE      TWO,OTBBAUDS            * now has been printed
          CALL      AUOTBOB                 * update file
          BRANCH    CLEARFLG,AUDT1000       * don't clear
          PACK      KEY19,OTBBAUDD,OTBBAUDT,OTBBAUDP,OTBBAUDR
          CALL      ADOTBOB
.
          GOTO      AUDT1000
.
AUDT5000  PACK      CFNAME,UID,FILAUDBA 
          RESET     CFNAME
          OPEN      OUTAUDBA,CFNAME
          CLOCK     TIME,CTIMEIS            * set up head132 variables
          MOVE      "AUDIT REPORT FOR ",PRGNAM
          MOVE      " - O/P bookings",CPHDROPT
          CALL      HEAD0000
          DISPLAY   *P10:23,SP10,*P10:23,*V2LON,AUTYPE;
          MOVE      SP20,KEY19
          CALL      ASOTBOA                 * position at start of file
.
AUDT6000  CALL      AKOTBOA                 * read next record
          BRANCH    OVRCD,AUDT9000
.
          COMPARE   "60",CLNO               * time for a new page
          CALL      HEAD0000 IF NOT LESS
. 
          MATCH     IDDD,OBASITE            * right site?
          GOTO      AUDT6000 IF NOT EQUAL   * no go back
.
          CALL      PRNA0000                * print one record
          MOVE      TWO,OTBAAUDS            * now has been printed
          CALL      AUOTBOA                 * update file
          BRANCH    CLEARFLG,AUDT6000       * don't clear
          PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,OTBAAUDR
          CALL      ADOTBOA
.
.
          GOTO      AUDT6000
.
AUDT9000  CALL      END0000                 * print totals and "end of report" 
.
AUDT9999  RETURN
.
.************************************************************************
.*                           HEAD0000                                   *
.*                        Print heading                                 *
.*                   called by : AUDT1000,AUDT6000                      *
.************************************************************************
HEAD0000  CALL      HEAD132
          MOVE      ZERO,CNOUNDLN
.
          CALL      UND132
          PRINT     "|",*5,"Date",*12,"|",*16,"Time":
                    *23,"|","Port","|","Type","|"," User ","|":
                    "Audit Data",*132,"|"
          CALL      UND132
          ADD       THREE,CLNO    * update line number
.
HEAD9999  RETURN
.************************************************************************
.*                           PRNB0000                                   *
.*               Print one audit for the bookings audit (outbokaf)      *
.*                   called by : AUDT0000.AUDT1000,AUDT6000             *
.************************************************************************
PRNB0000  UNPACK    OTBBAUDD,CCENT,CYEAR,CMON,CDAY
          PACK      PRDATE,CDAY,SLASH,CMON,SLASH,CYEAR
.
          PRINT     "|",*3,PRDATE,*12,"| ",OTBBAUDT:
                    *23,"|",*25,OTBBAUDP,*28,"|",*31,OTBBAUDR,*33,"|":
                    *35,OTBBAUDO,*40,"|",*42,OBAOUTNO,"~",OBASRCR,"~":
                    OBACOMP,"~",OBACLASS,"~",OBAINS,"~",OBAATTN,"~":
                    OBADOCT,"~",OBACOMM,"~",OTBBFUND,"~",OTBBTBLE,"~":
                    OTBBPVIS,"~",*132,"|",*N,"|",*12,"|",*23,"|",*28:
                    "|",*33,"|":
                    *40,"|",*42,OBADATE,"~",OBACAT,"~",OBAPRTY,"~":
                    OBARSCHD,"~",OBBPORT,"~",OBBBOOK,"~",OBBCTYP,"~":
                    OBOUSR1,"~",OBOUSR2,"~",OBOUSR3,"~",OBOUSR4,"~":
                    OBALADMN,"~",OTBBDEPT,"~":
                    OTBBTRAN,"~",OTBBLNGI,"~":
                    OTBBTMFS,"~",OTBBTMBO,*132,"|":
                    *N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    OTBBGPFA,"~",OTBBECRA,"~",OTBBRFGP,"~",OTBBGPPC,"~":
                    OTBBADCS,"~",OTBBCITM,"~",OTBBASTM,"~",OTBBDPTM,"~":
                    OTBBOUTC,*132,"|":
                    *N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    OBOUSR5,"~",OBOUSR6,"~",OBOUSR7,"~",OBOUSR8,"~":
                    OTBBRANK,*132,"|"
          ADD       FOUR,CLNO
.
          MOVE      ZERO,FORM1
          MOVE      OTBBAUDR,ANS
          REP       "A1B2C3D4",ANS
          MOVE      ANS,FORM1
          BRANCH    FORM1,PRNB1000,PRNB2000,PRNB9999,PRNB3000
          GOTO      PRNB9999
.
PRNB1000  ADD       ONE,TOTADD              * it is an add audit
          GOTO      PRNA9999 
.
PRNB2000  ADD       ONE,TOTCHA              * it is a change audit
          GOTO      PRNA9999 
.
PRNB3000  ADD       ONE,TOTDEL              * it is a delete audit
.
PRNB9999  RETURN
.************************************************************************
.*                           PRNA0000                                   *
.*         Print one audit record for the booking details (outbokb)     *
.*                   called by : AUDT0000.AUDT1000,AUDT6000             *
.************************************************************************
PRNA0000  UNPACK    OTBAAUDD,CCENT,CYEAR,CMON,CDAY
          PACK      PRDATE,CDAY,SLASH,CMON,SLASH,CYEAR
          PRINT     "|",*3,PRDATE,*12,"|",OTBAAUDT:
                    *23,"|",*25,OTBAAUDP,*28,"|",*31,OTBAAUDR,*33,"|":
                    *35,OTBAAUDO,*40,"|",*42,OBASITE,"~",OBACLIN,"~":
                    OBADATE,"~",OBASTRT,"~",OBASLOT,"~",OBATIME,"~":
                    OBACTYP,"~",OBAURNO,"~",OBAVISIT,"~",OBAOUTNO,"~":
                    OBASTAT,"~",OBADAY,"~",OBAEXSL,"~",OBAFINST,"~":
                    OBALOCK,"~",OBAPXRAY,"~",*132,"|",*N,"|",*12,"|":
                    *23,"|",*28,"|",*33,"|",*40,"| ",OBABKDTE,"~",OBAPULL,"~":
                    OBASESST,"~",*132,"|"
          ADD       TWO,CLNO
.
          MOVE      ZERO,FORM1
          MOVE      OTBAAUDR,ANS
          REP       "A1B2C3D4",ANS
          MOVE      ANS,FORM1
          BRANCH    FORM1,PRNA1000,PRNA2000,PRNA9999,PRNA3000
          GOTO      PRNB9999
.
PRNA1000  ADD       ONE,TOTADD              * it is an add audit
          GOTO      PRNB9999 
.
PRNA2000  ADD       ONE,TOTCHA              * it is a change audit
          GOTO      PRNB9999 
.
PRNA3000  ADD       ONE,TOTDEL              * it is a delete audit
.
PRNA9999  RETURN
.************************************************************************
.*                           END0000                                    *
.*     Print the totals and the end of report message                   *
.*                   called by : AUDT9000                               *
.************************************************************************
END0000   CALL      UND132
          PRINT     *N,"Total Additions Printed : ",TOTADD:
                    *N,"Total Changes Printed   : ",TOTCHA:
                    *N,"Total Deletions Printed : ",TOTDEL
          CALL      CLRT0000           * clear the totals
          COMPARE   TWO,OPTION         * printing all?
          GOTO      END9999 IF EQUAL
          PRINT     *N,"*** End of Report ***"
.
END9999   RETURN                   
.************************************************************************
.*                           CLRT0000                                   *
.*              Clear totals                                            *
.*                   called by : START1                                 *
.************************************************************************
CLRT0000  MOVE     ZERO,TOTADD
          MOVE     ZERO,TOTDEL
          MOVE     ZERO,TOTCHA
.
CLRT9999  RETURN
.************************************************************************
.*                           I/O ROUTINES                               *
.************************************************************************
          INC       OUTAUXIO/INC
          INCLUDE   STD001IO
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTOSFIO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
.
ENDIT     PREP      OUTAUDXX,AUFILE
          CLOSE     OUTAUDXX,DELETE
          CHAIN     PGM
          STOP

.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAINP78                                                 *
.* Description    :  Patient Theatre Times Report                             *
.******************************************************************************
.* Date           :  14/12/1994                                               *
.* Author         :  Greg Horvat                                              *
.* Function       :  This report asks for either the discharge or theatre date*
.*                   ranges. It then gives the option to report by :          *
.*                        (1) Operation sequence                              *
.*                        (2) Theatre / discharge date sequence               *
.*                        (3) Attending doctor sequence                       *
.*                   If a patient has more than one theatre item in the same  *
.*                   session (same date & time), the report will flag the 1st *
.*                   theatre item (principal item) & not report the remaining *
.*                   items for that patient and that session.                 *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.00.02 02/06/2025 Nikitha B       TSK 0955096                           *
.*           Alphanumeric error corrections                                   *
.* V12.00.01 21/05/2025 Nikitha B       TSK 0955096                           *
.*           Alphanumeric changes                                             *
.******************************************************************************
.* V11.01.01 22/02/2021  Tracey Nguyen  TSK 0888639                           *
.*           FD changed - patmmbsf.PATMMBS1 from KEY10 to KEY11               *
.******************************************************************************
.* V11.00.01 11/03/2020  J.Tan          TSK 0838262                           *
.*           Added Effective from and to date to MBS Item file                *
.* V10.15.01  21/11/2019 Thanh T.            TSK 0880202                      *
.*            Added WRITNC00 to cater for when there are no MBS items entered *
.******************************************************************************
.* V10.04.01  15/06/2014 Jill Parkinson      CAR 301639                       *
.*            Moved TFILENAM to INIT0000                                      *
.* V10.03.01  28/12/2012 Patrick Adair                             CAR 261630 *
.*            Removed port number from temp file name                         *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.11.01  03/02/2009  Mike Laming   CAR 179286                      *
.*                  Correct positions of CONTROLF parameters (v6 => v9)       *
.*        V9.10.01  06/10/2008  Peter Vela CAR 179286                         *
.*                  Ported to 910                                             *
.******************************************************************************
.*        V6.10.02  11/07/2002  Madhuri                                       *
.*                  SRF19375 - Fix alignment in Patient Theatre Times Report  *
.*        V6.07.01  04/10/2000  Dean Cramer                                   *
.*                  Change print areas to handle 4 character DRG & MDC codes  * 
.*        V6.06.04  20/09/1999  Davin                                         *
.*                  Changes for 4 character DRGs                              *
.*        V6.06.03  23/03/1999  Jill Habasque                                 *
.*                  Changed read of the tempfile so that a patient will only  *
.*                  display once if they have more than one theatre item in   *
.*                  same session                                              *
.*        V6.06.02  22/03/1999  Jill Habasque SRF 630816                      *
.*                  Fixed the keys for the tempfiles                          *
.*        V6.06.01  10/02/1999  Jill Habasque                                 *
.*                  Fixed DISPHEAD display                                    *
.*        V6.05.02  08/12/1998  Nicole Harrington                             *
.*                  Removed CCENTRY                                           *
.*        V6.05.01  30/10/1998  Jill Habasque                                 *
.*                  Changed to display centuries                              *
.*        V6.04.B01 30/06/1998  Davin  605                                    *
.*                  Fixed I*L on tempfile & U/R display                       *
.*        V6.04.04  18/01/98 J.Tan  SRF 62022                                 *
.*                  Add parameter to check for a prosth.when validating item  *
.*        V6.04.03  15/01/98 J.Tan    SRF 623073                              *
.*                  Fixed I*C second index of patdoc                          *
.*        V6.04.02  26/09/97 J.Tan    SRF 621239                              *
.*                  Mods to change size of Theatre duration to FORM5          *
.*        V6.04.01  25/02/1997  Greg Horvat      SRF 619034                   *
.*                  Fixed a bug when calculating the duration over midnight   *
.*        V6.03.06  30/04/1996  Greg Horvat      SRF 615076                   *
.*                  Fixed a bug when running the operation report when it goes*
.*                  over a new year                                           *
.*        V6.03.05  02/04/96 J.Tan    SRF 614782                              *
.*                  Fixed number of patients and to print patients with more  *
.*                  than one operations                                       *
.*        V6.03.04  05/02/1996  Greg Horvat      SRF 613838                   *
.*                  Changed to only report the patient once if they have the  *
.*                  same operation date & time, & fixed the keys on the       *
.*                  tempfiles                                                 *
.*        V6.03.03  30/06/1995    Justin Coates  SRF 136814                   *
.*                  adoctord was printing to much so print with *+ (pdte0000) *
.*        V6.03.02  31/03/1995  Greg Horvat      SRF 135834                   *
.*                  Changed to report by the operation date range as well as  *
.*                  the discharge date range.                                 *
.*        V6.03.B05 24/02/1995  Greg Horvat      New Release 6.03             *
.*                  Recompiled for PATITMVD - Added PATCKEXC & changed to     *
.*                  display linked MBS code & description                     *
.*        V6.03.B00 24/01/95  Paul Howells                                    *
.*                  Fixed a few miscellaneous errors                          *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCONFD/INC
          INC       PATDAYFD/INC            * Day file
          INC       PATDO1FD/INC            * Doctor file
          INC       PATDSCFD/INC            * Discharge file
          INC       PATITMFD/INC            * Item file
          INC       PATMA1FD/INC            * Patient master file
          INC       HL7BMTFD/INC
          INC       PATMI1FD/INC            * Patient admission file
          INC       PMSVX1FD/INC            * Patient admission file
          INC       OPRDEAFD/INC  
          INC       OPRSRGFD/INC  
          INC       PATMMBFD/INC            * MBS file
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. ----- Tempfile Definition -----
.
TEMPA     IFILE     SQL, FIXED=30, KEY="U1-8,9-14"
TEMPB     IFILE     SQL, FIXED=30, KEY="U1-8,15-23"
INDX1     INIT      " 30 U1-8,9-14"
INDX2     INIT      " 30 U1-8,15-23"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
XADMNO    DIM       8        8          1       Admission number
XADOCT    DIM       6        6          9       Attending doctor
XTHEAT    DIM       9        9         15       Theatre code
XSPARE    DIM       6        6         24       Spare
.EOR                                   30
.
TEMP1     IFILE     SQL, FIXED=216, KEY="U76-84,9-16,125-133,143-147,160-165,17-19"
TEMP2     IFILE     SQL, FIXED=216, KEY="U58-65,125-133,143-147,9-16,17-19"
TEMP3     IFILE     SQL, FIXED=216, KEY="U125-133,143-147,40-47,9-16,17-19"
TEMP4     IFILE     SQL, FIXED=216, KEY="U160-165,9-16,125-133,143-147,17-19"
TEMP5     IFILE     SQL, FIXED=216, KEY="U125-133,143-147,9-16,17-19"
.
FILELIN1  INIT      " 216 U125-133,143-147,9-16,17-19 U76-84,9-16,125-133,":
                    "143-147,160-165,17-19"
FILELIN2  INIT      " 216 U125-133,143-147,9-16,17-19 U58-65,125-133,143-147,":
                    "9-16,17-19"
FILELIN3  INIT      " 216 U125-133,143-147,9-16,17-19 U125-133,143-147,40-47,":
                    "9-16,17-19"
FILELIN4  INIT      " 216 U125-133,143-147,9-16,17-19 U160-165,9-16,125-133,":
                    "143-147,17-19"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
URNO      DIM       8        8          1       U/R number
DADMISS   DIM       8        8          9       Admission number
DTMPCONT  DIM       3        3          17      Tempfile counter
PATNAME   DIM       20       20         20      Patient name
ADMDATE   DIM       8        8          40      Admission date - CCYYMMDD
DSPADDTE  DIM       10       10         48      Display admission date
DSCDATE   DIM       8        8          58      Discharge date - CCYYMMDD
DSPDSDTE  DIM       10       10         66      Display discharge date
THTCODE   DIM       9        9          76      Theatre code
THTCODED  DIM       40       40         85      Theatre code description
THTDATE   DIM       8        8          125     Theatre date - CCYYMMDD
DSPTHDTE  DIM       10       10         134     Display theatre date
THTSTTME  DIM       5        5          143     Theatre start time
THTEDTME  DIM       5        5          148     Theatre end time
THTDURAN  FORM      5        4          153     Theatre time duration
THTMULTO  DIM       3        3          157     Theatre multiple operation flag
ADOCTOR   DIM       6        6          160     Attending doctor
ADOCTORD  DIM       40       40         166     Attending doctor description
DRG       DIM       4        4          206     DRG code
MDC       DIM       4        4          210     MDC code
SPARE     DIM       2        2          214     Spare variable
.
.End of Record                          216
.
. ----- Program Variables -----
.
ADMISS    DIM       8                       * Admission number
BLNKLINE  FORM      1                       * Print a blank line flag
.
CMDLINE1  DIM       100
CMDLINE2  DIM       40
CURDATE   DIM       8                       * Current date - CCYYMMDD
.
DIM1A     DIM       1A                      * For KEYINMBS & PATITMDS
DIM3      DIM       3                       * For PATCODDS
DIM16     DIM       16
DIM20     DIM       20                      * For PATCODDS
DIM25     DIM       25
DIM24     DIM       24                      * SRF 19375
DOCCODE   DIM       6                       * Doctor code
DOCCOUNT  FORM      3                       * Doctor counter
DSPEDDOC  DIM       60                      * Display ending doctor code
DSPHEAD1  DIM       14                      * Display heading 1
DSPHEAD2  DIM       13                      * Display heading 2
DSPSTDOC  DIM       60                      * Display starting doctor code
DSPEDDTE  DIM       10                      * Display ending date
DSPSTDTE  DIM       10                      * Display starting date
DSPEDOPR  DIM       60                      * Display ending operation type
DSPSTOPR  DIM       60                      * Display starting operation type
DURCOUNT  FORM      5                       * Theatre duration counter
DURSUBCT  FORM      5                       * Theatre duration sub counter
.
ENDDAT8   DIM       8                       * Ending date - CCYYMMDD
ENDDOCT   DIM       6                       * Ending doctor code
ENDOPER   DIM       9                       * Ending operation type
ENDTIME   FORM      5                       * Ending time
.
FNAME     DIM       8                       * Tempfile name
FNAMEA    DIM       8                       * Tempfile name
FORM8     FORM      8
.
MINUTES   FORM      2                       * Minutes from operation start &
.                                             end times
MBSFOUND  DIM       1 
.
OPDATE    DIM       8                       * Operation date
OPTDSP    DIM       9                       * Option display
OPRCOUNT  FORM      3                       * Code counter
.
PATCOUNT  FORM      3                       * Patient report counter
PRTHEAD1  DIM       18                      * Print heading 1
PRTHEAD2  DIM       17                      * Print heading 2
.
SAVDCCOD  DIM       6                       * Save doctor code
SAVTHCOD  DIM       9                       * Save theatre code
SAVTMPCT  FORM      3                       * Save tempfile counter
STRTCCYY  DIM       4                       * Starting century & year
STRTDAT8  DIM       8                       * Starting date - CCYYMMDD
STRTDOCT  DIM       6                       * Starting doctor code
STRTMMDD  DIM       4                       * Starting month & day
STRTOPER  DIM       9                       * Starting operation type
STRTTIME  FORM      5                       * Starting time
.
TMPCOUNT  FORM      3                       * Tempfile counter
.
WORKCCYY  DIM       4                       * Century & year
WORKDAT8  DIM       8                       * Date - CCYYMMDD
WORKMMDD  DIM       4                       * Month & day
.
. ----- Program Constants -----
.
DISCH     INIT      "Discharge"
.
DISP1     INIT      "By Disc. Date,"
DISP2     INIT      "By Oper. Date,"
DISP3     INIT      "Type of Oper."
DISP4     INIT      "Date Seq."
DISP5     INIT      "Attending Doc."
.
OPER      INIT      "Operation"
.
PRNT1     INIT      "By Discharge Date,"
PRNT2     INIT      "By Operation Date,"
PRNT3     INIT      "Type of Operation"
PRNT4     INIT      "Date Sequence"
PRNT5     INIT      "Attending Doctor"
.
SP12      INIT      "            "
SP60      INIT      "                                        ":
                    "                    "
SP120     INIT      "                                        ":
                    "                                        ":
                    "                                        "
ZED9      INIT      "zzzzzzzzz"
START     INIT      "Start"
FINISH    INIT      "Finish"
.
PRGID     INIT      "IBAINP78"
PRGNAM    INIT      "Patient Theatre Times Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialize variables & open files
ML1000    CALL      OPTN0000                * Choose Option
          BRANCH    EXIT,ML9000
.
ML2000    CALL      DATE0000                * Keyin date range
          BRANCH    EXIT,ML1000
.
ML3000    CALL      COPT0000                * Sub choose option
          BRANCH    EXIT,ML2000
.
          BRANCH    COPTION,ML4000,ML6000,ML5000
.                           Oper,  Date,  Doctor
.
ML4000    CALL      OPER0000                * Operation range
          BRANCH    EXIT,ML3000
          GOTO      ML6000
.
ML5000    CALL      DOCT0000                * Doctor range
          BRANCH    EXIT,ML3000
.
ML6000    CALL      BLNK0000                * Print a blank line (Y/N) ?
          IF        EXIT=1
            BRANCH    COPTION,ML4000,ML3000,ML5000
.                             Oper,  Date,  Doctor
          ENDIF
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML7000,ML6000,ML1000
.                         Yes,   No,    Cancel
.
ML7000    PERFORM   OPTION,LODA0000,LODB0000               * Load tempfiles
.                          Disch,   Oper
.
          PERFORM   COPTION,POPR0000,PDTE0000,PDOC0000     * Print reports
.                           Oper,    Date,    Doctor
.
          GOTO      ML1000
.
ML9000    CALL      KILL0000                * Delete tempfile
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P56:24,"Opening patdoctf";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmmbsf";
          OPEN      PATMMBS1,"patmmbsf"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"oprsrgaf";
          OPEN     OPRSRGA1,"oprsrgaf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEA
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*115,PTCNDGUP,*116,PTCNDGUA,*239,PTCNDGAD:
                                      *240,PTCNDGDS,*241,PTCNDGTR,*242,PTCNDGRG
..        READ      CONTROLF,SEVENTY9;*207,PTCNIWRN
          CLOSE     CONTROLF
.
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          MOVE      SP20,CPHDROPT
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
INIT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000               Called by: ML0000   *
.*                               Choose Option                                *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          CALL      DISPHEAD
          DISPLAY   *P1:3,*EF;
          DISPLAY   *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Discharge Date":
                    *P1:6,*V2LON,"2",*HOFF," = By Operation Date":
                    *P1:8,*HOFF,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,OPTION;
          BRANCH    OPTION,OPTN2000,OPTN2000
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CLEAR     OPTDSP
          LOAD      OPTDSP,OPTION,DISCH,OPER
.                                 Disch,Oper
          CLEAR     DSPHEAD1
          CLEAR     PRTHEAD1
          LOAD      DSPHEAD1,OPTION,DISP1,DISP2
          LOAD      PRTHEAD1,OPTION,PRNT1,PRNT2
.                                   Disch,Oper
          CALL      DISPLIN2
          DISPLAY   *P54:2,*V2LON," - ",DSPHEAD1
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 DATE0000               Called by: ML0000   *
.*                           Keyin The Date Range                             *
.******************************************************************************
DATE0000  MOVE      ONE,CCANLDTE            * Cancel default date
.
. ----- Enter Starting Date -----
.
DATE1000  DISPLAY   *P1:10,*EF,"Starting ",OPTDSP," Date : ":
                    *P1:11,*EL,"Ending   ",OPTDSP," Date : ";
          MOVE      "10",CVERT
          MOVE      "27",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,DATE9500
.
          PACK      STRTDAT8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDAT8
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,DSPSTDTE
          REP       " 0",DSPSTDTE
          UNPACK    STRTDAT8,STRTCCYY,STRTMMDD
          REP       " 0",STRTCCYY
          REP       " 0",STRTMMDD
.
. ----- Check If Starting Today Is Greater Than Today's Date -----
.
          MATCH     STRTDAT8,CURDATE
          IF        @LESS
            DISPLAY    *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                       "date. ";
            CALL       HITENTER
            GOTO       DATE1000
          ENDIF
.
. ----- Enter Ending Date -----
.
DATE2000  MOVE      "11",CVERT
          MOVE      "27",CCOL
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,DATE1000
.
          PACK      ENDDAT8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDAT8
          CALL      PACDATE                 * Pack the ending date
          MOVE      CPCDATE,DSPEDDTE
          REP       " 0",DSPEDDTE
.
. ----- Check If The Ending Today Is Greater Than Today's Date -----
.
          MATCH     ENDDAT8,CURDATE
          IF        @LESS
            DISPLAY    *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                       "date. ";
            CALL       HITENTER
            GOTO       DATE2000
          ENDIF
.
. ----- Check That The Ending Date Is Greater Than The Starting Date -----
.
          MATCH     STRTDAT8,ENDDAT8
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending date must be greater than the ":
                      "starting date. ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                 COPT0000               Called by: ML0000   *
.*                             Choose Sub Option                              *
.******************************************************************************
COPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:13,*EF;
          DISPLAY   *P1:13,*V2LON,"0",*HOFF," = Exit":
                    *P1:14,*V2LON,"1",*HOFF," = By Type of Operation":
                    *P1:15,*V2LON,"2",*HOFF," = By Date Sequence":
                    *P1:16,*V2LON,"3",*HOFF," = By Attending Doctor":
                    *P1:18,*HOFF,"Select Option : ";
.
COPT1000  KEYIN     *P17:18,*V2LON,COPTION;
          BRANCH    COPTION,COPT2000,COPT2000,COPT2000
.
          COMPARE   ZERO,COPTION
          GOTO      COPT9500 IF EQUAL
.
          BEEP
          GOTO      COPT1000
.
COPT2000  CLEAR     DSPHEAD2
          CLEAR     PRTHEAD2
          LOAD      DSPHEAD2,COPTION,DISP3,DISP4,DISP5
          LOAD      PRTHEAD2,COPTION,PRNT3,PRNT4,PRNT5
.                                    Oper, Date, Doctor
          CALL      DISPLIN2
          DISPLAY   *P54:2,*V2LON,DSPHEAD2
          MOVE      ZERO,EXIT
          GOTO      COPT9999
.
COPT9500  MOVE      ONE,EXIT
COPT9999  RETURN
+
.******************************************************************************
.*                                 OPER0000               Called by: ML0000   *
.*                        Keyin Operation Type Range                          *
.******************************************************************************
OPER0000  DISPLAY   *P1:20,*EF,"Starting Operation Type : ":
                    *P1:21,*EL,"Ending   Operation Type : ";
          MOVE      "20",EVERT
          MOVE      "27",ECOL 
          MOVE      SP30,CKYIDEF9
          MOVE      ZERO,CKYIMAND
          CALL      KEYINMBS
          BRANCH    EXIT,OPER3000,OPER9500
.
          DISPLAY   *P27:20,*EL,*V2LON,AMBS,*P40:20,*HOFF,IDESC
          MOVE      AMBS,STRTOPER  
          PACK      DSPSTOPR,STRTOPER,SP2,IDESC,SP60
          GOTO      OPER5000
.
OPER3000  MOVE      SP9,STRTOPER
          DISPLAY   *P27:20,*EL,*V2LON,START  
          PACK      DSPSTOPR,START,SP60
.          
OPER5000  MOVE      "21",EVERT
          MOVE      "27",ECOL 
          MOVE      SP30,CKYIDEF9
          MOVE      ZERO,CKYIMAND
          CALL      KEYINMBS
          BRANCH    EXIT,OPER7000,OPER9500
.
          DISPLAY   *P27:21,*EL,*V2LON,AMBS,*P40:21,*HOFF,IDESC
          MOVE      AMBS,ENDOPER  
          PACK      DSPEDOPR,ENDOPER,SP2,IDESC,SP60
          GOTO      OPER8000
.
OPER7000  MOVE      ZED9,ENDOPER
          DISPLAY   *P27:21,*EL,*V2LON,FINISH
          PACK      DSPEDOPR,FINISH,SP60
          GOTO      OPER9000
.
. ----- Check That The Ending Operation is Greater Than The Starting Op. -----
.
OPER8000  MATCH     STRTOPER,ENDOPER
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending operation must be greater than ":
                      "the starting operation. ";
            CALL      HITENTER
            GOTO      OPER0000
          ENDIF
.
OPER9000  MOVE     ZERO,EXIT
          GOTO     OPER9999
.
OPER9500  MOVE     ONE,EXIT
OPER9999  RETURN
+
.******************************************************************************
.*                                 DOCT0000               Called by: ML0000   *
.*                            Keyin Doctor Range                              *
.******************************************************************************
DOCT0000  DISPLAY   *P1:20,*EF,"Starting Doctor Code    : ":
                    *P1:21,*EL,"Ending   Doctor Code    : ";
          MOVE      "20",EVERT
          MOVE      "27",ECOL 
          MOVE      SP30,CKYIDEF6
          MOVE      ZERO,CKYIMAND
          CALL      KEYINDOC
          BRANCH    EXIT,DOCT3000,DOCT9500
.
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME 
          MOVE      DSNAME,PACSNAME 
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          DISPLAY   *P40:20,*EL,PACFNAME
          MOVE      DOCCODE,STRTDOCT
          PACK      DSPSTDOC,STRTDOCT,SP2,PACFNAME,SP60
          GOTO      DOCT5000
.
DOCT3000  MOVE      SP9,STRTDOCT
          DISPLAY   *P27:20,*EL,*V2LON,START
          PACK      DSPSTDOC,START,SP60
.          
DOCT5000  MOVE      "21",EVERT
          MOVE      "27",ECOL 
          MOVE      SP30,CKYIDEF6
          MOVE      ZERO,CKYIMAND
          CALL      KEYINDOC
          BRANCH    EXIT,DOCT7000,DOCT9500
.
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME 
          MOVE      DSNAME,PACSNAME 
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          DISPLAY   *P40:21,*EL,PACFNAME
          MOVE      DOCCODE,ENDDOCT  
          PACK      DSPEDDOC,ENDDOCT,SP2,PACFNAME,SP60
          GOTO      DOCT8000
.
DOCT7000  MOVE      ZED9,ENDDOCT
          DISPLAY   *P27:21,*EL,*V2LON,FINISH
          PACK      DSPEDDOC,FINISH,SP60
          GOTO      DOCT9000
.
. ----- Check That The Ending Doc Is Greater Than The Starting Doc -----
.
DOCT8000  MATCH     STRTDOCT,ENDDOCT
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending doctor must be greater than ":
                      "the starting doctor. ";
            CALL      HITENTER
            GOTO      DOCT0000
          ENDIF
.
DOCT9000  MOVE     ZERO,EXIT
          GOTO     DOCT9999
.
DOCT9500  MOVE     ONE,EXIT
DOCT9999  RETURN
+
.******************************************************************************
.*                                 LODA0000               Called by: ML0000   *
.*                    Load The Tempfile By Discharge Date                     *
.******************************************************************************
LODA0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,DURCOUNT
          MOVE      ZERO,DURSUBCT
          MOVE      ZERO,PATCOUNT
          MOVE      ZERO,TMPCOUNT
          CALL      OPEN0000                * Open the tempfile
          DISPLAY   *P1:24,*EL,"Scanning : ",*P23:24,"Admission : ":
                    *P46:24,"Loading : ";
          PACK      KEY16,STRTDAT8,SP12
          CALL      RDSDSCH2                * Position on & read the discharge
LODA1000  CALL      RDKDSCH2                  file
          BRANCH    OVRCD,LODA9999
.
          MATCH     DDATE,ENDDAT8
          GOTO      LODA9999 IF LESS        * Out of date range, exit
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          DISPLAY   *P12:24,*V2LON,CPCDATE,*P35:24,DADMNO;
.
          PACK      KEY8,DADMNO
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,LODA1000
.
          COMPARE   THREE,ASTAT
          GOTO      LODA1000 IF NOT EQUAL   * Patient not discharged, get next
.
          PACK      KEY8,AURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,LODA1000
.
          IF        COPTION=3
            CALL      CKDC0000              * Check the doctor code
            BRANCH    EXIT,LODA1000
          ENDIF
.
          CALL      WRIT0000                * Write to the tempfile
          GOTO      LODA1000
.
LODA9999  RETURN
+
.******************************************************************************
.*                                 LODB0000               Called by: ML0000   *
.*                    Load The Tempfile By Operation Date                     *
.******************************************************************************
LODB0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,DURCOUNT
          MOVE      ZERO,DURSUBCT
          MOVE      ZERO,PATCOUNT
          MOVE      ZERO,TMPCOUNT
          CALL      OPEN0000                * Open the tempfile
.
          DISPLAY   *P1:24,*EL,"Scanning : ",*P23:24,"Admission : ":
                    *P46:24,"Loading : ";
          MOVE      STRTCCYY,WORKCCYY
          MOVE      STRTMMDD,WORKMMDD
.
          PACK      CFNAME,FILDAYA1,WORKCCYY
          CLOSE     PATDAYA1
          TRAP      OPDY0000 IF IO
          OPEN      PATDAYA1,CFNAME         * Open the day file
          TRAPCLR   IO
.
          PACK      KEY12,WORKMMDD,SP10
          CALL      RSPTDAY1                * Position on & read the day file
LODB1000  CALL      RKPTDAY1
          BRANCH    OVRCD,LODB2000
.
          UNPACK    WORKCCYY,CCENT,CYEAR
          PACK      WORKDAT8,WORKCCYY,PTDYDATE
          REP       " 0",WORKDAT8
.
          MATCH     STRTDAT8,WORKDAT8
          GOTO      LODB9999 IF LESS        * Date less than starting date
.
          MATCH     WORKDAT8,ENDDAT8
          GOTO      LODB9999 IF LESS        * Date greater than ending date
.
          MOVE      PTDYADMN,AADMNO
          PACK      KEY8,AADMNO
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,LODB1000
.
          MATCH     ADATE,ENDDAT8
          GOTO      LODB1000 IF LESS        * Admitted after specified date
.
          UNPACK    WORKDAT8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          DISPLAY   *P12:24,*V2LON,CPCDATE,*P35:24,AADMNO;
.
          PACK      KEY8,AURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,LODB1000
.
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * Read the discharge file
          PERFORM   OVRCD,CLRD0000          * Clear the discharge file vars
.
          IF        COPTION=3
            CALL      CKDC0000              * Check the doctor code
            BRANCH    EXIT,LODB1000
          ENDIF
.
          CALL      WRIT0000                * Write to the tempfile
          GOTO      LODB1000
.
LODB2000  MATCH     "1231",PTDYDATE
          GOTO      LODB9999 IF NOT EQUAL   * Not last day of the year, exit
.
. ----- Open Next Year's File -----
.
          MOVE      WORKCCYY,FORM4
          ADD       ONE,FORM4
          MOVE      FORM4,WORKCCYY
.
          PACK      CFNAME,FILDAYA1,WORKCCYY
          CLOSE     PATDAYA1
          TRAP      OPDY0000 IF IO
          OPEN      PATDAYA1,CFNAME         * Open the day file
          TRAPCLR   IO
.
          MOVE      "0101",WORKMMDD
          PACK      KEY12,WORKMMDD,SP10
          CALL      RSPTDAY1                * Re-position on the day file
.
          GOTO      LODB1000
.
LODB9999  RETURN
+
.******************************************************************************
.*                                 BLNK0000               Called by: ML0000   *
.*                         Print A Blank Line (Y/N) ?                         *
.******************************************************************************
BLNK0000  MOVE      "23",CVERT
          IF        COPTION=2
            MOVE      "20",CVERT
          ENDIF
          MOVE      ZERO,BLNKLINE
          DISPLAY   *P1:CVERT,*EL,"Print a blank line between each line (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?";
BLNK1000  KEYIN     *P46:CVERT,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      BLNK9500 IF EQUAL       * Exit from question
.
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ONE,BLNKLINE          * Blank line
            GOTO      BLNK9000
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ZERO,BLNKLINE         * No blank line
            GOTO      BLNK9000
          ENDIF
.
          BEEP
          GOTO      BLNK1000
.
BLNK9000  MOVE      ZERO,EXIT
          GOTO      BLNK9999
.
BLNK9500  MOVE      ONE,EXIT
BLNK9999  RETURN
+
.******************************************************************************
.*                                 WRIT0000               Called by: LODA0000 *
.*                           Write To The Tempfile                 & LODB0000 *
.******************************************************************************
.
WRIT0000  MOVE      "0",MBSFOUND
          PACK      KEY11,AADMNO,SP8
          CALL      RDSMMBS1                * Position on & read the patient
WRIT1000  CALL      RDKMMBS1                  medical records MBS file
          BRANCH    OVRCD,WRIT9000
.
          MATCH     AADMNO,MMADMN
          GOTO      WRIT9000 IF NOT EQUAL   * Different admission no, get next
.
          MOVE      "1",MBSFOUND
          UNPACK    MMDATE,CCENT,CYEAR,CMON,CDAY
          PACK      OPDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",OPDATE
.
. ----- Check If Operation Date Is Within The Operation Date Range -----
.
          IF        OPTION=2
            MATCH     WORKDAT8,OPDATE
            GOTO      WRIT1000 IF NOT EQUAL * Not the operation date required
          ENDIF
.
          IF        COPTION=1
            CALL      CKOP0000              * Check the operation type
            BRANCH    EXIT,WRIT1000
          ENDIF
.
          MOVE      TMPCOUNT,SAVTMPCT
          PACK      KEY24,SP30
.         PACK      KEY24,OPDATE,MMSTIM,AADMNO,SP20
          CALL      RSTEMP5                 * Position on & read the 5th
WRIT3000  CALL      RKTEMP5                   tempfile
          IF        OVRCD<>1
            MATCH     OPDATE,THTDATE
            IF        @EQUAL
              MATCH     MMSTIM,THTSTTME
              IF        @EQUAL
                MATCH     ADMISS,AADMNO
                IF        @EQUAL
                  MOVE      "Yes",THTMULTO
                  CALL      UPTEMP5         * Update the 5th tempfile because
                  MOVE      SAVTMPCT,TMPCOUNT of duplicate data
                  GOTO      WRIT1000
.
                ENDIF
              ENDIF
            ENDIF
            GOTO      WRIT3000
          ENDIF
.
          MOVE      SAVTMPCT,TMPCOUNT
          CALL      CLRT0000                * Clear the tempfile variables
          MOVE      PURNO,URNO
          MOVE      AADMNO,ADMISS
          ADD       ONE,TMPCOUNT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patients name
          MOVE      PACFNAME,PATNAME
          MOVE      ADATE,ADMDATE
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format admission date
          MOVE      CPCDATE,DSPADDTE
          MOVE      DDATE,DSCDATE
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format discharge date
          MOVE      CPCDATE,DSPDSDTE
          MOVE      MMCODE,THTCODE
          PACK      KEY9,MMCODE
          PACK      KEY17,KEY9,OPDATE,SP70
          CALL      PATITMRD                * Read the item file
          IF        OVRCD<>1
            MOVE      IDESC,THTCODED
          ENDIF
          MOVE      OPDATE,THTDATE
          UNPACK    OPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format theatre date
          MOVE      CPCDATE,DSPTHDTE
          MOVE      MMSTIM,THTSTTME
          MOVE      MMETIM,THTEDTME
          CALL      DURA0000                * Calculate duration between start
          MOVE      ENDTIME,THTDURAN          & finish times
          MOVE      ADOCTA,ADOCTOR
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1                 * Read the dcotors file
          IF        OVRCD<>1
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME               * Format doctors name
            MOVE      PACFNAME,ADOCTORD
          ENDIF
          MOVE      PTDSDDRG,DRG
          MOVE      PTDSDMDC,MDC
          PACK      KEY24,THTDATE,THTSTTME,ADMISS,TMPCOUNT
          CALL      WRTEMP5                 * Write to the 5th tempfile
          DISPLAY   *P54:24,*V2LON,ADMISS;
          GOTO      WRIT1000
.
WRIT9000  MOVE      ZERO,EXIT
          MATCH     "0",MBSFOUND
          IF        @EQUAL
            CALL      WRITNC00
          ENDIF
.
WRIT9999  RETURN
.
.---------------------------------------------------------------------------
. Write To The Tempfile when there are no MBS items are entered 
.---------------------------------------------------------------------------
.
WRITNC00  IF        COPTION=1
            MATCH     SP70,STRTOPER     * if start operation not blank, skip 
            IF        !@EQUAL
              GOTO      WRITNC99
            ENDIF 
          ENDIF
.
          PACK      KEY31,AADMNO,SP70
          CALL      RSOPDEA2
WRITNC10  CALL      RKOPDEA2
          BRANCH    OVRCD,WRITNC99
.
          MATCH     DAADMNO,OPDAADMN
          GOTO      WRITNC99 IF NOT EQUAL
.
          PACK      KEY11,OPDAUNIQ,SP70
          CALL      RSOPSRG1
WRITNC20  CALL      RKOPSRG1
          BRANCH    OVRCD,WRITNC10
.
          MATCH     OPSGUNID,OPDAUNIQ
          GOTO      WRITNC10 IF NOT EQUAL
.
.----- Check If Operation Date Is Within The Operation Date Range -----
. 
          IF        OPTION=2
            MATCH     WORKDAT8,OPDADATE
            GOTO      WRITNC20 IF NOT EQUAL * Not the operation date required
          ENDIF
.
          MOVE      TMPCOUNT,SAVTMPCT
          PACK      KEY24,SP30
          CALL      RSTEMP5                 * Position on & read the 5th
WRITNC30  CALL      RKTEMP5                   tempfile
          IF        OVRCD<>1
            MATCH     OPDADATE,THTDATE
            IF        @EQUAL
              MATCH     OPSGTISS,THTSTTME
              IF        @EQUAL
                MATCH     ADMISS,AADMNO
                IF        @EQUAL
                  MOVE      "Yes",THTMULTO
                  CALL      UPTEMP5         * Update the 5th tempfile because
                  MOVE      SAVTMPCT,TMPCOUNT of duplicate data
                  GOTO      WRITNC10
                ENDIF
              ENDIF
            ENDIF
            GOTO      WRITNC30
          ENDIF
.
          MOVE      SAVTMPCT,TMPCOUNT
          CALL      CLRT0000                * Clear the tempfile variables
          MOVE      PURNO,URNO
          MOVE      AADMNO,ADMISS
          ADD       ONE,TMPCOUNT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patients name
          MOVE      PACFNAME,PATNAME
          MOVE      ADATE,ADMDATE
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format admission date
          MOVE      CPCDATE,DSPADDTE
          MOVE      DDATE,DSCDATE
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format discharge date
          MOVE      CPCDATE,DSPDSDTE
          MOVE      SP70,THTCODE
          MOVE      SP70,THTCODED
          MOVE      OPDADATE,THTDATE
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format theatre date
          MOVE      CPCDATE,DSPTHDTE
          MOVE      OPSGTISS,THTSTTME
          MOVE      OPSGTISS,MMSTIM 
          MOVE      OPSGTISE,THTEDTME
          MOVE      OPSGTISE,MMETIM
          CALL      DURA0000                * Calculate duration between start
          MOVE      ENDTIME,THTDURAN          & finish times
          MOVE      ADOCTA,ADOCTOR
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1                 * Read the dcotors file
          IF        OVRCD<>1
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME               * Format doctors name
            MOVE      PACFNAME,ADOCTORD
          ENDIF
          MOVE      PTDSDDRG,DRG
          MOVE      PTDSDMDC,MDC
          PACK      KEY24,THTDATE,THTSTTME,ADMISS,TMPCOUNT
          CALL      WRTEMP5                 * Write to the 5th tempfile
          DISPLAY   *P54:24,*V2LON,ADMISS;
.
          GOTO      WRITNC20

WRITNC99  RETURN
+
.******************************************************************************
.*                                 OPDY0000               Called by: LODB0000 *
.*                    Create The Day File That Is Needed                      *
.******************************************************************************
OPDY0000  CLEAR     CMDLINE2
          APPEND    "isbuild ",CMDLINE2
          APPEND    CFNAME,CMDLINE2
          APPEND    " 13 U1-4,5-12 ",CMDLINE2
          RESET     CMDLINE2
          EXECUTE   CMDLINE2,TASKID
          OPEN      PATDAYA1,CFNAME
OPDY9999  RETURN
+
.******************************************************************************
.*                                 CLRD0000               Called by: LODB0000 *
.*                      Clear The Discharge Variables                         *
.******************************************************************************
CLRD0000  UNPACK    SP60,DDATE,PTDSDDRG,PTDSDMDC
          MOVE      ZEROVISN,DADMNO
CLRD9999  RETURN
+
.******************************************************************************
.*                                 CKDC0000               Called by: WRIT0000 *
.*                           Check The Doctor Code                            *
.******************************************************************************
CKDC0000  MATCH     SP9,STRTDOCT
          IF        !@EQUAL
            MATCH     STRTDOCT,ADOCTA
            GOTO      CKDC9500 IF LESS
          ENDIF  
.
          MATCH     ZED9,ENDDOCT
          IF        !@EQUAL
            MATCH     ADOCTA,ENDDOCT
            GOTO      CKDC9500 IF LESS
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      CKDC9999
.
CKDC9500  MOVE      ONE,EXIT
CKDC9999  RETURN
+
.******************************************************************************
.*                                 CKOP0000               Called by: WRIT0000 *
.*                         Check The Operation Type                           *
.******************************************************************************
CKOP0000  MATCH     SP9,STRTOPER
          IF        !@EQUAL
            MATCH     STRTOPER,MMCODE
            GOTO      CKOP9500 IF LESS
          ENDIF  
.
          MATCH     ZED9,ENDOPER
          IF        !@EQUAL
            MATCH     MMCODE,ENDOPER
            GOTO      CKOP9500 IF LESS
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      CKOP9999
.
CKOP9500  MOVE      ONE,EXIT
CKOP9999  RETURN
+
.******************************************************************************
.*                                 CLRT0000               Called by: WRIT0000 *
.*                       Clear The Tempfile Variables                         *
.******************************************************************************
CLRT0000  UNPACK    SP120,URNO,PATNAME,ADMDATE,DSPADDTE,DSCDATE:
                          DSPDSDTE,THTCODE,THTCODED,THTCODE
          UNPACK    SP120,DSPTHDTE,THTSTTME,THTEDTME,THTMULTO,ADOCTOR:
                          ADOCTORD,DRG,MDC
          MOVE      ZEROVISN,ADMISS
          MOVE      ZERO,THTDURAN
CLRT9999  RETURN
+
.******************************************************************************
.*                                 DURA0000               Called by: WRIT0000 *
.*               Calculate Duration Between Start & Finish Times              *
.******************************************************************************
DURA0000  UNPACK    MMETIM,CHOUR,DIM1,CMIN  * End time
          MOVE      CHOUR,ENDTIME
          MOVE      CMIN,MINUTES
          ASSIGN    (60*ENDTIME)+MINUTES,ENDTIME
.
          UNPACK    MMSTIM,CHOUR,DIM1,CMIN  * Start time
          MOVE      CHOUR,STRTTIME
          MOVE      CMIN,MINUTES
          ASSIGN    (60*STRTTIME)+MINUTES,STRTTIME
.
          SUB       STRTTIME,ENDTIME        * End time - start time
          COMPARE   ZERO,ENDTIME            * Test if duration is positive
          GOTO      DURA9999 IF NOT LESS
.
          ADD       "1440",ENDTIME          * Adjust dura. by a day over 24hrs
DURA9999  RETURN
+
.******************************************************************************
.*                                 HEAD0000               Called by: POPR0000 *
.*                          Print The Main Heading                 & PDTE0000 *
.*                                                                 & PDOC0000 *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print main report heading
          PRINT     *40,PRTHEAD1,SP1,PRTHEAD2,*N,*N:
                    *40,"Starting ",OPTDSP," Date : ",DSPSTDTE,*N:
                    *40,"Ending   ",OPTDSP," Date : ",DSPEDDTE,*N
          ADD       "5",CLNO
.
. ----- Operation Type Option -----
.
          IF        COPTION=1
            PRINT     *40,"Starting Operation Type : ",DSPSTOPR,*N:
                      *40,"Ending   Operation Type : ",DSPEDOPR,*N
            ADD       "3",CLNO
          ENDIF
.
. ----- Attending Doctor Option -----
.
          IF        COPTION=3
            PRINT     *40,"Starting Doctor Code    : ",DSPSTDOC,*N:
                      *40,"Ending   Doctor Code    : ",DSPEDDOC,*N
            ADD       "3",CLNO
          ENDIF
HEAD9999  RETURN
+
.******************************************************************************
.*                                 POPR0000               Called by: ML0000   *
.*                        Print Operation The Report                          *
.******************************************************************************
POPR0000  MOVE      ZERO,OPRCOUNT
          DISPLAY   *P1:24,*EL,"Printing : ";
          CALL      HEAD0000                * Print the main report heading
          PACK      KEY39,SP60
          CALL      RSTEMP1                 * Position on & read the 1st
          CALL      RKTEMP1                   tempfile
          BRANCH    OVRCD,POPR9000
.
          CALL      HOPR0000                * Print the operation heading
          PACK      KEY39,SP60
          CALL      RSTEMP1                 * Position on & read the 1st
POPR1000  CALL      RKTEMP1                   tempfile
          BRANCH    OVRCD,POPR9100
.
          MATCH     THTCODE,SAVTHCOD
          IF        !@EQUAL
            IF        CLNO>49
              CALL      TOPR0000            * Print the operation tail
              CALL      HEAD0000            * Print the main report heading
              CALL      HOPR0000            * Print the operation heading
            ELSE
              CALL      TOPR0000            * Print the operation tail
              CALL      HOPR0000            * Print the operation heading
            ENDIF
          ENDIF
.
          IF        CLNO>58
            CALL      UND132                * Print an underline
            CALL      HEAD0000              * Print the main report heading
            CALL      HOPR0000              * Print the operation heading
          ENDIF
.
          MOVE      PATNAME,DIM16
.
          SETLPTR   ADOCTORD,16             * srf 19375 - changed 20 to 16
.123456 123456789012345|
.23456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
.        1         2         3         4         5         6         7         8         9         0         1         2         3
          PRINT     *1,"|",*2,ADOCTOR,*9,*+,ADOCTORD,*-,*25,"|",*26,DSPTHDTE:
                    *36,"|",*37,THTSTTME,*42,"|",*43,THTEDTME:
                    *48,"|",*49,THTDURAN,*54,"|",*55,THTMULTO,*60,"|",*61,URNO:
                    *70,"|",*71,ADMISS,*81,"|",*82,DIM16:
                    *100,"|",*101,DSPADDTE,*111,"|",*112,DSPDSDTE:
                    *122,"|",*123,DRG,*127,"|",*128,MDC,*132,"|"
          ADD       ONE,CLNO
          CALL      PBOP0000                * Print a blank line
          ADD       THTDURAN,DURCOUNT
          ADD       THTDURAN,DURSUBCT
.
.         Accumulate the total number of patients
.
          PACK      KEY17,ADMISS,SP10
          CALL      RSTEMPB
          CALL      RKTEMPB
          BRANCH    OVRCD,POPR8200
.
          MOVE      XADMNO,DIM8VISN
          MATCH     ADMISS,DIM8VISN
          GOTO      POPR8400 IF EQUAL
.
POPR8200  ADD       ONE,PATCOUNT
.
.         Accumulate number of patients per MBS/theatre
.
POPR8400  PACK      KEY17,ADMISS,THTCODE
          CALL      RATEMPB
          COMPARE   ZERO,OVRCD
          GOTO      POPR8600 IF EQUAL
.
          MOVE      ADMISS,XADMNO
          MOVE      THTCODE,XTHEAT
          MOVE      SP10,XADOCT
          CALL      WRTEMPB
          ADD       ONE,OPRCOUNT
.
POPR8600  DISPLAY   *P12:24,*V2LON,ADMISS;
          GOTO      POPR1000
.
POPR9000  CALL      HOPR0000                * Print the operation header
          GOTO      POPR9200
.
POPR9100  CALL      TOPR0000                * Print the operation tail
POPR9200  PRINT     *1,"Total Theatre Duration : ",DURCOUNT," minutes",*N:
                    *1,"Total Patients : ",PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete. ";
          CALL      HITENTER
POPR9999  RETURN
+
.******************************************************************************
.*                                 HOPR0000               Called by: POPR0000 *
.*                      Print The Sub Operation Heading                       *
.******************************************************************************
HOPR0000  PACK      KEY9,THTCODE
          PACK      KEY17,KEY9,SP70
          CALL      PATITMRD                * Read the item file
          IF        OVRCD=1
            MOVE      SP70,IDESC
          ENDIF
.
          MOVE      THTCODE,SAVTHCOD
          PRINT     *1,"MBS Code : ",THTCODE,SP2,IDESC
          ADD       ONE,CLNO
          CALL      UND132                  * Print an underline
.                                           * srf 19375 Chnged Durat,mins align
          PRINT     *1,"|Attending",*25,"|Theatre",*36,"|Start",*42,"|End":
                    *48,"|Durat",*54,"|Multi",*60,"|U/R",*70,"|Admin":
                    *81,"|Patient",*100,"|Admin",*111,"|Disch",*122,"|DRG":
                    *127,"|MDC",*132,"|"
          PRINT     *1,"|Doctor",*25,"|Date",*36,"|Time",*42,"|Time":
                    *48,"|mins ",*54,"|Thea.",*60,"|Number",*70,"|Number":
                    *81,"|Name",*100,"|Date",*111,"|Date",*122,"|":
                    *127,"|",*132,"|"
          ADD       "2",CLNO
          CALL      UND132                  * Print an underline
HOPR9999  RETURN
+
.******************************************************************************
.*                                 TOPR0000               Called by: POPR0000 *
.*                          Print The Operation Tail                          *
.******************************************************************************
TOPR0000  CALL      UND132                  * Print an underline
          STRIP     SAVTHCOD
          RESET     SAVTHCOD
          PRINT     *1,"Total Duration for MBS Code ",SAVTHCOD," is ":
                    DURSUBCT," minutes",*N:
                    *1,"Total Number of Patient(s) for MBS Code ",SAVTHCOD:
                    " is ",OPRCOUNT,*N
          ADD       "3",CLNO
          MOVE      ZERO,DURSUBCT
          MOVE      ZERO,OPRCOUNT
TOPR9999  RETURN
+
.******************************************************************************
.*                                 PBOP0000               Called by: POPR0000 *
.*                  Print A Blank Line On Operation Report                    *
.******************************************************************************
PBOP0000  IF        BLNKLINE=1
            PRINT     *1,"|",*25,"|",*36,"|",*42,"|",*48,"|",*54,"|",*60,"|":
                      *70,"|",*81,"|",*100,"|",*111,"|",*122,"|",*127,"|":
                      *132,"|"
            ADD       ONE,CLNO
          ENDIF
PBOP9999  RETURN
+
.******************************************************************************
.*                                 PDTE0000               Called by: ML0000   *
.*                        Print The Date Range Report                         *
.******************************************************************************
PDTE0000  DISPLAY   *P1:24,*EL,"Printing : ";
          CALL      HEAD0000                * Print the main report heading
          CALL      HDTE0000                * Print the date range heading
          PACK      KEY32,SP30,SP10
          PERFORM   OPTION,RSTEMP2,RSTEMP3  * Position on the tempfile
          PERFORM   OPTION,RKTEMP2,RKTEMP3  * Read the tempfile
          BRANCH    OVRCD,PDTE9000
.
          CALL      UND132                  * Print an underline
          PACK      KEY32,SP30,SP10
          PERFORM   OPTION,RSTEMP2,RSTEMP3  * Position on the tempfile
PDTE1000  PERFORM   OPTION,RKTEMP2,RKTEMP3  * Read the tempfile
          BRANCH    OVRCD,PDTE9000
.
          IF        CLNO>55
            CALL      UND132                * Print an underline
            CALL      HEAD0000              * Print the report heading
            CALL      HDTE0000              * Print the date range heading
            CALL      UND132                * Print an underline
          ENDIF
.
          CLEAR     DIM24                   * SRF 19375 - Chngd DIM28 to DIM24
          STRIP     THTCODE                 * srf begin 
          APPEND    THTCODE,DIM24           
          APPEND    SP1,DIM24
          APPEND    THTCODED,DIM24
          APPEND    SP30,DIM24
          RESET     DIM24                  
          SETLPTR   ADOCTORD,20
          PRINT     *1,"|",DSPTHDTE,*12,"|",*13,THTSTTME,*18,"|",*19,THTEDTME:
                    *25,"|",*26,THTDURAN,*31,"|",*32,THTMULTO:
                    *39,"|",*40,ADOCTOR,*47,*+,ADOCTORD,*-,*67,"|",*68,URNO:
                    *76,"|",*77,ADMISS,*85,"|",*86,DSPADDTE:
                    *96,"|",*97,DSPDSDTE,*107,"|",*108,DIM24,*132,"|" 
.                                           * SRF 19375 - end
          ADD       ONE,CLNO
          CALL      PBDT0000                * Print a blank line
          ADD       THTDURAN,DURCOUNT
.
.         Accumulate number of patients per MBS/theatre
.
          PACK      KEY17,ADMISS,SP10
          CALL      RATEMPB
          COMPARE   ZERO,OVRCD
          GOTO      PDTE8000 IF EQUAL
.
          MOVE      ADMISS,XADMNO
          MOVE      SP10,XTHEAT
          MOVE      SP10,XADOCT
          CALL      WRTEMPB
          ADD       ONE,PATCOUNT
.
PDTE8000  DISPLAY   *P12:24,*V2LON,ADMISS;
          GOTO      PDTE1000
.
PDTE9000  CALL      UND132                  * Print an underline
          PRINT     *1,"Total Theatre Duration : ",DURCOUNT,*N:
                    *1,"Total Patients : ",PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete. ";
          CALL      HITENTER
PDTE9999  RETURN
+
.******************************************************************************
.*                                 HDTE0000               Called by: PDTE0000 *
.*                       Print The Date Range Heading                         *
.******************************************************************************
.                                           * srf 19375 Chnged Durat,mins align
HDTE0000  CALL      UND132                  * Print an underline
          PRINT     *1,"|Theatre",*12,"|Start",*18,"|End",*25,"|Durat":
                    *31,"|Multi",*39,"|Attending",*67,"|U/R",*76,"|Admin":
                    *85,"|Admin",*96,"|Disch",*107,"|MBS Item &",*132,"|"
          PRINT     *1,"|Date",*12,"|Time",*18,"|Time",*25,"|mins ":
                    *31,"|Theatre",*39,"|Doctor",*67,"|Number",*76,"|Number":
                    *85,"|Date",*96,"|Date",*107,"|Description",*132,"|"
          ADD       "2",CLNO
HDTE9999  RETURN
+
.******************************************************************************
.*                                 PBDT0000               Called by: PDTE0000 *
.*                  Print A Blank Line On Date Range Report                   *
.******************************************************************************
PBDT0000  IF        BLNKLINE=1
            PRINT     *1,"|",*12,"|",*18,"|",*25,"|",*31,"|",*39,"|",*67,"|":
                      *76,"|",*85,"|",*96,"|",*107,"|",*132,"|"
            ADD       ONE,CLNO
          ENDIF
PBDT9999  RETURN
+
.******************************************************************************
.*                                 PDOC0000               Called by: ML0000   *
.*                         Print The Doctors Report                           *
.******************************************************************************
PDOC0000  MOVE      ZERO,DOCCOUNT
          DISPLAY   *P1:24,*EL,"Printing : ";
          CALL      HEAD0000                * Print the main report heading
          PACK      KEY30,SP30
          CALL      RSTEMP4                 * Position on & read the 4th
          CALL      RKTEMP4                   tempfile
          BRANCH    OVRCD,PDOC9000
.
          CALL      HDOC0000                * Print the doctor heading
          PACK      KEY30,SP30
          CALL      RSTEMP4                 * Position on & read the 4th
PDOC1000  CALL      RKTEMP4                   tempfile
          BRANCH    OVRCD,PDOC9100
.
          MATCH     ADOCTOR,SAVDCCOD
          IF        !@EQUAL
            IF        CLNO>49
              CALL      TDOC0000            * Print the doctor tail
              CALL      HEAD0000            * Print the main report heading
              CALL      HDOC0000            * Print the doctor heading
            ELSE
              CALL      TDOC0000            * Print the doctor tail
              CALL      HDOC0000            * Print the doctor heading
            ENDIF
          ENDIF
.
          IF        CLNO>58
            CALL      UND132                * Print an underline
            CALL      HEAD0000              * Print the main report heading
            CALL      HDOC0000              * Print the doctor heading
          ENDIF
.
          CLEAR     DIM25
          STRIP     THTCODE
          APPEND    THTCODE,DIM25
          APPEND    SP1,DIM25
          APPEND    THTCODED,DIM25
          APPEND    SP30,DIM25
          RESET     DIM25
          MOVE      PATNAME,DIM16
          PRINT     *1,"|",DSPTHDTE,*12,"|",*13,THTSTTME,*18,"|",*19,THTEDTME:
                    *24,"|",*25,THTDURAN,*31,"|",*33,THTMULTO,*37,"|",*38,URNO:
                    *46,"|",*47,ADMISS,*55,"|",*56,DIM16,*74,"|",*75,DSPADDTE:
                    *85,"|",*86,DSPDSDTE,*96,"|",*97,DRG,*101,"|",*102,MDC:
                    *106,"|",*107,DIM25,*132,"|"
          ADD       ONE,CLNO
          CALL      PBDC0000                * Print a blank line
          ADD       THTDURAN,DURCOUNT
          ADD       THTDURAN,DURSUBCT
.
.         Accumulate the total number of patients
.
          PACK      KEY14,ADMISS,SP10
          CALL      RSTEMPA
          CALL      RKTEMPA
          BRANCH    OVRCD,PDOC8200
.
          MOVE      XADMNO,DIM8VISN
          MATCH     ADMISS,DIM8VISN
          GOTO      PDOC8400 IF EQUAL
.
PDOC8200  ADD       ONE,PATCOUNT
.
.         Accumulate number of patients per MBS/theatre
.
PDOC8400  PACK      KEY14,ADMISS,ADOCTOR
          CALL      RATEMPA
          COMPARE   ZERO,OVRCD
          GOTO      PDOC8600 IF EQUAL
.
          MOVE      ADMISS,XADMNO
          MOVE      SP10,XTHEAT
          MOVE      ADOCTOR,XADOCT
          CALL      WRTEMPA
          ADD       ONE,DOCCOUNT
.
PDOC8600  DISPLAY   *P12:24,*V2LON,ADMISS;
          GOTO      PDOC1000
.
PDOC9000  CALL      HDOC0000                * Print the doctor heading
          GOTO      PDOC9200
.
PDOC9100  CALL      TDOC0000                * Print the doctor tail
PDOC9200  PRINT     *1,"Total Theatre Duration : ",DURCOUNT," minutes",*N:
                    *1,"Total Patients : ",PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete. ";
          CALL      HITENTER
PDOC9999  RETURN
+
.******************************************************************************
.*                                 HDOC0000               Called by: PDOC0000 *
.*                       Print The Sub Doctor Heading                         *
.******************************************************************************
HDOC0000  MOVE      ADOCTOR,SAVDCCOD
          MATCH     SP9,ADOCTOR
          IF        @EQUAL
            PRINT     *1,"Attending Doctor : No Doctor"
          ELSE
            PRINT     *1,"Attending Doctor : ",ADOCTOR,SP2,ADOCTORD
          ENDIF
          ADD       ONE,CLNO
          CALL      UND132                  * Print an underline
.                                           * srf 19375 Chnged Durat,mins align
          PRINT     *1,"|Theatre",*12,"|Start",*18,"|End",*24,"|Durat":
                    *31,"|Multi",*37,"|U/R",*46,"|Admin",*55,"|Patient":
                    *74,"|Admin",*85,"|Disch",*96,"|DRG",*101,"|MDC":
                    *106,"|MBS Item &",*132,"|"
          PRINT     *1,"|Date",*12,"|Time",*18,"|Time",*24,"|mins ":
                    *31,"|Thea.",*37,"|Number",*46,"|Number",*55,"|Name":
                    *74,"|Date",*85,"|Date",*96,"|",*101,"|":
                    *106,"|Description",*132,"|"
          ADD       "2",CLNO
          CALL      UND132                  * Print an underline
HDOC9999  RETURN
+
.******************************************************************************
.*                                 TDOC0000               Called by: PDOC0000 *
.*                           Print The Doctors Tail                           *
.******************************************************************************
TDOC0000  CALL      UND132                  * Print an underline
          PRINT     *1,"Total Duration for Attending Doctor ",SAVDCCOD," is ":
                    DURSUBCT," minutes"
          MATCH     SP9,SAVDCCOD
          IF        @EQUAL
            PRINT     *1,"Total Number of Patient(s) With No Attending ":
                      "Doctor is : ",DOCCOUNT,*N
          ELSE
            PRINT     *1,"Total Number of Patient(s) for Attending Doctor ":
                      SAVDCCOD," is ",DOCCOUNT,*N
          ENDIF
          ADD       "3",CLNO
          MOVE      ZERO,DOCCOUNT
          MOVE      ZERO,DURSUBCT
TDOC9999  RETURN
+
.******************************************************************************
.*                                 PBDC0000               Called by: PDOC0000 *
.*                    Print A Blank Line On Doctor Report                     *
.******************************************************************************
PBDC0000  IF        BLNKLINE=1
            PRINT     *1,"|",*12,"|",*18,"|",*24,"|",*31,"|",*37,"|",*46,"|":
                      *55,"|",*74,"|",*85,"|",*96,"|",*101,"|",*106,"|":
                      *132,"|"
            ADD       ONE,CLNO
          ENDIF
PBDC9999  RETURN
+
.******************************************************************************
.*                                 OPEN0000               Called by: LODA0000 *
.*                             Open The Tempfile                   & LODB0000 *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE1
          APPEND    "isbuild ",CMDLINE1
          APPEND    FNAMEA,CMDLINE1
.
          BRANCH    COPTION,OPEN0300,OPEN0300
          APPEND    INDX1,CMDLINE1          * by attending doctor
          RESET     CMDLINE1
          EXECUTE   CMDLINE1,TASKID
          OPEN      TEMPA,FNAMEA
          GOTO      OPEN0400
.
OPEN0300  APPEND    INDX2,CMDLINE1          * by theatre or date
          RESET     CMDLINE1
          EXECUTE   CMDLINE1,TASKID         * Create the 1st tempfile
          OPEN      TEMPB,FNAMEA
.
OPEN0400  CLEAR     CMDLINE1
          APPEND    "isbuild ",CMDLINE1
          APPEND    FNAME,CMDLINE1
          BRANCH    COPTION,OPEN1000,OPEN0500,OPEN4000
.                           1st Ind,          4th Ind
.
OPEN0500  BRANCH    OPTION,OPEN2000,OPEN3000
.                          2nd Ind, 3rd Ind
.
OPEN1000  APPEND    FILELIN1,CMDLINE1
          RESET     CMDLINE1
          EXECUTE   CMDLINE1,TASKID         * Create the 1st tempfile
          OPEN      TEMP1,FNAME
          OPEN      TEMP5,FNAME
          GOTO      OPEN9999
.
OPEN2000  APPEND    FILELIN2,CMDLINE1
          RESET     CMDLINE1
          EXECUTE   CMDLINE1,TASKID         * Create the 2nd tempfile
          OPEN      TEMP2,FNAME
          OPEN      TEMP5,FNAME
          GOTO      OPEN9999
.
OPEN3000  APPEND    FILELIN3,CMDLINE1
          RESET     CMDLINE1
          EXECUTE   CMDLINE1,TASKID         * Create the 3rd tempfile
          OPEN      TEMP3,FNAME
          OPEN      TEMP5,FNAME
          GOTO      OPEN9999
.
OPEN4000  APPEND    FILELIN4,CMDLINE1
          RESET     CMDLINE1
          EXECUTE   CMDLINE1,TASKID         * Create the 4th tempfile
          OPEN      TEMP4,FNAME
          OPEN      TEMP5,FNAME
OPEN9999  RETURN
+
.******************************************************************************
.*                                 KILL0000               Called by: ML0000   *
.*                            Delete The Tempfile                  & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
          CLOSE     TEMP2
          CLOSE     TEMP3
          CLOSE     TEMP4
          CLOSE     TEMP5
          CLOSE     TEMPA
          CLOSE     TEMPB
.
          CLEAR     CMDLINE1
          APPEND    "iserase ",CMDLINE1
          APPEND    FNAMEA,CMDLINE1
          RESET     CMDLINE1
          EXECUTE   CMDLINE1,TASKID
.
          CLEAR     CMDLINE1
          APPEND    "iserase ",CMDLINE1
          APPEND    FNAME,CMDLINE1
          RESET     CMDLINE1
          EXECUTE   CMDLINE1,TASKID
.
KILL9999  RETURN
+
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
RATEMPA   RESET     KEY14
          MOVE      ZERO,OVRCD
          READ      TEMPA,KEY14;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMPA   RESET     KEY14
          READ      TEMPA,KEY14;;
          RETURN
.
RKTEMPA   MOVE      ZERO,OVRCD
          READKS    TEMPA;XADMNO,XADOCT,XTHEAT,XSPARE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMPA   RESET     KEY14
          MOVE      SP10,XSPARE
          WRITE     TEMPA,KEY14;XADMNO,XADOCT,XTHEAT,XSPARE
          RETURN
.
RATEMPB   RESET     KEY17
          MOVE      ZERO,OVRCD
          READ      TEMPB,KEY17;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMPB   RESET     KEY17
          READ      TEMPB,KEY17;;
          RETURN
.
RKTEMPB   MOVE      ZERO,OVRCD
          READKS    TEMPB;XADMNO,XADOCT,XTHEAT,XSPARE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMPB   RESET     KEY17
          MOVE      SP10,XSPARE
          WRITE     TEMPB,KEY17;XADMNO,XADOCT,XTHEAT,XSPARE
          RETURN
.
RSTEMP1   RESET     KEY39
          READ      TEMP1,KEY39;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;URNO,DADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,THTCODE,THTCODED,THTDATE,DSPTHDTE:
                          THTSTTME,THTEDTME,THTDURAN,THTMULTO,ADOCTOR,ADOCTORD:
                          DRG,MDC
          GOTO      OVERCOND IF OVER
          MOVE      DADMISS,ADMISS
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
RSTEMP2   RESET     KEY32
          READ      TEMP2,KEY32;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMP2;URNO,DADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,THTCODE,THTCODED,THTDATE,DSPTHDTE:
                          THTSTTME,THTEDTME,THTDURAN,THTMULTO,ADOCTOR,ADOCTORD:
                          DRG,MDC
          GOTO      OVERCOND IF OVER
          MOVE      DADMISS,ADMISS
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
RSTEMP3   RESET     KEY32
          READ      TEMP3,KEY32;;
          RETURN
.
RKTEMP3   MOVE      ZERO,OVRCD
          READKS    TEMP3;URNO,DADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,THTCODE,THTCODED,THTDATE,DSPTHDTE:
                          THTSTTME,THTEDTME,THTDURAN,THTMULTO,ADOCTOR,ADOCTORD:
                          DRG,MDC
          GOTO      OVERCOND IF OVER
          MOVE      DADMISS,ADMISS
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
RSTEMP4   RESET     KEY30
          READ      TEMP4,KEY30;;
          RETURN
.
RKTEMP4   MOVE      ZERO,OVRCD
          READKS    TEMP4;URNO,DADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,THTCODE,THTCODED,THTDATE,DSPTHDTE:
                          THTSTTME,THTEDTME,THTDURAN,THTMULTO,ADOCTOR,ADOCTORD:
                          DRG,MDC
          GOTO      OVERCOND IF OVER
          MOVE      DADMISS,ADMISS
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
RSTEMP5   RESET     KEY24
          READ      TEMP5,KEY24;;
          RETURN
.
RKTEMP5   MOVE      ZERO,OVRCD
          READKS    TEMP5;URNO,DADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,THTCODE,THTCODED,THTDATE,DSPTHDTE:
                          THTSTTME,THTEDTME,THTDURAN,THTMULTO,ADOCTOR,ADOCTORD:
                          DRG,MDC
          GOTO      OVERCOND IF OVER
          MOVE      DADMISS,ADMISS
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
WRTEMP5   MOVE      ADMISS,DADMISS
          MOVE      TMPCOUNT,DTMPCONT
          RESET     KEY24
          WRITE     TEMP5,KEY24;URNO,DADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                                DSCDATE,DSPDSDTE,THTCODE,THTCODED,THTDATE:
                                DSPTHDTE,THTSTTME,THTEDTME,THTDURAN,THTMULTO:
                                ADOCTOR,ADOCTORD,DRG,MDC
          RETURN
.
UPTEMP5   MOVE      ADMISS,DADMISS
          MOVE      TMPCOUNT,DTMPCONT
          UPDATE    TEMP5;URNO,DADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,THTCODE,THTCODED,THTDATE,DSPTHDTE:
                          THTSTTME,THTEDTME,THTDURAN,THTMULTO,ADOCTOR,ADOCTORD:
                          DRG,MDC
          RETURN
.
.==============================================================================
.
          INC       STD001IO
.
          INC       PATDAYIO/INC            * Day file
          INC       PATDO1IO/INC            * Doctor file
          INC       PATDSCIO/INC            * Discharge file
          INC       PATITMIO/INC            * Item file
          INC       PATMA1IO/INC            * Patient master file
          INC       HL7BMTIO/INC
.         INC       HL7BMTMA
.         INC       NXTMESID
          INC       PATMI1IO/INC            * Patient admission file
          INC       PMSVX1IO/INC            * Patient admission file
          INC       OPRDEAIO/INC            
          INC       OPRSRGIO/INC                
          INC       PATMMBIO/INC            * MBS file
.
          INC       PATITMRD
          INC       KEYINDOC                * Doctor Keyin
          INC       KEYINMBS                * MBS Keyin
          INC       PATCKEXC                * Check if exclusion list item
.         INC       PATITMDS                * MBS ?   
          INC       PATDOCDS                * Doctor ? 
          INC       PATDSITM
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
+
.


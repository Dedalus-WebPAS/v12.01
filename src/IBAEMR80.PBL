. *****************************************************************************
. *                                                                           *
. *     PROGRAM NAME:     IBAEMR80                                            *
. *                                                                           *
. *     FUNCTION:         EMR STATISTICS                                      *
. *                                                                           *
. *     DATE WRITTEN:     16/04/09                                            *
. *                                                                           *
. *     AUTHOR:           SAROEUN SOEUR                                       *
. *                                                                           *
. *     MODS :                                                                *
. *     V9.12.01  20/01/2010 Ebon Clements CAR 213712                         *
. *               Collect statistics on blank codes FILL7000                  *
. *****************************************************************************
.------------------------------------------------------------------------------
.       file definition includes
.------------------------------------------------------------------------------
.
          INC       STD001FD
.
          INC       PATCOMM/INC
          INC       EMRCONFD/INC
          INC       EMRVISFD/INC
          INC       EMRSTAFD/INC
          INC       PATDRGFD/INC
          INC       PATCONFD/INC
+
.------------------------------------------------------------------------------
.         local variables
.------------------------------------------------------------------------------
.
CURRPER   FORM      1                * current period
.
CYRNMPRD  DIM       6                * current yr number period
DIM6      DIM       6                * temp variable
.
DISFRDTE  DIM       10               * display from date
DISTODTE  DIM       10               * display to date
.
FROMDATE  DIM       8                * from date
.
HSTANDM   FORM      1
.
MONTH     DIM       9
MONTH2    DIM       9
.
TODATE    DIM       8                * to date
.
WDATE     DIM       6
WDATE2    DIM       6
+
.------------------------------------------------------------------------------
.         constant variables
.------------------------------------------------------------------------------
.
CODECL    INIT      "CL"      * claim code
CODEA     INIT      "A "      
CODEU1    INIT      "U1"      * insurance
CODES     INIT      "S "
CODEES    INIT      "ES"
CODEEA    INIT      "EA"
CODEED    INIT      "ED"
CODEEW    INIT      "EW"
CODEA1    INIT      "A1"
CODEA2    INIT      "A2"
CODEA3    INIT      "A3"
CODEA4    INIT      "A4"
CODEA5    INIT      "A5"
CODEA6    INIT      "A6"
CODEA7    INIT      "A7"
CODEEP    INIT      "EP"
+
.------------------------------------------------------------------------------
.         program variable
.------------------------------------------------------------------------------
.
PRGID     INIT      "IBAEMR80"
PRGNAM    INIT      "RECALCULATE USAGE MONTHS"
VERSION   INIT      "V12.01.00"
+
.------------------------------------------------------------------------------
.         MAIN0000
.------------------------------------------------------------------------------
.
MAIN0000  CALL      INIT0000            * open files
.
MAIN1000  CALL      MENU0000            * display options
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      GETD0000            * get date range
.
          CALL      PROC0000            * proceed?
          BRANCH    EXIT,MAIN2000:      * No - get date range
                         MAIN1000       * Cancel - display menu option
.
          CALL      RSET0000            * reset stat table within user's date
.                                         range
.
          CALL      GEMR0000            * populate stat table with emrvisf
.                                         data
.
          CALL      FNSH0000            * display complete msg
          GOTO      MAIN1000            * display menu
.
MAIN9999  CHAIN     PGM
+
.------------------------------------------------------------------------------
.         INIT0000
.         open files
.------------------------------------------------------------------------------
.
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening emrstaaf";
          OPEN      EMRSTAA1,"emrstaaf"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      EMRVISA4,"emrvisaf"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,FIFTY9;*27,CPERMTH
          READ      CONTROLF,THIRTY;*132,HSTANDM
.
INIT9999  RETURN
+
.------------------------------------------------------------------------------
.         MENU0000
.
.         pre  condition: N/A
.
.         post condition: EXIT - sets EXIT flag to either
.                                a ONE to exit or ZERO to continue program 
.                                execution
.
.         called by     : MAIN0000
.------------------------------------------------------------------------------
.
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = EXIT":
                    *P1:5,*V2LON,ONE,*HOFF:
                    " = Re-calculate usage figures for a month"
.
          KEYIN     *P1:8,*EL,"Please select : ",*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION            * user wants to exit?
          GOTO      MENU9100 IF EQUAL
.  
          COMPARE   ONE,OPTION             * user wants to run stat
          GOTO      MENU9000 IF EQUAL
.
          DISPLAY   *P35:8,*B,*V2LON,"** Invalid selection **",*W2;
.
          GOTO      MENU0000
.
MENU9000  MOVE      ZERO,EXIT
          GOTO      MENU9999
.
MENU9100  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.------------------------------------------------------------------------------
.         GETD0000
.
.         pre  condition: N/A
.
.         post condition: FROMDATE - a valid from date
.                         TODATE   - a valid to date
.                         WDATE    - a valid financial period from date
.                         WDATE2   - a valid financial period to date
.                         CYRNMPRD - sets current year number period
.
.         description   : get a valid date range from the user
.
.         called by     : MAIN0000
.------------------------------------------------------------------------------
.
GETD0000  PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE           
          CALL      GPERD
.
          MOVE      SP6,CYRNMPRD
          PACK      CYRNMPRD,DRGYR,DRGNUM
          REP       " 0",CYRNMPRD
.
          DISPLAY   *P1:14,*EL,"From ",CPERMTH,"/Year: ",UNDLN2,SLASH,UNDLN4;
.
          UNPACK    SP4,CYEAR,CMON
          MOVE      CCC,CCENT
          MOVE      "19",CCOL
          MOVE      TEN4,CVERT
.
          CALL      KEYPER
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,GETD1000 
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISFRDTE
          REP       " 0",DISFRDTE
          MOVE      DRGMDSC,MONTH
          DISPLAY   *P30:14,*V2LON,MONTH,*P40:14,DISFRDTE
.
          PACK      WDATE,DRGYR,DRGNUM
          REP       " 0",WDATE
.
          MOVE      DRGFDTE,FROMDATE
          REP       " 0",FROMDATE
.
          DISPLAY   *P1:16,*EF,"To ",CPERMTH,"/Year: ",UNDLN2,SLASH,UNDLN4
.
          MOVE      "19",CCOL
          MOVE      TEN6,CVERT
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYPER
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,GETD1000
.
          MOVE      DRGMDSC,MONTH2
          DISPLAY   *P30:16,*V2LON,MONTH2
.
          PACK      WDATE2,DRGYR,DRGNUM
          REP       " 0",WDATE2
.
          MATCH     CYRNMPRD,WDATE2         * same period, set to todays date
          IF        @EQUAL
            MOVE      CCC,CC
            MOVE      CYY,YY
            MOVE      CMM,MM
            MOVE      CDD,DD
            CALL      DMYCON
            SUB       ONE,JULDAY
            MOVE      JULDAY,JWKDAY
            MOVE      JULYR,JWKYER
            MOVE      JULCC,JWKCC
            CALL      JULCON
            PACK      DRGTDTE,CC,YY,MM,DD
          ENDIF
.
          MOVE      DRGTDTE,TODATE
          REP       " 0",TODATE
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISTODTE
          REP       " 0",DISTODTE
          DISPLAY   *P40:16,*V2LON,DISTODTE
.
          MATCH     FROMDATE,TODATE
          GOTO      GETD2000 IF LESS
.
          GOTO      GETD9999
.
GETD1000  DISPLAY   *P40:24,*V2LON,*B,"** Invalid ",CPERMTH,"/Year **",*W2;
          GOTO      GETD0000
.
GETD2000  DISPLAY   *P1:24,*EL,*B,"The to date must be greater than the ":
                    "from date. ";
.
          CALL      HITENTER
          GOTO      GETD0000 
.
GETD9999  RETURN
+
.------------------------------------------------------------------------------
.         PROC0000
.         pre  condition: N/A
.
.         post condition: CURRPER - set current period to 1 - yes
.                                                         0 - no
.                         EXIT    - set EXIT flag 0 - continue
.                                                 1 - no
.                                                 2 - cancel
.                        
.         called by     : MAIN0000
.------------------------------------------------------------------------------
.
PROC0000  CALL      CONTQST
          BRANCH    CEXIT,PROC1000:             * Yes
                          PROC9100:             * No 
                          PROC9200              * Cancel
.
PROC1000  MOVE      ZERO,CURRPER                * current period = no
.
          MATCH     WDATE2,CYRNMPRD
          IF        @EQUAL | @LESS        
            MOVE      ONE,CURRPER               * current period = yes
          ENDIF
.
          DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"** Re-setting usage records **",*HOFF,*W2;
.
PROC9000  MOVE      ZERO,EXIT
          GOTO      PROC9999
.
PROC9100  MOVE      ONE,EXIT
          GOTO      PROC9999
.
PROC9200  MOVE      TWO,EXIT
          GOTO      PROC9999
.
PROC9999  RETURN
+
.------------------------------------------------------------------------------
.         RSET0000
.
.         pre  condition: WDATE and WDATE2 has a valid date range
.                         stat file open
.
.         post condition: stat table within date range gets its EMSTNUMB
.                         reset to zero 
.
.         discription   : finds all dates within WDATE and WDATE2 range,
.                         resets the EMSTNUMB to zero in the stat table for
.                         each date 
.       
.         called by     : MAIN0000
.------------------------------------------------------------------------------
.
RSET0000  DISPLAY   *P1:24,*EL,"Statistics usage : ";
          PACK      KEY13,SP10,SP3
          CALL      RSEMSTA1
RSET1000  CALL      RKEMSTA1
          BRANCH    OVRCD,RSET9999
.
          PACK      DIM6,SP6
.
          MOVE      EMSTDATE,DIM6
          MATCH     WDATE,DIM6
          GOTO      RSET1000 IF LESS
.
          MATCH     DIM6,WDATE2
          GOTO      RSET1000 IF LESS
.
          MOVE      ZERO,EMSTNUMB       * within range, set total num to zero
.                                         in stat table
          CALL      UPEMSTA1
.
          DISPLAY   *P20:24,*V2LON,EMSTCATA,SP5,EMSTCODE;
.
          GOTO      RSET1000
.
RSET9999  RETURN
+
.-------------------------------------------------------------------------------
.         GEMR0000
.
.         pre  condition: emrvisaf opened
.                         date range valid
.
.         post condition: stat table populate with emrvis data
.                          
.         discription   : populates stat table
.                          
.         called by     : MAIN0000
.-------------------------------------------------------------------------------
.
GEMR0000  DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"** Re-calculating **",*HOFF,*W2;
.
          PACK      KEY24,FROMDATE,SP20
          CALL      RSEMVIS4
.
          DISPLAY   *P1:24,*EL,"Date : ",*P40:24,"Number : ";
.
GEMR1000  CALL      RKEMVIS4
          BRANCH    OVRCD,GEMR9999
.
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P8:24,*V2LON,CPCDATE:
                    *P48:24,EMVIADMN;
.
          MATCH     SP8,EMVITIME          *check emr patient with arrival time
          GOTO      GEMR1000 IF EQUAL
.
          COMPARE   FOUR,EMVISTAT          *check cancel status and ignore record
          GOTO      GEMR1000 IF EQUAL
.         
          REP       " 0",EMVIDATE
          MATCH     EMVIDATE,TODATE       * todate < emvidate
          GOTO      GEMR2000 IF LESS
.
          MATCH     EMVIDATE,TODATE
          GOTO      GEMR3000 IF NOT LESS  * todate !< emvidate
.
          GOTO      GEMR9999
.
GEMR2000  COMPARE   ONE,CURRPER
          GOTO      GEMR9999 IF EQUAL
.
GEMR3000  CALL      FILL0000              * populate the stat table
          BRANCH    EXIT,GEMR9999
          GOTO      GEMR1000
.
GEMR9999  RETURN 
+
.------------------------------------------------------------------------------
.         FILL0000
.
.         pre  condition: emrvisaf opened
.                         date range valid
.
.         post condition: stat table populate with emrvis data
.
.         discription   : populates stat table
.
.         called by     : GEMR0000
.------------------------------------------------------------------------------
.
FILL0000  UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P8:24,*V2LON,CPCDATE:
                    *P48:24,EMVIADMN
.
          MOVE      EMVIADMN,KEY8
          CALL      RDEMVIS1
          BRANCH    OVRCD,FILL9999
.
FILL0500  MOVE      ZERO,FORM2
FILL1000  ADD       ONE,FORM2
.
FILL4000  COMPARE   TEN6,FORM2
          GOTO      FILL9999 IF EQUAL
.
.         iterate through catagory 
.
FILL5000  LOAD      EMSTCATA,FORM2,CODECL,CODEA,CODEU1,CODES,CODEES,CODEEA:
                                   CODEA1,CODEA2,CODEA3,CODEA4,CODEA5:
                                   CODEA6,CODEA7,CODEEW,CODEED,CODEEP
.
.         iterate through emr patient code 
.
          LOAD      EMSTCODE,FORM2,EMVICOMP,EMVICLAS,EMVIINSR,EMVISRCE,EMVISITN:
                                   EMVIMODE,EMVIUC01,EMVIUC02,EMVIUC03,EMVIUC04:
                                   EMVIUC05,EMVIUC06,EMVIUC07,EMVIWAIT,EMVIDSTA:
                                   EMVIPREV
.
          MOVE      EMVIDATE,CPTDATE                * covert to financial period date
          CALL      GPERD
          BRANCH    EXIT,FILL6000
.
          GOTO      FILL7000
.
FILL6000  UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Error: ",CPCDATE:
                    " not found in period file. "
. 
          CALL      HITENTER
          GOTO      FILL9100
.
FILL7000  PACK      KEY13,EMSTCATA,EMSTCODE,DRGYR,DRGNUM,SP2      
          CALL      RDEMSTA1                                    * read file
          BRANCH    OVRCD,FILL8000
.
          ADD       ONE,EMSTNUMB                                * increment emstnumb
          CALL      UPEMSTA1                                    
          GOTO      FILL1000
.
FILL8000  PACK      EMSTDATE,DRGYR,DRGNUM,SP2                   * write new date to file
          MOVE      ONE,EMSTNUMB
          MOVE      SP30,EMSTSPAR
          CALL      WREMSTA1
          GOTO      FILL1000
.
FILL9100  MOVE      ONE,EXIT
          GOTO      FILL9999
.
FILL9999  RETURN
+
.------------------------------------------------------------------------------
.       FNSH0000
.       displays compete
.------------------------------------------------------------------------------
.
FNSH0000  DISPLAY   *P1:24,*EL,*V2LON,"** Completed **",*W3;
.
FNSH9999  RETURN
+
.------------------------------------------------------------------------------
.         IO includes
.------------------------------------------------------------------------------
          INC       EMRVISIO/INC
          INC       PATDRGIO/INC
          INC       EMRSTAIO/INC
          INC       STD001IO
          INC       PATCOMN3







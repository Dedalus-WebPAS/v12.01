.***************************************************************************
.* System    :   HIC Claims                                                *
.* Program   :   IBAHIC13                                                  *
.* Desc      :   Analysis of Services                                      *
.***************************************************************************
.* Date      :   27/06/2005                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will summarise each item for each practice   *
.*               or Clinic ID                                              *
.*                                                                         *
.***************************************************************************
.* V11.02.01 09/02/2022  Thanh T        TSK 0905641                        *
.*           Recompiled as OUTMA1FD changes                                *
.* V10.11.01 26/07/2017  Ebon Clements  TSK 0268970                        *
.*           Change to use OUTCLIFD index 1 instead of index 2             *
.* V10.04.01 17/04/2014  J.Tan          CAR 261630                         *
.*                       Removed port number from temp file name           *
.* V9.05.01  10/03/2006  J.Tan         CAR 96067                           *
.*                       Mods maximum number of lines to print             *
.* V9.05.B02 18/11/2005  J.Tan          CAR 77215                          *
.*                       Mods to include all batches                       *
.* V9.05.B01 17/11/2005  J.Tan          CAR 77215                          *
.*                       Mods to Batch status                              *
.* V9.04.01  01/08/2005  J.Tan          CAR 69062                          *
.*                       Changed claim index 7 to include voucher number   *
.* V9.04.00  27/06/2005  J.Tan          CAR 56701                          *
.*                       Created program                                   *
.***************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       HICBTCFD/INC
          INC       HICCLMFD/INC
          INC       HICCITFD/INC
          INC       PMSHCPFD/INC
          INC       PRIPRAFD/INC
          INC       PRIDOCFD/INC
          INC       OUTSITFD/INC
          INC       OUTCLIFD/INC
          INC       OUTMA1FD/INC        * Session Master file
          INC       PATCODFD/INC
          INC       WEBSECFD/INC
          INC       PRIALSFD/INC
          INC       PRISECFD/INC
          INC       PRIITMFD/INC
.
.         Temporary File Definition
.         -------------------------
.
.         Filename : hic13axx.dat          (xx = port id)
.
HICTEMP1  IFILE SQL, FIXED=30, KEY="UC13-21,1-6"
HICTEMP2  IFILE SQL, FIXED=30, KEY="UC13-21,7-12"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TEMPCLID  DIM       6      6        1     Clinic Id
TEMPMPRA  DIM       6      6        7     Medical Practice
TEMPITMN  DIM       9      9        13    Item Code
TEMPAMNT  FORM      12.2   8        22    Claim Amount
.End of Record                      30
.
.         Temporary File Variables
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
TEMPFILE  DIM       8
UKEY1     INIT      " 30 UC13-21,1-6"
UKEY2     INIT      " 30 UC13-21,7-12"
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ALACCESS  FORM      1
CLINFROM  DIM       6
CLINICTO  DIM       6
CMDLINE   DIM       127
DISPNAME  DIM       60
DOCFNAME  DIM       50
FROMDATE  DIM       8
FRITMN    DIM       9
FILTRHCP  DIM       10
FILTPROV  DIM       10
FMTSNAME  DIM       35
FMTGNAME  DIM       35
FMTTITLE  DIM       10
HEADING0  DIM       30
HEADING1  DIM       30
HEADING2  DIM       30
HEADING3  DIM       40
HEADING5  DIM       30
HEADING6  DIM       30    
LOOPFLAG  FORM      1
PRACTIFR  DIM       6
PRACTITO  DIM       6
SORTFLAG  FORM      1
SAVEVAR   DIM       10
STATUS    DIM       2
SITE      DIM       6
TODATE    DIM       8
TOTAMNT   FORM      12.2
TOITMN    DIM       9
WEBUSRID  DIM       10
.
PRGID     INIT      "IBAHIC13"
PRGNAM    INIT      "Analysis of Services"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      PRCF0000               * keyin practice from
          BRANCH    EXIT,ML0100
.
          CALL      PRCT0000               * keyin practice to
          BRANCH    EXIT,ML0100
.
          CALL      FCLN0000               * keyin clinic from
          BRANCH    EXIT,ML0100
.
          CALL      TCLN0000               * keyin clinic to
          BRANCH    EXIT,ML0100
.
          CALL      KPRV0000               * keyin provider
          BRANCH    EXIT,ML0100
.
          CALL      DRNG0000               * keyin date range
          CALL      ITMN0000               * keyin item range
.
          CALL      WEBU0000               * keyin user
          BRANCH    EXIT,ML0100
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CREA0000               * Create tempfile
.
          CALL      PROC0000               * process
          CALL      PRIN0000               * Print data
          CALL      ENDP0000               * End of report
.
          CALL      KILL0000               * Delete tempfile
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          CALL      IBACLOCK
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
          OPEN      HICBTCA2,"hicbtcaf"       * batch file
          OPEN      HICBTCA3,"hicbtcaf"       * batch file
          OPEN      HICBTCA4,"hicbtcaf"       * batch file
          OPEN      HICBTCA6,"hicbtcaf"       * batch file
          OPEN      HICCLMA7,"hicclmaf"       * claim file
          OPEN      HICCITA1,"hiccitaf"       * item file
          OPEN      PMSHCPA1,"pmshcpaf"       * HCP file
          OPEN      PRIPRAC1,"pripracf"       * private practice
          OPEN      PRIDOCT1,"pridoctf"       * practice doctors
          OPEN      OUTSITA1,"outsitaf"       * outpatient site file
          OPEN      PATCODE1,"patcodes"       * codes file
          OPEN      WEBSECA1,"websecaf"       * web user id
          OPEN      PRIALSA1,"prialsaf"       * all practice security
          OPEN      PRISECA1,"prisecaf"       * practice security
          OPEN      PRIITEM1,"priitemf"       * practice MBS/AMA item file
.
          MOVE      "out",OSTFILE
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,INIT9000
.
          PACK      CFNAME WITH OSTFILE,FILCLIA1        * OUTCLIFD
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILMA1A1
          CLOSE     OUTMA1A1
          CALL      OPOTMA11                * Open the file
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9000  DISPLAY   *P1:24,*EL,*B,"Site Code not on file. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      INIT9999
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
CREA0000  MOVE      ONE,SORTFLAG         * sort by clinic id
          MATCH     SP70,CLINFROM
          GOTO      CREA2000 IF NOT EQUAL
          MOVE      TWO,SORTFLAG         * default to sort by medical practice
.
CREA2000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          IF        SORTFLAG=1
            PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY1
            EXECUTE   CMDLINE,TASKID               * create temporary index file
            OPEN      HICTEMP1,TEMPFILE            * open temp index file
          ELSE
            PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY2
            EXECUTE   CMDLINE,TASKID               * create temporary index file
            OPEN      HICTEMP2,TEMPFILE            * open temp index file
          ENDIF
.
          MOVE      ZERO,ALACCESS           * Set all access to no
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDPRALS1
          IF        OVRCD=0
            MOVE      ONE,ALACCESS
          ENDIF
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     HICTEMP1                     * close file
          CLOSE     HICTEMP2                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY43,FROMDATE,SP70
          CALL      RSHCBTC4
PROC2100  CALL      RKHCBTC4
          BRANCH    OVRCD,PROC9999
.
          MATCH     SP70,TODATE
          IF        !@EQUAL
            MATCH     HCBTDATE,TODATE
            GOTO      PROC9999 IF LESS
          ENDIF
.
          MATCH     IDDD,HCBTSITE
          GOTO      PROC2100 IF NOT EQUAL
.
          MATCH     SP70,FILTPROV
          IF        !@EQUAL
            MATCH     FILTPROV,HCBTPSRV       * Same provider
            GOTO      PROC2100 IF NOT EQUAL
          ENDIF
.
.         Checking range of clinic id
.
          MATCH     SP70,CLINFROM
          IF        !@EQUAL
            MATCH     CLINFROM,HCBTCLID
            GOTO      PROC2100 IF LESS
          ENDIF
.
          MATCH     SP70,CLINICTO
          IF        !@EQUAL
            MATCH     HCBTCLID,CLINICTO
            GOTO      PROC2100 IF LESS
          ENDIF
.
.         Checking range of medical practice
.
          MATCH     SP70,PRACTIFR
          IF        !@EQUAL
            MATCH     PRACTIFR,HCBTMPRA
            GOTO      PROC2100 IF LESS
          ENDIF
.
          MATCH     SP70,PRACTITO
          IF        !@EQUAL
            MATCH     HCBTMPRA,PRACTITO
            GOTO      PROC2100 IF LESS
          ENDIF
.
          BRANCH    ALACCESS,PROC3000       * Access to all practices 
.
          PACK      KEY16,HCBTMPRA,WEBUSRID,SP70
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROC2100          * No access to this practice
.
.         Loop through claim file for the batch file
.
PROC3000  PACK      KEY15,HCBTBTCH,SP70
          CALL      RSHCCLM7
PROC3200  CALL      RKHCCLM7
          BRANCH    OVRCD,PROC2100
          MATCH     HCBTBTCH,HCCLBTCH       * Same batch number?
          GOTO      PROC2100 IF NOT EQUAL
.
.         Loop through Item file for the claim number
.
          PACK      KEY18,HCCLCLMN,HCCLVISN,SP70
          CALL      RSHCCIT1
PROC3400  CALL      RKHCCIT1
          BRANCH    OVRCD,PROC3200
.
          MATCH     HCCLCLMN,HCCICLMN       * Same claim number?
          GOTO      PROC3200 IF NOT EQUAL
.
          MATCH     HCCLVISN,HCCIVISN       * Same visit number?
          GOTO      PROC3200 IF NOT EQUAL
.
.         Check range of items
.
          MATCH     SP70,FRITMN
          IF        !@EQUAL
            MATCH     FRITMN,HCCIITMN
            GOTO      PROC3400 IF LESS
          ENDIF
          MATCH     SP70,TOITMN
          IF        !@EQUAL
            MATCH     HCCIITMN,TOITMN
            GOTO      PROC3400 IF LESS
          ENDIF
.
          BRANCH    SORTFLAG,PROC4000
          PACK      KEY15,HCCIITMN,HCBTMPRA
          CALL      RDTEMP2
          IF        OVRCD=1
            MOVE      HCBTMPRA,TEMPMPRA
            MOVE      HCCIITMN,TEMPITMN
            MOVE      SP70,TEMPCLID
            MOVE      HCCICAMT,TEMPAMNT
            CALL      WRTEMP2
          ELSE
            ADD       HCCICAMT,TEMPAMNT
            CALL      UPTEMP2
          ENDIF
          GOTO      PROC3400
.
PROC4000  PACK      KEY15,HCCIITMN,HCBTCLID
          CALL      RDTEMP1
          IF        OVRCD=1
            MOVE      HCBTCLID,TEMPCLID
            MOVE      HCCIITMN,TEMPITMN
            MOVE      SP70,TEMPMPRA
            MOVE      HCCICAMT,TEMPAMNT
            CALL      WRTEMP1
          ELSE
            ADD       HCCICAMT,TEMPAMNT
            CALL      UPTEMP1
          ENDIF
          GOTO      PROC3400
.
PROC9999  RETURN
+
.**********************************************************************
.*                         PRIN0000                                   *
.*                        Print Data                                  *
.**********************************************************************
PRIN0000  MOVE      ZERO,CPAGENO
          MOVE      SP70,SAVEVAR
          MOVE      ZERO,TOTAMNT
          MOVE      "60",CLNO
.
          PACK      KEY15,SP70
          CALL      RSTEMP
PRIN1000  CALL      RKTEMP
          BRANCH    OVRCD,PRIN9000
.
          MATCH     SP70,SAVEVAR
          GOTO      PRIN1200 IF EQUAL            * first record
          CALL      CVAR0000                     * check if sort field changed
          BRANCH    EXIT,PRIN1100                * changed
.
          GOTO      PRIN1300
.
PRIN1100  CALL      UND80
          PRINT     *40,"| TOTAL",*57,"|",TOTAMNT,*80,"|":
                    *N,*40,"*----------------",*57,"*----------------------*"
          ADD       TWO,CLNO
          MOVE      ZERO,TOTAMNT
          MOVE      ONE,CNOUNDLN                 * no underline
.
PRIN1200  IF        CLNO > 48
            CALL      HEAD0000 
          ELSE
            CALL      PRHD0000
          ENDIF
          MOVE      ZERO,CNOUNDLN
          CALL      SVAR0000                     * save sort variable
.
PRIN1300  COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    SORTFLAG,PRIN2000
          MOVE      SP70,PRPRDESC
          PACK      KEY6,TEMPMPRA,SP70
          CALL      RDPRPR1
          PRINT     *1,"| ",PRPRDESC:
                    *57,"| ",TEMPAMNT,*80,"|"
          GOTO      PRIN3000
.
PRIN2000  CALL      CLID0000              * get name of clinic id
.
          PRINT     *1,"| ",OCADESC:
                    *57,"|",TEMPAMNT,*80,"|"
.
PRIN3000  ADD       TEMPAMNT,TOTAMNT
          ADD       ONE,CLNO
          GOTO      PRIN1000              * read next record
.
PRIN9000  COMPARE   ZERO,CPAGENO
          GOTO      PRIN9200 IF NOT EQUAL
          CALL      HEAD0000
.
PRIN9200  CALL      UND80                   * finish off report
          PRINT     *40,"| TOTAL",*57,"|",TOTAMNT,*80,"|":
                    *N,*40,"*----------------",*57,"*----------------------*"
.
PRIN9999  RETURN
+
.*********************************************************************
.         Save sort variable
.*********************************************************************
SVAR0000  MOVE      SP70,SAVEVAR
          PACK      SAVEVAR,TEMPITMN,SP70
SVAR9999  RETURN
+
.*********************************************************************
.         check if sort variable change
.*********************************************************************
CVAR0000  MOVE      ZERO,EXIT
.
          MATCH     SAVEVAR,TEMPITMN     * check item code
          GOTO      CVAR9999 IF EQUAL
.
CVAR9000  MOVE      ONE,EXIT
CVAR9999  RETURN

.*********************************************************************
.         Get clinic id
.*********************************************************************
CLID0000  MOVE      SP70,OCADESC
          PACK      KEY20,IDDD,TEMPCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                *To get Clinic ID Description
          BRANCH    OVRCD,CLID9000
.
          MATCH     IDDD,OCASITE
          GOTO      CLID9000 IF NOT EQUAL
.
          MATCH     TEMPCLID,OCACLIN
          GOTO      CLID9000 IF NOT EQUAL
.
          MATCH     SP70,OCADESC
          GOTO      CLID9999 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,CLID9000
.
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OCADESC
          GOTO      CLID9999
.
CLID9000  MOVE      SP70,OCADESC
CLID9999  RETURN
+
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD80
.
          PRINT     *1,"Medical Practice From : ",HEADING5," To : ",HEADING6
          PRINT     *1,"Clinic From           : ",HEADING1," To : ",HEADING2
          PRINT     *1,"Provider Number       : ",HEADING0
.
          MOVE      SP70,CPCDATE
          MATCH     SP70,FROMDATE
          IF        !@EQUAL
            UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
          PRINT     *1,"Batch Date From       : ",CPCDATE;
.
          MOVE      SP70,CPCDATE
          MATCH     SP70,TODATE
          IF        !@EQUAL
            UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
          PRINT     "            To : ",CPCDATE
.
          PRINT     *1,"Item From             : ",FRITMN," To : ",TOITMN
.
          ADD       FOUR,CLNO
          CALL      PRHD0000                      * print item heading
.
HEAD9999  RETURN
+
.*********************************************************************
.         print heading
.*********************************************************************
PRHD0000  UNPACK    SP70,PRITDESC,KEY30
          PACK      KEY22,SP1,ZERO,TEMPITMN,SP3,Z70
          CALL      RSPRIT1
          CALL      RPPRIT1
          BRANCH    OVRCD,PRHD1000
          PACK      KEY14,PRITFLAG,PRITITMN,PRITSUBN,SP70
          MATCH     KEY14,KEY22
          GOTO      PRHD1000 IF NOT EQUAL
          MOVE      PRITDESC,KEY30
.
PRHD1000  PRINT     *N,*1,"Item : ",TEMPITMN," ",KEY30
          CALL      UND80
          IF        SORTFLAG=1
            PRINT     *1,"| Clinic ID":
                      *57,"|          Amount",*80,"|"
          ELSE
            PRINT     *1,"| Medical Practice ":
                      *57,"|          Amount",*80,"|"
          ENDIF
          CALL      UND80
          ADD       THREE,CLNO
PRHD9999  RETURN
+
.**********************************************************************
.*                           ENDP0000                                 *
.*                      Print the end of the report                   *
.**********************************************************************
ENDP0000  
          PRINT     *N,*50,"*** End of Report ***"
ENDP9999  RETURN
+
.******************************************************************************
.*                                   FCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code From                     *
.******************************************************************************
FCLN0000  MOVE      SP70,CLINFROM
          KEYIN     *P1:9,*EL,"Clinic Id From : ",*V2LON,CLINFROM
.
          PACK      CLINFROM,CLINFROM,SP70
          MATCH     SP70,CLINFROM
          GOTO      FCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINFROM,SP70
          CALL      RDSCLIA1
FCLN1000  CALL      RDKCLIA1
          BRANCH    OVRCD,FCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINFROM     * Must be in the Same clinic
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      FCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      FCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
FCLN5000  MOVE      OCADESC,HEADING1
          DISPLAY   *P25:9,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN6000  MOVE      "Start",HEADING1
          DISPLAY   *P25:9,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
FCLN9999  RETURN
+
.******************************************************************************
.*                                   TCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code To                       *
.******************************************************************************
TCLN0000  MOVE      SP70,CLINICTO
          KEYIN     *P1:10,*EL,"Clinic Id To   : ",*V2LON,CLINICTO
.
          PACK      CLINICTO,CLINICTO,SP70
          MATCH     SP70,CLINICTO
          GOTO      TCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINICTO,SP70
          CALL      RDSCLIA1
TCLN1000  CALL      RDKCLIA1
          BRANCH    OVRCD,TCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINICTO     * Must be in the Same clinic
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      TCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      TCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
TCLN5000  MOVE      OCADESC,HEADING2
          DISPLAY   *P25:10,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN6000  MOVE      "Finish",HEADING2
          DISPLAY   *P25:10,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
TCLN9999  RETURN
.
+
.******************************************************************************
.*                                   KHCP0000             Called by: ML0000   *
.*                             Keyin The HCP code                             *
.******************************************************************************
KHCP0000  MOVE      SP70,FILTRHCP
          KEYIN     *P1:11,*EL,"HCP : ",*V2LON,FILTRHCP
.
          PACK      FILTRHCP,FILTRHCP,SP70
          MATCH     SP70,FILTRHCP
          GOTO      KHCP6000 IF EQUAL
.
          PACK      KEY10,FILTRHCP,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid HCP Code",PACFNAME
          ENDIF
.
KHCP5000  MOVE      PACFNAME,HEADING3
          DISPLAY   *P18:11,*EL,*V2LON,PACFNAME;
          MOVE      ZERO,EXIT
          GOTO      KHCP9999
.
KHCP6000  MOVE      "All",HEADING3
          DISPLAY   *P18:11,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      KHCP9999
.
KHCP9500  DISPLAY   *P1:24,*EL,*B,"Invalid HCP. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
KHCP9999  RETURN
.
+
.******************************************************************************
.*                                   KPRV0000             Called by: ML0000   *
.*                             Keyin the provider number                      *
.******************************************************************************
KPRV0000  MOVE      SP70,FILTPROV
          KEYIN     *P1:14,*EL,"Provider Number : ",*V2LON,FILTPROV
.
          PACK      FILTPROV,FILTPROV,SP70
          MATCH     SP70,FILTPROV
          GOTO      KPRV6000 IF EQUAL
.
KPRV5000  MOVE      FILTPROV,HEADING0
          DISPLAY   *P29:14,*EL,*V2LON,FILTPROV;
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV6000  MOVE      "All",HEADING0
          DISPLAY   *P29:14,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV9999  RETURN
+
.******************************************************************************
.*                                   PRCF0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCF0000  MOVE      SP70,PRACTIFR
          KEYIN     *P1:12,*EL,"Medical Practice : ",*V2LON,PRACTIFR
.
          PACK      PRACTIFR,PRACTIFR,SP70
          MATCH     SP70,PRACTIFR
          GOTO      PRCF6000 IF EQUAL
.
          PACK      KEY6,PRACTIFR,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCF9500
.
PRCF5000  MOVE      PRPRDESC,HEADING5
          DISPLAY   *P27:12,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF6000  MOVE      "Start",HEADING5
          DISPLAY   *P27:12,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCF9999  RETURN
+
.******************************************************************************
.*                                   PRCT0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCT0000  MOVE      SP70,PRACTITO
          KEYIN     *P1:13,*EL,"Medical Practice : ",*V2LON,PRACTITO
.
          PACK      PRACTITO,PRACTITO,SP70
          MATCH     SP70,PRACTITO
          GOTO      PRCT6000 IF EQUAL
.
          PACK      KEY6,PRACTITO,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCT9500
.
PRCT5000  MOVE      PRPRDESC,HEADING6
          DISPLAY   *P27:13,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT6000  MOVE      "Finish",HEADING6
          DISPLAY   *P27:13,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCT9999  RETURN
.
+
.******************************************************************************
.*                                   DRNG0000             Called by: ML0000   *
.*                             Keyin the date range                           *
.******************************************************************************
DRNG0000  DISPLAY   *P1:16,*EL,"From Date : ":
                    *P1:17,*EL,"  To Date : ";
          UNPACK    SP70,FROMDATE,TODATE
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          MOVE      TEN6,CVERT
          MOVE      TEN3,CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,DRNG2000
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
DRNG2000  MOVE      TEN7,CVERT
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,DRNG9999
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
DRNG9999  RETURN
+
.******************************************************************************
.*                                   ITMN0000             Called by: ML0000   *
.*                             Keyin the item range                           *
.******************************************************************************
ITMN0000  DISPLAY   *P1:18,*EL,"From Item : ":
                    *P1:19,*EL,"  To Item : ";
          UNPACK    SP70,FRITMN,TOITMN
          KEYIN     *P13:18,*EL,*V2LON,FRITMN
          ENDSET    FRITMN
          APPEND    SP70,FRITMN
          RESET     FRITMN
          MATCH     "Start",FRITMN
          IF        @EQUAL
            MOVE      SP70,FRITMN
          ENDIF
.
          KEYIN     *P13:19,*EL,*V2LON,TOITMN
          ENDSET    TOITMN
          APPEND    SP70,TOITMN
          RESET     TOITMN
          MATCH     "Finish",TOITMN
          IF        @EQUAL
            MOVE      SP70,TOITMN
          ENDIF
ITMN9999  RETURN
+
.******************************************************************************
.*                                   WEBU0000             Called by: ML0000   *
.*                             Keyin The web user id                          *
.******************************************************************************
WEBU0000  MOVE      SP70,WEBUSRID
          KEYIN     *P1:20,*EL,"User Id : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,WEBU9500
.
          MOVE      ZERO,EXIT
          GOTO      WEBU9999
.
WEBU9500  DISPLAY   *P1:24,*EL,*B,"Invalid User Id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
WEBU9999  RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      HICTEMP1,KEY15;TEMPCLID,TEMPMPRA,TEMPITMN,TEMPAMNT 
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP2   MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      HICTEMP2,KEY15;TEMPCLID,TEMPMPRA,TEMPITMN,TEMPAMNT 
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP    MOVE      SP70,KEY15
          IF        SORTFLAG=1
            READ      HICTEMP1,KEY15;;
          ELSE
            IF        SORTFLAG=2
              READ      HICTEMP2,KEY15;;
            ENDIF
          ENDIF
          RETURN
.
RKTEMP    MOVE      ZERO,OVRCD
          IF        SORTFLAG=1
            READKS    HICTEMP1;TEMPCLID,TEMPMPRA,TEMPITMN,TEMPAMNT 
          ELSE
            IF        SORTFLAG=2
              READKS    HICTEMP2;TEMPCLID,TEMPMPRA,TEMPITMN,TEMPAMNT 
            ENDIF
          ENDIF
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   MOVE      ZERO,OVRCD
          UPDATE    HICTEMP1;TEMPCLID,TEMPMPRA,TEMPITMN,TEMPAMNT 
          RETURN
.
UPTEMP2   MOVE      ZERO,OVRCD
          UPDATE    HICTEMP2;TEMPCLID,TEMPMPRA,TEMPITMN,TEMPAMNT 
          RETURN
.
WRTEMP1   WRITE     HICTEMP1,KEY15;TEMPCLID,TEMPMPRA,TEMPITMN,TEMPAMNT 
          RETURN
.
WRTEMP2   WRITE     HICTEMP2,KEY15;TEMPCLID,TEMPMPRA,TEMPITMN,TEMPAMNT 
          RETURN
.
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       HICBTCIO/INC
          INC       HICCLMIO/INC
          INC       HICCITIO/INC
          INC       PMSHCPIO/INC
          INC       PRIPRAIO/INC
          INC       PRIDOCIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       OUTMA1IO/INC        * Session Master file
          INC       PATCODIO/INC
          INC       WEBSECIO/INC
          INC       PRIALSIO/INC
          INC       PRISECIO/INC
          INC       PRIITMIO/INC

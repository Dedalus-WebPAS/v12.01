.******************************************************************************
.* System    :   Outpatients                                                  *
.* Program   :   IBAOUT15                                                     *
.* Desc      :   Outpatient Enquiry                                           *
.******************************************************************************
.* Date      :   03/08/1993                                                   *
.* Author    :   Glenn Berry                                                  *
.* Function  :   Perform an outpatient Enquiry.                               *
.* Note      :   This program does the same thing as the Outpatient Enquiry in*
.*               IBAOUT66, where most of this code was taken from             *
.* Mods      :                                                                *
.******************************************************************************
.* V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                            *
.*           Added alpanumeric visit number generation -DVIS3700,CPMI1000     * 
.* V11.02.01 09/02/2022  Thanh T       TSK 0905641                            *
.*           Recompiled as OUTMA1FD changes                                   *
.* V10.07.01 30/10/2015  Ebon Clements  CAR 268970                            *
.*           Change to use OUTCLIFD index 1 instead of index 2                *
.* 10.03.02  17/10/2012  Jill Parkinson CAR 273375                            *
.*           Recompiled for DATECONV - first day of year calculation          *
.* 10.03.01  19/03/2012 Peter McMullen    CAR 259587                          *
.*           Remove reference to redundant paytears file                      *
.* V10.02.01 07/07/2011  Steve Armstrong  CAR 240722                          *
.*           Added RDPMPX21 prior to calling PMIHEAD                          *
.* V9.03.03  16/02/2004  Mike Laming       CAR 47434                          *
.*           Change to use SCRNLN for Screen Line count - FORM2 used          *
.*           as a workarea in CALCAGE and other places                        *
.* V9.03.02  29/10/2003 Sandra Barcham         43783                          *
.*           Recompiled for NZHISIIO and NZHISIUP                             *
.* V9.03.01  11/09/2003  Sandra Barcham        43010                          *
.*           Open outpreaf without site prefix                                *
.******************************************************************************
.* V9.02.01  03/01/2003  Lina Vo           srf 22709                          *
.*           Open outpreaf with site prefix                                   *
.******************************************************************************
.*        V5.08.06  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.05  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.04  19/07/2000  Caleb Sharman  SRF 3394                       *
.*                  Recompiled for PATSRCH                                    *
.*        V5.08.03  19/05/2000  Steve Downing                                 *
.*                  Recompiled for OTHISENQ - Fixed next U/R prompt           *
.*        V5.08.02  19/05/2000  Steve Downing                                 *
.*                  Recompiled for OTHISENQ - Fixed display of history header *
.*        V5.08.01  19/05/2000  Steve Downing                                 *
.*                  Added heading in the History option                       *
.*       V5.08.B02  13/03/2000  Steve Armstrong  5.08 mods                    *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.*       V5.08.B01  14/01/00 J.Tan (changes for V5.08)                        *
.*                  Recompiled for OUTEQRFL,OTHISENQ (referral letter,history)*
.******************************************************************************
.*       V5.07.05   23/11/99 J.Tan (changes for V5.08)                        *
.*                  Recompiled for OUTEQRFL,OTHISENQ (referral letter,history)*
.*       V5.07.04   12/10/1999  Andrew Peel  SRF 646081                       *
.*                  Fixed cancellation date on enquiry screen                 *
.*       V5.07.03   01/06/1999  Caleb Sharman  SRF 645586                     *
.*                  Recompiled for OTHISENQ                                   *
.*       V5.07.02   05/02/1999  Jill Habasque  SRF 645225                     *
.*                  Changed to display PMIHEAD with referral letters          *
.*       V5.07.B01  14/12/1998  Jim Stathopoulos    SRF 643494                *
.*                  Recompiled for OTHISENQ                                   *
.******************************************************************************
.*        V5.05.02  26/02/1998  Nick   SRF 643848                             *
.*                  Recompiled for OUTEQRFL                                   *
.*        V5.05.01  26/02/1998  Nick   SRF 644197                             *
.*                  Added READ for OTCNDCRE to display cancellation reason    *
.*                  in queries. Also changed DIM15 from 10 to 15 chars!!!     *
.*                  Also reset TDESC to 6 instead of 11.                      *
.******************************************************************************
.*        V5.04.07  14/06/1997  Steve Armstrong                               *
.*                  Fixed bugs in global recompile                            *
.*        V5.04.06  28/04/1997  Howellsy          rha                         *
.*                  added history option                                      *
.*        V5.04.05  11/03/1997  Howellsy          5.05                        *
.*                  added clinic id to enquiry                                *
.******************************************************************************
.*        V5.04.04  30/01/1997  Howellsy          EOC & ACC                   *
.*                  Recompiled for OUTEQRFL                                   *
.*        V5.04.03  20/12/1996  Howellsy          EOC & ACC                   *
.*                  Recompiled for OUTRF1FD                                   *
.*        V5.04.02  14/06/1996  Steve Armstrong   SRF 641636                  *
.*                  Added code from 5.03 to display rescheduled bookings.     *
.*        V5.04.01  11/06/1996  Steve Armstrong   SRF 641617/641612           *
.*                  Recompiled for mods to OUTEQRFL                           *
.*                  Mods to pick up referral letters when no O/P visits       *
.******************************************************************************
.*        V5.03.03  11/04/1996  Howellsy         SRF 641361                   *
.*                  added display of rescheduled bookings in enquiry.         *
.*        V5.03.02  09/04/1996    Whirlpool      (NZ 5.04)                    *
.*                  Added Referral Letter enquiry when searching by U/R       *
.*        V5.03.01  18/08/1995       MAT          SRF 640578                  *
.*                  Recompiled for CALCAGE                                    *
.******************************************************************************
.*               V5.01.01  26/08/93  ROD                                      *
.*                         recompiled for NZHIS                               *
.*               V5.01.02  23/11/93  Paul Howells       NZHIS                 *
.*                         Added PATALRFD & IO for NZHISIIO                   *
.*        V5.01.03  23/03/1994    Matt Surridge                               *
.*                  Recompile for NZHISIIO and DONORDET.                      *
.*        V5.01.04  24/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.******************************************************************************
.*        V5.02.01  30.11.1994    B.G.Ackland                                 *
.*                  Remove Date of Death Link to NHI/MWS                      *
.*                  Recompile for Latest NHI/MWS Changes                      *
.*        V5.02.02  11/04/1995  Paul Howells srf 134941                       *
.*                  Display operator id for resch/canc visits in Enquiry.     *
.******************************************************************************
.
.====================================================
.  FD INCLUDES 
.====================================================
.
          INC       STD001FD/PBL
          INC       BOKRC1FD/INC            * booking file
          INC       IBAPASFD/INC
          INC       OUTBOAFD/INC            * booking files
          INC       OUTBB1FD/INC            * booking files
          INC       OUTBOCFD/INC            * booking files
          INC       OUTCANFD/INC            * cancelled bookings file
          INC       OUTCLIFD/INC            * OP clinic file
          INC       OUTCONFD/INC            * OP control file
          INC       OUTCTYFD/INC            * OP Clinic Type File
          INC       OUTDIAFD/INC            * OP diagnosis file
          INC       OUTMA1FD/INC            * OP clinic master
          INC       OUTPREFD/INC            * OP pre-attendance file
          INC       OUTRF1FD/INC            * Referral File
          INC       OUTRSHFD/INC            * reschedule        file
          INC       OUTSITFD/INC            * OP site file
          INC       PATALRFD/INC            * alerts file
          INC       PATCALAG/INC
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATCODFD/INC            * codes file
          INC       PATDO1FD/INC            * doctors file
          INC       PATDHEAD/INC 
          INC       NHIMASFD/INC
          INC       PATMA1FD/INC            * patient master file
          INC       PATMI1FD/INC            * admission file
          INC       PATPR1FD/INC            * pre-admission file
          INC       PATGSRFD/INC            * surname file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
          INC       PATDNRFD/INC
          INC       PATSDNFD/INC
          INC       PATSMWFD/INC
          INC       PATNIDFD/INC
          INC       NZHISIFD/INC
          INC       OUTHISFD/INC
          INC       SCRPSTFD/INC
.
.
.====================================================
. Variable List
.====================================================
++
ABORT     FORM      1                        * Abort flag. 1=Abort input
BJDAY     FORM      3
CJDAY     FORM      3
CANCFLAG  FORM      1
CATDO     INIT      "DO" 
CODECV    INIT      "CV"                     * Visit Type
CMDLINE   DIM       80
COUNT     FORM      2
DIM4      DIM       4
DIM8      DIM       8 
DIM10     DIM       10
DIM10A    DIM       10
DIM15     DIM       15
DIM17     DIM       17
DIM18     DIM       18
DIM20     DIM       20
DIM40     DIM       40
DIM30     DIM       30
DOSTATUS  DIM       10
DOSTAT0   INIT      "Not Booked"
DOSTAT1   INIT      "Booked"
DOSTAT4   INIT      "Attended"
DOSTAT5   INIT      "Not Attend"
FORM3     FORM      3
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
HASCMNS   DIM       3
ITEM      FORM      2
IBCNMHOS  FORM      1
MODE1     DIM       1
NMPNUMB   DIM       20
OPCODE    DIM       4
OPDESC    DIM       4
RFLTFLAG  FORM      1                        * o/p referral letters flag
.                                              0 = o/p referral letters on file
.                                              1 = no o/p referral letters found
SCNDGNAM  DIM       40
SCREEN    FORM      3
SCRNLN    FORM      2                                                  *I-47434
SESS1     DIM       25
SESS2     DIM       25
VAGE      DIM       3
VCASE     DIM       1                        * Case notes flag for VURNO 
VERTUR    FORM      2                        * keyin line for U/R number
VNAME     DIM       35                       * name for working U/R number
VSITE     DIM       6                        * Working Site
VURNO     DIM       8                        * Working U/R number
CODEO2    INIT      "O2"
HYPHEN    INIT      "-"
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
Z15       INIT      "zzzzzzzzzzzzzzz"
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
DASH      INIT      " - "
++
PRGID     INIT      "IBAOUT15"
PRGNAM    INIT      "Outpatient Enquiry"
VERSION   INIT      "V12.01.00"
+
.***********************************************************************
.         MAINLINE
.***********************************************************************
ML0000    CALL      INIT0000            * initialization & open files
          BRANCH    EXIT,ML9999         * initialization failure
.
ML1000    CALL      KOUT0000            * Get the U/R Number to enquire on.
          BRANCH    ABORT,ML9999        * Abort
          BRANCH    EXIT,ML2000         * No U/R Number entered
          GOTO      ML3000
.
ML2000    MATCH     ZEROUR,VURNO        * check if zero u/r entered
          GOTO      ML9999 IF NOT EQUAL
          CALL      PRAT0000            * display pre-att. details for "0" U/R.
          BRANCH    ABORT,ML9999        * abort
          BRANCH    EXIT,ML9999         * Exit
          GOTO      ML1000
.
ML3000    MOVE      SEVEN,EVERT         * set first display row
          MOVE      TWENTY3,SCRNLN      * set no of screen lines       *I-47434
          CALL      DVIS0000            * Display visit details for patient
          BRANCH    ABORT,ML9999        * Abort
          BRANCH    EXIT,ML9999         * Exit option selected
          GOTO      ML1000              * Get the next U/R Number
.
ML9999    CHAIN     PGM
+
.
.****************************************************************************
.*        Initialization & open files             
.****************************************************************************
INIT0000  CALL      DISPHEAD            * Display the standard heading
          MOVE      ZERO,EXIT
.
          MOVE      IDDD,VSITE          * Initialize the current site
          MATCH     SP6,VSITE           * Valid site ?
          GOTO      INIT9000 IF EQUAL   * No. Initialization failure
.
.         Open the site file so as an extra test on the site, and to get
.         the parameters for the site (ie. OSTSYST, OSTIRNG, OSTCHRG).
.
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf" * Open the site file
.
          MOVE      VSITE,KEY6          * Key for the site file
          CALL      RDSITA1             * Read the site record
          BRANCH    OVRCD,INIT9000      * Invalid site.
.
.         Open the control file to get the parameters needed by this program
.
          DISPLAY   *P64:24,"controlf"; * Display the name of the control file
          OPEN      CONTROLF,"controlf" * Open the control file
          READ      CONTROLF,THIRTY1;*183,OTCNNSID,*174,OTCNULET
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,THIRTY2;*38,OTCNOPCR
          READ      CONTROLF,SEVENTY9;*138,PTCNURHA
          READ      CONTROLF,THIRTY1;*181,OTCNDCRE
.
          IF        OTCNNSID = 1
            MOVE      "out",UID
            MOVE      "out",OSTFILE
          ENDIF
.
          MATCH     UID,OSTFILE         * Final check. Is UID correct for site?
          GOTO      INIT9000 IF NOT EQUAL
.
          DISPLAY   *P64:24,"Opening "
          DISPLAY   *P64:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
.
          PACK      CFNAME,UID,FILBOKA1     * Get the name of the BOKA1 file
          DISPLAY   *P64:24,CFNAME;         * Display the name
          OPEN      OUTBOKA1,CFNAME         * Open the file
.
          PACK      CFNAME,UID,FILBOKA2     * Get the name of the BOKA2 file
          DISPLAY   *P64:24,CFNAME;         * Display the name
          OPEN      OUTBOKA2,CFNAME         * Open the file
.
          PACK      CFNAME,UID,FILBOKA3     * Get the name of the BOKA3 file
          DISPLAY   *P64:24,CFNAME;         * Display the name
          OPEN      OUTBOKA3,CFNAME         * Open the file
.
          PACK      CFNAME,UID,FILBOKA4     * Get the name of the BOKA4 file
          DISPLAY   *P64:24,CFNAME;         * Display the name
          OPEN      OUTBOKA4,CFNAME         * Open the file
.
          PACK      CFNAME,UID,FILBB1A1     * Get the name of the BOKB1 file
          DISPLAY   *P64:24,CFNAME;         * Display the name
          CALL       OPOTBB11               * Open the file
.
          PACK      CFNAME,UID,FILBOKC1     * Get the name of the BOKC1 file
          DISPLAY   *P64:24,CFNAME;         * Display the name
          OPEN      OUTBOKC1,CFNAME         * Open the file
.
          PACK      CFNAME,UID,FILCLIA1     * Get the name of the CLIA1 file
          DISPLAY   *P64:24,CFNAME;         * Display the name
          OPEN      OUTCLIA1,CFNAME         * Open the file
.
          PACK      CFNAME,UID,FILDIAG1     * Get the name of the DIAG1 file
          DISPLAY   *P64:24,CFNAME;         * Display the name
          OPEN      OUTDIAG1,CFNAME         * Open the file
.
          PACK      CFNAME,UID,FILMA1A1     * Get the name of the MASA1 file
          DISPLAY   *P64:24,CFNAME;         * Display the name
          CALL      OPOTMA11                * Open the file
.
.         Open the rescheduling log file
.
          PACK      CFNAME,UID,FILRSHA1     * File name to open
          OPEN      OUTRSHA1,CFNAME         * Open the rescheduling file
.
.
          DISPLAY   *P64:24,"patcodes";     * Display the name of the codes file
          OPEN      PATCODE1,"patcodes"     * Open the file
.
          DISPLAY   *P64:24,"patdo1af";     * Display the name of doctors file
          OPEN      PATDO1A1,"patdo1af"               * Open the file
.
          DISPLAY   *P64:24,"patma1af";     * Display the name of the PMI file
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";     * Display the name of the PMI file
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patpramf";     * Display the name of pre-PMI file
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patpraxf";     * Display the name of pre-PMI ext
.
          DISPLAY   *P64:24,"bokrc1af";     * Display the name of bookings file
          OPEN      BOKRC1A6,"bokrc1af"
.
          READ      CONTROLF,FIFTY9;*231,PTCNRGNS
          OPEN      PATGSRN1,"patgsrnf"   * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          GOTO      INIT9999
.
INIT9000  DISPLAY   *P1:24,*EL,*B:
                    "Incorrect call to program - Site info wrong. ";
          CALL      HITENTER
          MOVE      ONE,EXIT                * Initialization failure
.
INIT9999  RETURN
+
. *****************************************************************************
. * KOUT0000 : Get the U/R Number for an outpatient enquiry and a request for *
. *            a medical record                                               *
. * RETURNS :    VURNO   = The U/R Number to do the search for                *
. *****************************************************************************
KOUT0000  DISPLAY   *P1:3,*EF;               * Clear the screen
          MOVE      FOUR,VERTUR              * Line to key in U/R Number
          CALL      GETU0000                 * Get the U/R Number
          BRANCH    ABORT,KOUT9999           * Abort
          BRANCH    EXIT,KOUT9999            * Nothing input
.
          MATCH     ZEROUR,VURNO             * Zero input ?
          GOTO      KOUT9000 IF EQUAL        * Treat as exit.
          MOVE      ZERO,EXIT
          GOTO      KOUT9999
.
KOUT9000  MOVE      ONE,EXIT
.
KOUT9999  RETURN
+ 
. *****************************************************************************
. * GETU0000: Keyin a U/R Number                                              *
. * PARAMETERS : VERTUR = Line to display U/R Number on                       *
. * RETURNS    : EXIT   = 0 means valid U/R entered                           *
. *                     = 1 means no valid U/R entered                        *
. *              OBAURNO= U/R entered (if EXIT=0)                             *
. *              VURNO  = U/R entered (if EXIT=0)                             *
. *              VCASE  = Case notes flag for VURNO                           *
. *****************************************************************************
GETU0000  DISPLAY   *P1:VERTUR,*EL,"U/R Number  : ";
          PACK      VNAME,SP30,SP30     * Initialize patients name
          MOVE      SP3,VAGE            * initialize patients age
          MOVE      SP8,VURNO           * initialize working u/r no.
          MOVE      SP1,VCASE           * Initialize case notes indicator
.
          MOVE      ZERO,CREGOPT    
.
          MOVE      TEN5,CCOL           * Column for keyin
          MOVE      VERTUR,CVERT        * Line for keyin
          MOVE      TWO,SRCHOPT         * Display address in surname searchs
          MOVE      ZERO,SRCHSYS        * Display all 0 U/R details
          OPEN      PATMI1A1,"patmi1af" * For 0 U/R's
          CALL      KURN                * Key in the U/R Number
          CLOSE     PATMI1A1            * Close admissions file
          BRANCH    ABORT,GETU9999      * Input aborted
          BRANCH    EXIT,GETU9999       * Nothing entered
.
          MATCH     ZEROUR,PURNO        * 0 entered for U/R ?
          GOTO      GETU9500 IF EQUAL   * Yes. Don't validate U/R's existance
.
          BRANCH    OVRCD,GETU9900      * U/R Number doesn't exist
.
.         Valid U/R Number entered
.
GETU9000  MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,GETU9900
.
          MOVE      PURNO,OBAURNO       * Set up return parameter
          MOVE      PURNO,VURNO         * Set up return parameter
          GOTO      GETU9999
.
GETU9500  CALL      CPMI0000
          MOVE      PURNO,VURNO
          MOVE      PURNO,OBAURNO       * Set up return parameter
          GOTO      GETU9999
.
GETU9900  DISPLAY   *P1:24,*EL,*B,"Invalid U/R Number.  ";
          CALL      HITENTER
          BRANCH    ABORT,GETU9999      * Input aborted
          GOTO      GETU0000
.
GETU9999  RETURN
+
.*************************************************************************
.*                         PRAT0000                                      *
.*       Routine to extract booking details if a Zero U/R no. entered.   *
.*   Details will be extracted from Outpatients Pre-Attendance Booking   *
.*   Details File and the the Clinic Session Booking File A. The summary *
.*   screen will only show records with a slot status of "1" = Booked,   *
.*   "5" = Not Attended....                                              *
.*************************************************************************
PRAT0000  DISPLAY   *P64:24,"outpreaf";         * Display the name
          OPEN      OUTPREA1,"outpreaf"      * open pre-attendance booking file
          MOVE      FALSE,EXIT               * clear exit flag
          MOVE      FALSE,FORM1              * clear no-more-records flag
          MOVE      SEVEN,CVERT              * set line number
.
.-------- set up screen headers ------
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,"U/R Number       : ",*V2LON,VURNO,*HOFF;
.
.-------- set up summary screen header -------
.
          DISPLAY   *P1:6,*V2LON,*ULON,"Outpat ##":
                    *P10:6,"Name",SP10,SP3:
                    *P29:6,"Date    ":
                    *P40:6,"Time ":
                    *P46:6,"Type  ":
                    *P53:6,"Clinic":
                    *P60:6,"Visit Type":
                    *P71:6,"Status";
. 
.-------- pack key and perform positional read on pre-attendance file ------
.
          PACK      KEY8,SP10
          CALL      RDSPREA1                          * positioning read
.
.-------- read key sequential for initial reads ------
.
PRAT0500  CALL      RDKPREA1
          BRANCH    OVRCD,PRAT9000                    * if no records, exit
          MATCH     IDDD,OPRSITE                      * match sites
          GOTO      PRAT0500 IF NOT EQUAL             * if not equal, read more
.
.-------- read outpatients clinic session booking file A ---------
.
          PACK      KEY28,OPRSITE,OPRCLIN,OPRDATE,OPRSTRT,OPRSLOT
          CALL      RDBOKA1
          BRANCH    OVRCD,PRAT9000                    * exit if no records exist
.
.-------- read key sequential ------
.
PRAT1000  CALL      RDKPREA1                         * read key seq pre-att.file
          BRANCH    OVRCD,PRAT4800
          MATCH     IDDD,OPRSITE                     * match sites, read next 
          GOTO      PRAT1000 IF NOT EQUAL            * record if not equal
.
.-------- read outpatients clinic session booking file A ---------
.
          PACK      KEY28,OPRSITE,OPRCLIN,OPRDATE,OPRSTRT,OPRSLOT
          CALL      RDBOKA1
          BRANCH    OVRCD,PRAT4800                * if no more records set flag
.
.------- record found, perform other tests -------
.
PRAT1500  MATCH     ZEROUR,OBAURNO              * check if U/R no. equal to 0.
          GOTO      PRAT1000 IF NOT EQUAL
.
          MOVE      SP10,DOSTATUS               * clear display variable
          COMPARE   ONE,OBASTAT                 * slot status must = "1",Booked
          GOTO      PRAT2000 IF NOT EQUAL       * OR "5", Not attended
          MOVE      DOSTAT1,DOSTATUS            * move "booked" to dim10
          GOTO      PRAT3000
.
PRAT2000  COMPARE   FIVE,OBASTAT                * slot status = "5",Not Attended
          GOTO      PRAT1000 IF NOT EQUAL       * if not satisfied read next rec
          MOVE      DOSTAT5,DOSTATUS            * move "not attended" to dim10
.
.-------- get patients name -----
.
PRAT3000  MOVE      SP20,DIM20                * clear variable
          PACK      KEY8,OPROUTN
          CALL      RDPRAM1                   * Read pre-admission master file
          BRANCH    OVRCD,PRAT3200
          CALL      FNAM0000                  * call format patient name routine
.
.-------- get visit type ------
.
PRAT3200  MOVE      SP20,DIM10
          PACK      KEY5,CODECV,OBAVISIT        * read patient codes file for 
          CALL      RDCODE1                     * for visit type description
          BRANCH    OVRCD,PRAT4000
          MOVE      TDESC,DIM10
.
.-------- display details -----
.
PRAT4000  RESET     DIM20
          UNPACK    DIM20,DIM18
          DISPLAY   *P1:CVERT,OBAOUTNO,*P10:CVERT,DIM18;
.
.-------- get date --------
.
          MOVE      TWENTY9,CCOL                * set up date column
          MOVE      ONE,CHIGHLT                 * set highlight off
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY     * unpack booking date
          CALL      DSPDATE                           * display date routine
.
          DISPLAY   *P40:CVERT,OBATIME:
                    *P46:CVERT,OBACTYP:
                    *P53:CVERT,OBACLIN:
                    *P60:CVERT,DIM10:
                    *P71:CVERT,DOSTATUS;
.   
          ADD       ONE,CVERT                 * add one to line 
          COMPARE   TWENTY3,CVERT             * check for end of screen
          GOTO      PRAT5000 IF NOT LESS     
          GOTO      PRAT1000                 * loop
.
.-------- if no more records on file then set flag ------
.
PRAT4800  MOVE      TRUE,FORM1                       * set no more records flag
.
.-------- if screen full with details, or no more records display options ----
.-------- (E)xit, (S)earch More, (N)ext ? ---------
.
PRAT5000  KEYIN     *P1:24,*EL," (",*V2LON,*DV,ANSE,*HOFF,")xit,":
                    " (",*V2LON,*DV,ANSS,*HOFF,")earch more,":
                    " (",*V2LON,*DV,ANSN,*HOFF,")ext ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     ANSE,ANS                         * Exit 
          GOTO      PRAT8000 IF EQUAL
          MATCH     ANSN,ANS                         * Next 
          GOTO      PRAT9999 IF EQUAL                * exit routine
          MATCH     ANSS,ANS                         * Search More
          GOTO      PRAT5500 IF EQUAL
          BEEP
          GOTO      PRAT5000                         * invalid option
.
.-------- if (S)earch More option chosen, check no-more-records flag -----
.
PRAT5500  BRANCH    FORM1,PRAT5800                   * "1" =  no more records
          DISPLAY   *P1:7,*EF;                       * if still more records
          MOVE      SEVEN,CVERT                      * reset line number
          GOTO      PRAT1000                         * read next record
.
.------- If No more records for (S)earch More, then warn user ------
.
PRAT5800  DISPLAY   *P1:24,*EL,*B,"No more details on file.  ";
          CALL      HITENTER
          GOTO      PRAT5000                         * goto options
.
.------- (E)xit chosen, set exit flag ------
.
PRAT8000  MOVE      TRUE,EXIT                        * set exit to true
          GOTO      PRAT9999
.
.------- if over-condition true on initial read, warn user ------
.
PRAT9000  DISPLAY   *P1:24,*EL,*B,"Details not on file.  "; 
          CALL      HITENTER
.
.------- close outpatient pre-attendance file and exit routine ------
.
PRAT9999  CLOSE     OUTPREA1              * close pre-attendance booking file
          RETURN 
+
.**********************************************************************
.*        Routine to format patients name   
.**********************************************************************
FNAM0000  PACK      DIM20,SP20
          MATCH     SP6,PTITL
          GOTO      FNAM1200 IF EQUAL
          MOVE      PTITL,DIM20                  * get title
          ENDSET    DIM20                        * reset pointers
.
.-------- get surname ------
.
FNAM0500  CMATCH    SP1,DIM20                    * character match for spaces
          GOTO      FNAM1000 IF NOT EQUAL
          BUMP      DIM20,-1                     * bump back by 1
          GOTO      FNAM0500                     * loop 
.
FNAM1000  APPEND    " ",DIM20                    * if no more spaces append
FNAM1200  MATCH     SP30,PSNAME
          GOTO      FNAM2100 IF EQUAL
          APPEND    PSNAME,DIM20                 * given name to field
.
.-------- get given name -----
.
FNAM1500  CMATCH    SP1,DIM20                    * character match for spaces
          GOTO      FNAM2000 IF NOT EQUAL
          BUMP      DIM20,-1                     * bump back by 1
          GOTO      FNAM1500                     * loop 
.
FNAM2000  APPEND    ", ",DIM20                   * if no more spaces append
FNAM2100  MATCH     SP20,PGNAME
          GOTO      FNAM9999 IF EQUAL
          APPEND    PGNAME,DIM20                 * given name to field
.
FNAM9999  RETURN
+
. *****************************************************************************
. * DVIS0000 : Display the outpatient visits/bookings for a given U/R Number  *
. * PARAMETERS : VURNO   = The U/R Number to do the search for                *
. *              EVERT     the starting row for display                       *
. *****************************************************************************
DVIS0000  PACK      CFNAME,OSTFILE,FILCANA2
          OPEN      OUTCANA2,CFNAME
.
          PACK      KEY36,VURNO,SP30         * Check for an outpatient visit
          CALL      RDSBOKA3                 * Position at start of patient
          CALL      RDKBOKA3                 * Read first record of patient
          BRANCH    OVRCD,DVIS0200           * No Bookings
.
          MATCH     VURNO,OBAURNO            * Still the correct patient ?
          GOTO      DVIS0200 IF NOT EQUAL    * No Bookings
          GOTO      DVIS0300
.
DVIS0200  PACK      KEY24,VURNO,SP30         * Check for an outpatient visit
          CALL      RSOTCAN2                 * Position at start of patient
          CALL      RKOTCAN2                 * Read first record of patient
          BRANCH    OVRCD,DVIS9500           * Never an outpatient
.
          MATCH     VURNO,OPCNURNO           * Still the correct patient ?
          GOTO      DVIS9500 IF NOT EQUAL    * Never an outpatient
.
.         Display the heading for this patient
.
DVIS0300  IF        EVERT = 7
            CALL      PMIHEAD
          ENDIF
.
          IF        OTCNOPCR = 0
            DISPLAY   *P1:EVERT,*EF,*ULON,*V2LON,"  Date  ":
                      *P12:EVERT,"Time ":
                      *P18:EVERT,"Clinic",SP9,SP10:
                      *P44:EVERT,"Visit Type",SP2,SP10:
                      *P67:EVERT,"Status    ";
            IF        IBCNMHOS = ONE
              DISPLAY   *P78:EVERT,*V2LON,*ULON,"Hos";
            ELSE
              DISPLAY   *P77:EVERT,*V2LON,*ULON,SP4;
            ENDIF
          ELSE
            DISPLAY   *P1:EVERT,*EF,*ULON,*V2LON,"   Date   ":
                      *P12:EVERT,"Time ":
                      *P18:EVERT,"Clinic",SP9,SP10:
                      *P44:EVERT,"Visit Type",SP7:
                      *P62:EVERT,"Status    ";
            IF        IBCNMHOS = ONE
              DISPLAY   *P73:EVERT,*V2LON,*ULON,"Hos";
            ELSE
              DISPLAY   *P72:EVERT,*V2LON,*ULON,SP4;
            ENDIF
            DISPLAY   *P77:EVERT,*V2LON,*ULON,"Oper";
          ENDIF
.
          MOVE      "11111111",ALRTMASK          * Display Patient Alerts
          CALL      PATALERT
.
          MOVE      EVERT,CVERT              * Starting line for display
          ADD       ONE,CVERT
.
.         Now loop over all the visits for this U/R Number,
.         from newest to oldest (ie. using RDP instead of RDK).
.
          MOVE      SP8,OPCNURNO             * clear U/R Number
          PACK      KEY24,VURNO,Z20
          CALL      RSOTCAN2                 * Position at the end of patient
          CALL      RPOTCAN2                 * Get the next record
          MOVE      OVRCD,CANCFLAG           * Set Flag for end of Cancelled
.
          MATCH     VURNO,OPCNURNO           * Still the same patient ?
          GOTO      DVIS0750 IF EQUAL        * No. Finished the visits
          MOVE      ONE,CANCFLAG             * Set Flag for end of Cancelled
.
DVIS0750  PACK      KEY36,VURNO,Z20,Z20
          CALL      RDSBOKA3                 * Position at the end of patient
DVIS1000  CALL      RDPBOKA3                 * Get the next record
          BRANCH    OVRCD,DVIS4000           * End of booking records
.
          MATCH     VURNO,OBAURNO            * Still the same patient ?
          GOTO      DVIS4000 IF NOT EQUAL    * No. Finished the bookings
.
          COMPARE   THREE,OBASTAT            * Extended slot ?
          GOTO      DVIS1000 IF EQUAL        * Yes. Ignore the record
.
.         Check if there is enough room for this record on the screen
.
          MOVE      TWENTY3,SCRNLN           * line to check for       *C-47434
          MATCH     SP3,OBADISCH
          IF        !@EQUAL & OBASTAT = FOUR
            MOVE      TWENTY2,SCRNLN         * need 2 lines to display *C-47434
          ENDIF
.
          COMPARE   SCRNLN,CVERT             * Only display max line 22*C-47434
          GOTO      DVIS3000 IF LESS         * There is room on screen
.
.         No room on screen for this record. Check what the user wants to do
.
DVIS1500  DISPLAY   *P1:24," (",*V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSS,*HOFF,")earch more, (":
                    *V2LON,ANSN,*HOFF,")ext U/R";
          IF        OTCNULET = 1
            DISPLAY   ", (",*V2LON,ANSR,*HOFF,")eferral Letters";
          ENDIF
          IF        PTCNURHA = 1
            DISPLAY   ", (",*V2LON,ANSH,*HOFF,")istory";
          ENDIF
          KEYIN     *EL," ? ",*V2LON,ANS;
.
          BRANCH    ABORT,DVIS9999           * Input aborted
          REP       UPPLOW,ANS
.
          MATCH     ANSE,ANS                 * Exit option ?
          GOTO      DVIS9000 IF EQUAL        * Yes. Take appropriate action
          MATCH     ANSS,ANS                 * Search more option ?
          GOTO      DVIS2000 IF EQUAL        * Yes. Take appropriate action
          MATCH     ANSN,ANS                 * Next option ?
          GOTO      DVIS8500 IF EQUAL        * Yes. Take appropriate action
          IF        OTCNULET = 1
            MATCH     ANSR,ANS
            GOTO      DVIS8400 IF EQUAL
          ENDIF
          IF        PTCNURHA = 1
            MATCH     ANSH,ANS
            GOTO      DVIS8600 IF EQUAL
          ENDIF
.
          BEEP
          GOTO      DVIS1500                 * Try to get a valid response
.
.         Search more option selected. Clear the screen and continue on
.
DVIS2000  MOVE      EVERT,CVERT             * Starting line for display
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,*EF;          * Clear the screen
.
.         Set up the current record data for the display
.
DVIS3000  BRANCH    CANCFLAG,DVIS3050        * no more cancelled records
.
          PACK      SESS1,OBADATE,OBATIME    * Booking Date/Time
          PACK      SESS2,OPCNDATE,OPCNTIME  * Cancelled Date/Time
.
          MATCH     SESS2,SESS1              * Check if Cancel Date is to be
          GOTO      DVIS4000 IF LESS         * Displayed before booking
.
DVIS3050  PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1             * Read in the session master record
          BRANCH    OVRCD,DVIS1000      * No record found. Error.
.
          MATCH     VURNO,OBAURNO            * Still the same patient ?
          GOTO      DVIS8000 IF NOT EQUAL    * No. Finished the bookings
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OBASITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OBACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,DVIS3100           * Record missing
.
.         We have the clinic record. Now set up the description
.
          CALL      CIDSC000                 * Get the clinic description
          GOTO      DVIS3200                 * Continue on with description
.
.         Missing clinic record. Set up an appropriate description
.
DVIS3100  CLEAR     OCADESC                  * Clear the description
          APPEND    "** Unknown Clinic Id ",OCADESC
          APPEND    OBACLIN,OCADESC          * Add unknown code to description
          APPEND    " **",OCADESC            * Trailing section of description
          RESET     OCADESC
.
.         Get the visit type description
.
DVIS3200  MOVE      SP20,TDESC               * Default description
          MOVE      TDESC,KEY20              * save desc
          MATCH     SP3,OBAVISIT             * Check for a blank visit type
          GOTO      DVIS3600 IF EQUAL        * No visit type. Use default desc.
.
          PACK      KEY5,CODECV,OBAVISIT     * Key for visit type
          CALL      RDCODE1                  * Read in the description
          BRANCH    OVRCD,DVIS3300           * Missing code file record
          MOVE      TDESC,KEY20              * save desc
          GOTO      DVIS3600                 * Proceed with this description
.
.         Missing codes file record
.
DVIS3300  CLEAR     TDESC                    * Clear the desciption
          APPEND    "* Unknown Code ",TDESC  * Add an appropriate description
          APPEND    OBAVISIT,TDESC           * Add unknown code to description
          APPEND    "* ",TDESC               * Trailing section of description
          RESET     TDESC
          MOVE      TDESC,KEY20              * save desc
.
.         Get the booking record status
.
DVIS3600  MOVE      DOSTAT0,DOSTATUS         * Default to status 0
          LOAD      DOSTATUS,OBASTAT,DOSTAT1,SP20,SP20,DOSTAT4,DOSTAT5
.
.         Set up booking date
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
.         see if have a discharge record
.
          MATCH     SP3,OBADISCH
          IF        !@EQUAL & OBASTAT = FOUR
            PACK      KEY5,CATDO,OBADISCH
            CALL      RDCODE1
            SETLPTR   OCADESC,27
            MOVE      TDESC,DIM17
            DISPLAY   *P1:CVERT,CPCDATE:
                      *P12:CVERT,OBATIME:
                      *P18:CVERT,*+,OCADESC,*-
.
            IF        OTCNOPCR = 0
              DISPLAY   *P44:CVERT,TDESC:
                        *P67:CVERT,"Discharged";
            ELSE
              DISPLAY   *P44:CVERT,DIM17:
                        *P62:CVERT,"Discharged";
            ENDIF
            ADD       ONE,CVERT                * Go to the next available line
            SETLPTR   OCADESC
            MOVE      SP5,OBATIME
            MOVE      SP30,OCADESC
          ENDIF
.
.         Display the record
.
          MOVE      SP3,KEY3
          LOAD      KEY3,IBCNMHOS,OTMAHOSP  * load hospital code
          SETLPTR   OCADESC,27
          DISPLAY   *P1:CVERT,CPCDATE:
                    *P12:CVERT,OBATIME:
                    *P18:CVERT,*+,OCADESC,*-
.
          IF        OTCNOPCR = 0
            DISPLAY   *P44:CVERT,KEY20:
                      *P67:CVERT,*+,DOSTATUS:
                      *P78:CVERT,KEY3
          ELSE
            MOVE      KEY20,DIM17
            DISPLAY   *P44:CVERT,DIM17:
                      *P62:CVERT,*+,DOSTATUS:
                      *P73:CVERT,KEY3
          ENDIF
          ADD       ONE,CVERT                * Go to the next available line
          SETLPTR   OCADESC
.
          PACK      KEY11,OBAOUTNO,Z15       * Get any resheduled records
          CALL      RDSRSHA1
DVIS3700  CALL      RDPRSHA1
          BRANCH    OVRCD,DVIS1000
.
          MATCH     OPRSOUTN,OBAOUTNO
          GOTO      DVIS1000 IF NOT EQUAL
.
          COMPARE   SCRNLN,CVERT                                       *C-47434
          GOTO      DVIS3900 IF LESS        * There is room on screen
.
          CALL      DSKY0000                * Line 24 keyin question
          BRANCH    ABORT,DVIS9999          * Input aborted
.
          BRANCH    FORM1,DVIS9000,DVIS3900,DVIS8500      * Exit,More,Next
.
DVIS3900  UNPACK    OPRSRDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DIM10A
          UNPACK    OPRSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          STRIP     OPRSCLIN
          SETLPTR   OPRSRTIM,5
          DISPLAY   *P16:CVERT,*EL,"Rescheduled (",*+,OPRSREAS,") from ":
                    OPRSCLIN,SP1,CPCDATE," at ",OPRSRTIM," on ",DIM10A
.
          IF        OTCNOPCR > 0
            MOVE      OPRSOPER,OPCODE 
            CALL      GETO0000              * Get the operator id description
            DISPLAY   *P77:CVERT,OPDESC
          ENDIF
.
          ADD       ONE,CVERT
          SETLPTR   OPRSRTIM
.
          GOTO      DVIS3700                 * Get the next record
.
. **************************************************
. ******* Display a Cancelled Booking Record *******
. **************************************************
.
DVIS4000  BRANCH    CANCFLAG,DVIS8000        * no more cancelled bookings
.
          PACK      KEY20,OPCNSITE,OPCNCLIN,OPCNDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OPCNSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OPCNCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,DVIS4100           * Record missing
.
.         We have the clinic record. Now set up the description
.
          CALL      CIDSC000                 * Get the clinic description
          GOTO      DVIS4200                 * Continue on with description
.
.         Missing clinic record. Set up an appropriate description
.
DVIS4100  CLEAR     OCADESC                  * Clear the description
          APPEND    "** Unknown Clinic Id ",OCADESC
          APPEND    OPCNCLIN,OCADESC         * Add unknown code to description
          APPEND    " **",OCADESC            * Trailing section of description
          RESET     OCADESC
.
DVIS4200  MOVE      SP30,TDESC               * Clear the Visit Type
          IF        OTCNDCRE = ONE
            PACK      KEY5,ANSC,ANSB,OPCNREAS * display cancellation reason
            CALL      RDCODE1
            UNPACK    OPCNRDTE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            RESET     TDESC,6 
.            RESET     TDESC,11
            APPEND    SP1,TDESC
            APPEND    CPCDATE,TDESC
            RESET     TDESC
          ENDIF
.
.         Set up cancellation date
.
          UNPACK    OPCNDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CALL      DATECONV
          MOVE      SP3,OTMAHOSP 
          PACK      KEY18,OPCNSITE,OPCNCLIN,WEEKDAY,OPCNSTRT
          CALL      RDMASA1             * Read in the session master record
.
.         Display the record
.
          MOVE      SP3,KEY3
          LOAD      KEY3,IBCNMHOS,OTMAHOSP  * load hospital code
          SETLPTR   OCADESC,27
          DISPLAY   *P1:CVERT,CPCDATE:
                    *P12:CVERT,OPCNTIME:
                    *P18:CVERT,*+,OCADESC,*-
.
          IF        OTCNOPCR = 0
            DISPLAY   *P44:CVERT,TDESC:
                      *P67:CVERT,"Cancelled":
                      *P78:CVERT,KEY3;
          ELSE
            MOVE      TDESC,DIM17
            MOVE      OPCNOPER,OPCODE 
            CALL      GETO0000              * Get the operator id description
            DISPLAY   *P44:CVERT,DIM17:
                      *P62:CVERT,"Cancelled":
                      *P73:CVERT,KEY3:
                      *P77:CVERT,OPDESC
          ENDIF
          ADD       ONE,CVERT                * Go to the next available line
          SETLPTR   OCADESC
.
          MOVE      SP8,OPCNURNO             * clear U/R Number
          CALL      RPOTCAN2                 * Get the next record
          MOVE      OVRCD,CANCFLAG           * Set Flag for end of Cancelled
.
          MATCH     VURNO,OPCNURNO           * Still the same patient ?
          GOTO      DVIS3000 IF EQUAL        * No. Finished the visits
          MOVE      ONE,CANCFLAG             * Set Flag for end of Cancelled
          GOTO      DVIS3000                 * Finish the Visits
.
.         Finished the loop over the booking records. Check what they want to
.         do now.
.
DVIS8000  DISPLAY   *P1:24," (",*V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSN,*HOFF,")ext U/R";
          IF        OTCNULET = 1
            DISPLAY   ", (",*V2LON,ANSR,*HOFF,")eferral Letters";
          ENDIF
          IF        PTCNURHA = 1
            DISPLAY   ", (",*V2LON,ANSH,*HOFF,")istory";
          ENDIF
          KEYIN     *EL," ? ",*V2LON,ANS;
          BRANCH    ABORT,DVIS9999           * Input aborted
          REP       UPPLOW,ANS
.
          MATCH     ANSE,ANS                 * Exit option ?
          GOTO      DVIS9000 IF EQUAL        * Yes. Take appropriate action
          MATCH     ANSN,ANS                 * Next option ?
          GOTO      DVIS8500 IF EQUAL        * Yes. Take appropriate action
.
.         Display   Referral Letter
.
          IF        OTCNULET = 1
            MATCH     ANSR,ANS
            GOTO      DVIS8400 IF EQUAL
          ENDIF
.
          IF        PTCNURHA = 1
            MATCH     ANSH,ANS
            GOTO      DVIS8600 IF EQUAL
          ENDIF
.
          BEEP
          GOTO      DVIS8000                 * Try to get a valid response
.
DVIS8400  DISPLAY  *P2:6,*V2LON,"Referral Letters :"
          CALL      OUTEQRFL
          DISPLAY  *P2:6,*EL
          BRANCH   EXIT,DVIS0000             * Exit back to Outpatient Enquiry
.
DVIS8500  MOVE      ZERO,EXIT                * next option selected - EXIT = 0
          GOTO      DVIS9999
.
DVIS8600  DISPLAY  *P2:6,*V2LON,"History :"
          PROC     OTHISENQ                * Display History
          BRANCH   EXIT,DVIS8500
          GOTO     DVIS0000
.
DVIS9000  MOVE      ONE,EXIT                 * Exit option selected - EXIT = 1
          GOTO      DVIS9999
.
.         The patient has no O/P visits, but they may have a referral letter,
.         so check outrf1af to see if the patient has any records.
.
DVIS9500  CALL      GRLT0000                 * referral letter on file ?
          BRANCH    RFLTFLAG,DVIS9900        * no - finished
          BRANCH    EXIT,DVIS9000            * exit completely
          GOTO      DVIS8500                 * get next u/r
.
.         This patient not an outpatient
.
DVIS9900  IF        EVERT = 7
            DISPLAY   *P1:24,*EL,*B,"U/R Number ",VURNO:
                      " not an Outpatient.  ";
          ELSE
            DISPLAY   *P1:24,*EL,*B,*V2LON:
                      "No previous Outpatient visits.  ";
          ENDIF
          CALL      HITENTER
.
DVIS9999  CLOSE     OUTCANA2
          RETURN
+
.*****************************************************************************
.*                                GRLT0000             Called by:            *
.*           See if the patient has any Outpatient Referral Letters          *
.* Returns:  RFLTFLAG  0 = referral letter records found                     *
.*                     1 = no referral letter records on file                *
.*           EXIT      (from OUTEQRFL)                                       *
.*                     0 = get next u/r                                      *
.*                     1 = leave enquiry                                     *
.*****************************************************************************
.
GRLT0000  COMPARE   ZERO,OTCNULET                * using referral letters ?
          GOTO      GRLT9500 IF EQUAL            * no - finished
.
          OPEN      OUTRF1A2,"outrf1af"                     * open file
          PACK      KEY24,VURNO,SP30
          CALL      RSOTRFL2                     * position in file
GRLT0500  CALL      RKOTRFL2                     * read next record
          BRANCH    OVRCD,GRLT9500               * eof - finished
.
          MATCH     VURNO,OTRLURNO               * same u/r still ?
          GOTO      GRLT9500 IF NOT EQUAL        * no - finished
.
          DISPLAY  *P2:6,*V2LON,"Referral Letters :"
          MOVE      "11111111",ALRTMASK
          CALL      PMIHEAD
          CALL      OUTEQRFL
          DISPLAY  *P2:6,*EL
.
          MOVE      ZERO,RFLTFLAG                * set flag for records found
          GOTO      GRLT9999
.
GRLT9500  MOVE      ONE,RFLTFLAG                 * set flag for no records
          CLOSE     OUTRF1A2
.
GRLT9999  RETURN
+
. *****************************************************************************
. * GETO0000 : Get Operator Id Description                                    *
. *****************************************************************************
GETO0000  MOVE      SP4,OPDESC
          MATCH     SP4,OPCODE
          GOTO      GETO9999 IF EQUAL
.
          BRANCH    OTCNOPCR,GETO1000,GETO5000
          GOTO      GETO9999
.
. ------- Login Description -------------------
.
GETO1000  CALL      OPPASS1
          MOVE      OPCODE,KEY4
          CALL      RDPASS1
          IF        OVRCD = 0
            MOVE      SECUSER,OPDESC
          ENDIF
          CALL      CLPASS1
.
          GOTO      GETO9999
.
. ------- User Defined (Cat O2) Description -------------------
.
GETO5000  PACK      KEY5,CODEO2,OPCODE,SP10
          CALL      RDCODE1
          IF        OVRCD = 0
            MOVE      TDESC,OPDESC
          ENDIF
.
GETO9999  RETURN
+
. *****************************************************************************
. * DSKY0000 : Line 24 Keyin                                                  *
. *            FORM1 = 1  Exit                                                *
. *                  = 2  Search More                                         *
. *                  = 3  Next Option                                         *
. *****************************************************************************
DSKY0000  MOVE      ONE,FORM1
.
          KEYIN     *P1:24," (",*V2LON,*DV,ANSE,*HOFF,")xit, (":
                    *V2LON,*DV,ANSS,*HOFF,")earch more, (":
                    *V2LON,*DV,ANSN,*HOFF,")ext ? ",*EL,*V2LON,ANS;
          REP       UPPLOW,ANS
          BRANCH    ABORT,DSKY9999          * Input aborted
.
          MATCH     ANSE,ANS                * Exit option ?
          GOTO      DSKY9999 IF EQUAL
.        
          MATCH     ANSS,ANS                * Search more option ?
          IF        @EQUAL
            MOVE      TWO,FORM1
            MOVE      EVERT,CVERT           * starting line for display
            ADD       ONE,CVERT
            DISPLAY   *P1:CVERT,*EF;        * clear the screen
            GOTO      DSKY9999
          ENDIF
.
          MATCH     ANSN,ANS                * Next option ?
          IF        @EQUAL
            MOVE      THREE,FORM1
            GOTO      DSKY9999
          ENDIF
.
          BEEP
          GOTO      DSKY0000
.
DSKY9999  RETURN
+
.
.******************************************************************************
.         CPMI0000 - Clear PMI & visit variables except PURNO & OBAURNO
.******************************************************************************
CPMI0000  MOVE      PURNO,KEY8
          CALL      CLPATMAS
          MOVE      KEY8,PURNO
..........CALL      DFNP0000                * clear national UR details
.
.-------  Clear Visit variables OBACOMM
.
CPMI1000  UNPACK    SP30,OBASRCR,OBACOMP,OBACLASS,OBAINS,OBAATTN,OBADOCT
          UNPACK    SP30,OBACAT,OBBPORT,OBBCTYP,OBBBOOK,OBAPRTY,OBARSCHD
          UNPACK    SP30,OBACOMM
          UNPACK    SP30,OBOUSR1,OBOUSR2,OBOUSR3,OBOUSR4,OBALADMN,HASCMNS
          UNPACK    SP30,OPDICODE,OTBBFUND,OTBBTBLE,OTBBDEPT
          UNPACK    SP30,OTBCRDAT
          MOVE      ZERO,OTBBPVIS
          MOVE      ZEROVISN,OBAOUTNO
          PACK      OPDIDESC,SP30,SP30
..........CALL      DCMN0000                 * Remove any existing comments
          UNPACK    SP30,OTBBTRAN,OTBBLNGI,OTBBTMFS,OTBBTMBO
          MOVE      SP20,OTBBGPFA
          MOVE      SP20,OTBBECRA
          UNPACK    SP30,OTBBRFGP,OTBBGPPC
          UNPACK    SP30,OTBBADCS,OTBBCITM,OTBBASTM,OTBBDPTM,OTBBOUTC
.
CPMI9999  RETURN
+
. *****************************************************************************
. * CIDSC000: Get the description for the current clinic id record            *
. * RETURNS    : OCADESC = Clinic description or Doctor's description         *
. *****************************************************************************
CIDSC000  MATCH     SP6,OCADOCT         * Do we have a doctors code ?
          GOTO      CIDSC500 IF EQUAL   * No. Use the description provided.
.
          MOVE      OCADOCT,KEY6        * Set up key for doctors file
          CALL      RDDOCT1             * Read the doctors code
          BRANCH    OVRCD,CIDSC999      * No doctors record found. Blank desc.
.
.         Pack up the doctors name into a "nice" format
.
          CALL      CONCATDR            * Routine found in OUTDSCLN
          PACK      OCADESC,OCACLIN,DASH,DIM40
          GOTO      CIDSC999
.
CIDSC500  PACK      DIM30,OCADESC,SP20,SP20
          PACK      OCADESC,OCACLIN,DASH,DIM30
.
CIDSC999  RETURN
+
OPEN0000
CLOS0000  RETURN
.
GETSVAR   RETURN                        * Dummy subroutine for surname searchs
+
.===============================
.    I/O   INCLUDES
.===============================
.
          INC       STD001IO
          INC       CALCAGE
          INC       CLPATMAS
          INC       DATECONV
          INC       NZIBSRCH  
          INC       NZHISIIO
          INC       OUTDSCLN
          INC       PMIHEAD
          INC       PATALERT
          INC       OUTEQRFL                * Referral letter display
          INC       BOKRC1IO/INC            * booking file
          INC       OUTCANIO/INC
          INC       OUTDIAIO/INC            * OP diagnosis file
          INC       OUTBOAIO/INC            * bookings file
          INC       OUTBB1IO/INC            * bookings file
          INC       OUTBOCIO/INC            * bookings file
          INC       OUTCLIIO/INC            * OP clinic file
          INC       OUTCTYIO/INC            * OP clinic type
          INC       OUTMA1IO/INC            * OP Clinic Master
          INC       OUTRF1IO/INC
          INC       OUTRSHIO/INC            * reschedule        file
          INC       OUTSITIO/INC            * OP site file
          INC       OUTPREIO/INC            * OP pre-attendance file
          INC       PATALRIO/INC            * alerts file
          INC       PATCOMN1/PBL            * holds KURN
          INC       NHIMASIO/INC
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       PATMA1IO/INC            * patient master file
          INC       PATMI1IO/INC            * admission file
          INC       PATPR1IO/INC            * pre-admission file
          INC       PATGSRIO/INC            * surname file
          INC       PATSNDX/PBL             * ? for surnames only
          INC       PATSNX2/PBL             * ? for surnames/given names
          INC       PATDNRIO/INC
          INC       PATSDNIO/INC
          INC       PATSMWIO/INC
          INC       PATNIDIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       IBAPASIO/INC
          INC       OUTHISIO/INC
          INC       OTHISENQ    
AGECHK    RETURN
.

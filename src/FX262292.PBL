.***************************************************************************
.* System    :   Inpatient, Outpatient, Allied Health and Interpreters     *
.* Program   :   FX262292                                                  *
.* Desc      :   Fix program for wahealth visit based interpreter changes  *
.***************************************************************************
.* Date      :   24/01/2012                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   Option 1 - This will populate the language fields for all *
.*           :              interpreter audit file records with the current*
.*           :              language from the pmi - visiauaf               *
.*           :                                                             *
.*           :   Option 2 - This will populate the visit extension file    *
.*           :              interpreter required for all waiting and       *
.*           :              active referrals according to the pmi          *
.*           :              interpreter required                           *
.*           :                                                             *
.*           :   Option 3 - This will populate the appointment request     *
.*           :              interpreter required for all outstanding       *
.*           :              requests according to the pmi interpreter      *
.*           :              required                                       *
.*           :                                                             *
.*           :   Option 4 - This will populate the visit extension file    *
.*           :              interpreter required for all future OP bookings*
.*           :              according to the pmi interpreter required.     *
.*           :              It will also add the visit the the interpreter *
.*           :              tables.                                        *
.***************************************************************************
.* Modifications  :                                                        *
.* V10.03.01 11/12/2013  Ebon Clements     CAR 293092                      *
.*           Added notified and confirmed to interpreter audit             *
.* V10.03.00 24/01/2013  Ebon Clements CAR 262292                          *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
          INC       BOKRC1FD/INC
          INC       EMRVISFD/INC
          INC       OUTARTFD/INC
          INC       OUTBOAFD/INC
          INC       OUTCANFD/INC
          INC       OUTSITFD/INC
          INC       OUTSESFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATVISFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       VISIAUFD/INC     
          INC       VISINTFD/INC     
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CFILEPRE  DIM       3
CURRDATE  DIM       8
KSECONDS  INIT      ":00"
PREVSITE  DIM       3
RECNUM    FORM      8
SESSKEYS  DIM       25
URNUMBER  DIM       8
.
PRGID     INIT      "FX262292"
PRGNAM    INIT      "Visit Based Interpreter Data Load"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    BRANCH    COPTION,ML3000,ML4000,ML5000,ML6000
          GOTO      ML0100
.
ML3000    CALL      LAUD0000               * Load audit file language fields
          GOTO      ML0100
.
ML4000    CALL      LREF0000               * Load referral interpreter fields
          GOTO      ML0100
.
ML5000    CALL      LREQ0000               * Load appointment request 
          GOTO      ML0100                 * interpreter fields
.
ML6000    CALL      LOUT0000               * Load future OP appointment
          GOTO      ML0100                 * interpreter fields - all OP sites
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P60:24,"allrefaf"
          OPEN      ALLREFA3,"allrefaf"
.
          DISPLAY   *P60:24,"emrvisaf"
          OPEN      EMRVISA1,"emrvisaf"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P60:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P60:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"visiauaf";
          OPEN      VISIAUA1,"visiauaf"
.
          DISPLAY   *P64:24,"visintaf";
          OPEN      VISINTA1,"visintaf"
.
          DISPLAY   *P60:24,"outartaf"
          OPEN      OUTARTA2,"outartaf"
.
          DISPLAY   *P60:24,"outsitaf"
          OPEN      OUTSITA1,"outsitaf"
          OPEN      OUTSITA3,"outsitaf"
.
          MOVE      "out",CFILEPRE          * Save Current File Prefix
          CALL      OPNOUT00                * Open Outpatient Files
.
INIT9999  RETURN
+
.****************************************************************************
. Open Outpatient Files
.****************************************************************************
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBOKA6  * Get the name of the BOKA6 file
          CLOSE     OUTBOKA6
          OPEN      OUTBOKA6,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILBOKA1  * Get the name of the BOKA1 file
          CLOSE     OUTBOKA1
          OPEN      OUTBOKA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCANA1
          CLOSE     OUTCANA1
          OPEN      OUTCANA1,CFNAME
.
          PACK      CFNAME,CFILEPRE,FILSESA6  * Get the name of the SESA6 file
          CLOSE     OUTSESA6
          OPEN      OUTSESA6,CFNAME           * Open the file
.

OPNOUT99  RETURN
+
.*******************************************************************************
. COTFIL00 - Check that the correct outpatient site prefix files are open
.            according to the site in the outpatient case key store in the  
.            sub system key                                                     
.*******************************************************************************
COTFIL00  MATCH     SP70,VSIASUBK            * Does the visit have a site
          GOTO      COTFIL50 IF NOT EQUAL
.
COTFIL10  MATCH     "out",CFILEPRE           * Is out currently open
          IF        !@EQUAL
            MOVE      "out",OSTFILE          * open default prefix of out
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open out Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
COTFIL50  MOVE      VSIASUBK,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,COTFIL10           * error invalid site code
.
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open prefixed Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
COTFIL99  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Load interpreter audit language ":
                    "fields from the PMI.":
                    *P1:6,*V2LON,TWO,*HOFF,". Load interpreter required for ":
                    "all waiting and active referrals from the PMI.":
                    *P1:7,*V2LON,THREE,*HOFF,". Load interpreter required for ":
                    "outstanding appointment requests from the PMI.":
                    *P1:8,*V2LON,FOUR,*HOFF,". Load interpreter required for ":
                    "future OP appointments from the PMI.";
.
OPTN0500  KEYIN     *P1:10,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:10,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000:   * run clean-up
                            OPTN9000,OPTN9000
. 
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               LAUD0000              Called by: ML3000  *
.*                                                                        *
.**************************************************************************
LAUD0000  MOVE      ZERO,RECNUM
.
          PACK      KEY25,SP70
          CALL      RSVSIAU1
LAUD1000  CALL      RKVSIAU1
          BRANCH    OVRCD,LAUD9999
.
          ADD       ONE,RECNUM
.
          IF        (RECNUM%1000) = 0 | RECNUM = 1
            DISPLAY   *P15:20,*V2LON,"Records Processed : ",RECNUM
          ENDIF
.  
          MATCH     SP70,VSIALNG1                * Skip if language 1 populated
          GOTO      LAUD1000 IF NOT EQUAL
.
          BRANCH    VSIATYPE,LAUD2000,LAUD3000,LAUD4000
          GOTO      LAUD1000
.
LAUD2000  PACK      KEY8,VSIAVISN,SP70
          CALL      RDEMVIS1                     * Read ED visit to get U/R
          BRANCH    OVRCD,LAUD1000
.
          MOVE      EMVIURNO,URNUMBER
          GOTO      LAUD6000
.
LAUD3000  CALL      COTFIL00                     * Open correct outpatient files
.
          PACK      KEY36,VSIAVISN,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,LAUD3100
.
          MATCH     VSIAVISN,DOBAOUTN
          GOTO      LAUD3100 IF NOT EQUAL
.
          MOVE      OBAURNO,URNUMBER
          GOTO      LAUD6000
.
LAUD3100  PACK      KEY8,VSIAVISN,SP70
          CALL      RDOTCAN1
          BRANCH    OVRCD,LAUD1000
.
          MOVE      OPCNURNO,URNUMBER
          GOTO      LAUD6000
.
LAUD4000  PACK      KEY8,VSIAVISN,SP70
          CALL      RDMISS1                      * Read IP Adm to get U/R
          BRANCH    OVRCD,LAUD4100
.
          MOVE      AURNO,URNUMBER  
          GOTO      LAUD6000
.
LAUD4100  PACK      KEY8,VSIAVISN,SP70
          CALL      RDBKREC1                     * Read Booking to get U/R
          BRANCH    OVRCD,LAUD1000
.
          MOVE      BKREURNO,URNUMBER
          GOTO      LAUD6000
.
LAUD6000  PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1                      * Read PMI to get language
          BRANCH    OVRCD,LAUD1000
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,LAUD1000
.         
          MATCH     SP70,PMPXLNG1                * Skip if no language 1
          GOTO      LAUD1000 IF EQUAL
.
          MOVE      PMPXLNG1,VSIALNG1
          MOVE      PMPXLNG2,VSIALNG2
          MOVE      PMPXLNG3,VSIALNG3
.
          CALL      UPVSIAU1
          GOTO      LAUD1000
.
LAUD9999  RETURN
+
.**************************************************************************
.*                               LREF0000              Called by: ML4000  *
.*                                                                        *
.**************************************************************************
LREF0000  MOVE      ZERO,RECNUM
.
          PACK      KEY17,ZERO,SP70
          CALL      RSALREF3
LREF1000  CALL      RKALREF3
          BRANCH    OVRCD,LREF9999
.
          MATCH     "0",ALRESTAT
          IF        !@EQUAL
            MATCH     "1",ALRESTAT
            GOTO      LREF9999 IF NOT EQUAL      * Waiting and active referrals
          ENDIF
.
          ADD       ONE,RECNUM
.
          IF        (RECNUM%1000) = 0 | RECNUM = 1
            DISPLAY   *P15:20,*V2LON,"Records Processed : ",RECNUM
          ENDIF
.
          PACK      KEY8,ALREURNO,SP70
          CALL      RDMAST1                      * Read PMI to get language
          BRANCH    OVRCD,LREF1000               * and interpreter required
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,LREF1000
.
          MATCH     "1",PMPXINTR                 * PMI interpreter required yes
          GOTO      LREF1000 IF NOT EQUAL        * if not skip
.
          MATCH     SP70,PMPXLNG1                * PMI language blank so skip  
          GOTO      LREF1000 IF EQUAL       
.
          PACK      KEY8,ALREVISN,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,LREF1000
.
          MATCH     "1",PMVXINTR                 * Skip visit interpreter 
          GOTO      LREF1000 IF EQUAL            * already set to yes
.
          MOVE      "1",PMVXINTR                 * Set visit interpreter to yes
          MOVE      PMPXLNG1,PMVXLNG1            * Set language from pmi
.
          CALL      UPPMVX11
          GOTO      LREF1000
.
LREF9999  RETURN
+
.**************************************************************************
.*                               LREQ0000              Called by: ML5000  *
.*                                                                        *
.**************************************************************************
LREQ0000  MOVE      ZERO,RECNUM
.
          PACK      KEY33,ZERO,SP70
          CALL      RSOTART2
LREQ1000  CALL      RKOTART2
          BRANCH    OVRCD,LREQ9999
.
          MATCH     "0",OTARSTAT                 * Requested only
          GOTO      LREQ9999 IF NOT EQUAL
.
          ADD       ONE,RECNUM
.
          IF        (RECNUM%1000) = 0 | RECNUM = 1
            DISPLAY   *P15:20,*V2LON,"Records Processed : ",RECNUM
          ENDIF
.
          PACK      KEY8,OTARURNO,SP70
          CALL      RDMAST1                      * Read PMI to get language
          BRANCH    OVRCD,LREQ1000               * and interpreter required
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,LREQ1000
.
          MATCH     "1",PMPXINTR                 * PMI interpreter required yes
          GOTO      LREQ1000 IF NOT EQUAL        * if not skip
.
          MATCH     SP70,PMPXLNG1                * PMI language blank so skip  
          GOTO      LREQ1000 IF EQUAL       
.
          MATCH     "1",OTARINTR                 * Skip request interpreter
          GOTO      LREQ1000 IF EQUAL            * already set to yes
.
          MOVE      "1",OTARINTR                 * Set request interpreter yes
          MOVE      PMPXLNG1,OTARLNG1            * Set language from pmi
.
          CALL      UPOTART2
          GOTO      LREQ1000
.
LREQ9999  RETURN  
+
.**************************************************************************
.*                               LOUT0000              Called by: ML6000  *
.*                                                                        *
.**************************************************************************
LOUT0000  MOVE      ZERO,RECNUM
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      SP70,PREVSITE           
.
          PACK      KEY9,SP70
          CALL      RDSSITA3                 * Loop all site file prefixes
LOUT1000  CALL      RDKSITA3
          BRANCH    OVRCD,LOUT9999
.
          MATCH     SP70,PREVSITE
          GOTO      LOUT1500 IF EQUAL
.
          MATCH     PREVSITE,OSTFILE
          GOTO      LOUT1000 IF EQUAL
          
LOUT1500  MOVE      OSTFILE,PREVSITE         * Save current site file prefix
.
          MATCH     OSTFILE,CFILEPRE
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open prefixed Outpatient Files
          ENDIF
.
          PACK      KEY25,OSESITE,CURRDATE,SP70
          CALL      RDSSESA6
LOUT2000  CALL      RDKSESA6
          BRANCH    OVRCD,LOUT1000
.
          MATCH     OSESITE,OSESITE
          GOTO      LOUT1000 IF NOT EQUAL
.
          PACK      SESSKEYS,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
.
          PACK      KEY28,SESSKEYS,SP70
          CALL      RDSBOKA1
LOUT3000  CALL      RDKBOKA1       
          BRANCH    OVRCD,LOUT2000 
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          MATCH     SESSKEYS,KEY25
          GOTO      LOUT2000 IF NOT EQUAL        * different session
.
          COMPARE   ONE,OBASTAT                  * Booked only
          GOTO      LOUT3000 IF NOT EQUAL
.
          ADD       ONE,RECNUM
.
          IF        (RECNUM%1000) = 0 | RECNUM = 1
            DISPLAY   *P15:20,*V2LON,"Records Processed : ",RECNUM
          ENDIF
.
          PACK      KEY8,OBAURNO,SP70
          CALL      RDMAST1                      * Read PMI to get language
          BRANCH    OVRCD,LOUT3000               * and interpreter required
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,LOUT3000
.
          MATCH     "1",PMPXINTR                 * PMI interpreter required yes
          GOTO      LOUT3000 IF NOT EQUAL        * if not skip
.
          MATCH     SP70,PMPXLNG1                * PMI language blank so skip  
          GOTO      LOUT3000 IF EQUAL       
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,LOUT3000
.
          MATCH     "1",PMVXINTR                 * Skip visit interpreter
          GOTO      LOUT3000 IF EQUAL            * already set to yes
.
          MOVE      "1",PMVXINTR                 * Set visit interpreter to yes
          MOVE      PMPXLNG1,PMVXLNG1            * Set language from pmi
.
          CALL      UPPMVX11
.
          CALL      INTUPD00                     * Interpreter Table Processing
.
          GOTO      LOUT3000
.
LOUT9999  RETURN
+
.*******************************************************************************
. Update Interpreter Table
.*******************************************************************************
INTUPD00  MOVE      OBAOUTNO,VSINVISN
          MOVE      OBADATE,VSINDATE
          PACK      VSINTIME,OBATIME,KSECONDS
          MOVE      "2",VSINTYPE
          PACK      VSINSUBK,OBASITE,OBACLIN,OBADATE,OBASTRT,DOBASLOT,SP70
          MOVE      SP70,VSINWUID
          MOVE      ZERO,VSINNOTF
          MOVE      SP70,VSINUDC1
          MOVE      SP70,VSINUDC2
          MOVE      SP70,VSINUDC3
          MOVE      SP70,VSINUDC4
          MOVE      SP70,VSININTP
          MOVE      SP70,VSINCHON
          MOVE      ZERO,VSINCONF
          MOVE      SP70,VSINSPAR
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RAVSINT1
          IF        OVRCD=1
            CALL      WRVSINT1
            CALL      INTAUD00
          ENDIF
.
INTUPD99  RETURN
+
.*******************************************************************************
. Update Interpreter Audit Table
.*******************************************************************************
INTAUD00  PACK      VSIACDAT,CCC,CYY,CMM,CDD
          REP       " 0",VSIACDAT
          MOVE      CTIMEIS,VSIACTIM
          MOVE      "A",VSIARTYP
.
          MOVE      VSINVISN,VSIAVISN
          MOVE      VSINDATE,VSIADATE
          MOVE      VSINTIME,VSIATIME
          MOVE      VSINTYPE,VSIATYPE
          MOVE      VSINSUBK,VSIASUBK
          MOVE      VSINWUID,VSIAWUID
          MOVE      PMPXLNG1,VSIALNG1
          MOVE      PMPXLNG2,VSIALNG2
          MOVE      PMPXLNG3,VSIALNG3
          MOVE      VSINNOTF,VSIANOTF
          MOVE      VSINCONF,VSIACONF
.
          PACK      KEY25,VSIACDAT,VSIACTIM,VSIARTYP,VSIAVISN,SP70
          CALL      RAVSIAU1
          IF        OVRCD=1
            CALL      WRVSIAU1
          ENDIF
.
INTAUD99  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLREFIO/INC
          INC       BOKRC1IO/INC
          INC       EMRVISIO/INC
          INC       OUTARTIO/INC
          INC       OUTBOAIO/INC
          INC       OUTCANIO/INC
          INC       OUTSESIO/INC
          INC       OUTSITIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       VISIAUIO/INC     
          INC       VISINTIO/INC     

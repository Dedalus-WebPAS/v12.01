.*****************************************************************************
.*    Program      : IBAWAT66                                                *
.*    Description  : Waiting List Security Master Listing                    *
.*                                                                           *
.*    Author       : ROD     (17/12/92)                                      *
.*                                                                           *
.*    Modifications: V5.01.02  18/01/93  ROD                                 *
.*                             Fix printing problem when no records on file  *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       IBAPASFD/INC
.
CONSNAME  DIM       25
DESC15    DIM       15
DESC17    DIM       17
DESC18    DIM       18
DESC20    DIM       20
DESC25    DIM       25
DESC37    DIM       37
DIM3      DIM       3
DOCCODE   DIM       6
ENDCODE   DIM       6
FNDREC    FORM      1
LTYPDSC   DIM       15
PENDESC   DIM       25
PENDCODE  DIM       6
PSTDCODE  DIM       6
PSTDESC   DIM       25
PTCNDCQS  FORM      1
SAVCONS   DIM       6
SAVLTYP   DIM       3
SAVUSER   DIM       4
STATDSC   DIM       15
STDCODE   DIM       6
.
PRGID     INIT      "IBAWAT66"
PRGNAM    INIT      "Waiting List Security Master Listing"
VERSION   INIT      "V12.01.00"
+
.****************************************************************************
.*  ML0000  :  Mainline code                                                *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    MOPTION,ML3000,ML3000
          GOTO      ML9999                        * Exit
.
ML3000    CALL      CONTQST                       * Ok to Continue?
          BRANCH    CEXIT,ML5000,ML1000,ML1000
.
ML5000    CALL      RPRT0000                      * Print report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.*  INIT0000  :  Initialisation                                             *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"watseaaf"
          OPEN      WATSEAA1,"watseaaf"
          DISPLAY   *P60:24,"watseiaf"
          OPEN      WATSEIA1,"watseiaf"
          OPEN      WATSEIA2,"watseiaf"
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P60:24,"ihapassf"
          CALL       OPPASS1
.
          DISPLAY   *P60:24,*EL,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.****************************************************************************
.*  MENU0000  :  Main Menu                                                  *
.****************************************************************************
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = by User Code":
                    *P1:6,*V2LON,"2",*HOFF," = by Waiting List":
                    *P1:8,"Select Option : "
.
MENU1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9999 IF EQUAL
.
          BRANCH    MOPTION,MENU9999,MENU9999
.
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.*  RPRT0000  :  Print report                                               *
.****************************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO                     * force new page
          MOVE      SP6,SAVCONS
          MOVE      ZERO,FNDREC
.
. firstly, print ALL Access
.
          PACK      KEY4,SP4,SP10
          CALL      RSWTSEA1
RPRT0500  CALL      RKWTSEA1                      * get next record
          BRANCH    OVRCD,RPRT3000                * no more records
.
          MOVE      ONE,FNDREC                    * we have a record
.
          COMPARE   "55",CLNO                     * need new page?
          GOTO      RPRT1000 IF LESS
.
          CALL      HEAD80                        * set up for new page
          PRINT     *N,*1,"ALL Access",*1,"ALL Access"
          CALL      UND80
          PRINT     *1,"| User    User Name",*80,"|"
          CALL      UND80
          MOVE      TEN,CLNO
.
RPRT1000  MOVE      "<< Missing Code >>",SECUSER
          PACK      KEY4,WTSAUSER
          CALL      RDPASS1
          PRINT     *1,"| ",SECODE,SP4,SECUSER,*80,"|"
          ADD       ONE,CLNO                      * increment line counter
          GOTO      RPRT0500
.
. --- now print by USER or WL ---
.
RPRT3000  IF        FNDREC=1
            CALL      UND80
          ELSE
            CALL      HEAD80
          ENDIF
          BRANCH    MOPTION,RPRT3050,RPRT5000
.
. report by User code
. 
RPRT3050  PRINT     *N,*N,*1,"by User Code",*1,"by User Code"
          CALL      UND80
          PRINT     *1,"| User  User Name",*30,"| Consultant":
                    *57,"| List Type",*80,"|"
          CALL      UND80
          ADD       FIVE,CLNO
          MOVE      SP4,SAVUSER
.
          PACK      KEY13,SP20
          CALL      RSWTSEI2
RPRT3100  CALL      RKWTSEI2                      * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
          COMPARE   "55",CLNO                     * need new page?
          GOTO      RPRT4000 IF LESS
.
          CALL      UND80
          CALL      HEAD80                        * set up for new page
          PRINT     *N,*1,"by User Code",*1,"by User Code"
          CALL      UND80
          PRINT     *1,"| User  User Name",*30,"| Consultant":
                    *57,"| List Type",*80,"|"
          CALL      UND80
          MOVE      TEN,CLNO
          MOVE      SP4,SAVUSER
.
RPRT4000  MOVE      "<< Missing Code >>",SECUSER
          PACK      KEY4,WTSIUSER
          CALL      RDPASS1
          MOVE      SECUSER,DESC20
.
          MOVE      "<< Missing Code >>",PACFNAME
          PACK      KEY6,WTSICONS
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DGNAME,PACGNAME
            MOVE      DSNAME,PACSNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
          ENDIF
          MOVE      PACFNAME,DESC17
.
          MOVE      "<< Missing Code >>",TDESC
          PACK      KEY5,ANSW,ANSU,WTSILTYP
          CALL      RDCODE1
          MOVE      TDESC,DESC15
.
          MATCH     SECODE,SAVUSER
          IF        @EQUAL
            PRINT     *1,"| ",*30,"| ",WTSICONS,SP1:
                      DESC17,*57,"| ",WTSILTYP,SP1,DESC15,*80,"|"
          ELSE
            PRINT     *1,"| ",SECODE,SP2,DESC20,*30,"| ",WTSICONS,SP1:
                      DESC17,*57,"| ",WTSILTYP,SP1,DESC15,*80,"|"
          ENDIF
          ADD       ONE,CLNO                      * increment line counter
          MOVE      SECODE,SAVUSER
          GOTO      RPRT3100
.
. report by Waiting list
.
RPRT5000  PRINT     *N,*1,"by Waiting List",*1,"by Waiting List"
          CALL      UND80
          PRINT     *1,"| Consultant",*29,"| List Type":
                    *51,"| User  User Name",*80,"|"
          CALL      UND80
          ADD       FIVE,CLNO
          MOVE      SP6,SAVCONS
          MOVE      SP3,SAVLTYP
.
          PACK      KEY13,SP20
          CALL      RSWTSEI1
RPRT5100  CALL      RKWTSEI1                      * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
          COMPARE   "55",CLNO                     * need new page?
          GOTO      RPRT6000 IF LESS
.
          CALL      UND80
          CALL      HEAD80                        * set up for new page
          PRINT     *N,*1,"by Waiting List",*1,"by Waiting List"
          CALL      UND80
          PRINT     *1,"| Consultant",*29,"| List Type":
                    *51,"| User  User Name",*80,"|"
          CALL      UND80
          MOVE      TEN,CLNO
          MOVE      SP6,SAVCONS
          MOVE      SP3,SAVLTYP
.
RPRT6000  MOVE      "<< Missing Code >>",SECUSER
          PACK      KEY4,WTSIUSER
          CALL      RDPASS1
          MOVE      SECUSER,DESC20
.
          MOVE      "<< Missing Code >>",PACFNAME
          PACK      KEY6,WTSICONS
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DGNAME,PACGNAME
            MOVE      DSNAME,PACSNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
          ENDIF
          MOVE      PACFNAME,DESC18
.
          MOVE      "<< Missing Code >>",TDESC
          PACK      KEY5,ANSW,ANSU,WTSILTYP
          CALL      RDCODE1
          MOVE      TDESC,DESC15
.
          MATCH     WTSICONS,SAVCONS
          GOTO      RPRT6500 IF NOT EQUAL
          MATCH     WTSILTYP,SAVLTYP
          GOTO      RPRT6500 IF NOT EQUAL
          PRINT     *1,"| ",*29,"|",*51,"| ",SECODE,SP2,DESC20,*80,"|"
          ADD       ONE,CLNO                      * increment line counter
          GOTO      RPRT5100
.
RPRT6500  PRINT     *1,"| ",WTSICONS,SP1,DESC18,*29,"| ",WTSILTYP,SP1:
                    DESC15,*51,"| ",SECODE,SP2,DESC20,*80,"|"
          ADD       ONE,CLNO                      * increment line counter
          MOVE      WTSICONS,SAVCONS
          MOVE      WTSILTYP,SAVLTYP
          GOTO      RPRT5100
.
RPRT9000  CALL      UND80                         * print underlines
          PRINT     *N,*1,"*** End of report ***" 
.
RPRT9999  RETURN
+
.****************************************************************************
.*  PTAB0000  :  Print Table Heading                                        *
.****************************************************************************
PTAB0000  CALL      UND132
          PRINT     *1,"|",*36,"|",*58,"|",*98,"|WTime|WTime|",*132,"|":
                    *N,*1,"| Consultant",*36,"| List Type",*58,"| Description":
                    *98,"|Elec.| D.C.| Status",*132,"|"
          CALL      UND132
.
PTAB9999  RETURN
+
.
          INC       STD001IO
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       IBAPASIO/INC

.*****************************************************************************
.*    Program      : IBAMEH53                                                *
.*    Description  : Inpatient by Hospital Report                            *
.*                                                                           *
.*    Author       : ROD    (09/12/91)                                       *
.*    Function     : List all Mental Health patients who are currently       *
.*                   in hospital or on leave.                                *
.*                                                                           *
.*    Modifications:                                                         *
.*                                                                           *
.*        V12.00.01 30/05/2025  Don Nguyen      TSK 0955096                  *
.*                  Alphanumeric visit number changes                        *
.*        V10.05.01 01/01/2015  Ania P            CAR 261630                 *
.*                  Removed use of PORT for temp file naming                 *
.*        V10.02.01 25/06/2011 Steve Armstrong    CAR 240722                 *
.*                  Mods to cater for changes to PATLOCFD.                   *
.*                       - LSNAME & LGNAME extended to 40 chars.             *
.*                       - PTLCGNM2 added.                                   *
.*****************************************************************************
.*        V10.01.02 10/02/2011  Jill Parkinson CAR 233088                    *
.*                  Added pmsvx1af                                           *
.*        V10.00.01 31/03/2010  Mike Laming  CAR 217575                      *
.*                  Recompiled for changes to MEHDIAFD                       *
.*****************************************************************************
.*        V9.02.02  17/01/2003  Jill Habasque SRF 35330                      *
.*                  Fixed printing of Commit Date                            *
.*        V9.02.01  15/01/2003  Jill Habasque SRF 35330                      *
.*                  Fixed REPDATE to include century                         *
.*        V9.02.00  25/10/2002  Lina Vo     SRF 22733                        *
.*                  Ported to V9                                             *
.*****************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                *
.*                  Changed Health Fund variables to be 8 chars              *
.*        V5.08.B01  30/12/1999  Steve Armstrong                             *
.*                   Fixed global recompile bugs                             *
.*****************************************************************************
.*       V5.07.B02 14/01/1999  Jim Stathopoulos                              *
.*                 Dates fixed for Report                                    *
.*        V5.07.00 27/10/1998  Davin                                         *
.*                 Mods for 507                                              *
.*****************************************************************************
.*        V5.05.01 22/09/1997  Steve Armstrong                               *
.*                 Mods for AXIS V to be a free text description.            *
.*                 (MEHRVDIA)                                                *
.*****************************************************************************
.*        V5.04.02  10/01/1997  Steve Armstrong  MEH Changes                 *
.*                  Mods to cater for new CMH MHVISTAT statuses              *
.*        V5.04.01  07/11/1996  Howellsy                                     *
.*                  Added Review Time & Legal Status Time.                   *
.*****************************************************************************
.*                   V5.01.002  27/02/92  ROD                                *
.*                              Made Transfer time part of key to temp file  *
.*                   V5.01.003  07/05/92  ROD                                *
.*                              Fixed Report heading for Surname seq.        *
.*                   V5.01.004  15/06/92  ROD                                *
.*                              Changed program to use Day file.             *
.*                   V5.01.005  24/06/92  ROD                                *
.*                              Dont use day file.                  Shit!    *
.*                   V5.01.006  24/06/92  ROD                                *
.*                              Use current date , dont keyin a date         *
.*                              23/07/92  ROD                                *
.*                              Use latest legal status not PTMXLS           *
.*                   V5.01.007  29/07/92  ROD                                *
.*                              Dont use day file.                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       MEHVI1FD/INC
          INC       MEHDIAFD/INC
          INC       MEHDS1FD/INC
          INC       MEHLEGFD/INC
          INC       PATTRNFD/INC
          INC       PATDAYFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATDSCFD/INC
          INC       PATLOCFD/INC
          INC       PATCODFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
CMDLINE   DIM       80
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMONDAY   DIM       4
DRDATE    DIM       10
DSADMD    DIM       8 
DSRECD    DIM       8
DSDOB     DIM       8
CURDATE   DIM       8
DAYFILE   DIM       8
FNAME     DIM       8
FNDTL     FORM      1
LVETMP    DIM       3
NOABS     FORM      4
NOHOL     FORM      4
NOHOS     FORM      4
NOSHL     FORM      4
NOTRL     FORM      4
REPDATE   DIM       8
REPSEQ    DIM       19
SAVKEY30  DIM       30
SAVLTYP   DIM       3
SAVTDATE  DIM       8
SAVWARD   DIM       3
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
. Temp file vars
MEHTMPA1  IFILE     ASCII, FIXED=111, KEY="U9-40,1-8,103-110"
MEHTMPA2  IFILE     ASCII, FIXED=111, KEY="U41-43,99-99,9-40,1-8,103-110"
.
XXURNO    DIM       8         1       U/R Number
XXNAME    DIM       32        9       Patients Name
XXWARD    DIM       3        41       Ward code
XXCOND    DIM       3        44       Patient condition
XXLSCOD   DIM       3        47       Legal Status code
XXRECD    DIM       8        50       Reception date
XXSEX     DIM       1        58       Sex
XXAGE     FORM      3        59       Age in years
XXRELIG   DIM       3        62       Religion
XXADMD    DIM       8        65       Admission date
XXLSTAY   FORM      5        73       Length of stay
XXLLEAV   FORM      5        78       Length of leave
XXADOCT   DIM       16       83       Attending Doctor
DXXBSTAT  DIM       1        99       Bed status
XXVRES    DIM       3       100       Visits restricted?
XXTIME    DIM       8       103       Transfer time (to make key unique)
.              Rec length   111
XXBSTAT   FORM      1
.
PRGID     INIT      "IBAMEH53"
PRGNAM    INIT      "Inpatients By Hospital Report"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
          CALL      CRET0000                      * create temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML1000,ML9999    * Yes, No, Cancel
.
ML5000    CALL      LOAD0000                      * load data into temp file
.
          CALL      RPRT0000                      * Print Report
          CALL      CRET0000                      * erase temp file
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"mehdiaaf"
          OPEN      MEHDIAA1,"mehdiaaf"
.
          DISPLAY   *P60:24,"mehlegaf"
          OPEN      MEHLEGA1,"mehlegaf"
.
          DISPLAY   *P60:24,"mehds1af"
          OPEN      MEHDS1A1,"mehds1af"
.
          DISPLAY   *P60:24,"mehvi1af"
          OPEN      MEHVI1A1,"mehvi1af"
          OPEN      MEHVI1A2,"mehvi1af"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P60:24,"patlocaf"
          OPEN      PATLOCA1,"patlocaf"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P60:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAME
.
          PACK      REPDATE,CCC,CYY,CMM,CDD          * get current date
          REP       " 0",REPDATE
          PACK      CURDATE,CCC,CYY,CMM,CDD      * get current date as dim 8
          REP       " 0",CURDATE
.
          PACK      DRDATE,CCC,CYY,CMM,CDD        * format date for display
          UNPACK    DRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Surname":
                    *P1:6,*V2LON,"2",*HOFF," = By Ward/Bed Status"
.
MENU1000  KEYIN     *P1:8,"Select Option ? ":
                    *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU8000,MENU8200
          BEEP
          GOTO      MENU1000
.
MENU8000  MOVE      "(Surname Sequence)",REPSEQ
          GOTO      MENU9999
.
MENU8200  MOVE      "(Ward Sequence)",REPSEQ
.
MENU9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  DISPLAY   *P30:24,"Printing...";
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      TEN,CLNO                      * line counter
          MOVE      "&&&",SAVWARD                 * init to rubbish
          CALL      ISUM0000                      * initialise summary data
.
          CALL      HEAD132
          PRINT     *40,"Report Date : ",DRDATE,SP2,REPSEQ,*N
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
. Now loop thru temp file depending on sequence selected and print data
.
          BRANCH    OPTION,RPRT0900,RPRT1100
.
. Surname sequence
.
RPRT0900  PACK      KEY48,SP30,SP20
          CALL      RSTEMP1
RPRT1000  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT8000                * no more records
          GOTO      RPRT1500
.
. Ward/Bed sequence
.
RPRT1100  PACK      KEY52,SP30,SP30
          CALL      RSTEMP2
RPRT1200  CALL      RKTEMP2
          BRANCH    OVRCD,RPRT8000                * no more records
.
          MATCH     XXWARD,SAVWARD                * still same ward?
          GOTO      RPRT1500 IF EQUAL
.
          MATCH     "&&&",SAVWARD                 * Start of report?
          GOTO      RPRT1250 IF EQUAL
.
          CALL      UND132
          CALL      PSUM0000                      * Print Summary details
          CALL      ISUM0000                      * initialise summary stats
.
RPRT1250  CALL      HEAD132
          PRINT     *40,"Report Date : ",DRDATE,SP2,REPSEQ,*N
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
          MOVE      XXWARD,SAVWARD                * save the ward
          GOTO      RPRT2000
.
RPRT1500  COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT2000 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          PRINT     *40,"Report Date : ",DRDATE,SP2,REPSEQ,*N
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
RPRT2000  UNPACK    XXRECD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,DSRECD               * display var (Reception date)
.
          UNPACK    XXADMD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,DSADMD                 * display var (Adm Date)
.
          LOAD      DXXBSTAT,XXBSTAT,ANSO,ANSL,ANSH,ANST,ANSW
.
          PRINT    *1,"|",XXURNO,*11,"|",XXNAME,*45,"| ",XXWARD,*50,"| ",XXCOND:
                   *56,"| ",XXLSCOD,*62,"|",DSRECD,*71,"| ",XXSEX," |",XXAGE:
                   "|",XXRELIG,"|",DSADMD,"|",XXLSTAY,"|",XXLLEAV,"|",XXADOCT:
                   *121,"| ",DXXBSTAT,*126,"| ",XXVRES,*132,"|",*N:
                   *1,"|",*11,"|",*45,"|",*50,"|":
                   *56,"| ",*62,"|",*71,"|",*75,"|":
                   *79,"|",*83,"|",*92,"|",*98,"|",*104,"|":
                   *121,"|",*126,"|",*132,"|"
.
          CALL      USUM0000                      * update summary statistics
          ADD       TWO,CLNO
          BRANCH    OPTION,RPRT1000,RPRT1200      * get next record
.
. we have finished printing patient data so print end of report details
.
RPRT8000  CALL      UND132 
          CALL      PSUM0000                      * Print Summary details
          PRINT     *N,"*** End of Report ***"
.
RPRT9999  RETURN
+
.***************************************************************************
.*  LOAD0000  :  Load data into temp file                                  *
.***************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading data... [        ]"
.
. loop thru the day file for date entered
.
          PACK      KEY9,ONE,SP10
          CALL      RSMHVIS2
LOAD1000  CALL      RKMHVIS2
          BRANCH    OVRCD,LOAD9000
.
          COMPARE   MHVISTAT,SIX                 * ignore cmh records
          GOTO      LOAD1000 IF LESS
.
          DISPLAY   *P47:24,MHVIADMN;
.
          CALL      IDAT0000                      * init temp vars
.
. check whether patient was in hospital or on leave at current date
.
          MOVE      ONE,XXBSTAT                   * assume currently in hosp
.
          PACK      KEY30,MHVIADMN,REPDATE,Z30
          CALL      RDSTRAN2
LOAD2000  CALL      RDPTRAN2
          BRANCH    OVRCD,LOAD1000                * ignore patient
.
          MATCH     MHVIADMN,TADMN                * same admission number?
          GOTO      LOAD1000 IF NOT EQUAL
.
          MATCH     ANSD,TMOVE                    * Discharge record? Ignore
          GOTO      LOAD1000 IF EQUAL
          MATCH     ANSC,TMOVE                    * Change record?
          GOTO      LOAD5000 IF EQUAL
          MATCH     ANSR,TMOVE             
          GOTO      LOAD5000 IF EQUAL
.
. get bed status
.
          MATCH     ANSL,TMOVE
          GOTO      LOAD5000 IF NOT EQUAL         * currently admitted
.
          MOVE      SP1,TCINDC1                   * get type of leave
          PACK      KEY5,ANST,ANSL,PTTRLTYP 
          CALL      RDCODE1
          MOVE      TCINDC1,ANS
          REP       " 2H3T4A5",ANS
          MOVE      ANS,XXBSTAT
.
. now set up details for report
.
LOAD5000  PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD1000                * ignore if no admission rec
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD1000                * ignore if no master rec
.
          PACK      KEY8,AURNO
          CALL      RDPMPX21
          BRANCH    OVRCD,LOAD1000                * ignore if no master rec
.
          MOVE      SP3,LPCONDC                   * get patient condition
          PACK      KEY88,PTMASNAM,PMPXFNAM,AADMNO
          CALL      RDLOCA1
          MOVE      LPCONDC,XXCOND
.
          MOVE      PURNO,XXURNO
          STRIP     PSNAME
          PACK      XXNAME,PSNAME,COMMA,SP1,PGNAME
          MOVE      PREG,XXRELIG
          MOVE      PSEX,XXSEX
          MOVE      PSAGE,XXAGE
          MOVE      ADATE,XXADMD
          MOVE      TWARD,XXWARD
          UNPACK    MHVIRODT,CCENT,CYEAR,CMON,CDAY
          PACK      XXRECD,CCENT,CYEAR,CMON,CDAY
          LOAD      XXVRES,MHVIVISI,DYES,DNO
          MOVE      TTIME,XXTIME
.
. now get latest legal status
.
          MOVE      SP3,XXLSCOD
          PACK      KEY21,AADMNO,Z30
          CALL      RSMHLEG1
          CALL      RPMHLEG1                      * read last rec
          BRANCH    OVRCD,LOAD6000
.
          MATCH     MHLEADMN,AADMNO               * same admission no?
          GOTO      LOAD6000 IF NOT EQUAL
.
          MOVE      MHLESTAT,XXLSCOD              * legal status
.
LOAD6000  CALL      ADOC0000                      * get attending doctor
          CALL      LSTY0000                      * get Length of Stay
          CALL      LLVE0000                      * get Length of Leave
.
. now post details to temp file
.
          DISPLAY   *P47:24,*V2LON,MHVIADMN;
.
          PACK      KEY48,XXNAME,XXURNO,XXTIME
          CALL      WRTEMP1
          GOTO      LOAD1000
.
LOAD9000  DISPLAY   *P1:24,*EL
.
LOAD9999  RETURN
+
.**************************************************************************
.*  LSTY0000  :  Calculate Length of Stay  (include Short Term Leave)     *
.**************************************************************************
LSTY0000  PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED    * save key
          MOVE      CURDATE,CDYSFDTE              * from date
          MOVE      ZERO,XXLSTAY                  * init length of stay
.
. find last admission date
.
          PACK      KEY30,MHVIADMN,Z30
          CALL      RDSTRAN2
LSTY1000  CALL      RDPTRAN2
          BRANCH    OVRCD,LSTY9000
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LSTY9000 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE                    * admission?
          GOTO      LSTY1000 IF NOT EQUAL
.
          PACK      CDYSFDTE,TDATE                * set up FROM date
          REP       " 0",CDYSFDTE
.
. now get TO date
.
LSTY2000  CALL      RDKTRAN2                      * read next record
          BRANCH    OVRCD,LSTY6000                * no more records
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LSTY6000 IF NOT EQUAL
.
          MATCH     ANSC,TMOVE                    * Change record?
          GOTO      LSTY2000 IF EQUAL             * ignore
.
          MATCH     ANSD,TMOVE                    * Discharge?
          GOTO      LSTY5500 IF EQUAL
.
          MATCH     ANSL,TMOVE                    * Leave record?
          GOTO      LSTY4000 IF NOT EQUAL
.
. we have a leave record so get type of leave (ignore Short Term Leave)
.
          MOVE      PTTRLTYP,SAVLTYP              * use lve type from "L" rec
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED    * save key
          CALL      RDKTRAN2                      * get "R" record
          BRANCH    OVRCD,LSTY2500
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LSTY2500 IF NOT EQUAL
.
          MOVE      PTTRLTYP,SAVLTYP              * use lve type from "R" rec
.
LSTY2500  PACK      KEY5,ANST,ANSL,SAVLTYP        * check if Short Term leave
          CALL      RDCODE1
          BRANCH    OVRCD,LSTY2000
          MATCH     SP1,TCINDC1                   * Short Term Leave?
          GOTO      LSTY2000 IF EQUAL
.
          CALL      RDTRAN2                       * restore key
.
. patient is on Long Term Leave so calc days between FROM (Admiss or last
. Return) date and Leave date
. 
LSTY3000  PACK      CDYSTDTE,TDATE                * Leave date = TO date
          REP       " 0",CDYSTDTE
          CALL      CALCDAYS
          ADD       CDYSDAYS,XXLSTAY              * increment length of stay
          SUB       TWO,XXLSTAY                   * not inclusive
          IF        XXLSTAY<1
            MOVE      ONE,XXLSTAY
          ENDIF
.
.  get the return from leave record (if any)
.
          CALL      RDKTRAN2
          BRANCH    OVRCD,LSTY9000
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LSTY9000 IF NOT EQUAL         * no return from leave record
.
          PACK      CDYSFDTE,TDATE                * set up FROM date
          REP       " 0",CDYSFDTE
          GOTO      LSTY2000
.
. Return from Leave record
. we have a Return from Long Term Leave
.
LSTY4000  PACK      CDYSFDTE,TDATE                * Return date = FROM date
          REP       " 0",CDYSFDTE
          GOTO      LSTY2000
.
. patient has been discharged
.
LSTY5500  PACK      CDYSTDTE,TDATE                * Discharge date = TO date
          REP       " 0",CDYSTDTE
          CALL      CALCDAYS
          ADD       CDYSDAYS,XXLSTAY              * Length of stay
          SUB       ONE,XXLSTAY                   * not inclusive
          GOTO      LSTY9000
.
. patient has not been discharged so use todays date as the TO date
.
LSTY6000  MOVE      CURDATE,CDYSTDTE              * to date
          CALL      CALCDAYS
          ADD       CDYSDAYS,XXLSTAY              * Length of stay
          SUB       ONE,XXLSTAY                   * not inclusive
.
. restore key
.
LSTY9000  MOVE      SAVKEY30,KEY30
          CALL      RDTRAN2
.
LSTY9999  RETURN
+
.**************************************************************************
.*  LLVE0000  :  Calculate Length of Leave (Exclude Short Term Leave)     *
.**************************************************************************
LLVE0000  PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED    * save key
          MOVE      ZERO,XXLLEAV                  * init length of leave
          MOVE      SP8,CDYSFDTE
.
. find last admission date
.
          PACK      KEY30,MHVIADMN,Z30
          CALL      RDSTRAN2
LLVE1000  CALL      RDPTRAN2
          BRANCH    OVRCD,LLVE9000
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LLVE9000 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE                    * admission?
          GOTO      LLVE1000 IF NOT EQUAL
.
. now get FROM date
.
LLVE2000  CALL      RDKTRAN2                      * read next record
          BRANCH    OVRCD,LLVE6000                * no more records
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LLVE6000 IF NOT EQUAL
.
          MATCH     ANSC,TMOVE                    * Change record?
          GOTO      LLVE2000 IF EQUAL             * ignore
.
          MATCH     ANSD,TMOVE                    * Discharge?
          GOTO      LLVE2000 IF EQUAL
.
          MATCH     ANSL,TMOVE                    * Leave record?
          GOTO      LLVE4000 IF NOT EQUAL
.
. we have a leave record so get type of leave (ignore Short Term Leave)
. get type of leave that is on the "R" record if it exists
.
          MOVE      PTTRLTYP,SAVLTYP              * use lve type from "L" rec
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED    * save key
          CALL      RDKTRAN2                      * get "R" record
          BRANCH    OVRCD,LLVE2500                * still on leave
.
          MATCH     MHVIADMN,TADMN                * same admission no?
          GOTO      LLVE2500 IF NOT EQUAL
.
          MOVE      PTTRLTYP,SAVLTYP              * use lve type from "R" rec
.
LLVE2500  PACK      KEY5,ANST,ANSL,SAVLTYP        * check if Short Term leave
          CALL      RDCODE1
          BRANCH    OVRCD,LLVE2000
          MATCH     SP1,TCINDC1                   * Short Term Leave?
          GOTO      LLVE2000 IF EQUAL
.
. patient is on Long Term Leave so calc days between FROM (Admiss or last
. Return) date and Leave date
. 
LLVE3000  PACK      CDYSFDTE,TDATE                * Leave date = FROM date
          REP       " 0",CDYSFDTE
          CALL      RDTRAN2                       * restore record
          GOTO      LLVE2000
.
. Return from Leave record
. we have a Return from Long Term Leave
.
LLVE4000  PACK      CDYSTDTE,TDATE                * Return date = FROM date
          REP       " 0",CDYSTDTE
.
          CALL      CALCDAYS
          ADD       CDYSDAYS,XXLLEAV              * increment length of leave
          SUB       ONE,XXLLEAV                  * not inclusive
          MOVE      SP8,CDYSFDTE                 * init from date
          GOTO      LLVE2000
.
. patient has not been discharged so use todays date as the TO date
.
LLVE6000  MATCH     SP8,CDYSFDTE                  * do we have a Leave date?
          GOTO      LLVE9000 IF EQUAL
.
          MOVE      CURDATE,CDYSTDTE              * to date
          CALL      CALCDAYS
          ADD       CDYSDAYS,XXLLEAV              * Length of leave
          SUB       ONE,XXLLEAV                   * not inclusive
.
. restore key
.
LLVE9000  MOVE      SAVKEY30,KEY30
          CALL      RDTRAN2
.
LLVE9999  RETURN
+
.**************************************************************************
.*  ISUM0000  :  Initialise summary stats                                 *
.**************************************************************************
ISUM0000  MOVE      ZERO,NOHOS
          MOVE      ZERO,NOSHL
          MOVE      ZERO,NOHOL
          MOVE      ZERO,NOTRL
          MOVE      ZERO,NOABS
.
ISUM9999  RETURN
+
.**************************************************************************
.*  USUM0000  :  Update Summary Stats                                     *
.**************************************************************************
USUM0000  BRANCH    XXBSTAT,USUM1000,USUM2000,USUM3000,USUM4000,USUM5000
.
USUM1000  ADD       ONE,NOHOS                     * patients in hospital
          GOTO      USUM9999
.
USUM2000  ADD       ONE,NOSHL                     * patients on Short Term Leave
          GOTO      USUM9999
.
USUM3000  ADD       ONE,NOHOL                     * patients on Holiday Leave
          GOTO      USUM9999
.
USUM4000  ADD       ONE,NOTRL                     * patients on Trial Leave
          GOTO      USUM9999
.
USUM5000  ADD       ONE,NOABS                     * patient Absent Without Leave
.
USUM9999  RETURN
+
.**************************************************************************
.*  ADOC0000  :  Get Attending doctor                                     *
.**************************************************************************
ADOC0000  MOVE      SP30,XXADOCT
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1
          BRANCH    OVRCD,ADOC9999
.
          STRIP     DSNAME
          MOVE      DGNAME,ANS
          PACK      XXADOCT,DSNAME,COMMA,SP1,ANS
.
ADOC9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
PTHD0000  CALL      UND132
          PRINT     *1,"|",*11,"|",*45,"|",*50,"|Cond-|Legal| Commit |":
                    *75,"|",*79,"|",*83,"|",*92,"|L. of|L. of|",*121,"|":
                    "Bed |Visit|":
                    *N,*1,"| U/R No. | Patient Name",*45,"|Ward|ition|Stat.|":
                    "  Date  |Sex|Age|Rel|Adm Date|Stay |Leave|Attending ":
                    "Doctor|Stat|Rest.|"
          CALL      UND132
.
PTHD9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  initialise vars                                         *
.*************************************************************************
IDAT0000  UNPACK    SP70,XXURNO,XXNAME,XXWARD,XXCOND,XXLSCOD,XXRECD,XXSEX
          UNPACK    SP70,XXRELIG,XXADMD,XXADOCT,XXVRES
          MOVE      ZERO,XXAGE
          MOVE      ZERO,XXLSTAY
          MOVE      ZERO,XXLLEAV
          MOVE      SP8,XXTIME
.
IDAT9999  RETURN
+
.*************************************************************************
.*  PSUM0000  :  Print Summary Statistics                                *
.*************************************************************************
PSUM0000  PRINT     *1,"Number of patients in hospital      = ",NOHOS,*N:
                    *1,"Number of patients on short leave   = ",NOSHL,*N:
                    *1,"Number of patients on holiday leave = ",NOHOL,*N:
                    *1,"Number of patients on trial leave   = ",NOTRL,*N:
                    *1,"Number of patients AWOL             = ",NOABS
.
PSUM9999  RETURN  
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLOSE     MEHTMPA2
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 111 U9-40,1-8,103-110",CMDLINE
          APPEND    " U41-43,99-99,9-40,1-8,103-110",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,FNAME
          OPEN      MEHTMPA2,FNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RSTEMP1   RESET     KEY48
          READ      MEHTMPA1,KEY48;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;XXURNO,XXNAME,XXWARD,XXCOND,XXLSCOD,XXRECD,XXSEX:
                             XXAGE,XXRELIG,XXADMD,XXLSTAY,XXLLEAV,XXADOCT:
                             DXXBSTAT,XXVRES,XXTIME
          GOTO      OVERCOND IF OVER
          MOVE      DXXBSTAT,XXBSTAT
          RETURN
.
WRTEMP1   RESET     KEY48
          MOVE      XXBSTAT,DXXBSTAT
          WRITE     MEHTMPA1,KEY48;XXURNO,XXNAME,XXWARD,XXCOND,XXLSCOD,XXRECD:
                             XXSEX,XXAGE,XXRELIG,XXADMD,XXLSTAY,XXLLEAV,XXADOCT:
                             DXXBSTAT,XXVRES,XXTIME
          RETURN
.
. and now for 2nd index
.
RSTEMP2   RESET     KEY52
          READ      MEHTMPA2,KEY52;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    MEHTMPA2;XXURNO,XXNAME,XXWARD,XXCOND,XXLSCOD,XXRECD,XXSEX:
                             XXAGE,XXRELIG,XXADMD,XXLSTAY,XXLLEAV,XXADOCT:
                             DXXBSTAT,XXVRES,XXTIME
          GOTO      OVERCOND IF OVER
          MOVE      DXXBSTAT,XXBSTAT
          RETURN
.
.
          INC       STD001IO
          INC       MEHVI1IO/INC
          INC       MEHDIAIO/INC
          INC       PATDAYIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATCODIO/INC
          INC       MEHDS1IO/INC
          INC       MEHLEGIO/INC
          INC       PATDSCIO/INC
          INC       PATLOCIO/INC
          INC       PATDO1IO/INC
          INC       PATTRNIO/INC
          INC       CALCDAYS
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

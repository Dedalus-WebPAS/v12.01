.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   EXTRALAS                                                    *
.* Desc      :   Patient Aliases Data Extraction Program                     *
.*****************************************************************************
.* Date      :   06/03/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will extract the patient aliases into a        *
.*               pipe delimited file in a format that will feed into         *
.*               CONVALAS.                                                   *
.* Mods:                                                                     *
.*        V10.04.00 06/03/2014  Steve Armstrong  CAR 296128                  *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATGSRFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
.
.
ALIASEXT  FILE      ASCII,FIXED=4096
.
.         Patient Aliases upload file layout - convalas.txt
.         (as per CONVALAS.PBL)
.
.Name     Type    Length Description
.----     ----    ------ -----------
ALASURNO  DIM       8    U/R number                 (patgsrnf.gsrurno)
ALASSURN  DIM       40   Alias Surname              (patgsrnf.gsrsnam)
ALASGIV1  DIM       40   Alias Given Name 1         (patgsrnf.gsrgnam)
ALASGIV2  DIM       40   Alias Given Name 2         (patgsrnf.ptgsgnm2)
.
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CURCOUNT  FORM      10
ERRCOUNT  FORM      10
EXTCOUNT  FORM      10
RECCOUNT  FORM      10
SAVEURNO  DIM       8
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
.
PRGID     INIT      "EXTRALAS"
PRGNAM    INIT      "Patient Aliases Extraction"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * patient aliases extract
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      CFIL0000               * create file
          BRANCH    EXIT,MAIN0100          * file exists
.
          CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data extraction                     *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Patient Aliases Data Extraction"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run extraction
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                                  CFIL0000       Called by: MAIN0000       *
.*                         Create the extract file                           *
.*****************************************************************************
.
CFIL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ALIASEXT,"convalas"     * extract file exists ?
          TRAPCLR   IO
          BRANCH    OVRCD,CFIL5000          * no - create it
.
CFIL1000  KEYIN     *P1:23,*B,*EF,"The extract file convalas.txt already ":
                    "exists.":
                    *P1:24,"Overwrite it (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,")?":
                    *P21:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS                * "Y" entered ?
          GOTO      CFIL2000 IF EQUAL       * yes - continue
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      CFIL9100 IF EQUAL       * no
.
          BEEP
          GOTO      CFIL1000
.
.         Delete the existing file
.
CFIL2000  CLOSE     ALIASEXT,DELETE
.
.         Create a new extract file
.
CFIL5000  PREP      ALIASEXT,"convalas"
.
          MOVE      ZERO,EXIT
          GOTO      CFIL9999
.
CFIL9100  MOVE      ONE,EXIT
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through the alias file and extract each record                *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          MOVE      ZERO,EXTCOUNT                * initialise extract count
          MOVE      ZERO,ERRCOUNT                * initialise error count
          MOVE      ZERO,CURCOUNT                * initialise current count
          MOVE      SP8,SAVEURNO                 * initialise saved U/R
          DISPLAY   *P1:24,*EL,"Processing:";
.
          PACK      KEY115,SP70,SP70
          CALL      RSPTGSR1                     * position at start of file
PROC0500  CALL      RKPTGSR1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          ADD       ONE,RECCOUNT                 * increment record counter
.
          DISPLAY   *P13:24,*V2LON,RECCOUNT
.
          MATCH     SAVEURNO,GSRURNO             * same U/R still ?
          GOTO      PROC1000 IF EQUAL            * yes
.
          MOVE      GSRURNO,KEY8                 * no - get PMI master details
          CALL      RDMAST1                      * PMI master record on file ?
          IF        OVRCD = 1
            ADD       ONE,ERRCOUNT               * increment current count
            GOTO      PROC0500
          ENDIF
.
          MOVE      GSRURNO,SAVEURNO             * save new U/R
.
          CALL      CLPMSPX2
          MOVE      GSRURNO,KEY8
          CALL      RDPMPX21                     * PMI ext record found ?
.
.         Check if this is the current record or a real alias for the patient
.
PROC1000  MATCH     GSRSNAM,PTMASNAM             * same surname ?
          GOTO      PROC2000 IF NOT EQUAL        * no - valid alias
.
          MATCH     GSRGNAM,PMPXFNAM             * same 1st given name ?
          GOTO      PROC2000 IF NOT EQUAL        * no - valid alias
.
          MATCH     PTGSGNM2,PMPXSNAM            * same 2nd given name ?
          GOTO      PROC2000 IF NOT EQUAL        * no - valid alias
.
          ADD       ONE,CURCOUNT                 * increment bypass count
          GOTO      PROC0500                     * yes - current patient record
.
PROC2000  MOVE      GSRURNO,ALASURNO
          SQUEEZE   ALASURNO
          MOVE      GSRSNAM,ALASSURN
          STRIP     ALASSURN
          REP       UPPLOW,ALASSURN
          MOVE      GSRGNAM,ALASGIV1
          STRIP     ALASGIV1
          REP       UPPLOW,ALASGIV1
          MOVE      PTGSGNM2,ALASGIV2
          STRIP     ALASGIV2
          REP       UPPLOW,ALASGIV2
.
          WRITE     ALIASEXT,SEQ;*+,ALASURNO,PIPE,ALASSURN,PIPE,ALASGIV1,PIPE:
                                    ALASGIV2,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:21,*EF,"Extraction complete.":
                    *P1:22,*V2LON,CURCOUNT,*HOFF," current records ignored.  ":
                    *P1:23,*V2LON,ERRCOUNT,*HOFF," records with no master.  ":
                    *P1:24,*V2LON,EXTCOUNT,*HOFF," records extracted.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       CLPMSPX2
.
          INC       PATGSRIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC

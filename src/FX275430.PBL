.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   FX275430                                                    *
.* Desc      :   Fixit to make sure that pmstspaf records have the latest    *
.*               U/R number for a patient.                                   *
.*****************************************************************************
.* Date      :   01/11/2012                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will fix the U/R number for any pmstspaf       *
.*               records where the U/R has been merged, but the U/R not      *
.*               updated (given that pmstspaf was not included in the merge  *
.*               program).                                                   *
.* Mods      :                                                               *
.*        V10.03.03 22/11/2012  Steve Armstrong  CAR 275430                  *
.*                  Mods to right justify the unique counter as some records *
.*                  have a left justifed unique counter instead of right     *
.*                  justified (new Option 2).                                *
.*                  Also modified Option 1 to handle left or right justifed  *
.*                  unique counters.                                         *
.*        V10.03.02 15/11/2012  Steve Armstrong  CAR 275430                  *
.*                  Mods to initialise FORM3.                                *
.*        V10.03.01 14/11/2012  Steve Armstrong  CAR 275430                  *
.*                  Mods to ensure that new record doesn't already exist     *
.*                  prior to updating with the new U/R.                      *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC
          INC       PMSTSPFD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
DIM3      DIM       3
FORM3     FORM      3
RECCOUNT  FORM      8             * updated record counter
SAVKEY19  DIM       19
.
.
PRGID     INIT      "FX275430"
PRGNAM    INIT      "Fixit for PMSTSPFD"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000:      * fix merged U/R's
                            MAIN1000       * fix unique count justification
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  BRANCH    COPTION,MAIN5000:      * fix merged U/R's
                            MAIN7000       * fix unique count justification
.
MAIN5000  CALL      PROC0000               * fix merged U/R's
          GOTO      MAIN0100
.
MAIN7000  CALL      FLEF0000               * fix unique count justification
          GOTO      MAIN0100
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmstspaf";
          OPEN      PMSTSPA1,"pmstspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (Merged U/R's)":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit ":
                          "(Unique Count Justification)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run fixit option 1
                            OPTN9000             * run fixit option 2
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through the pmstspaf file and for each record, check if       *
.*        the U/R has been merged.  If so, get the patient's latest U/R      *
.*        and update the pmstspaf record.                                    *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      SP20,KEY19
PROC0200  CALL      RSPMTSP1                     * position at start of file
PROC0500  CALL      RKPMTSP1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,PMTSURNO,PMTSVISN,PMTSUNIQ;
.
          MOVE      PMTSURNO,KEY8
          CALL      RDMAST1                      * PMI record on file ?
          BRANCH    OVRCD,PROC0500               * no - ignore record
.
          COMPARE   ONE,PSTAT                    * merged PMI record ?
          GOTO      PROC0500 IF NOT EQUAL        * no - ignore record
.
          MATCH     PPSNAME,SP20                 * blank new U/R ?
          GOTO      PROC0500 IF EQUAL            * yes - ignore record
.
.         We have a merged U/R, so check if the new U/R has also been merged.
.
PROC1000  MOVE      PPSNAME,KEY8
          CALL      RDMAST1                      * PMI record on file ?
          BRANCH    OVRCD,PROC0500               * no - ignore record
.
          COMPARE   ZERO,PSTAT                   * merged PMI record ?
          GOTO      PROC5000 IF EQUAL            * no - update record
.
          MATCH     PPSNAME,SP20                 * blank new U/R ?
          GOTO      PROC0500 IF EQUAL            * yes - ignore record
          GOTO      PROC1000                     * no - check new U/R
.
.         We have the latest U/R for the patient, so update the pmstspaf
.         record after making sure that the new record doesn't already exist
.
PROC5000  PACK      SAVKEY19,PMTSURNO,PMTSVISN,PMTSUNIQ
          RJUSTIFY  PMTSUNIQ
          MOVE      PMTSUNIQ,FORM3
PROC5500  PACK      KEY19,PURNO,PMTSVISN,PMTSUNIQ
          CALL      RAPMTSP1                     * record already on file ?
          BRANCH    OVRCD,PROC6000               * no - update existing record
.
          MOVE      PMTSUNIQ,FORM3               * yes - increment counter
          ADD       ONE,FORM3
          MOVE      FORM3,PMTSUNIQ
          GOTO      PROC5500
.
PROC6000  MOVE      SAVKEY19,KEY19
          CALL      RDPMTSP1                     * re-read the old record again
          MOVE      PURNO,PMTSURNO               * load new U/R
          MOVE      FORM3,PMTSUNIQ               * load new counter
          CALL      UPPMTSP1                     * update record
.
          ADD       ONE,RECCOUNT                 * increment record counter
.
          MOVE      SAVKEY19,KEY19
          GOTO      PROC0200
.
PROC9000  DISPLAY   *P1:23,*EF,"Processing completed.  ",*V2LON,RECCOUNT,*HOFF:
                    " records updated.":
                    *P1:24;
          CALL      HITENTER
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               FLEF0000              Called by: MAIN0000   *
.*        Fix records with a left justified unique counter                   *
.*****************************************************************************
.
.         Loop through the pmstspaf file and look for records where the
.         Unique Counter is left justified
.
FLEF0000  MOVE      ZERO,RECCOUNT                * initialise record count
          DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      SP20,KEY19
          CALL      RSPMTSP1                     * position at start of file
FLEF0500  CALL      RKPMTSP1                     * read next record
          BRANCH    OVRCD,FLEF9000               * eof - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,PMTSURNO,PMTSVISN,PMTSUNIQ;
.
          MATCH     SP1,PMTSUNIQ                 * first character blank ?
          GOTO      FLEF0500 IF EQUAL            * ignore - right justified
.
          MOVE      PMTSUNIQ,DIM3
          STRIP     DIM3                         * remove trailing spaces
          MOVELPTR  DIM3,FORM1                   * get length of count
          COMPARE   THREE,FORM1                  * 3-digits ?
          GOTO      FLEF0500 IF EQUAL            * yes - get next record
.
.         The record has a left justified unique counter, so right justify
.         it and update the record if possible, otherwise, increment
.         the unique counter until the record can be updated.
.
          PACK      SAVKEY19,PMTSURNO,PMTSVISN,PMTSUNIQ
          RJUSTIFY  PMTSUNIQ
          MOVE      PMTSUNIQ,FORM3
FLEF5500  PACK      KEY19,PMTSURNO,PMTSVISN,PMTSUNIQ
          CALL      RAPMTSP1                     * record already on file ?
          BRANCH    OVRCD,FLEF6000               * no - update existing record
.
          MOVE      PMTSUNIQ,FORM3               * yes - increment counter
          ADD       ONE,FORM3
          MOVE      FORM3,PMTSUNIQ
          GOTO      FLEF5500
.
FLEF6000  MOVE      SAVKEY19,KEY19
          CALL      RDPMTSP1                     * re-read the old record again
          MOVE      FORM3,PMTSUNIQ               * load new counter
          CALL      UPPMTSP1                     * update record
.
          ADD       ONE,RECCOUNT                 * increment record counter
.
          MOVE      SAVKEY19,KEY19
          GOTO      FLEF0500
.
FLEF9000  DISPLAY   *P1:23,*EF,"Processing completed.  ",*V2LON,RECCOUNT,*HOFF:
                    " records updated.":
                    *P1:24;
          CALL      HITENTER
.
FLEF9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATMA1IO/INC
          INC       PMSTSPIO/INC

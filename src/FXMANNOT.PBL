.***************************************************************************
.* System    :   Emergency                                                 *
.* Program   :   FXMANNOT                                                  *
.* Desc      :   Copy management notes from viscmtaf to vismdtfd & vismtxaf*
.***************************************************************************
.* Date      :   10/03/2010                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will copy all management notes from viscmtaf *
.*           :   and copy them to vismdtaf and vismtxaf.                   *
.*           :                                                             *
.* Mods      :   V9.12.00  Ebon Clements   CAR 216621                      *
.*           :             Created program                                 *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       VISCMTFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       EMRVISFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
DIM2A     DIM       2
DIM2B     DIM       2
DIM2C     DIM       2
LIN1BLNK  FORM      1
NOTETYPE  DIM       3
NOTENUMB  FORM      6
RECNUM    FORM      8
.
PRGID     INIT      "FXMANNOT"
PRGNAM    INIT      "Emergency Management Notes Conversion "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      VISCMTA1,"viscmtaf"
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      EMRVISA1,"emrvisaf"
.
          MOVE      ZERO,RECNUM
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      "002",NOTETYPE          * Management notes
          MOVE      ONE,NOTENUMB            * Note number
.
          PACK      KEY8,SP70
          CALL      RSEMVIS1
PROC1000  CALL      RKEMVIS1                * Check all emrvisaf records
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,RECNUM                    * update record counter
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      ZERO,LIN1BLNK                 * Set line 1 blank to no
.
          PACK      KEY14,EMVIADMN,NOTETYPE,SP70
          CALL      RSVSCMT1
PROC1100  CALL      RKVSCMT1                * Check for clinical notes
          BRANCH    OVRCD,PROC1000
.
          MATCH     DEMVIADM,VSCTVIST
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     NOTETYPE,VSCTTYPE
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     "  1",VSCTLINE
          IF        @EQUAL
            MATCH    SP70,VSCTDATA
            IF       @EQUAL | @EOS
              MOVE     ONE,LIN1BLNK        * Line 1 is blank so skip 
              GOTO     PROC1100
            ENDIF
          ENDIF
.
          MATCH     "  2",VSCTLINE
          IF        @EQUAL
            MATCH    SP70,VSCTDATA
            IF       @EQUAL | @EOS
              BRANCH   LIN1BLNK,PROC1100   * Line 1 & 2 are blank so skip 
            ENDIF
          ENDIF
.
          PACK      KEY17,VSCTVIST,VSCTTYPE,NOTENUMB,SP70 
          CALL      RDVSMDT1               * Create Clinical Notes header record
          IF        OVRCD=1
            MOVE      VSCTVIST,VSMDVISN
            MOVE      VSCTTYPE,VSMDTYPE
            MOVE      NOTENUMB,VSMDNOTE
.
            MOVE      SP70,VSMDDATE 
            MOVE      SP70,VSMDTIME 
.
            MATCH     SP70,VSCTUKEY
            IF        !@EQUAL
              UNPACK    VSCTUKEY,VSMDDATE,DIM2A,DIM2B,DIM2C
              PACK      VSMDTIME,DIM2A,COLON,DIM2B,COLON,DIM2C,SP70
            ENDIF
.
            MOVE      SP70,VSMDUSER 
            MOVE      SP70,VSMDOCCG 
            MOVE      ZERO,VSMDDELT 
            MOVE      SP70,VSMDDDAT 
            MOVE      SP70,VSMDDTIM 
            MOVE      SP70,VSMDDUSE 
            MOVE      SP70,VSMDDOCC 
            MOVE      SP70,VSMDSPAR 
.
            PACK      KEY17,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
            CALL      WRVSMDT1
          ENDIF
.
          PACK      KEY20,VSCTVIST,VSCTTYPE,NOTENUMB,VSCTLINE,SP70
          CALL      RDVSMTX1                * Created clinical notes record
          IF        OVRCD=1
            MOVE      VSCTVIST,VSMTVISN
            MOVE      VSCTTYPE,VSMTTYPE
            MOVE      NOTENUMB,VSMTNOTE
            MOVE      VSCTLINE,VSMTUNIQ
            MOVE      VSCTDATA,VSMTCMNT
            MOVE      SP70,VSMTSPAR
            PACK      KEY20,VSMTVISN,VSMTTYPE,VSMTNOTE,VSMTUNIQ,SP70
            CALL     WRVSMTX1
          ENDIF
.
          GOTO      PROC1100
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       VISCMTIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       EMRVISIO/INC

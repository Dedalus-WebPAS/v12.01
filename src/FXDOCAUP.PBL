.*******************************************************************************
.* System    :   Patient Management                                            *
.* Program   :   FXDOCAUP                                                      *
.* Desc      :   Loop through PATDO1AF. If PTDOAUPD is M change it to 0        *
.*               and if A change it to 1. M and A are invalid values.          *
.*******************************************************************************
.* Date      :   27/09/2013                                                    *
.* Author    :   Ania P                                                        *
.* Function  :   This program will loop the patdo1af table and replace all     *
.*               occurances of "M" in the PTDOAUPD field with a zero and all   *
.*               occurences of "A" with a 1.                                   *
.* Mods      :                                                                 * 
.*               V10.03.00 27/09/2013 Ania P               CAR 292026          *
.*                         Created Fixit                                       *
.*                                                                             *
.*******************************************************************************
.
          INC       STD001FD
          INC       PATDO1FD/INC
.
.
. VARIABLES
. -------------------------
.
COUNT     FORM      8                  * Row count
DIM8      DIM       8                  * Formatted row count
.
PRGID     INIT      "FXDOCAUP"
PRGNAM    INIT      "HCP Auto Update Corrections"
VERSION   INIT      "V12.01.00"
+
.*******************************************************************************
.*                              ML0000                                         *
.*                      Controlling Logic (Mainline code)                      *
.*******************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.*******************************************************************************
.*                                  INIT0000              Called by: ML0000    *
.*                               initialisation                                *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                 OPTN0000               Called by: ML0000    *
.*                         get user options or exit                            *
.*                                                                             *
.*    Returns:  EXIT  = FALSE (0)  Run fixit                                   *
.*                      TRUE  (1)  Exit option selected                        *
.*******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*******************************************************************************
.*                                PROC0000                Called by: ML0000    *
.*******************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing: ";
          MOVE      ZERO,COUNT
.
PROC1000  PACK      KEY6,SP70
          CALL      RDSDOCT1 
PROC1100  CALL      RDKDOCT1 
          BRANCH    OVRCD,PROC9000
.
          MATCH     "A",PTDOAUPD
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,"Updating Doctor: ",DCODE
            MOVE      "1",PTDOAUPD
            CALL      UPDOCT1
            ADD       ONE,COUNT
            GOTO      PROC1100
          ENDIF
.
          MATCH     "M",PTDOAUPD
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,"Updating Doctor: ",DCODE
            MOVE      "0",PTDOAUPD
            CALL      UPDOCT1
            ADD       ONE,COUNT
            GOTO      PROC1100
          ENDIF
.
          GOTO      PROC1100
.
PROC9000  MOVE      COUNT,DIM8
          SQUEEZE   DIM8
          DISPLAY   *P1:24,*EL,"Fixit completed. ":
                    *P18:24,"No. of rows updated: ",DIM8
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATDO1IO/INC

.******************************************************************************
.*                                                                            *
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*      PROGRAM NAME:     IBAHOS12                                            *
.*                                                                            *
.*      FUNCTION:         MISCELLANEOUS MASTER LISTING                        *
.*                                                                            *
.*      DATE WRITTEN:     11/03/88                                            * 
.*                                                                            *
.*      AUTHOR:           PAUL LO       (I.B.A)                               *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.* V12.00.01  11/07/2025 J.Tan          TSK 0961623                           *
.*            Fixed I*D writing to tempfile                                   *
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved TFILENAM to START and added extra KILL0000 when finished  *
.* V10.03.01  27/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*       V10.01.02  16/02/2011  Mike Laming   CAR 233046                      *
.*                  Removed Option for reporting "new file"                   *
.*       V10.01.01  16/12/2010  Mike Laming   CAR 233046                      *
.*                  Mods to Misc.Charges PATMCHFD - Key change, logic changes *
.*       V10.00.01  30/04/2010  J.Tan     CAR 220887                          *
.*                  Mods to print Active/Inactive of Misc.charge field        *
.*       V9.04.01   04/08/2005  J.Tan        CAR 62750                        *
.*                  Mods not to use the redefined amount variables            *
.*       V9.03.01   16/09/2003  Lina Vo      CAR 40497                        *
.*                  Changed index size of patmchgf and TEMPFILE to include    *
.*                  effective date.                                           *
.*       V9.02.02   11/04/2003  Davin  CAR/Quote 23297                        *
.*                  Added "keyin rebate flag" to report                       *
.*       V9.02.01   21/03/2003  Dean Cramer   CAR 36516                       *
.*                  Load whole key of tempfile not just first 15 chars        *
.*                  this will stop duplicate key error and crash              * 
.*       V5.09.B02  15/12/2000  Ebon Clements    509 Mods                     *
.*                  Changes to print PTMCCFCY                                 *
.*       V5.08.05   27/10/2000  Dean Cramer   SRF 6710                        *
.*                  Recomplied for PATFNDDS via PATFNDKY                      *
.*                  Changed for health fund table code size increase to       *
.*                  8 characters                                              *
.*       V5.08.04   16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*       V5.08.03   10/07/2000  Greg Horvat                                   *
.*                  Added PTMCEDIE - EDI Equivalent                           *
.*       V5.08.02   31/05/2000  Jill Habasque                                 *
.*                  Fixed printing when not using GST                         *
.*       V5.08.01   03/05/2000  Jill Habasque                                 *
.*                  Fixed printing of description                             *
.*       V5.08.B04  03/04/2000  Jill Habasque                                 *
.*                  Mods to print report by GST Code                          *
.*       V5.08.B03  08/03/2000  Steve Downing                                 *
.*                  Fixed read position for IBCNUGST                          *
.*       V5.08.B02  24/01/2000 J Habasque                                     *
.*                  Added printing of GST Applicable  &                       *
.*                  GST Code                                                  *
.******************************************************************************
.*       W2.01      25/03/88 J.TAN  (I.B.A.)                                  *
.*                  Add change desc.field                                     *
.*       V5.01      05/06/89 J.Tan                                            *
.*                  Fixed openning new file                                   *
.*                  SRF 187                                                   *
.*       V5.01.01   05/07/89 M.Ohleiter                                       *
.*                  Changes for 8 char ur and                                 *
.*                  admiss no - release 5.01                                  *
.*                  08/08/89 Graeme Williams                                  *
.*                  Added CMCNOHF to determine                                *
.*                  if HF to bbe put on report                                *
.*       V5.01.02   28/09/89 Graeme Williams                                  *
.*                  Fix health fund ranges                                    *
.*                  SRF 102322                                                *
.*       V5.01.03   03/06/92 Graeme Williams                                  *
.*                  Changed the misc. charge                                  *
.*                  code from 4 to 9 chars                                    *
.*                  SRF 114865                                                *
.*       V5.01.04   31/01/94 Matt Surridge                                    *
.*                  Removed Rebate Portion and                                *
.*                  No of Invoices for CFEETYP                                *
.*                  equal to 6.                                               *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD  
          INC       STDHLPDF
          INC       STDKWSDF
.
          INC       IBAGSTFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATFN1FD/INC
          INC       PATMCHFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
...TEMPFILE  IFILE     SQL, FIXED=94, KEY="U1-6,7-15,40-42,43-48,49-56,85-92"
TEMPFILE  IFILE     SQL, FIXED=105, KEY="U1-6,7-15,40-42,43-48,94-96,85-92"
.
XXGSTC    DIM       6        1
XXCHG     DIM       9        7
XXDESC    DIM       24       16
XXCLM     DIM       3        40
XXFUND    DIM       6        43
XXTABL    DIM       8        49             * no longer in use        *C-233046
XXITMT    DIM       1        57
XXGRP     DIM       3        58
XXPPOR    FORM      6.2      61
XXRPOR    FORM      6.2      66
XXNINV    DIM       1        71
XXICHG    DIM       1        72
XXGSTA    DIM       1        73
XXKREB    DIM       1        74
XXEDIEQ   DIM       10       75
XXEFDT    DIM       8        85
XXACTV    DIM       1        93
XXTTYPE   DIM       3        94
XXETDT    DIM       8        97
.
.End of record               105
.
CFEETYP   FORM      1
CMCNOHF   FORM      1
CMDLINE   DIM       80
CODE      DIM       2
.
DIM10     DIM       10
DIM3A     DIM       3
DIM3B     DIM       3
DIM6A     DIM       6
DIM6B     DIM       6
DIM6F1    DIM       6
DIM6F2    DIM       6
DIM6S1    DIM       6
DIM6S2    DIM       6
.
ENDGDESC  DIM       20
ENDGST    DIM       6
.
FILENAM1  INIT      "hos12a"
FNAMEA    DIM       8
FORM6P2   FORM      6.2
.
IBCNUGST  FORM      1
KANS      DIM       3
RECCNT    FORM      6
.
STRTDESC  DIM       20
STRTGST   DIM       6
.
PRGID     INIT      "IBAHOS12"
PRGNAM    INIT      "Miscellaneous Master Listing"
VERSION   INIT      "V12.01.00"
+
.
.         START OF PROGRAM
.
START     CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"ibagstaf"; 
          OPEN      IBAGSTA1,"ibagstaf"
          OPEN      IBAGSTA2,"ibagstaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patmchgf";
          OPEN      PATMCHG1,"patmchgf"
.
          READ      CONTROLF,ZERO;*85,IBCNUGST
          READ      CONTROLF,TEN9;*212,CFEETYP,*215,CMCNOHF
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEA
.
          MOVE      ZERO,CKYIMAND
          MOVE      ZERO,CKYIMODE
          MOVE      ONE,CNEWDS
          MOVE      ONE,CNOUNDLN
.
.             SELECT OPTION
.
MENU      DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Claim Code Sequence";
          IF        IBCNUGST=2
            DISPLAY   *P1:6,*V2LON," 2",*HOFF," = GST Code Sequence";
          ENDIF
.
REKEY     IF        IBCNUGST=2
            KEYIN     *P1:8,"Select Option : ",*EL,*V2LON,OPTION;
          ELSE
            KEYIN     *P1:7,"Select Option : ",*EL,*V2LON,OPTION;
          ENDIF
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          IF        IBCNUGST=2
            BRANCH    OPTION,OPENOF,NORGST
          ELSE
            BRANCH    OPTION,OPENOF
          ENDIF
.
          BEEP
          GOTO      REKEY
.
OPENOF    CLOSE     PATMCHG1
          OPEN      PATMCHG1,"patmchgf"
          MOVE      SP10,DIM10
.
KEYCLM    MOVE      SP6,DIM6S1
          MOVE      SP6,DIM6F1
          MOVE      SP6,DIM6S2
          MOVE      SP6,DIM6F2
.
          DISPLAY   *P1:11,*EF,"Starting Claim Code  : ":
                    *P1:12,"Ending   Claim Code  : ";
.
          BRANCH    CMCNOHF,KCODE1
.
          DISPLAY   *P1:14,"Starting Health Fund : ":
                    *P1:15,"Ending   Health Fund : ";
.
KCODE1    MOVE      "24",ECOL
          MOVE      "11",EVERT
          PACK      CODE,ANSC,ANSL
          MOVE      SP3,CKYIDEF3
          CALL      PATCODKY
          BRANCH    EXIT,KCODE1A,MENU
.
          MOVE      ACODE,DIM3A
          MOVE      ACODE,DIM6S1
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DIM6S1,SP2,TDESC;
          GOTO      KCODE2
.
KCODE1A   MOVE      SP3,DIM3A
          MOVE      "Start ",DIM6S1
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DIM6S1;
.
KCODE2    MOVE      "24",ECOL
          MOVE      "12",EVERT
          PACK      CODE,ANSC,ANSL
          MOVE      DIM3A,CKYIDEF3
          CALL      PATCODKY
          BRANCH    EXIT,KCODE2A,MENU
.
          MOVE      ACODE,DIM3B
          MOVE      ACODE,DIM6F1
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DIM6F1,SP2,TDESC;
          GOTO      KCODE2B
.
KCODE2A   MOVE      "zzz",DIM3B
          MOVE      "Finish",DIM6F1
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DIM6F1;
.
KCODE2B   MATCH     DIM3A,DIM3B
          GOTO      KFUND1 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,"Starting claim code must be before the ":
                    "ending claim code.  ";
          CALL      HITENTER
          DISPLAY   *P24:11,*EL,*P24:12,*EL;
          GOTO      KEYCLM
.
.
KFUND1    BRANCH    CMCNOHF,CONFIRM
.
          MOVE      "24",ECOL
          MOVE      "14",EVERT
          MOVE      SP6,CKYIDEF6
          CALL      PATFNDKY
          BRANCH    EXIT,KFUND1A,MENU,KFUND2A
.
          MOVE      HCODE,DIM6A
          MOVE      HCODE,DIM6S2
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DIM6S2,SP2,HFNAME;
          GOTO      KFUND2
.
KFUND1A   MOVE      SP6,DIM6A
          MOVE      "Start ",DIM6S2
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DIM6S2;
.
KFUND2    MOVE      "24",ECOL
          MOVE      "15",EVERT
          MOVE      DIM6A,CKYIDEF6
          CALL      PATFNDKY
          BRANCH    EXIT,KFUND2A,MENU,KFUND2A
.
          MOVE      HCODE,DIM6B
          MOVE      HCODE,DIM6F2
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DIM6F2,SP2,HFNAME;
          GOTO      KFUND2B
.
KFUND2A   MOVE      "zzzzzz",DIM6B
          MOVE      "Finish",DIM6F2
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,DIM6F2;
.
KFUND2B   MATCH     DIM6A,DIM6B
          GOTO      CONFIRM IF NOT LESS
.
          DISPLAY   *P1:24,*EL,"Starting health fund must be before the ":
                    "ending health fund.  ";
          CALL      HITENTER
          DISPLAY   *P24:14,*EL,*P24:15,*EL;
          GOTO      KFUND1
.
CONFIRM   CALL      CONTQST
          BRANCH    CEXIT,CHKOPT,KEYCLM,MENU
.
CHKOPT    MOVE      "60",CLNO
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:24,*EL,"Claim Code : ",*P35:24,"Charge Code : ";
          PACK      KEY29,DIM3A,DIM6A,SP70                            *C-233046
          CALL      RDSMCHG1
NEXTA     CALL      RDKMCHG1
          BRANCH    OVRCD,ENDP 
.
          MATCH     SP3,DIM3B
          GOTO      NOCHK1 IF EQUAL
.
          MATCH     MCLMCOD,DIM3B
          GOTO      ENDP IF LESS
.
NOCHK1    MATCH     SP6,DIM6A
          GOTO      NOCHK2 IF EQUAL
.
          MATCH     DIM6A,MHFUND
          GOTO      NEXTA IF LESS
.
NOCHK2    MATCH     SP6,DIM6B
          GOTO      NOCHK3 IF EQUAL
.
          MATCH     MHFUND,DIM6B
          GOTO      NEXTA IF LESS
.
NOCHK3    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      DNO,KANS
          MATCH     ANSY,PTMCKREB
          IF        @EQUAL
            MOVE      DYES,KANS
          ENDIF
.
          MOVE      MDESC,KEY24
          MOVE      DNO,KEY3
          LOAD      KEY3,MINDIC,DYES,DNO
.
.Claim|    Health         | Charge  |Description             |CmxF|Chg |Chg | Patient | Rebate  |No  |Key|Chg|GST| GST  |EDI       |
.Code | Fund  Tabl.Type   |  Code   |                        |Chg |Type|Grp | Portion | Portion |Inv |Reb|Des|App| Code |Equivalent|
.  123|123456 12345678    |123456789|123456789012345678901234| 1  | 1  |123 |123456.12|123456.12|  1  |  123 | 1 |123456|1234567890|
.23456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
.        1         2         3         4         5         6         7         8         9         0         1         2         3
.
          MATCH     "1",PTMCACTV               * Inactive
          IF        @EQUAL
            PRINT     *1,"|* ",MCLMCOD;
          ELSE
            PRINT     *1,"|  ",MCLMCOD;
          ENDIF
          IF        CMCNOHF<>1
            PRINT     "|",MHFUND,SP1,PTMCHFTT,SP9;                    *C-233046
          ENDIF
          MOVE      MPATPOR,FORM6P2
          PRINT     "|",MCHARGE,"|",KEY24,"| ",PTMCCFCY:
                    "  | ",MITMTYP,"  |",MMSCGRP:
                    " |",FORM6P2;
.
          MOVE      MHFPOR,FORM6P2
          PRINT     "|",FORM6P2,"| ",MNINV,"  |",KANS,"|",KEY3;
.
          IF        IBCNUGST=2
            PRINT     "| ",PTMCGSTA," |",PTMCGSTC;
          ENDIF
          PRINT     "|",PTMCEDIE,"|"
.
          ADD       ONE,CLNO
          DISPLAY   *P14:24,*V2LON,MCLMCOD,*P49:24,MCHARGE;
          GOTO      NEXTA
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOCODE IF EQUAL
.
          CALL      UND
. 
          PRINT     *1,"   * Asterisk on the first column denotes that ":
                    "the Misc.Item is Inactive."
          PRINT     *N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,*B,"Report Generation Completed.  ";
          CALL      HITENTER
          GOTO      MENU
.
NOCODE    DISPLAY   *P1:24,*EL,*B,"No Data Found.  ";
          CALL      HITENTER
          GOTO      MENU
.
UND       PRINT     "*-------------------------------------------------":
                    "---------------------------------------------------";
          IF        CMCNOHF<>1
            PRINT     "----------------";
          ENDIF
          IF        IBCNUGST=2
            PRINT     "-----------";
          ENDIF
          IF        OPTION=1
            PRINT     "----";
          ENDIF
          IF        OPTION=2
            PRINT     "-";
          ENDIF
         
          PRINT     "*"
          ADD       ONE,CLNO
          RETURN
.
HEAD      IF        CPAGENO<>0
            CALL      UND
          ENDIF
          CALL      HEAD132
.
          PRINT     *40,"(Claim Code - Sequence)  ",DIM10:
                    *N,*N,*40,"Claim Code  From : ",DIM6S1," To ",DIM6F1
          ADD       THREE,CLNO
          IF        CMCNOHF<>1
            PRINT     *40,"Health Fund From : ",DIM6S2," To ",DIM6F2
            ADD       ONE,CLNO
          ENDIF
.
          CALL      UND
.
          PRINT     *1,"|Claim";
          IF        CMCNOHF<>1
            PRINT     "|    Health         ";
          ENDIF
          PRINT     "| Charge  |Description             |CmxF|Chg |Chg ":
                    "| Patient | Rebate  |No  |Key|Chg";
          IF        IBCNUGST=2
            PRINT     "|GST| GST  ";
          ENDIF
          PRINT     "|EDI       |"
          ADD       ONE,CLNO
.
          PRINT     *1,"|Code ";
          IF        CMCNOHF<>1
            PRINT     "| Fund  Tabl.Type   ";
          ENDIF
          PRINT     "|  Code   |                        |Chg |Type|Grp ":
                    "| Portion | Portion |Inv |Reb|Des";
          IF        IBCNUGST=2
            PRINT     "|App| Code ";
          ENDIF
          PRINT     "|Equivalent|"
          ADD       ONE,CLNO
.
          CALL      UND
          RETURN
.
.******************************************************************************
.*      NORGST - Print MBS Items by GST Code                                  *
.******************************************************************************
NORGST    CALL      KSTGST00              * Keyin Starting GST Code
          CALL      KEDGST00              * Keyin Ending GST Code
          CLOCK     TIME,CTIMEIS
.
          CALL      CONTQST
          BRANCH    CEXIT,NORGST20,NORGST,NORGST99
.
NORGST20  CALL      PROC0000              * Read items and write to tempfile
          CALL      PGST0000                * Print GST report
.
NORGST99  CALL      KILL0000                * Kill tempfile
          GOTO      MENU
+
.******************************************************************************
.*      KSTGST00                                                              *
.******************************************************************************
KSTGST00  DISPLAY   *P1:11,*EF,"Starting GST Code : ";
.
          MOVE      "21",CCOL
          MOVE      "11",CVERT
.
          MOVE      TWO,CKEYTYP
          MOVE      SP10,IBGSCODE
          OPEN      IBAGSTA1,"ibagstaf"
          OPEN      IBAGSTA2,"ibagstaf"
          CALL      KIBGSA00
          MATCH     UNDLN6,IBGSCODE
          IF        @EQUAL
            MOVE      "Start",STRTDESC
            MOVE      SP6,STRTGST
            DISPLAY   *PCCOL:CVERT,*V2LON,STRTDESC
            GOTO      KSTGST99
          ELSE
            PACK      KEY6,IBGSCODE
            CALL      RDIBGS1
            BRANCH    OVRCD,KSTGST80
            MOVE      IBGSDESC,STRTDESC
          ENDIF
.
          MOVE      IBGSCODE,STRTGST
          DISPLAY   *PCCOL:CVERT,*V2LON,STRTGST,SP2,STRTDESC
          GOTO      KEDGST99
.
KSTGST80  DISPLAY   *P1:24,"Invalid Code. ";
          CALL      HITENTER
          GOTO      KSTGST00
.
KSTGST99  RETURN
+
.******************************************************************************
.*      KEDGST00                                                              *
.******************************************************************************
KEDGST00  DISPLAY   *P1:12,*EF,"Ending GST Code   : ";
.
          MOVE      "21",CCOL
          MOVE      "12",CVERT
.
          MOVE      SP10,IBGSCODE
          OPEN      IBAGSTA1,"ibagstaf"
          OPEN      IBAGSTA2,"ibagstaf"
          CALL      KIBGSA00
          MATCH     UNDLN6,IBGSCODE
          IF        @EQUAL
            MOVE      "Finish",ENDGDESC
            MOVE      "zzzzzz",ENDGST
            DISPLAY   *PCCOL:CVERT,*V2LON,ENDGDESC
            GOTO      KEDGST90
          ELSE
            PACK      KEY6,IBGSCODE
            CALL      RDIBGS1
            BRANCH    OVRCD,KEDGST80
            MOVE      IBGSDESC,ENDGDESC
          ENDIF
.
          MOVE      IBGSCODE,ENDGST
          DISPLAY   *PCCOL:CVERT,*V2LON,ENDGST,SP2,ENDGDESC
          GOTO      KEDGST90
.
KEDGST80  DISPLAY   *P1:24,"Invalid Code. ";
          CALL      HITENTER
          GOTO      KEDGST00
.
KEDGST90  MATCH     ENDGST,STRTGST
          GOTO      KEDGST99 IF LESS
          GOTO      KEDGST99 IF EQUAL
.
          DISPLAY   *P1:24,*V2LON,"Ending GST Code must be greater than ":
                    "Starting Code.  ",*HOFF;
          CALL      HITENTER
          GOTO      KEDGST00
.
KEDGST99  RETURN
+
.******************************************************************************
.*      PROC0000   - loop through patitemn and write to tempfile              *
.******************************************************************************
PROC0000  CALL      CREA0000
          MOVE      ZERO,RECCNT
          DISPLAY   *P1:24,*EL,"Loading : ";
          OPEN      PATMCHG1,"patmchgf"
          PACK      KEY9,SP10
          PACK      KEY29,SP70                                        *C-233046
          CALL      RDSMCHG1
PROC1000  CALL      RDKMCHG1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,RECCNT
          IF        RECCNT%20=1
            DISPLAY   *P11:24,*V2LON,PTMCGSTC,SP2,MCHARGE;
          ENDIF
.
          MATCH     STRTGST,PTMCGSTC
          GOTO      PROC1000 IF LESS
.
          MATCH     PTMCGSTC,ENDGST
          GOTO      PROC1000 IF LESS
.
          MOVE      PTMCGSTC,XXGSTC
          MOVE      MCHARGE,XXCHG
          MOVE      MDESC,XXDESC
          MOVE      MCLMCOD,XXCLM
          MOVE      MHFUND,XXFUND
...       MOVE      MHFTAB,XXTABL
          MOVE      MITMTYP,XXITMT
          MOVE      MMSCGRP,XXGRP
          MOVE      MPATPOR,XXPPOR
          MOVE      MHFPOR,XXRPOR
          MOVE      MNINV,XXNINV
          MOVE      MINDIC,XXICHG
          MOVE      PTMCGSTA,XXGSTA
          MOVE      PTMCKREB,XXKREB
          MOVE      PTMCEDIE,XXEDIEQ
          MOVE      PTMCEFDT,XXEFDT
          MOVE      PTMCACTV,XXACTV
          MOVE      PTMCHFTT,XXTTYPE
          MOVE      PTMCETDT,XXETDT
.
          PACK      KEY40,XXGSTC,XXCHG,XXCLM,XXFUND,XXTTYPE,XXEFDT,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
          GOTO      PROC1000
.
PROC9999  RETURN
+
.*****************************************************************************
.*                         CREA0000                                          *
.*                  Create a temp file                                       *
.*****************************************************************************
CREA0000  CALL      KILL0000                * Kill tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    " 105 U1-6,7-15,40-42,43-48,94-96,85-92",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * build the tempfile
          OPEN      TEMPFILE,FNAMEA
.
CREA9999  RETURN
+
.*****************************************************************************
.*                         KILL0000                                          *
.*                   Kill  a temp file                                       *
.*****************************************************************************
KILL0000  CLOSE     TEMPFILE                * close tempfile
          CLEAR     CMDLINE                 * clear command line
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * delete the tempfile
.
KILL9999  RETURN
+
.******************************************************************************
.*                                 PGST0000               Called by:          *
.*                             Print GST Report                               *
.******************************************************************************
PGST0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,RECCNT
          DISPLAY   *P1:24,*EL,"Printing : ";
          CALL      HGST0000                * Print GST header
          PACK      KEY40,SP70
          CALL      RSTEMP1                 * Position on & read a tempfile
PGST1000  CALL      RKTEMP1                   record
          BRANCH    OVRCD,PGST8000
.
          ADD       ONE,RECCNT
          IF        RECCNT%20=1
            DISPLAY   *P12:24,*V2LON,XXGSTC,SP2,XXITMT;
          ENDIF
.
          COMPARE   "60",CLNO
          CALL      HGST0000 IF NOT LESS    * Line counter >= 60
.
          MOVE      ZERO,FORM1
          MOVE      XXICHG,FORM1
          MOVE      DNO,KEY3
          LOAD      KEY3,FORM1,DYES
.
          MOVE      DNO,CKYIDEF3
          MATCH     ANSY,PTMCKREB
          IF        @EQUAL
            MOVE      DYES,CKYIDEF3
          ENDIF
.
          MATCH     "1",XXACTV                 * Inactive
          IF        @EQUAL
            PRINT     *1,"|* ",XXGSTA," |",XXGSTC,"| ",XXCLM,SP1;
          ELSE
            PRINT     *1,"|  ",XXGSTA," |",XXGSTC,"| ",XXCLM,SP1;
          ENDIF
          IF        CMCNOHF<>1
            PRINT     "|",XXFUND,SP1,XXTABL;
          ENDIF
          PRINT     "|",XXCHG,"|",XXDESC,"|   ",XXITMT,"  |  ",XXGRP:
                    " |",XXPPOR,"|",XXRPOR,"| ",XXNINV," |",CKYIDEF3,"|  ",KEY3:
                    " |",XXEDIEQ,"|"
          ADD       ONE,CLNO
          GOTO      PGST1000
.
PGST8000  CALL      UND
          PRINT     *1,"   * Asterisk on the first column denotes that ":
                    "the Misc.Item is Inactive."
          PRINT     *N,*1,"Records Printed : ",RECCNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Completed.  ";
          CALL      HITENTER
.
PGST9000  MOVE      ZERO,EXIT
PGST9999  RETURN
+
.******************************************************************************
.*                                 HGST0000               Called by: PGST0000 *
.*                             Print GST Header                               *
.******************************************************************************
HGST0000  IF        CPAGENO<>0
            CALL      UND
          ENDIF
          CALL      HEAD132
.
          PRINT     *20,"Starting GST Code : ",STRTDESC:
                    *80,"Ending GST Code : ",ENDGDESC,*N
          CALL      UND
.
          PRINT     *1,"| GST| GST  |Claim";
          IF        CMCNOHF<>1
            PRINT     "|    Health     ";
          ENDIF
          PRINT     "| Charge  |Description             |Charge|Charge":
                    "| Patient | Rebate  |No |Key|Change|EDI       |"
.
          PRINT     *1,"| App| Code |Code ";
          IF        CMCNOHF<>1
            PRINT     "| Fund  Tabl.Typ";
          ENDIF
          PRINT     "|  Code   |                        | Type |Group ":
                    "| Portion | Portion |Inv|Reb| Desc |Equivalent|"
.
          CALL      UND
          ADD       "5",CLNO
.
HGST9000  MOVE      ZERO,EXIT
HGST9999  RETURN
+
.*****************************************************************************
.*                   tempfile I/O                                            *
.*****************************************************************************
RATEMP1  RESET     KEY40
         MOVE      ZERO,OVRCD
         READ      TEMPFILE,KEY40;ANS
         GOTO      OVERCOND IF OVER
         RETURN
.
RSTEMP1   READ      TEMPFILE,KEY40;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXGSTC,XXCHG,XXDESC,XXCLM,XXFUND,XXTABL:
                             XXITMT,XXGRP,XXPPOR,XXRPOR,XXNINV,XXICHG:
                             XXGSTA,XXEDIEQ,XXEFDT,XXACTV
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     TEMPFILE,KEY40;XXGSTC,XXCHG,XXDESC,XXCLM,XXFUND,XXTABL:
                                   XXITMT,XXGRP,XXPPOR,XXRPOR,XXNINV,XXICHG:
                                   XXGSTA,XXEDIEQ,XXEFDT,XXACTV
          RETURN
.
          INC       STD001IO
          INC       STDHLPCD
          INC       STDKWSCD
          INC       IBAGKIKY
          INC       IBAGSTKY
          INC       PATCODKY
          INC       PATFNDKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBAGSTIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATFN1IO/INC
          INC       PATMCHIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
ENDIT     CHAIN     PGM
          STOP
+
.


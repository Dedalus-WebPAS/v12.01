.******************************************************************************
.* System  : Waiting Lists                                                    *
.* Program : IBAWAT69                                                         *
.* Desc    : Statistics Validation Program                                    *
.******************************************************************************
.* Date    : 21/06/90                                                         *
.* Author  : Steve O'Gorman                                                   *
.* Mods.   :                                                                  *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.03.01  17/11/1995    Justin Coates  (SUHT mods)                  *
.*                  check for wmstat = 1 for booked patietns                  *
.*        V5.01.03  22/02/1993    Justin Coates  (DUDLEY)                     *
.*                  added check for status 7 and 8                            *
.*        V5.01.02  24/01/1991    Justin Coates                               *
.*                  Changed to use BOKRC1FD (and not BOKMASFD)                *
.******************************************************************************
.
          INC       STD001FD
          INC       BOKRC1FD/INC            * booking file
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       WATPNPFD/INC
          INC       WATTR1FD/INC
.
.********************************************************************** 
.*                            Constants                               *
.********************************************************************** 
.
PIPE      INIT      "|"
UNDLN65   INIT      "-----------------------------------":
                    "------------------------------"
.
.********************************************************************** 
.*                            Variables                               *
.********************************************************************** 
.
ERRMESS   DIM       70
.
.
PRGNAM    INIT      "Statistical Validation Program"
PRGID     INIT      "IBAWAT69"
VERSION   INIT      "V12.01.00"
+
.********************************************************************** 
.*                             ML0000                                 *
.*                        Mainline Routine                            *
.********************************************************************** 
ML0000    CALL      INIT0000               * initialization and open files
          CALL      OPTN0000               * select options
          CALL      SCR0000                * display screen format
          CALL      HEAD0000               * Print Report Heading
.
          PACK      KEY19,SP20
          CALL      RDSTREA1
.
ML1000    CALL      RDKTREA1
          BRANCH    OVRCD,ML9999
.
          BRANCH    WMSTAT,ML1000           * unscheduled
.
          DISPLAY   *P16:10,*EL:            * clear the screen
                    *P16:11,*EL:
                    *P16:12,*EL:
                    *P16:13,*EL:
                    *P16:14,*EL:
                    *P16:15,*EL;
.
          DISPLAY   *P16:10,*V2LON,WMBOOK:  * display waiting lists variables
                    *P16:11,WMSTAT:
                    *P16:12,WMDATE1;

          BRANCH    WMSTAT,ML1000,ML2000,ML3000,ML4000,ML5000:
                           ML1000,ML7000,ML8000
.
. ----    Ivalid Status  ----
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Waiting Lists Status ":
                               *V2LON,DADMNO,*HOFF,".  ";
          CALL      HITENTER
          MOVE      "Invalid Waiting List Status",ERRMESS
          CALL      PRNT0000
          GOTO      ML1000
.
.
. ----    Patient is Scheduled, Check Booking File  ----
.
ML2000    CALL      BOOK0000
          GOTO      ML1000
.
. ----    Patient is Pre-Admitted, Check Booking File  ----
.
ML3000    CALL      BOOK0000
          GOTO      ML1000
.
. ----    Patient is Admitted, Check Booking & Admission Files  ----
.
ML4000    CALL      BOOK0000
          CALL      ADMN0000
          GOTO      ML1000
.
. ----    Patient is Discharged, Check Booking Admission & Dischage File  ----
.
ML5000    CALL      BOOK0000
          CALL      ADMN0000
          CALL      DSCH0000
          GOTO      ML1000
.
. ------- patient is performed -------
.
ML7000    CALL      BOOK0000                * check booking file
          CALL      ADMN0000                * check admission file
          GOTO      ML1000
.
. ------- patient is not-performed -------
.
ML8000    CALL      BOOK0000                * check booking file
          CALL      NOTP0000                * check not performed file
          GOTO      ML1000
.
ML9999    CALL      END0000                * print end of report
          CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.*  Any other general initialization can also be performed here.            *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
          CALL      IBACLOCK
.
          DISPLAY   *P64:24,"Opening"
.
          DISPLAY   *P72:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P72:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P72:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P72:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P72:24,"watpnpaf";
          OPEN      WATPNPA1,"watpnpaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.*              COPTION = 1  Insert option selected                         *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Run Statistics Validation ":
                                            "Program":
                    *P1:7,"Please Select : ",UNDLN1;
.
OPTN0100  KEYIN     *P17:7,*EL,*V2LON,COPTION;
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN1000
.
          BEEP
          GOTO      OPTN0100
.
OPTN1000  MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT                   * set EXIT = 1
OPTN9999  RETURN
+
.**************************************************************************
.*                                  SCR0000            Called by: ML0000  *
.*                         display screen format                          *
.**************************************************************************
.
SCR0000   DISPLAY   *P1:3,*EF:
                    *P1:10,"Waiting List : ":
                    *P1:11,"Status       : ":
                    *P1:12,"W.L. Date    : ":
                    *P1:13,"Booking      : ":
                    *P1:14,"Admission    : ":
                    *P1:15,"Discharge    : ";
.
SCR9999   RETURN
+
.**************************************************************************
.*                                 HEAD0000            Called by: ML0000  *
.*                           Print Report Heading                         *
.**************************************************************************
.
HEAD0000  MOVE      ONE,CNOUNDLN
          PRINT     *N,*1;
          CALL      HEAD132
.
          PRINT     *N:
                    *N,*1,ASK,UNDLN65,UNDLN65,ASK:
                    *N,*1,PIPE," UR Number":
                       *13,PIPE," Procedure":
                       *25,PIPE," Count":
                       *32,PIPE," Booking":
                       *43,PIPE," W.L. Date":
                       *56,PIPE," Error Message":
                       *132,PIPE:
                    *N,*1,ASK,UNDLN65,UNDLN65,ASK;
.
          MOVE      SEVEN,CLNO
.
HEAD9999  RETURN
+
.**************************************************************************
.*                               BOOK0000              Called by: ML0000  *
.*                       Validate the Booking File                        *
.**************************************************************************
BOOK0000  MOVE      WMBOOK,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,BOOK9000
.
          DISPLAY   *P16:13,*V2LON,BKREBOOK
          GOTO      BOOK9999
.
BOOK9000  DISPLAY   *P1:24,*EL,*B,"Missing Booking Record ":
                               *V2LON,BKREBOOK,*HOFF," - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          DISPLAY   *P1:24,*EL;
.
          MOVE      "Missing Booking File Record",ERRMESS
          CALL      PRNT0000
.
BOOK9999  RETURN
+
.**************************************************************************
.*                               ADMN0000              Called by: ML0000  *
.*                       Validate the Admission File                      *
.**************************************************************************
.
ADMN0000  MOVE      WMBOOK,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,ADMN9000
.
          DISPLAY   *P16:14,*V2LON,AADMNO;
          GOTO      ADMN9999
.
ADMN9000  DISPLAY   *P1:24,*EL,*B,"Missing Admission Record ":
                               *V2LON,AADMNO,*HOFF," - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
.
          MOVE      "Missing Admission File Record",ERRMESS
          CALL      PRNT0000
.
ADMN9999  RETURN
+
.**************************************************************************
.*                               DSCH0000              Called by: ML0000  *
.*                      Validate the Discharge File                       *
.**************************************************************************
DSCH0000  MOVE      WMBOOK,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,DSCH9000
.
          DISPLAY   *P16:15,*V2LON,DADMNO
          GOTO      DSCH9999
.
DSCH9000  DISPLAY   *P1:24,*EL,*B,"Missing Discharge Record ":
                               *V2LON,DADMNO,*HOFF," - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
.
          MOVE      "Missing Discharge File Record",ERRMESS
          CALL      PRNT0000
.
DSCH9999  RETURN
.
.*********************************************************************
.                   NOTP0000
.         Validate the Not performed file
.*********************************************************************
NOTP0000  MOVE      WMBOOK,KEY8
          CALL      RDWTPNP1
          BRANCH    OVRCD,NOTP9000
.
          DISPLAY   *P16:13,*V2LON,WMBOOK
          GOTO      NOTP9999
.
NOTP9000  DISPLAY   *P1:24,*EL,*B,"Missing Record from watpnpaf for visit ":
                               *V2LON,BKREBOOK,*HOFF,".  ";
          CALL      HITENTER
          MOVE      "Missing Not Performed File Record",ERRMESS
          CALL      PRNT0000
.
NOTP9999  RETURN
+
.********************************************************************** 
.*                            PRNT0000                                *
.*                   Print a Line on the Report                       *
.********************************************************************** 
PRNT0000  PRINT     *N,*1,PIPE,SP1,WMURNO:
                       *13,PIPE,SP1,WMCODE:
                       *25,PIPE,SP1,WMCNT:
                       *32,PIPE,SP1,WMBOOK:
                       *43,PIPE,SP1,WMDATE1:
                       *56,PIPE,SP1,ERRMESS:
                       *132,PIPE:
                    *N,*1,PIPE,UNDLN65,UNDLN65,PIPE;
.
          ADD       TWO,CLNO
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
PRNT9999  RETURN
.
.********************************************************************** 
.*                            END0000                                 *
.*                   Print End of Report Line                         *
.********************************************************************** 
END0000   PRINT     *N,*N,*N," ****  End of Report ";
END9999   RETURN
+
.********************************************************************** 
.*                           I/O Routine                              *
.********************************************************************** 
.
          INC       STD001IO
          INC       BOKRC1IO/INC            * booking file
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       WATTR1IO/INC
          INC       WATPNPIO/INC
............................................................................

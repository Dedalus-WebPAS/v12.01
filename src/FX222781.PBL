.***************************************************************************
.* System    :   Patient Management                                        *
.* Program   :   FX222781                                                  *
.* Desc      :   Fixit for Program for Outpatient billing charges          *
.***************************************************************************
.* Date      :   31/05/2010                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will check outpatient invoice pending        *
.* Modifications  :                                                        *
.*        V9.11.00  31/05/2010  J.Tan      CAR 222781                      *
.*                  Created program                                        *
.***************************************************************************
.
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSMTIFD/INC
          INC       PATIPEFD/INC
          INC       OUTBOAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECNUM    FORM      8
SAVADMN   DIM       8
.
PRGID     INIT      "FX222781"
PRGNAM    INIT      "Fixit for Outpatient Billing"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process files with coded fields
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
.
          DISPLAY   *P64:24,"patipenf";
          OPEN      PATIPEN1,"patipenf"
.
          DISPLAY   *P64:24,"outbokaf";
          OPEN      OUTBOKA6,"outbokaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*     Fix all Psych and Rehab suffixes                                      *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EF,"Processing:";
.
          MOVE      ZERO,RECNUM
          MOVE      SP70,SAVADMN
          PACK      KEY14,SP70
          CALL      RSPMMTI1                     * position in file
PROC1000  CALL      RKPMMTI1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof 
.
          DISPLAY   *P13:24,*V2LON,PMMIVISN,"   ",PMMITRAN
.
          MATCH     " 2",PMMISYST
          GOTO      PROC1000 IF NOT EQUAL        * Not an outpatient visit
.
          MATCH     SP70,SAVADMN
          GOTO      PROC2000 IF EQUAL
.
          MATCH     SAVADMN,PMMIVISN
          GOTO      PROC1000 IF EQUAL
.
PROC2000  MOVE      PMMIVISN,SAVADMN
.
          MOVE      ZERO,F1
          MOVE      PMMIVISN,KEY8
          CALL      RDIPEN1
          IF        OVRCD=1
            PRINT     *1,PMMIVISN,*10,"Missing patipenf record. ";
            MOVE      ONE,F1
          ENDIF
          ADD       ONE,RECNUM
.
          PACK      KEY36,PMMIVISN,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,PROC3000
.
          MATCH     PMMIVISN,DOBAOUTN
          GOTO      PROC3000 IF NOT EQUAL
.
          COMPARE   FOUR,OBASTAT                  * Attended?
          GOTO      PROC1000 IF EQUAL
.
          MOVE      SP70,KEY10
          IF        OBASTAT=0
            MOVE      "Not Booked",KEY10
          ENDIF
.
          IF        OBASTAT=1
            MOVE      "Booked",KEY10
          ENDIF
.
          IF        OBASTAT=2
            MOVE      "Booked",KEY10
          ENDIF
.
          IF        OBASTAT=3
            MOVE      "NOT USED",KEY10
          ENDIF
.
          IF        OBASTAT=5
            MOVE      "No Attend",KEY10
          ENDIF
.
          IF        OBASTAT=6
            MOVE      "Suspended",KEY10
          ENDIF
.
          IF        F1=0
            PRINT     *1,DOBAOUTN;
          ENDIF
          PRINT     *40," Invalid Status: ",OBASTAT," - ",KEY10
          GOTO      PROC1000                     * get next record
.
PROC3000  IF        F1=0
            PRINT     *1,PMMIVISN;
          ENDIF
          PRINT     *40,"Missing outbokaf record."
          GOTO      PROC1000                     * get next record
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit complete.  Number of Records found: ":
                    RECNUM;
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSMTIIO/INC
          INC       PATIPEIO/INC
          INC       OUTBOAIO/INC

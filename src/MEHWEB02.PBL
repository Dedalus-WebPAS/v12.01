.----------------------------------------------------------------------------
. Program       : MEHWEB02
.
. Function      : Mental Health Extract Web Server
.
. Modifications : 
.----------------------------------------------------------------------------
. V11.01.01 30/03/2021  Tracey Nguyen   TSK 0904417
.           Recompiled for MEHPATFD - added MHPTFAMW Family/Whanau Involvement
.-----------------------------------------------------------------------------
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V10.14.01 14/08/2019  Davin          TSK 0878359
.           Added RDPMPX21 at SUPCR800 to correctly output pmspx2 tags
. V10.11.05 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.04 26/09/2017  Ebon Clements  TSK 0845423
.           Read IBCNMHOS in INIT0000
. V10.11.03 25/09/2017  Richa Phogat   TSK 0845561
.           Added opganisation ID 'MEHPTFAF.MHPFSORG' in PMFL0000 to display
.           in PRIMHD Transmissions extract
. V10.11.02 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
.----------------------------------------------------------------------
. V10.10.01 03.05.2017  Ebon Clements  TSK 0836364
.           Clear supplementary consumer file vars prior to cgi move - SUPCR110
. V10.08.05 15.08.2016  Ebon Clements  TSK 0820809
.           Only allow copy active supplementary consumer records - SUPCR000
. V10.08.04 18.05.2016  Jill Parkinson TSK 0817063
.           Added call to INTMAS00
. V10.08.03 06.05.2016  Jill Parkinson TSK 0817063
.           Fixed storing of mhscstat
. V10.08.02 05.05.2016  Jill Parkinson TSK 0817063
.           Added WINPAR00
. V10.08.01 29.04.2016  Jill Parkinson TSK 0817063
.           Added in report option 3 - Supplementary Consumer Record
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
. V10.03.02 10/04/2012  Mike Laming    CAR 261603
.           Recompiled for changes to MEHPATFD/IO - Key change & extend MHPTOCUR
. V10.03.01 09/01/2012  Jill Parkinson CAR 258278
.           Fixed 6 months prior calcultion
. V9.12.01  26/06/2009  Mike Laming   CAR 194293
.           Change PRIMHD to handle multi Errors - use new table "mehpewaf"
. ........  22/05/2009  Mike Laming   CAR 196789
.           Change PRIMHD Error Id format from "Ra-ann-nn" to "Raannnn  " to
.           handle "Raannnnn "
. V9.11.02  18/03/2009  Mike Laming   CAR 191877
.           Change PRIMHD Unique No. field to Dim 3 - change appropriate KEYs
. V9.11.01  19/11/2008  Mike Laming   CAR 166690
.           Added PRIMHD Transmission Lists - Report 2 - (as in V9.10)     
. V9.09.01  09/10/2007  J.Tan       CAR 91327
.           Added Data Environment
. V9.06.00  18/04/2006  J.Tan       CAR 91327
.           Created Web Server.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
          INC       RSHWEBDF
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
.
          INC       ALLREFFD/INC            * Allied Health Referral
          INC       MEHCONFD/INC
          INC       MEHINCFD/INC
          INC       MEHSCRFD/INC
          INC       MEHSCRTD/INC
.
          INC       MEHPMEFD/INC            * Mental Health PRIMHD Error Table
          INC       MEHPLSFD/INC            * PRIMHD "LS" Record - Legal Status
          INC       MEHPRDFD/INC            * PRIMHD "RD" Record - Referral Disc
          INC       MEHPATFD/INC            * PRIMHD "AT" Record
          INC       MEHPCNFD/INC            * PRIMHD "CN" Record
          INC       MEHPCOFD/INC            * PRIMHD "CO" Record
          INC       MEHPOIFD/INC            * PRIMHD "OT" & "OI" record
          INC       MEHPTFFD/INC            * PRIMHD Transmission File table
          INC       MEHPEWFD/INC            * PRIMHD multi Errors table
.
          INC       PATDO1FD/INC   * Doctor File
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PMSAIDFD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       OUTSITFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       PMSCCDFD/INC   * Concession Card Details
.
.----------------------------------------------------------------------
COUNTER   FORM      5
NEXTPAGE  FORM      1         * Nextpage parameter
UPDTTYPE  DIM       1         * Update Type (A)dd,(U)pdate,(D)elete,Blank=disp
EXTRACTN  DIM       1
EXTRLINE  DIM       100
FORM8     FORM      8
COLLDATE  DIM       11
DOCTCODE  DIM       8
.
MHINC001  DIM       4      * Selection ID
MHINC002  DIM       20     * Selection Description
MHINC003  DIM       8      * From Date (CCYYMMDD)
MHINC004  DIM       8      * Date (CCYYMMDD)
DATAENVR  DIM       4      * Data Environment (PROD/TEST)
ALCNRAUD  DIM       1
DIM2A     DIM       2
DIM3A     DIM       3
DIM8A     DIM       8
DIM8B     DIM       8
DIM10A    DIM       10
DIM12A    DIM       12
DIM12B    DIM       12
DIM20A    DIM       20
DIM20B    DIM       20
DIM25A    DIM       25
DIM30     DIM       30
ERRUNIQ   DIM       9                                                 *I-194293
FRSTTIME  FORM      1
HTMLLNK9  DIM       80
NOTFOUND  FORM      1                                                 *I-194293
MBSDESC   DIM       70
WELLDESC  DIM       50
ACCMDESC  DIM       50
EMPLDESC  DIM       50
EDUCDESC  DIM       50
.
PMHDIDNT  DIM       23
PMHDSTAT  DIM       10
PMFLIDNO  DIM       11
PMFLRECT  DIM       2
PMFLUNIQ  DIM       2
PRTPDATE  DIM       8
FBOKDATE  DIM       12
OLDDATE   DIM       8
OLDCR002  DIM       8
ENDSTRK   DIM       10
STRTSTRK  DIM       10
.
PREVEXST  DIM       1
PREVMH04  DIM       3
PREVMH05  DIM       3
PREVMH06  DIM       3
PREVMH07  DIM       3
.
LANSS     INIT      "s"
SENT      INIT      "Sent"
REJECT    INIT      "Rejected"
ACCEPT    INIT      "Accepted"
EROR      INIT      "Error"
WARN      Init      "Warning"
DASH      INIT      "-"
.
PATHNAME  DIM      70
INCFILE1  FILE      ASCII, FIXED=256    * see IBARSH16 for the layout
.
.----------------------------------------------------------------------
PRGID     INIT      "MEHWEB02"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Mental Health Extract Web Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      EXTRCT00        * Mental Health Extract
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00       
          GOTO      MAIN1000
.
MAIN1200  CALL      PRMLST00        * PRIMHD Transmission List
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      SUPCR000        * Suplementary Consumer List
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40,NXTPAG50
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG50  CALL      WINPAR00      * Close dframe and redirect
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
+
.*****************************************************************************
.*        Close Window or Frame and Goto New Location                         *
.******************************************************************************
WINPAR00  CALL      OPENHTML
          BRANCH    EXIT,WINPAR99
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#" ":
                             "src=#"../js/Standard.js#"></script>":
                             "<script type=#"text/javascript#" ":
                             "src=#"../js/Custom.js#"></script>":
                             "<script type=#"text/javascript#"> {":
                             " if (self.name==#"PopUpFrame#") {":
                             "  reloadParentFrame(#"",REDIRECT,"#"); }":
                             " else if (self.name==#"PopUpFrame1#") {":
                             "  reload2ParentFrame(#"",REDIRECT,"#"); }":
                             " else { ":
                             "  opener.location.href=#"",REDIRECT,"#";":
                             "  self.close(); }":
                             "}":
                             "</script>"
          CALL      CLOSHTML
.
WINPAR99  RETURN
.
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSAIDA1,"pmsaidaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATIN1A1,"patin1af"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      MEHINCA1,"mehincaf"
          OPEN      MEHINCA2,"mehincaf"
          OPEN      MEHSCRA1,"mehscraf"
          OPEN      MRTMASA1,"mrtmasaf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PMSCCDA1,"pmsccdaf"
.
          OPEN      ALLREFA1,"allrefaf"
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,SIXTY9;*43,MHCNAUDA,*94,MHCNSINP
          READ      CONTROLF,SEVENTY7;*45,PTCNNZED
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          IF        PTCNHDPS = 1
            OPEN      MEHPLSA1,"mehplsaf"     * NZ PRIMHD Extract
            OPEN      MEHPLSA2,"mehplsaf"
            OPEN      MEHPLSA3,"mehplsaf"
            OPEN      MEHPRDA1,"mehprdaf"
            OPEN      MEHPRDA2,"mehprdaf"
            OPEN      MEHPRDA3,"mehprdaf"
            OPEN      MEHPATA1,"mehpataf"
            OPEN      MEHPCNA1,"mehpcnaf"
            OPEN      MEHPCOA1,"mehpcoaf"
            OPEN      MEHPOIA1,"mehpoiaf"
            OPEN      MEHPTFA1,"mehptfaf"
            OPEN      MEHPMEA1,"mehpmeaf"
            OPEN      MEHPEWA1,"mehpewaf"
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      ZERO,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,EXTRACTN
          MOVE      SP70,DATAENVR
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
.
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,VIEWTYPE
          UNPACK    SP70,PMFLRECT,PMFLIDNO
.
          CALL      CPMHSC00
          MOVE      ZERO,PREVEXST
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          eNDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "extractn=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EXTRACTN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "dataenvr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DATAENVR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "oldcr002=",QRYNAME
          IF        @EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00
            PACK      OLDCR002,CCENT,CYEAR,CMON,CDAY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mhinc",QRYNAME
          IF        @EQUAL
            CALL      STINC000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mhscr",QRYNAME
          IF        @EQUAL
            CALL      STSCR000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmflidno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PMFLIDNO      * PRIMHD File Id.
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmflrect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PMFLRECT      * PRIMHD Record Type (LS or RD)
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.
.------------------------------------------------------------
. Set parameters to MHINC file     
.------------------------------------------------------------
STINC000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STINC010,STINC020,STINC030,STINC040
          GOTO      STINC910
.
STINC010  MOVE      QRYDATA,MHINC001
          GOTO      STINC900
.
STINC020  MOVE      QRYDATA,MHINC002
          GOTO      STINC900
.
STINC030  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      MHINC003,CCENT,CYEAR,CMON,CDAY
          GOTO      STINC900
.
STINC040  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      MHINC004,CCENT,CYEAR,CMON,CDAY
          GOTO      STINC900
.
STINC900  MOVE      ZERO,EXIT
          GOTO      STINC999
.
STINC910  MOVE      ONE,EXIT
          GOTO      STINC999
.
STINC999  RETURN
+
.------------------------------------------------------------------------------
. Print Invoces in Bulk
.------------------------------------------------------------------------------
.
EXTRCT00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE            * Display formaction
          GOTO      EXTRCT80 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add formaction
          GOTO      EXTRCT10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update formaction
          GOTO      EXTRCT20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete formaction
          GOTO      EXTRCT30 IF EQUAL
.
          GOTO      EXTRCT91
.
.         ************ ADD FORMACTION **********
.
EXTRCT10  PACK      KEY4,MHINC001,SP70
          CALL      RDMHINC1         * read bulk invoice selection
          COMPARE   ZERO,OVRCD
          GOTO      EXTRCT93 IF EQUAL
          CALL      MOVFLD00
          MOVE      ZERO,MHINRECN
          MOVE      ZERO,MHINERRN
.
          MOVE      USERID,MHINCUID
          PACK      MHINCDAT,CCC,CYY,CMM,CDD
          REP       " 0",MHINCDAT
          CLOCK     TIME,MHINCTIM
          MOVE      "0",MHINEFLG     * Generated
          UNPACK    SP70,MHINUUID,MHINUDAT,MHINUTIM,MHINFILN
          MOVE      SP70,MHINSPAR
.
          CALL      WRMHINC1         * write new invoice selection options
          CALL      SCHSEL00         * schedule bulk invoice selection
          GOTO      EXTRCT99
.
.         ************ UPDATE FORMACTION **********
.
EXTRCT20  PACK      KEY4,MHINC001,SP70
          CALL      RDMHINC1         * read bulk invoice selection
          BRANCH    OVRCD,EXTRCT92
          CALL      MOVFLD00
.
          MOVE      USERID,MHINUUID
          PACK      MHINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",MHINUDAT
          CLOCK     TIME,MHINUTIM
          MOVE      "0",MHINEFLG     * Processed
.
          CALL      UPMHINC1         * update new invoice selection options
          CALL      SCHSEL00         * schedule bulk invoice selection
          GOTO      EXTRCT99
.
.         ************ DELETE FORMACTION **********
.
EXTRCT30  PACK      KEY4,MHINC001,SP70
          CALL      RDMHINC1         * Add into Current File
          BRANCH    OVRCD,EXTRCT92
          CALL      DEMHINC1
          GOTO      EXTRCT99
.
.         ************ DISPLAY FORMACTION **********
.
EXTRCT80  PACK      KEY4,MHINC001,SP10
          CALL      RDMHINC1
          IF        OVRCD=1
            CALL      CLRINC00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Mental Health Information National Collection",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,EXTRCT99
          CALL      WEBBOD00                * Process Web Body
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EXTRCT99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
EXTRCT91  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EXTRCT99
.
EXTRCT92  MOVE      "Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EXTRCT99
.
EXTRCT93  MOVE      "Record already exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EXTRCT99
.
EXTRCT99  RETURN
+
.------------------------------------------------------------
.         CLRINC00 - Clear fields
.------------------------------------------------------------
CLRINC00  MOVE      SP70,MHINSEID
          MOVE      SP70,MHINDESC
          MOVE      SP70,MHINFDAT
          MOVE      SP70,MHINTDAT
          MOVE      SP70,MHINRECN
          MOVE      SP70,MHINERRN
          MOVE      SP70,MHINFILN
          MOVE      SP70,MHINSPAR
CLRINC99  RETURN
+
.------------------------------------------------------------
.         MOVFLD00 - Move cgi variables to fields
.------------------------------------------------------------
MOVFLD00  MOVE      MHINC001,MHINSEID
          MOVE      MHINC002,MHINDESC
          MOVE      MHINC003,MHINFDAT
          MOVE      MHINC004,MHINTDAT
MOVFLD99  RETURN
+
.--------------------------------------------------------------------------
.*                             SCHSEL00                                   *
.*               Schedule Mental health Extraction in IBARSH16            *
.--------------------------------------------------------------------------
SCHSEL00  MOVE      "IBARSH16",SCHDPGID
          MOVE      "Mental Health Extraction",SCHDDESC
          PACK      SCHDDATE,CCC,CYY,CMM,CDD
          REP       " 0",SCHDDATE
          CLOCK     TIME,SCHDTIME
.
.         Write Keyin parameters
.
          MATCH     "N",EXTRACTN
          IF        @EQUAL
            MOVE      "1",KEYSTARR[1]        * Option 1 - Validate
            MOVE      MHINC001,KEYSTARR[2]   * Selection id
            MOVE      TWO,KEYSTCNT           * Number of Keyins
          ELSE
            MOVE      "2",KEYSTARR[1]        * Option 1 - Process Extract
            MOVE      DATAENVR,KEYSTARR[2]   * Data Environment
            MOVE      MHINC001,KEYSTARR[3]   * Selection id
            MOVE      THREE,KEYSTCNT         * Number of Keyins
          ENDIF
          CALL      SCHPRO00                 * Schedule Extract
.
SCHSEL99  RETURN
+
.-----------------------------------------------------------------------------.
. PRIMHD Transmission File Lists - Report 2
.-----------------------------------------------------------------------------.
PRMLST00  MOVE      ZERO,EXIT
.
          CLEAR     WEBTITLE
          MOVE      "PRIMHD Transmissions",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PRMLST99
          CALL      WEBBOD00   * Process Web Body (calls PROFLD00)
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
.
PRMLST99  RETURN
+
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      MINC0000          * MHINC
          GOTO      PROFLD99
.
PROFLD20  CALL      PRMH0000          * PRIMHD
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32,PROFLD33
          GOTO      PROFLD99
.
PROFLD31  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD32  CALL      MSCR0000    * Supplementary Consumer Record info
          GOTO      PROFLD99
.
PROFLD33  CALL      MISC0000    * Misc fields
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------------------------
. MHINC Extract
.------------------------------------------------------------------------------
MINC0000  BRANCH    FLDITMNO,MINC0100,MINC0200,MINC0300,MINC0400,MINC0500:
                             MINC0600,MINC0700,MINC0800,MINC0900
          GOTO      MINC9999
.
MINC0100  CALL      INCTAB00          * List of MHINC extract
          GOTO      MINC9999
.
MINC0200  MATCH     MHINSEID,SP70
          GOTO      MINC9999 IF EQUAL
          WRITE     HTMLFILE;MHINSEID;
          GOTO      MINC9999
.
MINC0300  MATCH     MHINDESC,SP70
          GOTO      MINC9999 IF EQUAL
          WRITE     HTMLFILE;MHINDESC;
          GOTO      MINC9999
.
MINC0400  MATCH     MHINFDAT,SP70
          GOTO      MINC9999 IF EQUAL
          UNPACK    MHINFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      MINC9999
.
MINC0500  MATCH     MHINTDAT,SP70
          GOTO      MINC9999 IF EQUAL
          UNPACK    MHINTDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      MINC9999
.
MINC0600  WRITE     HTMLFILE;MHINEFLG;
          GOTO      MINC9999
.
MINC0700  WRITE     HTMLFILE;MHINFILN;
          GOTO      MINC9999
.
MINC0800  CALL      LEXTR000               * List Extraction file
          GOTO      MINC9999
.
MINC0900  WRITE     HTMLFILE;MHINC001;
          GOTO      MINC9999
.
MINC9999  RETURN
+
.------------------------------------------------------------------------------
.         List of MHINC extract
.------------------------------------------------------------------------------
INCTAB00  MOVE      Z70,KEY12
          CALL      RSMHINC2
INCTAB10  CALL      RPMHINC2
          BRANCH    OVRCD,INCTAB99
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateSelections('",HTMLLINK
          APPEND    MHINSEID,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      "Extracted ",KEY10
          MATCH     "1",MHINEFLG
          IF        @EQUAL
            MOVE      "Processed ",KEY10
          ENDIF
.
          MATCH     SP70,MHINUDAT
          IF        @EQUAL
            MOVE      MHINCDAT,MHINUDAT         * use the date created
          ENDIF
.
          MATCH     SP70,MHINUTIM
          IF        @EQUAL
            MOVE      MHINCTIM,MHINUTIM         * use the time created
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",MHINSEID,"#",":
                             "#"",MHINDESC,"#",":
                             "#"",MHINFDAT,"#",":
                             "#"",MHINTDAT,"#",":
                             "#"",MHINFILN,"#",":
                             "#"",MHINRECN,"#",":
                             "#"",MHINERRN,"#",":
                             "#"",MHINUDAT,MHINUTIM,"#",":
                             "#"",KEY10,"#");"
.
          GOTO      INCTAB10
.
INCTAB99  RETURN
+
.------------------------------------------------------------------------------
.         List of MHINC extract
.------------------------------------------------------------------------------
LEXTR000  PACK      KEY4,MHINC001,SP70
          CALL      RDMHINC1
          BRANCH    OVRCD,LEXTR9999
.
          CALL      OPNFIL00            * Open extraction file
          BRANCH    EXIT,LEXTR9999
.
LEXTR100  READ      INCFILE1,SEQ;EXTRLINE
          GOTO      LEXTR900 IF OVER
.
          WRITE     HTMLFILE;"<tr><td>",EXTRLINE,"</td></tr>"
          GOTO      LEXTR100
.
LEXTR900  CLOSE     INCFILE1
LEXTR999  RETURN
+
.------------------------------------------------------------------------------
.         Open Extraction file
.------------------------------------------------------------------------------
OPNFIL00  STRIP     PTCNNZED
          MOVE      SP70,PATHNAME
          CLEAR     PATHNAME
          MOVE      PTCNNZED,PATHNAME
          ENDSET    PATHNAME
          APPEND    MHINFILN,PATHNAME
          RESET     PATHNAME
.
          CLOSE     INCFILE1
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      INCFILE1,PATHNAME
          TRAPCLR   IO
          BRANCH    OVRCD,OPNFIL90
          MOVE      ZERO,EXIT
          GOTO      OPNFIL99
.
OPNFIL90  MOVE      ONE,EXIT
OPNFIL99  RETURN
+
.******************************************************************************
. PRMH0000   PRIMHD list output routine                                       .
.******************************************************************************
PRMH0000  BRANCH    FLDITMNO,PRMH0010,PRMH0020,PRMH0030,PRMH0040
          GOTO      PRMH9999
.
PRMH0010  CALL      PMFL0000                * List Transmission Files
          GOTO      PRMH9999
.
PRMH0020  CALL      PMSF0000                * List selected Transmission File
          GOTO      PRMH9999
.
PRMH0030  CALL      PMSFH000                * selected Transmit File Heading
          GOTO      PRMH9999
.
PRMH0040  CALL      PMER0000                * Error list for selected item
          GOTO      PRMH9999
.
.
PRMH9999  RETURN
.-------------------------------------------------------------------
.   List Transmission files
.-------------------------------------------------------------------
PMFL0000  MOVE      ZERO,EXIT
          PACK      KEY4,CCC,CYY            * calc. a date 6 months earlier
          MOVE      KEY4,F4
          MOVE      CMM,F2
          IF        F2 > 6
            SUB       "6",F2
          ELSE
            ADD       "6",F2
            SUB       ONE,F4
          ENDIF
          MOVE      F4,KEY4
          PACK      OLDDATE,KEY4,F2,CDD
          REP       " 0",OLDDATE
.
          PACK      KEY11,Z70
          CALL      RSMHPTF1
PMFL1000  CALL      RPMHPTF1
          BRANCH    OVRCD,PMFL9999
.
          MATCH     OLDDATE,MHPFXDAT
          GOTO      PMFL9999 IF LESS
.
          CLEAR     HTMLLINK
          APPEND    "javascript:ListXmitData('",HTMLLINK
          APPEND    MHPFXDAT,HTMLLINK
          APPEND    MHPFXNUM,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          PACK      PMHDIDNT,MHPFXDAT,SP2,MHPFXNUM,SP2,MHPFSORG,SP70
          MOVE      MHPFSTAT,F1
          LOAD      PMHDSTAT,F1,SENT,REJECT,ACCEPT
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":       0
                           "#"",PMHDIDNT,"#",":                  1
                           "#"",MHPFXDAT,"#",":                  2
                           "#"",PMHDSTAT,"#",":                  3
                           "#"",MHPFFDAT,"#",":                  4
                           "#"",MHPFTDAT,"#",":                  5
                           "#"",MHPFRDNO,"#",":                  6
                           "#"",MHPFRDER,"#",":                  7
                           "#"",MHPFRDWN,"#",":                  8
                           "#"",MHPFLSNO,"#",":                  9
                           "#"",MHPFLSER,"#",":                  10
                           "#"",MHPFLSWN,"#");"            *     11
          GOTO      PMFL1000
.
PMFL9999  RETURN
.
.-------------------------------------------------------------------
.   List selected Transmission file
.-------------------------------------------------------------------
PMSF0000  MOVE      ZERO,EXIT
          MATCH     SP20,PMFLIDNO
          GOTO      PMSF9999 IF EQUAL
.
          PACK      KEY11,PMFLIDNO          * read the Transmission File record
          CALL      RDMHPTF1
          BRANCH    OVRCD,PMSF9999
          MOVE      MHPFSTAT,F1
          LOAD      PMHDSTAT,F1,SENT,REJECT,ACCEPT
.
          PACK      KEY22,PMFLIDNO,SP20     * read thru "LS" records first
          CALL      RSMHPLS1
PMSF1000  CALL      RKMHPLS1
          BRANCH    OVRCD,PMSF5000
.
          PACK      KEY11,MHPSXDAT,MHPSXNUM,SP20
          MATCH     KEY11,PMFLIDNO
          GOTO      PMSF5000 IF NOT EQUAL   * not the same PRIMHD File Id.?
.
          PACK      KEY8,MHPSURNO,SP10
          CALL      PMMAST00                * read and format Master Info
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateLSrecord('",HTMLLINK
          APPEND    MHPSURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    MHPSVISN,HTMLLINK       * Unique Legal Status Id. (not a vis
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      "Legal Status",DIM12A
          MOVE      MHPSUNIQ,KEY2
          REP       " 0",KEY2
          PACK      DIM12B,MHPSVISN,SP2,KEY2,SP20
          MOVE      SP2,KEY2
          MOVE      MHPFSTAT,F1
          LOAD      PMHDSTAT,F1,SENT,SP20,ACCEPT
          IF        F1 = 2
            MOVE      MHPSSTAT,F2
            LOAD      PMHDSTAT,F2,SENT,REJECT,SP20
          ENDIF
.
          MOVE      "1",KEY1
          MOVE      " / ",DIM3A
          MATCH     SP3,MHPSECNT
          IF        @EQUAL
            MATCH     SP3,MHPSWCNT
            IF        @EQUAL
              MOVE      "0",KEY1
              MOVE      SP3,DIM3A
            ENDIF
          ENDIF
          PACK      DIM10A,MHPSECNT,DIM3A,MHPSWCNT
.
          CLEAR     HTMLLNK9
          APPEND    "javascript:ViewErrors('",HTMLLNK9
          APPEND    MHPFXDAT,HTMLLNK9
          APPEND    MHPFXNUM,HTMLLNK9
          APPEND    "','",HTMLLNK9
          APPEND    MHPSVISN,HTMLLNK9
...       APPEND    "','",HTMLLNK9
...       APPEND    MHPSUNIQ,HTMLLNK9
          APPEND    "','LS",HTMLLNK9
          APPEND    "')",HTMLLNK9
          RESET     HTMLLNK9
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":       0
                           "#"",FORMATNM,"#",":                  1
                           "#"",PSEX,"#",":                      2
                           "#"",PBDATE,"#",":                    3
                           "#"",PURNO,"#",":                     4
                           "#"",DIM12A,"#",":                    5
                           "#"",DIM12B,"#",":                    6
                           "#"",PMHDSTAT,"#",":                  7
                           "#"",DIM10A,"#",":                    8
                           "#"",HTMLLNK9,"#",":                  9
                           "#"",KEY1,"#");"                *    10
.
          GOTO      PMSF1000
.
.
PMSF5000  PACK      KEY19,PMFLIDNO,SP20     * read thru "RD" records first
          CALL      RSMHPRD1
PMSF5500  CALL      RKMHPRD1
          BRANCH    OVRCD,PMSF9999
.
          PACK      KEY11,MHPDXDAT,MHPDXNUM,SP20
          MATCH     KEY11,PMFLIDNO
          GOTO      PMSF9999 IF NOT EQUAL   * not the same PRIMHD File Id.?
.
          PACK      KEY8,MHPDURNO,SP10
          CALL      PMMAST00                * read and format Master Info
.
          MOVE      SP3,ALREDEPT
          PACK      KEY8,MHPDVISN,SP10
          CALL      RDALREF1                * read and format Master Info
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateReferral('",HTMLLINK
          APPEND    MHPDURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    MHPDVISN,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    ALREDEPT,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      "Referral",DIM12A
          PACK      DIM12B,MHPDVISN,SP20
          MOVE      MHPFSTAT,F1
          LOAD      PMHDSTAT,F1,SENT,SP20,ACCEPT
          IF        F1 = 2
            MOVE      MHPDSTAT,F2
            LOAD      PMHDSTAT,F2,SENT,REJECT,SP20
          ENDIF
.
          MOVE      "1",KEY1
          MOVE      " / ",DIM3A
          MATCH     SP3,MHPDECNT
          IF        @EQUAL
            MATCH     SP3,MHPDWCNT
            IF        @EQUAL
              MOVE      "0",KEY1
              MOVE      SP3,DIM3A
            ENDIF
          ENDIF
          PACK      DIM10A,MHPDECNT,DIM3A,MHPDWCNT
.
          CLEAR     HTMLLNK9
          APPEND    "javascript:ViewErrors('",HTMLLNK9
          APPEND    MHPFXDAT,HTMLLNK9
          APPEND    MHPFXNUM,HTMLLNK9
          APPEND    "','",HTMLLNK9
          APPEND    MHPDVISN,HTMLLNK9
          APPEND    "','RD",HTMLLNK9
          APPEND    "')",HTMLLNK9
          RESET     HTMLLNK9
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":       0
                           "#"",FORMATNM,"#",":                  1
                           "#"",PSEX,"#",":                      2
                           "#"",PBDATE,"#",":                    3
                           "#"",PURNO,"#",":                     4
                           "#"",DIM12A,"#",":                    5
                           "#"",DIM12B,"#",":                    6
                           "#"",PMHDSTAT,"#",":                  7
                           "#"",DIM10A,"#",":                    8
                           "#"",HTMLLNK9,"#",":                  9
                           "#"",KEY1,"#");"                 *    10
.
          GOTO      PMSF5500
.
PMSF9999  RETURN
.
.-------------------------------------------------------------------
.   selected Transmission file Heading
.-------------------------------------------------------------------
PMSFH000  UNPACK    DIM127,D1,KEY1,DIM127
          MATCH     SP20,PMFLIDNO
          GOTO      PMSFH999 IF EQUAL
.
          PACK      KEY11,PMFLIDNO          * read the Transmission File record
          CALL      RDMHPTF1
          BRANCH    OVRCD,PMSFH999
.
          MOVE      KEY1,F1
          BRANCH    F1,PMSFD500
.
          WRITE     HTMLFILE;KEY11
          GOTO      PMSFH999

PMSFD500  MOVE      MHPFSTAT,F1
          LOAD      PMHDSTAT,F1,SENT,REJECT,ACCEPT
.
          WRITE     HTMLFILE;"PRIMHD Transmission File - Id. ":
                             KEY11," - File ",PMHDSTAT;
.
PMSFH999  RETURN
.
.-------------------------------------------------------------------
.   selected Transmission file Errors List
.-------------------------------------------------------------------
PMER0000  MOVE      ZERO,EXIT
          MATCH     SP20,PMFLIDNO
          GOTO      PMER9999 IF EQUAL
.
          MATCH     "LS",PMFLRECT
          GOTO      PMER5000 IF NOT EQUAL
.
.                                           * Legal Status Errors List
          MOVE      "1",FRSTTIME
          PACK      KEY22,PMFLIDNO,ADMISSNO,PMFLUNIQ,SP20
          CALL      RSMHPLS1
PMER1000  CALL      RKMHPLS1
          BRANCH    OVRCD,PMER9999
.
          PACK      KEY19,MHPSXDAT,MHPSXNUM,MHPSVISN,SP20
          MATCH     KEY19,KEY22
          GOTO      PMER9999 IF NOT EQUAL   * not the same PRIMHD LS Id.?
.
          MOVE      MHPSETYP,KEY1
          REP       "E-W-",KEY1
          MATCH     "-",KEY1
          GOTO      PMER1000 IF NOT EQUAL   * no errors or warnings
.
          COMPARE   "1",FRSTTIME
          GOTO      PMER2000 IF NOT EQUAL
.
          PACK      KEY8,MHPSURNO,SP10
          CALL      PMMAST00                * read and format Master Info
.
.                                           * setup list headings
          WRITE     HTMLFILE;"<tr><td><img src=../images/PatientFolder.gif ":
                             " class=ListIcon onclick='UpdateLSrecord(#"":
                             MHPDURNO,"#",#"",MHPDVISN,"#")'>";
          WRITE     HTMLFILE;"&nbsp;",FORMATNM,"</td></tr>"
....      WRITE     HTMLFILE;"Legal Status Errors/Warnings</td></tr>"
....      WRITE     HTMLFILE;"<tr><td><img src=../images/PatientFolder.gif>";
....      WRITE     HTMLFILE;"&nbsp;",FORMATNM,"</td></tr>"
          WRITE     HTMLFILE;"<tr><td class=HeadingCell>Record Type</td>"
          WRITE     HTMLFILE;"<td class=HeadingCell>Record Id.</td>"
          WRITE     HTMLFILE;"<td class=HeadingCell>Type</td>"
          WRITE     HTMLFILE;"<td class=HeadingCell>Reference</td>"
          WRITE     HTMLFILE;"<td class=HeadingCell>Message</td></tr>"
.
PMER2000  MOVE      ZERO,FIRSTIME
          PACK      MHPEEMSG,SP70,SP70,SP70 * clear the Message table variables
          UNPACK    SP70,MHPEERCD,MHPETYPE,MHPEREFR
.
          MOVE      "Legal Status",DIM20A
          PACK      DIM20B,MHPSVISN,SP1,MHPSUNIQ,SP20
          REP       "- T ",MHPSSDAT
          SQUEEZE   MHPSSDAT                * remove - & T from Date/Time
          UNPACK    MHPSSDAT,DIM8A,DIM8B    * set up Date & Time fields
.
.                                                                     *C-194293
          PACK      ERRUNIQ,MHPSERID,SP10
          CALL      PMERX000                * write the html line/s
.
          GOTO      PMER1000
.
.         ------------------------- end of "LS" routine ----------------
.
.
.                                           * Referral Errors/Warnings List
PMER5000  MOVE      ZERO,EXIT
          PACK      KEY19,PMFLIDNO,ADMISSNO,SP20
          CALL      RDMHPRD1
          BRANCH    OVRCD,PMER9999
.
          PACK      KEY6,MHPDECNT,MHPDWCNT
          MATCH     SP6,KEY6
          GOTO      PMER9999 IF EQUAL
.
          PACK      KEY8,MHPDURNO,SP10
          CALL      PMMAST00                * read and format Master Info
.
          MOVE      SP3,ALREDEPT
          PACK      KEY8,MHPDVISN,SP10
          CALL      RDALREF1
.
.                                           * setup list headings
          WRITE     HTMLFILE;"Referral Errors/Warnings</td></tr>"
          WRITE     HTMLFILE;"<tr><td><img src=../images/PatientFolder.gif ":
                             " class=ListIcon onclick='UpdateReferral(#"":
                             MHPDURNO,"#",#"",MHPDVISN,"#",#"",ALREDEPT,"#")'>";
          WRITE     HTMLFILE;"&nbsp;",FORMATNM,"</td></tr>"
          WRITE     HTMLFILE;"<tr><td class=HeadingCell>Record Type</td>"
          WRITE     HTMLFILE;"<td class=HeadingCell>Record Id.</td>"
          WRITE     HTMLFILE;"<td class=HeadingCell>Type</td>"
          WRITE     HTMLFILE;"<td class=HeadingCell>Reference</td>"
          WRITE     HTMLFILE;"<td class=HeadingCell>Message</td></tr>"
.
          MOVE      "Referral",DIM20A
          MOVE      MHPDVISN,DIM20B
.                                                                     *C-194293
          PACK      ERRUNIQ,MHPDERID,SP10
          CALL      PMERX000                * write the html line/s
.
.
.                                           * Referral "AT" Error List
PMER5100  PACK      KEY32,PMFLIDNO,ADMISSNO,SP30                      *C-261603
          CALL      RSMHPAT1
PMER5200  CALL      RKMHPAT1
          BRANCH    OVRCD,PMER5500
.
          PACK      KEY19,MHPTXDAT,MHPTXNUM,MHPTVISN,SP20
          MATCH     KEY19,KEY30
          GOTO      PMER5500 IF NOT EQUAL
.
          MOVE      "Activity",DIM20A
          PACK      DIM20B,MHPTVISN,MHPTOCUR,SP20
.                                                                     *C-194293
          PACK      ERRUNIQ,MHPTERID,SP10
          CALL      PMERX000                * write the html line/s
.
          GOTO      PMER5200
.
.
.                                           * Referral "CN" Error List
PMER5500  PACK      KEY30,PMFLIDNO,ADMISSNO,SP30
          CALL      RSMHPCN1
PMER5700  CALL      RKMHPCN1
          BRANCH    OVRCD,PMER6000
.
          PACK      KEY19,MHPNXDAT,MHPNXNUM,MHPNVISN,SP20
          MATCH     KEY19,KEY30
          GOTO      PMER6000 IF NOT EQUAL
.
          MOVE      "Classification",DIM20A
          PACK      DIM20B,MHPNVISN,MHPNOCUR,SP20
.                                                                     *C-194293
          PACK      ERRUNIQ,MHPNERID,SP10
          CALL      PMERX000                * write the html line/s
.
          GOTO      PMER5700
.
.
.                                           * Referral "CO" Error List
PMER6000  PACK      KEY30,PMFLIDNO,ADMISSNO,SP30
          CALL      RSMHPCO1
PMER6200  CALL      RKMHPCO1
          BRANCH    OVRCD,PMER9900
.
          PACK      KEY19,MHPOXDAT,MHPOXNUM,MHPOVISN,SP20
          MATCH     KEY19,KEY30
          GOTO      PMER9900 IF NOT EQUAL
.
          MOVE      "Collection Occassion",DIM20A
          PACK      DIM20B,MHPOVISN,MHPOOCUR,SP20                     
.                                                                     *C-194293
          PACK      ERRUNIQ,MHPOERID,SP10   
          CALL      PMERX000                * write the html line/s
.
.
.                                           * Referral "OT/OI" Error List
PMER6500  PACK      KEY36,PMFLIDNO,ADMISSNO,MHPOURNO,MHPOOCUR,SP30
          CALL      RSMHPOI1
PMER6700  CALL      RKMHPOI1
          BRANCH    OVRCD,PMER6200          * read next "CO" record
.
          PACK      DIM30,MHPIXDAT,MHPIXNUM,MHPIVISN,MHPIURNO,MHPIOCUR,SP20
          MATCH     DIM30,KEY36
          GOTO      PMER6200 IF NOT EQUAL   * read next "CO" record
.
          MATCH     "  0",MHPIOCOI
          IF        @EQUAL
            MOVE      "Outcome Tool",DIM20A
            PACK      DIM20B,MHPIVISN,MHPIOCUR,MHPIOCOT,SP20
          ELSE
            MOVE      "Outcome Item",DIM20A
            PACK      DIM20B,MHPIVISN,MHPIOCUR,MHPIOCOT,MHPIOCOI,SP20
          ENDIF
.
.                                                                     *C-194293
          PACK      ERRUNIQ,MHPIERID,SP10
          CALL      PMERX000                * write the html line/s
.
          GOTO      PMER6700
.
.
PMER9900  MOVE      ZERO,EXIT
.
PMER9999  RETURN
.
.-------------------------------------------------------------------
.   common code to write the html for PMER0000 record types
.-------------------------------------------------------------------
PMERX000  MOVE      ZERO,EXIT
          UNPACK    SP70,MHPEERCD,MHPETYPE,MHPEREFR  * clear Error Message vars
          PACK      MHPEEMSG,SP70,SP70,SP70
.
          MATCH     SP9,ERRUNIQ            * check for no errors
          IF        @EQUAL
            CALL      PMERZ000              * write the html line
.
          ELSE                              * get the Error/Warning Messages
            CMATCH    "R",ERRUNIQ           * is it "old" format single message?
            IF        @EQUAL
              PACK      KEY9,ERRUNIQ
              CALL      RDMHPME1            * read the Error Message Text
              CALL      PMERZ000            * write the html lineA
.
            ELSE                            * errors now on "mehpewaf" I-194293
              CALL      PMERY0000           * read errors in "mehpewaf" + html
            ENDIF
          ENDIF
.
PMERX999  RETURN
.-------------------------------------------------------------------
.   common code to write the html for PMER0000 record types
.-------------------------------------------------------------------
PMERY000  MOVE      ONE,NOTFOUND
          UNPACK    SP70,MHPEERCD,MHPETYPE,MHPEREFR   * clear variables
          PACK      MHPEEMSG,SP70,SP70,SP70
.                                           * KEY12 contains Error Id.
          PACK      KEY12,ERRUNIQ,SP20
          CALL      RSMHPEW1                * position on "mehpewaf"
PMERY100  CALL      RKMHPEW1
          BRANCH    OVRCD,PMERY500
.
          MATCH     ERRUNIQ,MHPWUNIQ
          GOTO      PMERY500 IF NOT EQUAL
.
          MOVE      ZERO,NOTFOUND
          PACK      KEY9,MHPWERCD           * read new Error Message Text
          CALL      RDMHPME1
.
          CALL      PMERZ000                * write the html
          GOTO      PMERY100                * read next error
.
.
PMERY500  COMPARE   ONE,NOTFOUND
          GOTO      PMERY999 IF NOT EQUAL
          MOVE      "System error",MHPEERCD
          MOVE      "Error/Warning message cannot be found",MHPEEMSG
          CALL      PMERZ000                * write the html
.
PMERY999  RETURN
.
.-------------------------------------------------------------------
.   common code to write the html for PMER0000 record types
.-------------------------------------------------------------------
PMERZ000  MOVE      ZERO,EXIT
          MOVE      ZERO,F1
          UNPACK    SP70,DIM12A,DIM25A       * clear variables
.
          MATCH     SP9,MHPEERCD
          GOTO      PMERZ500 IF EQUAL
.
          UNPACK    MHPEERCD,KEY7,KEY2      * reformat the Message ID.
          MATCH     SP2,KEY2
          IF        @EQUAL
            UNPACK    MHPEERCD,KEY2,KEY3,DIM2A
            PACK      DIM12A,KEY2,DASH,KEY3,DASH,DIM2A
          ELSE
            UNPACK    MHPEERCD,KEY2,KEY4,KEY3
            PACK      DIM12A,KEY2,DASH,KEY4,DASH,KEY3
          ENDIF
.
          REP       "E1W2",MHPETYPE         * set text for error type
          MOVE      MHPETYPE,F1
          LOAD      DIM25A,F1,EROR,WARN
.
PMERZ500  WRITE     HTMLFILE;"<tr><td>&nbsp;",DIM20A,"</td>";  * Record Type
          WRITE     HTMLFILE;"<td>&nbsp;",DIM20B,"</td>";      * Record ID.
          WRITE     HTMLFILE;"<td>&nbsp;",DIM25A,"</td>";      * Error Type
          WRITE     HTMLFILE;"<td>&nbsp;",MHPEREFR,"</td>";    * Error Ref.Id.
          WRITE     HTMLFILE;"<td>&nbsp;",MHPEEMSG,"</td>"     * Text Message
.
PMERZ999
.
.-------------------------------------------------------------------
.   Read Master info
.-------------------------------------------------------------------
PMMAST00  MOVE      ZERO,EXIT
.                                           * KEY8 already primed
          CALL      RDMAST1                 * Read the master file for details
          IF        OVRCD=1
            CALL      CLPATMAS
            CLEAR     PSNAME
            APPEND    "Invalid U/R -",PSNAME
            APPEND    KEY8,PSNAME
            RESET     PSNAME
          ENDIF
.
...       CALL      RDPMPX21                * Get Master file 2nd extension
...       CALL      PATFLD00                * Set Patient Folder suffix
...       MOVE      KEY3,FOLDERSF           * Save returned value
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000                * format Patient Name
.
...       MOVE      PBDATE,PRTPDATE
...       CALL      MEHDAT00
.
PMMAST99  RETURN
.
.-----------------------------------------------------------------------------.
.          Format Date
.-----------------------------------------------------------------------------.
MEHDAT00  MOVE      SP70,FBOKDATE
.
          MATCH     PRTPDATE,SP70
          IF        @EQUAL
            MOVE     "&nbsp",FBOKDATE
            GOTO     MEHDAT99
          ENDIF
          UNPACK    PRTPDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      FBOKDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
MEHDAT99  RETURN
+
.------------------------------------------------------------------------------
. Supplementary Consumer Records
.------------------------------------------------------------------------------
.
SUPCR000  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE            * Display formaction
          GOTO      SUPCR800 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add formaction
          GOTO      SUPCR100 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update formaction
          GOTO      SUPCR200 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete formaction
          GOTO      SUPCR300 IF EQUAL
.
          GOTO      SUPCR910
.
.         ************ ADD FORMACTION **********
.
SUPCR100  MOVE      ONE,COUNTER    * calculate counter for this UR (used by
          PACK      KEY16,MHSCR001,SP70        * PRIMHD extract only )
          CALL      RSMHSCR1
SUPCR105  CALL      RKMHSCR1
          BRANCH    OVRCD,SUPCR110
.
          MATCH     MHSCR001,MHSCURNO
          IF        @EQUAL
            ADD       ONE,COUNTER
            GOTO      SUPCR105
          ENDIF
.
SUPCR110  PACK      KEY16,MHSCR001,MHSCR002,SP70
          CALL      RDMHSCR1
          COMPARE   ONE,OVRCD
          GOTO      SUPCR930 IF NOT EQUAL
.
          CALL      CLRSCR00
          CALL      MVSCR000
          MOVE      COUNTER,MHSCCOUN
.
          MOVE      USERID,MHSCCUID
          PACK      MHSCCDAT,CCC,CYY,CMM,CDD
          REP       " 0",MHSCCDAT
          REP       " 0",MHSCSTAT
          CLOCK     TIME,MHSCCTIM
          UNPACK    SP70,MHSCUUID,MHSCUDAT,MHSCUTIM
          MOVE      SP70,MHSCSPAR
.
          PACK      KEY16,MHSCURNO,MHSCSDAT,SP70
          CALL      RAMHSCR1
          IF        OVRCD=1
            CALL      WRMHSCR1
          ENDIF
          GOTO      SUPCR900
.
.         ************ UPDATE FORMACTION **********
.
SUPCR200  MATCH     Z70,OLDCR002
          GOTO      SUPCR250 IF EQUAL
.
          MATCH     OLDCR002,MHSCR002   * check if changing the key value
          GOTO      SUPCR250 IF EQUAL
.
          PACK      KEY16,MHSCR001,MHSCR002,SP70
          CALL      RDMHSCR1
          IF        OVRCD<>1
            GOTO      SUPCR970
          ENDIF
.
          PACK      KEY16,MHSCR001,OLDCR002,SP70
          CALL      RDMHSCR1
          IF        OVRCD=1
            GOTO      SUPCR980
          ENDIF
          GOTO      SUPCR260
.
SUPCR250  PACK      KEY16,MHSCR001,MHSCR002,SP70
          CALL      RDMHSCR1
          BRANCH    OVRCD,SUPCR920
.
SUPCR260  CALL      MVSCR000
.
          MOVE      USERID,MHSCUUID
          PACK      MHSCUDAT,CCC,CYY,CMM,CDD
          REP       " 0",MHSCUDAT
          CLOCK     TIME,MHSCUTIM
.
          CALL      UPMHSCR1
          GOTO      SUPCR900
.
.         ************ DELETE FORMACTION **********
.
SUPCR300  PACK      KEY16,MHSCR001,MHSCR002,SP70
          CALL      RDMHSCR1
          BRANCH    OVRCD,SUPCR920
          CALL      DEMHSCR1
          GOTO      SUPCR999
.
.         ************ DISPLAY FORMACTION **********
.
SUPCR800  PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            CALL      SUPCR940
          ENDIF
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          MOVE      ZERO,PREVEXST
          PACK      PREVMH04,SP70
          PACK      PREVMH05,SP70
          PACK      PREVMH06,SP70
          PACK      PREVMH07,SP70
.
. *** find last record for this UR so it may be defaulted if requested ***
          PACK      KEY16,URNUMBER,Z70
          CALL      RSMHSCR1
SUPCR805  CALL      RPMHSCR1
          BRANCH    OVRCD,SUPCR810
.
          MATCH     URNUMBER,MHSCURNO
          GOTO      SUPCR810 IF NOT EQUAL
.
          MATCH     "1",MHSCSTAT            * Skip if inactive
          GOTO      SUPCR805 IF EQUAL
.
          MOVE      "1",PREVEXST
          PACK      PREVMH04,MHSCUC01,SP70
          PACK      PREVMH05,MHSCUC02,SP70
          PACK      PREVMH06,MHSCUC03,SP70
          PACK      PREVMH07,MHSCUC04,SP70
.
SUPCR810  PACK      KEY16,MHSCR001,MHSCR002,SP70
          CALL      RDMHSCR1
          IF        OVRCD=1
            CALL      CLRSCR00
          ENDIF
.

.    Webtplaf must be read here to get correct description for WEBTITLE
.
          CLEAR     WEBTITLE
          MOVE      "Supplementary Consumer Record",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,SUPCR999
          CALL      WEBBOD00                * Process Web Body
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SUPCR999
.
.         ************ DISPLAY ERROR MESSAGE **********
.
SUPCR900  MOVE      ZERO,EXIT
          GOTO      SUPCR999
.
SUPCR910  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SUPCR999
.
SUPCR920  MOVE      "Supplementary Consumer Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SUPCR999
.
SUPCR930  MOVE      "Existing Supplementary Consumer Record already exists for this date.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SUPCR999
.
SUPCR940  MOVE      "Invalid UR Number",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SUPCR999
.
SUPCR970  MOVE      "Record already exists for this date",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SUPCR999
.
SUPCR980  MOVE      "Invalid Supplementary Consumer Record",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SUPCR999
.
SUPCR999  RETURN
+
.------------------------------------------------------------------------------
. MISC Outputs  
.------------------------------------------------------------------------------
MISC0000  BRANCH    FLDITMNO,MISC0100,MISC0200,MISC0300,MISC0400,MISC0500
          GOTO      MISC9999
.
MISC0100  WRITE     HTMLFILE;PREVEXST;* previous mehscraf record exists for ur
          GOTO      MISC9999
.
MISC0200  WRITE     HTMLFILE;PREVMH04;
          GOTO      MISC9999
.
MISC0300  WRITE     HTMLFILE;PREVMH05;
          GOTO      MISC9999
.
MISC0400  WRITE     HTMLFILE;PREVMH06;
          GOTO      MISC9999
.
MISC0500  WRITE     HTMLFILE;PREVMH07;
          GOTO      MISC9999
.
MISC9999  RETURN
+
.------------------------------------------------------------------------------
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       DAYOFWEK
          INC       RSHWEBCD
          INC       PATNAMCD
          INC       NAMSTRCD
          INC       MEHSCRTM
          INC       PATMASTM
          INC       PMSHCPTM
.
          INC       MEHINCIO/INC
          INC       MEHSCRIO/INC
          INC       ALLREFIO/INC            * Allied Health Referral
.
          INC       MEHPMEIO/INC            * Mental Health PRIMHD Error Table
          INC       MEHPLSIO/INC            * PRIMHD "LS" Record - Legal Status
          INC       MEHPRDIO/INC            * PRIMHD "RD" Record - Referral Disc
          INC       MEHPATIO/INC            * PRIMHD "AT" Record
          INC       MEHPCNIO/INC            * PRIMHD "CN" Record
          INC       MEHPCOIO/INC            * PRIMHD "CO" Record
          INC       MEHPOIIO/INC            * PRIMHD "OT" & "OI" record
          INC       MEHPTFIO/INC            * PRIMHD Transmission File table
          INC       MEHPEWIO/INC            * PRIMHD Rmulti Errors Table
.
          INC       CLPATMAS
          INC       CLPATMIS
          INC       PATDO1IO/INC   * Doctor File
          INC       PATMI1IO/INC
          INC       PMSAIDIO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PATALRIO/INC
          INC       OUTSITIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATFN1IO/INC
          INC       PATWR1IO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       PATITMIO/INC
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATCMCIO/INC
          INC       PATASFIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       APSMASIO/INC
          INC       PATIN1IO/INC
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       PMSCCDIO/INC   * Concession Card Details
.
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       PATDOCTM
          INC       PMSHCGTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATDOCCD
          INC       CALCAGE
.


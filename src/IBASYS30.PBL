.******************************************************************************
.* System    :   Accounting System                                            *
.* Program   :   IBASYS30                                                     *
.* Desc      :   Printer Type Maintenance                                     *
.******************************************************************************
.* Date      :   22.10.90                                                     *
.* Author    :   B.G.Ackland (IBA)                                            *
.* Mods      :                                                                *
.*          C2.00.01  22/02/2000  Steve Downing                               *
.*                    Changed (E)xit to E(X)it                                *
.*               5.01 9/11/1990 Glen Hobby                                    *
.*                    Recompiled for change to IBAESCFD                       *
.*                                                                            *
.******************************************************************************
.$$$$$$
.        PROGRAM IBASYS30
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.        END
.$$$$$$
. Standard System Include
.---------------------------------------
          INC       STDGENDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       IBAPTYFD/INC
          INC       IBAESCFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
PRINTER   DIM       2
SCVERT    FORM      2
UNDLN35   INIT      "___________________________________"
.
CACCTIND  FORM      "0"      * FMSAMAKY ind.  0=all, 1=posting accounts, 2=other
CATEGORY  DIM       2        * category for FMSCODKY
CDATASAV  DIM       20       * save var for keyins
CMDLINE   DIM       80       * Command Line
CODE      DIM       3        * code for FMSCODKY
CSTRING   DIM       35       * keyed in string (routine GSTR)
CSTRLEN   FORM      2        * string length   (routine GSTR)
.
HELPOPTN  FORM      1        * help search option
HELPSRCH  DIM       35       * help search string
ITEMNO    FORM      2
.
PRGID     INIT      "IBASYS30"
PRGNAM    INIT      "Printer Type Maintenance"
VERSION   INIT      "V12.01.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELOPTN            * insert/modify/delete/exit ?
          BRANCH    COPTION,ML1000,ML2000,ML3000
          GOTO      ML9999
.
ML1000    CALL      SCRA0000           * Display Screen Setup CVERT CCOL
          CALL      AESCIB00           * Get Key
          BRANCH    EXIT,ML0100,ML0100
          CALL      ACTA0000           * Accept Fields
          BRANCH    EXIT,ML1100
          CALL      SELA0000           * Allow Modification
          GOTO      ML1000
ML1100    CALL      DELA0000
          GOTO      ML1000
.
.---- Modify data ----
.
ML2000    CALL      SCRA0000
          CALL      KESCIB00
          BRANCH    EXIT,ML0100,ML0100
          CALL      SELA0000           * Allow Modification
          GOTO      ML2000
.
.---- delete data ----
.
ML3000    CALL      SCRA0000
          CALL      KESCIB00
          BRANCH    EXIT,ML0100,ML0100
          CALL      DISA0000
          CALL      DELQST             * ok to delete (y/n/c) ?
          BRANCH    CEXIT,ML2100,ML3000,ML0100
.
ML2100    CALL      DELA0000           * delete screen A data
          GOTO      ML3000             * get next entry
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P50:24,"Opening ibaesca1"; 
          OPEN      IBAESCA1,"ibaescaf"
.
          DISPLAY   *P58:24,"ibaesca2"; 
          OPEN      IBAESCA2,"ibaescaf"
.
          DISPLAY   *P58:24,"ibaptya1"; 
          OPEN      IBAPTYA1,"ibaptyaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA0000, redisps  *
.******************************************************************************
SCRA0000  DISPLAY   *P1:4,"    Printer Type Code    : ",*EF:
                    *P1:6,"    Description          : ":
                    *P1:7,"    Laser Printer  (Y/N) : ",*V2LON,*ULON:
                    *P1:8,"Item",*P6:8,"Print Type",*P45:8,"ASCII Decimal Equ.";
          MOVE      "4",CVERT
          MOVE      "28",CCOL
SCRA9999  RETURN
.******************************************************************************
.* ACTA - Accept  Screen A                       Called by ML,                *
.******************************************************************************
ACTA0000  MOVE      IBESPR,PRINTER
          DISPLAY   *P28:4,*V2LON,PRINTER          * Keyin in description
          KEYIN     *P28:6,*DV,UNDLN35:          * Keyin in description
                    *P28:6,*V2LON,*RV,IBESSEQ;
          PACK      IBESSEQ,IBESSEQ,SP70
.
          MATCH     IBESSEQ,SP70
          GOTO      ACTA0200 IF EQUAL        * description spaces ?
          MATCH     IBESSEQ,EXITCHAR
          GOTO      ACTA9500 IF EQUAL            * exit char
          DISPLAY   *P28:6,*V2LON,IBESSEQ;
          GOTO      ACTA0500
.
ACTA0200  DISPLAY   *P1:24,*EL,*B:               * nothing entered
                    "A Description Must Be Entered - ";
          CALL      HITENTER
          GOTO      ACTA0000
.
ACTA0500  MOVE      "28",CCOL
          MOVE      "7",CVERT
          KEYIN     *PCCOL:CVERT,*DV,ANS,"   ":  * read in description
                    *PCCOL:CVERT,*V2LON,*RV,ANS;
          PACK      ANS,ANS,SP70
          REP       "1x2xN1n1Y2y2",ANS
          MOVE      ZERO,FYESNO
          MOVE      ANS,FYESNO
          LOAD      YESNO,FYESNO,NO,YES          * get description
          MATCH     SP70,YESNO
          GOTO      ACTA0600 IF NOT EQUAL        * legal option ?
          BEEP
          GOTO      ACTA0500
ACTA0600  DISPLAY   *PCCOL:CVERT,*V2LON,YESNO
          MOVE      FYESNO,IBESLAS
          SUB       ONE,IBESLAS
.
ACTA1000  PACK      KEY5,PRINTER,SP3
          MOVE      PRINTER,IBESPR
          MOVE      SP3,IBESPT
          CALL      RDIBES1
          COMPARE   ZERO,OVRCD
          GOTO      ACTA9500 IF EQUAL
          CALL      WRIBES1
.
          MOVE      SP70,KEY3
          MOVE      ONE,ITEMNO
          MOVE      "9",CVERT
          CALL      RSIBPT1
ACTA1100  CALL      RKIBPT1
          BRANCH    OVRCD,ACTA9900
          DISPLAY   *P1:CVERT,*V2LON,ITEMNO,*HOFF,".",*P6:CVERT,IBPTCODE,SP1:
                    IBPTDESC
ACTA1110  KEYIN     *P45:CVERT,*DV,UNDLN35:
                    *P45:CVERT,*V2LON,IBESSEQ
          ENDSET    IBESSEQ
          APPEND    SP70,IBESSEQ
          RESET     IBESSEQ
          MATCH     EXITCHAR,IBESSEQ        * Check for EXIT
          GOTO      ACTA9900 IF EQUAL
.
          CALL      CHESC000
          COMPARE   ZERO,EXIT
          GOTO      ACTA1150 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"Invalid Control Sequence. - ";
          CALL      HITENTER
          GOTO      ACTA1110
.
ACTA1150  PACK      KEY5,PRINTER,IBPTCODE
          MOVE      PRINTER,IBESPR
          MOVE      IBPTCODE,IBESPT
          CALL      RAIBES1
          BRANCH    OVRCD,ACTA1200
          CALL      UPIBES1
          GOTO      ACTA1300
.
ACTA1200  CALL      WRIBES1
ACTA1300  ADD       ONE,ITEMNO
          ADD       ONE,CVERT
          COMPARE   "22",CVERT
          GOTO      ACTA1100 IF NOT EQUAL
          MOVE      ONE,ITEMNO
          MOVE      "9",CVERT
          DISPLAY   *P1:8,*EF
          GOTO      ACTA1100 
.
ACTA9500  MOVE      ONE,EXIT
          GOTO      ACTA9999
.
ACTA9900  MOVE      ZERO,EXIT
ACTA9999  MOVE      PRINTER,IBESPR
          RETURN
.******************************************************************************
.* DISA - Display screen A data                  Called by ML0000             *
.******************************************************************************
DISA0000  MOVE      IBESPR,PRINTER
          MOVE      NO,YESNO
          LOAD      YESNO,IBESLAS,YES
          DISPLAY   *P28:4,*V2LON,IBESPR:
                    *P28:6,IBESSEQ:
                    *P28:7,YESNO
          PACK      KEY5,PRINTER,SP70
          MOVE      ONE,ITEMNO
          MOVE      "9",CVERT
          CALL      RDIBES1
DISA1000  CALL      RKIBES1
          BRANCH    OVRCD,DISA9999
          MATCH     IBESPR,PRINTER
          GOTO      DISA9999 IF NOT EQUAL
          MOVE      IBESPT,KEY3
          CALL      RDIBPT1
          DISPLAY   *P1:CVERT,*V2LON,ITEMNO,*HOFF,".",*P6:CVERT,IBPTCODE,SP1:
                    IBPTDESC,*P45:CVERT,IBESSEQ
          ADD       ONE,ITEMNO
          ADD       ONE,CVERT
          COMPARE   "22",CVERT
          GOTO      DISA1000 IF NOT EQUAL
DISA9999  RETURN
.******************************************************************************
.* DELA - Delete data                            Called by ML0000             *
.******************************************************************************
DELA0000  PACK      KEY5,PRINTER,SP70
          CALL      RSIBES1                 * delete old record (if possible)
          CALL      RPIBES1                 * delete old record (if possible)
DELA1000  CALL      RKIBES1
          BRANCH    OVRCD,DELT9999
          MATCH     PRINTER,IBESPR
          GOTO      DELT9999 IF NOT EQUAL
          PACK      KEY5,IBESPR,IBESPT
          CALL      DEIBES1
          CALL      RDIBES1
          GOTO      DELA1000
.
DELT9999  RETURN
.------------------------------------------------------------------------------
. Display and Modify List of All Codes
.------------------------------------------------------------------------------
SELA0000  MOVE      "8",CVERT
          MOVE      ZERO,ITEMNO
          MOVE      ZERO,CPAGENO
          MOVE      IBESPR,PRINTER
          PACK      KEY5,PRINTER,SP70
          MOVE      NO,YESNO
          LOAD      YESNO,IBESLAS,YES
          DISPLAY   *P28:4,*V2LON,PRINTER: 
                    *P28:6,IBESSEQ:
                    *P28:7,YESNO;
.
          CALL      RDIBES1
SELA0010  CALL      RKIBES1
          BRANCH    OVRCD,SELA0020
          MATCH     PRINTER,IBESPR
          GOTO      SELA0020 IF NOT EQUAL
          MATCH     SP3,IBESPT
          GOTO      SELA0010 IF EQUAL
.
          ADD       ONE,ITEMNO
          ADD       ONE,CVERT
          PACK      KEY5,IBESPT,IBESPR
          STORE     KEY5,ITEMNO,ST1,ST2,ST3,ST4,ST5:
                                    ST6,ST7,ST8,ST9,ST10:
                                    ST11,ST12,ST13,ST14,ST15
.
          MOVE      IBESPT,KEY3
          CALL      RDIBPT1
          DISPLAY   *P1:CVERT,*V2LON,ITEMNO,*HOFF,".",*P6:CVERT,IBPTCODE,SP1:
                    IBPTDESC,*P45:CVERT,IBESSEQ
          COMPARE   "14",ITEMNO
          GOTO      SELA0010 IF NOT EQUAL
.
SELA0020  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
          DISPLAY   *P1:24,*EL,"Select Item, (";
          COMPARE   "14",ITEMNO
          GOTO      SELA0025 IF NOT EQUAL
          DISPLAY   *V2LON,"N",*HOFF,")ext page, (";
.
SELA0025  COMPARE   ZERO,CPAGENO
          GOTO      SELA0026 IF EQUAL
          DISPLAY   *V2LON,"P",*HOFF,")revious page, (";
.
SELA0026  DISPLAY   *V2LON,"D",*HOFF,")escription, (";
          DISPLAY   *V2LON,"L",*HOFF,")aser, E(";
          KEYIN     *V2LON,"X",*HOFF,")it ? ",*JR,*V2LON,CCITEMD;
.
          RESET     CCITEMD
          GOTO      SELA0030 IF EOS      * Next Page
          REP       UPPLOW,CCITEMD
          MATCH     "  N",CCITEMD        * Next Page
          GOTO      SELA0030 IF EQUAL
          MATCH     "  P",CCITEMD        * Previous Page
          GOTO      SELA0035 IF EQUAL
          MATCH     "  X",CCITEMD        * Exit
          GOTO      SELA0091 IF EQUAL
          MATCH     "  D",CCITEMD        * Exit
          GOTO      SELA0080 IF EQUAL
          MATCH     "  L",CCITEMD        * Exit
          GOTO      SELA0085 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      SELA0090 IF NOT EQUAL  * Invalid Entry
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      SELA0090 IF LESS       * Invalid Entry
          COMPARE   CCITEM,ITEMNO
          GOTO      SELA0070 IF LESS       * Add Entries
          GOTO      SELA0092               * Item Selected
.
SELA0030  COMPARE   "14",ITEMNO            * Next Page Selected
          GOTO      SELA0090 IF NOT EQUAL  * No Next Page Invalid Selection
          ADD       ONE,CPAGENO
          MOVE      ZERO,ITEMNO            
          MOVE      EIGHT,CVERT
          DISPLAY   *P1:9,*EF
          GOTO      SELA0010
.
SELA0035  COMPARE   ZERO,CPAGENO           * Previous Page Selected
          GOTO      SELA0090 IF EQUAL      * No Previous Page Invalid Selection
          DISPLAY   *P1:9,*EF
          SUB       ONE,CPAGENO
          COMPARE   "14",ITEMNO      
          GOTO      SELA0036 IF EQUAL 
          CALL      RPIBES1            * Initial reverse read
.
SELA0036  CALL      RPIBES1
          SUB       ONE,ITEMNO
          COMPARE   ZERO,ITEMNO
          GOTO      SELA0036 IF NOT EQUAL
          COMPARE   EIGHT,CVERT
          GOTO      SELA0010 IF EQUAL   * Display Down The Page
          MOVE      EIGHT,CVERT
          MOVE      "14",ITEMNO
          GOTO      SELA0036
.
SELA0070  BEEP                          * Not Written
          GOTO      SELA0020
.
SELA0080  PACK      KEY5,SP3,PRINTER
          CALL      RDIBES2
          BRANCH    OVRCD,SELA0020
          KEYIN     *P28:6,*DV,IBESSEQ:          * Keyin in description
                    *P28:6,*V2LON,*RV,IBESSEQ;
          PACK      IBESSEQ,IBESSEQ,SP70
          DISPLAY   *P28:6,*V2LON,IBESSEQ          * Keyin in description
          CALL      UPIBES2
          GOTO      SELA0020
.
SELA0085  PACK      KEY5,SP3,PRINTER
          CALL      RDIBES2
          BRANCH    OVRCD,SELA0020
          KEYIN     *P28:7,*DV,ANS,"   ":  * read in description
                    *P28:7,*V2LON,*RV,ANS;
          PACK      ANS,ANS,SP70
          REP       "1x2xN1n1Y2y2",ANS
          MOVE      ZERO,FYESNO
          MOVE      ANS,FYESNO
          LOAD      YESNO,FYESNO,NO,YES          * get description
          MATCH     SP70,YESNO
          GOTO      SELA0087 IF NOT EQUAL        * legal option ?
          BEEP
          GOTO      SELA0085
SELA0087  DISPLAY   *P28:7,*V2LON,YESNO
          MOVE      FYESNO,IBESLAS
          SUB       ONE,IBESLAS
          CALL      UPIBES2
          GOTO      SELA0020
.
SELA0090  BEEP                          * invalid entry
          GOTO      SELA0020
.
SELA0091  MOVE      SP70,KEY5
          GOTO      SELA0099
.
SELA0092  MOVE      SP70,KEY5
          LOAD      KEY5,CCITEM,ST1,ST2,ST3,ST4,ST5:
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15
          CALL      RDIBES2
          BRANCH    OVRCD,SELA0020
          MOVE      CVERT,SCVERT
          MOVE      CCITEM,CVERT
          ADD       EIGHT,CVERT
SELA0093  KEYIN     *P45:CVERT,*DV,UNDLN35:
                    *P45:CVERT,*V2LON,IBESSEQ
          ENDSET    IBESSEQ
          APPEND    SP70,IBESSEQ
          RESET     IBESSEQ
          CALL      CHESC000
          BRANCH    EXIT,SELA0095
          DISPLAY   *P45:CVERT,IBESSEQ
          CALL      UPIBES2
          MOVE      SCVERT,CVERT
          GOTO      SELA0020
.
SELA0095  DISPLAY   *P1:24,*EL,"Invalid Control Sequence. - ";
          CALL      HITENTER
          GOTO      SELA0093
.
.
SELA0099  RETURN
.
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
RESCIB00  CALL      SCRA0000
          RETURN
.
          INC       IBAESCIO/INC  
          INC       IBAESCKY
          INC       IBAPTYIO/INC  
          INC       STDGENCD
.

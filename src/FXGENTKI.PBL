.*****************************************************************************
.*    Prog         : FXGENTKI                                                *
.*    Description  : Regenerate Private Practice Pathology Test Key Words    *
.*                                                                           *
.*    Author       : Pat Dirito                                              *
.*    Date         : 30/01/2004                                              *
.*    Modifications:                                                         *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       PRITESFD/INC
          INC       PRITKIFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECNUM    FORM      8
TESTCODE  DIM       9
ITEMDESC  DIM       30
SAVITEM   DIM       9
.
PRGID     INIT      "FXGENTKI"
PRGNAM    INIT      "Regenerate Private Practice Pathology Test Keyword File"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000
.
ML1000    CALL      PRIKW000               * Regen Pathology Test Keyword
          GOTO      ML9999
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PRITEST1,"pritestf"
          OPEN      PRITKIA1,"pritkiaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    =       (0)  Valid option selected                  *
.*                              (1)  Exit option selected                   *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  DISPLAY   *P65:2,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Regenerate Private Practice":
                                            " Pathology Test Keyword Index"
.
          KEYIN     *P1:11,*EL,"Select Option : _",*P17:11,*V2LON,COPTION
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    COPTION,OPTN1000
          BEEP
          GOTO      OPTN0000
.
OPTN1000  MOVE      COPTION,OPTION
.
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.****************************************************************************
.*      Regeneration of Key Word Index                                      *
.****************************************************************************
PRIKW000  DISPLAY   *P1:24,*EL,"Recalibrating Key Word Index ..."
          MOVE      ZERO,RECNUM
.
          PACK      KEY24,SP70
          CALL      RSPRTKI1
PRIKW100  CALL      RKPRTKI1
          BRANCH    OVRCD,PRIKW200
.
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY       *P40:24,*V2LON,RECNUM
          ENDIF
.
          PACK      KEY24,PRTKPTST,PRTKPKWD,SP70
          CALL      DEPRTKI1
          GOTO      PRIKW100
.
PRIKW200  DISPLAY   *P1:24,*EL,"Creating Pathology Test Keyword Index..."
          MOVE      ZERO,RECNUM
.
          MOVE      SP70,SAVITEM
.
          PACK      KEY17,Z70      * Want to read newest Item code one
          CALL      RSPRTE1
PRIKW300  CALL      RPPRTE1  
          BRANCH    OVRCD,PRIKW999
.
          MATCH     SAVITEM,PRTECODE    * Only want newest Item Code once
          GOTO      PRIKW300 IF EQUAL  
.
          MOVE      PRTECODE,SAVITEM    * Save Item
.
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY       *P40:24,*V2LON,RECNUM
          ENDIF
.
          MOVE      PRTECODE,TESTCODE   * Input var for PRITKIUP
          MOVE      PRTEDESC,ITEMDESC   * Input var for PRITKIUP
          PROC      PRITKIUP            * Update Pathology Test Keyword Index
          GOTO      PRIKW300
.
PRIKW999  RETURN
. =========================================================================
.
          INC       STD001IO
.
          INC       PRITESIO/INC
          INC       PRITKIIO/INC
          INC       PRITKIPR

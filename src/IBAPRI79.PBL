. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAPRI79                                            *
. *                                                                           *
. *     FUNCTION:         Credit Note Report                                  *
. *                                                                           *
. *     DATE WRITTEN:     13/07/2005                                          *
. *                                                                           *
. *     AUTHOR:           J.Tan                                               *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *     V10.13.01 26/07/2018  J.Tan          TSK 0848506                      *
. *               Changed PP Doctor from DIM6 to DIM10                        *
. *     V10.11.01 04/09/2017  Thanh T.      TSK 0821710                       *
. *               Removed PATCOMFD/IO
. *     V9.11.01  20/10/2008  J.Tan         CAR 178415                        *
. *               Mods checking for Medical practice user access              *
. *          V9.04.00  13/07/2004  J.Tan        CAR 60588                     *
. *                    Created report                                         *
. *****************************************************************************
.
          INC       STD001FD   
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PRICNOFD/INC
          INC       PATSELDT/INC
          INC       PRIINVFD/INC
          INC       PRIALSFD/INC                 * Medical Prac.all user access
          INC       PRIPRAFD/INC                 * medical practice file
          INC       PRISECFD/INC                 * Medical Prac.security access
.
.
.         VARIABLES DEFINITIONS
.
.
CNSTFULL        INIT      "Fully Credited"
CNSTITEM        INIT      "Credit by Item"
FINISH          INIT      "Finish"
START           INIT      "Start"
CNSYSDSC        DIM       3
CNSTDESC        DIM       14
CODE            DIM       2
CODEDATE        DIM       8
CURRDATE        DIM       8
DIM10B          DIM       10
DSPEDDTE        DIM       10               * Display end date (dd/mm/ccyy)
DSPSTDTE        DIM       10               * Display start date (dd/mm/ccyy)
ENDDTE          DIM       8                * End date (ccyymmdd)
EPRAC           DIM       6                * end medical practice code
EMPDESC         DIM       6
GRANDTOT        FORM      12.2
MPRAC           DIM       6
PNAME           DIM       20
SMPDESC         DIM       6
STRTDTE         DIM       8                * Start date (ccyymmdd)
SPRAC           DIM       6                * start medical practice code
NOTESPAT        FORM      6
USERID          DIM       10
ZED6            INIT      "zzzzzz"
.
.*********************************************************************
PRGID     INIT      "IBAPRI79"
PRGNAM    INIT      "Credit Note Report"
VERSION   INIT      "V12.01.00"
.*********************************************************************
+
.*********************************************************************
.         ML1000 OF PROGRAM
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000
          BRANCH    EXIT,ML9000
.
ML1500    CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,ML1000
.
          CALL      SPRA0000               * get starting Medical practice
          BRANCH    EXIT,ML1000            * exitchar input
.
          CALL      EPRA0000               * get ending Medical practice
          BRANCH    EXIT,ML1000            * exitchar input
.
          BRANCH    OPTION,ML2000
          GOTO      ML9000
.
ML2000    CALL      PROCRP00
          BRANCH    EXIT,ML9000
          GOTO      ML1000
.
ML9000    CHAIN     PGM
          STOP
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pricnoaf";
          OPEN      PRICNOA1,"pricnoaf"
          OPEN      PRICNOA4,"pricnoaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          OPEN      PRIPRAC1,"pripracf"
          OPEN      PRIALSA1,"prialsaf"
          OPEN      PRISECA1,"prisecaf"
          OPEN      PRIINVO1,"priinvof"
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.*********************************************************************
.         enter the option
.*********************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Date Range":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  KEYIN     *P1:11,*EL,"User Id : ",USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      OPTN1000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.**************************************************************************
.*                               SPRA0000              Called by: ML0000  *
.*                       input medical practice code                      *
.* Returns:          EXIT  0 = valid code input                           *
.*                         1 = exitchar input                             *
.**************************************************************************
SPRA0000  KEYIN     *P1:13,*EF,"From Medical Practice : ",*DV,UNDLN6:
                    *P25:13,*V2LON,MPRAC,*P25:11,*DV,MPRAC;
.
          CALL      CHKP0000                     * see what was entered
          BRANCH    EXIT,SPRA5000:               * nothing entered
                         SPRA4000:               * ? entered
                         SPRA9500:               * exitchar input
                         SPRA8000                * valid input
          GOTO      SPRA0000                     * invalid input
.
SPRA4000  MOVE      ZERO,HLEF
          CALL      GETSCR00                     * save screen
.
SPRA4200  CALL      PRIPRADS                     * list codes on file
.
SPRA4500  KEYIN     *P1:24,*EL,"From Medical Practice : ",*DV,UNDLN6:
                    *P25:24,*V2LON,MPRAC,*P25:24,*DV,MPRAC;
.
          CALL      CHKP0000                     * see what was entered
          BRANCH    EXIT,SPRA4900:               * nothing entered
                         SPRA4200:               * ? entered
                         SPRA9400:               * exitchar input
                         SPRA7900                * valid input
          GOTO      SPRA4500                     * invalid input
.
SPRA4900  CALL      PUTSCR00                     * restore screen
SPRA5000  MOVE      SP6,SPRAC
          DISPLAY   *P25:13,*V2LON,START;
          MOVE      START,SMPDESC         * save description
          GOTO      SPRA8500
.
SPRA7900  CALL      PUTSCR00                     * restore screen
          DISPLAY   *P25:13,*V2LON,MPRAC;
.
SPRA8000  MOVE      MPRAC,SPRAC
          DISPLAY   *P35:13,PRPRDESC;
          MOVE      MPRAC,SMPDESC         * save description
.
SPRA8500  MOVE      ZERO,EXIT
          GOTO      SPRA9999
.
SPRA9400  CALL      FRESCR00
SPRA9500  MOVE      ONE,EXIT
.
SPRA9999  RETURN
+
.**************************************************************************
.*                               EPRA0000              Called by: ML0000  *
.*                       input medical practice code                      *
.* Returns:          EXIT  0 = valid code input                           *
.*                         1 = exitchar input                             *
.**************************************************************************
EPRA0000  KEYIN     *P1:15,*EF,"To   Medical Practice : ",*DV,UNDLN6:
                    *P25:15,*V2LON,MPRAC,*P25:15,*DV,MPRAC;
.
          CALL      CHKP0000                     * see what was entered
          BRANCH    EXIT,EPRA5000:               * nothing entered
                         EPRA4000:               * ? entered
                         EPRA9500:               * exitchar input
                         EPRA3000                * valid input
          GOTO      EPRA0000                     * invalid input
.
EPRA3000  MATCH     SPRAC,MPRAC                  * end prac < start prac ?
          GOTO      EPRA8000 IF NOT LESS         * no
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "To Practice must be greater than or = From Practice.    ";
          CALL      HITENTER
          GOTO      EPRA0000
.
EPRA4000  MOVE      ZERO,HLEF
          CALL      GETSCR00                     * save screen
.
EPRA4200  CALL      PRIPRADS                     * list codes on file
.
EPRA4500  KEYIN     *P1:24,*EL,"To   Medical Practice : ",*DV,UNDLN6:
                    *P25:24,*V2LON,MPRAC,*P25:24,*DV,MPRAC;
.
          CALL      CHKP0000                     * see what was entered
          BRANCH    EXIT,EPRA4900:               * nothing entered
                         EPRA4200:               * ? entered
                         EPRA9400:               * exitchar input
                         EPRA6000                * valid input
          GOTO      EPRA4500                     * invalid input
.
EPRA4900  CALL      PUTSCR00                     * restore screen
EPRA5000  MOVE      ZED6,EPRAC
          DISPLAY   *P25:15,*V2LON,FINISH;
          MOVE      FINISH,EMPDESC         * save description
          GOTO      EPRA8500
.
EPRA6000  MATCH     SPRAC,MPRAC                  * end prac < start prac ?
          GOTO      EPRA7900 IF NOT LESS         * no
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "To Practice must be greater than or = From Practice.    ";
          CALL      HITENTER
          GOTO      EPRA4500
.
EPRA7900  CALL      PUTSCR00                     * restore screen
          DISPLAY   *P25:15,*V2LON,MPRAC;
.
EPRA8000  MOVE      MPRAC,EPRAC
          DISPLAY   *P35:15,PRPRDESC;
          MOVE      MPRAC,EMPDESC         * save description
.
EPRA8500  MOVE      ZERO,EXIT
          GOTO      EPRA9999
.
EPRA9400  CALL      FRESCR00
EPRA9500  MOVE      ONE,EXIT
.
EPRA9999  RETURN
+
.**************************************************************************
.*                             CHKP0000                Called by: SPRA0000*
.*             see what was input for medical practice            EPRA0000*
.* Returns:           EXIT   0 = invalid input                            *
.*                           1 = nothing input                            *
.*                           2 = ? input                                  *
.*                           3 = exitchar input                           *
.*                           4 = valid input                              *
.**************************************************************************
CHKP0000  ENDSET     MPRAC
          APPEND     SP6,MPRAC
          RESET      MPRAC
.
          MATCH      SP6,MPRAC                    * anything entered ?
          GOTO       CHKP8000 IF EQUAL             * yes
.
          CMATCH     EXITCHAR,MPRAC               * exitchar input ?
          GOTO       CHKP9000 IF EQUAL             * yes
.
          CMATCH     QUEST,MPRAC                  * "?" input ?
          GOTO       CHKP8500 IF EQUAL             * yes
.
          MOVE       MPRAC,KEY6
          CALL       RDPRPR1
          COMPARE    ZERO,OVRCD                   * record on file ?
          GOTO       CHKP9500 IF EQUAL             * yes
.
          DISPLAY    *P1:24,*EL,*B,*V2LON,"Medical Practice not on file.    ";
          CALL       HITENTER
          MOVE       ZERO,EXIT
          GOTO       CHKP9999
.
CHKP8000  MOVE       ONE,EXIT
          GOTO       CHKP9999
.
CHKP8500  MOVE       TWO,EXIT
          GOTO       CHKP9999
.
CHKP9000  MOVE       THREE,EXIT
          GOTO       CHKP9999
.
CHKP9500  MOVE       FOUR,EXIT
.
CHKP9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      "17",CCOL
          DISPLAY   *P1:9,"Starting Date : ",*EF:
                    *P1:10,"Ending Date   : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "9",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      STRTDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE
.
          CALL      PACDATE                 * Format the start date
          MOVE      CPCDATE,DSPSTDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPSTDTE;
.
          MATCH     STRTDTE,CURRDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Start date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE1000
          ENDIF
.
. ----- Keyin end date -----
.
DATE2000  MOVE      "10",CVERT
          UNPACK    STRTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE
.
          CALL      PACDATE                 * Format the end date
          MOVE      CPCDATE,DSPEDDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPEDDTE;
.
          MATCH     ENDDTE,CURRDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
          MATCH     STRTDTE,ENDDTE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must after the start date.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.*********************************************************************
.       Process Report
.*********************************************************************
PROCRP00  DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                     "** Generating Report **"
          DISPLAY   *P1:24,*EL,*P50:24,"U/R Number: ";
.
          MOVE      ZERO,GRANDTOT
          MOVE      ZERO,NOTESPAT
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
.
          PACK      KEY16,STRTDTE,SP70
          CALL      RSPRCNO4
PROCRP10  CALL      RKPRCNO4
          BRANCH    OVRCD,PROCRP60
.
          MATCH     PRCODATE,ENDDTE
          GOTO      PROCRP60 IF LESS
.
          PACK      KEY8,PRCOINVN
          CALL      RDPRIN1
          BRANCH    OVRCD,PROCRP10
.
          PACK      KEY10,USERID,SP70
          CALL      RDPRALS1
          COMPARE   ZERO,OVRCD
          GOTO      PROCRP20 IF EQUAL       * User has access to all Med.Prac.
.
          PACK      KEY16,PRINPRAC,USERID,SP70                              
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROCRP10          * no access
.
PROCRP20  MATCH     SP70,SPRAC
          IF        !@EQUAL
            MATCH     SPRAC,PRINPRAC
            GOTO      PROCRP10 IF LESS
          ENDIF     
.         
          MATCH     SP70,EPRAC
          IF        !@EQUAL
            MATCH     PRINPRAC,EPRAC             * past end medical practice?
            GOTO      PROCRP10 IF LESS           * yes
          ENDIF
.
          ADD       ONE,NOTESPAT
          ADD       PRCOAMNT,GRANDTOT
.
          UNPACK    PRCODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DIM10B,CPCDATE
.
          MOVE      "Unknown",PNAME
          PACK      KEY8,PRCODEBT
          CALL      RDMAST1
          MOVE      PGNAME,KEY1
          PACK      PNAME,KEY1,DOT,PSNAME,SP70
.
          MOVE      SP10,CNSTDESC
          MOVE      ZERO,F1
          MOVE      PRCOSTAT,F1
          LOAD      CNSTDESC,F1,CNSTFULL,CNSTITEM
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.         Print data for report
.
          PRINT     *1,DIM10B,*15,PRCOCRNO,*31,PRCOINVN:
                    *43,PRCODEBT,*55,PNAME,*80,CNSTDESC:
                    *98,PRCOAMNT
          ADD       ONE,CLNO
          GOTO      PROCRP10
.
.         Print end of report
.
PROCRP60  COMPARE   ZERO,CPAGENO
          GOTO      PROCRP84 IF EQUAL
.
          CALL      PRTST000       * Print System Total
          GOTO      PROCRP90
.
PROCRP84  DISPLAY   *P1:24,*EL,*P10:24,*B,*V2LON,"No Patient Selected"
          GOTO      PROCRP90
.
PROCRP90  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Report Generation Completed **"
.
PROCRP99  RETURN
+
.*********************************************************************
.      Display System Totals
.*********************************************************************
PRTST000  PRINT     *N,*50,"Number of Patients with Credit Notes : ",NOTESPAT
          PRINT     *61,"Total of All Credit Notes : ",*89,"$",GRANDTOT,*N:
                    *N,*N,"  ***  END OF REPORT  ***"
PRTST999  RETURN
+
.*********************************************************************
.                 HEADINGS FOR OUTPUT
.*********************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *40,"Date Range : ",DSPSTDTE," To ",DSPEDDTE:
                    *N:
                    *40,"From Medical Practice: ",SPRAC:
                    *70,"To : ",EPRAC:
                    *N,*N,*1,"Credit Note",*15,"Credit Note":
                    *31," Invoice",*44,"   U/R",*80," Credit Note":
                    *N,*3," Date",*15,"  Number",*32,"Number":
                    *43," Number",*55,"Patient Name",*82,"  Status":
                    *104,"  Amount":
                    *N,*1,"-----------",*15,"-----------":
                    *31,"--------",*43,"--------",*55,"--------------------":
                    *80,"--------------",*98,"---------------"
          MOVE      SIX,CLNO
.
HEAD9999  RETURN
+
.
          INC       STD001IO
          INC       PRIPRADS
.
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PRICNOIO/INC
          INC       PRIINVIO/INC
          INC       PRIALSIO/INC                 * Medical Prac.all user access
          INC       PRIPRAIO/INC                 * medical practice file
          INC       PRISECIO/INC                 * Medical Prac.security access
.

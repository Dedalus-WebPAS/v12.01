.******************************************************************************
.* Program   :   WIES11                                                       *
.* Desc      :   Convert text fields into form fields                         *
.******************************************************************************
.* Date      :   24/07/2002                                                   *
.* Author    :   Davin                                                        *
.* Mods      :                                                                *
.*        V9.03.01  11/08/2003  Henry Son                                     *
.*                  Mods for WIES 11 added Hospital in the home fields        *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
          INC       PATW11FD/INC            * WIES11 weights file
.
. ----- file layout - weights.txt -----
.
WEIGHTMP  FILE      ASCII, FIXED=140
.
.Name     Type    Length    Start  Description
.----     ----    ------    -----  -----------
DRGC      DIM       4          1   DRG
DRGD      DIM       60         5   DRG Description
SDMT      DIM       1         65   Same Day Med. Target
MVELIG    DIM       1         66   Mech. Vent. Elig.
OTHCOP    DIM       3         67   Other Copayments (T)
LIBP      DIM       4         70   LIBP
HIBP      DIM       4         74   HIBP
AILOS     DIM       7         78   Av. ILOS
SDOD      DIM       1         85   Same Day / One Day
TSDW      DIM       11        86   Total SDW
TODW      DIM       11        97   Total ODW
TLOMDW    DIM       11       108   Total LOMDW
TIMDW     DIM       11       119   Total IMDW
THOMDW    DIM       11       130   Total HOMDW
THITHW    DIM       10       141   Total HITHW
.
.End of Record               151 
.
. ----- Program Variables -----
.
. ----- Program Constants -----
.
PRGNAM    INIT      "Convert Weights"
PRGID     INIT      "WIES11"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
.
ML0000    CALL      INIT0000                * Initialise variables & open files
          BRANCH    EXIT,ML9000
.
          CALL      OPTN0000                * Ask proceed question
          BRANCH    EXIT,ML9000
.
          CALL      PMST0000                * Process the master file
.
ML9000    MATCH     SP6,PGM
          GOTO      ML9999 IF EQUAL
.
          CHAIN     PGM                     * Chain back to appropriate menu
ML9999    SHUTDOWN  ""
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          OPEN      PATW11A1,"patw11af"
.
. ------- open weights.txt ---------------------------------------------------
.
          OPEN      WEIGHTMP,"weights"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                            Ask Proceed Question                            *
.******************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,"Note : This program converts all ":
                    "records in the patwis11.txt file"
.
          DISPLAY   *P1:24,*EL,*HOFF,"Ok to Proceed (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ";
.
OPTN1000  MOVE      SP1,ANS
          KEYIN     *P23:24,*V2LON,*RV,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  PMST0000              Called by: ML0000   *
.*                           Process The Weight File                          *
.******************************************************************************
.
PMST0000  CLOCK     TIME,CTIMEIS
.
. ----- Read the patwis10.txt file -----
.
PMST1000  READ      WEIGHTMP,SEQ;DRGC,DRGD,SDMT,MVELIG,OTHCOP,LIBP,HIBP:
                                 AILOS,SDOD:
                                 TSDW,TODW,TLOMDW,TIMDW,THOMDW,THITHW
          GOTO      PMST9000 IF OVER
.
. ----- New record to be posted -----
.
PMST5000  CALL      SETM0000                * Set up the master file variables
          GOTO      PMST1000
.
. ----- Finished -----
.
PMST9000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:21,"Finished : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:24,*EL,*B,"No more records.  ";
          CALL      HITENTER
PMST9999  RETURN
+
.******************************************************************************
.*                                  SETM0000              Called by: PMST0000 *
.*                      Set up The File Variables                             *
.******************************************************************************
.
SETM0000  MOVE      DRGC,PT11DRGC
          MOVE      DRGD,PT11DRGD
          MOVE      SDMT,PT11SDMT
          MOVE      MVELIG,PT11MVEL
          MOVE      OTHCOP,PT11CPAY
          MOVE      LIBP,PT11LIBP
          MOVE      HIBP,PT11HIBP
          MOVE      AILOS,PT11ILOS
          MOVE      SDOD,PT11SDOD
          MOVE      TSDW,PT11TSDW
          MOVE      TODW,PT11TODW
          MOVE      TLOMDW,PT11TLOM
          MOVE      TIMDW,PT11TIMW
          MOVE      THOMDW,PT11THOM
          MOVE      THITHW,PT11THIH
          PACK      PT11SPAR,SP70
.
          PACK      KEY4,DRGC
          CALL      WRPT111
.
SETM9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       PATW11IO/INC            * WIES11 weights file
+
.


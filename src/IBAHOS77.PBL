.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAHOS77.PBL                                              *
.* Name           : Pulling Lists                                             *
.******************************************************************************
.* Date           : 09/09/1988                                                *
.* Author         : Greame Williams                                           *
.* Function       : To print pulling lists for patients                       *
.* Modifications  :                                                           *
.******************************************************************************
.*        V12.00.02 11/06/2025  Ebon Clements    TSK 0961623                  *
.*                  Alphanueric visit number changes                          *
.*        V12.00.01 21.05.2025  DA Sarkies       TSK 0905641                  *
.*                  Updated for Alpha-Numeric Visit Number                    *
.******************************************************************************
.*        V11.02.01 09/02/2022  Thanh T          TSK 0905641                  *
.*                  Recompiled as OUTHEDFD/OUTMA1FD changes                   *
.*        V10.11.02 27/11/2017 Thanh T.        TSK 0821710                    *
.*                  Recompiled as PATMISTD changed                            *
.*        V10.11.01 07/09/2017 Thanh T.        TSK 0821710                    *
.*                  Audit Amission/outpatient visit comments                  *
.******************************************************************************
.*        V10.07.01 30/10/2015  Ebon Clements  CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.04.01 15/06/2014  Jill Parkinson CAR 301639                     *
.*                  Moved TFILENAM to INIT0000                                *
.*        V10.03.02 27/12/2012 Patrick Adair   CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.03.01 21/02/2012  Ebon Clements  CAR 235459                     *
.*                  Change IP pulling list to get hospital from visit extn    *
.*        V10.02.02 28/07/2011  Peter McMullen CAR 245597                     *
.*                  Check for clinics excluded from pulling list              *
.*        V10.02.01 12/07/2011  Mike Laming   CAR 240716                      *
.*                  Mods to use MR Digit Sort Sequence - see routine MRTRKSEQ *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.12.01  08/05/2009  WeeLee Koo  CAR 196317                        *
.*                  Get last discharged date from patma1af if no admission    *
.*                  is found in patvisaf- PINP6000
.*        V9.09.01  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.08.01  31/01/2007  Peter Vela     CAR 127930                     *
.*                  Recompiled for MRTMASFD                                   *
.*        V9.02.01  01/05/2002  Dean Cramer                                   *
.*                  Make loop to initial options after processing - for web   *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 08/03/2000  Steve Armstrong    5.08 Mods                  *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.*        V5.07.B03 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B02 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 14/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
.*        V5.01.01  04/07/89 K.Peak                                           *
.*                  Changed Adm & U/R to 8 chrs                               *
.*        V5.01.02  06/09/89 K.Peak                                           *
.*                  Fixed master key & head132                                *
.*        V5.01.03  27/10/89 S.Barcham                                        *
.*                  Fix Ok to Continue     SRF 102848                         *
.*        V5.01.04  23/11/89 Graeme Williams                                  *
.*                  Handle a missing BOKB rec. in a different way. It used    *
.*                  to be ignored.          SRF 101412                        *
.*        V5.01.14  03/07/90 Paul Duncan                                      *
.*                  Added audits of outbok files    SRF105033                 *
.*        V5.01.15  18/07/90 D.Di..Paola                                      *
.*                  Extract 2 lines of comments from patcmntf file instead    *
.*                  of bokbf for outpatients   only...   SRF 105694           *
.*        V5.01.16  10/08/90 D.Di..Paola                                      *
.*                  Took out check for w.proc  comments because it uses       *
.*                  patcmnts even if free format is switched on....           *
.*                  Don't ask me why !. It's   as clear as a nose full of     *
.*                  snot !....SRF 105933    SRF 105941                        *
.*        V5.01.121 27/11/90 D.Di.Paola                                       *
.*                  Fix zero outpatient no's  in extraction. SRF 106925       *
.*        V5.01.122 27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to  PATMX1FD                       *
.*        V5.01.123 05/10/92 J.Tan  SRF 117625                                *
.*                  Fixed the visit type                                      *
.*        V5.01.124 16/03/93 Whirlpool                                        *
.*                  New outpatient layout for dudley Handles multi hospitals  *
.*        V5.01.125 01/04/93 Whirlpool                                        *
.*                  Fixed spelling of Tine to Time                            *
.*        V5.01.126 17/05/93 Robert Sumsion                                   *
.*                  Converted from TRK to MRT                                 *
.*        V5.01.127 25/06/1993    Justin Coates                               *
.*                  modularised code                                          *
.*        V5.01.128 13/12/1993    Justin Coates  SRF 440326                   *
.*                  print last visit details on report for UK layout          *
.*        V5.01.129 25/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.130 20/07/1994  Rob Leonard                                   *
.*                  Fixed hospital keyin                                      * 
.*        V5.01.131 08/09/1994  ROD                SRF 441094                 *
.*                  Allow cancellation of default date                        * 
.*        V5.03.01  07/10/1994  Paul Howells       SRF 441081                 *
.*                  Fix bug with patient title.                               * 
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       MRTHISFD/INC                 * tracking history file
          INC       MRTLOCFD/INC                 * tracking location file
          INC       MRTMASFD/INC                 * tracking master file
          INC       OUTBOAFD/INC                 * Outpatients Booking file
          INC       OUTBB1FD/INC                 * Outpatients Booking file
          INC       OUTCLIFD/INC                 * Outpatients Clinics file
          INC       OUTSESFD/INC                 * Outpatients Clinic Sessions
          INC       OUTHEDFD/INC                 * Outpatients Session Headers 
          INC       OUTCONFD/INC
          INC       OUTMA1FD/INC                 * Clinic Master file
          INC       OUTSITFD/INC                 * Outpatients Sites file
          INC       PATBAYFD/INC                 * Bay coding file
          INC       VISMDTFD/INC                 * Visit Comment file
          INC       VISMTXFD/INC
          INC       PATMISTD/INC
          INC       PATDO1FD/INC                 * Doctors file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC                 * Patients Masterfile details
          INC       PATMI1FD/INC                 * Inpatients details file
          INC       PMSVX1FD/INC                 * Inpatients details file
          INC       PATTRNFD/INC                 * Inpatients Bed Transfers file
          INC       PATVISFD/INC                 * Visit details file
          INC       PMSHCPFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.        File layout for temporary index file
.
PSTIDX    IFILE     SQL, FIXED=91, KEY="U1-22"
PSTROL    FILE      ASCII, FIXED=256
.
.VAR     TYPE       SIZE  PHYS. START  DESCRIPTION
.----    ----       ----  ----- -----  -----------
XCLIN     DIM       6       6       1  Clinic Id (Blank for Inpatients)
XSTIM     DIM       5       5       7  Clinic Start time (Blank for Inp)
XBAYC     DIM       3       3      12  Bay Code
XSRTS     DIM       8       8      15  MR Digit sort sequence         *I-240716
XURNO     DIM       8       8      23  U/R Number
XCTYP     DIM       6       6      31  Clinic Type (Blank for Inpatients)
XSLOT     FORM      3       3      37  Clinic Slot Number (Zero for Inp)
XDOFW     FORM      1       2      40  Clinic Day of Week (Zero for Inp)
XCOMM     DIM       30      30     42  Comments **** no longer used ****
XADMN     DIM       8       8      72  Admission Number
XDOCA     DIM       6       6      80  Attending Doctor (Blank for Outp)
XATIM     DIM       5       5      86  Appointment Time
.         End of record            91
.
.         program variables
.
AUDIFLAG  FORM      1
CCNOTES   DIM       1
CLINIC    DIM       6
CMDLINE   DIM       50
COMENT1   DIM       30
COMENT2   DIM       30
CURCLIN   DIM       6
CURCTYP   DIM       6
CURDOFW   FORM      1
CURSTIM   DIM       5
D8DATE    DIM       8
D10DATE   DIM       10
DADDOPT   FORM      1
DADDS     DIM       11
DATE      DIM       8
DAYOFW    DIM       9
DIM20     DIM       20
DIM40     DIM       40
DIM20A    DIM       20
DIM23     DIM       23
DIM6      DIM       6
DOPTION   DIM       10
DYOFW     DIM       2
FNAMEI    DIM       8
DIM8URNO  DIM       8
HOSPITAL  DIM       3
IBCNMHOS  FORM      1
LOCATION  DIM       20
LST1CLIN  DIM       18
LST2CLIN  DIM       18
LST2UR    DIM       2
LSTADMN   DIM       8
LSTWRD    DIM       7
LASTDATE  DIM       10        * last visit date
LASTLOCN  DIM       7         * last visit location
NUMURS    FORM      8
PTCNH77U  FORM      1
SITE      DIM       6
.                                                                     *I-240716
CTYPEUR   FORM      1
MRCNSRTO  DIM       1
SORTORDN  DIM       1         * Sort Sequence No.
SORTORDR  DIM       8         * Sort Sequence code (from UR Number)
WBSEHOSP  DIM       3
T1A       DIM       1
T2A       DIM       2
T2B       DIM       2
T3A       DIM       3
T4A       DIM       4
T5A       DIM       5
.
.         program constants
.
DMO       INIT      "MO"
DTU       INIT      "TU"
DWE       INIT      "WE"
DTH       INIT      "TH"
DFR       INIT      "FR"
DSA       INIT      "SA"
DSU       INIT      "SU"
DOPT1     INIT      "INPATIENTS"
DOPT2     INIT      "OUTPATIENT"
DADD1     INIT      "(Additions)"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBAHOS77"
PRGNAM    INIT      "Pulling Lists"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000
ML1000    CALL      OPTN0000                * run program ??
          BRANCH    EXIT,ML9999             * exit
          BRANCH    OPTION,ML2000,ML3000,ML4000
.
ML2000    CALL      INPT0000                * inpatient list
          GOTO      ML1000
.
ML3000    CALL      OUTS0000                * outpatient single clinic
          GOTO      ML1000
.
ML4000    CALL      OUTA0000                * outpatient all clinics
          GOTO      ML1000
.
ML9999    CALL      DTPI0000
          CHAIN     PGM
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          BRANCH    HOAUDA,INIT1000
          OPEN      OUTAUDBA,"outaudba"
.
INIT1000  DISPLAY   *P64:24,"patbaysf";
          OPEN      PATBAYS1,"patbaysf"
.
          DISPLAY   *P64:24,"patbaysf";
          OPEN      PATBAYS2,"patbaysf"
.
          DISPLAY   *P64:24,"vismdtaf";
          OPEN      VISMDTA1,"vismdtaf"
          DISPLAY   *P64:24,"vismtxaf";
          OPEN      VISMTXA1,"vismtxaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEI
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,THIRTY1;*51,HOAUDA,*134,HWORDPRO
          READ      CONTROLF,TWENTY1;*117,PTCNH77U
          READ      CONTROLF,FIFTY9;*34,CTYPEUR
          IF        IBCNMHOS = 1
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
       
          CLOSE     CONTROLF
.
          MOVE      ONE,CNEWDS
.
          MOVE      SP20,XBAYC                                        *I-240716
          MOVE      SP20,SORTORDN                                     *I-240716
.
INIT9999  RETURN
+
.*********************************************************************
.         run the report ??
.*********************************************************************
OPTN0000  MOVE      ZERO,NUMURS
          MOVE      SP20,DADDS
          MOVE      ZERO,DADDOPT
.
          HLINE     *G37,2,47,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Inpatient Pulling List":
                    *P1:6,*V2LON,TWO,*HOFF:
                    " = Outpatient Pulling List - Single Clinic":
                    *P1:7,*V2LON,THREE,*HOFF:
                    " = Outpatient Pulling List - All Clinics"
.
OPTN1000  KEYIN     *P1:9,"Select option : ",*V2LON,OPTION
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL       * exit
          MOVE      ZERO,EXIT
          BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.*********************************************************************
.         Inpatient Pulling List
.*********************************************************************
INPT0000  DISPLAY   *P47:2,*V2LON,"- Inpatients "
.
INPT1000  CALL      KDTA0000                * Keyin Date for list
          BRANCH    OVRCD,INPT9999          * nothing input
.
          IF        IBCNMHOS = 1
            MOVE      SIX,CVERT
            CALL      KHOS0000
            BRANCH    EXIT,INPT1000 
            MOVE      KEY3,HOSPITAL
            MOVE      KEY3,WBSEHOSP                                   *I-240716
            CALL      GTSRTIND              * get MR Digit Sort Seq. indicator
          ENDIF
.                                  
          CALL      CONTQST
          BRANCH    CEXIT,INPT2000,INPT1000,INPT9999
.
INPT2000  CLOCK     TIME,CTIMEIS
          CALL      CTPI0000
          MOVE      SP6,XCLIN
          MOVE      SP6,XCTYP
          MOVE      SP5,XSTIM
          MOVE      ZERO,XSLOT
          MOVE      ZERO,XDOFW
.
.         Loop over the admissions for the date selected
.
          DISPLAY   *P1:24,*EL,*P10:24,"U/R No:",*P60:24,"Scanning:";
          UNPACK    DATE,TDATE
          PACK      KEY16,TDATE,SP30
          CALL      RDSMISS3
INPT3000  CALL      RDKMISS3
          BRANCH    OVRCD,INPT5000
.
          MATCH     TDATE,ADATE
          GOTO      INPT5000 IF NOT EQUAL
.
          DISPLAY   *P70:24,AADMNO;
.
.         Check for a valid pre-admission
.
          COMPARE   ONE,ASTAT
          GOTO      INPT3000 IF NOT EQUAL
.
          MATCH     SP8,AURNO
          GOTO      INPT3000 IF EQUAL
.
.-----   If we are using multi-hospital system ------
.
          IF        IBCNMHOS = 1
            MATCH     HOSPITAL,PMVXMHOS          * Visit extn ready by admission
            GOTO      INPT3000 IF NOT EQUAL      * file read
          ENDIF
.
.         Write a record to the temporary file
.
          MOVE      AURNO,DIM8URNO
          MOVE      DIM8URNO,XURNO
          MOVE      AADMNO,XADMN
          MOVE      ADOCTA,XDOCA
.                                                           * start   *I-240716
          MOVE      XURNO,SORTORDR
          CALL      MRSRTORD                * generate MR Sort Order code
          MOVE      SORTORDR,XSRTS
.
          MATCH     XURNO,SORTORDN          * is Sort Seq the same as UR No.?
          IF        @EQUAL
            CALL      GBAY0000
          ENDIF                                             * end     *I-240716
.
          PACK      KEY22,XCLIN,XSTIM,XBAYC,XSRTS                     *C-240716
          CALL      WRTEMP1
          BRANCH    OVRCD,INPT3000
.
          DISPLAY   *P18:24,*V2LON,XURNO;
          ADD       ONE,NUMURS
          GOTO      INPT3000
.
INPT5000  CALL      REPT0000                * print the report
.
INPT9999  RETURN
+
.*********************************************************************
.         Outpatient Pulling List - Single Clinic
.*********************************************************************
OUTS0000  DISPLAY   *P47:2,*V2LON,"- Outpatients (Single Clinic) "
          CALL      KCLN0000                * Keyin Clinic details
          BRANCH    EXIT,OUTS9999           * exit
.
          CLOCK     TIME,CTIMEIS
          CALL      CTPI0000
          MOVE      SP6,XDOCA
          DISPLAY   *P1:24,*EL,*P10:24,"U/R No:",*P60:24,"Scanning:";
          CALL      POPS0000                * process the clinic
          CALL      REPT0000                * print the report
.
OUTS9999  RETURN
+
.*********************************************************************
.         Outpatients Pulling List - All Clinics
.*********************************************************************
OUTA0000  DISPLAY   *P47:2,*V2LON,"- Outpatients (All Clinics) "
          CALL      KSIT0000                * Keyin Site/Date details
          BRANCH    EXIT,OUTA9999
.
          CLOCK     TIME,CTIMEIS
          CALL      CTPI0000
          MOVE      SP6,XDOCA
          DISPLAY   *P1:24,*EL,*P10:24,"U/R No:",*P60:24,"Scanning:";
.
.         Loop over all clinics 
.
          PACK      KEY20,SITE,SP20
OUTA0500  CALL      RDSCLIA1
OUTA1000  CALL      RDKCLIA1
          BRANCH    OVRCD,OUTA5000
.
          MATCH     SITE,OCASITE
          GOTO      OUTA5000 IF NOT EQUAL   * different site
.
          DISPLAY   *P1:24,*V2LON,OCACLIN;
.
.         Process the data for this clinic
.
          MOVE      OCACLIN,CLINIC
          CALL      POPS0000
.
.         Position for the next clinic
.
          PACK      KEY20,SITE,CLINIC,Z30
          GOTO      OUTA0500
.
OUTA5000  CALL      REPT0000                * print the report
.
OUTA9999  RETURN
+
.*********************************************************************
.         do the report
.*********************************************************************
REPT0000  COMPARE   ZERO,NUMURS
          GOTO      REPT9000 IF EQUAL       * nothing found
.
.         Generate the report from the temporary file
.
          CALL      GRPT0000
          DISPLAY   *P1:24,*EL,*V2LON,*P40:24,*B:
                    "** Report Generation Completed **"
          GOTO      REPT9100 
.
.         No data for the pulling list found
.
REPT9000  DISPLAY   *P1:24,*EL,*B,"No Data found for Pulling List.  ";
          CALL      HITENTER
.
REPT9100  CALL      DTPI0000
.
REPT9999  RETURN
+
.*********************************************************************
.         Subroutine to ask if Additions only are wanted
.*********************************************************************
ADDN0000  KEYIN     *P1:CVERT,*EL,"Additions Only (Y/N) : ",*V2LON,ANS
          REP       UPPLOW,ANS
          CMATCH    ANSY,ANS
          GOTO      ADDN1000 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      ADDN2000 IF EQUAL
          BEEP
          GOTO      ADDN0000
.
ADDN1000  MOVE      ONE,DADDOPT
          MOVE      DADD1,DADDS
          DISPLAY   *P24:CVERT,*V2LON,"Yes";
          GOTO      ADDN9999
.
ADDN2000  MOVE      ZERO,DADDOPT
          MOVE      SP20,DADDS
          DISPLAY   *P24:CVERT,*V2LON,"No ";
.
ADDN9999  RETURN
+
.*********************************************************************
.         Subroutine to get and validate a single date
.*********************************************************************
KDTA0000  DISPLAY   *P1:4,*EF
          MOVE      FOUR,CVERT
          CALL      GDOL0000
KDTA9999  RETURN
+
.*********************************************************************
.         Subroutine to get the date of the pulling list
.*********************************************************************
GDOL0000  DISPLAY   *P1:CVERT,*EL,"Enter Report Date    : "
          CALL      TMRW0000
          MOVE      TWENTY4,CCOL
          MOVE      ONE,CDEFDTE
          MOVE      ONE,CCANLDTE
          CALL      KEYDATE
          BRANCH    CQUEST,GDOL0000
          BRANCH    OVRCD,GDOL9999
.
          PACK      DATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATE
.
GDOL9999  RETURN
.
.*********************************************************************
.         Subroutine to get tomorrow's date
.*********************************************************************
TMRW0000  MOVE     CDD,DD
          MOVE     CMM,MM
          MOVE     CYY,YY
          MOVE     CCC,CC
.
          CALL     DMYCON                  * Convert to Julian date
.
          ADD      ONE,JULDAY
          MOVE     JULDAY,JWKDAY
          MOVE     JULYR,JWKYER
          MOVE     JULCC,JWKCC
.
          CALL     JULCON 
.
          MOVE     DD,CDAY
          MOVE     MM,CMON
          MOVE     YY,CYEAR
          MOVE     CC,CCENT
          REP      " 0",CYEAR              * incase the year was 2000-2009
.
TMRW9999  RETURN
.
.*********************************************************************
.         Inpatient pulling list header
.*********************************************************************
HDRI0000  PRINT     *40,"PRE-ADMISSIONS FOR ":
                    CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,*N
.
          ADD       TWO,CLNO
          IF        IBCNMHOS = 1
            PRINT     *40,"HOSPITAL : ",HOSPITAL,SP3,PTHSNAME,*N
            ADD       TWO,CLNO
          ENDIF
.
          CALL      UNLN0000
          IF        PTCNH77U = ONE
            PRINT     *1,"| Bay",*7,"|   U/R No",*19,"| Patients Name":
                      *67,"|   Adm No",*78,"| Attending Doctor":
                      *111,"| Last Visit Details",*132,"|"
          ELSE
            PRINT     *1,"| Bay",*7,"|   U/R No",*19,"| Patients Name":
                      *67,"|   Adm No",*78,"| Attending Doctor":
                      *111,"! Lst Dsch",*123,"! Lst Wrd",*132,"!"
          ENDIF
          CALL      UNLN0000
          ADD       ONE,CLNO
.
HDRI9999  RETURN
+
.*********************************************************************
.         print an underline
.*********************************************************************
UNLN0000  PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
UNLN9999  RETURN
+
.*********************************************************************
.         Subroutine to print the tail for outpatients
.*********************************************************************
TAIL0000  BRANCH    OPTION,TAIL9999
.
.         Outpatients only - print the clinic details on the end of the page
.
TAIL1000  COMPARE   "60",CLNO
          GOTO      TAIL2000 IF NOT LESS
.
          PRINT     *N;
          ADD       ONE,CLNO           * Loop to the bottom of the page
          GOTO      TAIL1000
.
TAIL2000  PACK      KEY20,SITE,CURCLIN,DATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     CURCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      GCLD0000
          ENDIF
          UNPACK    DATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          LOAD      DAYOFW,CURDOFW,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
.
          PRINT     *1,DAYOFW,SP1,CPCDATE," AT ",CURSTIM:
                    *40,"CLINIC : ",CURCTYP,SP1,CURCLIN,SP4,OCADESC:
                    *N,*N,"**** End of Report ****"
.
TAIL9999  RETURN
+
.*********************************************************************
.         Subroutine to open site dependant files
.*********************************************************************
OSDF0000  CLOSE     OUTCLIA1
          PACK      CFNAME,OSTFILE,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
          CLOSE     OUTBOKA1
          PACK      CFNAME,OSTFILE,FILBOKA1
          OPEN      OUTBOKA1,CFNAME
.
          CLOSE     OUTBOKA3
          PACK      CFNAME,OSTFILE,FILBOKA3
          OPEN      OUTBOKA3,CFNAME
.
          CLOSE     OUTBOKA4
          PACK      CFNAME,OSTFILE,FILBOKA4
          OPEN      OUTBOKA4,CFNAME
.
          CLOSE     OUTSESA1
          PACK      CFNAME,OSTFILE,FILSESA1
          OPEN      OUTSESA1,CFNAME
.
          CLOSE     OUTHEDA1
          PACK      CFNAME,OSTFILE,FILHEDA1
          OPEN      OUTHEDA1,CFNAME
.
  
          CLOSE     OUTBB1A1
          PACK      CFNAME,OSTFILE,FILBB1A1
          CALL       OPOTBB11
.
          IF         IBCNMHOS = 1
            CLOSE     OUTMA1A1
            PACK      CFNAME,OSTFILE,FILMA1A1
            CALL       OPOTMA11
          ENDIF
.
OSDF9999  RETURN
.
.****************************************************************************
.         AUDIT ROUTINES for BOKA 
.         AUDIFLAG : 1   Write a A audit to file
.                    2   Write a B audit to file
.                    3   Write a C audit to file
.                    4   Write a D audit to file
.                    5   Delete  B audit to file
.****************************************************************************
.                                 AUDOTBOA
.    Audits for patient outpatients bookings file (outaudba)
.****************************************************************************
AUDOTBOA  BRANCH    HOAUDA,OTBOA999
.
          COMPARE   THREE,AUDIFLAG      * is it a after
          GOTO      OTBOA200 IF EQUAL
.
          COMPARE   FIVE,AUDIFLAG      * is it a delete
          GOTO      OTBOA600 IF EQUAL
.
          CALL      IBACLOCK           * get current date
          PACK      OTBAAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OTBAAUDD
          CLOCK     TIME,OTBAAUDT      * clock current time
          MOVE      ONE,OTBAAUDS       * set print status
          MOVE      PASSCODE,OTBAAUDO  * get user ID
          MOVE      PORT,OTBAAUDP      * get port number
.
          BRANCH    AUDIFLAG,OTBOA200,OTBOA200,OTBOA200,OTBOA200,OTBOA600
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      OTBOA999
.
OTBOA200  MOVE      AUDIFLAG,OTBAAUDR
          REPLACE   "1A2B3C4D",OTBAAUDR
          PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,OTBAAUDR  
          CALL      AWOTBOA           * write to audit file
          GOTO      OTBOA999
.
OTBOA600  PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,ANSB
          CALL      ADOTBOA           * delete audit file
.
OTBOA999  RETURN
.
.*********************************************************************
.         Keyin the hospital code
.*********************************************************************
KHOS0000  DISPLAY   *P1:CVERT,"Enter Hospital ID    : "
          MOVE      CVERT,EVERT
          MOVE      "24",ECOL
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS9999,KHOS0000
.
          DISPLAY   *P40:EVERT,PTHSNAME
.
KHOS9999  RETURN
.
.*********************************************************************
.         Get the current location of MR
.*********************************************************************
LOCA0000  OPEN      MRTMASA1,"mrtmasaf"
          MOVE      SP30,MRLODESC
          PACK      KEY20,XURNO,Z30                                   *C-240724
          CALL      RSMRMAS1
LOCA2000  CALL      RPMRMAS1
          BRANCH    OVRCD,LOCA9999
.
          MATCH     XURNO,MRMAURNO
          GOTO      LOCA9999 IF NOT EQUAL
.
          OPEN      MRTHISA1,"mrthisaf"
          OPEN      MRTLOCA1,"mrtlocaf"
.
. get latest movement for this volume
.
          PACK      KEY26,MRMAKEY,Z30
          CALL      RSMRHIS1
          CALL      RPMRHIS1
          BRANCH    OVRCD,LOCA9999
.
          MATCH     MRMAKEY,MRHIKEY
          GOTO      LOCA9999 IF NOT EQUAL
.
. format data for temp file write
.
          MOVE      SP30,MRLODESC
          PACK      KEY7,MRHIDHOS,MRHILOC                             *C-240724
          CALL      RDMRLOC1
.
LOCA9999  RETURN
.
.*********************************************************************
.         Subroutine to get and validate a single clinic
.*********************************************************************
KCLN0000  KEYIN     *P1:4,*EF:
                    *P1:4,"Enter Site Code      : ",*V2LON,SITE
          PACK      SITE,SITE,SP6
          MATCH     SP6,SITE
          GOTO      KCLN9100 IF EQUAL
.
.         Validate the site code
.
          MOVE      SITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,KCLN7100
.
          DISPLAY   *P40:4,OSTDESC
          CALL      OSDF0000                * open site dependant files
.
KCLN1000  DISPLAY   *P1:6,*EF,"Enter Clinic Code    : "
          MOVE      "24",ECOL
          MOVE      SIX,EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
          MOVE      SITE,IDDD
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLN0000,KCLN9100,KCLN0000
.
          MOVE      OCACLIN,CLINIC
          CALL      GCLD0000
          DISPLAY   *P1:8,"Clinic Description   : ",*V2LON,OCADESC
          MOVE      TEN,CVERT
          CALL      GDOL0000
          BRANCH    OVRCD,KCLN1000                * nothing input
.
.         see if want additions only
.
          MOVE      TEN2,CVERT
          CALL      ADDN0000
.
          IF        IBCNMHOS = 1
            LOAD      CVERT,OPTION,TEN4,TEN4,TEN2
            CALL      KHOS0000
            BRANCH    EXIT,KCLN0000
            MOVE      KEY3,HOSPITAL
            MOVE      KEY3,WBSEHOSP                                   *I-240716
            CALL      GTSRTIND              * get MR Digit Sort Seq. indicator
          ENDIF
.
          CALL      CONTQST
          BRANCH    CEXIT,KCLN5000,KCLN0000,KCLN9100
.
KCLN5000  MOVE      ZERO,EXIT
          GOTO      KCLN9999
.
KCLN7100  DISPLAY   *P1:24,*B,"Invalid Site Code.  ",*EL;
          CALL      HITENTER
          GOTO      KCLN0000
.
KCLN7200  DISPLAY   *P1:24,*B,"Invalid Clinic Code.  ",*EL;
          CALL      HITENTER
          GOTO      KCLN1000
.
KCLN9100  MOVE      ONE,EXIT
.
KCLN9999  RETURN
+
.*********************************************************************
.         Subroutine to get and validate a single site
.*********************************************************************
KSIT0000  KEYIN     *P1:4,*EF:
                    *P1:4,"Enter Site Code      : ",*V2LON,SITE
.
          PACK      SITE,SITE,SP6
          MATCH     SP6,SITE
          GOTO      KSIT9100 IF EQUAL
.
.         Validate the clinic code
.
          MOVE      SITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,KSIT7100
.
          CALL      OSDF0000                * open site dep. files
          DISPLAY   *P1:6,"Site Description     : ",*V2LON,OSTDESC
.
          MOVE      EIGHT,CVERT
          CALL      GDOL0000                * nothing input
          BRANCH    OVRCD,KSIT0000
.
.         Check if we want additions only
.
          MOVE      TEN,CVERT
          CALL      ADDN0000 
.
          IF        IBCNMHOS = 1
            LOAD      CVERT,OPTION,TEN4,TEN4,TEN2
            CALL      KHOS0000
            BRANCH    EXIT,KSIT0000
            MOVE      KEY3,HOSPITAL
            MOVE      KEY3,WBSEHOSP                                   *I-240716
            CALL      GTSRTIND              * get MR Digit Sort Seq. indicator
          ENDIF
.
          CALL      CONTQST
          BRANCH    CEXIT,KSIT9000,KSIT0000,KSIT9100
.
KSIT7100  DISPLAY   *P1:24,*B,"Invalid Site Code.  ",*EL;
          CALL      HITENTER
          GOTO      KSIT0000
.
KSIT9000  MOVE      ZERO,EXIT 
          GOTO      KSIT9999
.
KSIT9100  MOVE      ONE,EXIT 
KSIT9999  RETURN
+
.*********************************************************************
.         Subroutine to get the description for a clinic
.*********************************************************************
GCLD0000  MATCH     SP6,OCADOCT
          GOTO      GCLD9999 IF EQUAL
.
.         Use the doctor's code from the clinic file to get the doctor's name
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GCLD2000
.
.        Pack up the doctors name
.
          CLEAR     OCADESC
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OCADESC
          GOTO      GCLD9999
.
.         Doctor code not found
.
GCLD2000  CLEAR     OCADESC
          APPEND    "UNKNOWN DOCT. ",OCADESC
          APPEND    OCADOCT,OCADESC
          APPEND    SP30,OCADESC
          RESET     OCADESC
.
GCLD9999  RETURN
+
.*********************************************************************
.         Subroutine to get the bay code for a given U/R number
.*********************************************************************
GBAY0000  MOVE      SP3,BAYCODE
          MOVE      SP3,XBAYC
.
.         Get the last two digits of the U/R number
.
          UNPACK    XURNO,DIM6,LST2UR
          REP       " 0",LST2UR
.
.         Check for a record in the bay code file
.
          PACK      KEY5,LST2UR,SP3
          CALL      RDSBAYS2
          CALL      RDKBAYS2
          BRANCH    OVRCD,GBAY9999
          MATCH     LST2UR,BAYUR
          GOTO      GBAY9999 IF NOT EQUAL
.
          MOVE      BAYCODE,XBAYC
.
GBAY9999  RETURN
.
.*********************************************************************
.         Subroutine to process the temporary file
.*********************************************************************
GRPT0000  DISPLAY   *P1:24,*EL,*P40:24,"** Generating Report **":
                    *P1:24,*EL,*P10:24,"U/R No : ";
.
.         Initialize some variables
.
          MOVE      "60",CLNO
          MOVE      ZERO,CPAGENO
          MOVE      SP6,CURCLIN
          MOVE      SP5,CURSTIM
          MOVE      SP6,CURCTYP
          MOVE      ZERO,CURDOFW
.
.         Loop over the temporary file
.
          MOVE      SP30,KEY22
          CALL      RDSTEMP1
GRPT1000  CALL      RDKTEMP1
          BRANCH    OVRCD,GRPT9000
.
          DISPLAY   *P19:24,*V2LON,XCLIN,SP1,XURNO;
.
.         Get the master file details
.
          MOVE      XURNO,DIM8URNO
          MOVE      DIM8URNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GRPT1000
.
.         Check if we have changed clinics
.
          MATCH     XCLIN,CURCLIN
          GOTO      GRPT2000 IF NOT EQUAL
.
          MATCH     XSTIM,CURSTIM
          GOTO      GRPT3000 IF EQUAL
.
.         Changed clinic's - force a new page
.
GRPT2000  CALL      HEAD0000
.
.         Print the data for this record
.
GRPT3000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          UNPACK    SP30,LASTDATE,LASTLOCN
          BRANCH    OPTION,GRPT4100,GRPT4200,GRPT4200
.
.         Inpatient Report
.
GRPT4100  CALL      PINP0000
          GOTO      GRPT1000
.
.         Outpatient Report
.
GRPT4200  CALL      POPA0000
          GOTO      GRPT1000
.
.         End of the report - print a line and finish
.
GRPT9000  CALL      UNLN0000
          CALL      TAIL0000
          IF        OPTION=1
            PRINT     *N,*N,"**** End of Report ****"
          ENDIF
.
GRPT9999  RETURN
.
.*********************************************************************
.         Subroutine to print the inpatient pulling list details
.*********************************************************************
PINP0000  MOVE      SP30,OCADESC
          MOVE      XDOCA,OCADOCT
          CALL      GCLD0000               * Get the doctor's description
.
          IF        PTCNH77U = ONE
            CALL      GLVD0000                * get last visit details
            GOTO      PINP7000
          ENDIF 
.
.         Find the last discharge details for this patient by looping over
.         the visit file
.
          MOVE      SP8,D8DATE
          MOVE      SP10,D10DATE
          MOVE      SP7,LSTWRD
          MOVE      ZEROVISN,LSTADMN
          PACK      KEY24,XURNO,SP20
          CALL      RDSVISA2
.
PINP1000  CALL      RDKVISA2
          BRANCH    OVRCD,PINP4000
.
          MATCH     XURNO,PVIURNO
          GOTO      PINP4000 IF NOT EQUAL
.
          COMPARE   THREE,PVITYPE
          GOTO      PINP1000 IF NOT EQUAL
.
.         Check if this admission has been discharged
.
          MOVE      PVIBILL,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PINP1000
.
          COMPARE   THREE,ASTAT
          GOTO      PINP1000 IF NOT EQUAL
.
.         Save the admission number of this discharged patient
.
          MOVE      PVIBILL,LSTADMN
          GOTO      PINP1000
.
.         We have the admission number of the last discharge for this patient,
.         now find the last ward/bed and discharge date
.
PINP4000  MATCH     ZEROVISN,LSTADMN
          GOTO      PINP6000 IF EQUAL
.
.         Loop through the transfer file, and find the discharge record
.
          MOVE      LSTADMN,AADMNO
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
PINP5000  CALL      RDKTRAN2
          BRANCH    OVRCD,PINP6000
.
          MATCH     TADMN,LSTADMN
          GOTO      PINP6000 IF NOT EQUAL
.
          CMATCH    ANSD,TMOVE
          GOTO      PINP5000 IF NOT EQUAL
.
.         Set up the discharge date and last ward/bed variables
.
          UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,D10DATE
.
          PACK      LSTWRD,TWARD,SLASH,TBED
          GOTO      PINP7000
.
.         Get last discharge date from patient master file
.
PINP6000  PACK      KEY8,XURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PINP7000
.
          MATCH     SP8,PLDDATE
          GOTO      PINP7000 IF EQUAL
.
          UNPACK    PLDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,D10DATE
.
.         Set the case note's indicator and print the data
.
PINP7000  MOVE      SP1,ANS
          LOAD      ANS,PCASE,CCNOTES
.
          IF        PTCNH77U = ONE
            PRINT     *1,"|",*3,XBAYC,*7,"|",*9,XURNO,ANS:
                      *19,"|",*21,PSNAME,PGNAME:
                      *67,"|",*69,XADMN,*78,"|",*80,OCADESC:
                      *111,"|",*112,LASTDATE,*123,"|",*124,LASTLOCN,*132,"|"
          ELSE
            PRINT     *1,"|",*3,XBAYC,*7,"|",*9,XURNO,ANS:
                      *19,"|",*21,PSNAME,PGNAME:
                      *67,"|",*69,XADMN,*78,"|",*80,OCADESC:
                      *111,"|",*112,D10DATE,*123,"|",*124,LSTWRD,*132,"|"
          ENDIF
          ADD       ONE,CLNO
.
PINP9999  RETURN
+
.*********************************************************************
.         Subroutine to process the booking file for a single clinic
.*********************************************************************
POPS0000  PACK      KEY28,SITE,CLINIC,DATE,SP30
          CALL      RDSBOKA1
POPS1000  CALL      RDKBOKA1
          BRANCH    OVRCD,POPS9999
.
.         Check to see if we have finished this clinic
.
          MATCH     SITE,OBASITE
          GOTO      POPS9999 IF NOT EQUAL
.
          MATCH     CLINIC,OBACLIN
          GOTO      POPS9999 IF NOT EQUAL
.
          MATCH     DATE,OBADATE
          GOTO      POPS9999 IF NOT EQUAL
.
.  * Check for Exclude form pulling list
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,POPS1000
          PACK      KEY28,OBASITE,OBACLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,POPS1000
          MATCH     "1",OTHEXPUL
          GOTO      POPS1000 IF EQUAL
.
          DISPLAY   *P70:24,OBAOUTNO;
.
.         Check for a valid outpatient number
.
.#####    MATCH     ZERO,OBAOUTNO             
.#####    GOTO      POPS1000 IF EQUAL
.
          MATCH     SP8,OBAURNO
          GOTO      POPS1000 IF EQUAL
.
          COMPARE   ONE,OBASTAT
          GOTO      POPS1000 IF NOT EQUAL
.
          MOVE      "*Missing details record* ",OBACOMM
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
.
.         Check if we only want additions ( Reject if DADDOPT=1 & OBAPULL=1 )
.
          MOVE      ZERO,FORM1
          LOAD      FORM1,DADDOPT,OBAPULL
.
          BRANCH    FORM1,POPS1000
.
.-------   If we are using multi hospitals we need to check the hospital ----
.-------     stored on the clinic master                                ----
.
          IF        IBCNMHOS = 1
            PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
            CALL      RDMASA1
            BRANCH    OVRCD,POPS1000
.
            MATCH     HOSPITAL,OTMAHOSP
            GOTO      POPS1000 IF NOT EQUAL
          ENDIF
.
.         Write a record to the temporary file
.
          MOVE      OBAURNO,XURNO
          MOVE      OBACLIN,XCLIN
          MOVE      OBACTYP,XCTYP
          MOVE      OBASTRT,XSTIM
          MOVE      OBASLOT,XSLOT
          MOVE      OBADAY,XDOFW
          MOVE      OBAOUTNO,XADMN
          MOVE      OBACOMM,XCOMM
          MOVE      OBATIME,XATIM
.                                                           * start   *I-240716
          MOVE      XURNO,SORTORDR
          CALL      MRSRTORD                * generate MR Sort Order code
          MOVE      SORTORDR,XSRTS
.
          MATCH     XURNO,SORTORDN          * is Sort Seq the same as UR No.?
          IF        @EQUAL
            CALL      GBAY0000
          ENDIF                                             * end     *I-240716
.
          PACK      KEY22,XCLIN,XSTIM,XBAYC,XSRTS                     *C-240716
          CALL      WRTEMP1
          BRANCH    OVRCD,POPS1000
.
          DISPLAY   *P18:24,*V2LON,XURNO;
          ADD       ONE,NUMURS
          GOTO      POPS1000
.
POPS9999  RETURN
+
.*********************************************************************
.         Subroutine to print the outpatient pulling list details
.*********************************************************************
POPA0000  MOVE      SP30,LST1CLIN
          MOVE      SP30,LST2CLIN
.
          IF        PTCNH77U = ONE
            CALL      GLVD0000                * get last visit details
            GOTO      POPA5000
          ENDIF 
.
.        Find the last two clinic details for this patient by looping over
.        the visit file
.
          PACK      KEY24,XURNO,SP20
          CALL      RDSVISA2
POPA1000  CALL      RDKVISA2
          BRANCH    OVRCD,POPA5000
          MATCH     XURNO,PVIURNO
          GOTO      POPA5000 IF NOT EQUAL
          COMPARE   TWO,PVITYPE
          GOTO      POPA1000 IF NOT EQUAL
.
.         Get the details for this outpatient visit
.
          PACK      KEY36,PVIURNO,PVIDATE,SP30
          CALL      RDSBOKA3
.
POPA2000  CALL      RDKBOKA3
          BRANCH    OVRCD,POPA1000
.
          MATCH     PVIBILL,OBAOUTNO
          GOTO      POPA3000 IF EQUAL
.
          MATCH     PVIURNO,OBAURNO
          GOTO      POPA1000 IF NOT EQUAL
.
          MATCH     PVIDATE,OBADATE
          GOTO      POPA2000 IF EQUAL
          GOTO      POPA1000
.
.         Push the details of the stay onto the list of clinics
.
POPA3000  COMPARE   FOUR,OBASTAT
          GOTO      POPA1000 IF NOT EQUAL
.
          MOVE      LST1CLIN,LST2CLIN
          LOAD      DYOFW,XDOFW,DMO,DTU,DWE,DTH,DFR,DSA,DSU
          UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
.
          PACK      LST1CLIN,DYOFW,SP1,CDAY,SLASH,CMON,SLASH,CYEAR:
                                 SP1,PVIDOCT
          GOTO      POPA1000
.
.         Set the case note's indicator and print the data
.
POPA5000  MOVE      SP1,ANS
          LOAD      ANS,PCASE,CCNOTES
          MOVE      SP30,COMENT1
          MOVE      SP30,COMENT2
          MOVE      XCOMM,COMENT1                 * get comments from bokbf
.
.--------  get comments - if using word processor ------
.
          MOVE      XADMN,KEY8
          CALL      VSCMNT00
.
POPA5500  IF        PTCNH77U =1
            CALL       LOCA0000                    * Records location
          ENDIF
          PACK      KEY28,SITE,XCLIN,DATE,XSTIM,XSLOT
          CALL      RDBOKA1
          BRANCH    OVRCD,POPA9999
.
          IF        PTCNH77U = 1
            MOVE      COMENT1,DIM20
            MOVE      COMENT2,DIM20A
            MOVE      MRLODESC,LOCATION
            MOVE      PGNAME,DIM1
            MOVE      DIM1,PACGNAME
            MOVE      PSNAME,PACSNAME
            MOVE      PTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME 
            MOVE      PACFNAME,DIM23
            PRINT     *1,"|",*3,XBAYC,*7,"|",*9,XURNO,ANS:
                      *19,"|",*21,DIM23:
                      *44,"|",*46,XATIM,*52,"|",*54,XSLOT,*58,"|",*60,OBAVISIT:
                      *64,"|",*66,DIM20,*89,"|",*91,LOCATION,*112,"|":
                      *114,LST1CLIN,*132,"|",*N:
                      *1,"|",*7,"|",*19,"|":          
                      *44,"|",*52,"|",*58,"|":
                      *64,"|",*66,DIM20A,*89,"|",*112,"|":
                      *113,LASTDATE,SP1,LASTLOCN,*132,"|"
          ELSE 
            PRINT     *1,"|",*3,XBAYC,*7,"|",*9,XURNO,ANS:
                      *19,"|",*21,PSNAME,PGNAME:
                      *67,"|",*69,XSLOT,*73,"|",*75,OBAVISIT:
                      *79,"|",*81,COMENT1,*112,"|",*114,LST1CLIN,*132,"|",*N:
                      *1,"|",*7,"|",*19,"|":
                      *67,"|",*73,"|",*79,"|",*81,COMENT2,*112,"|":
                      *114,LST2CLIN,*132,"|"
          ENDIF
          ADD       TWO,CLNO
.
.         Flag this outpatient record as having appeared on a pulling list
.
          MOVE      TWO,AUDIFLAG                  * set flag to write a b audit
          CALL      AUDOTBOA                      * write audit
          MOVE      ONE,OBAPULL    
          CALL      UPBOKA1
          MOVE      THREE,AUDIFLAG                * set flag to write a c audit
          CALL      AUDOTBOA                      * write audit
.
POPA9999  RETURN
.----------------------------------------------------------------------
. Retrieve the 1st 2 comment text lines of the latest active visit
. comment details
.----------------------------------------------------------------------
VSCMNT00  PACK      KEY17,KEY8,VSCMTTYP,Z70      * Key for start of comment
          CALL      RSVSMDT1
VSCMNT20  CALL      RPVSMDT1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     KEY8,VSMDVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSCMTTYP,VSMDTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT          * is comment deleted
          GOTO      VSCMNT20 IF EQUAL
.
. Retrieve the comment text line details
.
          MOVE      ZERO,F1
          PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
VSCMNT60  CALL      RKVSMTX1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          ADD       ONE,F1 
          IF        F1 = ONE
            MOVE      VSMTCMNT,COMENT1    * get 1st comment line
          ENDIF
.
          IF        F1 = TWO
            MOVE      VSMTCMNT,COMENT2    * get 2nd comment line
          ENDIF
.
          COMPARE   TWO,F1                * only 2 comment text is used
          GOTO      VSCMNT60 IF NOT EQUAL          
.
VSCMNT99  RETURN
+
.*********************************************************************
.         Subroutine to print the headings for the reports
.*********************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          GOTO      HEAD1000 IF EQUAL
.
          CALL      UNLN0000
          CALL      TAIL0000
.
.         Print the top section of the heading
.
HEAD1000  LOAD      DOPTION,OPTION,DOPT1,DOPT2,DOPT2
          UNPACK    DATE,CCENT,CYEAR,CMON,CDAY
.
.         Print the basic heading that is common to both forms of the report
.
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          MOVE      DADDS,CPHDROPT
          CALL      HEAD132 
.
          MOVE      THREE,CLNO
.
.         Branch to the appropriate heading details, depending on option
.
          BRANCH    OPTION,HEAD2100,HEAD2200,HEAD2200
.
HEAD2100  CALL      HDRI0000
          GOTO      HEAD9999
.
.         Outpatient pulling list
.            - Get the clinic description
.
HEAD2200  PACK      KEY20,SITE,XCLIN,DATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     XCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      GCLD0000
          ENDIF
          LOAD      DAYOFW,XDOFW,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          PRINT     *40,"CLINIC :",*49,XCTYP,SP1,XCLIN,SP4,OCADESC,*N:
                    *49,DAYOFW,SP1,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    " AT ",XSTIM,*N
.
          IF        IBCNMHOS = 1
            PRINT     *40,"HOSPITAL : ",HOSPITAL,SP3,PTHSNAME,*N
            ADD       TWO,CLNO
          ENDIF
.
          ADD       THREE,CLNO
          CALL      UNLN0000
.
.         Print the headings for each of the fields to be printed
.
          IF        PTCNH77U = 1
            PRINT     *1,"| Bay",*7,"|   U/R No",*19,"| Patients Name":
                      *44,"| App.":
                      *52,"|Slot",*58,"|Visit":
                      *64,"| Comments",*89,"| Case Notes Location":
                      *112,"| Prev. Outpatient",*132,"|",*N:
                      *1,"|",*7,"|",*19,"|",*44,"| Time",*52,"| No",*58,"| Type":
                      *64,"|",*89,"|",*112,"| Last Visit ",*132,"|"
          ELSE
            PRINT     *1,"| Bay",*7,"|   U/R No",*19,"| Patients Name":
                      *67,"|Slot",*73,"|Visit":
                      *79,"| Comments",*112,"| Prev. Outpatient",*132,"|",*N:
                      *1,"|",*7,"|",*19,"|",*67,"| No",*73,"| Type":
                      *79,"|",*112,"| Visits",*132,"|"
          ENDIF
.
          ADD       TWO,CLNO
          CALL      UNLN0000
          MOVE      XCLIN,CURCLIN
          MOVE      XSTIM,CURSTIM
          MOVE      XDOFW,CURDOFW
          MOVE      XCTYP,CURCTYP
HEAD9999  RETURN
+
.****************************************************************************
.         Delete the temporary index file
.****************************************************************************
DTPI0000  CLOSE     PSTIDX
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
DTPI9999  RETURN
.
.*********************************************************************
.         get the last visit details for patient
.*********************************************************************
GLVD0000  UNPACK    SP30,LASTDATE,LASTLOCN
          PACK      KEY24,XURNO,DATE,Z30
          CALL      RDSVISA2
.
GLVD1000  CALL      RDPVISA2
          BRANCH    OVRCD,GLVD9999
          MATCH     XURNO,PVIURNO
          GOTO      GLVD9999 IF NOT EQUAL
.
          BRANCH    PVITYPE,GLVD1000,GLVD1500,GLVD1500
          GOTO      GLVD1000
.
.         if an outpatient, get last date and clinic
.
GLVD1500  IF        PVITYPE = TWO
            UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,LASTDATE
            MOVE      PVIDOCT,LASTLOCN
          ENDIF
.
.         if an inpatient, get last visit discharge date and ward
.
          IF        PVITYPE = THREE
            MOVE      PVIBILL,AADMNO
            MOVE      AADMNO,KEY8
            CALL      RDMISS1
            BRANCH    OVRCD,GLVD1000
            COMPARE   THREE,ASTAT
            GOTO      GLVD1000 IF NOT EQUAL      * not discharged
.
.           get the last discharge date and ward
.
            PACK      KEY30,AADMNO,Z30
            CALL      RDSTRAN2
            CALL      RDPTRAN2
            BRANCH    OVRCD,GLVD1000
            MATCH     TADMN,AADMNO
            GOTO      GLVD1000 IF NOT EQUAL
            CMATCH    ANSD,TMOVE
            GOTO      GLVD1000 IF NOT EQUAL
.
            UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,LASTDATE
            PACK      LASTLOCN,TWARD,SLASH,TBED
          ENDIF
.
GLVD9999  RETURN
.
.*********************************************************************
.         Create the temporary index file
.*********************************************************************
CTPI0000  CALL      DTPI0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 91 u1-22",CMDLINE                               *C-240716
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      PSTIDX,FNAMEI
CTPI9999  RETURN
+
.
.         I/O for the temporary index file
.
RDSTEMP1  RESET     KEY22
          READ      PSTIDX,KEY22;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    PSTIDX;XCLIN,XSTIM,XBAYC,XSRTS,XURNO,XCTYP:       *C-240716
                           XSLOT,XDOFW,XCOMM,XADMN,XDOCA,XATIM
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY22
.
          READ      PSTIDX,KEY22;ANS
          GOTO      OVERCOND IF NOT OVER
.
          WRITE     PSTIDX,KEY22;KEY22,XURNO,XCTYP,XSLOT,XDOFW:       *C-240716
                                 XCOMM,XADMN,XDOCA,XATIM
          RETURN
+
.............................................................................
          INC       STD001IO
          INC       KYOUTCLI
          INC       PATHSPKY
          INC       MRTRKSEQ                *Medical Records Tracking Sort Order
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       MRTHISIO/INC            * tracking history file
          INC       MRTLOCIO/INC            * tracking location file
          INC       MRTMASIO/INC            * tracking master file
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTSESIO/INC
          INC       OUTHEDIO/INC
          INC       OUTDSCLN
          INC       OUTMA1IO/INC            * Clinic Master file
          INC       OUTSITIO/INC
          INC       PATHSPIO/INC
          INC       PATBAYIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PMSHCPIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging.............................................................................

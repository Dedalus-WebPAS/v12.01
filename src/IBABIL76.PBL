.******************************************************************************
.*                                                                            *
.*      PROGRAM NAME:     IBABIL76                                            *
.*                                                                            *
.*      FUNCTION:         CROSS INDEX OF DISEASES                             *
.*                                                                            *
.*      DATE WRITTEN:     28/05/87                                            *
.*                                                                            *
.*      AUTHOR:           PAUL LO                                             *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.******************************************************************************
.* V12.00.01 28/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*       V11.05.01  24/03/2025  Davin         TSK 0956210                     *
.*                  Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*       V11.02.01  31/03/2022  Davin         TSK 0917793                     *
.*                  Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*       V11.00.01  28/01/2021  Thanh T       TSK 0887203                     *
.*                  Added BILDRG00 section for Billed DRG functionality       *
.******************************************************************************
.*       V10.10.01  17/03/2017  Davin          TSK 0833093     HDP 2017/2018  *
.*                  Add ICD10 Ed.10 - Go-live Date PTCNGDVX (Sect.110 Pos.85) *
.*       V10.06.01  17/03/2015  Davin         CAR 311669      HDP 2015/16     *
.*                  Add PTCNGDV9 for ICD10 Ed.9 Go-live Date (Sect.110 Pos.77)*
.*       V10.05.01  02/02/2015  Ania P        CAR 261630                      *
.*                  Removed use of PORT for temp file naming                  *
.*       V10.03.02  17/07/2013  Davin         CAR 288254 / 288700             *
.*                  Populate icdrddte with endate to validate icd codes       *
.*       V10.03.01  23/04/2013  Davin         CAR 284420      HDP 2013/14     *
.*                  Add PTCNGDV8 for ICD10 Ed.8 Go-live Date (Sect.110 Pos.69)*
.*       V10.02.02  08/07/2011  Ebon Clements CAR 240724                      *
.*                  Mods for MRT hospital/location - Added hospital to        *
.*                  location key                                              *
.*       V10.02.01  28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*       V10.01.01  23/03/2011  Mike Laming   CAR 240107                      *
.*                  Remove PTCNUCEP PATMRDFD/IO and PATMROFD/IO               *
.*                  Mods for Key changes in PATECDFD & PATECOFD               *
.*       V10.00.02  26/05/2010  Mike Laming   CAR 222491                      *
.*                  Mod to accept header Code (e.g "A01" not "A01.1") at KTDIS*
.*                  Mods to stop duplicate Codes causing duplicate print lines*
.*       V10.00.01  07/04/2010  Mike Laming   CAR 219246      HDP 2010        *
.*                  Add PTCNGDV7 for ICD10 Ed.7 Go-live Date (Sect.110 Pos.61)*
.*        V9.11.02  24/04/2009  WEELEE KOO    CAR 194310                      *
.*                  Display output even when there's no record                *
.*        V9.11.01  13/02/2009  Mike Laming   CAR 167192                      *
.*                  Add "Doctor Codes" to "Doctor Type" option (as per v6.16) *
.*        V9.09.01  20/03/2008  Mike Laming   CAR 163918     HDP 2008         *
.*                  Add PTCNGDV6 for ICD10 Ed.6 Go-live                       *
.*        V9.08.01  31/01/2007  Peter Vela     CAR 127930                     *
.*                  Recompiled for MRTMASFD                                   *
.*        V9.06.01  15/05/2006  Mike Laming   CAR 105244      2006 HDP        *
.*                  ADD PTCNGDV5 for ICD10 Ed.5 Go-live                       *
.*        V9.04.01  23/08/2004  Lina Vo CAR 52901                             *
.*                  Added Multi-Hospital processing                           *
.*        V9.03.03  23/06/2004  Henry Son        CAR 49037                    *
.*                  ICD10 Ed.4 changes - add PTCNGDV4.                        *
.*        V9.03.02  20/01/2004  Davin          CAR 45361                      *
.*                  Added two new sequence options (disch.date & u/r number)  *
.*                  Added new filter (unit)                                   *
.*                  Added unit, disch.destination & DRG to report             *
.*        V9.03.01  31/12/2003  Sylvek Litewka CAR 20222                      *
.*                  Replaced calls to RDPT10D1 (ICD10 Disease V2 file read)   *
.*                  with RDPTICD1 (latest ICD10 version read).                *
.*        V9.02.02  29/08/2002  Jill Habasque  SRF 17573                      *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V9.02.01  11/09/2001  Mike Laming                                   *
.*                  Remove internal READKP for MATMASA1 - Use RKMRMAS1 in IO  *
.*        V5.09.00  20.12.2000 Bronko Sosic                                   *
.*                  Recompiled for MRTHISFD                                   *
.*        V5.08.04  29/11/2000  Dean Cramer                                   *
.*                  Alter so subtotaling occurs after a change of significant *
.*                  digits ie digits before . or - or /.                      *
.*                  Also make cosmetic changes to the report layout as        *
.*                  directed                                                  *
.*        V5.08.03  25.10.2000 J.Tan                                          *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 15/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*        V5.07.B03 21/01/1999  Nicole Harrington  507 rebound 121            *
.*                  Mods to print century on dates                            *
.*        V5.07.B02 01/12/1998  Steve Armstrong                               *
.*                  Removed reference to PATIC9FD                             *
.*        V5.07.B01 12/11/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
.*                        W2.01 25/08/87  J.Tan (I.B.A.)                      *
. *                       Add a new option to keyin doctor                    *
. *                       type                                                *
. *                       W2.02 03/09/87  J.Tan (I.B.A.)                      *
. *                       Mods the summary report                             *
. *                       W2.03 12/11/87  J.Tan (I.B.A.)                      *
. *                       Check doct type using codes file                    *
. *                       W2.04 22/12/87  J.Tan (I.B.A.)                      *
. *                       Add separate page option                            *
. *                       V5.01 20/03/89 J.Tan                                *
. *                             Fixed PATIOMDI                                *
. *                       V5.02 29.04.89 S.Barcham                            *
. *                             Print year on discharge dat                   *
. *                       V5.03 16/05/89 S.O'Gorman                           *
. *                             Allow 8 Digit U.R's & Adm                     *
. *                             Numbers. Include STD001                       *
. *                       V5.04 27/05/89 J.Tan                                *
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMI1FD).     SRF 100750                    *
. *                       V5.05 31/05/89 J.Tan                                *
. *                             Add Att.doctor sequence                       *
. *                             SRF 100797                                    *
. *                       V5.06 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                    V5.01.01 06/07/89 K.Peak                               *
. *                             Changed Adm & U/R to 8 chrs                   *
. *                             11/08/89 Graeme Williams                      *
. *                             The "P", "S", "C" and "A"                     *
. *                             prefixs are now parameters                    *
. *                    V5.01.02 18/01/90 K.Peak SRF 103138                    *
. *                             Fixed the printing of                         *
. *                             disease code description                      *
. *                    V5.01.03 06/04/90 D.Di.Paola                           *
. *                             Fixed I*I by re-coding the                    *
. *                             creation of tempfiles to                      *         
. *                             standards and taking out                      *
. *                             the rollout file. Also                        *
. *                             fixed option 8. ,spelling                     *
. *                             of disease....  SRF 104534                    *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *                   V5.01.122 20/05/91 Steve O'Gorman                       *
. *                            Fixed Errors from new Comp.                    *
. *                   V5.01.123 23/11/92 Chucky + DIG                         *
. *                             Added file location for                       *
. *                             medical record for H.R.H.                     *
. *                   V5.01.124 26/01/93 ROD + no-one                         *
. *                             Changed for Consultant Episodes               *
. *                             Made this BOX bigger!                         *
. *        V5.01.125 23.02.93 Neeriem "I Hate Support" Dye (I.B.A.)           *
. *                  Modify to use standard routine KEYDATE                   *
. *        V5.01.126 04.03.93 Neeriem "I Hate Support" Dye (I.B.A.)           *
. *                  Deleted "invalid day" error message                      *
. *        V5.01.127 10.03.93 Neeriem "I Hate Support" Dye (I.B.A.)           *
. *                  Cleared date range error message                         *
. *        V5.01.128 16.03.93 Neeriem "I Hate Support" Dye (I.B.A.)           *
. *                  Made BIL76,77,78 all handle dates the same               *
. *        V5.01.129 07/05/93  ROD                                            *
. *                  Replace PTCNACPS with PTCNUCEP                           *
. *        V5.01.130 17/05/93  Robert Sumsion                                 *
. *                  Converted from TRK to MRT system                         *
. *        V5.01.131 22/06/93  WHIRLPOOL      SRF 123343                      *
. *                  Fixed READ on MRD File and changed to use                *
. *                  PATMRO file for uk                                       *
. *        V5.01.132 30/07/1993  Glenn Berry     SRF 123878                   *
. *                  Added Sub-total for "LOS Primary" for CMDISL             *
. *                  disease types                                            *
. *        V5.01.133 21/02/1994  Matt Surridge                                *
. *                  Added MAINQST, HEAD120 and general upgraded              *          
. *                  standards.                                               *
. *        V5.01.134 06/04/1994  Ian Rutt                                     *
. *                  Changed to use PATCODKY                                  *          
. *        V5.02.00  27/10/94  ROD              SRF 132849                    *
. *                  if keyin ALL Dr types set fld (bug in above mod)         *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
+
          INC       MRTMASFD/INC
.
          INC       IBACONFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATECOFD/INC
          INC       PATHSPFD/INC
          INC       PATTRNFD/INC
          INC       PATICDFD/INC
          INC       PATCODFD/INC
          INC       PMSVX1FD/INC
          INC       MRTHISFD/INC
          INC       MRTLOCFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
          INC       PMSEDGFD/INC  
          INC       PATFN1FD/INC
          INC       PATPGRFD/INC
          INC       PATFX1FD/INC
          INC       PATCFAFD/INC
+
.               WORK AREA
.
CMDISP    DIM       1
CMDISS    DIM       1
CMDISC    DIM       1
CMDISA    DIM       1
CMDISL    DIM       1
CODES     DIM       5[200]
CODESNO   FORM      3
DOCCODE   DIM       6
DOCNAME   DIM       22
FOUND     FORM      1
FSTTIM    DIM       1
.
DDRGCODE  DIM       4
DMDCCODE  DIM       4
DRGVER    DIM       3
.
PTCNDGED  DIM       1                 * Enable DRG Effective Dates Functionality
.                                       0 or blank = No
.                                       1 = Yes
PTCNLTYP  FORM      1
PTCNI10D  DIM       8
PTCNGDV3  DIM       8
PTCNGDV4  DIM       8                       * Ed4 golive date          *I-49037
PTCNGDV5  DIM       8                       * Ed5 golive date         *I-105244
PTCNGDV6  DIM       8                       * Ed.6 Go-live date       *I-163918
PTCNGDV7  DIM       8                       * Ed.7 Go-live date       *I-219246
PTCNGDV8  DIM       8                       * Ed.8 Go-live date       *I-284420
PTCNGDV9  DIM       8                       * Ed.9 Go-live date       *I-311669
PTCNGDVX  DIM       8                       * Ed.10 Go-live date      *I-0833093
PTCNGDX1  DIM       8                       * Go live date for ICD10 Ed.11
PTCNGDX2  DIM       8                       * Go live date for ICD10 Ed.12
PTCNGDX3  DIM       8                       * Go live date for ICD10 Ed.13
.
.*******************************************************************************
.*        PATCONFD.INC : Sector 119                                            *
.*******************************************************************************
.          Grouper Effective Dates
PTCNED41  DIM       8        183      Effective Date for DRG 4.1      *C-284669
PTCNED42  DIM       8        191      Effective Date for DRG 4.2      *C-284669
PTCNED50  DIM       8        199      Effective Date for DRG 5.0      *C-284669
PTCNED51  DIM       8        207      Effective Date for DRG 5.1      *C-284669
PTCNED52  DIM       8        215      Effective Date for DRG 5.2      *C-284669
PTCNED60  DIM       8        223      Effective Date for DRG 6.0      *C-284669
PTCNED62  DIM       8        231      Effective Date for DRG 6.2      *C-284669
PTCNED70  DIM       8        239      Effective Date for DRG 7.0      *C-284669
PTCNED80  DIM       8        232      Effective Date for DRG 8.0
PTCNED90  DIM       8        232      Effective Date for DRG 9.0
PTCNE100  DIM       8        224      Effective Date for DRG 10.0
PTCNE110  DIM       8        239      Effective Date for DRG 11.0
PTCNE120  DIM       8         94      Effective Date for DRG 12.0
.
PTCNDGPV  DIM       3        158      Default Grouper Version (031/041/042)
.
CFPAYCOD  DIM       3        * 3M Codefinder PAY Code
.
PRIMFLG   DIM       1
PAGEFLG   DIM       1
.
SELUNIT   DIM       3
SKEY30    DIM       30
STDOCT    DIM       6
SCRNFLAG  FORM      1
SUMFLAG   DIM       1
.
SHDFLG    FORM      "0"
WRFLG     FORM      "0"
ADDFLG    FORM      "0"
HEADFLG   FORM      "0"
ENDFLG    FORM      "0"
.
FORM8     FORM      8
FIRST     DIM       1
FLINE     DIM       1
NODIS     DIM       1
NOOPR     DIM       1
FIELD     FORM      2
LNO       FORM      2
PAGENO    FORM      3
PRNTHEAD  FORM      1
.
.
.
SP12      INIT      "            "
SP17      INIT      "                 "
SP22      INIT      "                        "
SP23      INIT      "                         "
SP28      INIT      "                            "
.
PSTROL    FILE      ASCII, FIXED=256
.
GNDFILE   IFILE     SQL, FIXED=9, KEY="u1-8"
.
.Name     Type      Size    Physical    Start
.----     ----      ----    --------    -----
.KEY8     DIM        8         8          1        Admission Number
.                              EOR        9
.
.        2nd Temporary index file for report
.
SORTSTR   DIM       20
SORT1     INIT      "uc1-25"                 * admission sequence  (key25)
SORT2     INIT      "uc1-9,97-116,18-25"     * doctor sequence     (key37)
SORT3     INIT      "uc1-9,89-96,18-25"      * disch.date sequence (key25)
SORT4     INIT      "uc1-9,48-55,18-25"      * u/r number sequence (key25)
TEMPFILE  IFILE     SQL, FIXED=117, KEY=SORTSTR
.
.Name    Type      Size    Physical    Start
.----    ----      ----    --------    -----
.PTEOCODE DIM        9         9          1
.DDADMNO  DIM        8         8         10
DUNIQUE   DIM        8         8         18
.PNAME12  DIM        12        12        26
.ACLSSFT  DIM        3         3         38      * unit
.DDEST    DIM        3         3         41      * discharge destination
.PTDSDDRG DIM        4         4         44      * drg code
.PURNO    DIM        8         8         48
.PSEX     DIM        1         1         56
.PSAGE    FORM       3         3         57
DNAME18   DIM        18        18        60
.NBRDAYS  FORM       3         3         78
FROMDATE  DIM        8         8         81
TODATE    DIM        8         8         89
.DSNAME   DIM        20        20        97
.                              EOR      117
.
DISCOUNT  FORM      1
FNAMET    DIM       8
FNAMEG    DIM       8
.
STATUS    FORM      1
CMDLINE   DIM       50
TIMEIS    DIM       5
DIM5      DIM       5
UNIQUE    FORM      8
COUNT     FORM      8
.
.
.      EXTRA VARIABLES
.
DESC22    DIM       22
MODE      DIM       1
.
.         WORK AREA
.
NBRDAYS   FORM      4          /*  NHOSPDAY  VARIABLES  */            *C-222491
RTDAYS    FORM      4          /*  NHOSPDAY  VARIABLES  */
CALDAYS   FORM      4                                                 *C-222491
JDAYS     FORM      4                                                 *C-222491
JYEAR     FORM      2
LYEAR     FORM      "366"
OYEAR     FORM      "365"
WDATE1    DIM       8
WDATE2    DIM       8
.
BILDRGFL  DIM       1
.
SUBHEAD   DIM       9
START3    DIM       3
START6    DIM       6
END6      DIM       6
.
XDIM6     DIM       6
YDIM6     DIM       6
.
START9    DIM       9
END9      DIM       9
.
XDIM9     DIM       9
YDIM9     DIM       9
.
HYPH      INIT      "-"
FSLASH    INIT      "/"
FSTREC    INIT      "Start    "
LSTREC    INIT      "Finish   "
ALLREC    INIT      "ALL      "
.
PADATE    DIM       10
PDDATE    DIM       10
PFDATE    DIM       10
PTDATE    DIM       10 
STARTD    DIM       8
ENDATE    DIM       8
.
PNAME12   DIM       12
DNAME18A  DIM       18
TYPDESC   DIM       25
.
GNDPAT    FORM      6
GNDDAY    FORM      8
GNDLOS    FORM      3.2
.
SUBPAT    FORM      5
SUBPRI    FORM      5
SUBDAY    FORM      8
SUBLOS    FORM      3.2
SUBLOSP   FORM      5                        * Subtotal for LOS Primary
SUBASS    FORM      5
SUBSEC    FORM      5
SUBCOM    FORM      5
TMPCAL    FORM      8.2
.
DIM3      DIM       3
DIM4      DIM       4
DIM9      DIM       9
DIM9A     DIM       9
DIM9B     DIM       9
DIM70A    DIM       70
DIM70B    DIM       70
CODEAC    INIT      "AC"
CODEDT    INIT      "DT"
CODE      DIM       3
.
SAVALL    DIM       9                                                   
SAVCODE   DIM       6            * Save Disease code for Ennn.n, Mnnn.n
SAVCODS   DIM       5            * Save Disease code for nnn.n, Vnn.n
SAVHALF   DIM       4
SAVCD     DIM       5
SAVADM    DIM       8
SAVMDC    DIM       9
VERT      FORM      2
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBABIL76"
PRGNAM    INIT      "Cross Index Of Diseases"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"mrtmasaf";
          OPEN      MRTMASA1,"mrtmasaf"
.
          DISPLAY   *P64:24,"mrthisaf";
          OPEN      MRTHISA1,"mrthisaf"
.
          DISPLAY   *P64:24,"mrtlocaf";
          OPEN      MRTLOCA1,"mrtlocaf"
.
OPFIL     DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*235,CMDISP,CMDISS,CMDISC,CMDISA
          READ      CONTROLF,TWENTY1;*212,PTCNLTYP:
                                     *217,CMDISL
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P64:24,"paticddf";
          CALL      OPICD1
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEG
+
.        START OF PROGRAM
.
.             SELECT OPTION
.
START0    HLINE     *G37,2,52,80
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"1",*HOFF,". Starting Discharge Date       :":
                    *P1:5,*V2LON,"2",*HOFF,". Ending   Discharge Date       :":
                    *P1:7,*V2LON,"3",*HOFF,". From Disease Code             :":
                    *P1:8,*V2LON,"4",*HOFF,". To   Disease Code             :":
                    *P1:10,*V2LON,"5",*HOFF,". Keyin Doctor Type/Doctor Code :":
                    *P55:10,"   <RETURN> for All ":
                    *P1:12,*V2LON,"6",*HOFF,". Primary Doctor Type Only (Y/N):":
                    *P1:13,*V2LON,"7",*HOFF,". Keyin Unit                    :":
                    *P1:14,*V2LON,"8",*HOFF,". Summary Details Only     (Y/N):":
                    *P1:15,*V2LON,"9",*HOFF,". New page for each Disease(Y/N):"
.
          IF        IBCNMHOS=1
            DISPLAY   *P1:16,*V2LON,"10",*HOFF,".Hospital Id":
                      *P34:16,":"
          ENDIF
.
START     MOVE      ZERO,PAGENO
          MOVE      ZERO,HEADFLG
          MOVE      ZERO,ENDFLG
          MOVE      ZERO,SHDFLG
          MOVE      FALSE,PRNTHEAD
          MOVE      "A",MODE
          MOVE      "60",LNO
          CALL      ZEROS
          CALL      ZEROG
          CALL      CLRA0000
.
KSTART    MOVE      ZERO,IDAY
          MOVE      ZERO,IMON
          MOVE      ZERO,IYEAR
          MOVE      ZERO,ICENT
          MOVE      FOUR,VERT
          CALL      KEYINDTE
.
          COMPARE   ZERO,IDAY
          GOTO      CHKDAT1 IF NOT EQUAL
.
          MATCH     "C",MODE
          GOTO      ENDIT IF NOT EQUAL
.
          GOTO      KSTART
.
CHKDAT1   MOVE      IDAY,XDAY
          MOVE      IMON,XMON
          MOVE      IYEAR,XYEAR
          MOVE      ICENT,XCENT
          PACK      STARTD WITH XCENT,XYEAR,XMON,XDAY
          PACK      PFDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          REP       " 0",PFDATE
          REP       " 0",STARTD
          RESET     STARTD
.
          MATCH     "C",MODE
          GOTO      KEND IF NOT EQUAL
          MATCH     STARTD,ENDATE
          GOTO      SFIELD IF NOT LESS
          DISPLAY   *P50:4,*B,"Ending Date Can Not Be Greater Than":
                    " Starting Date. ";
          CALL      HITENTER
.
          GOTO      KSTART
.
KEND      
          MOVE      CDD,IDAY
          MOVE      CMM,IMON
          MOVE      CYY,IYEAR
          MOVE      CCC,ICENT
.
          MOVE      FIVE,VERT
          CALL      KEYINDTE
.
          MOVE      IDAY,XDAY
          MOVE      IMON,XMON
          MOVE      IYEAR,XYEAR
          MOVE      ICENT,XCENT
          PACK      PTDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          REP       " 0",PTDATE
          PACK      ENDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",ENDATE
          RESET     ENDATE
.
          MATCH     STARTD,ENDATE
          GOTO      NEXTS IF NOT LESS
          DISPLAY   *P50:5,*B,"Ending Date Can Not Be Greater Than":
                    " Starting Date. ";
          CALL      HITENTER
.
          GOTO      KEND
.
NEXTS     MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
.
KFDIS     KEYIN     *P36:7,*EL,*V2LON,START9
          RESET     START9
          GOTO      NEXT3 IF NOT EOS
.
          MOVE      SP9,START9
          DISPLAY   *P36:7,*V2LON,FSTREC
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KTDIS
.
NEXT3     ENDSET    START9
          APPEND    SP9,START9
          RESET     START9
.
          MOVE      ENDATE,ICDRDDTE              * use to validate icd code
          MOVE      START9,KEY9
          CALL      RDPTICD1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9D1
          ENDIF
          BRANCH    OVRCD OF INVCOD
.
          MATCH     "C",MODE
          GOTO      KTDIS IF NOT EQUAL
          MATCH     SP9,END9
          GOTO      SFIELD IF EQUAL
          MATCH     START9,END9
          GOTO      SFIELD IF NOT LESS
.
          DISPLAY   *P1:24,*B,"From Code Can Not Be Greater Than":
                    " To Code. ";
          CALL      HITENTER
.
          GOTO      KFDIS
.
INVCOD    DISPLAY   *P1:24,*B,"Code Does Not Exist. ";
          CALL      HITENTER
.
          GOTO      KFDIS
.
KTDIS     KEYIN     *P36:8,*EL,*V2LON,END9
          RESET     END9
          GOTO      NEXT4 IF NOT EOS
.
...       MOVE      SP9,END9
          MOVE      "~~~~~~~~~",END9                                  *C-222491
          DISPLAY   *P36:8,*V2LON,LSTREC
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KTDISEND                * was "KFDOC"             *C-222491
.
NEXT4     ENDSET    END9
          APPEND    SP9,END9
          RESET     END9
.
          MOVE      ENDATE,ICDRDDTE              * use to validate icd code
          MOVE      END9,KEY9
          CALL      RDPTICD1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9D1
          ENDIF
          BRANCH    OVRCD OF INVCODX
.
          MATCH     START9,END9
          GOTO      INVCOD1 IF LESS
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KTDISEND                * was "KFDOC"             *C-222491
.
INVCOD1   DISPLAY   *P50:8,*B,*V2LON,"From Code Can Not Be Greater Than ":
                    "To Code. ";
          CALL      HITENTER
          GOTO      KTDIS
.
INVCODX   DISPLAY   *P50:8,*B,*V2LON,"Code Does Not Exist. ";
          CALL      HITENTER
.
          GOTO      KTDIS
.
KTDISEND  STRIP     END9                    * strip header codes      *I-222491
          STRIP     START9                  * strip header codes      *I-222491
.
. enter the doctor type
.
KFDOC     CALL      KDTP0000                * enter the doctor type
.
.         keyin Primary Type Only or ALL types selection
.
KPRIM     MOVE      SP1,PRIMFLG
          KEYIN     *P36:12,*EL,*V2LON,PRIMFLG
          RESET     PRIMFLG
          GOTO      NXT01 IF NOT EOS
.
          MOVE      "N",PRIMFLG
          DISPLAY   *P36:12,*V2LON,PRIMFLG
          GOTO      NXT03
.
NXT01     REP       UPPLOW,PRIMFLG
          DISPLAY   *P36:12,*V2LON,PRIMFLG
          MATCH     "Y",PRIMFLG
          GOTO      NXT02 IF EQUAL
.
          MATCH     "N",PRIMFLG
          GOTO      NXT03 IF EQUAL
          BEEP
          GOTO      KPRIM
.
NXT02     MOVE      "PRIMARY DOCTOR TYPE ONLY",TYPDESC
          GOTO      NXT04
.
NXT03     MOVE      "ALL DOCTOR TYPES",TYPDESC
.
NXT04     MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
.
.         keyin Unit
.
KUNIT     DISPLAY   *P36:13,*EL;
          MOVE      "36",ECOL
          MOVE      "13",EVERT
          MOVE      CODEAC,CODE
          CALL      PATCODKY
          BRANCH    EXIT,NEXTP,START
          MOVE      ACODE,SELUNIT
          DISPLAY   *P42:13,*V2LON,TDESC
          MATCH     "C",MODE
          GOTO      KSUMM IF NOT EQUAL
          GOTO      SFIELD
.
NEXTP     MOVE      SP3,SELUNIT
          DISPLAY   *P36:13,*V2LON,ALLREC
          MATCH     "C",MODE
          GOTO      SFIELD IF EQUAL
.
.
.         keyin Summary Only indicator
.
KSUMM     MOVE      SP1,SUMFLAG
          KEYIN     *P36:14,*EL,*V2LON,SUMFLAG
          RESET     SUMFLAG
          GOTO      NEXT7 IF NOT EOS
.
          MOVE      "N",SUMFLAG
          DISPLAY   *P36:14,*V2LON,SUMFLAG
          GOTO      NEXT7A
.
NEXT7     REP       UPPLOW,SUMFLAG
          DISPLAY   *P36:14,*V2LON,SUMFLAG
.
          MATCH     "Y",SUMFLAG
          GOTO      NEXT7A IF EQUAL
          MATCH     "N",SUMFLAG
          GOTO      NEXT7A IF EQUAL
          BEEP
          GOTO      KSUMM
.
NEXT7A    MATCH     "C",MODE
          GOTO      KSPAGE IF NOT EQUAL
          GOTO      SFIELD
.
.         Keyin for separate page option
.
KSPAGE    MOVE      SP1,PAGEFLG
          KEYIN     *P36:15,*EL,*V2LON,PAGEFLG
          RESET     PAGEFLG
          GOTO      NEXT8 IF NOT EOS
.
          MOVE      "N",PAGEFLG
          DISPLAY   *P36:15,*V2LON,PAGEFLG
          GOTO      KSHOSP00
.
NEXT8     REP       UPPLOW,PAGEFLG
          DISPLAY   *P36:15,*V2LON,PAGEFLG
.
          MATCH     "Y",PAGEFLG
          GOTO      KSHOSP00 IF EQUAL
.
          MATCH     "N",PAGEFLG
          GOTO      KSHOSP00 IF EQUAL
          BEEP
          GOTO      KSPAGE
.
.         Keyin for Hospital id
.
KSHOSP00  COMPARE   ONE,IBCNMHOS
          GOTO      SFIELD IF NOT EQUAL
.
          MOVE      "36",ECOL
          MOVE      "16",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
.
          CALL      PATHSPKY
          BRANCH    EXIT,KSHOSP20,START0
          DISPLAY   *P42:16,*V2LON,PTHSNAME;
          GOTO      SFIELD
.
KSHOSP20  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          DISPLAY   *P36:16,*V2LON,"ALL";
.
.         select the field to update
.
SFIELD    CALL      MAINQST 
.
          COMPARE   ZERO,CCITEM
          GOTO      CHKSEQ IF EQUAL
          GOTO      CLRSCR IF LESS
          MOVE      "C",MODE
.
          BRANCH    CCITEM,KSTART,KEND,KFDIS,KTDIS,KFDOC:
                           KPRIM,KUNIT,KSUMM,KSPAGE,KSHOSP00
.
          BEEP
          GOTO      SFIELD
.
CHKSEQ    DISPLAY   *P1:17,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:18,*V2LON,ONE,*HOFF," = Admission Sequence":
                    *P1:19,*V2LON,TWO,*HOFF," = Attending Doctor Sequence":
                    *P1:20,*V2LON,THREE,*HOFF," = Discharge Date Sequence":
                    *P1:21,*V2LON,FOUR,*HOFF," = U/R Sequence"
.
          KEYIN     *P1:22,"Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      CKBILDRG IF NOT EQUAL
.
          DISPLAY   *P1:17,*EF;
          GOTO      SFIELD
.
CKBILDRG  MOVE      SP1,BILDRGFL
          DISPLAY   *P1:23,"Display Billed DRG in DRG Column? (Y/N): ";
          KEYIN     *P42:23,*EL,*V2LON,BILDRGFL;
          REP       UPPLOW,BILDRGFL
          DISPLAY   *P42:23,*EL,*V2LON,BILDRGFL
.
          MATCH     "Y",BILDRGFL
          GOTO      CHKCONT IF EQUAL
          MATCH     "N",BILDRGFL
          GOTO      CHKCONT IF EQUAL
          GOTO      CKBILDRG
.
.
CHKCONT   KEYIN     *P1:24,*EL,"Ok to continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    "/",*V2LON,"C",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    "C",ANS
          GOTO      CLRSCR IF EQUAL
          CMATCH    "N",ANS
          GOTO      CHKSEQ IF EQUAL
          CMATCH    "Y",ANS
          GOTO      PROCESS IF EQUAL
          BEEP
          GOTO      CHKCONT
.
.
. *******************************************************************
PROCESS   BRANCH    OPTION OF BYADMN,BYATTD,BYDDATE,BYURNO
.
BYADMN    DISPLAY   *P52:2,*V2LON,"- Admission Sequence "
          MOVE      SORT1,SORTSTR
          GOTO      CNTIDX
.
BYATTD    DISPLAY   *P52:2,*V2LON,"- Attending Doctor Sequence "
          MOVE      SORT2,SORTSTR
          GOTO      CNTIDX
.
BYDDATE   DISPLAY   *P52:2,*V2LON,"- Discharge Date Sequence "
          MOVE      SORT3,SORTSTR
          GOTO      CNTIDX
.
BYURNO    DISPLAY   *P52:2,*V2LON,"- U/R Sequence "
          MOVE      SORT4,SORTSTR
.
CNTIDX    CALL      KILLIDX
          CALL      CREAIDX
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE TO UNIQUE
.
          OPEN      GNDFILE,FNAMEG
.
.          DISPLAY   *P1:24,*EL:
.                    *P20:24,"Processing Discharge File",*W2:
.                    *HOFF,*P1:24,*EL:
          DISPLAY   *P10:24,*EL,"Discharged Date :",*P44:24,"Disease Code :"
.
.                       /* READ DISCHARGE FILE BASE ON START & END DATE  */
.
          PACK      KEY16 WITH STARTD,SP8
          CALL      RDSDSCH2
LOOP      CALL      RDKDSCH2
          BRANCH    OVRCD OF PRINTR
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      XDATE WITH XCENT,XYEAR,XMON,XDAY
.
          MATCH     SP8,ENDATE
          GOTO      CHKHOS00 IF EQUAL
.
          MATCH     XDATE,ENDATE
          GOTO      PRINTR IF LESS
.
.         Multi-Hospital Processing
.
CHKHOS00  COMPARE   ONE,IBCNMHOS
          GOTO      NEXT10 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      NEXT10 IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LOOP
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LOOP IF NOT EQUAL
.
.       Check the admission file
.
NEXT10    DISPLAY   *P28:24,*V2LON,XDAY,SLASH,XMON,SLASH,XYEAR;
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF LOOP
.
          MATCH     DADMNO,AADMNO
          GOTO      LOOP IF NOT EQUAL
.
          MATCH     DURNO,AURNO
          GOTO      LOOP IF NOT EQUAL
.
          MATCH     SP3,SELUNIT             * check unit
          GOTO      NEXT10A IF EQUAL
.
          MATCH     SELUNIT,ACLSSFT
          GOTO      LOOP IF NOT EQUAL
.
NEXT10A   MATCH     SP3,START3
          GOTO      NEXT14 IF EQUAL
.
.         Check if doctor code was keyin
.
          MATCH     SP6,STDOCT
          GOTO      NODOCT IF EQUAL
.
          MATCH     ADOCTA,STDOCT       * Same doctor ?
          GOTO      LOOP IF NOT EQUAL
.
NODOCT    MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF LOOP
.
          MATCH     DRTYPE,START3
          GOTO      NEXT14 IF EQUAL
.
.       Check with the flag of primary type only
.
          MATCH     "Y",PRIMFLG
          GOTO      LOOP IF EQUAL
.
          MATCH     START3,DRTYPE2
          GOTO      NEXT14 IF EQUAL
.
          MATCH     START3,DRTYPE3
          GOTO      NEXT14 IF EQUAL
.
          MATCH     START3,DRTYPE4
          GOTO      NEXT14 IF EQUAL
.
          MATCH     START3,DRTYPE5
          GOTO      NEXT14 IF EQUAL
          GOTO      LOOP
.
NEXT14    MOVE      DURNO,KEY8
          CALL      RDMAST1
          MOVE      SP70,PNAME12
          MOVE      PGNAME,ANS
          PACK      PNAME12,ANS,DOT,PSNAME
.
          PACK      DNAME18,SP70
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NODOCA
          MOVE      DGNAME,ANS
          PACK      DNAME18,ANS,DOT,DSNAME
.
.         Check Disease Code range
.
NODOCA    UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      FROMDATE WITH XCENT,XYEAR,XMON,XDAY
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      TODATE WITH XCENT,XYEAR,XMON,XDAY
          CALL      RTIODAYS
.
          MOVE      ZERO,WRFLG
...       MOVE      SP5,DIM5
          MOVE      ZERO,F3                 * clear out Patient codes *I-222491
          REPEAT
            ADD       ONE,F3
            MOVE      SP5,CODES[F3]
          UNTIL     F3 = 200
          MOVE      ZERO,CODESNO                             * end    *I-222491
.
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
.
LOOP1     MOVE      SP1,KEY1
          CALL      RKPTECD2
          BRANCH    OVRCD,LOOP
.
          MATCH     DADMNO,PTEDADMN
          GOTO      LOOP IF NOT EQUAL
          MATCH     SP9,START9
          GOTO      NEXT11 IF EQUAL
          MATCH     START9,PTEDCODE
          GOTO      LOOP1 IF LESS
.
NEXT11    MATCH     SP9,END9
          GOTO      NEXT12 IF EQUAL
          MATCH     END9,PTEDCODE
          GOTO      NEXT12 IF LESS
          GOTO      LOOP1 IF NOT EQUAL
.
NEXT12    DISPLAY   *P59:24,*V2LON,PTEDCODE;
          COMPARE   ZERO,WRFLG
          GOTO      NEXT13 IF EQUAL
.
...       MATCH     DIM5,PTEDCODE                                     *C-222491
...       GOTO      LOOP1 IF EQUAL
          MOVE      ZERO,FOUND
          IF        CODESNO > 0
            MOVE      ZERO,F3
            REPEAT
              ADD       ONE,F3
              MATCH     PTEDCODE,CODES[F3]
              IF        @EQUAL
                MOVE      ONE,FOUND
              ENDIF
            UNTIL    FOUND = 1 | F3 = CODESNO
          ENDIF
          COMPARE   ONE,FOUND
          GOTO      LOOP1 IF EQUAL                         * end      *C-222491
.
NEXT13    MOVE      DADMNO,DDADMNO
          MOVE      DDADMNO,KEY8
          READ      GNDFILE,KEY8;KEY8
          GOTO      SAMEP IF NOT OVER
.
          ADD       ONE,GNDPAT
          ADD       NBRDAYS,GNDDAY
          WRITE     GNDFILE,KEY8;KEY8
.
SAMEP     MOVE      UNIQUE,DUNIQUE
          MOVE      DADMNO,DIM8VISN
          MOVE      DIM8VISN,DDADMNO
.
          MATCH     "Y",BILDRGFL
          GOTO      SAME05 IF NOT EQUAL
.
. Billed DRG - This will override the PTDSDDRG with the billed DRG code 
.
BILDRG00  CALL      GETDRG
          MOVE      DDRGCODE,PTDSDDRG 
.
.                          Admn   Att.Doc Disc.Dt UR.No.
.
SAME05    BRANCH    OPTION,SAME10,SAME11,SAME12,SAME13
.
SAME10    PACK      KEY25 WITH PTEDCODE,DDADMNO,DUNIQUE
          WRITE     TEMPFILE,KEY25;PTEDCODE,DDADMNO,DUNIQUE,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX:
                    PSAGE,DNAME18,NBRDAYS,FROMDATE,TODATE,DSNAME
          GOTO      SAME20
.
SAME11    PACK      KEY37 WITH PTEDCODE,DSNAME,DUNIQUE
          WRITE     TEMPFILE,KEY37;PTEDCODE,DDADMNO,DUNIQUE,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX:
                    PSAGE,DNAME18,NBRDAYS,FROMDATE,TODATE,DSNAME
          GOTO      SAME20
.
SAME12    PACK      KEY25,PTEDCODE,TODATE,DUNIQUE
          WRITE     TEMPFILE,KEY25;PTEDCODE,DDADMNO,DUNIQUE,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX:
                    PSAGE,DNAME18,NBRDAYS,FROMDATE,TODATE,DSNAME
          GOTO      SAME20
.
SAME13    PACK      KEY25,PTEDCODE,PURNO,DUNIQUE
          WRITE     TEMPFILE,KEY25;PTEDCODE,DDADMNO,DUNIQUE,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX:
                    PSAGE,DNAME18,NBRDAYS,FROMDATE,TODATE,DSNAME
          GOTO      SAME20
.
SAME20    ADD       ONE TO UNIQUE
.
          MOVE      ONE,WRFLG
...       MOVE      PTEDCODE,DIM5                                     *C-222491
          ADD       ONE,CODESNO
          MOVE      PTEDCODE,CODES[CODESNO]                * end      *C-222491
          GOTO      LOOP1
.
PRINTR    
.          DISPLAY   *P1:24,*EL,*V2LON:
.                    *P20:24,"Print Report",*W2,*HOFF,*P1:24,*EL
.
          CALL      HEADSET
.
PRTALL    SUB       ONE,UNIQUE
          DISPLAY   *P1:23,*EL,"Total Records       : ",*V2LON,UNIQUE,*HOFF:
                    *P1:24,"Printing Record No. :",*P35:24,"Disease :":
                    *P60:24,"Admission :"
.
          MOVE      ONE,COUNT
          MOVE      "Y",FIRST
          MOVE      SP5,SAVCD
          MOVE      ZEROVISN,SAVADM
.
          BRANCH    OPTION,PRT10,PRT11,PRT12,PRT13
.
PRT10     MOVE      SP25 TO KEY25
          READ      TEMPFILE,KEY25;;
          GOTO      READLOP
.
PRT11     PACK      KEY37 WITH SP20,SP20
          READ      TEMPFILE,KEY37;;
          GOTO      READLOP
.
PRT12     PACK      KEY25,SP70
          READ      TEMPFILE,KEY25;;
          GOTO      READLOP
.
PRT13     PACK      KEY25,SP70
          READ      TEMPFILE,KEY25;;
          GOTO      READLOP
.
READLOP   READKS    TEMPFILE;PTEDCODE,DDADMNO,DUNIQUE,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX:
                    PSAGE,DNAME18,NBRDAYS,FROMDATE,TODATE,DSNAME
          GOTO      ENDP IF OVER
.
          MOVE      DUNIQUE,UNIQUE
          MOVE      DDADMNO,DIM8VISN
          MOVE      DIM8VISN,DADMNO
.
          MOVE      PTEDCODE,SAVMDC
.
          MATCH     DADMNO,SAVADM
          GOTO      CONTR IF NOT EQUAL
.
          MATCH     PTEDCODE,SAVCD
          GOTO      READLOP IF EQUAL
.
CONTR     MOVE      ZERO,ADDFLG
          DISPLAY   *P23:24,*V2LON,COUNT,*P45:24,PTEDCODE,*P72:24,DADMNO;
.
          MATCH     "Y",FIRST
          GOTO      NOTFST IF NOT EQUAL
          MOVE      "N",FIRST
          MOVE      PTEDCODE,SAVCODE
          MOVE      PTEDCODE,SAVCODS
.
.     Only use the first digit of disease code, up to the digit before a dot
.
          MOVE      SP9,DIM9
          MOVE      PTEDCODE,DIM9
          CALL      FINDOT
          BUMP      DIM9,-1
          APPEND    SP9,DIM9
          RESET     DIM9
.>>>>>>    MOVE      DIM9,SAVHALF
          MOVE      DIM9,SAVALL
.
          CALL      SETSUB
          GOTO      SAMEH
.
NOTFST    MOVE      SP9,DIM9
          MOVE      PTEDCODE,DIM9
          CALL      FINDOT
          BUMP      DIM9,-1
          APPEND    SP9,DIM9
          RESET     DIM9
.
.>>>>>>    MATCH     SAVHALF,DIM9
          MATCH     SAVALL,DIM9
          GOTO      SAMEH IF EQUAL
.
          CALL      PRTSUB
          CALL      SETSUB
.
SAMEH     MOVE      SP9,SUBHEAD
          MOVE      PTEDCODE,DIM9B
.
          MOVE      SP9,DIM9
          MOVE      PTEDCODE,DIM9
          CALL      FINDOT
.
          CMATCH    DOT,DIM9
          GOTO      SAMEH1 IF NOT EQUAL
          BUMP      DIM9,1
.
SAMEH1    APPEND    SP9,DIM9
          RESET     DIM9
          MOVE      ENDATE,ICDRDDTE              * use to validate icd code
          MOVE      DIM9,KEY9
          CALL      RDPTICD1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9D1
          ENDIF
.
          MOVE      DDESC,DIM70B
          MOVE      KEY9,SUBHEAD
.
.       Make sure only the first sub-disease code is counted for number of
.       patients and bed days
.
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
CLP1      CALL      RKPTECD2
          BRANCH    OVRCD,CKHD
.
          MATCH     DADMNO,PTEDADMN
          GOTO      CKHD IF NOT EQUAL
.
          MOVE      PTEDCODE,DIM9
          MOVE      SP9,DIM9
          CALL      FINDOT
          BUMP      DIM9,-1
          APPEND    SP9,DIM9
          RESET     DIM9
.
.>>>>>>    MATCH     SAVHALF,DIM9
          MATCH     SAVALL,DIM9
          GOTO      CLP1 IF NOT EQUAL
.
.       We now have the first sub-disease code for this disease code
.
          MATCH     PTEDCODE,DIM9B
          GOTO      CKHD IF NOT EQUAL  * Patient has already been counted
.
          ADD       ONE,SUBPAT
.
CKHD      MOVE      DIM9B,PTEDCODE     * Restore disease code
          COMPARE   FIFTY5,LNO
          CALL      HEAD IF NOT LESS
.
          COMPARE   ONE,SHDFLG
          CALL      HEAD30 IF EQUAL
.
.       We only want to break the report at the four digit point
.
          MATCH     SAVCODE,PTEDCODE
          GOTO      SAMEC IF EQUAL        * First five digits agree
.
          MATCH     "E",PTEDCODE
          GOTO      NSAMEC IF EQUAL
.
          MATCH     "M",PTEDCODE
          GOTO      NSAMEC IF EQUAL
.
          MATCH     SAVCODS,PTEDCODE
          GOTO      SAMEC IF EQUAL         * First four digits agree
.
NSAMEC    MOVE      PTEDCODE,SAVCODE
          MOVE      PTEDCODE,SAVCODS
.
          BRANCH    HEADFLG OF NSAME1
.
          COMPARE   "13",LNO
          GOTO      SAMEC IF EQUAL
          GOTO      NSAME2
.
NSAME1    COMPARE   SIX,LNO
          GOTO      SAMEC IF EQUAL
.
NSAME2    MATCH     "Y",SUMFLAG
          GOTO      SAMEC IF EQUAL
.
          PRINT     *N,*1,"---------------------------------------------------":
                    *52,"---------------------------------------------------":
                    *103,"------------------------------",*N:
                    *1,"Sub-Disease Code : ",SUBHEAD,*35,DIM70B,*N:
                    *1,"---------------------------------------------------":
                    *52,"---------------------------------------------------":
                    *103,"------------------------------";
          ADD       "4",LNO
.
SAMEC     CALL      PRINTD1
          MOVE      DADMNO,SAVADM
          MOVE      SAVMDC,SAVCD
.
          ADD       ONE,COUNT
          GOTO      READLOP
.
ADDING    MATCH     CMDISA,PTEDTYPE
          GOTO      JUMP1 IF NOT EQUAL
          ADD       ONE,SUBASS
          RETURN
.
JUMP1     MATCH     CMDISP,PTEDTYPE
          GOTO      JUMP2 IF NOT EQUAL
.
          COMPARE   ONE,ADDFLG
          RETURN    IF EQUAL
.
          MOVE      ONE,ADDFLG
          ADD       ONE,SUBPRI
          ADD       NBRDAYS,SUBDAY
          RETURN
.
JUMP2     MATCH     CMDISS,PTEDTYPE
          GOTO      JUMP3 IF NOT EQUAL
          ADD       ONE,SUBSEC
          RETURN
.
JUMP3     MATCH     CMDISC,PTEDTYPE
          GOTO      JUMP4 IF NOT EQUAL
          ADD       ONE,SUBCOM
          RETURN
.
JUMP4     COMPARE   ONE,PTCNLTYP             * Using "L" Types?
          GOTO      JUMP19 IF NOT EQUAL
          MATCH     "L",PTEDTYPE
          GOTO      JUMP19 IF NOT EQUAL
          ADD       ONE,SUBLOSP
          RETURN
.
JUMP19    RETURN
.
.       set sub-heading
.
SETSUB    MATCH     "E",PTEDCODE
          GOTO      CHAR4 IF EQUAL
          MATCH     "M",PTEDCODE
          GOTO      CHAR4 IF EQUAL
          MOVE      PTEDCODE,DIM3
          PACK      DIM9A WITH DIM3,SP6
          GOTO      RDALL
.
CHAR4     MOVE      PTEDCODE,DIM4
          PACK      DIM9A WITH DIM4,SP6
.
RDALL     PACK      DIM70A,SP30,SP30,SP20
          MOVE      SP70,DDESC
          MOVE      ENDATE,ICDRDDTE              * use to validate icd code
          MOVE      DIM9A,KEY9
          CALL      RDPTICD1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9D1
          ENDIF
.
          MOVE      DDESC,DIM70A
          RETURN
.
.     Routine to find a dot
.
FINDOT    CMATCH    DOT,DIM9
          RETURN    IF EQUAL
.
          CMATCH    HYPH,DIM9
          RETURN    IF EQUAL
.
          CMATCH    FSLASH,DIM9
          RETURN    IF EQUAL
.
          CMATCH    SP1,DIM9
          RETURN    IF EQUAL
.
          BUMP      DIM9,1
          GOTO      FINDOT IF NOT EOS
.
          RESET     DIM9
          RETURN
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
.ENDP      COMPARE   ZERO,PAGENO
.          GOTO      NOREC IF EQUAL

ENDP      MOVE      ONE,ENDFLG
          CALL      PRTSUB
          MATCH     "Y",SUMFLAG
          GOTO      ENDP1 IF NOT EQUAL
.
          COMPARE   "40",LNO
          CALL      HEAD IF NOT LESS
          GOTO      ENDP2
.
ENDP1     CALL      HEAD
ENDP2     CALL      PRTGND
.
JUMP5     PRINT     *N,*N,"  ***  END OF REPORT  ***"
          DISPLAY   *P1:23,*EL,*P1:24,*EL,*P40:24:
                    "Report Generation Completed. ";
.
          CALL      KILLIDX
.
          GOTO      CLRSCR
.
NOREC     DISPLAY   *P1:24,*EL,*P20:24,*B:
                    "No Record Selected. ";
.          CALL      HITENTER
.
          CALL      KILLIDX
.
          GOTO      CLRSCR
.
.     PRINT ACTUAL DATA
.
PRINTD1   MOVE      ZERO,DISCOUNT
          MOVE      "Y",FLINE
          PACK      KEY16,DADMNO,SP20                                 *C-240107
.
          MOVE      SP1,PTEDTYPE
          MOVE      SP9,PTEDCODE
          MOVE      SP9,PTEOCODE
          MOVE      "N",NODIS
          MOVE      "N",NOOPR
          PACK      DNAME18A,SP70
          CALL      RSPTECD2
          CALL      RSPTECO2
.
LOOPX     MATCH     "N",NODIS
          GOTO      RDOPR IF NOT EQUAL
          CALL      RKPTECD2
          BRANCH    OVRCD OF NODISS
.
          MATCH     DADMNO,PTEDADMN
          GOTO      NODISS IF NOT EQUAL
.
          MATCH     "E",PTEDCODE
          GOTO      CHKFIVE IF EQUAL
.
          MATCH     "M",PTEDCODE
          GOTO      CHKFIVE IF EQUAL
.
          MATCH     SAVCODS,PTEDCODE
          CALL      ADDING IF EQUAL
          GOTO      JUMP20
.
CHKFIVE   MATCH     SAVCODE,PTEDCODE
          CALL      ADDING IF EQUAL
.
JUMP20    MATCH     "N",NOOPR
          GOTO      RDOPR IF EQUAL
          CALL      PRTDET
          GOTO      LOOPX
.
NODISS    MOVE      "Y",NODIS
          MOVE      SP9,PTEDCODE
          MOVE      SP1,PTEDTYPE
.
RDOPR     MATCH     "N",NOOPR
          GOTO      CHKBOTH IF NOT EQUAL
.      
LOOPY     CALL      RKPTECO2
          BRANCH    OVRCD OF NOOPRS
.
          MATCH     DADMNO,PTEOADMN
          GOTO      NOOPRS IF NOT EQUAL
          CALL      PRTDET
          MATCH     "N",NODIS
          GOTO      LOOPX IF EQUAL
          GOTO      LOOPY
.
NOOPRS    MOVE      "Y",NOOPR
          MOVE      SP9,PTEOCODE
.
CHKBOTH   MATCH     "N",NODIS
          GOTO      ENDPRT IF NOT EQUAL
          CALL      PRTDET
          GOTO      LOOPX
.
ENDPRT    COMPARE   FIFTY5,LNO
          CALL      HEAD IF NOT LESS
          MATCH     "Y",FLINE
          CALL      LNONE IF EQUAL
          RETURN
.
PRTDET    COMPARE   FIFTY5,LNO
          CALL      HEAD IF NOT LESS
.
          MATCH     "Y",FLINE
          GOTO      LNONE IF EQUAL
.
          MATCH     "Y",SUMFLAG
          RETURN    IF EQUAL
.
          IF        PRNTHEAD = TRUE
          PRINT     *N;
          MOVE      FALSE,PRNTHEAD
          ENDIF
.
          PRINT     *43,PTEDTYPE,*45,PTEDCODE,*56,PTEOCODE,*68,DNAME18A
          ADD       ONE,LNO
          MOVE      ONE,DISCOUNT                   * count no. diseases printed
          RETURN
.
LNONE     MOVE      "N",FLINE
.
          MATCH     "Y",SUMFLAG
          RETURN    IF EQUAL
.
          UNPACK    FROMDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PADATE,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          UNPACK    TODATE,XCENT,XYEAR,XMON,XDAY
          PACK      PDDATE,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
.        10        20        30        40        50        60        70        80        90        100       110       120       130
.23456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
.atient Name  U/R No    Adm No   Sex Age  Diseases     Operations  Attending Doctor    Unit  DDest  DRG  BDays Admission  Discharge
.xxxxxxxxxxx  99999999  99999999  x  999  x xxxxxxxxx  xxxxxxxxx   xxxxxxxxxxxxxxxxxx  xxx    xxx   xxxx  xxx  xxxxxxxxxx xxxxxxxxxx
.
          PRINT     *N,*1,PNAME12,*15,PURNO,*25,DADMNO,*35,PSEX,*38,PSAGE:
                    *43,PTEDTYPE,*45,PTEDCODE,*56,PTEOCODE,*68,DNAME18:
                    *88,ACLSSFT,*95,DDEST,*101,PTDSDDRG,*107,NBRDAYS:
                    *112,PADATE,*123,PDDATE
          ADD       ONE,LNO
          MOVE      FALSE,PRNTHEAD
.
          CALL      ALOC0000                       * add location (Med Rec)
.
          ADD       ONE,LNO
          RETURN
+
.***************************************************************************
.*  ALOC0000  :  Add location  (Medical Records)                           *
.***************************************************************************
ALOC0000  PACK      KEY20,PURNO,Z30
          CALL      RSMRMAS1
. -----   CALL      READPTM1                                            D-90201
          CALL      RKMRMAS1                                           *I-90201
          BRANCH    OVRCD,ALOC9999
.
          MATCH     PURNO,MRMAURNO
          GOTO      ALOC9999 IF NOT EQUAL
.
          PRINT     *1,"Record: ",MRMAHHOS,MRMAHLOC,SLASH,MRMADOTY:
                    SLASH,MRMAVOLN;
.
. we have master details now get current location
.
          PACK      KEY26,MRMAKEY,Z30
          CALL      RSMRHIS1
          CALL      RPMRHIS1
          BRANCH    OVRCD,ALOC9999
.
          MATCH     MRMAKEY,MRHIKEY
          GOTO      ALOC9999 IF NOT EQUAL
.
          MOVE      SP30,MRLODESC
          PACK      KEY7,MRHIDHOS,MRHILOC
          CALL      RDMRLOC1
          MOVE      MRLODESC,DESC22
          PRINT     SP2,"Curr. Loc.: ",DESC22;
.
ALOC9999  RETURN
.
.
. ----------------------------------------- Start of Deletion --------- D-90201
. ---READPTM1  MOVE      ZERO,OVRCD
. ---     READKP    MRTMASA1;MRMAURNO,MRMAHLOC,MRMADOTY,MRMAVOLN,MRMASTAT:
. ---               MRMACOMM,MRMAKEY,MRMASPAR
. ---     GOTO      OVERCOND IF OVER
. ---     RETURN
. ----------------------------------------- End of Deletion ----------- D-90201
+
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,PAGENO
          CLOCK     TIME,CTIMEIS
.
          BRANCH    OPTION,HEAD00,HEAD01,HEAD02,HEAD03
.
HEAD00    MOVE      "- Admission Sequence",CPHDROPT
          GOTO      HEAD05
.
HEAD01    MOVE      "- Attending Doctor Sequence",CPHDROPT
          GOTO      HEAD05
.
HEAD02    MOVE      "- Discharge Date Sequence",CPHDROPT
          GOTO      HEAD05
.
HEAD03    MOVE      "- U/R Sequence",CPHDROPT
          GOTO      HEAD05
.
HEAD05    CALL      HEAD132
          MOVE      "3",LNO
          MOVE      TRUE,PRNTHEAD
.
          BRANCH    HEADFLG OF HEAD30
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME
          ENDIF
.
          PRINT     *40,"Discharge Date From  ",PFDATE,"   To  ",PTDATE:
                    *N,*40,"Disease   Code From  ",XDIM9,"  To  ",YDIM9
.
          MOVE      ONE,HEADFLG
          MATCH     SP6,YDIM6
          GOTO      HEAD10 IF EQUAL
.
          PRINT     *40,"Doctor    Type From  ",XDIM6:
                    "     To  ",YDIM6,*100,TYPDESC,*N
.
          GOTO      HEAD13
.
HEAD10    MATCH     SP6,STDOCT
          GOTO      HEAD12 IF EQUAL
          PRINT     *40,"Doctor    Code       ",STDOCT:
                    *100,DOCNAME,*N:
                    *40,"Doctor    Type       ",XDIM6:
                    *100,TYPDESC
          ADD       TWO,LNO
          GOTO      HEAD13
.
HEAD12    PRINT     *40,"Doctor    Type       ",XDIM6:
                    *100,TYPDESC
          ADD       TWO,LNO
.
HEAD13    MATCH     "Y",BILDRGFL
          IF        @EQUAL
            PRINT     *40,"DRG",SP20,SP5,"Billed DRG",*N
          ELSE
            PRINT     *40,"DRG",SP20,SP5,"Grouper DRG",*N
          ENDIF
          ADD       ONE,LNO
.
.
HEAD20    MOVE      "8",LNO
.
HEAD30    MATCH     "Y",SUMFLAG
          RETURN    IF EQUAL
.
          COMPARE   ONE,ENDFLG
          RETURN    IF EQUAL
.
.        10        20        30        40        50        60        70        80        90        100       110       120       130
.23456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
.atient Name   U/R No    Adm No  Sex Age  Diseases     Operations  Attending Doctor    Unit  DDest  DRG  BDays Admission  Discharge
.xxxxxxxxxxx  99999999  99999999  x  999  x xxxxxxxxx  xxxxxxxxx   xxxxxxxxxxxxxxxxxx  xxx    xxx   xxxx  xxx  xxxxxxxxxx xxxxxxxxxx
.
          MOVE      DIM9B,DIM4
          PRINT     *1,"Disease Code     : ",DIM9A,*35,DIM70A,*N,*N:
                    *1,"Patient Name",*16,"U/R No",*26,"Adm No",*34,"Sex":
                    *38,"Age",*43,"Diseases",*56,"Operations":
                    *68,"Attending Doctor":
                    *88,"Unit",*94,"DDest",*101,"DRG",*106,"BDays":
                    *112,"Admission",*123,"Discharge",*N:
                    *1,"---------------------------------------------------":
                    *52,"---------------------------------------------------":
                    *103,"------------------------------",*N:
                    *1,"Sub-Disease Code : ",SUBHEAD,*35,DIM70B:
                    *N,"---------------------------------------------------":
                    *52,"---------------------------------------------------":
                    *103,"------------------------------";
.
          BRANCH    SHDFLG OF HEAD40
.
          MOVE      "13",LNO        * Changes required if this
.                                   * line is changed
          COMPARE   ZERO,HEADFLG
          RETURN    IF EQUAL
.
          MOVE      SIX,LNO
          RETURN
.
HEAD40    ADD       SIX,LNO
          MOVE      ZERO,SHDFLG
          RETURN
.
.    Print the sub-total
.
PRTSUB    COMPARE   "45",LNO
          CALL      HEAD IF NOT LESS
.
          COMPARE   ZERO,SUBPRI
          GOTO      OKPRT IF NOT EQUAL
          MOVE      ZERO,SUBLOS
          GOTO      OKPRT1
.
OKPRT     MOVE      SUBDAY,TMPCAL
          DIV       SUBPRI,TMPCAL
          MOVE      TMPCAL,SUBLOS
.
OKPRT1    MATCH     "Y",SUMFLAG
          GOTO      OKPRT2 IF NOT EQUAL
.
          PRINT     *1,"Disease Code     : ",DIM9A,*35,DIM70A
.
OKPRT2    IF        DISCOUNT=0
          PRINT     *N;
          ENDIF
          MOVE      ZERO,DISCOUNT
          PRINT     *N,*37,"-----Totals For Disease Code ",DIM9A,"--------":
                    *N,*N,*21,"Sub-Total Patients          :     ",SUBPAT:
                    *66,"Sub-Bed Days Primary              :  ",SUBDAY,*N:
                    *21,"Sub-Total Primary           :     ",SUBPRI:
                    *66,"Sub-Average Length Of Stay Primary:    ",SUBLOS,*N:
                    *21,"Sub-Total Sequela           :     ",SUBSEC:
                    *66,"Sub-Total Associated              :     ",SUBASS,*N:
                    *21,"Sub-Total Complication      :     ",SUBCOM;
.
          COMPARE   ONE,PTCNLTYP
          GOTO      OKPRT2A IF NOT EQUAL 
          PRINT     *66,"Sub-Total LOS Primary             :     ",SUBLOSP
          GOTO      OKPRT2B
.
OKPRT2A   PRINT     *N
OKPRT2B   CALL      ZEROS
          MOVE      PTEDCODE,SAVCODE
          MOVE      PTEDCODE,SAVCODS
.
          MOVE      SP9,DIM9
          MOVE      PTEDCODE,DIM9
          CALL      FINDOT
          BUMP      DIM9,-1
          APPEND    SP9,DIM9
          RESET     DIM9
.>>>>>>    MOVE      DIM9,SAVHALF
          MOVE      DIM9,SAVALL
.
          MATCH     "Y",SUMFLAG
          GOTO      OKPRT3 IF EQUAL
.
          MATCH     "Y",PAGEFLG
          GOTO      OKPRT4 IF EQUAL
.
OKPRT3    PRINT     *N;
          ADD       "9",LNO
          MOVE      ONE,SHDFLG
          RETURN
.
OKPRT4    MOVE      "60",LNO
          RETURN
.
PRTGND    MOVE      GNDDAY,TMPCAL
          DIV       GNDPAT,TMPCAL
          MOVE      TMPCAL,GNDLOS
          PRINT     *N,*N,*N,*N,*N,*N,*N,*N:
                    *37,"-------   G R A N D    T O T A L S   ---------",*N,*N:
                    *41,"Total Patients          :     ",GNDPAT,*N:
                    *41,"Bed Days                :   ",GNDDAY,*N:
                    *41,"Average Length Of Stay  :     ",GNDLOS,*N
          RETURN
.
. ------- Remove temp indexed files -------
.
KILLIDX   CLOSE     TEMPFILE
          CLOSE     GNDFILE
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEG,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
. ------- Create indexed files -------
.
CREAIDX   PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 117 ",CMDLINE
          APPEND    SORTSTR,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEG,CMDLINE
          APPEND    " 9 uc1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          RETURN
.
.
CLRSCR    DISPLAY   *P36:4,*EL,*P36:5,*EL,*P36:7,*EL,*P36:8,*EL:
                    *P36:10,*EL,*P36:11,*EL,*P36:12,*EF
          GOTO      START0
.
.
HEADSET   MOVE      SP9,XDIM9
          MOVE      SP9,YDIM9
.
          MOVE      START9,XDIM9
          MOVE      END9,YDIM9
.
          MATCH     SP9,START9
          GOTO      CONT1 IF NOT EQUAL
          MOVE      FSTREC,XDIM9
.
CONT1     MATCH     SP9,END9
          GOTO      CONT2 IF NOT EQUAL
          MOVE      LSTREC,YDIM9
.
CONT2     MOVE      SP6,XDIM6
          MOVE      SP6,YDIM6
.
          MOVE      START6,XDIM6
          MOVE      END6,YDIM6
.
          MATCH     SP6,START6
          GOTO      CONT3 IF NOT EQUAL
          MOVE      FSTREC,XDIM6
.
CONT3     MATCH     SP6,END6
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM6
          RETURN
.
ZEROS     MOVE      ZERO,SUBPAT
          MOVE      ZERO,SUBPRI
          MOVE      ZERO,SUBDAY
          MOVE      ZERO,SUBLOS
          MOVE      ZERO,SUBLOSP
          MOVE      ZERO,SUBASS
          MOVE      ZERO,SUBSEC
          MOVE      ZERO,SUBCOM
          RETURN
.
ZEROG     MOVE      ZERO,GNDPAT
          MOVE      ZERO,GNDDAY
          MOVE      ZERO,GNDLOS
          RETURN
+
.*********************************************************************
.         enter the doctor type
.*********************************************************************
KDTP0000  MOVE      "36",CCOL
          MOVE      "10",CVERT
          MOVE      ONE,SCRNFLAG
          UNPACK    SP30,DOCCODE,KEY6,STDOCT
. 
KDTP1000  MOVE      SP10,STDOCT             
          KEYIN     *PCCOL:CVERT,*DV,UNDLN6,*PCCOL:CVERT,*V2LON,KEY6:
                    *PCCOL:CVERT,*DV,KEY6;
          PACK      KEY6,KEY6,SP3
          MATCH     SP6,KEY6
          GOTO      KDTP4000 IF EQUAL       * set for all codes
.         
          CMATCH    QUEST,KEY6
          GOTO      KDTP2000 IF NOT EQUAL   * validate the code
.
. display valid doctors and types
.
          MOVE      CODEDT,CODE
          CALL      PATCODDS
.
          KEYIN     *P1:24,*EL,"Do you want to see Doctor Code (Y/N) ? ",ANS;
          MOVE      ZERO,SCRNFLAG
          MOVE      "24",CVERT
          MOVE      "18",CCOL
          REP       UPPLOW,ANS
          CMATCH    "N",ANS
          GOTO      KDTP1500 IF EQUAL
.
          CALL      PATDOCDS
          MOVE      "24",CVERT
          MOVE      "18",CCOL
          MATCH     SP6,DOCCODE
          GOTO      KDTP1500 IF EQUAL
          MOVE      DOCCODE,KEY6
          GOTO      KDTP2000
.
KDTP1500  DISPLAY   *P1:24,*EL,"Enter the Code : ",*EL;
          GOTO      KDTP1000
.
. validate code
.
KDTP2000  UNPACK    SP10,KEY3,DIM3          * Initialise
          UNPACK    KEY6,KEY3,DIM3
          MATCH     SP3,DIM3                * Check if doctor type/code entered
          GOTO      KDTP2010 IF EQUAL       * Must be doctor type
          CALL      RDDOCT1
          COMPARE   ZERO,OVRCD
          GOTO      KDTP2020 IF EQUAL       * Valid doctor code
.
KDTP2010  MOVE      KEY6,KEY3
          PACK      KEY5,ANSD,ANST,KEY3
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      KDTP3000 IF EQUAL       * valid code
.
          PACK      KEY6,KEY3,SP10
          CALL      RDDOCT1
          BRANCH    OVRCD,KDTP2100          * invalid code
.
KDTP2020  MOVE      KEY6,STDOCT
          MOVE      DRTYPE,ACODE
          PACK      KEY5,CODEDT,ACODE
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      KDTP3000 IF EQUAL       * valid code
.
KDTP2100  DISPLAY   *P1:24,*EL:
                    *B,*V2LON,"** Invalid Doctor Type/Doctor Code. **    ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,KDTP1000
          GOTO      KDTP1500
.
. have a valid code
.
KDTP3000  MOVE      ACODE,START3
          BRANCH    SCRNFLAG,KDTP3100
          CALL      RDIS0000
.
KDTP3100  MATCH     SP6,STDOCT
          GOTO      KDTP3200 IF EQUAL
          MOVE      "Y",PRIMFLG
          MOVE      DGNAME,ANS
          PACK      DOCNAME,ANS,DOT,DSNAME
          DISPLAY   *P36:10,*EL,*V2LON,STDOCT,*HOFF,*P45:10,DOCNAME:
                    *P36:11,*EL,*V2LON,START3,*HOFF,*P45:11,TDESC:
                    *P36:12,*EL,*V2LON,PRIMFLG;
          GOTO      KDTP9999
.
KDTP3200  DISPLAY   *P36:10,*EL,*V2LON,START3,*HOFF,*P45:10,TDESC:
                    *P36:11,*EL;
          GOTO      KDTP9999
.
KDTP4000  BRANCH    SCRNFLAG,KDTP4100
          CALL      RDIS0000
.
KDTP4100  MOVE      SP3,START3
          DISPLAY   *P36:10,*V2LON,ALLREC,*EL;
.
KDTP9999  RETURN
+
.*********************************************************************
.         redisplay the current data
.*********************************************************************
RDIS0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"1",*HOFF,". Starting Discharge Date       : ":
                    *V2LON,PFDATE,*HOFF:
                    *P1:5,*V2LON,"2",*HOFF,". Ending   Discharge Date       : ":
                    *V2LON,PTDATE,*HOFF:
                    *P1:7,*V2LON,"3",*HOFF,". From Disease Code             : ":
                    *V2LON,START9,*HOFF:
                    *P1:8,*V2LON,"4",*HOFF,". To   Disease Code             : ":
                    *V2LON,END9,*HOFF:
                    *P1:10,*V2LON,"5",*HOFF,". Keyin Doctor Type/Doctor Code :":
                    SP1,*V2LON,START3,*HOFF:
                    *P55:10,"   <RETURN> for All ":
                    *P1:12,*V2LON,"7",*HOFF,". Primary Doctor Type Only (Y/N):":
                    SP1,*V2LON,PRIMFLG,*HOFF:
                    *P1:13,*V2LON,"6",*HOFF,". Keyin Unit                    :":
                    SP1,*V2LON,SELUNIT,*HOFF:
                    *P1:14,*V2LON,"8",*HOFF,". Summary Details Only     (Y/N):":
                    SP1,*V2LON,SUMFLAG,*HOFF:
                    *P1:15,*V2LON,"9",*HOFF,". New page for each Disease(Y/N):":
                    SP1,*V2LON,PAGEFLG,*HOFF
.
          IF        IBCNMHOS=1
            DISPLAY   *P1:16,*V2LON,"10",*HOFF,".Hospital Id":
                      *P34:16,":",SP1,*V2LON,PTHSNAME,*HOFF;
          ENDIF
.
          MATCH     SP9,START9
          GOTO      RDIS2000 IF NOT EQUAL
          DISPLAY   *P36:7,*V2LON,FSTREC;
.
RDIS2000  MATCH     "Y",FSTTIM
          GOTO      RDIS9999 IF EQUAL
.
          MATCH     SP9,END9
          GOTO      RDIS9999 IF NOT EQUAL
.
          DISPLAY   *P36:8,*V2LON,LSTREC;
.
RDIS9999  RETURN
+
.*********************************************************************
.         clear the variables
.*********************************************************************
CLRA0000  UNPACK    SP20,START9,END9
          UNPACK    SP20,STARTD,ENDATE
          UNPACK    SP20,PFDATE,PTDATE
          UNPACK    SP10,SELUNIT
          UNPACK    SP10,START3,PRIMFLG,SUMFLAG,PAGEFLG
          UNPACK    SP10,STDOCT
          UNPACK    SP30,DOCNAME
          UNPACK    SP30,PTHSNAME
CLRA9999  RETURN
.
+
.*********************************************************************
.         Subroutine to key in a valid date
.*********************************************************************
KEYINDTE  
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            REP       " 0",CDAY
            MOVE      IMON,CMON
            REP       " 0",CMON
            MOVE      IYEAR,CYEAR
            REP       " 0",CYEAR
            MOVE      ICENT,CCENT
            REP       " 0",CCENT
          ENDIF
.
          MOVE      VERT,CVERT         * set up position
          MOVE      "36",CCOL
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
.
          RETURN
+
          INC       STD001IO
.
          INCLUDE   RTIODAYS
.
          INCLUDE   PATCODKY
          INCLUDE   PATDOCDS
          INCLUDE   PATHSPKY
.
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATHSPIO/INC
          INC       PATICDIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATDO1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
          INC       MRTMASIO/INC
          INC       MRTHISIO/INC
          INC       MRTLOCIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
          INC       GETDRG  
          INC       GETEFDRG 
          INC       PATPGRIO/INC   
          INC       PMSEDGIO/INC   
          INC       PATCFAIO/INC   
          INC       PATFX1IO/INC   
          INC       PATFN1IO/INC   
+
ENDIT     CHAIN     PGM                           * Top label
          STOP

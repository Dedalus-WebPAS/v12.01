. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBABIL29                                            *
. *                                                                           *
. *     FUNCTION:         Credit Note Report                                  *
. *                                                                           *
. *     DATE WRITTEN:     01/10/2004                                          *
. *                                                                           *
. *     AUTHOR:           Davin                                               *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *         V10.11.01  11/09/2017  Thanh T.     TSK 0821710                   *
. *                    Audit Amission/outpatient visit comments.              *
. *                    Removed PATCOMFD/IO                                    * 
. *          V9.11.02  23/04/2009 J.Tan  CAR 194893                           *
. *                    Mods to print cancelled admissions if any              *
. *          V9.11.01  02/02/2009 J.Tan  CAR 188549                           *
. *                    Mods to flag rounding                                  *
. *          V9.10.01  23/07/2008  J.Tan        CAR 170967                    *
. *                    Mods to print GST amount                               *
. *          V9.04.00  01/10/2004  Davin        CAR 52465                     *
. *                    Created report                                         *
. *                    18/10/2004  Lina Vo      CAR 52465                     *
. *                    Finished report                                        *
. *****************************************************************************
.
          INC       STD001FD   
          INC       COMCODFD/INC
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC
          INC       PATHSPFD/INC
          INC       PATINVFD/INC
          INC       PMSCNOFD/INC
          INC       PMSVX1FD/INC
          INC       PATSELDT/INC
.
.
.         VARIABLES DEFINITIONS
.
.
ABBREMR         INIT      "EMR"
ABBRINP         INIT      "INP"
ABBROUT         INIT      "OUT"
CFEETYP         FORM      1
CNSTFULL        INIT      "Fully Credited"
CNSTITEM        INIT      "Credit by Item"
CNSYSDSC        DIM       3
CNSTDESC        DIM       14
CODE            DIM       2
CODEDATE        DIM       8
CURRDATE        DIM       8
DESCEMR         INIT      "Emergency Credit Notes"
DESCINP         INIT      "Inpatient Credit Notes"
DESCOUT         INIT      "Outpatient Credit Notes"
DESCSYST        DIM       25                 
DIM10B          DIM       10
DSPEDDTE        DIM       10               * Display end date (dd/mm/ccyy)
DSPSTDTE        DIM       10               * Display start date (dd/mm/ccyy)
ENDDTE          DIM       8                       * End date (ccyymmdd)
FORM10P2        FORM      10.2
FORM11P2        FORM      11.2
GRANDCRD        FORM      10.2
GRANDGST        FORM      10.2
GRANDTOT        FORM      12.2
PNAME           DIM       20
STCLM           DIM       3
STRTDTE         DIM       8                       * Start date (ccyymmdd)
SRNDFLG         DIM       1
IRNDFLG         DIM       1
ORNDFLG         DIM       1
ERNDFLG         DIM       1
RNDAMNT         FORM     12.2
LOOPFLAG        FORM      1
.
CRDINP          FORM     10.2
CRDOUT          FORM     10.2
CRDEMR          FORM     10.2
GSTINP          FORM     10.2
GSTEMR          FORM     10.2
GSTOUT          FORM     10.2
TOTALEMR        FORM     12.2
TOTALINP        FORM     12.2
TOTALOUT        FORM     12.2
NOTESINP        FORM      6
NOTESEMR        FORM      6
NOTESOUT        FORM      6
.
.*********************************************************************
PRGID     INIT      "IBABIL29"
PRGNAM    INIT      "Credit Note Report"
VERSION   INIT      "V12.01.00"
.*********************************************************************
+
.*********************************************************************
.         ML1000 OF PROGRAM
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000
          BRANCH    EXIT,ML9000
.
ML1500    CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,ML1000
.
          BRANCH    OPTION,ML2000
          GOTO      ML9000
.
ML2000    CALL      REPOPT00
          BRANCH    EXIT,ML1000
.
          IF        IBCNMHOS=1
            CALL      KHOSID00       * Keyin hospital id
            BRANCH    EXIT,ML1000
          ENDIF
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      ZERO,LOOPFLAG
          CALL      PROCRP00
          BRANCH    EXIT,ML9000
.
          MOVE      "60",CLNO
          MOVE      ONE,LOOPFLAG     * processing any Cancelled Admissions
          CALL      PROCRP00
.
          GOTO      ML1000
.
ML9000    CHAIN     PGM
          STOP
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening comcodaf";
          OPEN      COMCODA1,"comcodaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmscnoaf";
          OPEN      PMSCNOA1,"pmscnoaf"
          OPEN      PMSCNOA4,"pmscnoaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
          READ      CONTROLF,TEN9;*212,CFEETYP
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.*********************************************************************
.         enter the option
.*********************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Date Range":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      "17",CCOL
          DISPLAY   *P1:9,"Starting Date : ",*EF:
                    *P1:10,"Ending Date   : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "9",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      STRTDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE
.
          CALL      PACDATE                 * Format the start date
          MOVE      CPCDATE,DSPSTDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPSTDTE;
.
          MATCH     STRTDTE,CURRDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Start date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE1000
          ENDIF
.
. ----- Keyin end date -----
.
DATE2000  MOVE      "10",CVERT
          UNPACK    STRTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE
.
          CALL      PACDATE                 * Format the end date
          MOVE      CPCDATE,DSPEDDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPEDDTE;
.
          MATCH     ENDDTE,CURRDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
          MATCH     STRTDTE,ENDDTE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must after the start date.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.*********************************************************************
.         enter the report option
.*********************************************************************
REPOPT00  MOVE      SP3,STCLM
          MOVE      ZERO,FSTSYS
          MOVE      NINE,FEDSYS                   * Default system range 0-9
          DISPLAY   *P1:12,*EF:                    * Clear screen
                    *P1:12,"Enter Report Type  : ",UNDLN3
.
          PACK      CMCDDESC,SP70
          MOVE      TEN2,EVERT
          MOVE      TWENTY2,ECOL
          MOVE      ZERO,CKYIMAND                 * mandatory
          MOVE      CURRDATE,CODEDATE
          REP       " 0",CODEDATE
          MOVE      "RP",CODE
          CALL      COMCODKY
.
          MOVE      CMCDACOD,STCLM
.
          MATCH     SP3,STCLM
          GOTO      REPOPT99 IF EQUAL
.
          DISPLAY   *P40:12,*V2LON,CMCDDESC
.
          MOVE      "Consolidated",DESCSYST
          MOVE      ZERO,F1
          MOVE      CMCDSI01,F1
          IF        F1>0
            MOVE      F1,FSTSYS
            MOVE      F1,FEDSYS
            LOAD      DESCSYST,F1,DESCEMR,DESCOUT,DESCINP
          ENDIF
.
REPOPT99  RETURN
.*********************************************************************
.       Enter Hospital ID
.*********************************************************************
KHOSID00  DISPLAY    *P1:15,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       "15",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOSID80,KHOSID90
          MOVE       ZERO,EXIT
          GOTO       KHOSID99
.
KHOSID80  MOVE       "All Hospitals",PTHSNAME
          MOVE       ZERO,EXIT
          GOTO       KHOSID99
.
KHOSID90  MOVE       ONE,EXIT
KHOSID99  RETURN
+
.*********************************************************************
.       Process Report
.*********************************************************************
PROCRP00  DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                     "** Generating Report **"
          DISPLAY   *P1:24,*EL,*P50:24,"Visit Number: ";
.
          MOVE      ZERO,CRDINP
          MOVE      ZERO,CRDOUT
          MOVE      ZERO,CRDEMR
          MOVE      ZERO,GSTINP
          MOVE      ZERO,GSTEMR
          MOVE      ZERO,GSTOUT
          MOVE      ZERO,GRANDCRD
          MOVE      ZERO,GRANDGST
          MOVE      ZERO,TOTALINP
          MOVE      ZERO,TOTALEMR
          MOVE      ZERO,TOTALOUT
          MOVE      ZERO,GRANDTOT
          MOVE      ZERO,NOTESINP
          MOVE      ZERO,NOTESEMR
          MOVE      ZERO,NOTESOUT
          MOVE      SP70,IRNDFLG
          MOVE      SP70,ORNDFLG
          MOVE      SP70,ERNDFLG
          MOVE      SP70,SRNDFLG
          MOVE      ZERO,RNDAMNT
.
          PACK      KEY18,STRTDTE,SP70
          CALL      RSPMCNO4
PROCRP10  CALL      RKPMCNO4
          BRANCH    OVRCD,PROCRP60
.
          MATCH     PMCNDATE,ENDDTE
          GOTO      PROCRP60 IF LESS
.
          MOVE      PMCNSYST,FORM2
.
          COMPARE   FSTSYS,FORM2
          GOTO      PROCRP10 IF LESS
.
          COMPARE   FORM2,FEDSYS
          GOTO      PROCRP10 IF LESS
.
          DISPLAY   *P65:24,*V2LON,PMCNVISN;
.
          COMPARE   ONE,LOOPFLAG
          GOTO      PROCRP16 IF NOT EQUAL
.
.         We are printing credit notes for cancelled admissions,where no 
.         record in visit file
.
          PACK        KEY8,PMCNVISN,SP8
          CALL        RDVISA1
          BRANCH      OVRCD,PROCRP18   * must be cancelled admission
          CALL        RDPMVX11
          BRANCH      OVRCD,PROCRP18
          GOTO        PROCRP10
.
PROCRP16  PACK        KEY8,PMCNVISN,SP8
          CALL        RDVISA1
          BRANCH      OVRCD,PROCRP10
          CALL        RDPMVX11
          BRANCH      OVRCD,PROCRP10
.
          IF          IBCNMHOS=1
            MATCH       SP10,PTHSHOSP
            IF          !@EQUAL
              MATCH       PMVXMHOS,PTHSHOSP
              GOTO        PROCRP10 IF NOT EQUAL
            ENDIF
          ENDIF
.
          BRANCH    FORM2,PROCRP18,PROCRP18,PROCRP18
          GOTO      PROCRP10
.
PROCRP18  MOVE      SP70,SRNDFLG
          MOVE      PMCNINVN,KEY8
          CALL      RDINV1
          IF        OVRCD=0 & PTINROUN<>0
            MOVE      "*",SRNDFLG
            ADD       PTINROUN,RNDAMNT
          ENDIF
.
          BRANCH    FORM2,PROCRP20:       * Emergency
                          PROCRP30:       * Outpatient
                          PROCRP40        * Inpatient
          GOTO      PROCRP10
.
PROCRP20  ADD       ONE,NOTESEMR
          ADD       PMCNAMNT,CRDEMR
          SUB       PMCNGSTM,CRDEMR
          ADD       PMCNGSTM,GSTEMR
          ADD       PMCNAMNT,TOTALEMR
          IF        PTINROUN<>0
            MOVE      "*",ERNDFLG
          ENDIF
          GOTO      PROCRP50
.
PROCRP30  ADD       ONE,NOTESOUT
          ADD       PMCNAMNT,CRDOUT
          SUB       PMCNGSTM,CRDOUT
          ADD       PMCNGSTM,GSTOUT
          ADD       PMCNAMNT,TOTALOUT
          IF        PTINROUN<>0
            MOVE      "*",ORNDFLG
          ENDIF
          GOTO      PROCRP50
.
PROCRP40  ADD       ONE,NOTESINP
          ADD       PMCNAMNT,CRDINP
          SUB       PMCNGSTM,CRDINP
          ADD       PMCNGSTM,GSTINP
          ADD       PMCNAMNT,TOTALINP
          IF        PTINROUN<>0
            MOVE      "*",IRNDFLG
          ENDIF
.
PROCRP50  UNPACK    PMCNDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DIM10B,CPCDATE
.
          MOVE      "Unknown",PNAME
          MOVE      SP10,PVIURNO
          PACK      KEY8,PMCNVISN
          CALL      RDVISA1
          IF        OVRCD=1
            PACK      KEY8,PINVUR,SP70
          ELSE
            PACK      KEY8,PVIURNO,SP70
          ENDIF
          CALL      RDMAST1
          MOVE      PGNAME,KEY1
          PACK      PNAME,KEY1,DOT,PSNAME,SP70
.
          MOVE      "UNK",CNSYSDSC
          MOVE      ZERO,F2
          MOVE      PMCNSYST,F2
          LOAD      CNSYSDSC,F2,ABBREMR,ABBROUT,ABBRINP
.
          MOVE      SP10,CNSTDESC
          MOVE      ZERO,F1
          MOVE      PMCNSTAT,F1
          LOAD      CNSTDESC,F1,CNSTFULL,CNSTITEM
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.         Print data for report
.
          MOVE      ZERO,FORM10P2
.
          MOVE      PMCNAMNT,FORM10P2
          SUB       PMCNGSTM,FORM10P2
          PRINT     *1,DIM10B,*12,PMCNCRNO,*22,PMCNVISN,*32,PMCNINVN:
                    *41,PVIURNO,*50,PNAME,*71,CNSYSDSC,*75,CNSTDESC:
                    *90,FORM10P2;
.
          MOVE      PMCNGSTM,FORM10P2
          PRINT     *104,FORM10P2;
.
          IF        CFEETYP=5
            MOVE      PMCNAMNT,FORM11P2
            PRINT     *118,FORM11P2,SRNDFLG
          ELSE
            PRINT     *118,PMCNAMNT
          ENDIF
.
          ADD       ONE,CLNO
          GOTO      PROCRP10
.
.         Print end of report
.
PROCRP60 
.
          COMPARE   ZERO,CPAGENO
          GOTO      PROCRP84 IF EQUAL
.
          CALL      PRTST000       * Print System Total
          GOTO      PROCRP90
.
.         Print Final Total only if Consolidated/All Report
.
.>>>>     IF        FSTSYS<>FEDSYS
.>>>>       BRANCH    OPTION,PROCRP82
.>>>>     ENDIF
.>>>>     GOTO      PROCRP90
.
PROCRP84  BRANCH    LOOPFLAG,PROCRP90
          DISPLAY   *P1:24,*EL,*P10:24,*B,*V2LON,"No Patient Selected"
          GOTO      PROCRP90
.
PROCRP90  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Report Generation Completed **"
.
PROCRP99  RETURN
+
.*********************************************************************
.      Display System Totals
.*********************************************************************
PRTST000  COMPARE   ONE,LOOPFLAG
          GOTO      PRTST100 IF NOT EQUAL
.
          COMPARE   ZERO,NOTESINP
          GOTO      PRTST900 IF EQUAL
.
          PRINT     *N,*1,"Number of Cancelled Inpatient Credit Notes  : ":
                    NOTESINP:
                    *80," Total : ",*89,"$",CRDINP,*104,GSTINP;
          GOTO      PRTST900
.
PRTST100  PRINT     *N,*1,"Number of Inpatient Credit Notes  : ",NOTESINP:
                      *80," Total : ",*89,"$",CRDINP,*104,GSTINP;
.
          IF        CFEETYP=5
            MOVE      TOTALINP,FORM11P2
            PRINT     *118,FORM11P2,IRNDFLG
          ELSE
            PRINT     *118,TOTALINP
          ENDIF
          BRANCH    LOOPFLAG,PRTST900
.
          PRINT     *1,"Number of Outpatient Credit Notes : ",NOTESOUT:
                    *80," Total : ",*89,"$",CRDOUT,*104,GSTOUT;
.
          IF        CFEETYP=5
            MOVE      TOTALOUT,FORM11P2
            PRINT     *118,FORM11P2,ORNDFLG
          ELSE
            PRINT     *118,TOTALOUT
          ENDIF
.
          PRINT     *1,"Number of Emergency Credit Notes  : ",NOTESEMR:
                    *80," Total : ",*89,"$",CRDEMR,*104,GSTEMR;
.
          IF        CFEETYP=5
            MOVE      TOTALEMR,FORM11P2
            PRINT     *118,FORM11P2,ERNDFLG
          ELSE
            PRINT     *118,TOTALEMR
          ENDIF
.
          ADD       THREE,CLNO
          ADD       CRDINP,GRANDCRD
          ADD       CRDOUT,GRANDCRD
          ADD       CRDEMR,GRANDCRD
.
          ADD       GSTINP,GRANDGST
          ADD       GSTEMR,GRANDGST
          ADD       GSTOUT,GRANDGST
.
          ADD       TOTALINP,GRANDTOT
          ADD       TOTALEMR,GRANDTOT
          ADD       TOTALOUT,GRANDTOT
.
          PRINT     *N,*61,"Total of All Credit Notes : ":
                    *89,"$",GRANDCRD:
                    *104,GRANDGST;
          IF        CFEETYP=5
            MOVE      GRANDTOT,FORM11P2
            MOVE      SP70,KEY1
            IF        RNDAMNT<>0
              MOVE      "*",KEY1
            ENDIF
            PRINT     *118,FORM11P2,KEY1,*N:
                      *N," * Denotes Invoices Changed by Rounding Total; ":
                         RNDAMNT
          ELSE
            PRINT     *118,GRANDTOT,*N;
          ENDIF
          GOTO      PRTST999
.
PRTST900  PRINT     *N,*N,"  ***  END OF REPORT  ***"
.
PRTST999  RETURN
+
.*********************************************************************
.                 HEADINGS FOR OUTPUT
.*********************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital   : ",PTHSNAME
            MOVE      TEN,CLNO
          ELSE
            MOVE      NINE,CLNO
          ENDIF
.
          PRINT     *40,"Date Range : ",DSPSTDTE," To ",DSPEDDTE:
                    *N,*40,"Report     : ",DESCSYST:
                    *N;
.
          IF        LOOPFLAG=1
            PRINT     *40,"Invoices for Cancelled Admissions ";
            IF        IBCNMHOS=1
              PRINT     "From All Hospitals";
            ENDIF
            PRINT     *N
            ADD       TWO,CLNO
          ENDIF
.
          PRINT     *N,*1,"Cred Note",*12,"Cred Note",*22,"Admission":
                    *33,"Invoice",*44,"U/R",*77,"Credit Note":
                    *N,*3,"Date",*14,"Number",*24,"Number",*34,"Number":
                    *43,"Number",*50,"Patient Name",*71,"Sys",*79,"Status":
                    *98,"Amount",*114,"GST",*127,"Total":
                    *N,*1,"----------",*12,"---------",*22,"---------":
                    *32,"--------",*41,"--------",*50,"--------------------":
                    *71,"---",*75,"--------------",*90,"-------------":
                    *104,"-------------",*118,"---------------"
.
HEAD9999  RETURN
+
.
          INC       STD001IO
          INC       COMCODKY
          INC       PATHSPKY
.
          INC       COMCODIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATHSPIO/INC
          INC       PATINVIO/INC
          INC       PATVISIO/INC
          INC       PMSCNOIO/INC
          INC       PMSVX1IO/INC
.

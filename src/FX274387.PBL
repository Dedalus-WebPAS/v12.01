.*****************************************************************************
.*    Program      : FX274387                                                *
.*    Description  : Write triggers for discharges where cat A = 0 or 5      *
.*                                                                           *
.*    Author       : Davin Sloan  CAR 274387                                 *
.*    Date         : 10/10/2012                                              *
.*    Modifications:                                                         *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PMSWTRFD/INC
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       100
.
DEFPATH   DIM       60
DIM60     DIM       60
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "Write HMDS Triggers"
PRGID     INIT      "FX274387"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN7000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN7000  CALL      OFIX0000                      * run fix
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSWTRA1,"pmswtraf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Write triggers for client status"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                            OFIX0000                                       *
.*            fix to populate pmswtraf for discharges with cat A = 5 or 0    *
.*****************************************************************************
.
OFIX0000  DISPLAY   *P1:20,*EL,"Record No. :";
.
          PACK      KEY9,THREE,SP70
          CALL      RDSMISS2                      * position at start of file
OFIX0500  CALL      RDKMISS2                      * read next record
          BRANCH    OVRCD,OFIX9000                * eof - finished
.
          COMPARE   THREE,ASTAT
          GOTO      OFIX9000 IF NOT EQUAL         * only want discharged
.
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,OFIX0500
.
          MATCH     "0",THCSCOD
          IF        !@EQUAL
            MATCH     "5",THCSCOD
            GOTO    OFIX0500 IF NOT EQUAL         * want cat.A = 0 or 5
          ENDIF
.
          MATCH     SP70,PMVXMHOS
          GOTO      OFIX0500 IF EQUAL             * must have hospital
.
          PACK      KEY19,PMVXMHOS,AADMNO,SP70
          CALL      RAPMWTR1
          IF        OVRCD=1
            PACK      PMWTHOSP,PMVXMHOS,SP70
            PACK      PMWTVISN,AADMNO,SP70
            PACK      PMWTSDAT,SP70
            PACK      PMWTMDAT,SP70
            CALL      IBACLOCK                    * Get the current date
            PACK      PMWTCDAT,CCC,CYY,CMM,CDD
            REP       " 0",PMWTCDAT
            MOVE      CTIMEIS,PMWTCTIM
            MOVE      "FX274387  ",PMWTWEBC
            PACK      PMWTSPAR,SP70,SP70
.
            PACK      KEY19,PMWTHOSP,PMWTVISN,PMWTSDAT,SP70
            CALL      WRPMWTR1
            ADD       ONE,RECNUM                    * increment record counter
          ENDIF
.         
          IF        (RECNUM%1000) = 0 | (RECNUM%1000) = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      OFIX0500                       * get next record
.         
OFIX9000  DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,"Update completed.  ";
          CALL      HITENTER
.
OFIX9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PMSWTRIO/INC

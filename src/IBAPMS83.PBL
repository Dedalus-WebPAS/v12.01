.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAPMS83                                             *
.*                                                                            *
.*     FUNCTION:         HEALTH FUND WARD INCOME REPORT BY RANGE OF WARDS     *
.*                                                                            *
.*     DATE WRITTEN:     09/01/2002                                           *
.*                                                                            *
.*     AUTHOR:           J.TAN                                                *
.*                                                                            *
.*     MODIFICATIONS:    Copied from IBABIL83 - added range of wards          *
.*                                                                            *
.*        V12.00.01 28/05/2025  Bella Turco     TSK 0955096                   *
.*                  Alphanumeric changes                                      *
.*        V11.03.01 10/08/2023  J.Tan           TSK 0936142                   *
.*                  Fixed the first index of tempfile                         *
.*        V11.01.01 13/04/2021  J.Tan           TSK 0863287                   *
.*                  Recompiled for STEPDOWN -Patient Invoice new functionality*
.*        V11.00.01 11/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.10.01 13/06/2017  J.Tan          TSK 0839991                    *
.*                  Recompiled for PATDBTYP (PATCMSTP)                        *
.*        V10.05.01 19/01/2015  Ania P         CAR 261630                     *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.04.01 27/05/2014  J.Tan          CAR 300784                     *
.*                  Recompiled for STEPDOWN - force stepdown prior inv.fromdat*
.*        V10.03.02 06/05/2013  J.Tan          CAR 284902                     *
.*                  Recompiled for STEPDOWN - force stepdown prior Inv.Fromdat*
.*        V10.03.01 05/09/2012  J.Tan          CAR 267784                     *
.*                  Mods checking for TCINDC19 Claim Code                     *
.*        V10.02.03 14/06/2011  Saroeun Soeur CAR 243759                      *
.*                  opened "patfn1af"                                         *
.*        V10.02.02 27/04/2011  Saroeun Soeur CAR 241405                      *
.*                  fixed non multi hospital                                  *
.*        V10.02.01 15/04/2011  Saroeun Soeur CAR 241405                      *
.*                  added hospital filter for multi hospital                  *
.*        V10.01.02 24/02/2011  Peter Vela    CAR 233034                      *
.*                  Removed PATBF2FD and PATBF2IO                             *
.*        V10.01.01 28/01/2011  J.Tan         CAR 233050                      *
.*                  Mods to Casemix funding                                   *
.*        V10.00.03 26/08/2010  J.Tan         CAR 226777 (HEA)                *
.*                  Mods NOT to print Prov.Casemix code if PTMICMXP is 'No'   *
.*        V10.00.02 20/07/2010  J.Tan         CAR 226158                      *
.*                  Fixed stepdown after bed fees update                      *
.*        V10.00.01 14/07/2010  J.Tan         CAR 226158                      *
.*                  Recompiled for STEPDOWN - bed fees update                 *
.*        V9.12.06  11/03/2010  J.Tan         CAR 216044                      *
.*                  Mods to include Leave days in stepdown count for TAC      *
.*        V9.12.05  19/01/2010  J.Tan         CAR 166439                      *
.*                  Recompiled for PATCMSTP - C/P additional charge stepdown  *
.*        V9.12.04  21/12/2009  J.Tan         CAR 203578                      *
.*                  Mods to use Prov.Casemix instead of Prov.DRG for C/P      *
.*        V9.12.03  24/11/2009  Davin         CAR 210721                      *
.*                  Recompiled for CHKCMXPT - I*C on patcmxaf file            *
.*        V9.12.02  19/11/2009  J.Tan         CAR 210536                      *
.*                  Append CMCODE with spaces                                 *
.*        V9.12.01  07/09/2009  J.Tan         CAR 205303                      *
.*                  Added Contract ID to casemix files                        *
.*        V9.11.02  08/01/2009  Steve Armstrong  CAR 185927                   *
.*                  Removed isadd on temp file and included extra index in    *
.*                  isbuild.                                                  *
.*        V9.11.01  06/10/2008  J.Tan            CAR 180188                   *
.*                  Mods to set up CASMXKEY                                   *
.******************************************************************************
.*        V9.08.00  28/02/2007  Mike Laming      CAR 131208                   *
.*                  Ported to V9.08                                           *
.******************************************************************************
.*        V6.14.03  13/02/2007  Mike Laming      CAR 132798                   *
.*                  Recompiled for PATCMSTP - fix NOFEE from ADIP0000         *
.*        V6.14.02  30/10/2006  Steve Armstrong  CAR 120088                   *
.*                  Added low outlier to report                               *
.*        V6.14.01  09/06/2006  Peter Vela       CAR 103745                   *
.*                  Recompiled for PATCMSTP                                   *
.******************************************************************************
.*        V6.12.02  26/11/2004  Mike Laming   CAR 54534                       *
.*                  Re-comple for PATCMSTP - fix Addit.Charge Description     *
.*        V6.12.01  26/10/2004  Mike Laming   CAR 48340                       *
.*                  Re-compile for STEPDOWN - fix Auto-stepdown start date    *
.******************************************************************************
.*        V6.11.04  26/03/2004  Mike Laming   CAR 47732                       *
.*                  Re-compile for PATCMSTP - fix Casemix Stepdown            *
.*        V6.11.03  19/09/2003  CAR 43487   Guomin Fu                         *
.*                  Recompiled for PATCATBI                                   *
.*        V6.11.02  01/09/2003  Guomin CAR 42590 42962                        *
.*                  Recompiled for CMXMATRX                                   *
.*        V6.11.01  07/07/2003  Steve Armstrong   CAR 21882                   *
.*                  Mods for Quote 10522                                      *
.*        V6.11.B01 24/04/2003  Guomin CAR 38369                              *
.*                  Recompiled for CMXMATRX                                   *
.******************************************************************************
.*        V6.10.13  06/01/2003  Henry Son SRF 35029                           *
.*                              Re-compile for SAR G/L Accruals               *
.*                              Include PATDINVS                              *
.*        V6.10.12  27/11/2002  Madhuri  SRF 33707                            *
.*                  Recompiled for CMXMATRX                                   *
.*        V6.10.11  23/10/2002  Sunil Kumar  SRF 23084                        *
.*                  Recompiled for PATCATBI - Fixed I*C error                 *
.*        V6.10.10  19/09/2002  J.Tan                                         *
.*                  Recompiled for PATCMSTP - casemix funding                 *
.*        V6.10.09  12/09/2002  Guomin Fu     SRF 22099                       *
.*                  Fixed bugs 1) Optimal LOS incorrect - if actual LOS is    *
.*                  less the optimal,Optimal LOS should display instead of    *
.*                  actual LOS                                                *
.*                  2) "As At" report does not show admissions and transfers  *
.*                  done that day.                                            *
.*                  3)  Sort by stepdown/outlier option does not work -       *
.*                  sorts by ward/bed-stepdown instead of ward-stepdown       *
.*        V6.10.08  30/08/2002  J.Tan                                         *
.*                  Recompiled for PATCATBI - casemix funding                 *
.*        V6.10.07  30/08/2002  Mike Laming   SRF 17373                       *
.*                  Correct check for new page (REPT1400) so that a change of *
.*                  ward causes a Ward header to be printed                   *
.*        V6.10.06  19/08/2002  Guomin Fu     SRF 17469                       *
.*                  Recompiled for PATDINVS - Order of printing               *
.*                  invoices                                                  *
.*        V6.10.05  09/08/2002  J.Tan                                         *
.*                  - Recompiled for STEPDOWN - Changes for Specialty bed     *
.*        V6.10.04  16/07/2002  Greg Horvat      SRF 17570 Rebound            *
.*                  - Recompiled for STEPDOWN - Changed to use the highest day*
.*                    in period rate (TRBEND) from the transfer file if a     *
.*                    manual stepdown                                         *
.*                  - Recompiled for STEPDOWN - Changed to set the manual     *
.*                    stepdown flag (PTTREPSD) to yes when writing automatic  *
.*                    stepdowns & the flag write manual stepdown (WRMNSTDN) is*
.*                    yes                                                     *
.*        V6.10.03  15/07/2002  Mike Laming      SRF 17570 Rebound            *
.*                  Recompiled for PATCMSTP - Changes to PTTREPSD & ADIP0000  *
.*        V6.10.02  14/07/2002  Greg Horvat      SRF 17570 Rebound            *
.*                  Recompiled for PATCMSTP - Recorrected the test of PTTREPSD*
.*                  in the previous version change & removed the record       *
.*                  repositioning on pattranf that was put in in the previous *
.*                  version change                                            *
.*        V6.10.01  12/07/2002 Mike Laming  SRF 19094                         *
.*                  Recompile for STEPDOWN                                    *
.*       V6.09.06  30/05/2002  Raghunandan Surve,HPS  SRF17373                *
.*                 Fixed claim code, diagnosis printing problems              *
.*                 For OPTION=2, sort by ward, then bed and then Days Left    *
.*       V6.09.05  14/03/2001  J.Tan                                          *
.*                 Mods to days left to outlier                               *
.*       V6.09.04  13/03/2001  Mike Laming  Mayne Request                     *
.*                 Restore New Page question                                  *
.*       V6.09.03  12/03/2002  Steve Downing  Mayne Defect No 24              *
.*                 Fixed Start/Finish ward description in report              *
.*       V6.09.02  01/03/2001  Mike Laming  Mayne Request                     *
.*                 Report layout changes as requested                         *
.*       V6.09.01  Tonii  billing mods                                        *
.*                 Changed program name to be "Daily Patient LOS Action"      *
.*                 Changed layout of report, printing comments, WARD on       *
.*                 separate lines                                             *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATDINVS/INC
          INC       PATASDFD/INC
          INC       PATCFAFD/INC
          INC       PATHDFFD/INC
          INC       PATHLFFD/INC
          INC       PATLSDFD/INC
          INC       PATLDFFD/INC
          INC       PATAFEFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATBFEFD/INC
          INC       PATCMCFD/INC
          INC       PATCODFD/INC
          INC       PMSCIDFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATDTRFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       HL7BMTFD/INC
          INC       PATMI1FD/INC
          INC       PATRCAUD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       PATCMXFD/INC
          INC       PATITMFD/INC
          INC       PMSVX1FD/INC
          INC       NZPBFEFD/INC
          INC       WEBSECFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
. ******  VARIABLES DEFINITIONS  ******
.
ADNAME    DIM       20
AVDC      FORM      6.2
AVPAT     FORM      6.2
ASDATE    DIM       8
ASTER     INIT      "*"
.
CAUDQ     FORM      1
CALDAYS   FORM      4
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       80
CRDATE    DIM       8
CRDRTYP   FORM      1
CHOSTYP   FORM      1
CSTDOWN   FORM      1
CURRDATE  DIM       8
CDLSTEP   FORM      1
.
DAYRATE   FORM      6.2
DAYCASE   FORM      1
DAYLEFT   FORM      4
DCNUM     FORM      6
DCRATE    FORM      6.2
.
DIM4      DIM       4
DIM9      DIM       9
DIM14     DIM       14
DIM18     DIM       18
DIM18A    DIM       18
DIM30     DIM       30
.
FORM2A    FORM      2
FORM3A    FORM      3
FNAMEI    DIM       8
FNAMER    DIM       8
FRSTFOOT  DIM       1
IBCNMHOS  FORM       1
JYEAR     FORM      2
.
LOUTLIER  DIM       5
LSTDATE   DIM       8
MAXLINES  FORM      4
NOBF      FORM      1
NUMDAY    FORM      4
.
ODATE     DIM       8
.
PTCNUCMX  DIM       1
PATNUM    FORM      6
PATNAM    DIM       14
PTCNADES  FORM      1
PTCNIDES  FORM      1
PTCNCNDY  FORM      1
PTCNXCHS  DIM       1
PTCNPPIN  DIM       1
.
PSTSRT    FILE      ASCII, FIXED=256
PTRESDAY  FORM      1
PNAM1     DIM       1
PNAM2     DIM       1
PHEADFL   FORM      1
PSKIP     FORM      1
.
PTCNDCLM  DIM       3
PTCNCLEV  DIM       3
.
DSTDOWN   FORM      3
SOUTLIER  FORM      4
SRBDAY    DIM       4
SRYEAR    FORM      4
SEC       FORM      2
STATUS    FORM      1
SAVDISC   FORM      6.2
SAVALLW   FORM      6.2
SAVBEND   FORM      3
.
TDAYNO    FORM      4
TRDATE    DIM       8
.
UNIQUE    FORM      8
.
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WSTAT     FORM      1
WARDFROM  DIM       3
WARDTO    DIM       3
WARDFPRT  DIM       5
WARDTPRT  DIM       6
.
XCBEND    FORM      4
XBED      DIM       3
XDATE1    DIM       8                       * Yesterday date DDMMYY
XMON2     DIM       2
XDAY2     DIM       2
XWARD     DIM       3
XYEAR2    DIM       2
XCENT2    DIM       2
YTDATE    DIM       8
HOSNAME   DIM       50        * hospital name - multi hospital
HOSCODE   DIM       3         * hospital code - multi hospital
.
.                                           * varibles added for V9.08
CFEETYP   FORM      1        212       Type of inpatient daily charge
EFTDATE   DIM       8                       * Effective date (ccyymmdd)
. ******  Temporary index file for report  ******
.
TEMP1     IFILE     SQL, FIXED=223, KEY="u1-3,4-6,7-20,21-28"
TEMP2     IFILE     SQL, FIXED=223, KEY="u1-3,108-111,4-6,21-28"
.
.Name     Type      Size    Physical    Start
.----     ----      ----    --------    -----
.XWARD    DIM        3         3          1
.XBED     DIM        3         3          4
.PATNAM   DIM        14        14         7
XUNIQUE   DIM        8         8         21
.PGNAME   DIM        25        25        29
.PSEX     DIM        1         1         54
.PSAGE    FORM       3         3         55
.ADOCTA   DIM        6         6         58
.AFUNDH   DIM        6         6         64
.AFNDTB   DIM        8         8         70
.ADATE    DIM        8         8         78
.ODATE    DIM        8         8         86
.ACLAIM   DIM        3         3         94
.ATYPE    DIM        3         3         97
.NUMDAY   FORM       4         3        100
.DAYRATE  FORM       6.2       5        103
SDAYLEFT  DIM        4         4        108
.DAYCASE  FORM       1         2        112
.NOBF     FORM       1         2        114
.TDAYNO   FORM       4         3        116
.STRBTYP  DIM        3         3        119
XAADMNO   DIM        8         8        122
XCOLNUM   FORM       3         3        130
.PTMIPDRG DIM        9         9        133
.PTCMXLOS FORM       4         3        142
.PRICMBS  DIM        9         9        145
PTCASMX   DIM        1         1        154
SDSTDOWN  DIM        4         4        155
SFNDDAY   DIM        3         3        159
SUFNDAY   DIM        3         3        162
.ADIAG1   DIM        50        50       165
.SOUTLIER FORM       4         3        215
.LOUTLIER DIM        5         5        218
.
.End of Record                          223
.
SWARD    DIM        3
UFNDAY   FORM       3
FNDDAY   FORM       3
.
. ******  CONSTANTS  ******
.
ASK1      INIT      "*"
ASK2      INIT      "**"
ASK3      INIT      "***"
ASK4      INIT      "****"
CODEA     INIT      "A "
CODEBT    INIT      "BT"
OPNFLAG   FORM      "1"
.
PRGID     INIT      "IBAPMS83"
PRGNAM    INIT      "DAILY PATIENT LOS ACTION REPORT"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.         Display screen header and open files
.******************************************************************************
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patbfeef";
          OPEN      PATBFEE1,"patbfeef"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"patcmxaf";
          OPEN      PATCMXA1,"patcmxaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
.
          READ      CONTROLF,TEN;*217,CAUDQ
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TEN5;*249,CRDRTYP,*247,CSTDOWN
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,TWENTY;*164,PTRESDAY,*211,PTCNCNDY
          READ      CONTROLF,FIFTY9;*1,CDLSTEP
          READ      CONTROLF,EIGHTY8;*233,PTCNADES
          READ      CONTROLF,HUND16;*218,PTCNUCMX
          READ      CONTROLF,HUND29;*89,PTCNPPIN
.
          PACK      CURRDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
.
.******************************************************************************
.        START OF PROGRAM
.******************************************************************************
START     MOVE      ZERO,PHEADFL 
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Ward/Bed Sequence":
                    *P1:6,*V2LON,TWO,*HOFF:
                          " = Days Left to Stepdown/Outlier Sequence":
                    *P1:8,"Select Option :";
.
START1    KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF START2,START2
          DISPLAY   *P35:8,*EL,*B,*V2LON,"** Invalid Option **",*W2;
          GOTO      START1
.
START2    GOTO      KWARD
.
.******************************************************************************
.            Keyin a ward
.******************************************************************************
KWARD     DISPLAY   *P1:10,*EF,"Enter Ward  From :":
                        *P1:11,"            To   : ";
          MOVE      "22",CCOL
          MOVE      "10",CVERT
          CALL      KWRD0000            * Enter ward from
          BRANCH    EXIT,START
.
          MOVE      KEY3,DIM6
          MATCH     SP3,KEY3
          IF        @EQUAL
            MOVE      "Start",DIM5
            DISPLAY   *P22:10,"Start";
          ENDIF
          MOVE      KEY3,WARDFROM
.
KWARD1    MOVE      "11",CVERT
          CALL      KWRD0000            * Enter ward to
          BRANCH    EXIT,KWARD
.
          MOVE      KEY3,DIM6
          MATCH     SP3,KEY3
          IF        @EQUAL
            MOVE      "Finish",DIM6
            DISPLAY   *P22:11,"Finish";
            MOVE      "zzz",KEY3
          ENDIF
          MOVE      KEY3,WARDTO
.
          MATCH     WARDFROM,WARDTO
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,*V2LON:
                      "Ward To must be greater than Ward From. ";
            GOTO      KWARD1
          ENDIF
.
           MOVE      ZERO,PSKIP
           IF        OPTION=1
.                                          * Skip to new page on change of ward
.                                          * (Option 1 only)
             CALL      KSPW0000            * Ask if a separate for each ward
           ENDIF
.
          CALL      SETYDAY                * Set def Date/call KEYDATE
          CALL      KHOS0000
          BRANCH    EXIT,KWARD
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,STPROC,KWARD,START
.
.
.******************************************************************************
.            PROCESS START
.******************************************************************************
STPROC    CALL      INDZD
          CLOCK     TIME,CTIMEIS
.
          MOVE      ONE,UNIQUE
          MOVE      SP8,DDATE
          MOVE      ZERO,DAYCASE
          MOVE      ZERO,CPAGENO
          MOVE      " ",ANS
.
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Processing Current Admissions **",*HOFF:
                    *P1:24,*EL,*P10:24,"Current Admission No :":
                               *P50:24,"Scanning :";
          MOVE      "2",WSTAT
          CALL      ADMFN
.
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Processing On-Leave Admissions **",*HOFF:
                    *P1:24,*EL,*P10:24,"On-leave Admission No :":
                               *P50:24,"Scanning :";
          MOVE      "4",WSTAT
          CALL      ADMFN
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "** Processing Discharge Admissions **",*HOFF:
                    *P1:24,*EL,*P10:24,"Discharged Admission No :":
                               *P50:24,"Scanning :";
.
          MOVE      "3",WSTAT
          MOVE      "000000" TO DDATE
.
.         Get the patients discharged from yesterday
.
          PACK      KEY16 WITH YTDATE,SP8
          CALL      RDSDSCH2
NXTDSH    CALL      RDKDSCH2
          BRANCH    OVRCD OF REPT0000
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,DADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      NXTDSH IF NOT EQUAL
              ELSE
                GOTO      NXTDSH
              ENDIF
            ENDIF
          ENDIF
.
          DISPLAY   *P62:24,DADMNO;
          MOVE      ZERO,DAYCASE
.
.         Check for the Admission Date
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF NXTDSH
.
          MATCH     DDATE,YTDATE
          GOTO      NXTDS1 IF NOT EQUAL
.
          MATCH     ADATE,YTDATE            * patient was discharged yesterday
          GOTO      NXTDSH IF NOT EQUAL
.
          MOVE      ONE,DAYCASE             * It is a daycase - Patient
          GOTO      NXTDS2                    discharged & admitted yesterday
.
.         Not discharged yesterday, so it must be today
.
NXTDS1    UNPACK    ADATE WITH XCENT,XYEAR,XMON,XDAY
          PACK      XDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",XDATE
.
          MATCH     YTDATE,XDATE            * Check if the patient was admitted
          GOTO      NXTDS2 IF LESS          * before today
          GOTO      NXTDS2 IF EQUAL         * before today
          GOTO      NXTDSH                  * before today
.
NXTDS2    CALL      TRANS
          GOTO      NXTDSH
+
.******************************************************************************
.             READ ADMISSIONS FILE
.******************************************************************************
ADMFN     PACK      KEY9 WITH WSTAT,SP8
          CALL      RDSMISS2
.
NEXT5     CALL      RDKMISS2
          COMPARE   ONE,OVRCD
          RETURN    IF EQUAL
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,AADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      NEXT5 IF NOT EQUAL
              ELSE
                GOTO      NEXT5 
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   WSTAT,ASTAT
          RETURN    IF NOT EQUAL
.
          DISPLAY   *P62:24,AADMNO;
          UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      WDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",WDATE
.
          MATCH     YTDATE,WDATE
          GOTO      NEXT6 IF EQUAL
          GOTO      NEXT6 IF LESS
          GOTO      NEXT5                         * if its more than 
.         GOTO      NEXT5 IF NOT LESS             * it loops back
.
NEXT6     MOVE      ZERO,DAYCASE
          CALL      TRANS  
          GOTO      NEXT5
+
.******************************************************************************
.            Subroutine to process the DTR and Transfer files for the patient
.******************************************************************************
TRANS     MOVE      "00000000",ODATE
          CALL      IVAR0000
.
          PACK      KEY22 WITH AADMNO,SP20
          CALL      RDSDTRN1
.
NXTTRN    CALL      RDKDTRN1
          BRANCH    OVRCD OF NXT10
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,TADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      NXTTRN IF NOT EQUAL
              ELSE
                GOTO      NXTTRN
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     AADMNO,TADMNO
          GOTO      NXT10 IF NOT EQUAL
.
          COMPARE   TWO,TRECTYPE
          GOTO      NXTTRN IF NOT EQUAL
.
          PACK      TRDATE WITH TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",TRDATE
.
          MATCH     YTDATE,TRDATE
          GOTO      NXT09 IF EQUAL
          GOTO      NXT09 IF LESS
          GOTO      NXTTRN                        * if its more than 
.          GOTO      NXTTRN IF NOT LESS
.
NXT09     MATCH     ODATE,TRDATE
          GOTO      NXTTRN IF LESS
.
          MOVE      TRDATE,ODATE            * Save the latest date of theatre
          REP       " 0",ODATE
          GOTO      NXTTRN
.
NXT10     DISPLAY   *P37:24,*V2LON,AADMNO;
.
.         Routine to get the rate
.
          MOVE      SP10,SIDATET
          MOVE      ZERO,ENDFLAG
          MOVE      SP10,STATYPE
          MOVE      SP10,FROMDATE
          MOVE      ONE,PROGFLAG
          MOVE      SP3,STWARD
          MOVE      SP3,STBED
          MOVE      ZERO,XCBEND
          MOVE      ZERO,XCOLNUM            * Column of current stepdown
          MOVE      ZERO,COLNUM
          MOVE      ZERO,PTCMXLOS
          MOVE      SP70,CASMXKEY
          MOVE      SP70,PTCASMX
          MOVE      ZERO,SOUTLIER         * Start of outlier period
          MOVE      ZERO,CMXFLAG
.
          MATCH     ANSY,PTMICMXP
          GOTO      NXT12 IF EQUAL
.
          MOVE      SP70,PTMIPCMX               * No prov.casemix
          MOVE      SP70,PTMIPDRG
          MOVE      SP70,CMCODE
          GOTO      NXT20
.
NXT12     MATCH     "1",PTCNUCMX
          IF        @EQUAL
            MOVE      PTMIPCMX,CMCODE
            MOVE      PTMIPCMX,PTMIPDRG
          ELSE
            PACK      CMCODE,PTMIPDRG,SP70
          ENDIF
.
          PACK      KEY9,ACLAIM,AFUNDH
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,NXT20
.
          PACK      PTMIPCMX,CMCODE,SP70
          PACK      KEY18,ACLAIM,AFUNDH,CMCODE,SP70
          MOVE      ADATE,CEFFDATE
          LOAD      CEFFDATE,CNTRCEFR,ADATE,DDATE
.
.         If Contract Effective From is 'For Discharges From', Discharged date
.         is blank and TCINDC19=D, default Effective date to Current date
.
          IF        CNTRCEFR=2
            PACK      DDATE,DDATE,SP70
            MATCH     SP70,DDATE
            IF        @EQUAL
              PACK      KEY5,ANSC,ANSL,ACLAIM
              CALL      RDCODE1
              IF        OVRCD=0
                MATCH     "D",TCINDC19
                IF        @EQUAL
                  PACK      CEFFDATE,CCC,CYY,CMM,CDD,SP70
                  REP       " 0",CEFFDATE
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          CALL      VALCMXFN              * get the contract ID (CONTRCID)
          BRANCH    EXIT,NXT20
.
          MOVE      ONE,CMXFLAG
          PACK      KEY9,CMCODE,SP70
          CALL      RDPTCMC1
          IF        OVRCD=1
            MOVE      ZERO,PTCAELOS          * initialise Optimal LOS
          ENDIF
.
          CALL      GTAB0000                 * get health fund table
          PACK      CMDRG,PTMIPDRG,SP20      * Default to provisional DRG code
.
          CALL      GLOW0000                 * get cut off for low outlier
          CALL      GOUT0000               * generate start of outliers
.
NXT20     PACK      KEY30 WITH AADMNO,SP20,SP10
          CALL      RDSTRAN2
NXTTRAN   CALL      RDKTRAN2
          BRANCH    OVRCD OF TRAN40
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,TADMN,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      NXTTRAN IF NOT EQUAL
              ELSE
                GOTO      NXTTRAN
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     TADMN,AADMNO
          GOTO      TRAN40 IF NOT EQUAL
.
          MOVE      TDATE,XDATE1
          MOVE      TDATE,LSTDATE
          MOVE      TMOVE,STMOVE
.
.         Get yesterday ward/bed
.
          MATCH     YTDATE,TDATE
          GOTO      TRAN04 IF LESS
          GOTO      TRAN04 IF EQUAL   
          GOTO      TRAN05             
TRAN04    MOVE      TWARD,STWARD
          MOVE      TBED,STBED
.
TRAN05    COMPARE   ONE,CMXFLAG
          GOTO      TRAN05A IF NOT EQUAL 
.
          CMATCH    ANSA,TMOVE
          IF        @EQUAL
            MOVE      ZERO,ACDAYCS      * initialise daycase flag
            MATCH     ADATE,DDATE
            IF        @EQUAL
              MOVE      ONE,ACDAYCS     * set one to daycase flag
            ENDIF
          ENDIF

          IF        ASTAT=3      
            MATCH     ADATE,DDATE
            GOTO      TRAN10 IF EQUAL   * sameday, no stepdown
          ENDIF 

          CALL      PATCMSTP
          GOTO      TRAN10
.
.         Not a casemix funding patient
.
TRAN05A   BRANCH    CHOSTYP,TRAN10       * Public hospitals don't have stepdown
          COMPARE   ONE,CSTDOWN
          CALL      STEPDOWN IF EQUAL
.
TRAN10    COMPARE   ZERO,ENDFLAG
          GOTO      TRAN20 IF EQUAL
.
          MATCH     LSTDATE,TDATE
          GOTO      TRAN30 IF EQUAL
.
TRAN20    UNPACK    TDATE,XCENT,XYEAR,XMON,XDAY
.
          CMATCH    ANSD,TMOVE
          GOTO      TRAN50 IF EQUAL
          CMATCH    ANSL,TMOVE
          GOTO      TRAN25 IF EQUAL
.
          MOVE      TRATEH,DAYRATE
          ADD       TRATEF,DAYRATE
          ADD       PTTRADPT,DAYRATE
          ADD       PTTRADRB,DAYRATE
          SUB       TDISC,DAYRATE
          SUB       TALLW,DAYRATE
.
          MATCH     STATYPE,TATYPE
          GOTO      TRAN30 IF EQUAL
          MOVE      TDATE,FROMDATE
          GOTO      TRAN30
.
.         On-leave record
.
TRAN25    MOVE      ZERO,TRATEF
          MOVE      ZERO,TDISC
          MOVE      ZERO,TALLW
          MOVE      ZERO,TRATEH
          MOVE      ZERO,DAYRATE
.
.         Save some variables for use by STEPDOWN
.
TRAN30    MOVE      TATYPE,STATYPE
          MOVE      TRBTYP,STRBTYP
          MOVE      TRBEND,STRBEND
          MOVE      PTTRAEND,SPTTRAEN
          MOVE      PTTREPSD,SPTTREPS
          MOVE      TDATE,SIDATET
.
          BRANCH    DAYCASE,NXTTRAN
          MATCH     YTDATE,TDATE
          GOTO      TRAN31 IF LESS
          GOTO      TRAN31 IF EQUAL   
          GOTO      NXTTRAN            
.
TRAN31    MOVE      COLNUM,XCOLNUM          * From the stepdown routine
.
          COMPARE   ONE,CMXFLAG
          GOTO      TRAN36 IF NOT EQUAL
.
          MOVE      PTTRAEND,XCBEND          * casemix patients
          MOVE      ZERO,FORM4
          MOVE      TRBEND,FORM4
          IF        FORM4 < PTTRAEND
            MOVE      TRBEND,XCBEND          * use the lowest ending day
          ENDIF
          GOTO      NXTTRAN
.
TRAN36    MOVE      CURBEND,XCBEND
          GOTO      NXTTRAN
.
.                                           * no more Tran records for this Adm
.
TRAN40    MOVE      YTDATE,TDATE           * Use current date
.
          COMPARE   ONE,CMXFLAG
          GOTO      TRAN40A IF NOT EQUAL
.
          IF        ACDAYCS = 1
.                                           * its a Day Case
            GOTO       TRAN50
          ELSE
            MOVE      YTDATE,XDATE1
            REP       " 0",XDATE1
            MOVE      XDATE1,TDATE
            MOVE      SP1,TMOVE
            LOAD      TMOVE USING ASTAT OF ANSC,ANSC,ANSC,ANSR
            MOVE      STATYPE,TATYPE
            MOVE      STRBTYP,TRBTYP
            MOVE      STRBEND,TRBEND
            MOVE      SPTTRAEN,PTTRAEND
            MOVE      SPTTREPS,PTTREPSD
            CALL      PATCMSTP
            MOVE      ONE,ENDFLAG
            GOTO      TRAN45
          ENDIF
.
.
TRAN40A   BRANCH    CHOSTYP,TRAN50
.
.         Check if a stepdown is required
.
          COMPARE   ONE,CSTDOWN
          GOTO      TRAN50 IF NOT EQUAL
.
          MOVE      YTDATE,XDATE1
          REP       " 0",XDATE1
          MOVE      XDATE1,TDATE
.
          MOVE      SP1,TMOVE
          LOAD      TMOVE USING ASTAT OF ANSC,ANSC,ANSC,ANSR
          MOVE      STATYPE,TATYPE
          MOVE      STRBTYP,TRBTYP
          MOVE      STRBEND,TRBEND
          MOVE      TDATE,SAVDATE             * treat as "D" - loop stopper
          IF        ASTAT<>4 & SPTTREPS=2
            MOVE      SAVDATE,FRDATE
            MOVE      ZERO,PTTREPSD
          ELSE
            MOVE      SPTTREPS,PTTREPSD
          ENDIF
.
STEPD2    CALL      STEPDOWN
          MOVE      ONE,ENDFLAG
.
.         Check if a stepdown was indicated = shown by a new TDATE
.
TRAN45    MATCH     TDATE,XDATE1
          GOTO      TRAN50 IF EQUAL
.
          MATCH     TDATE,LSTDATE
          GOTO      TRAN10 IF LESS
          GOTO      TRAN30
.
TRAN50    BRANCH    DAYCASE OF TRAN80
          MATCH     YTDATE,TDATE
          GOTO      TRAN51 IF EQUAL   
          GOTO      TRAN51 IF LESS
          GOTO      TRAN60            
TRAN51    MOVE      COLNUM,XCOLNUM          * From the stepdown routine
.
          IF        CMXFLAG=1
            MOVE      PTTRAEND,XCBEND
            MOVE      ZERO,FORM4
            MOVE      TRBEND,FORM4
            IF        FORM4<PTTRAEND
              MOVE      TRBEND,XCBEND
            ENDIF
          ELSE
            MOVE      CURBEND,XCBEND
          ENDIF
.
.         If not a daycase then day number is calculated from the last date the
.         admission type got changed to the current/dischgarged date
.
TRAN60    REP       " 0",DDATE
          MATCH     "000000",DDATE
          GOTO      TRAN65 IF EQUAL
.
          MOVE      DDATE,TODATE
          REP       " 0",TODATE
          GOTO      TRAN70
.
TRAN65    MOVE      YTDATE,TODATE           * Use current date
          REP       " 0",TODATE
.
TRAN70    REP       " 0",FROMDATE
          REP       " 0",TODATE
          CALL      NHOSPDAY                * Calculate no of days since the 
          MOVE      NBRDAYS,NUMDAY          * last change of admission type
.
          MOVE      ADATE,FROMDATE          * Cal. total number of days in hosp.
          REP       " 0",FROMDATE
          CALL      NHOSPDAY
          MOVE      NBRDAYS,TDAYNO
.
          IF        ACDAYCS = 0
.                                           * not a Day Case
            MOVE      NBRDAYS,NUMDAY        * Casemix-no of days since admission
          ENDIF
.
          GOTO      TRAN85
.
TRAN80    MOVE      ONE,NUMDAY
          MOVE      ONE,TDAYNO
.
TRAN85    MOVE      STWARD,XWARD
          MOVE      STBED,XBED
          CALL      CHKWRD00           * check range of ward
          BRANCH    EXIT,TRAN99
.
          CALL      GDAY0000           * Generates stepdown/outlier days
.
.         Updating the temp file
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NOMAST
.
          MOVE      NOFEE,NOBF
          MOVE      SP20,PATNAM
          MOVE      PSNAME,PATNAM
          PACK      KEY28 WITH XWARD,XBED,PATNAM,UNIQUE
.
          PACK      ASDATE,CCC,CYY,CMM,CDD        * Current date
          CALL      PATAGED                       * Get the current age
.
          CALL      GETM0000
.
          MOVE      CMXFLAG,PTCASMX
          CALL      WRTEMP1
          ADD       ONE,UNIQUE
TRAN99    RETURN
+
DCLM0000  RETURN                            * dummy routine for STEPDOWN
.******************************************************************************
NOMAST    DISPLAY   *P1:23,*EL:
                    *P1:24,*EL,*B,"** Adm. ",*V2LON,AADMNO,*HOFF:
                                  " has no Master file record **    ",*W2;
.....     CALL      HITENTER
.
          COMPARE   "2",WSTAT
          GOTO      NOMAST1 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*P10:24,"Current Admission No :";
          GOTO      NOMAST3
.
NOMAST1   COMPARE   "4",WSTAT
          GOTO      NOMAST2 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*P10:24,"On-leave Admission No :";
          GOTO      NOMAST3
.
NOMAST2   DISPLAY   *P1:24,*EL,*P10:24,"Discharged Admission No :";
.
NOMAST3   DISPLAY   *P50:24,"Scanning :";
          RETURN
+
.******************************************************************************
.             Initialise variables  
.******************************************************************************
IVAR0000  MOVE      ZERO,DAYLEFT
          MOVE      ZERO,TDAYNO
          MOVE      ZERO,DSTDOWN
          MOVE      ZERO,FNDDAY
.
          MOVE      SP10,SUFNDAY
          MOVE      SP10,SDAYLEFT
          MOVE      SP10,SFNDDAY
          MOVE      SP10,SDSTDOWN
IVAR9999  RETURN
+
.******************************************************************************
.             Generate the inlier/funded/unfunded etc...
.******************************************************************************
GDAY0000  COMPARE   ONE,CMXFLAG
          GOTO      GDAY8000 IF NOT EQUAL    * patcat
.
.         Casepayment
.
          COMPARE   ZERO,PTCAELOS
          GOTO      GDAY2000 IF EQUAL
.
          CALL      GUFN0000         * Calculate unfunded day
          CALL      GFND0000         * Calculate funded days
          CALL      GLEF0000         * Calculate no of days left to Outlier
          CALL      GINL0000         * Calculate days in  outlier
          GOTO      GDAY9999
.
GDAY2000  MOVE      "N/A",SUFNDAY       * unfunded day
          MOVE      "-",SFNDDAY         * Funded day
          MOVE      "N/A",SDAYLEFT
          MOVE      "N/A",SDSTDOWN
          GOTO      GDAY9999
.
.         Patient classification - get stepdown days
.
GDAY8000  CALL      GDLT0000         * Calculate no of days before/in stepdown
GDAY9999  RETURN
+
.******************************************************************************
.             Generate unfunded day 
.   If LOS is less than Optimal then Unfunded =0
.   If LOS is greater than Optimal but still an Inlier then Unfunded is +ve
.   IF LOS is greater than Optimal and an Outlier then Unfunded is -ve
.******************************************************************************
GUFN0000  MOVE      ZERO,UFNDAY
          MOVE      "  0",SUFNDAY
.
.         If LOS <= Optimal LOS then unfunded day =0
.
          COMPARE   TDAYNO,PTCAELOS
          GOTO      GUFN9999 IF NOT LESS     * no funded day
.
.         If LOS > Optimal LOS and LOS < start of Outlier
.            Unfunded days = LOS - Optimal LOS
.
          IF        TDAYNO < SOUTLIER
            MOVE      TDAYNO,UFNDAY          * LOS < outlier
            SUB       PTCAELOS,UFNDAY
            MOVE      UFNDAY,SUFNDAY
          ELSE
.
.         If LOS >= start of outliers then
.         Unfunded Days = Outlier Commencement Period - Optimal LOS
.
            ASSIGN    (SOUTLIER-PTCAELOS),UFNDAY
            MOVE      UFNDAY,SUFNDAY
          ENDIF
.
GUFN9999  RETURN
+
.******************************************************************************
.             Generate funded day 
.******************************************************************************
GFND0000  MOVE      ZERO,FNDDAY         * Funded day
.
.         If LOS <= Optimal LOS, then funded days = LOS
.                   
          COMPARE   TDAYNO,PTCAELOS
          GOTO      GFND1000 IF LESS     * no funded day
          MOVE      PTCAELOS,FNDDAY 
          GOTO      GFND9000
.
.         If LOS > Optimal LOS, then funded days = Optimal LOS
.
GFND1000  MOVE      PTCAELOS,FNDDAY
GFND9000  MOVE      FNDDAY,SFNDDAY
GFND9999  RETURN
+
.******************************************************************************
.             Generate Day left to Outlier for casepayment
.
.         If LOS < Outlier Commencement period
.            then Days to Outlier = Outlier Commencement Period - LOS
.         Else if LOS > Outlier Commencement Period
.            then Days to Outlier = ***
.         Else if LOS < Optimal LOS
.            then Days to Outlier = N/A         
.******************************************************************************
GLEF0000  MOVE      ZERO,DAYLEFT
          MOVE      SP10,SDAYLEFT
.
          IF        TDAYNO < PTCAELOS
            MOVE      "N/A",SDAYLEFT
          ENDIF
.
.         Check of start of Outlier
.
          IF        TDAYNO < SOUTLIER
            MOVE      SOUTLIER,DAYLEFT      
            SUB       TDAYNO,DAYLEFT          * Days to Outlier
            MOVE      DAYLEFT,SDAYLEFT
          ELSE
            MOVE      "***",SDAYLEFT           * Days to Outlier
          ENDIF
.
GLEF9999  RETURN
+
.******************************************************************************
.             Generate day in Outlier for casepayment
.         If LOS <= Outlier Commencement period
.           then number of days in outliers = 0
.         If LOS > Outlier Commencement period
.           then number of days in outliers = LOS - Outlier commencement period
.******************************************************************************
GINL0000  MOVE      ZERO,DSTDOWN
          MOVE      SP10,SDSTDOWN
.
          COMPARE   TDAYNO,SOUTLIER
          GOTO      GINL9999 IF NOT LESS
.
          MOVE      TDAYNO,DSTDOWN
          SUB       SOUTLIER,DSTDOWN
.
GINL9000  MOVE      DSTDOWN,SDSTDOWN
GINL9999  RETURN
+
.******************************************************************************
.             Generate day left in stepdown for patcat
.******************************************************************************
GDLT0000  MATCH     SP9,PTMIPDRG
          IF        !@EQUAL
            CALL      GFND0000
          ENDIF
.
          MOVE      ZERO,DAYLEFT
          MOVE      SP10,SDAYLEFT
          MOVE      ZERO,DSTDOWN     * days in current stepdown
          MOVE      SP10,SDSTDOWN     * days in current stepdown
          MOVE      "N/A",SUFNDAY
.
          BRANCH    DAYCASE,GDLT9000
.
          COMPARE   NUMDAY,XCBEND
          GOTO      GDLT9000 IF LESS
.
          MOVE      XCBEND,DAYLEFT
          SUB       NUMDAY,DAYLEFT   * no of days left before next stepdown
          MOVE      DAYLEFT,SDAYLEFT
.
.         LASTDAYS is generated from STEPDOWN or PATCMSTP routine
.
          MOVE      NUMDAY,DSTDOWN
          SUB       LASTDAYS,DSTDOWN * no of days in current stepdown
.
GDLT9000  MOVE      DSTDOWN,SDSTDOWN
GDLT9999  RETURN
+
.******************************************************************************
.             PRINT REPORT
.******************************************************************************
REPT0000  DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report",*HOFF:
                     *V2LON," **",*HOFF,*P45:24,"Patient  :";
.
          CALL      IBACLOCK
.
          MOVE      "57",MAXLINES           * Maximum lines on a Page
          MOVE      SP1,FRSTFOOT
          MOVE      ZERO,DCNUM
          MOVE      ZERO,DCRATE
          MOVE      ZERO,PATNUM
          MOVE      ZERO,PATRATE
          MOVE      SP3,SAVWARD
          MOVE      SP3,SWARD
          MOVE      "99",CLNO
.
          IF        OPTION=1
            PACK      KEY28 WITH SP20,SP20
            CALL      RDSTEMP1
          ELSE
            PACK      KEY18 WITH SP20
            CALL      RDSTEMP2
          ENDIF
REPT1000  IF        OPTION=1
            CALL      RDKTEMP1
          ELSE
            CALL      RDKTEMP2
          ENDIF
.
          BRANCH    OVRCD OF REPT8000
          DISPLAY   *P57:24,*V2LON,PATNAM;
.
          COMPARE   ZERO,DAYCASE
          IF        @EQUAL         
            ADD       ONE,DCNUM
            ADD       DAYRATE,DCRATE
          ELSE
            ADD       ONE,PATNUM
            ADD       DAYRATE,PATRATE
          ENDIF
.
          MOVE      CLNO,FORM4
.
          IF        OPTION = 1
.                                           * Check for change of Ward
            MATCH     SAVWARD,XWARD
            IF        @EQUAL
              ADD       FOUR,FORM4            * room for next patient
            ELSE
              ADD       SEVEN,FORM4           * room for Ward Header & Patient
              IF        PSKIP = 1
                MOVE      "99",FORM4          * Force a new page anyway
              ENDIF
            ENDIF
          ENDIF  
.                      
.                                           * Check if paging is required
REPT1400  COMPARE   MAXLINES,FORM4
          GOTO      REPT1500 IF LESS        * goto check if Ward header required
.
.                                           * Force the paging
          CALL      PRNTFOOT                * Print the footers
          CALL      HEAD0000
       
REPT1500  MATCH     SAVWARD,XWARD                                      *C-61007
          GOTO      REPT2000 IF EQUAL       * Dont bother to reprint Ward name
.
          IF        OPTION = 1
.                                           * Print the Ward header
            PACK      KEY6,XWARD,SP3
            CALL      RDWARD1 
            IF        OVRCD = 0
              PRINT     *1,"|"," WARD : ",*10,WWARD,*16,WBDESC,*132,"|"
            ELSE
              PRINT     *1,"|"," WARD : ",*132,"|"
            ENDIF
            CALL      PRNTUNDR
            ADD       TWO,CLNO
          ENDIF
.
.                                           * Now set up and print Patient
.
REPT2000  MOVE      XWARD,SAVWARD
          MATCH     XWARD,SWARD
.
.             Get the bed type
.
          MOVE      SP8,DIM8
          PACK      KEY5,CODEBT,STRBTYP
          CALL      RDCODE1
          BRANCH    OVRCD,REPT3000
          MOVE      TDESC,DIM8
.
.             Get the patient initial given name
.
REPT3000  MOVE      SP1,PNAM1
          MOVE      PGNAME,PNAM1
.
.             Get the attending doctor name
.
          MOVE      SP20,ADNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF REPT4000
.
          MOVE      DGNAME,PNAM2
          MOVE      DSNAME,ADNAME
.
.             Get the admission and operation date
.
REPT4000  UNPACK    ADATE WITH XCENT,XYEAR,XMON,XDAY
.
.             Get the admission type
.
          MOVE      PTMIPDRG,DIM9
          SQUEEZE   DIM9
          PACK      DIM20,AFUNDH,SLASH,AFNDTB
          SQUEEZE   DIM20
          MOVE      DIM20,DIM14
....      MOVE      PATNAM,DIM20
          MOVE      PATNAM,DIM16
.
          IF        OPTION = 1
            PRINT     *1,"|",XBED;
          ELSE
            PRINT     *1,"|",XWARD;
          ENDIF
          PRINT     *6,"|",AADMNO:
                    *15,"|",PNAM1,DOT,DIM16:                     * was DIM20:
                    *34,"| ",PSEX,*38,"|",PSAGE:
                    *42,"|",DIM9:                      * PTMIPDRG
                    *52,"|",ACLAIM:
                    *56,"|",DIM14:                     * AFUNDH,SLASH,AFNDTB
                    *71,"|",XDAY,SLASH,XMON,SLASH,XYEAR:
                    *81,"|";
.
          MATCH     "000000",ODATE
          GOTO      REPT5000 IF EQUAL

          UNPACK    ODATE WITH XCENT1,XYEAR1,XMON1,XDAY1
          PRINT     XDAY1,SLASH,XMON1,SLASH,XYEAR1;
.
REPT5000  PRINT     *91,"|",ATYPE;
.
          MATCH     "1",PTCASMX
          IF        @EQUAL
            PACK      DIM9,ANSY,SLASH,SFNDDAY
          ELSE
            MATCH     SP9,PTMIPDRG
            IF        @EQUAL
              PACK      DIM9,ANSN,SP9
            ELSE
              PACK      DIM9,ANSN,SLASH,SFNDDAY
            ENDIF
          ENDIF
          SQUEEZE   DIM9
          MOVE      DIM9,DIM5
          PRINT     *95,"|",DIM5;
.
          PACK      DIM8,SDSTDOWN,SP70
          RJUSTIFY  SDSTDOWN                * set up Level & Days in S/D Out
.
.         See if days in outlier stepdown is numeric
.
          TYPE      SDSTDOWN
          IF        @EQUAL 
            MOVE      SDSTDOWN,FORM4
            IF        FORM4 > 0
              MATCH     "1",PTCASMX
              IF        @EQUAL
                IF        XCOLNUM > 1
                  PACK      DIM20,SDSTDOWN
                ELSE
                  PACK      DIM20,ANSL,XCOLNUM,SLASH,SDSTDOWN
                ENDIF
              ELSE
                IF        XCOLNUM = 1
                  MOVE      SP20,DIM20
                ELSE
                  PACK      DIM20,ANSL,XCOLNUM,SLASH,SDSTDOWN
                ENDIF
              ENDIF
              SQUEEZE   DIM20
              MOVE      DIM20,DIM8
            ELSE
              MOVE      "   0",DIM8
            ENDIF
          ELSE 
            MATCH     SP70,SDSTDOWN
            IF        @EQUAL
              MOVE      "   0",DIM8     
            ENDIF
          ENDIF
.
          MATCH     SP70,SUFNDAY
          IF        @EQUAL
            MOVE      "  0",SUFNDAY
          ENDIF
.
          PRINT     *101,"|  ",TDAYNO,*109,"|",SUFNDAY:
                    *114,"|  ",SDAYLEFT,*123,"|",DIM8,*132,"|"
          IF        OPTION = 1
            PRINT     *1,"|";
          ELSE
            PRINT     *1,"|",XBED;
          ENDIF
          PRINT     *6,"|--------------------------":
                    *33,"-----------------------------------------------":
                    *80,"----------------------------------------------------|"
.
          UNPACK    ADIAG1,DIM35           *Report has room for 35 chars only
.
          PRINT     *1,"|",*6,"|",DIM35,*42,"|",PNAM2,DOT,ADNAME,*66,"|";
          MATCH     "1",PTCASMX
          IF        @EQUAL
            PRINT     *68,"(",LOUTLIER,")";
          ENDIF
.
          PRINT     *75,"|";
.
          MATCH     "1",PTCASMX
          IF        @EQUAL
            IF        SOUTLIER = 0
              PRINT     *123,"| (N/A)";
            ELSE
              PRINT     *123,"| (",SOUTLIER,")";
            ENDIF
          ENDIF
          PRINT     *132,"|"
          CALL      PRNTUNDR
          ADD       FOUR,CLNO
.
          GOTO      REPT1000
.
.             Report ended
.
REPT8000  CALL      PRNTFOOT
          ADD       "1",CLNO
          COMPARE   CLNO,MAXLINES
          GOTO      REPT8500 IF LESS
          CALL      HEAD0000
.
REPT8500  MOVE      ZERO,AVPAT
          MOVE      ZERO,AVDC
.
          COMPARE   ZERO,PATNUM
          GOTO      REPT9000 IF EQUAL
.
          MOVE      PATRATE,AVPAT
          DIV       PATNUM,AVPAT
.
REPT9000  COMPARE   ZERO,DCNUM
          GOTO      REPT9999 IF EQUAL
.
          MOVE      DCRATE,AVDC
          DIV       DCNUM,AVDC
.
REPT9999  PRINT     *N,*N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P25:24,*V2LON:
                    "** Report Generation Completed **";
          CALL      KILLIDZ
          GOTO      START
.
.******************************************************************************
.             HEADING
.******************************************************************************
HEAD0000  
          IF        OPTION = 1
            MOVE      "(Ward/Bed Sequence)",CPHDROPT
          ELSE
            MOVE      "(Days Left Sequence)",CPHDROPT
          ENDIF
          CALL      HEAD132
.
          ADD       ONE,CLNO
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital Id :",HOSNAME
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     YTDATE,CRDATE
          IF        @EQUAL
            MOVE      CTIMEIS,DIM5
            PRINT     *N,*40,"From Ward : ",WARDFPRT," To ",WARDTPRT:
                      *N,*40,"Patients as at ",DIM5," ":
                         XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2,*N
          ELSE
            PRINT     *N,*40,"From Ward : ",WARDFPRT," To ",WARDTPRT:
                      *N,*40,"Patients at Midnight ":
                         XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2,*N
          ENDIF
.
          CALL      PRNTUNDR
.
          IF       OPTION = 1
            PRINT    *1,"|Bed ";
          ELSE
            PRINT    *1,"|Ward";
          ENDIF
.
          PRINT    *6,"|Adm.",*15,"|Patient":
                   *34,"|Sex",*38,"|Age",*42,"|Adm.":
                   *52,"|Clm",*56,"|Health Fund/":
                   *71,"|Admission",*81,"|Operation":
                   *91,"|Adm",*95,"|CPAY",*101,"|Days in":
                   *109,"|Un-",*114,"|Days To":
                   *123,"|Days In",*132,"|" 
.
          IF       OPTION = 1
            PRINT    *1,"|    ";
          ELSE
            PRINT    *1,"|/Bed";
          ENDIF
.
          PRINT    *6,"|No.",*15,"|Name":
                   *34,"|",*38,"|",*42,"|CMBS/DRG":
                   *52,"|Typ",*56,"|Table":
                   *71,"|Date",*81,"|Date":
                   *91,"|Typ",*95,"|OLOS",*101,"|Hosp":
                   *109,"|Fund",*114,"|S/D Out":
                   *123,"|S/D Out",*132,"|"
          CALL      PRNTUNDR
          PRINT    *1,"|",*6,"|Adm Diagnosis",*42,"|Att. Doctor":
                   *66,"|Lowlier",*75,"|Area for Handwritten Comments":
                   *123,"|Outlier",*132,"|"
          CALL      PRNTUNDR
.
          MOVE      TEN2,CLNO
          RETURN
.
.******************************************************************************
PRNTUNDR  PRINT     *1,"*-------------------------------":
                    *33,"-----------------------------------------------":
                    *80,"----------------------------------------------------*"
          RETURN
.
.******************************************************************************
PRNTFOOT  MATCH     SP1,FRSTFOOT
          GOTO      PRNTFT99 IF EQUAL
.
          PRINT     *1,"Note:     *** = Outlier patient":
                    *40,"S/D = Stepdown   Out = Outlier":
                    *78,"Unfunded days = Length of Stay - Optimal Length ":
                    "of Stay"
.
PRNTFT99  MOVE      "99",CLNO
          MOVE      "99",FORM4
          MOVE      ANSY,FRSTFOOT
          RETURN
.
.******************************************************************************
.             Set up yesterday date
.******************************************************************************
SETYDAY   MOVE      CDD,DD
          MOVE      CMM,MM
          MOVE      CYY,YY
          MOVE      CCC,CC
          PACK      CRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CRDATE
          CALL      DMYCON
.
          SUB       ONE,JULDAY
.
SET20     MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      YTDATE WITH CC,YY,MM,DD
          REP       " 0",YTDATE
.
.                                           * Ask user for another date
.
SETY8000  UNPACK    YTDATE,CCENT,CYEAR,CMON,CDAY     * set KEYDATE vars
          DISPLAY   *P01:13,"Date 'as at'     :";
          MOVE      "22",CCOL1
          MOVE      "13",CVERT
          CALL      KEYDATE
          PACK      YTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",YTDATE
.
          MATCH     YTDATE,CRDATE
            IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Cannot Enter a future Date ";
            PRINT     *10,"Cannot Enter a future Date "
....        CALL      HITENTER
            GOTO      SETY8000
          ENDIF
.
SETY9999  UNPACK    YTDATE WITH XCENT2,XYEAR2,XMON2,XDAY2
          RETURN
+
.
.******************************************************************************
.         Create an indexed file based on port
.         for accumulating Admission Type totals
.******************************************************************************
INDZD     MOVE      ZERO,STATUS
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
.         Check if previously exists. if so delete it
.
          CALL      KILLIDZ
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 223 u1-3,4-6,7-20,21-28",CMDLINE
          APPEND    " u1-3,108-111,4-6,21-28",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ONE,STATUS
.
.         Open Temp Index file
.
          MOVE      ZERO,OPCND
          TRAP      NOINDZ IF IO
.
          OPEN	    TEMP1,FNAMEI
          OPEN	    TEMP2,FNAMEI
          TRAPCLR   IO
ENDC      RETURN
+
.******************************************************************************
.
NOINDZ    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,"** Temporary Index File ",*V2LON,FNAMEI:
                               *HOFF," not found **   ";
          PRINT     *10,"** Temporary Index File ",FNAMEI," not found ** " 
          TRAPCLR   IO
          RETURN
+
.******************************************************************************
.         Keyin a ward
.******************************************************************************
KWRD0000  MOVE      SP10,KEY3
          KEYIN     *PCCOL:CVERT,*EL,*DV,UNDLN3,*PCCOL:CVERT,*V2LON,KEY3;
.
          ENDSET    KEY3
          APPEND    SP3,KEY3
          RESET     KEY3
.
          DISPLAY   *PCCOL:CVERT,*V2LON,KEY3;
.
          CMATCH    "?",KEY3                     * Wards scan ?
          GOTO      KWRD8000 IF EQUAL            * yes
.
          MATCH     SP3,KEY3
          GOTO      KWRD8200 IF EQUAL
.
          PACK      KEY6,KEY3,SP3
          CALL      RDWARD1
          BRANCH    OVRCD,KWRD2000
.
          COMPARE   ZERO,WACTIVE                 * is Ward actvie ?
          GOTO      KWRD4000 IF EQUAL            * yes
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Ward is INACTIVE **    ";
          GOTO      KWRD3000
.
KWRD2000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ward does not exist on file **    ";
.
KWRD3000  CALL      HITENTER
          GOTO      KWRD0000
.
KWRD4000  DISPLAY   *P30:CVERT,WBDESC;
          MOVE      ZERO,EXIT
          GOTO      KWRD9999
.
.         Wards scan
.
KWRD8000  MOVE      CCOL,FORM2
          MOVE      CVERT,FORM2A
          CALL      GETSCR00
          CALL      PATWRDDS
          MOVE      FORM2,CCOL
          MOVE      FORM2A,CVERT
          CALL      PUTSCR00
          GOTO      KWRD0000
.
KWRD8200  MOVE      ZERO,EXIT
          GOTO      KWRD9999
.
KWRD9000  MOVE      ONE,EXIT
KWRD9999  RETURN
+
.******************************************************************************
.         Ward on a separate page ?
.******************************************************************************
KSPW0000  MOVE      ANSN,ANS
          KEYIN     *P1:20,*EL,"Would you like each Ward ":
                             "on a separate Page (":
                           *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                           *V2LON,*DV,ANSN,*HOFF,") ? ":
                    *P53:20,*RV,*DV,ANS:
                    *P53:20,*V2LON,ANS;
.
          REP       "yYnN",ANS
          CMATCH    ANSN,ANS
          GOTO      KSPW9999 IF EQUAL
          CMATCH    ANSY,ANS
          GOTO      KSPW1000 IF NOT EQUAL
          MOVE      ONE,PSKIP
          GOTO      KSPW9999
.
KSPW1000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Must be Y or N **    ";
          CALL      HITENTER
          GOTO      KSPW0000
.
KSPW9999  RETURN
+
.******************************************************************************
.         Checking range of ward
.******************************************************************************
CHKWRD00  MOVE      ZERO,EXIT
          MATCH     SP3,WARDFROM
          IF        !@EQUAL
            MOVE      WARDFROM,WARDFPRT
            MATCH     WARDFROM,XWARD
            GOTO      CHKWRD90 IF LESS
          ELSE
            MOVE      "Start ",WARDFPRT
          ENDIF
.
          MATCH     "zzz",WARDTO
          IF        @EQUAL
            MOVE      "Finish",WARDTPRT
            GOTO      CHKWRD99
          ELSE
            MOVE      WARDTO,WARDTPRT
            MATCH     XWARD,WARDTO
            GOTO      CHKWRD99 IF NOT LESS
          ENDIF
.
CHKWRD90  MOVE      ONE,EXIT
CHKWRD99  RETURN
+
.******************************************************************************
.         Deleting an indexed file based on port
.******************************************************************************
KILLIDZ   CLOSE     TEMP1
          CLOSE     TEMP2
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** Deleting Index File **";
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP1,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KILL9999           * file doesn't exit
.
          PACK      KEY28 WITH SP20,SP20
          CALL      RDSTEMP1
KILL2000  CALL      RDKTEMP1
          BRANCH    OVRCD,KILL9999
.
          PACK      KEY28 WITH XWARD,XBED,PATNAM,UNIQUE
          MATCH     SP70,KEY28
          GOTO      KILL9999 IF EQUAL
          CALL      DETEMP1
          GOTO      KILL2000
.
KILL9999  RETURN
+
.******************************************************************************
.           I/O routines for the Temp Index file
.******************************************************************************
WRTEMP1   RESET     KEY28
          MOVE      AADMNO,XAADMNO
          MOVE      UNIQUE,XUNIQUE
          WRITE     TEMP1,KEY28;XWARD,XBED,PATNAM,XUNIQUE,PGNAME,PSEX,PSAGE:
                                   ADOCTA,AFUNDH,AFNDTB,ADATE,ODATE,ACLAIM:
                                   ATYPE,NUMDAY,DAYRATE,SDAYLEFT,DAYCASE,NOBF:
                                   TDAYNO,STRBTYP,XAADMNO,XCOLNUM,PTMIPDRG:
                                   PTCMXLOS,PRICMBS,PTCASMX,SDSTDOWN:
                                   SFNDDAY,SUFNDAY,ADIAG1,SOUTLIER,LOUTLIER
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMP1;XWARD,XBED,PATNAM,XUNIQUE,PGNAME,PSEX,PSAGE:
                             ADOCTA,AFUNDH,AFNDTB,ADATE,ODATE,ACLAIM:
                             ATYPE,NUMDAY,DAYRATE,SDAYLEFT,DAYCASE,NOBF:
                             TDAYNO,STRBTYP,XAADMNO,XCOLNUM,PTMIPDRG:
                             PTCMXLOS,PRICMBS,PTCASMX,SDSTDOWN:
                             SFNDDAY,SUFNDAY,ADIAG1,SOUTLIER,LOUTLIER
          GOTO      OVERCOND IF OVER
          MOVE      XAADMNO,AADMNO
          RETURN
.
RDSTEMP1  MOVE      ZERO,OVRCD
          RESET     KEY28
          READ      TEMP1,KEY28;;
          GOTO      OVERCOND IF OVER
          RETURN
.
DETEMP1   MOVE      ZERO,OVRCD
          RESET     KEY28
          DELETE    TEMP1,KEY28
          RETURN
.
RDKTEMP2  MOVE      ZERO,OVRCD
          READKS    TEMP2;XWARD,XBED,PATNAM,XUNIQUE,PGNAME,PSEX,PSAGE:
                             ADOCTA,AFUNDH,AFNDTB,ADATE,ODATE,ACLAIM:
                             ATYPE,NUMDAY,DAYRATE,SDAYLEFT,DAYCASE,NOBF:
                             TDAYNO,STRBTYP,XAADMNO,XCOLNUM,PTMIPDRG:
                             PTCMXLOS,PRICMBS,PTCASMX,SDSTDOWN:
                             SFNDDAY,SUFNDAY,ADIAG1,SOUTLIER,LOUTLIER
          GOTO      OVERCOND IF OVER
          MOVE      XAADMNO,AADMNO
          RETURN
.
RDSTEMP2  MOVE      ZERO,OVRCD
          RESET     KEY18
          READ      TEMP2,KEY18;;
          GOTO      OVERCOND IF OVER
          RETURN
+
.******************************************************************************
.*         GETM0000 - loop thru patdtraf to find the Primary, Secondary &     *
.*                    Tertiary MBS item.  Primary will be the mbs with the    *
.*                    highest scheduled fee                                   *
.******************************************************************************
GETM0000  MOVE      SP10,PRICMBS            * initialise primary MBS code
          MOVE      SP10,SECCMBS            * initialise secondary MBS code
          MOVE      SP10,TERCMBS            * initialise tertiary MBS code
          MOVE      ZERO,FORM12A
          MOVE      ZERO,FORM12B
          MOVE      ZERO,FORM12C
          OPEN      PATDTRA1,"patdtraf"
          PACK      KEY22,AADMNO,SP20,SP20
          CALL      RDSDTRN1
GETM1000  CALL      RDKDTRN1
          BRANCH    OVRCD,GETM9999
.
          MATCH     SP70,HOSCODE        * All Hospitals
          IF        !@EQUAL
            PACK      KEY8,TADMNO,SP70
            CALL      RDPMVX11
            IF        OVRCD <> 1
              MATCH     PMVXMHOS,HOSCODE    * Correct hospital
              GOTO      GETM1000 IF NOT EQUAL
            ELSE
              GOTO      GETM1000
            ENDIF
          ENDIF
.
          MATCH     AADMNO,TADMNO           * compare admission number
          GOTO      GETM9999 IF NOT EQUAL
.
          COMPARE   TWO,TRECTYPE
          GOTO      GETM1000 IF NOT EQUAL   * not a theatre item
.
          OPEN      PATITEM1,"patitemn"
          PACK      KEY9,TITEMNO
          PACK      KEY8,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY
          REP       " 0",KEY8
          PACK      KEY17,KEY9,KEY8,SP70
          CALL      PATITMRD
          CLOSE     PATITEM1
          BRANCH    OVRCD,GETM1000          * not a valid item code
.
          COMPARE   QIAMT,FORM12A
          IF        !@LESS
            MOVE      FORM12A,FORM12B
            MOVE      FORM12B,FORM12C
            MOVE      QIAMT,FORM12A
            MOVE      PRICMBS,SECCMBS
            MOVE      SECCMBS,TERCMBS
            MOVE      TITEMNO,PRICMBS
            GOTO      GETM1000
          ENDIF
.
          COMPARE   QIAMT,FORM12B
          IF        !@LESS
            MOVE      FORM12B,FORM12C
            MOVE      QIAMT,FORM12B
            MOVE      SECCMBS,TERCMBS
            MOVE      TITEMNO,SECCMBS
            GOTO      GETM1000
          ENDIF
.
          COMPARE   QIAMT,FORM12C
          IF        !@LESS
            MOVE      QIAMT,FORM12C
            MOVE      TITEMNO,TERCMBS
          ENDIF
.
          GOTO      GETM1000
.
GETM9999  RETURN
+
.*******************************************************************************
.         GOUT0000 - Generate start of outlier
.*******************************************************************************
GOUT0000  MOVE      ZERO,NOFEE
          MOVE      ZERO,FORM4         * end of inlier period
.
          OPEN      PATHDFA1,"pathdfaf"
          PACK      SDIM27,CONTRCID,ACLAIM,AFUNDH,PTHFBCAT,CMCODE
          PACK      SDIM27,CASMXKEY,CMCODE,SP70
.
          MOVE      ZERO,FORM1
          MOVE      ZERO,FORM2
.
GOUT2000  PACK      KEY31,SDIM27,SP10
          CALL      RSPTHDF1
GOUT3000  CALL      RKPTHDF1
          BRANCH    OVRCD,GOUT4000
          PACK      DIM27,PTHDCNID,PTHDCLMN,PTHDHFND,PTHDTABT,PTHDCASM
.
          MATCH     DIM27,SDIM27
          GOTO      GOUT7000 IF EQUAL
.
GOUT4000  MOVE      ONE,NOFEE
          GOTO      GOUT9000
.
GOUT7000  MOVE      ONE,FORM2
          COMPARE   TWO,PTHDDTYP          * Outlier ?
          GOTO      GOUT7200 IF NOT EQUAL
.
          MOVE      FORM4,SOUTLIER    * get the end of inlier period
          ADD       ONE,SOUTLIER        * this must be start of outlier 
          GOTO      GOUT9000
.
GOUT7200  MOVE      PTHDEDAY,FORM4      * save the end period of inlier
          GOTO      GOUT3000
.
GOUT9000  CLOSE     PATHDFA1
GOUT9999  RETURN
+
.*****************************************************************************
.*                          GLOW0000                                         *
.*              Get low outlier cutoff                                       *
.* Requires:  ACLAIM - Claim code                                            *
.*            AFUNDH - Health Fund Code                                      *
.*            PTHFBCAT - Health Fund Table Type                              *
.*            CMCODE - DRG code                                              *
.* Returns: LOUTLIER - low outlier cutoff                                    *
.*****************************************************************************
.
GLOW0000  OPEN      PATHLFA1,"pathlfaf"          * open file
.
.         See if a record exists for the claim code, health fund, table type
.         and casemix code
.
          PACK      KEY27,CONTRCID,ACLAIM,AFUNDH,PTHFBCAT,CMCODE
          CALL      RDPTHLF1
          IF        OVRCD = 0
            MOVE      PTHLCOFF,LOUTLIER
            PACK      CASMXKEY,CONTRCID,ACLAIM,AFUNDH,PTHFBCAT,SP70
            GOTO      GLOW9900
          ENDIF
.
.         See if a record exists for the claim code, blank health fund and
.         table type, and casemix code
.
          PACK      KEY27,CONTRCID,ACLAIM,SP6,SP3,CMCODE
          CALL      RDPTHLF1
          IF        OVRCD = 0
            MOVE      PTHLCOFF,LOUTLIER
            PACK      CASMXKEY,CONTRCID,ACLAIM,SP70
            GOTO      GLOW9900
          ENDIF
.
.         See if a record exists for the default claim code, blank health fund
.         and table type, and casemix code
.
          CALL      GDCL0000
          PACK      KEY27,CONTRCID,PTCNDCLM,SP6,SP3,CMCODE
          CALL      RDPTHLF1
          IF        OVRCD = 0
            MOVE      PTHLCOFF,LOUTLIER
            PACK      CASMXKEY,CONTRCID,PTCNDCLM,SP70
            GOTO      GLOW9900
          ENDIF
.
GLOW9000  MOVE      "    *",LOUTLIER
.
GLOW9900  CLOSE     PATHLFA1
.
GLOW9999  RETURN
+
.*******************************************************************************
.         Get the default charging claim code for multi hospital
.*******************************************************************************
GDCL0000  IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
          MATCH     SP70,PTCNDCLM
          IF        @EQUAL
            MOVE      "0  ",PTCNDCLM          * use zero claim code
          ENDIF
GDCL9999  RETURN
+
.*******************************************************************************
ENDIT     CHAIN     PGM
          STOP
+
.==============================================================================
.
.------------------------------------------------------------------------------
.         KHOS0000
.         Enter The Hospital
.------------------------------------------------------------------------------
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:21,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TWENTY1,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:21,*V2LON,PTHSNAME;
          PACK      HOSNAME,PTHSNAME,SP70
          PACK      HOSCODE,PTHSHOSP,SP70
.
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.
.--------------------------------------------------------------------------------
          INCLUDE   STD001IO
          INC       CALCDAYS
          INCLUDE   NHOSPDAY
          INCLUDE   STEPDOWN
          INC       PATCMSTP
          INC       PATAGED
          INC       PATHSPKY
          INC       PATWRDDS
          INC       GETCNEFF
          INC       VALCMXFN
          INC       GETBFEES
          INC       PATITMRD
          INC       PATASDIO/INC
          INC       PATCFAIO/INC
          INC       PATHDFIO/INC
          INC       PATHLFIO/INC
          INC       PATLSDIO/INC
          INC       PATLDFIO/INC
          INC       PATAFEIO/INC
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATBFEIO/INC
          INC       PATCMCIO/INC
          INC       PATCODIO/INC
          INC       PMSCIDIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATDTRIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATRCAIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PATCMXIO/INC
          INC       PATITMIO/INC
          INC       PMSVX1IO/INC
          INC       NZPBFEIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

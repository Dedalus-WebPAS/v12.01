.------------------------------------------------------------------------------
. System    :   OPR - Theatre
. Program   :   IBAOPR16
. Desc      :   REcovery adverse event extract
.------------------------------------------------------------------------------
. Date      :   08/07/2010
. Author    :   Saroeun J. Soeur
. Function  :   
.
. Mods      :   extract patient details based on recovery adverse event details
.
.------------------------------------------------------------------------------
. V11.03.02  18/04/2023   Thanh T  TSK 0909393
.            Changed GTOP0000 to use RSOPDEA6 instead of RSOPDEA5   
. V11.03.01  20/03/2023   PJ Le Febour   TSK 0909393
.            GTOS0000 - amended PACK KEY19 to KEY22
. V11.01.01  27/04/2021   Thanh T        TSK 0895165
.            Recompiled for OPRARDFD changes
.------------------------------------------------------------------------------
. V10.04.01   23/06/2014   J.Tan           CAR 261630
.                          Removed port number from temp file name
. V10.03.01   05/03/2013   Patrick Adair   CAR 281898
.                          Corrected key for patecda1 and patecoa1
. V10.00.02   20.09.2010   Saroeun Soeur CAR 218727
.                          changed  report to match template change
. V10.00.00   29.07.2010   Saroeun Soeur CAR 218727
.                          created new extract program for recovery adverse
.                          events
.------------------------------------------------------------------------------
.  FD INCLUDES
.------------------------------------------------------------------------------
.
                    INC       STD001FD                * std library
                    INC       IBASEQFD/INC            * Sequential Numbers File
                    INC       TFILEVAR/INC            * Generate Temp File Name
                    INC       WEBERRFD/INC            * Web Server Error Logging
.
                    INC       PATCODFD/INC            * patcodes file
                    INC       OPRSESFD/INC            * theatre session file
.
                    INC       OPRARDFD/INC            * Operating Arrival 
.                                                       & Recovery
.
                    INC       PATVISFD/INC            * patient admiss. file
                    INC       PMSHCPFD/INC            * hcp file
                    INC       PATMA1FD/INC            * patient master file
                    INC       OPRAAEFD/INC            * recovery adv. evt file
                    INC       OPRCLIFD/INC            * clinic file
                    INC       PATECDFD/INC
                    INC       PATECOFD/INC
                    INC       OPRDEAFD/INC
                    INC       OPRSRGFD/INC
.
+
FLATFILE  FILE      ASCII, FIXED=262
.------------------------------------------------------------------------------
. Name          Type      Length     Physical    Description
.------------------------------------------------------------------------------
.
EXTDATOB       DIM       10         10          Patient date of birth
EXTDATAD       DIM       10         10          Date of admission
EXTDATTV       DIM       10         10          Date of theatre visit
EXTDURSU       DIM        5          5          Duration of Surgery
EXTGENDR       DIM        1          1          Sex
. EXTLOCAT       ???        ?          ?          Location ?
EXTSURGE       DIM       25         25          Surgeon
EXTANAES       DIM       25         25          Anaesthetist
EXTTPOCD       DIM        5          5          Time pre-op commenced
EXTANACD       DIM        5          5          Time anaesthetic commenced
EXTANATY       DIM       20         20          Anaesthetic type
EXTASASE       DIM        3          3          ASA score
EXTTIRC1       DIM        5          5          Time in recovery - 1st ( front )
EXTTIRC2       DIM        5          5          Time in recovery - 2nd ( back )
EXTTIRCD       DIM        5          5          Time in recovery - Day
EXTPWICU       DIM        5          5          Time patient went to ICU
EXTRYTDT       DIM        5          5          Ready to depart time
EXTTPDIE       DIM        5          5          Time patient died
EXTPDICD       DIM        9          9          Primary diagnosis code 1
EXTSCDC1       DIM        9          9          Secondary diagnosis code 1
EXTSCDC2       DIM        9          9          Secondary diagnosis code 2
EXTSCDC3       DIM        9          9          Secondary diagnosis code 3
EXTSCDC4       DIM        9          9          Secondary diagnosis code 4
EXTSCDC5       DIM        9          9          Secondary diagnosis code 5
EXTPMPRO       DIM        9          9          Primary procedure code
EXTSPDC1       DIM        9          9          Secondary procedure code 1
EXTSPDC2       DIM        9          9          Secondary procedure code 2
EXTSPDC3       DIM        9          9          Secondary procedure code 3
EXTSPDC4       DIM        9          9          Secondary procedure code 4
EXTSPDC5       DIM        9          9          Secondary procedure code 5
.              -----------------------
.              Total      262    
.------------------------------------------------------------------------------
.
ADVEREVT  DIM       1         * Adverse Event?
GENEVENT  DIM       1
PAINMANG  DIM       1
RESPFUNC  DIM       1
CARDFUNC  DIM       1
BODYTEMP  DIM       1
OTHEVENT  DIM       1
COUNT     FORM      1
MAXC      FORM      1
EVNTD     DIM       50
FLFND     FORM      1
TEMP      DIM       1
NTEMP     FORM      1
URNUMBR   DIM       8
USERID    DIM       10
EVENT     DIM       1
CCOUNT    FORM      2
SURGEONC  DIM       10        * surgeon doctor code
OPCNCLSU  FORM      1
FADMDTE   DIM       8
TADMDTE   DIM       8
TTODATE   DIM       8
ANAESTHC  DIM       10
TMPCNT    FORM      1
FILENAME  DIM       50
CURRDATE  DIM       8
YYYY      DIM       4
SPECIAL   FORM      1         * Special print flag
DMM       DIM       2
DDD       DIM       2
DIM8      DIM       8
DIM10A    DIM       10
DIM10B    DIM       10
DIM10C    DIM       10
DIM10D    DIM       10
DIM10E    DIM       10
DIM10F    DIM       10
DIAGNOSP  DIM       1
DIAGNOSS  DIM       1
.
DESCCNT   FORM      2        
TEMPVAR   DIM       1
.                              * general event description counter 
GEN1VAR   DIM       1
GEN2VAR   DIM       1
GEN3VAR   DIM       1
GEN4VAR   DIM       1
.
PAI1VAR   DIM       1
PAI2VAR   DIM       1
PAI3VAR   DIM       1
.
RES1VAR   DIM       1         
RES2VAR   DIM       1
RES3VAR   DIM       1
RES4VAR   DIM       1
RES5VAR   DIM       1
RES6VAR   DIM       1
RES7VAR   DIM       1
RES8VAR   DIM       1
RES9VAR   DIM       1
RESAVAR   DIM       1
RESBVAR   DIM       1
.
CAR1VAR   DIM       1
CAR2VAR   DIM       1
CAR3VAR   DIM       1
CAR4VAR   DIM       1
CAR5VAR   DIM       1
CAR6VAR   DIM       1
CAR7VAR   DIM       1
CAR8VAR   DIM       1
.
BOD1VAR   DIM       1
BOD2VAR   DIM       1
BOD3VAR   DIM       1
.
OTH1VAR   DIM       1
OTH2VAR   DIM       1
OTH3VAR   DIM       1
OTH4VAR   DIM       1
OTH5VAR   DIM       1
OTH6VAR   DIM       1
OTH7VAR   DIM       1
OTH8VAR   DIM       1
.
ELEVEN    FORM      "11"
.
CMDLINE   DIM       100
.
SEMICOMA  INIT      ";"
DQUOTES   INIT      "#""
ALL       INIT      "All"
FSLASH    INIT      "/"

+
CATEOA    INIT      "OA"
GENEDESC  INIT      "General Event? (Y/N)                  :"
PAINDESC  INIT      "Pain Management? (Y/N)                :"
RESPDESC  INIT      "Respiratory Function? (Y/N)           :"
CARDDESC  INIT      "Cardiac / Circulatory Function? (Y/N) :"
BODYDESC  INIT      "Body Temperature? (Y/N)               :"
OTHEDESC  INIT      "Other Event? (Y/N)                    :"
+
GEN1DESC  INIT      "Unresponsive > 10 mins (Y/N)            :"
GEN2DESC  INIT      "Agitated confused / hallucinations (Y/N):"
GEN3DESC  INIT      "PONV (Y/N)                              :"
GEN4DESC  INIT      "PONV requiring treatment with PACU protocol (Y/N):"
.
PAI1DESC  INIT      "Reviewed by an anaesthetist to treate severe pain (Y/N) :"
PAI2DESC  INIT      "Pain score >= 8 on waking (Y/N)                         :"
PAI3DESC  INIT      "Pain score >= 8 after 45 min (Y/N)                      :"
PAI4DESC  INIT      "Severe Pain in PACU requiring tratment with PACU pain protocol (Y/N):"
.
RES1DESC  INIT      "Persistent sat < 95% (Y/N)                                :"
RES2DESC  INIT      "Tachypnea (Y/N)                                            :"
RES3DESC  INIT      "Bradypnea (Y/N)                                           :"
RES4DESC  INIT      "Respiratory distress requiring tracheal intubation or insertion of laryngeal mask in recovery period (Y/N)         :"
RES5DESC  INIT      "Respiratory depression (Y/N)                              :"
RES6DESC  INIT      "Laryngospasm (Y/N)                                        :"
RES7DESC  INIT      "Bronchospasm (Y/N)                                        :"
RES8DESC  INIT      "Aspiration (Y/N)                                          :"
RES9DESC  INIT      "Ventilated / Bagged (Y/N)                                 :"
.RESADESC  INIT      "Reintubated (Y/N)                                         :"
RESBDESC  INIT      "Respiratory or cardiac arrest requiring intervention (Y/N):"
.
CAR1DESC  INIT      "Intervention of anaesthetist required (Y/N) :"
CAR2DESC  INIT      "Chest pain (Y/N)                            :"
CAR3DESC  INIT      "ECG Ischaemia (Y/N)                         :"
CAR4DESC  INIT      "New arrhythmia (Y/N)                        :"
CAR5DESC  INIT      "Hypotension (Y/N)                           :"
CAR6DESC  INIT      "Hypertension (Y/N)                          :"
CAR7DESC  INIT      "Bradycardia (Y/N)                           :"
CAR8DESC  INIT      "Tachycardia (Y/N)                           :"
.
BOD1DESC  INIT      "Body temp > 39C (Y/N)                  :"
.BOD2DESC  INIT      "Body temp < 35.5C (Y/N)                :"
BOD3DESC  INIT      "Body temp < 35C recorded in PACU (Y/N):"
.
OTH1DESC  INIT      "Death (Y/N)                                         :"
OTH2DESC  INIT      "Drug error (Y/N)                                    :"
OTH3DESC  INIT      "Anaphylaxis (Y/N)                                   :"
OTH4DESC  INIT      "Seizures (Y/N)                                      :"
OTH5DESC  INIT      "Unplanned transfer to ICU (Y/N)                     :"
OTH6DESC  INIT      "Inadequate reversal of neuromuscular blockade (Y/N) :"
OTH7DESC  INIT      "Unpredicted numbness / weakness  (Y/N)              :"
OTH8DESC  INIT      "Unplanned stay in PACU > 2hrs* for medical reasons (Y/N):"
.

.------------------------------------------------------------------------------
.         Constant program variables
.------------------------------------------------------------------------------
.
PRGID     INIT      "IBAOPR16"
PRGNAM    INIT      "Recovery Anaesthetist Adverse Event Extract"
VERSION   INIT      "V12.01.00"
+
.------------------------------------------------------------------------------

.------------------------------------------------------------------------------
.         MAIN
.------------------------------------------------------------------------------
MAIN0000  CALL      INIT0000  * initialise 
.
          CALL      GETC0000  * get criteria
          BRANCH    EXIT,MAIN9999
.
          CALL      GXTR0000  * generate extract
          CALL      CLOS0000
.
          CALL      MVEXT000
.
MAIN9999  CHAIN PGM
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
MVEXT000  CLEAR     CMDLINE
          APPEND    "ibaopr16.us1 ",CMDLINE
          UNPACK    FILENAME,DIM8
          APPEND    DIM8,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
MVEXT999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
CLOS0000  CLOSE     FLATFILE
.
CLOS9999  RETURN
.------------------------------------------------------------------------------
.         Initialise
.------------------------------------------------------------------------------
.
INIT0000  CALL      DISPHEAD             * header
.
          OPEN      OPRAAE11,"opraaeaf"; * open files
          OPEN      OPRSESA1,"oprsesaf";
          OPEN      OPRSESA7,"oprsesaf";
          OPEN      PATVISA1,"patvisaf";
          OPEN      PMSHCPA1,"pmshcpaf";
          OPEN      PATMA1A1,"patma1af";
          OPEN      PATMX1A1,"patmx1af";
          OPEN      PATCODE1,"patcodes";
          OPEN      OPRARDA1,"oprardaf";
          OPEN      OPRCLIA1,"oprcliaf";
          OPEN      CONTROLF,"controlf";
          OPEN      OPRDETA6,"oprdetaf";
          OPEN      PATECDA1,"patecdaf"; 
          OPEN      PATECOA1,"patecoaf";
          OPEN      OPRSRGA1,"oprsrgaf";

          READ      CONTROLF,FORTY;*111,OPCNCLSU  * read control parameter
          READ      CONTROLF,TEN;*235,DIAGNOSP,DIAGNOSS
.
          CALL      IBACLOCK                * Get current date
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FILENAME
.
          PREP      FLATFILE,FILENAME
.
INIT9999  RETURN
+
.-----------------------------------------------------
.         GETC0000
.-----------------------------------------------------
.
GETC0000  CALL      KDAT0000            * keyin date
          BRANCH    EXIT,GETC9999
.
          CALL      KSUR0000            * keyin surgeon
          CALL      KANA0000            * keyin anaesthetist
          CALL      KADE0000            * keyin adverse event
          CALL      GTEV0000            * get events
          CALL      USER0000
          CALL      CONTQST
          MOVE      ZERO,EXIT
          BRANCH    CEXIT,GETC9999,GETC0000,GETC9000
.
GETC9000  MOVE      ONE,EXIT
.
GETC9999  RETURN
+
.------------------------------------------------------------------------------
.         GTEV0000
.------------------------------------------------------------------------------
.
GTEV0000  MOVE      ZERO,TMPCNT
.
GTEV0050  COMPARE   SIX,TMPCNT
          IF        @LESS
            ADD       ONE,TMPCNT

            LOAD      EVNTD,TMPCNT,GENEDESC,PAINDESC,RESPDESC,CARDDESC,BODYDESC:
                                   OTHEDESC
.
            CALL      EVNT0000
.
            BRANCH    TMPCNT,GTEV1000,GTEV2000,GTEV3000,GTEV4000,GTEV5000:
                             GTEV6000
.
          ENDIF
.
          GOTO      GTEV9999
.
GTEV1000  MOVE      EVENT,GENEVENT
.
          MATCH     "1",EVENT
          IF        @EQUAL
            CALL      GNCT0000
          ENDIF
.
          GOTO      GTEV0050
.
GTEV2000  MOVE      EVENT,PAINMANG
.
          MATCH     "1",EVENT
          IF        @EQUAL
            CALL      PNCT0000
          ENDIF
.
          GOTO      GTEV0050

GTEV3000  MOVE      EVENT,RESPFUNC
.
          MATCH     "1",EVENT
          IF        @EQUAL
            CALL      RFCT0000
          ENDIF
.
          GOTO      GTEV0050

GTEV4000  MOVE      EVENT,CARDFUNC
          MATCH     "1",EVENT
          IF        @EQUAL
            CALL      CRCT0000
          ENDIF
.
          GOTO      GTEV0050

GTEV5000  MOVE      EVENT,BODYTEMP
          MATCH     "1",EVENT
          IF        @EQUAL
            CALL      BTCT0000
          ENDIF        
.
          GOTO      GTEV0050

GTEV6000  MOVE      EVENT,OTHEVENT
          MATCH     "1",EVENT
          IF        @EQUAL
            CALL      OTCT0000
          ENDIF
.
          GOTO      GTEV0050
.
GTEV9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
OTCT0000  MOVE      ZERO,DESCCNT
.
OTCT0050  COMPARE   EIGHT,DESCCNT
          IF        @LESS
            ADD       ONE,DESCCNT

            LOAD      EVNTD,DESCCNT,OTH1DESC,OTH2DESC,OTH3DESC,OTH4DESC,OTH5DESC:
                                   OTH6DESC,OTH7DESC,OTH8DESC
.
            CALL      EVNT0000
.
            BRANCH    DESCCNT,OTCT1000,OTCT2000,OTCT3000,OTCT4000,OTCT5000:
                              OTCT6000,OTCT7000,OTCT8000
.
          ENDIF
.
          GOTO      OTCT9999
.
OTCT1000  MOVE      EVENT,OTH1VAR
          GOTO      OTCT0050
.
OTCT2000  MOVE      EVENT,OTH2VAR
          GOTO      OTCT0050
.
OTCT3000  MOVE      EVENT,OTH3VAR
          GOTO      OTCT0050
.
OTCT4000  MOVE      EVENT,OTH4VAR
          GOTO      OTCT0050
.
OTCT5000  MOVE      EVENT,OTH5VAR
          GOTO      OTCT0050
.
OTCT6000  MOVE      EVENT,OTH6VAR
          GOTO      OTCT0050
.
OTCT7000  MOVE      EVENT,OTH7VAR
          GOTO      OTCT0050
.
OTCT8000  MOVE      EVENT,OTH8VAR
          GOTO      OTCT0050
.
OTCT9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
BTCT0000  MOVE      ZERO,DESCCNT
.
BTCT0050  COMPARE   TWO,DESCCNT
          IF        @LESS
            ADD       ONE,DESCCNT

            LOAD      EVNTD,DESCCNT,BOD1DESC,BOD3DESC
.
            CALL      EVNT0000
.
            BRANCH    DESCCNT,BTCT1000,BTCT3000
.
          ENDIF
.
          GOTO      BTCT9999          
.
BTCT1000  MOVE      EVENT,BOD1VAR
          GOTO      BTCT0050
.
BTCT3000  MOVE      EVENT,BOD3VAR
          GOTO      BTCT0050

.
BTCT9999  RETURN
.------------------------------------------------------------------------------
.         CRCT0000
.------------------------------------------------------------------------------
.
CRCT0000  MOVE      ZERO,DESCCNT
.
CRCT0050  COMPARE   EIGHT,DESCCNT
          IF        @LESS
            ADD       ONE,DESCCNT

            LOAD      EVNTD,DESCCNT,CAR1DESC,CAR2DESC,CAR3DESC,CAR4DESC,CAR5DESC:
                                   CAR6DESC,CAR7DESC,CAR8DESC
.
            CALL      EVNT0000
.
            BRANCH    DESCCNT,CRCT1000,CRCT2000,CRCT3000,CRCT4000,CRCT5000:
                              CRCT6000,CRCT7000,CRCT8000
.
          ENDIF
.
          GOTO      CRCT9999
.
CRCT1000  MOVE      EVENT,CAR1VAR
          GOTO      CRCT0050
.
CRCT2000  MOVE      EVENT,CAR2VAR
          GOTO      CRCT0050
.
CRCT3000  MOVE      EVENT,CAR3VAR
          GOTO      CRCT0050
.
CRCT4000  MOVE      EVENT,CAR4VAR
          GOTO      CRCT0050

CRCT5000  MOVE      EVENT,CAR5VAR
          GOTO      CRCT0050
.
CRCT6000  MOVE      EVENT,CAR6VAR
          GOTO      CRCT0050
.
CRCT7000  MOVE      EVENT,CAR7VAR
          GOTO      CRCT0050
.
CRCT8000  MOVE      EVENT,CAR8VAR
          GOTO      CRCT0050
.
CRCT9999  RETURN
+
.------------------------------------------------------------------------------
.         RFCT0000
.------------------------------------------------------------------------------
.
RFCT0000  MOVE      ZERO,DESCCNT
.
RFCT0050  COMPARE   TEN,DESCCNT
          IF        @LESS
            ADD       ONE,DESCCNT

            LOAD      EVNTD,DESCCNT,RES1DESC,RES2DESC,RES3DESC,RES4DESC,RES5DESC:
                                   RES6DESC,RES7DESC,RES8DESC,RES9DESC,RESBDESC
.
            CALL      EVNT0000
.
            BRANCH    DESCCNT,RFCT1000,RFCT2000,RFCT3000,RFCT4000,RFCT5000:
                              RFCT6000,RFCT7000,RFCT8000,RFCT9000,RFCTB000
.
          ENDIF
.
          GOTO      RFCT9999
.
RFCT1000  MOVE      EVENT,RES1VAR
          GOTO      RFCT0050
.
RFCT2000  MOVE      EVENT,RES2VAR
          GOTO      RFCT0050
.
RFCT3000  MOVE      EVENT,RES3VAR
          GOTO      RFCT0050
.
RFCT4000  MOVE      EVENT,RES4VAR
          GOTO      RFCT0050

RFCT5000  MOVE      EVENT,RES5VAR
          GOTO      RFCT0050
.
RFCT6000  MOVE      EVENT,RES6VAR
          GOTO      RFCT0050
.
RFCT7000  MOVE      EVENT,RES7VAR
          GOTO      RFCT0050
.
RFCT8000  MOVE      EVENT,RES8VAR
          GOTO      RFCT0050
.
RFCT9000  MOVE      EVENT,RES9VAR
          GOTO      RFCT0050
.
RFCTB000  MOVE      EVENT,RESBVAR
          GOTO      RFCT0050
.
RFCT9999  RETURN
+
.------------------------------------------------------------------------------
.         PNCT0000
.------------------------------------------------------------------------------
PNCT0000  MOVE      ZERO,DESCCNT
.
PNCT0050  COMPARE   THREE,DESCCNT
          IF        @LESS
            ADD       ONE,DESCCNT

            LOAD      EVNTD,DESCCNT,PAI1DESC,PAI2DESC,PAI3DESC,PAI4DESC

.
            CALL      EVNT0000
.
            BRANCH    DESCCNT,PNCT1000,PNCT2000,PNCT3000,PNCT4000
.
          ENDIF
.
          GOTO      PNCT9999
.
PNCT1000  MOVE      EVENT,PAI1VAR
          GOTO      PNCT0050
.
PNCT2000  MOVE      EVENT,PAI2VAR
          GOTO      PNCT0050
.
PNCT3000  MOVE      EVENT,PAI3VAR
          GOTO      PNCT0050
.
PNCT4000  MOVE      EVENT,BOD2VAR
          GOTO      PNCT0050
.
PNCT9999  RETURN
+
.------------------------------------------------------------------------------
.         GNCT0000
.------------------------------------------------------------------------------
.
GNCT0000 MOVE      ZERO,DESCCNT
.
GNCT0050  COMPARE   FOUR,DESCCNT
          IF        @LESS
            ADD       ONE,DESCCNT

            LOAD      EVNTD,DESCCNT,GEN1DESC,GEN2DESC,GEN3DESC,GEN4DESC
.
            CALL      EVNT0000
.
            MOVE    TEMPVAR,F1
            BRANCH    DESCCNT,GNCT1000,GNCT2000,GNCT3000,GNCT4000
.
          ENDIF
.
          GOTO      GNCT9999
.
GNCT1000  MOVE      EVENT,GEN1VAR
          GOTO      GNCT0050
.
GNCT2000  MOVE      EVENT,GEN2VAR
          GOTO      GNCT0050
.
GNCT3000  MOVE      EVENT,GEN3VAR
          GOTO      GNCT0050
.
GNCT4000  MOVE      EVENT,GEN4VAR
          GOTO      GNCT0050
.
GNCT9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
EVNT0000  KEYIN     *P1:CVERT,*EF,*DV,EVNTD,*V2LON:
                    EVENT
.
          ENDSET    EVENT
          APPEND    SP70,EVENT
          RESET     EVENT
.
          MATCH     SP70,EVENT            * Test for spaces
          IF        @EQUAL
            PACK      EVENT,SP70
            DISPLAY   *P1:CVERT,*EF,EVNTD,*V2LON,"Y"

            MOVE      "1",EVENT
            GOTO      EVNT9999
          ENDIF
.
          REP       "yYnN",EVENT
.
          MATCH     EVENT,ANSY
          IF        @EQUAL
            MOVE      "1",EVENT
            GOTO      EVNT9999
          ENDIF
.
          MOVE      "0",EVENT
.
EVNT9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
KDAT0000  DISPLAY   *P1:4,*EF,"From Date :":
                        *P1:6,"To Date   :";
.
KDAT1000  MOVE      FOUR,CVERT
          MOVE      TWENTY,CCOL
          MOVE      ONE,CCENTRY
          MOVE      CCC,CCENT
          MOVE      ZERO,CDAY
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                     * Keyin todate
          BRANCH    OVRCD,KDAT9000
.
          PACK      FADMDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FADMDTE
.
          MOVE      SIX,CVERT
          CALL      KEYDATE                     * Keyin fromdate
          BRANCH    OVRCD,KDAT1000
.
          PACK      TADMDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TADMDTE
.
          MATCH     FADMDTE,TADMDTE             * match date range
          GOTO      KDAT2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid date range entered **    ";
          CALL      HITENTER
          GOTO      KDAT0000
.
KDAT2000  CALL      CONTQST
          MOVE      ZERO,EXIT
          BRANCH    CEXIT,KDAT3000,KDAT1000,KDAT9000
.
.------ yes selected ------
.
KDAT3000  UNPACK    TADMDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                  * convert to julian format
.
          ADD       ONE,JULDAY              * add one day to todate
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                  * convert back to DDMMYY format
.
          PACK      TTODATE,CC,YY,MM,DD
          REP       " 0",TTODATE
.
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT
KDAT9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
KANA0000  KEYIN     *P1:CVERT,*EF,"Anaesthetist Code  : ",*V2LON:
                    ANAESTHC
.
          ENDSET    ANAESTHC
          APPEND    SP70,ANAESTHC
          RESET     ANAESTHC
.
          MATCH     SP70,ANAESTHC            * Test for spaces
          IF        @EQUAL
            PACK      ANAESTHC,ALL,SP70
            DISPLAY   *P1:CVERT,*EF,"Anaesthetist Code  : ",*V2LON:
                                ANAESTHC
            MOVE      SP70,ANAESTHC
            GOTO      KANA9999
          ENDIF
.
KANA9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------------------------------
.         User name
.------------------------------------------------------------------------------
.
URNU0000  KEYIN     *P1:CVERT,*EF,"UR Number  : ",*V2LON:
                    URNUMBR
.
          ENDSET    URNUMBR
          APPEND    SP70,URNUMBR
          RESET     URNUMBR
.
          MATCH     SP70,URNUMBR            * Test for spaces
          IF        @EQUAL
            PACK      URNUMBR,ALL,SP70
            DISPLAY   *P1:CVERT,*EF,"UR Number  : ",*V2LON:
                                URNUMBR
            MOVE      SP70,URNUMBR
            GOTO      URNU9999
          ENDIF
.
URNU9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------------------------------
.         User name
.------------------------------------------------------------------------------
.
USER0000  KEYIN     *P1:CVERT,*EF,"User ID  : ",*V2LON:
                    USERID
.
          ENDSET    USERID
          APPEND    SP70,USERID
          RESET     USERID
.
          MATCH     SP70,USERID            * Test for spaces
          IF        @EQUAL
            GOTO      USER0000
          ENDIF
.
          DISPLAY   *P1:CVERT,*EF,"User ID  : ",*V2LON:
                              USERID

USER9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
KSUR0000  KEYIN     *P1:4,*EF,"Surgeon Code  : ",*V2LON:
                    SURGEONC
.
          ENDSET    SURGEONC
          APPEND    SP6,SURGEONC
          RESET     SURGEONC
.
          MOVE      FIVE,CVERT
          MATCH     SP6,SURGEONC            * Test for spaces
          IF        @EQUAL
            PACK      SURGEONC,ALL,SP70
            DISPLAY   *P1:4,*EF,"Surgeon Code  : ",*V2LON:
                            SURGEONC
            MOVE      SP70,SURGEONC
            GOTO      KSUR9999
          ENDIF
.
KSUR9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
KADE0000  KEYIN     *P1:CVERT,*EF,"Adverse Event?  (Y/N): ",*V2LON:
                    ADVEREVT
.
          ENDSET    ADVEREVT
          APPEND    SP70,ADVEREVT
          RESET     ADVEREVT
.
          MATCH     SP70,ADVEREVT            * Test for spaces
          IF        @EQUAL
            PACK      ADVEREVT,SP70
            DISPLAY   *P1:CVERT,*EF,"Adverse Event? (Y/N): ",*V2LON:
                                    "ALL"
            MOVE      SP70,ADVEREVT
            GOTO      KADE9999
          ENDIF
.
          REP       "yYnN",ADVEREVT
.
          MATCH     ADVEREVT,ANSY
          IF        @EQUAL
            MOVE      "1",ADVEREVT
            GOTO      KADE9999
          ENDIF
.
          MOVE      "0",ADVEREVT
.
KADE9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------------------------------
.         GXTR0000 
.------------------------------------------------------------------------------
.
GXTR0000  PACK      KEY30,SP70
          CALL      RSOPAAE1
GXTR1000  CALL      RKOPAAE1
          BRANCH    OVRCD,GXTR9999
.
          MATCH     TADMDTE,OPAADATE
          GOTO      GXTR2000 IF EQUAL
.         
          MATCH     TADMDTE,OPAADATE
          GOTO      GXTR9999 IF NOT LESS  
.
GXTR2000  CALL      MTCR0000            * match criteria
          BRANCH    CEXIT,GXTR1000
.
          CALL      GTOS0000            * operation session
          CALL      GTOP0000            * operational details
          CALL      GTRA0000            * recovery time details
          CALL      GTDS0000            * diagnosis and procedure details
          CALL      GTPV0000            * patient visit
          CALL      GTPM0000            * patient master
.
          CALL      WRTE0000            * write extract         
          GOTO      GXTR1000
.         
GXTR9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
WRTE0000  UNPACK    EXTDATOB,YYYY,DMM,DDD                   * patient dob
          PACK      DIM10A,DDD,FSLASH,DMM,FSLASH,YYYY
.
          UNPACK    EXTDATAD,YYYY,DMM,DDD                   * admission date
          PACK      DIM10B,DDD,FSLASH,DMM,FSLASH,YYYY
.
          UNPACK    EXTDATTV,YYYY,DMM,DDD                   * theatre date
          PACK      DIM10C,DDD,FSLASH,DMM,FSLASH,YYYY
.
          WRITE     FLATFILE,SEQ;DQUOTES,DIM10A,DQUOTES,SEMICOMA:
                             DQUOTES,DIM10B,DQUOTES,SEMICOMA:
                             DQUOTES,DIM10C,DQUOTES,SEMICOMA:
                             DQUOTES,EXTDURSU,DQUOTES,SEMICOMA:
                             DQUOTES,EXTGENDR,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSURGE,DQUOTES,SEMICOMA:
                             DQUOTES,EXTANAES,DQUOTES,SEMICOMA:
                             DQUOTES,EXTTPOCD,DQUOTES,SEMICOMA:
                             DQUOTES,EXTANACD,DQUOTES,SEMICOMA:
                             DQUOTES,EXTANATY,DQUOTES,SEMICOMA:
                             DQUOTES,EXTASASE,DQUOTES,SEMICOMA:
                             DQUOTES,EXTTIRC1,DQUOTES,SEMICOMA:
                             DQUOTES,EXTTIRC2,DQUOTES,SEMICOMA:
                             DQUOTES,EXTTIRCD,DQUOTES,SEMICOMA:
                             DQUOTES,EXTPWICU,DQUOTES,SEMICOMA:
                             DQUOTES,EXTRYTDT,DQUOTES,SEMICOMA:
                             DQUOTES,EXTTPDIE,DQUOTES,SEMICOMA:
                             DQUOTES,EXTPDICD,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSCDC1,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSCDC2,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSCDC3,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSCDC4,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSCDC5,DQUOTES,SEMICOMA:
                             DQUOTES,EXTPMPRO,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSPDC1,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSPDC2,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSPDC3,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSPDC4,DQUOTES,SEMICOMA:
                             DQUOTES,EXTSPDC5,DQUOTES
.
WRTE9999  RETURN
+
.------------------------------------------------------------------------------
.         GTPV0000 - patient visit
.------------------------------------------------------------------------------
GTPV0000  PACK      PVIDATE,SP70
.
          PACK      KEY8,OPAAADMN,SP70
          CALL      RDPTVIS1
.
          PACK      EXTDATAD,PVIDATE
.
GTPV9999  RETURN
+
.------------------------------------------------------------------------------
.         GTPM0000 - patient master
.------------------------------------------------------------------------------
.
GTPM0000  PACK      PBDATE,SP70
          PACK      PSEX,SP70
.
          PACK      KEY8,PVIURNO
          CALL      RDMAST1
.
          PACK      EXTDATOB,PBDATE
          PACK      EXTGENDR,PSEX
.
GTPM9999  RETURN
+
.------------------------------------------------------------------------------
.         GTOP0000 - operational details
.------------------------------------------------------------------------------
.
GTOP0000  PACK      TDESC,SP70
          MOVE      ZERO,OPDATDUR
.
          PACK      KEY26,OPAADATE,OPAATIME,OPAACLIN,OPAACASE,SP70
          CALL      RSOPDEA6
GTOP1000  CALL      RKOPDEA6
          BRANCH    OVRCD,GTOP9999
.
          MATCH     OPAADATE,OPDADATE
          GOTO      GTOP1000 IF NOT EQUAL
.
          MATCH     OPAATIME,OPDATIME
          GOTO      GTOP1000 IF NOT EQUAL
.
          MATCH     OPAACLIN,OPDACLIN
          GOTO      GTOP1000 IF NOT EQUAL
.
          MATCH     OPAACASE,OPDACASE
          GOTO      GTOP1000 IF NOT EQUAL
.
          MATCH     OPAAADMN,OPDAADMN
          GOTO      GTOP1000 IF NOT EQUAL
.
          MOVE      DOPDASTA,F1
          BRANCH    F1,GTOP1000,GTOP2000,GTOP1000,GTOP2000
.
          GOTO      GTOP1000
.
GTOP2000  PACK      KEY5,CATEOA,OPDAANAE
          CALL      RDCODE1
.
          PACK      EXTANATY,TDESC
          PACK      EXTDURSU,OPDAEXPD
          PACK      EXTDATTV,OPAADATE
.
GTOP9999  RETURN
+
.------------------------------------------------------------------------------
.         GTOS0000 - operation session
.------------------------------------------------------------------------------
.
GTOS0000  PACK      OPARANAP,SP70
.
          PACK      KEY22,OPAADATE,OPAATIME,OPAACLIN,SP70
          CALL      RSOPSES7
          BRANCH    OVRCD,GTOS9999
.
          COMPARE   ZERO,OPCNCLSU
          IF        @EQUAL
            CALL      RDOPCLI1
            IF        OVRCD=0
              PACK    KEY10,OPCLDOCT
            ELSE
              PACK    KEY10,OPSECLIN
              CALL      RDPMHCP1
              IF        OVRCD=0
                MOVE      PMHCTITL,PACTITLE
                MOVE      PMHCGNAM,PACGNAME
                MOVE      PMHCSNAM,PACSNAME
                MOVE      TWO,PACFRMT             * title name surname of doctor
                CALL      PACNAME                 * pack up doctor's name
                MOVE      PACFNAME,EXTSURGE
              ELSE
                PACK      EXTSURGE,SP70
              ENDIF
            ENDIF
          ENDIF
.
          PACK    KEY10,OPSEANAE
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,PACTITLE
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCSNAM,PACSNAME
            MOVE      TWO,PACFRMT             * title name surname of doctor
            CALL      PACNAME                 * pack up doctor's name
            MOVE      PACFNAME,EXTANAES
          ELSE
            PACK      EXTANAES,SP70
          ENDIF
.
GTOS9999  RETURN
+
.------------------------------------------------------------------------------
.         GTRA0000 - recovery details
.------------------------------------------------------------------------------
.
GTRA0000  PACK      OPARUC11,SP70
          PACK      OPARTIRF,SP70
          PACK      OPARTIRB,SP70
          PACK      OPARTIRD,SP70
          PACK      OPARTICU,SP70
          PACK      OPARTIDP,SP70
          PACK      OPARTEXP,SP70
          PACK      OPDAPRE2,SP70
          PACK      OPDAPRE3,SP70
          PACK      OPSGTIPS,SP70
.
          PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
.
          PACK      EXTASASE,OPARUC11          
          PACK      EXTTIRC1,OPARTIRF
          PACK      EXTTIRC2,OPARTIRB 
          PACK      EXTTIRCD,OPARTIRD    
          PACK      EXTPWICU,OPARTICU    
          PACK      EXTRYTDT,OPARTIDP    
          PACK      EXTTPDIE,OPARTEXP
          PACK      EXTANACD,OPARANAP   
.
          PACK      KEY11,OPDAUNIQ,ONE,SP70
          CALL      RDOPSRG1
.
          PACK      EXTTPOCD,OPSGTIPS
.
GTRA9999  RETURN
+
.------------------------------------------------------------------------------
.         GTDS0000 - diagnosis and procedure details
.------------------------------------------------------------------------------
.
GTDS0000  PACK      EXTPDICD,SP70
          PACK      EXTSCDC1,SP70
          PACK      EXTSCDC2,SP70
          PACK      EXTSCDC3,SP70
          PACK      EXTSCDC4,SP70
          PACK      EXTSCDC5,SP70
.
          PACK      EXTPMPRO,SP70
          PACK      EXTSPDC1,SP70
          PACK      EXTSPDC2,SP70
          PACK      EXTSPDC3,SP70
          PACK      EXTSPDC4,SP70
          PACK      EXTSPDC5,SP70
          
          MOVE      ZERO,COUNT
          PACK      KEY13,OPAAADMN,SP70
          CALL      RSPTECD1
GTDS0050  CALL      RKPTECD1
          BRANCH    OVRCD,GTDS3000
.
          MATCH     OPAAADMN,DPTEDADM
          GOTO      GTDS3000 IF NOT EQUAL
.
          ADD       ONE,COUNT

          COMPARE   SIX,COUNT
          IF        !@LESS
            GOTO      GTDS9999
          ENDIF

          BRANCH    COUNT,GTDS1000,GTDS1100,GTDS1200,GTDS1300,GTDS1400:
                          GTDS1500

          GOTO      GTDS0050
.
GTDS1000  MOVE      PTEDCODE,EXTPDICD
          GOTO      GTDS0050
.
GTDS1100  MOVE      PTEDCODE,EXTSCDC1
          GOTO      GTDS0050
.         
GTDS1200  MOVE      PTEDCODE,EXTSCDC2
          GOTO      GTDS0050
.         
GTDS1300  MOVE      PTEDCODE,EXTSCDC3
          GOTO      GTDS0050
.         
GTDS1400  MOVE      PTEDCODE,EXTSCDC4
          GOTO      GTDS0050
.         
GTDS1500  MOVE      PTEDCODE,EXTSCDC5
          GOTO      GTDS0050
.         
GTDS3000  MOVE      ZERO,COUNT
          PACK      KEY12,OPAAADMN,SP70
          CALL      RSPTECO1
GTDS3100  CALL      RKPTECO1
          BRANCH    OVRCD,GTDS9999
.
          MATCH     OPAAADMN,DPTEOADM
          GOTO      GTDS9999 IF NOT EQUAL
         
          ADD       ONE,COUNT     

          COMPARE   SIX,COUNT
          IF        !@LESS
            GOTO      GTDS9999
          ENDIF
.
          BRANCH    COUNT,GTDS4000,GTDS4100,GTDS4200,GTDS4300,GTDS4400:
                          GTDS4500

          GOTO      GTDS3100
.
GTDS4000  MOVE      PTEOCODE,EXTPMPRO
          GOTO      GTDS3100
.
GTDS4100  MOVE      PTEOCODE,EXTSPDC1
          GOTO      GTDS3100
.
GTDS4200  MOVE      PTEOCODE,EXTSPDC2
          GOTO      GTDS3100
.
GTDS4300  MOVE      PTEOCODE,EXTSPDC3
          GOTO      GTDS3100
.
GTDS4400  MOVE      PTEOCODE,EXTSPDC4
          GOTO      GTDS3100
.
GTDS4500  MOVE      PTEOCODE,EXTSPDC5
          GOTO      GTDS3100
.
GTDS9999  RETURN
+
.------------------------------------------------------------------------------
.         MTCR0000 - Match select criteria
.------------------------------------------------------------------------------
.
MTCR0000  MOVE      ZERO,CEXIT
.
          MATCH     "1",GENEVENT
          GOTO      MTCR1000 IF NOT EQUAL
.
          CALL      CHKG0000
          BRANCH    CEXIT,MTCR8000
.
MTCR1000  MATCH     "1",PAINMANG
          GOTO      MTCR2000 IF NOT EQUAL
.
          CALL      CHPM0000
          BRANCH    CEXIT,MTCR8000
.
MTCR2000  MATCH     "1",RESPFUNC
          GOTO      MTCR3000 IF NOT EQUAL
.
          CALL      CHRF0000
          BRANCH    CEXIT,MTCR8000
.          
MTCR3000  MATCH     "1",CARDFUNC
          GOTO      MTCR4000 IF NOT EQUAL
.
          CALL      CHCC0000
          BRANCH    CEXIT,MTCR8000
.
MTCR4000  MATCH     "1",BODYTEMP
          GOTO      MTCR5000 IF NOT EQUAL
.
          CALL      CHBT0000
          BRANCH    CEXIT,MTCR8000
.
MTCR5000  MATCH     "1",OTHEVENT
          GOTO      MTCR6000 IF NOT EQUAL
.
          CALL      CHOT0000
          BRANCH    CEXIT,MTCR8000

MTCR6000  GOTO      MTCR9999
.
MTCR8000  MOVE      ONE,CEXIT
.
MTCR9999  RETURN
+
.-----------------------------------------------------
.         CHPM0000
.-----------------------------------------------------
.
CHPM0000  MOVE      ZERO,CEXIT
.         
          MOVE      ONE,CCOUNT
          MOVE      ZERO,TEMP
CHPM1000  COMPARE   FOUR,CCOUNT       * check general event
          IF        @LESS
            LOAD    TEMP,CCOUNT,OPAAPMRA,OPAAPMP1,OPAAPMP2,OPAABTB2
            LOAD    TEMPVAR,CCOUNT,PAI1VAR,PAI2VAR,PAI3VAR,BOD2VAR
.
            MATCH   "1",TEMPVAR
            IF      @EQUAL
              MOVE    TEMP,F1
              COMPARE ONE,F1
              IF     !@LESS
                ADD   ONE,CCOUNT         * in search criteria
                GOTO  CHPM1000
              ELSE
                GOTO  CHPM8000
              ENDIF
            ELSE
              ADD   ONE,CCOUNT          * not in search criteria ignore
              GOTO  CHPM1000
            ENDIF
          ENDIF
.
          GOTO      CHPM9999
.
CHPM8000  MOVE      ONE,CEXIT
.
CHPM9999  RETURN
+
.-----------------------------------------------------
.         CHCC0000
.-----------------------------------------------------
.
CHCC0000  MOVE      ZERO,CEXIT
.
          MOVE      ONE,CCOUNT
          MOVE      ZERO,TEMP
CHCC1000  COMPARE   EIGHT,CCOUNT       * check general event
          IF        @LESS
            LOAD    TEMP,CCOUNT,OPAACCIA,OPAACCCP,OPAACCEC,OPAACCNA,OPAACCHP:
                                  OPAACCHR,OPAACCBR,OPAACCTA
            LOAD    TEMPVAR,CCOUNT,CAR1VAR,CAR2VAR,CAR3VAR,CAR4VAR,CAR5VAR:
                                   CAR6VAR,CAR7VAR,CAR8VAR
            MATCH   "1",TEMPVAR
            IF      @EQUAL
              MATCH   "1",TEMP
              IF      @EQUAL
                ADD     ONE,CCOUNT         * in search criteria
                GOTO    CHCC1000
              ELSE
                GOTO    CHCC8000
              ENDIF
            ELSE
              ADD     ONE,CCOUNT          * not in search criteria ignore
              GOTO    CHCC1000
            ENDIF
          ENDIF

          GOTO      CHCC9999
.
CHCC8000  MOVE      ONE,CEXIT
.
CHCC9999  RETURN
+
.-----------------------------------------------------
.         CHRF0000
.-----------------------------------------------------
.
CHRF0000  MOVE      ZERO,CEXIT
.
          MOVE      ONE,CCOUNT
          MOVE      ZERO,TEMP
CHRF1000  COMPARE   TEN,CCOUNT       * check general event
          IF        @LESS
            LOAD    TEMP,CCOUNT,OPAARFPS,OPAARFTA,OPAARFBR,OPAARFRD,OPAARFDE:
                                OPAARFLA,OPAARFBO,OPAARFAS,OPAARFVB,OPAARFRC
            LOAD    TEMPVAR,CCOUNT,RES1VAR,RES2VAR,RES3VAR,RES4VAR,RES5VAR:
                                   RES6VAR,RES7VAR,RES8VAR,RES9VAR,RESBVAR
            MATCH   "1",TEMPVAR
            IF      @EQUAL
              MATCH   "1",TEMP
              IF      @EQUAL
                ADD     ONE,CCOUNT         * in search criteria
                GOTO    CHRF1000
              ELSE
                GOTO    CHRF8000
              ENDIF
            ELSE
              ADD     ONE,CCOUNT          * not in search criteria ignore
              GOTO    CHRF1000
            ENDIF
          ENDIF
.
          GOTO      CHRF9999
.
CHRF8000  MOVE      ONE,CEXIT
.
CHRF9999  RETURN
+
.-----------------------------------------------------
.         CHOT0000
.-----------------------------------------------------
.
CHOT0000  MOVE      ZERO,CEXIT
.
          MOVE      ONE,CCOUNT
          MOVE      ZERO,TEMP
CHOT1000  COMPARE   EIGHT,CCOUNT       * check general event
          IF        @LESS
            LOAD    TEMP,CCOUNT,OPAAORDE,OPAAORDT,OPAAORAN,OPAAORSE,OPAAORUN:
                                OPAAORIR,OPAAORNB,OPAAORST
.
            LOAD    TEMPVAR,CCOUNT,OTH1VAR,OTH2VAR,OTH3VAR,OTH4VAR,OTH5VAR:
                                   OTH6VAR,OTH7VAR,OTH8VAR
.
            MATCH   "1",TEMPVAR
            IF      @EQUAL
              MATCH  "1",TEMP
              IF     @EQUAL
                ADD   ONE,CCOUNT         * in search criteria
                GOTO  CHOT1000
              ELSE
                GOTO  CHOT8000
              ENDIF
            ELSE
              ADD   ONE,CCOUNT          * not in search criteria ignore
              GOTO  CHOT1000
            ENDIF
          ENDIF
.
          GOTO      CHOT9999
            
.
CHOT8000  MOVE      ONE,CEXIT
.
CHOT9999  RETURN
+

.-----------------------------------------------------
.         CHBT0000
.-----------------------------------------------------
.
CHBT0000  MOVE      ZERO,CEXIT
.
          MOVE      ONE,CCOUNT
          MOVE      ZERO,TEMP
CHBT1000  COMPARE   TWO,CCOUNT       * check general event
          IF        @LESS
            LOAD    TEMP,CCOUNT,OPAABTB1,OPAABTB3
            LOAD    TEMPVAR,CCOUNT,BOD1VAR,BOD3VAR
.
            MATCH   "1",TEMPVAR
            IF      @EQUAL
              MATCH  "1",TEMP
              IF     @EQUAL
                ADD   ONE,CCOUNT         * in search criteria
                GOTO  CHBT1000
              ELSE
                GOTO  CHBT8000
              ENDIF
            ELSE
              ADD   ONE,CCOUNT          * not in search criteria ignore
              GOTO  CHBT1000
            ENDIF
          ENDIF
.
          GOTO      CHBT9999
.
CHBT8000  MOVE      ONE,CEXIT
.
CHBT9999  RETURN
+
               
.------------------------------------------------------------------------------
.         CHKG0000
.------------------------------------------------------------------------------
.
CHKG0000  MOVE      ZERO,CEXIT
.
          MOVE      ONE,CCOUNT
          MOVE      ZERO,TEMP
CHKG1000  COMPARE   FOUR,CCOUNT       * check general event
          IF        @LESS
            LOAD    TEMP,CCOUNT,OPAAGEUR,OPAAGEAC,OPAAGEP1,OPAAGEP2
            LOAD    TEMPVAR,CCOUNT,GEN1VAR,GEN2VAR,GEN3VAR,GEN4VAR

            MATCH   "1",TEMPVAR
            IF      @EQUAL
              MATCH  "1",TEMP
              IF     @EQUAL
                ADD   ONE,CCOUNT         * in search criteria
                GOTO  CHKG1000
              ELSE
                GOTO  CHKG8000
              ENDIF
            ELSE
              ADD   ONE,CCOUNT          * not in search criteria ignore
              GOTO  CHKG1000
            ENDIF
          ENDIF 
.
          GOTO      CHKG9999
.
CHKG8000  MOVE      ONE,CEXIT
.
CHKG9999  RETURN
+
.
.-------------------------------------------------------------------------------
                    INC       STD001IO
                    INC       TFILENAM                * Generate Temp File Name
                    INC       IBASEQIO/INC            * Sequential Numbers File
                    INC       WEBERRIO/INC            * Web Server Error Logging
.
                    INC       OPRSESIO/INC            * theatre session file
.
                    INC       OPRARDIO/INC            * Operating Arrival
.                                                       & Recovery
.
                    INC       PATCODIO/INC

                    INC       PATVISIO/INC            * patient admiss. file
                    INC       PMSHCPIO/INC            * hcp file
                    INC       PATMA1IO/INC            * patient master file
                    INC       OPRAAEIO/INC            * recovery adv. evt file
                    INC       OPRCLIIO/INC            * clinic file
                    INC       PATECDIO/INC
                    INC       PATECOIO/INC
                    INC       OPRDEAIO/INC
                    INC       OPRSRGIO/INC
.

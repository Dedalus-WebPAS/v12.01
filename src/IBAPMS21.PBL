. *****************************************************************************
. *                                                                           *
. *                       P R O G R A M   S U M M A R Y                       *
. *                       -------------   -------------                       *
. *                                                                           *
. *     PROGRAM NAME:    IBAPMS21                                             *
. *                                                                           *
. *     FUNCTION:        HOSPITAL OCCUPANCY REPORT                            *
. *                                                                           *
. *     DATE WRITTEN:    17/04/90                                             *
. *                                                                           *
. *     AUTHOR:          DIG (Adapted from IBABIL87)                          *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *        V12.00.01 28/05/2025  PJ Le Febour   TSK 0955096 AlphaNum Visitnum *
. *                  Replace  COMPARE TADMN,AADMNO to MATCH TADMN,AADMNO
. *        V10.04.01 10/07/2014  J.Tan          CAR 261630                    *
. *                  Removed port number from temp file name                  *
. *        V10.03.01 18/10/2012  Jill Parkinson CAR 273375                    *
. *                  Recompiled for DATECONV - first day of year calculation  *
. *        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                     *
. *                  Added pmsvx1af                                           *
. *       V9.09.01  25/10/2007  Peter Vela    CAR 142394                      *
. *                 Fixed endless loop on non-active ward                     *
. *        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2     *
. *                  - Add Sex Description routine SEXDSC00 with Code "R" -   *
. *                  "Intersex" added                                         *
. *       V9.02.01  07/11/2001  Dean Cramer                                   *
. *                  Change "ZZZ" start load to "~~~" due to infinite loop    *
. *       V5.08.02  11/09/2000  Tonii                                         *
. *                 - Mods to accept Unknown and Indeterminate as valid       *
. *                   patient sex description                                 *
. *                 - Mods to print 2 extra columns for the above changes when*
. *                   running Hospital occupancy report                       *
. *       V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *                      V5.07.03  04/01/2000  Steve Armstrong  SRF 636822    *
. *                                                                 636800    *
. *                      v5.07.02  29/07/1999  Steve Downing                  *
. *                                Mods to use DAYSEP,changes to INCRDTE      *
. *                      V5.07.01  22/02/1999  Jim Stathopoulos               *
. *                                Report Modification                        *
. *                      V5.07.B02 28/01/1999  Jim Stathopoulos               *
. *                                Report Modification                        *
. *****************************************************************************
. *                      V5.01.121 03/10/90 i chung        SRF 105741         *
. *                                Added Ward parameters for detail report    *
. *                      V5.01.122 27/11/90 Glen Hobby                        *
. *                                Recompiled for changes to PATMX1FD         *
. *                      V5.01.123 04/10/91 J.Tan          SRF 110938         *
. *                                Fixed up the heading                       *
. *        V5.01.124 08.03.93 Neeriem "I Hate Support" Dye (I.B.A.)           *
. *                  Modify to use standard routine KEYDATE                   *
. *        V5.01.125 19/04/93 DIG                                             *
. *                  Made sure the TEXTRA variable is now used for additional *
. *                  charging.                                                *
. *        V5.01.126 03/06/93 J.Tan   SRF 121660                              *
. *                  Added ward/bed history file                              *
. *        V5.01.127 07/01/94 P.Howells                                       *
. *                  Changed Bed Occupancy % & Number of Beds                 *
. *        V5.01.128 21/04/94 Ian Rutt  MTA                                   *
. *                  Modified do if PTCNWBHB is on program uses BED TYPE (BT) *
. *        V5.02.001 02/03/95 Paul Howells     SRF 135026                     *
. *                  Fixed the use of PATWBHFD file.                          *
. *        V5.03.01  03/04/1996  Steve Armstrong  SRF 611348                  *
. *                  Mods to use WDACTVDY to calculate active days for a ward *
. *                  instead of using PATACDAY (to balance with HOS82).       *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       PATCODFD/INC
          INC       PATDAYFD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       PATWBHFD/INC
          INC       PATWR1FD/INC
          INC       WDACTVVR/INC
.
PATFNAME  DIM       8
.
.          Temporary index files for report
.
.
PATMP1XX  IFILE     SQL, FIXED=9, KEY="u1-8"
BEDTEMP1  IFILE     SQL, FIXED=9, KEY="u1-3"
.
TEMPTYPE  DIM        3                 * Type of bed or room
TEMPBEDD  FORM       7.2               * Number of bed days
TOTBEDS   FORM       9
WARBEDAC  FORM       8.2
WARTOTBD  FORM       6
SVHNOCC   FORM       3
TOJULDD   FORM       3
TOJULYY   FORM       2
TOJULCC   FORM       2
FNAMEI    DIM        8
FROMDATE  DIM        8
OPENFILE  FORM       1
TODATE    DIM        8
WKDATE    DIM        8
WKMM      DIM        2
.
PATOCCP1  IFILE     SQL, FIXED=131, KEY="U1-9"               * was 95  *C-49105
PATOCCP2  IFILE     SQL, FIXED=131, KEY="U1-9"               * was 95  *C-49105
.                   (*** THE ISI IS ACTUALLY PATOCCP1.ISI ***)
.                   (*** PATOCCP2.ISI DOES NOT EXIST      ***)
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
OCWARD    DIM       3           3         1      OCCUPANCY WARD NUMBER
OCROOM    DIM       3           3         4      OCCUPANCY ROOM TYPE
.                                                 "ZZZ" = TOTAL ALL WARD
OCTYPE    DIM       3           3         7      OCCUPANCY ADMISSION TYPE
.                                                 "ZZZ" = TOTAL ALL ROOM TYPES
.
OCBEDS    FORM      7.2         6        10      OCCUPANCY No BEDS ROOM TYPES
.
OCDAYM    FORM      7           5        16      OCCUPANCY BED DAYS - MALE
OCDAYF    FORM      7           5        21      OCCUPANCY BED DAYS - FEMALE
OCDAYA    FORM      7           5        26      OCCUPANCY BED DAYS - ADULT
OCDAYC    FORM      7           5        31      OCCUPANCY BED DAYS - CHILD
OCDAYD    FORM      7           5        36      OCCUPANCY BED DAYS - DAY CASES
OCDAYT    FORM      7           5        41      OCCUPANCY BED DAYS - TOTAL
.
. USED BY TABBING ON READ PATOCCP2     PRETOTAL = OCDAYT
.
PRETOTAL  FORM      7    TAB POS   41  *** TOTAL BED DAYS FOR PAT MIX CALC
.
OCAMTM    FORM      9.2         7        46      OCCUPANCY AMOUNT - MALE
OCAMTF    FORM      9.2         7        53      OCCUPANCY AMOUNT - FEMALE
OCAMTA    FORM      9.2         7        60      OCCUPANCY AMOUNT - ADULT
OCAMTC    FORM      9.2         7        67      OCCUPANCY AMOUNT - CHILD
OCAMTD    FORM      9.2         7        74      OCCUPANCY AMOUNT - DAY CASES
OCAMTT    FORM      9.2         7        81      OCCUPANCY AMOUNT - TOTAL
OCDAYS    FORM      8           5        88      OCCUPANCY ACTIVE DAYS
OCCHNG    FORM      1           2        93      OCCUPANCY BED NO. CHANGE FLAG
.
OCDAYI    FORM      7           5        95      OCCUPANCY BED DAYS - INDETER
OCDAYR    FORM      7           5        100     OCCUPANCY BED DAYS - INTERSEX
OCDAYU    FORM      7           5        105     OCCUPANCY BED DAYS - UNKNOWN
.
OCAMTI    FORM      9.2         7        110     OCCUPANCY AMOUNT - INDETER
OCAMTR    FORM      9.2         7        117     OCCUPANCY AMOUNT - INTERSEX
OCAMTU    FORM      9.2         7        124     OCCUPANCY AMOUNT - UNKNOWN
.
.        END OF RECORD                   131
.
+
.
.               VARIABLES DEFINITIONS
.
STATUS    FORM      1
OPCND     FORM      1
CMDLINE   DIM       50

PCENT     INIT      "%"
PTCNWBHB  FORM      1
ZZZ       INIT      "~~~"
ZEIGHT    INIT      "ZZZZZZZZ"
.
HPCENT    FORM      "100.00"
.
OYEAR     FORM      "365"
LYEAR     FORM      "366"
LYR       FORM      1
.
.
.
.    CODES FILE VARIABLES
.
CHNGFLAG  FORM      1             * change flag
.                                   0 = no change in # of beds during period
.                                   1 = change in # of beds during period
.
CODE      DIM       2
CODEA     INIT      "A "
CODEBT    INIT      "BT"
CODEC     INIT      "C "
CODECC    INIT      "CC"
CODECL    INIT      "CL"
CODEDC    INIT      "DC"
CODEM     INIT      "M "
CODEP     INIT      "P "
CODER     INIT      "R "
CODERT    INIT      "RT"
CODES     INIT      "S "
CODET     INIT      "T "
CODEUNK   INIT      "UNK"
.
ASDATE    DIM       8
DIM4      DIM       4
DIM8      DIM       8
SRBDAY    DIM       4
SRYEAR    FORM      4
.
DNAME     DIM       30
.
DDTYPE    DIM       16
DDROOM    DIM       23
.
CAGECOFF  FORM      2
WSTAT     FORM      1
.
FINISH    INIT      "Finish"                  * added for V5.01.121 - SRF 105741
START     INIT      "Start"                   * added for V5.01.121 - SRF 105741
FRWARD    DIM       3                         * added for V5.01.121 - SRF 105741
TOWARD    DIM       3                         * added for V5.01.121 - SRF 105741
FRWRD     DIM       6                         * added for V5.01.121 - SRF 105741
TOWRD     DIM       6                         * added for V5.01.121 - SRF 105741
FWRDDESC  DIM       20                        * added for V5.01.121 - SRF 105741
TWRDDESC  DIM       20                        * added for V5.01.121 - SRF 105741
QUESTFLG  FORM      1                         * added for V5.01.121 - SRF 105741
WARDFLG   FORM      1                         * added for V5.01.121 - SRF 105741
.
FEDAYS    FORM      4
MADAYS    FORM      4
FEAMT     FORM      6.2
MAAMT     FORM      6.2
GBTOTAL   FORM      5
GATOTAL   FORM      7.2
BEDCODE   FORM      5
AMTCODE   FORM      7.2
BOBED     FORM      5
BOAMT     FORM      7.2
CADAYS    FORM      4
CAAMT     FORM      6.2
.
PRBTRT    DIM       9
PTCNH82W  FORM      1
SAVAMT    FORM      6.2
.
SAVWWARD  DIM       3
SAVWBED   DIM       3
SAVROOM   DIM       3
SAVTYPE   DIM       3
.
SUMFLG    DIM       1
GRSUM     FORM      "0"
ROOMFLAG  FORM      1     ** 1 = ROOM TYPE (ROOM) HEADING HAS BEEN PRINTED"
.
TOCBEDS    FORM      7    ROOM TYPE TOTAL NUMBER BEDS
.
TOCDAYM   FORM      7    ROOM TYPE TOTAL BED DAYS - MALE
TOCDAYF   FORM      7    ROOM TYPE TOTAL BED DAYS - FEMALE
TOCDAYI   FORM      7    ROOM TYPE TOTAL BED DAYS - INDETERMINATE
TOCDAYR   FORM      7    ROOM TYPE TOTAL BED DAYS - INTERSEX           *I-49016
TOCDAYU   FORM      7    ROOM TYPE TOTAL BED DAYS - UNKNOWN
TOCDAYA   FORM      7    ROOM TYPE TOTAL BED DAYS - ADULT
TOCDAYC   FORM      7    ROOM TYPE TOTAL BED DAYS - CHILD
TOCDAYD   FORM      7    ROOM TYPE TOTAL BED DAYS - DAY CASES
TOCDAYT   FORM      7    ROOM TYPE TOTAL BED DAYS - TOTAL
.
TOCAMTM   FORM      9.2  ROOM TYPE TOTAL AMOUNT - MALE
TOCAMTF   FORM      9.2  ROOM TYPE TOTAL AMOUNT - FEMALE
TOCAMTI   FORM      9.2  ROOM TYPE TOTAL AMOUNT - INDETERMINATE
TOCAMTR   FORM      9.2  ROOM TYPE TOTAL AMOUNT - INTERSEX             *I-49016
TOCAMTU   FORM      9.2  ROOM TYPE TOTAL AMOUNT - UNKNOWN
TOCAMTA   FORM      9.2  ROOM TYPE TOTAL AMOUNT - ADULT
TOCAMTC   FORM      9.2  ROOM TYPE TOTAL AMOUNT - CHILD
TOCAMTD   FORM      9.2  ROOM TYPE TOTAL AMOUNT - DAY CASES
TOCAMTT   FORM      9.2  ROOM TYPE TOTAL AMOUNT - TOTAL
.
CHG       FORM      1
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
XCENT2    DIM       2
.
JDAYS     FORM      3
JYEAR     FORM      2
.
DAYWRK1   FORM      9.4     **** working variables
DAYWRK2   FORM      9.4
.
NODAYS    FORM      3
.
DAYANLM   FORM      3
DAYANLF   FORM      3
DAYANLI   FORM      3
DAYANLU   FORM      3
DAYANLA   FORM      3
DAYANLC   FORM      3
DAYANLT   FORM      3
.
DAYPERI   FORM      3.2
DAYPERD   FORM      3.2
.
BEDOCCP   FORM      3.2
PATMIXP   FORM      3.2
.
SDAYMON   DIM       4
SYEAR     DIM       4
SCENT     DIM       4
.
TDAYMON   DIM       4
TMPADMN   DIM       8
TOTDAY    FORM      8
TOTAMT    FORM      9.2
TYEAR     DIM       4
TCENT     DIM       2
.
WDATE1    DIM       8
WDATE2    DIM       8
.
CUDATE    DIM       8
WDATE     DIM       8
.
FDATE     DIM       8
BEDTDATE  DIM       8
.
VERT      FORM      2
.
PRGID     INIT      "IBAPMS21"
PRGNAM    INIT      "Hospital Occupancy Report"
VERSION   INIT      "V12.01.00"
+
.         Display screen header and open files
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"       * added for V5.01.121 - SRF 105741
.
          DISPLAY   *P64:24,"patwbhaf";
          OPEN      PATWBHA1,"patwbhaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN;*94,CAGECOFF
          READ      CONTROLF,TWENTY;*250,PTCNH82W
          READ      CONTROLF,SEVENTY7;*239,PTCNWBHB
.
          PACK      CUDATE,CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PATFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEI
.
          MOVE      ONE,CNEWDS                 * for PATWRDDS - SRF 105741
          MOVE      ZERO,OPENFILE
+
.
.         START OF PROGRAM
.
START     CALL      SCREEN1
          MOVE      ONE,WARDFLG                * initialize ward range flag
          GOTO      KADAT1
.
SCREEN1   MOVE      SP20,WBDESC
          DISPLAY   *P1:4,*EF,"Keyin From Date : ":
                        *P1:6,"Keyin To   Date : ":
                    *P1:8,"Grand Summary Only (",*V2LON,"Y",*HOFF,"/":
                                                 *V2LON,"N",*HOFF,") : "
          RETURN
+
.
.         Keyin FROM date
.
KADAT1    MOVE      FOUR,VERT
          MOVE      ZERO,IDAY
          CALL      KEYDATEX
.
          COMPARE   ZERO,IDAY
          GOTO      ENDIT IF EQUAL
.
          PACK      FDATE,ICENT,IYEAR,IMON,IDAY
          REP       " 0",FDATE
.
          MATCH     FDATE,CUDATE
          GOTO      INVDTE1 IF LESS
.
          UNPACK    FDATE,XCENT1,XYEAR1,XMON1,XDAY1
          PACK      FROMDATE,FDATE
          REP       " 0",FROMDATE
.
.             Key in TO date
.
KADAT2    MOVE      SIX,VERT
          CALL      KEYDATEX
.
          PACK      BEDTDATE,ICENT,IYEAR,IMON,IDAY
          REP       " 0",BEDTDATE
.
          MATCH     BEDTDATE,CUDATE
          GOTO      INVDTE2 IF LESS
.
          MATCH     FDATE,BEDTDATE
          GOTO      INVDATG IF LESS
.
          UNPACK    BEDTDATE,XCENT2,XYEAR2,XMON2,XDAY2
          MOVE      FROMDATE,ACTVFDTE
          PACK      ACTVTDTE,BEDTDATE
          GOTO      INCRDTE
.
INVDATG   DISPLAY   *P1:24,*B,*EL,"To Date must be greater than or equal to ":
                                  "From Date. ";
          CALL      HITENTER
          GOTO      BADAT2
.
INVDTE1   DISPLAY   *P1:24,*B,*EL,"From Date must be less than or equal to ":
                                  "today's date. ";
          CALL      HITENTER
          GOTO      KADAT1
.
INVDTE2   DISPLAY   *P1:24,*B,*EL,"To Date must be less than or equal to ":
                                  "today's date. ";
          CALL      HITENTER
.
BADAT2    MOVE      XDAY1,IDAY
          MOVE      XMON1,IMON
          MOVE      XYEAR1,IYEAR
          MOVE      XCENT1,ICENT
          GOTO      KADAT2
.
.       Add one to the BEDTDATE. This is because initially the program did not
.       include the end date, so it looks at all the data upto but including
.       the BEDTDATE.
.
INCRDTE   PACK      TODATE,BEDTDATE
          MOVE      IDAY,DD
          MOVE      IMON,MM
          MOVE      IYEAR,YY
          MOVE      ICENT,CC
          CALL      DMYCON
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          PACK      BEDTDATE,CC,YY,MM,DD
          REP       " 0",BEDTDATE
.
.         Keyin response to "Grand Summary Only" question
.
KSUMM     KEYIN     *P28:8,*DV,UNDLN1,*P28:8,*V2LON,SUMFLG
.
          RESET     SUMFLG
          GOTO      KSUM10 IF NOT EOS
          MOVE      "Y",SUMFLG
          DISPLAY   *P28:8,*V2LON,SUMFLG
          GOTO      NUMDAY
.
KSUM10    REP       UPPLOW,SUMFLG
          DISPLAY   *P28:8,*V2LON,SUMFLG
.
          MATCH     "Y",SUMFLG
          GOTO      NUMDAY IF EQUAL
          MATCH     "N",SUMFLG
          GOTO      KSUM20 IF EQUAL
          BEEP
          GOTO      KSUMM
.
KSUM20    MOVE      ZERO,WARDFLG
.
.******************************************************************************
.
.         Keyin Ward range if answer to "Grand Summary Only" question is (N)o
.
.         From Ward
.         ---------
.
KFWARD    CALL      SCREEN2                    * display Ward range screen
          CALL      INWRDVAR                   * initialize variables
.
          MOVE      "12",VERT                  * initialize row position
          MOVE      ZERO,QUESTFLG              * initialize "?" flag
.
KFWARD1   DISPLAY   *P22:VERT,*EL:
                    *P25:VERT,"<hit Enter for ",*V2LON,START,*HOFF," >"
.
KFWARD2   KEYIN     *P13:VERT,*DV,UNDLN3,*P13:VERT,*V2LON,FRWARD
          ENDSET    FRWARD
          APPEND    SP3,FRWARD
          RESET     FRWARD
.
          MATCH     SP3,FRWARD                 * null From Ward ?
          GOTO      KFWARD3 IF NOT EQUAL       * no
          MOVE      START,FRWRD                * yes
.
          BRANCH    QUESTFLG,KFWARD6
          DISPLAY   *P13:12,*EL,*V2LON,START
          GOTO      KTWARD
.
KFWARD3   DISPLAY   *P13:VERT,*V2LON,FRWARD
          CMATCH    QUEST,FRWARD               * "?" routine requested ?
          GOTO      KFWARD7 IF EQUAL           * yes
.
          PACK      KEY6,FRWARD,SP3            * validate From Ward
          CALL      RDWARD1
          COMPARE   ZERO,OVRCD
          GOTO      KFWARD5 IF EQUAL
.
          CALL      INVWARD                    * invalid From Ward
          BRANCH    QUESTFLG,KFWARD4
          GOTO      KFWARD2
.
KFWARD4   DISPLAY   *P1:24,*EL,"From Ward :"
          GOTO      KFWARD1
.
KFWARD5   MOVE      WBDESC,FWRDDESC            * valid From Ward
          MOVE      FRWARD,FRWRD
          BRANCH    QUESTFLG,KFWARD6
          DISPLAY   *P22:12,*EL,FWRDDESC
          GOTO      KTWARD
.
KFWARD6   CALL      REDISP                     * re-display screen after "?"
          GOTO      KTWARD
.
KFWARD7   CALL      PATWRDDS                   * Wards scan
.
          MOVE      "24",VERT
          MOVE      ONE,QUESTFLG
          GOTO      KFWARD4
.
.     *** Invalid Ward selected ***
.
INVWARD   DISPLAY   *P1:24,*EL,*B,"Invalid Ward.  ";
          CALL      HITENTER
          RETURN
+
.
.         To Ward
.         -------
.
KTWARD    MOVE      "14",VERT                  * initialize row position
          MOVE      ZERO,QUESTFLG              * initialize "?" flag
.
KTWARD1   DISPLAY   *P22:VERT,*EL:
                    *P25:VERT,"<hit Enter for ",*V2LON,FINISH,*HOFF," >"
.
KTWARD2   KEYIN     *P13:VERT,*DV,UNDLN3,*P13:VERT,*V2LON,TOWARD
          ENDSET    TOWARD
          APPEND    SP3,TOWARD
          RESET     TOWARD
.
          MATCH     SP3,TOWARD                 * null To Ward ?
          GOTO      KTWARD3 IF NOT EQUAL       * no
          MOVE      ZZZ,TOWARD                 * yes
          MOVE      FINISH,TOWRD
.
          BRANCH    QUESTFLG,KTWARD6
          DISPLAY   *P13:14,*EL,*V2LON,FINISH
          GOTO      WRDSCHK                    * go to check Ward range entered
.
KTWARD3   DISPLAY   *P13:VERT,*V2LON,TOWARD
          CMATCH    QUEST,TOWARD               * "?" routine requested ?
          GOTO      KTWARD7 IF EQUAL           * yes
.
          PACK      KEY6,TOWARD,SP3            * validate To Ward
          CALL      RDWARD1
          COMPARE   ZERO,OVRCD
          GOTO      KTWARD5 IF EQUAL
.
          CALL      INVWARD                    * invalid To Ward
          BRANCH    QUESTFLG,KTWARD4
          GOTO      KTWARD2
.
KTWARD4   DISPLAY   *P1:24,*EL,"  To Ward :"
          GOTO      KTWARD1
.
KTWARD5   MOVE      WBDESC,TWRDDESC            * valid To Ward
          MOVE      TOWARD,TOWRD
          BRANCH    QUESTFLG,KTWARD6
          DISPLAY   *P22:14,*EL,TWRDDESC
          GOTO      WRDSCHK
.
KTWARD6   CALL      REDISP                     * re-display screen after "?"
          GOTO      WRDSCHK
.
KTWARD7   CALL      PATWRDDS                   * Wards scan
.
          MOVE      "24",VERT
          MOVE      ONE,QUESTFLG
          GOTO      KTWARD4
.
.         check if Ward range is valid or not
.
WRDSCHK   MATCH     FRWARD,TOWARD
          GOTO      NUMDAY IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"To Ward must be greater than or equal to ":
                                  "From Ward. ";
          CALL      HITENTER
          GOTO      KFWARD
.
.         Ward parameters screen
.
SCREEN2   DISPLAY   *P1:12,*EF,"From Ward :":
                        *P1:14,"To   Ward :"
          RETURN
+
.
.         Initialize Ward range variables
.
INWRDVAR  MOVE      SP3,FRWARD
          MOVE      SP3,TOWARD
          MOVE      SP20,FWRDDESC
          MOVE      SP20,TWRDDESC
          RETURN
+
.
.         Re-display screen after Wards scan
.
REDISP    CALL      SCREEN1
.
          DISPLAY   *P20:4,*V2LON,XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *P20:6,*V2LON,XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2:
                    *P28:8,*V2LON,SUMFLG
.
          CALL      SCREEN2
.
          MATCH     SP3,FRWARD
          GOTO      REDISP1 IF EQUAL
          DISPLAY   *P13:12,*V2LON,FRWARD,*HOFF,*P22:12,FWRDDESC
          GOTO      REDISP2
.
REDISP1   DISPLAY   *P13:12,*V2LON,START
.
REDISP2   MATCH     ZZZ,TOWARD
          GOTO      REDISP3 IF EQUAL
          DISPLAY   *P13:14,*V2LON,TOWARD,*HOFF,*P22:14,TWRDDESC
          RETURN
.
REDISP3   DISPLAY   *P13:14,*V2LON,FINISH
          RETURN
+
.******************************************************************************
.
.      CALCULATE NODAYS FOR PERCENTAGE CALCULATION ON BED OCCUPANCY
.
NUMDAY    DAYSEP    FDATE,BEDTDATE,TOTDAY
          MOVE      TOTDAY,NODAYS       *** number days covered
.
.         DISPLAY   *P1:20," NODAYS COVERED IS : ",NODAYS;
.
.         Check if dates are OK or not
.
REKEY     DISPLAY   *P1:24,*EF,"Ok to continue (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? "
.
REKEY1    KEYIN     *P24:24,*DV,UNDLN1,*P24:24,*V2LON,ANS;
          RESET     ANS
          GOTO      REKEY2 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P24:24,*V2LON,ANS
.
          CMATCH    "N",ANS
          GOTO      START IF EQUAL
.
          CMATCH    "Y",ANS
          GOTO      CNTCHK IF EQUAL
.
REKEY2    BEEP
          GOTO      REKEY1
.
.         PROCESS START
.
CNTCHK    CALL      INITV
          CALL      INITC
.
.------ Create the Indexed Temp File ------
.
          DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Creating the Indexed Temp":
                    " File",*HOFF," - Please Wait";
.
.------ Kill the Tempfile if it Already Exists ------
.
          CLEAR     CMDLINE
          CLOSE     PATMP1XX
          APPEND    "iserase ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
.------ Build new indexed temp file ------
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          APPEND    " 9 u1-8",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      PATMP1XX,PATFNAME
.
          CALL      DAYA0000        * load the temp file from the patdayaf file
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Admission Numbers **",*W;
          DISPLAY   *P1:24,*EL,*P20:24,"Admission No :":
                    *P56:24,"Scanning :";
.
.------ position on the temp file ------
.
          MOVE      SP8,KEY8
          CALL      RSTMPR1
.
.------ read through the indexed temp file and process each admission ------
.
NEXTADM   CALL      RKTMPR1
          BRANCH    OVRCD,GETBEDS
.
          MOVE      TMPADMN,KEY8
          CALL      RDMISS1              * read the admissions file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     BEDTDATE,ADATE         * make sure admission is in date range
          GOTO      NEXTADM IF NOT LESS
.
          DISPLAY   *P68:24,AADMNO;
          MOVE      SP8,DDATE
.
          BRANCH    ASTAT,NEXTADM,CONTADM,CONTDIS,CONTADM,NEXTADM
.
CONTDIS   MOVE      AADMNO,KEY8
          CALL      RDDSCH1              * read discharge file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     FDATE,DDATE       * make sure admission is in date range
          GOTO      NEXTADM IF LESS
.
.------ we have a valid admission so extract details ------
.------ and write to summary file ------
.
CONTADM   DISPLAY   *P35:24,*V2LON,AADMNO;
          CALL      TRANS
          GOTO      NEXTADM
+
.**********************************************************************
.*                            DAYA0000                                *
.*     Process patdayaf Data writing to temp indexed file patmp1xx    *
.**********************************************************************
DAYA0000  UNPACK    ACTVTDTE,CCENT,CYEAR,TDAYMON
          PACK      TYEAR,CCENT,CYEAR
          REP       " 0",TYEAR
.
          UNPACK    FDATE,CCENT,CYEAR,SDAYMON
          PACK      SYEAR,CCENT,CYEAR
          REP       " 0",SYEAR
.
          DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Initialising the Indexed Temp":
                    " File",*HOFF," - Please Wait";
.
.------ close previous day file and set up the name of the next one ------
.
DAYA1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      DAYA4900 IF IO        * trap if file does not exist
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10    * position on daily patient list file
          CALL      RSPTDAY1
.
.------ read through the patient daily list file ------
.
DAYA2000  CALL      RKPTDAY1
          BRANCH    OVRCD,DAYA5000
.
          MATCH     TYEAR,SYEAR            * see if the current file is the one
          GOTO      DAYA3000 IF NOT EQUAL    for the last year in the date range
.
          MATCH     PTDYDATE,TDAYMON       * if so, make sure the month and day
          GOTO      DAYA9999 IF LESS         are still in range
.
DAYA3000
.------ Only need to write to the temporary file if the admission number -----
.------ is not already there ------
.
          MOVE      PTDYADMN,KEY8
          CALL      RDTMPR1                * read the temp file
          BRANCH    OVRCD,DAYA4000
          GOTO      DAYA2000
.
.------ write a new record to the temp file ------
.
DAYA4000  CALL      WRTMPR1
          GOTO      DAYA2000
.
.------ need to pop the return address off the stack if I/O error trapped -----
.
DAYA4900  NORETURN
.
.------ either file does not exist or we have reached the end of the ------
.------ current file ------
.
DAYA5000  MATCH     TYEAR,SYEAR             * test if we have reached the end
          GOTO      DAYA9999 IF NOT LESS      year in the date range
.
          MOVE      SYEAR,FORM4             * if not, increment the start year
          ADD       ONE,FORM4                 to the next year and process
          MOVE      FORM4,SYEAR               the file for this year
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
.
          GOTO      DAYA1000
.
DAYA9999  RETURN
+
.**********************************************************************
.
GETBEDS   DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "*** Processing No of Beds ***",*W;
          DISPLAY   *P1:24,*EL,*P20:24,"Ward/Bed :";
.
          BRANCH    WARDFLG,GETBEDS1          * added for V5.01.121 - SRF 105741
          MATCH     SP3,FRWARD                * From Ward = spaces ?
          GOTO      GETBEDS1 IF EQUAL         * yes - position read from start
          PACK      KEY6,FRWARD,SP3           * no - use From Ward as part of
          CALL      RDWARD1                          Key to read first record
          BRANCH    OVRCD,REPORT
          GOTO      NEXTW1
.
GETBEDS1  MOVE      SP6,KEY6
          CALL      RDSWARD1
NEXTW     CALL      RDKWARD1
          BRANCH    OVRCD,REPORT
.
NEXTW1    DISPLAY   *P32:24,*V2LON,WWARD,"/",WBED
.
          BRANCH    WARDFLG,GETBEDS3          * added for V5.01.121 - SRF 105741
          MATCH     WWARD,TOWARD              * Ignore record if ward is not in
          GOTO      REPORT IF LESS              Ward range
.
GETBEDS3  MOVE      WWARD,SAVWWARD
          MOVE      WBED,SAVWBED
.
.         Check if the ward was active during the period.  If not, then
.         ignore all records for the ward and position on the ward header
.         record for the next ward.
.
          MATCH     SP3,WBED                     * ward header record ?
          IF        @EQUAL
            MOVE      ONE,ACTVWFLG               * don't want weekend days
            PROC      WDACTVDY                   * get days ward was active
            COMPARE   ZERO,ACTVDAYS              * ward active during period ?
            IF        @EQUAL
              PACK      KEY6,WWARD,ZZZ           * no
              CALL      RDSWARD1                 * posn. on next ward header rec
              GOTO      NEXTW
            ENDIF
            MOVE      ACTVBCHG,CHNGFLAG
          ELSE
.
.         This is a ward/bed record, so if the ward has changed the number
.         of beds in the period, then use WDACTVDY to get the number of
.         active days for the bed (this routine will also return the room
.         type).  Otherwise use the number of active days for the ward (which
.         has already been calculated from the ward header record), then get
.         the latest room type for this bed using GTRMTYPE.
.
            IF        CHNGFLAG = 1
              MOVE      ONE,ACTVWFLG             * don't want weekend days
              PROC      WDACTVDY                 * get days bed was active
            ELSE
              PROC      GTRMTYPE                 * get latest room type
              BRANCH    EXIT,NEXTW               * bed inactive
            ENDIF
            MOVE      ONE,TOTBED                 * set for single bed
            GOTO      ADDBDT
          ENDIF
.
.         We are processing a ward header record (WBED = spaces)
.         If this is a ward/bed ward, then get the next record and process beds.
.
          COMPARE   ONE,WINPUT                   * ward only input ?
          GOTO      NEXTW IF NOT EQUAL
.
.         If we aren't printing zero stat wards, check if ward has stats first
.
ADDBDT    IF        PTCNH82W = 0
            PACK      KEY9,WWARD,ZZZ,ZZZ
            CALL      RDOCCP1
            IF        OVRCD = 1
              PACK      KEY6,WWARD,ZZZ           * yes
              CALL      RDSWARD1                 * posn. on next ward header rec
              GOTO      NEXTW
            ENDIF
          ENDIF
.
.         We have a ward/bed record.
.         Firstly, update grand total for room type for all wards
.
          PACK      KEY9,ZZZ,ACTVRTYP,ZZZ
          CALL      RDOCCP1                      * read grand total record
          COMPARE   ZERO,OVRCD                   * record on file ?
          CALL      NEWREC IF NOT EQUAL          * no - write new record
.
.         If this is a ward header record then it must be a ward only input,
.         so use the active bed days from WDACTVDY.  If this is a ward bed and
.         there was a change in the number of beds in the ward, then also use
.         the active bed days from WDACTVDY.  Otherwise, use the number of
.         active days for the ward for the period calculated for the ward
.         header record.
.
          MATCH     SP3,WBED
          IF        !@EQUAL & CHNGFLAG = 0
            MOVE      ACTVDAYS,ACTVBDAY
          ENDIF
          ADD       ACTVBDAY,OCDAYS
.
          ADD       TOTBED,OCBEDS                * update total
          CALL      UPOCCP1
.
.         Update total for room type for a given ward
.
          PACK      KEY9,WWARD,ACTVRTYP,ZZZ
          CALL      RDOCCP1                      * read grand total record
          COMPARE   ZERO,OVRCD                   * record on file ?
          CALL      NEWREC IF NOT EQUAL          * no - write new record
.
          ADD       TOTBED,OCBEDS                * update total
          ADD       ACTVBDAY,OCDAYS
          CALL      UPOCCP1
.
.         Update total beds for all room types in a given ward
.
          PACK      KEY9,WWARD,ZZZ,ZZZ
          CALL      RDOCCP1                      * read ward total record
          COMPARE   ZERO,OVRCD                   * record on file ?
          CALL      NEWREC IF NOT EQUAL          * no - write new record
.
          ADD       TOTBED,OCBEDS                * update total
          ADD       ACTVBDAY,OCDAYS
          MOVE      CHNGFLAG,OCCHNG
          CALL      UPOCCP1
.
.         Update total beds for all room types for all wards
.
          PACK      KEY9,ZZZ,ZZZ,ZZZ
          CALL      RDOCCP1                      * read ward total record
          COMPARE   ZERO,OVRCD                   * record on file ?
          CALL      NEWREC IF NOT EQUAL          * no - write new record
.
          ADD       TOTBED,OCBEDS                * update total
          ADD       ACTVBDAY,OCDAYS
          IF        CHNGFLAG = 1
            MOVE      ONE,OCCHNG
          ENDIF
          CALL      UPOCCP1
.
          MATCH     SP3,WBED                     * ward header record ?
          IF        @EQUAL
            PACK      KEY6,WWARD,ZZZ             * yes
            CALL      RDSWARD1                   * posn. on next ward header rec
          ENDIF
          GOTO      NEXTW                        * get next ward record
.
.       Add a new record to the occupancy file
.
NEWREC    CALL      ZEROBED
          UNPACK    KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      WROCCP1
          PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          RETURN
+
.
.             INITIALISE WORKING VARIABLES
.
INITV     MOVE      ZERO,AMTCODE
          MOVE      ZERO,BEDCODE
          MOVE      ZERO,CADAYS
          MOVE      ZERO,CAAMT
          MOVE      ZERO,SAVAMT
          MOVE      SP8,STDATE
          RETURN
+
.
.      Create an indexed file based on port
.
INDZD     MOVE      ZERO,STATUS
. 
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT
.
          CALL      KILLIDZ
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE        * "t77bnk" + PORT
          APPEND    " 131 u1-9",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MOVE      ONE,OPENFILE
.
          MOVE      ZERO,OPCND
          TRAP      NOINDZ IF IO
          OPEN      PATOCCP1,FNAMEI
          TRAPCLR   IO
          OPEN      PATOCCP2,FNAMEI   SAME AS P1 INDEX
ENDC      RETURN
.
NOINDZ    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMEI," Not Found **",*W4;
          TRAPCLR   IO
          RETURN
+
.
.         Deleting an indexed file based on port
.
KILLIDZ   MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Deleting Indexed File **",*W;
.
          CLOSE     PATOCCP1
          CLOSE     PATOCCP2
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.
.         INITIALISE OCCUPANCY FILE
.
INITC     DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialising Occupancy File **",*P1:10;
          CALL      INDZD
          BRANCH    OPCND OF ENDIT
          RETURN
+
.
.         READ TRANSFER FILE
.
TRANS     MOVE      SP8,STDATE
          MOVE      ZERO,SAVAMT
          MOVE      SP6,STMOVE
          MOVE      ZERO,STDISC
          MOVE      ZERO,STALLW
          MOVE      ZERO,STEXTRA
.
          PACK      KEY30,AADMNO,SP20,SP20
          CALL      RDSTRAN2
NEXTT     CALL      RDKTRAN2
          BRANCH    OVRCD OF XXXX
.
          MATCH     TADMN,AADMNO
          GOTO      XXXX IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          RESET     TDATE
          REP       " 0",TDATE
          RESET     TDATE
.
          CMATCH    "A",TMOVE
          GOTO      ARITYPE IF EQUAL
.
          CMATCH    "R",TMOVE
          GOTO      ARITYPE IF EQUAL
.
          CMATCH    "C",TMOVE
          GOTO      LOTYPE IF EQUAL
.
          CMATCH    "L",TMOVE
          GOTO      LOTYPE IF EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      NOSAME IF NOT EQUAL
.
          SUB       STDISC FROM SAVAMT
          SUB       STALLW FROM SAVAMT
.
          MOVE      ONE,TOTDAY
.
          MOVE      TATYPE,OCTYPE
          MOVE      SP6,OCWARD      *** OCCUPANCY WARD
          MOVE      SP6,OCROOM     *** OCCUPANCY ROOM TYPE
          MOVE      SP3,SAVWBED
.
          PACK      KEY6 WITH TWARD,TBED
          CALL      RDWARD1
          BRANCH    OVRCD,ENDSAM
.
          PROC      GTRMTYPE                     * get latest room type
.
          MOVE      WWARD,OCWARD      *** OCCUPANCY WARD
          IF        PTCNWBHB=0
            MOVE      ACTVRTYP,OCROOM
          ELSE
            MOVE      WEFRBT,OCROOM
          ENDIF
          MOVE      WBED,SAVWBED
ENDSAM
.
.      include DAY CASES IN FIGURES
.
.       DISPLAY  *P1:20,*B," DAYCASE FOR ADMISSION ",*V2LON,AADMNO
.       KEYIN    ANS,*P1:20,*EL;
.
          CALL      NEXTDX
          RETURN
.
NOSAME    MATCH     FDATE,TDATE
          RETURN    IF LESS
          MATCH     BEDTDATE,TDATE
          GOTO      XXXX IF LESS
          MOVE      BEDTDATE,TDATE
          GOTO      XXXX
.
+
.
.       End of a period - calculate statistics
.
LOTYPE    MATCH     FDATE,TDATE
          GOTO      ARITYPE IF LESS
.
          MATCH     BEDTDATE,TDATE
          GOTO      CONT8 IF LESS
.
          MOVE      BEDTDATE,WDATE1
          MOVE      STDATE,WDATE2
.
          CALL      CALC
          RETURN
.
CONT8     MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CALC
.
.       Start of a period - save variables
.
ARITYPE   MATCH     BEDTDATE,TDATE
          RETURN    IF NOT LESS
          MATCH     FDATE,TDATE
          GOTO      CONT2 IF NOT LESS
          MOVE      FDATE,STDATE
          GOTO      CONT30
.
CONT2     MOVE      TDATE,STDATE
.
CONT30    MOVE      TDISC,STDISC
          MOVE      TALLW,STALLW
          MOVE      TRATEF,SAVAMT
          ADD       TRATEH,SAVAMT
          ADD       TEXTRA,SAVAMT
.
          MOVE      TATYPE,OCTYPE
          MOVE      SP6,OCWARD      *** OCCUPANCY WARD
          MOVE      SP6,OCROOM     *** OCCUPANCY ROOM TYPE
          MOVE      SP3,SAVWBED
.
          PACK      KEY6 WITH TWARD,TBED
          CALL      RDWARD1
          BRANCH    OVRCD OF NEXTT
.
          PROC      GTRMTYPE                     * get latest room type
.
          MOVE      WWARD,OCWARD      *** OCCUPANCY WARD
          IF        PTCNWBHB=0
            MOVE      WRTYPE,OCROOM
          ELSE
            MOVE      WEFRBT,OCROOM
          ENDIF
          MOVE      WBED,SAVWBED
          GOTO      NEXTT
.
.       End of Transfer File
.
XXXX      MATCH     "L",STMOVE
          RETURN    IF EQUAL
          MATCH     " ",STMOVE
          RETURN    IF EQUAL
.
          MATCH     "D",STMOVE
          GOTO      CONT9 IF NOT EQUAL
          MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          GOTO      CONT10
.
. still admitted
.
CONT9     
........  MOVE      TDATE,WDATE1
          MOVE      BEDTDATE,WDATE1               * (TO date)
          MOVE      STDATE,WDATE2                 * admission date   (From date)
CONT10    CALL      CALC
          RETURN
+
.
.              CALCULATE DATES DIFFERENCE
.
CALC      DAYSEP    WDATE2,WDATE1,TOTDAY
.
.              READ PATIENT MASTER FILE
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          COMPARE   ONE,OVRCD
          RETURN    IF EQUAL
.
          SUB       STALLW,SAVAMT
          SUB       STDISC,SAVAMT
          MULT      TOTDAY BY SAVAMT
.
          COMPARE   ZERO,TOTDAY
          RETURN    IF EQUAL
          GOTO      NEXTDX IF NOT LESS
.
          DISPLAY   *P1:23,*EL,"*** Transfer Record Missing for ":
                    "Admission : ",*V2LON,AADMNO:
                    *HOFF," Contact I.B.A ***"
.
NEXTDX    BRANCH    WARDFLG,NEXTDX0           * added for V5.01.121 - SRF 105741
          MATCH     FRWARD,OCWARD             * Ignore writing to temp file if
          RETURN    IF LESS                     ward is not in Ward range
          MATCH     OCWARD,TOWARD
          RETURN    IF LESS
.
NEXTDX0   MOVE      OCWARD,SAVWWARD
          PACK      KEY6,SAVWWARD,SAVWBED
          CALL      RDWARD1 
.
          MOVE      ONE,ACTVWFLG                 * don't want weekend days
          PROC      WDACTVDY                     * check if ward was active
.
          COMPARE   ZERO,ACTVDAYS                * ward/bed active in period ?
          RETURN    IF EQUAL                     * no - ignore
.
.       --- --- ---   normal bed record
.
.       --- --- ZZZ   Total Bed record
.       --- ZZZ ---   Grand total Ward rec for each Type
.       --- ZZZ ZZZ   Grand total for the complete Ward
.
.       GRAND SUMMARY ON LAST PAGE IS AS ABOVE BUT WARD = ZZZ (ALL WARDS)
.
.       ZZZ --- ---   Total Line record
.       ZZZ --- ZZZ   Total Bed record
.       ZZZ ZZZ ---   Grand total Ward rec for each Type
.       ZZZ ZZZ ZZZ   Grand total for the complete Ward
.
          PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      NEWREC IF NOT EQUAL
.
          MOVE      ZERO,TOCBEDS
.
          MOVE      ZERO,TOCDAYM
          MOVE      ZERO,TOCDAYF
          MOVE      ZERO,TOCDAYI
          MOVE      ZERO,TOCDAYR                                       *I-49016
          MOVE      ZERO,TOCDAYU
          MOVE      ZERO,TOCDAYA
          MOVE      ZERO,TOCDAYC
          MOVE      ZERO,TOCDAYD
          MOVE      ZERO,TOCDAYT
.
          MOVE      ZERO,TOCAMTM
          MOVE      ZERO,TOCAMTF
          MOVE      ZERO,TOCAMTI
          MOVE      ZERO,TOCAMTR                                       *I-49016
          MOVE      ZERO,TOCAMTU
          MOVE      ZERO,TOCAMTA
          MOVE      ZERO,TOCAMTC
          MOVE      ZERO,TOCAMTD
          MOVE      ZERO,TOCAMTT
.
          MATCH     "D",STMOVE
          GOTO      NODAYCC IF NOT EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      NODAYCC IF NOT EQUAL
.
.    DAY CASES NOT INCLUDED IN OTHER STATS
.
          MOVE      ONE,TOCDAYD
          ADD       SAVAMT,TOCAMTD
          GOTO      ENDOCCP
.
NODAYCC   CMATCH    "M",PSEX
          GOTO      MALOCC IF EQUAL
.
          CMATCH    "F",PSEX
          GOTO      FELOCC IF EQUAL
.
          CMATCH    "I",PSEX                 * patient sex indeterminate
          GOTO      INLOCC IF EQUAL
.
          CMATCH    "R",PSEX                 * intersex                *I-49016
          GOTO      ISLOCC IF EQUAL                                    *I-49016
.
          MOVE      TOTDAY,TOCDAYU           * sex is unknown
          ADD       SAVAMT,TOCAMTU
          GOTO      OCAGE
.
MALOCC    MOVE      TOTDAY,TOCDAYM
          ADD       SAVAMT,TOCAMTM
          GOTO      OCAGE
.
FELOCC    MOVE      TOTDAY,TOCDAYF
          ADD       SAVAMT,TOCAMTF
          GOTO      OCAGE
.
INLOCC    MOVE      TOTDAY,TOCDAYI
          ADD       SAVAMT,TOCAMTI
.
ISLOCC    MOVE      TOTDAY,TOCDAYR                                     *I-49016
          ADD       SAVAMT,TOCAMTR                                     *I-49016
.
OCAGE     PACK      ASDATE,ADATE
.
          CALL      PATAGED                * calculate age using admission date
.
          COMPARE   CAGECOFF,PSAGE
          GOTO      CHILD IF LESS
.
          MOVE      TOTDAY,TOCDAYA
          ADD       SAVAMT,TOCAMTA
          GOTO      ENDOCCP
.
CHILD     MOVE      TOTDAY,TOCDAYC
          ADD       SAVAMT,TOCAMTC
.
ENDOCCP   MOVE      TOCDAYM,TOCDAYT
          ADD       TOCDAYF,TOCDAYT
          ADD       TOCDAYI,TOCDAYT
          ADD       TOCDAYR,TOCDAYT                                    *I-49016
          ADD       TOCDAYU,TOCDAYT
          ADD       TOCDAYD,TOCDAYT
.
          MOVE      TOCAMTM,TOCAMTT
          ADD       TOCAMTF,TOCAMTT
          ADD       TOCAMTI,TOCAMTT
          ADD       TOCAMTR,TOCAMTT                                    *I-49016
          ADD       TOCAMTU,TOCAMTT
          ADD       TOCAMTD,TOCAMTT
.
          CALL      ADDBED
.
.       --- --- ---   Normal Record
.
          CALL      UPOCCP1
.
          MOVE      OCWARD,SAVWWARD
          MOVE      OCROOM,SAVROOM
          MOVE      OCTYPE,SAVTYPE
.
.       --- --- ZZZ   Total Bed record
.
          MOVE      SAVWWARD,OCWARD
          MOVE      SAVROOM,OCROOM
          MOVE      ZZZ,OCTYPE
          PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      NEWREC IF NOT EQUAL
.
          CALL      ADDBED
          CALL      UPOCCP1
.
.       --- ZZZ ---   Grand total Ward rec for each Type
.
          MOVE      SAVWWARD,OCWARD
          MOVE      ZZZ,OCROOM
          MOVE      SAVTYPE,OCTYPE
          PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      NEWREC IF NOT EQUAL
.
          CALL      ADDBED
          CALL      UPOCCP1
.
.       --- ZZZ ZZZ   Grand total for the complete Ward
.
          MOVE      SAVWWARD,OCWARD
          MOVE      ZZZ,OCTYPE
          MOVE      ZZZ,OCROOM
          PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      NEWREC IF NOT EQUAL
.
          CALL      ADDBED
          CALL      UPOCCP1
+
.
.      GRAND TOTAL FOR LAST PAGE RECORDS
.
.       ZZZ --- ---   Total Line record
.
          MOVE      ZZZ,OCWARD
          MOVE      SAVROOM,OCROOM
          MOVE      SAVTYPE,OCTYPE
          PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      NEWREC IF NOT EQUAL
.
          CALL      ADDBED
          CALL      UPOCCP1
.
.
.       ZZZ --- ZZZ   Total Bed record
.
          MOVE      ZZZ,OCWARD
          MOVE      SAVROOM,OCROOM
          MOVE      ZZZ,OCTYPE
          PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      NEWREC IF NOT EQUAL
.
          CALL      ADDBED
          CALL      UPOCCP1
.
.       ZZZ ZZZ ---   Grand total Ward rec for each Type
.
          MOVE      ZZZ,OCWARD
          MOVE      ZZZ,OCROOM
          MOVE      SAVTYPE,OCTYPE
          PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      NEWREC IF NOT EQUAL
.
          CALL      ADDBED
          CALL      UPOCCP1
.
.       ZZZ ZZZ ZZZ   Grand total for the complete Ward
.
          MOVE      ZZZ,OCWARD
          MOVE      ZZZ,OCTYPE
          MOVE      ZZZ,OCROOM
          PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      NEWREC IF NOT EQUAL
.
          CALL      ADDBED
          CALL      UPOCCP1
          RETURN
+
.
.          SET UP BED VALUES
.
ZEROBED   MOVE      ZERO,OCBEDS
          MOVE      ZERO,OCDAYS
          MOVE      ZERO,OCCHNG
.
          MOVE      ZERO,OCDAYM
          MOVE      ZERO,OCDAYF
          MOVE      ZERO,OCDAYI
          MOVE      ZERO,OCDAYU
          MOVE      ZERO,OCDAYA
          MOVE      ZERO,OCDAYC
          MOVE      ZERO,OCDAYD
          MOVE      ZERO,OCDAYT
.
          MOVE      ZERO,OCAMTM
          MOVE      ZERO,OCAMTF
          MOVE      ZERO,OCAMTA
          MOVE      ZERO,OCAMTC
          MOVE      ZERO,OCAMTD
          MOVE      ZERO,OCAMTT
          RETURN
+
.
.     ADD UP BED TOTALS
.
ADDBED    ADD       TOCBEDS,OCBEDS
.
          ADD       TOCDAYM,OCDAYM
          ADD       TOCDAYF,OCDAYF
          ADD       TOCDAYI,OCDAYI
          ADD       TOCDAYR,OCDAYR                                     *I-49016
          ADD       TOCDAYU,OCDAYU
          ADD       TOCDAYA,OCDAYA
          ADD       TOCDAYC,OCDAYC
          ADD       TOCDAYD,OCDAYD
          ADD       TOCDAYT,OCDAYT
.
          ADD       TOCAMTM,OCAMTM
          ADD       TOCAMTF,OCAMTF
          ADD       TOCAMTI,OCAMTI
          ADD       TOCAMTR,OCAMTR                                     *I-49016
          ADD       TOCAMTU,OCAMTU
          ADD       TOCAMTA,OCAMTA
          ADD       TOCAMTC,OCAMTC
          ADD       TOCAMTD,OCAMTD
          ADD       TOCAMTT,OCAMTT
          RETURN
+
.
.              PRINT REPORT
.
REPORT    DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Generating Report **",*W2;
.
          DISPLAY   *P1:24,*EL,*P1:24," Printing Ward :":
                    *P40:24,"Bed Type :",*P60:24,"Admn Type :";
.
          MOVE      ZERO,GRSUM
          GOTO      PRTCODE
.
ENDP      PRINT     *N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          CLOSE     PATOCCP1
          CLOSE     PATOCCP2
          CALL      KILLIDZ
          GOTO      START
.
ENDNOUT   DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** No Output Generated **",*W2;
          CLOSE     PATOCCP1
          CLOSE     PATOCCP2
          CALL      KILLIDZ
          GOTO      START
+
.
.         HEADINGS FOR OUTPUT
.
HEAD      MATCH     "Y",SUMFLG
          GOTO      HEADA IF NOT EQUAL
.
          MATCH     ZZZ,WWARD
          RETURN    IF NOT EQUAL
.
HEADA     CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          PRINT     *35,"FROM : ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *55,"TO : ",XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2
          MOVE      FOUR,CLNO
.
          MATCH     ZZZ,WWARD
          GOTO      NRMWRD IF NOT EQUAL
.
          MOVE      ONE,GRSUM
          BRANCH    WARDFLG,HEADA1            * added for V5.01.121 - SRF 105741
          PRINT     *N,*31,"*** GRAND SUMMARY for Wards : ",*+,FRWRD," to ":
                           TOWRD," ***",*N
          GOTO      HEADA2
.
HEADA1    PRINT     *N,*35,"***  GRAND SUMMARY OF ALL WARDS  ***",*N
.
HEADA2    ADD       THREE,CLNO
.
          BRANCH    WARDFLG,HEADA3            * added for V5.01.121 - SRF 105741
          MOVE      "WARDS TOTAL",WBDESC
          GOTO      PRTCONT
.
HEADA3    MOVE      "ALL WARDS TOTAL",WBDESC
          GOTO      PRTCONT
.
NRMWRD    PACK      KEY6 WITH OCWARD,WBED
          CALL      RDWARD1
.
          BRANCH    GRSUM OF NRMW10
.
          MATCH     "Y",SUMFLG
          GOTO      PRTCONT IF EQUAL
.
NRMW10    PRINT     *35,"WARD : ",WWARD,*55,WBDESC,*N
          ADD       TWO,CLNO
.
PRTCONT   DISPLAY   *P18:24,*V2LON,WBDESC;
.
          MOVE      ZERO,ROOMFLAG
.
          BRANCH    OPTION OF HEAD1,HEAD2
.
HEAD1     IF       PTCNWBHB=0
            MOVE     "Room Type",PRBTRT
          ELSE
            MOVE     "Bed Type",PRBTRT
          ENDIF
.
          PRINT     *26,"No of":
                    *35,"----------- Inpatient Bed Days -----------  - All -":
                    *96,"Bed Type",*107,"Patient Mix":
                    *N,PRBTRT,"                ":
                    *26,"Beds",*35,"Male",*42,"Female":
                    *49,"Indeter",*57,"Unknown",*65,"Adult":
                    *72,"Child",*79,"D Cases":
                    *88,"Total",*96,"Occupancy":
                    *107,"     %"
          ADD       TWO,CLNO
          CALL      PAGSEP1
          RETURN
.
HEAD2     
.
          IF       PTCNWBHB=0
            MOVE     "Room Type",PRBTRT
          ELSE
            MOVE     "Bed Type",PRBTRT
          ENDIF
.
          PRINT     *25,"    -----------  -  I N P A T I E N T S -":
                    "  -----------  -- A L L --  -------------":
                    *N,*1,PRBTRT,"          ":
                    "             Male       Female   ":
                    "    Indeter      Unknown ":
                    "     Adult        Child   ":
                    "    Day Cases      Total"
          ADD       TWO,CLNO
          CALL      PAGSEP2
          RETURN
+
.
.         PRINT OCCUPANCY FILE
.
PRTCODE
.
.        OPTION 1 IS DAYS ......OPTION 2 IS AMOUNT
.
          PACK      KEY9,SP70
          CALL      RDSOCCP1
          CALL      RDKOCCP1
          BRANCH    OVRCD,ENDNOUT
.
          MOVE      ONE,OPTION
.
          MOVE      SP3,OCWARD
          MOVE      SP3,OCTYPE
          MOVE      SP3,OCROOM
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      ZERO,ROOMFLAG
.
REPOSO    PACK      KEY9 WITH OCWARD,OCROOM,OCTYPE
          MOVE      SP6,WWARD  **USED AS DUMMY VARIABLES
          MOVE      SP6,WBED
          CALL      RDSOCCP1
.
NEXTO     CALL      RDKOCCP1
          BRANCH    OVRCD OF LASTPRT
.
          COMPARE   ZERO,CPAGENO
          GOTO      NOTGTOT IF EQUAL
.
          MATCH     ZZZ,OCROOM
          GOTO      SAMEWRD IF NOT EQUAL
.
          MATCH     ZZZ,OCTYPE
          GOTO      SAMEWRD IF NOT EQUAL
.
.         CHANGED WARDS
.
LASTPRT   COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    OPTION OF ENDL1,ENDL2
.
ENDL1     MOVE      ZERO,PATMIXP
          MOVE      ZERO,BEDOCCP
.
          COMPARE   ZERO,OCDAYT
          GOTO      PRTWTS IF EQUAL
.
          COMPARE   ZERO,OCDAYS
          GOTO      PRTWTS IF EQUAL
.
          MOVE      HPCENT,PATMIXP       *** Patient Mix (Tot = 100.00)
.
          ASSIGN    ((OCDAYT*100)/OCDAYS),BEDOCCP
.
PRTWTS    BRANCH    GRSUM OF PRT10
.
          MATCH     "Y",SUMFLG
          GOTO      PRT20 IF EQUAL
.
PRT10     CALL      PAGASK1
          PRINT     *1,WBDESC,*22,OCBEDS;
          IF        OCCHNG = 1
            PRINT     *32,"*";
          ENDIF
          PRINT     *34,OCDAYM,*41,OCDAYF,*49,OCDAYI,*56,OCDAYU:
                    *64,OCDAYA,*71,OCDAYC,*79,OCDAYD,*88,OCDAYT:
                    *97,BEDOCCP," ",PCENT,*110,PATMIXP," ",PCENT
          ADD       ONE,CLNO
          CALL      PAGASK1
.
.       PRINT  BED DAY ANAYSIS
.
          MOVE      ZERO,DAYANLM
          MOVE      ZERO,DAYANLF
          MOVE      ZERO,DAYANLI
          MOVE      ZERO,DAYANLU
          MOVE      ZERO,DAYANLA
          MOVE      ZERO,DAYANLC
          MOVE      ZERO,DAYANLT
          MOVE      ZERO,DAYPERI
          MOVE      ZERO,DAYPERD
.
.         Check if there is no patients in ward
.
          COMPARE   ZERO,OCDAYT
          GOTO      PRTSTS IF EQUAL
.
          MOVE      OCDAYM,DAYWRK1
          MOVE      OCDAYM,DAYWRK2
          ADD       OCDAYF,DAYWRK2
          ADD       OCDAYI,DAYWRK2
          ADD       OCDAYR,DAYWRK2                                     *I-49016
          ADD       OCDAYU,DAYWRK2
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLM     *** male percentage
.
          MOVE      OCDAYF,DAYWRK1
          MOVE      OCDAYF,DAYWRK2
          ADD       OCDAYM,DAYWRK2
          ADD       OCDAYI,DAYWRK2
          ADD       OCDAYR,DAYWRK2                                     *I-49016
          ADD       OCDAYU,DAYWRK2
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLF     *** Female percentage
.
          MOVE      OCDAYI,DAYWRK1
          MOVE      OCDAYI,DAYWRK2
          ADD       OCDAYR,DAYWRK2                                     *I-49016
          ADD       OCDAYM,DAYWRK2	     
          ADD       OCDAYF,DAYWRK2
          ADD       OCDAYU,DAYWRK2
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLI     *** Indterminate percentage
.
.******************************************** Start of Addition        *I-49016
          MOVE      OCDAYR,DAYWRK1
          MOVE      OCDAYR,DAYWRK2
          ADD       OCDAYM,DAYWRK2	     
          ADD       OCDAYF,DAYWRK2
          ADD       OCDAYI,DAYWRK2
          ADD       OCDAYU,DAYWRK2
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLI     *** Intersex percentage
.********************************************   End of Addition        *I-49016
.
          MOVE      OCDAYU,DAYWRK1
          MOVE      OCDAYU,DAYWRK2
          ADD       OCDAYM,DAYWRK2
          ADD       OCDAYF,DAYWRK2
          ADD       OCDAYI,DAYWRK2
          ADD       OCDAYR,DAYWRK2                                     *I-49016
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLU     *** Unknown percentage
.
          MOVE      OCDAYA,DAYWRK1
          MOVE      OCDAYA,DAYWRK2
          ADD       OCDAYC,DAYWRK2
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLA     *** adult percentage
          MOVE      HPCENT,DAYANLC
          SUB       DAYANLA,DAYANLC     *** child percentage
.
          MOVE      HPCENT,DAYANLT       *** total always 100%
.
          MOVE      OCDAYA,DAYWRK1
          ADD       OCDAYC,DAYWRK1
          DIV       OCDAYT,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYPERI     *** IN-PATIENTS %
.
          MOVE      HPCENT,DAYPERD
          SUB       DAYPERI,DAYPERD
.
PRTSTS    PRINT     *N,"Bed Day Analysis",*37,DAYANLM,PCENT:
                    *44,DAYANLF,PCENT,*52,DAYANLI,PCENT,*60,DAYANLU,PCENT:
                    *67,DAYANLA,PCENT,*74,DAYANLC,PCENT,*91,DAYANLT,PCENT:
                    *N,*N,*73,"In-Patients",*88,DAYPERI,PCENT,*N:
                    *73,"Day Cases",*88,DAYPERD,PCENT
          ADD       FIVE,CLNO
          IF        OCCHNG = 1
            MATCH     ZZZ,OCWARD
            IF        @EQUAL
              PRINT     *1,"* - Some Wards ";
            ELSE
              PRINT     *1,"* - Ward ";
            ENDIF
            PRINT     "changed number of beds during period."
            ADD       ONE,CLNO
          ENDIF
PRT20     MOVE      TWO,OPTION
.
.   SET FOR REPOSITION  TO TOP OF WARD
.
          MOVE      SP3,OCROOM
          MOVE      SP3,OCTYPE
          GOTO      ENDLST
.
ENDL2     BRANCH    GRSUM OF ENDL3
.
          MATCH     "Y",SUMFLG
          GOTO      ENDL4 IF EQUAL
.
ENDL3     CALL      PAGASK2
          PRINT     *1,WBDESC,*28,OCAMTM,*41,OCAMTF,*54,OCAMTI,*67,OCAMTU:
                    *80,OCAMTA,*93,OCAMTC,*106,OCAMTD,*120,OCAMTT
          ADD       ONE,CLNO
          CALL      PAGASK2
.
ENDL4     MOVE      ONE,OPTION
.
.         PROGRAM FINISHES AT THIS POINT
.
          MATCH     ZZZ,OCWARD
          GOTO      ENDP IF EQUAL
.
ENDLST    MOVE      ZERO,ROOMFLAG
          MOVE      "60",CLNO
.
          BRANCH    OPTION OF NEXTO,REPOSO
.
.         SAMEWRD
.
.    Check if the Room Type title has been printed yet (ROOMFLAG=1)
.    If it has, and this is the Room Type summary record (OCTYPE=ZZZ)
.    then print the summary
.
SAMEWRD   COMPARE   ZERO,ROOMFLAG
          GOTO      NOTGTOT IF EQUAL
.
          MATCH     ZZZ,OCTYPE
          GOTO      NOTGTOT IF NOT EQUAL
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    OPTION OF LINTOT1,LINTOT2
.
LINTOT1   MOVE      ZERO,PATMIXP
          MOVE      ZERO,BEDOCCP
.
          COMPARE   ZERO,OCDAYT
          GOTO      PRTBTS IF EQUAL
.
          COMPARE   ZERO,OCDAYS
          GOTO      PRTBTS IF EQUAL
.
          MOVE      HPCENT,PATMIXP       *** Patient Mix (Tot = 100.00)
.
          ASSIGN    ((OCDAYT*100)/OCDAYS),BEDOCCP
.
PRTBTS    BRANCH    GRSUM,PRTB10
.
          MATCH     "Y",SUMFLG
          GOTO      LINTOT4 IF EQUAL
.
PRTB10    CALL      PAGSEP1
          PRINT     *9,"TOTAL :",*22,OCBEDS:
                    *34,OCDAYM,*41,OCDAYF,*49,OCDAYI,*56,OCDAYU:
                    *64,OCDAYA,*71,OCDAYC,*79,OCDAYD,*88,OCDAYT:
                    *97,BEDOCCP," ",PCENT,*110,PATMIXP," ",PCENT
          CALL      PAGSEP1
          GOTO      LINTOTST
.
LINTOT2   BRANCH    GRSUM OF LINTOT3
.
          MATCH     "Y",SUMFLG
          GOTO      LINTOT4 IF EQUAL
.
LINTOT3   CALL      PAGSEP2
          PRINT     *9,"TOTAL :":
                    *28,OCAMTM,*41,OCAMTF,*54,OCAMTI,*67,OCAMTU:
                    *80,OCAMTA,*93,OCAMTC,*106,OCAMTD,*120,OCAMTT
          CALL      PAGSEP2
.
LINTOTST  ADD       ONE,CLNO
LINTOT4   MOVE      ZERO,ROOMFLAG
          GOTO      NEXTO
.
.      NEW WARD
.
NOTGTOT   MOVE      OCWARD,WWARD
.
          DISPLAY   *P52:24,*V2LON,OCROOM;
.
          MATCH     SP3,OCROOM
          GOTO      NOROOM IF EQUAL
.
          IF        PTCNWBHB=1
            PACK      KEY5,CODEBT,OCROOM
          ELSE 
            PACK      KEY5 WITH CODERT,OCROOM
          ENDIF 
          CALL      RDCODE1
          BRANCH    OVRCD OF UNKROOM
          MOVE      TDESC,DDROOM
          GOTO      OKROOM
.
NOROOM    IF        PTCNWBHB = 1
            MOVE      "NO BED TYPE GIVEN           ",DDROOM
          ELSE
            MOVE      "NO ROOM TYPE GIVEN          ",DDROOM
          ENDIF
          GOTO      OKROOM
.
UNKROOM   CLEAR     DDROOM
          IF        PTCNWBHB = 1
            APPEND    "UNKNOWN BED TYPE:  ",DDROOM
          ELSE
            APPEND    "UNKNOWN ROOM TYPE: ",DDROOM
          ENDIF
          APPEND    OCROOM,DDROOM
          RESET     DDROOM
.
OKROOM    COMPARE   ZERO,CPAGENO
          GOTO      NRMLIN IF EQUAL
.
          IF        ROOMFLAG = 1 & CLNO < FIFTY5
            GOTO      SAMERM
          ENDIF
.
          MATCH     ZZZ,OCROOM
          GOTO      NRMLIN IF NOT EQUAL
.
          MOVE      "TOTALS :            ",DDROOM
.
NRMLIN    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    GRSUM OF NRML10
.
          MATCH     "Y",SUMFLG
          GOTO      NRML20 IF EQUAL
.
NRML10    COMPARE   FIFTY3,CLNO
          CALL      HEAD IF NOT LESS
          PRINT     *N,*2,DDROOM
          ADD       TWO,CLNO
.
NRML20    MOVE      ONE,ROOMFLAG
.
SAMERM    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
.         Check if this is the summary record, and if so then print summary
.         This test will only succeed when there is no admission types for
.         this room type/ward combination, and the ROOMFLAG=0 test at
.         SAMEWRD therefore succeeded.
.
          MATCH     ZZZ,OCTYPE
          GOTO      SAMEWRD IF EQUAL
.
          MATCH     SP3,OCTYPE
          GOTO      UNKTYPE IF EQUAL
.
          PACK      KEY5 WITH CODEA,OCTYPE
          CALL      RDCODE1
          BRANCH    OVRCD OF UNKTYPE
          MOVE      TDESC,DDTYPE
          GOTO      OKTYPE
.
UNKTYPE   MOVE      "UNKNOWN TYPE",DDTYPE
.
OKTYPE    DISPLAY   *P73:24,*V2LON,OCTYPE;
.
          BRANCH    OPTION OF PDAYS,PAMTS
.
PDAYS     COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
.         GET PRETOTAL VALUE FROM FILE
.
          PACK      KEY9 WITH OCWARD,OCROOM,ZZZ
          CALL      RDOCCP2
.
          MOVE      ZERO,PATMIXP
.
          COMPARE   ZERO,OCDAYT
          GOTO      PRTATS IF EQUAL
.
          COMPARE   ZERO,PRETOTAL
          GOTO      PRTATS IF EQUAL
.
          MOVE      OCDAYT,DAYWRK1
          MULT      HPCENT,DAYWRK1
          DIV       PRETOTAL,DAYWRK1
          MOVE      DAYWRK1,PATMIXP       *** Patient Mix
.
PRTATS    BRANCH    GRSUM OF TATS10
.
          MATCH     "Y",SUMFLG
          GOTO      NEXTO IF EQUAL
.
TATS10    PRINT     *9,DDTYPE,*34,OCDAYM,*41,OCDAYF,*49,OCDAYI,*56,OCDAYU:
                    *64,OCDAYA,*71,OCDAYC,*79,OCDAYD,*88,OCDAYT:
                    *110,PATMIXP," ",PCENT
          ADD       ONE,CLNO
          GOTO      NEXTO
.
PAMTS     COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    GRSUM OF TATS20
.
          MATCH     "Y",SUMFLG
          GOTO      NEXTO IF EQUAL
.
TATS20    PRINT     *9,DDTYPE,*28,OCAMTM,*41,OCAMTF,*54,OCAMTI,*67,OCAMTU:
                    *80,OCAMTA,*93,OCAMTC,*106,OCAMTD,*120,OCAMTT
.
          ADD       ONE,CLNO
          GOTO      NEXTO
.
ENDLN     RETURN
+
.
NOPAT     DISPLAY   *P10:24,*EL,*V2LON:
                    "*** No Patient Selected ***",*W3;
          GOTO      KADAT1
.
.         LAST LINE ON THE PAGE
.
PAGEND1   PRINT     "-----------------------  ------   ------ ------":
                    " ------- ------- ------ ------ ------- -------- ":
                    "---------  -----------"
          ADD       ONE,CLNO
          RETURN
+
.
.         PAGE SEPERATOR
.
PAGSEP1   PRINT     "-----------------------  ------   ------ ------":
                    " ------- ------- ------ ------ ------- -------- ":
                    "---------  -----------"
          ADD       ONE,CLNO
          RETURN
+
.
.         PAGE ASTERISKS
.
PAGASK1   PRINT     "***********************  ******   ****** ******":
                    " ******* ******* ****** ****** ******* ******** ":
                    "*********  ***********"
          ADD       ONE,CLNO
          RETURN
+
.
.         LAST LINE ON THE PAGE
.
PAGEND2   PRINT     "----------------------- ":
                    "    -----------  -----------  -----------":
                    "  -----------  -----------":
                    "  -----------  -----------  ------------"
          ADD       ONE,CLNO
          RETURN
+
.
.         PAGE SEPERATOR
.
PAGSEP2   PRINT     "----------------------- ":
                    "    -----------  -----------  -----------":
                    "  -----------  -----------":
                    "  -----------  -----------  ------------"
          ADD       ONE,CLNO
          RETURN
+
.
.         PAGE ASTERISKS
.
PAGASK2   PRINT     "*********************** ":
                    "    ***********  ***********  ***********":
                    "  ***********  ***********":
                    "  ***********  ***********  ************"
          ADD       ONE,CLNO
          RETURN
+
.
.         Subroutine to key in a valid date
.
KEYDATEX  
....
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      CCC,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      VERT,CVERT         * set up row
          MOVE      "20",CCOL          * set up collumn
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
.
          RETURN
+
.
WROCCP1   RESET     KEY9
          WRITE     PATOCCP1,KEY9;KEY9,OCBEDS:
                    OCDAYM,OCDAYF,OCDAYA,OCDAYC,OCDAYD,OCDAYT:
                    OCAMTM,OCAMTF,OCAMTA,OCAMTC,OCAMTD,OCAMTT:
                    OCDAYS,OCCHNG,OCDAYI,OCDAYR,OCDAYU,OCAMTI,OCAMTR,OCAMTU
          RETURN
.
RDOCCP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      PATOCCP1,KEY9;OCWARD,OCROOM,OCTYPE,OCBEDS:
                    OCDAYM,OCDAYF,OCDAYA,OCDAYC,OCDAYD,OCDAYT:
                    OCAMTM,OCAMTF,OCAMTA,OCAMTC,OCAMTD,OCAMTT:
                    OCDAYS,OCCHNG,OCDAYI,OCDAYR,OCDAYU,OCAMTI,OCAMTR,OCAMTU
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKOCCP1  MOVE      ZERO,OVRCD
          READKS    PATOCCP1;OCWARD,OCROOM,OCTYPE,OCBEDS:
                    OCDAYM,OCDAYF,OCDAYA,OCDAYC,OCDAYD,OCDAYT:
                    OCAMTM,OCAMTF,OCAMTA,OCAMTC,OCAMTD,OCAMTT:
                    OCDAYS,OCCHNG,OCDAYI,OCDAYR,OCDAYU,OCAMTI,OCAMTR,OCAMTU
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSOCCP1  MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      PATOCCP1,KEY9;;
          GOTO      OVERCOND IF OVER
          RETURN
.
DEOCCP1   RESET     KEY9
          DELETE    PATOCCP1,KEY9
          RETURN
.
UPOCCP1   UPDATE    PATOCCP1;OCWARD,OCROOM,OCTYPE,OCBEDS:
                    OCDAYM,OCDAYF,OCDAYA,OCDAYC,OCDAYD,OCDAYT:
                    OCAMTM,OCAMTF,OCAMTA,OCAMTC,OCAMTD,OCAMTT:
                    OCDAYS,OCCHNG,OCDAYI,OCDAYR,OCDAYU,OCAMTI,OCAMTR,OCAMTU
          RETURN
.
.         INDEX 2 DOES NOT EXIST IT IS A COPY OF INDEX 1
.
RDOCCP2   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      PATOCCP2,KEY9;*41,PRETOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
.------ I/O Routines for temp file ------
.
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKS    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKP    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          WRITE     PATMP1XX,KEY8;KEY8
          RETURN
.
UPTMPR1   UPDATE    PATMP1XX;TMPADMN
          RETURN
.
DETMPR1   RESET     KEY8
          DELETE    PATMP1XX,KEY8
          RETURN
.
ENDIT     
.
.------ Kill the Tempfiles ------
.
          IF        OPENFILE=1
            CALL      KILLIDZ
.
            CLEAR     CMDLINE
            CLOSE     PATMP1XX
            APPEND    "iserase ",CMDLINE
            APPEND    PATFNAME,CMDLINE
            RESET     CMDLINE
            EXECUTE   CMDLINE,TASKID
.
          ENDIF
          CHAIN     PGM
          STOP
+
WRTEMP1   RESET     KEY3
          WRITE     BEDTEMP1,KEY3;TEMPTYPE,TEMPBEDD
          RETURN
.
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      BEDTEMP1,KEY3;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   RESET     KEY3
          READ      BEDTEMP1,KEY3;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      BEDTEMP1,KEY3;TEMPTYPE,TEMPBEDD
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    BEDTEMP1;TEMPTYPE,TEMPBEDD
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    BEDTEMP1;TEMPTYPE,TEMPBEDD
          RETURN
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       CALCDAYS    
          INC       DATECONV
          INC       GTRMTYPE
          INC       PATAGED
          INC       WDACTVDY
          INC       PATDAYIO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PATWBHIO/INC
          INC       PATWRDDS                  * added for V5.01.121 - SRF 105741
................................................................................

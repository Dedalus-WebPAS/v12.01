. **********************************************************************
. *              P R O G R A M  S U M M A R Y                          *
. *              -------------  -------------                          *
. *                                                                    *
. * Program Name:           IBAPAT54                                   *
. *                                                                    *
. * Function:               ITEMS REGISTER LISTING                     *
. *                                                                    *
. * Date Written:           12/06/85                                   *
. *                                                                    *
. * Modifications:                                                     *
.* V10.03401  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved TFILENAM to INIT0000 and added KILL0000 on exit           *
.* V10.03.01  31/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
. *       V10.01.01  13/01/2011  J.Tan        CAR 233048               *
. *                  Mods PATTFEFD to use HF Table type                 *
. *       V9.08.01   18/04/2008  Peter Vela   CAR 135210               *
. *                  Added PTITMCSH Misc Service Charge Indicator       *
. *       V9.03.02   30/05/2003  Dean Cramer  CAR 38200                *
. *                  Fix ? on GST option                               *
. *       V9.03.01   06/05/2003  Dean Cramer  CAR 38200                *
. *                  Fix GST option - no key load on tempfile          *
. *                  Keyin for from to GST codes fixed                 *
. **********************************************************************
. *       V9.02.03   28/04/2003  Dean Cramer  CAR 38200                *
. *                  Fix print overlaps for web                        *
. *       V9.02.02   27/03/2003  Dean Cramer  CAR 36519                *
. *                  Mod for better web front end IBARSH               *
. *       V9.02.01   25/10/2001  J.Tan                                 *
. *                  Mods to print QIAMT instead of IAMT for MBS       *
. *       V5.08.B05  23/03/2000  Jill Habasque                         *
. *                  Mods to heading of report                         *
. *       V5.08.B03  08/03/2000  Steve Downing                         *
. *                  Fixed read position for IBCNUGST                  *
. *       V5.08.B02  08/02/2000  J Habasque                            *
. *                  Mods to check IBCNUGST                            *
. *       V5.08.B01  28/01/2000  J Habasque                            *
. *                  GST mods - PTITGSTA & PTITGSTC                    *
. *                V5.01.02 22/05/90 J.Tan SRF104901                   *
. *                         Add 8 new theatre classifications          *
. *                V5.01.03 22/02/94 Matt Surridge                     *
. *                         Added DISPHEAD, HEAD132 and                *
. *                         generaly upgraded standards.               *
. *                V5.01.04 11/04/94 Sandra Barcham                    *
. *                         New Keyins                                 *
. *                                                                    *
. **********************************************************************
.
          INC           STD001FD
.
          INC           STDHLPDF    
          INC           STDKWSDF    
          INC           IBAGSTFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC           PATITMFD/INC
          INC           PATCODFD/INC
          INC           PATCONFD/INC
          INC		PATTFEFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging+
.        	WORK AREA
.
ASEQ            INIT    "(ALPHA SEQUENCE)"
.
CDESC           DIM     20
CMDLINE         DIM     80
CODEA           INIT    "A "
CODECL          INIT    "CL"
.
DMSC            DIM     3
DAYRATE         DIM     7
DIM5            DIM     5
DIM10           DIM     10
DIM14           DIM     14
DIM14A          DIM     14
DIM13           DIM     13
DIM13A          DIM     13
ENDGST          DIM     6
ENDGDESC        DIM     20
FNAMEA          DIM     8
IBCNUGST        FORM    1
ITDESC          DIM     25
LINENO          FORM    3
.
NSEQ            INIT    "(CODE SEQUENCE)"
GSEQ            INIT    "(GST CODE SEQUENCE)"
.
SELNUM          FORM    1
STITEM          DIM     2
STRTGST         DIM     6
STRTDESC        DIM     20
.
THCFLG          FORM    2
THTRFEE         DIM     7
.
TEMPFILE  IFILE     SQL,FIXED=118,KEY="U1-6,7-15" 
.
XXGSTC    DIM       6        1 
XXMBS     DIM       9        7
XXGSTA    DIM       1        16
XXALPH    DIM       6        17
XXDESC    DIM       26       23
XXMFEE    FORM      12.2     49
XXMGRP    DIM       3        54
XXSCLS    DIM       3        57
XXBD01    DIM       3        60
XXBD02    DIM       3        63
XXBD03    DIM       3        66
XXBD04    DIM       3        69
XXBD05    DIM       3        72
XXBD06    DIM       3        75
XXBD07    DIM       3        78
XXBD08    DIM       3        81
XXBD09    DIM       3        84
XXBD10    DIM       3        87
XXBD11    DIM       3        90
XXBD12    DIM       3        93
XXBD13    DIM       3        96
XXBD14    DIM       3        99
XXBD15    DIM       3        102
XXBD16    DIM       3        105
.              end of record 118
.
PRGID     INIT      "IBAPAT54"
PRGNAM    INIT      "Mbs Item Register               "
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL          DISPHEAD
. 
          DISPLAY       *P56:24,"Opening patitemn";
          OPEN		PATITEM1,"patitemn"
          OPEN		PATITEM2,"patitemn"
.
          DISPLAY       *P64:24,"pattfeef";
          OPEN		PATTFEE1,"pattfeef"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEA
.
          DISPLAY       *P64:24,"patcodes";
          OPEN		PATCODE1,"patcodes"
          OPEN          CONTROLF,"controlf"
          READ          CONTROLF,ZERO;*85,IBCNUGST
          MOVE          ZERO,LINENO
.
.
.         Get the theatre indictor for claim code 0
.
          MOVE      ZERO,TCFORM6
          PACK      KEY5 WITH CODECL,ZERO,SP2
          CALL      RDCODE1
          MOVE      TCFORM6,THCFLG
.
.             SELECT OPTION
.
START   CLOSE      PATITEM1
        CLOSE      PATITEM2
        MOVE      ZERO,CPAGENO
        MOVE      ONE,CNOUNDLN
        CLOCK     TIME,CTIMEIS
.
        DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = Code Sequence":
                  *P1:6,*V2LON,TWO,*HOFF," = Alpha Sequence"
        MOVE      THREE,SELNUM
        MOVE      SEVEN,CVERT
        IF        IBCNUGST=2
          DISPLAY   *P1:7,*V2LON,SELNUM,*HOFF," = GST Code Sequence"
          ADD       ONE,CVERT
          ADD       ONE,SELNUM
        ENDIF
        DISPLAY   *P1:CVERT,*V2LON,SELNUM,*HOFF:
                         " = Theatre Charge List - Code Sequence"
        ADD       ONE,CVERT
        ADD       ONE,SELNUM
        DISPLAY   *P1:CVERT,*V2LON,SELNUM,*HOFF:
                         " = Theatre Charge List - Alpha Sequence"
        ADD       ONE,CVERT
        ADD       ONE,SELNUM
        DISPLAY   *P1:CVERT,*V2LON,SELNUM,*HOFF," = Code Sequence (New File)"
        ADD       ONE,CVERT
        ADD       ONE,SELNUM
        DISPLAY   *P1:CVERT,*V2LON,SELNUM,*HOFF," = Alpha Sequence (New File)"
.
REKEY
        KEYIN     *P1:13,"Select Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
BRNCH   IF        IBCNUGST=2
          BRANCH    OPTION OF NORITM,NORALP,NORGST,NORITM,NORALP,NEWITM,NEWALP
        ELSE 
          BRANCH    OPTION OF NORITM,NORALP,NORITM,NORALP,NEWITM,NEWALP
        ENDIF
        BEEP
        GOTO      REKEY
.
NOITEM  CALL      IBACLOCK
        CALL      HEAD132
        DISPLAY   *P1:24,*EL,*B:
                  "No MBS/Items On File. ";
        CALL      HITENTER
        PRINT     *N,*1,"No MBS/Items On File"
.
        GOTO      START
+
.
NORITM  OPEN      PATITEM1,"patitemn"
        OPEN      PATITEM2,"patitemn"
        MOVE      SP10,DIM10
        GOTO      ITEMNO
.
NEWITM  TRAP      NONEW IF IO
        OPEN      PATITEM1,"newitemn"
        OPEN      PATITEM2,"newitemn"
        TRAPCLR   IO
        MOVE      " ,New File",DIM10
.
.               PRINT VIA ITEMS NUMBER
.
ITEMNO  MOVE       SP9,KEY9
        CALL       RDSITEM1
        MOVE       "60",CLNO
.
        DISPLAY    *P25:24,*EL,"Item Number : ";
.
NEXTA   CALL       RDKITEM1
        BRANCH     OVRCD OF ENDP 
.
        CALL            DISPDA
.
        DISPLAY         *P45:24,*V2LON,ITEMNO
        GOTO            NEXTA
+
.
NORALP  OPEN      PATITEM1,"patitemn"
        OPEN      PATITEM2,"patitemn"
        MOVE      SP10,DIM10
        GOTO      ALPHANO
.
NEWALP  TRAP      NONEW IF IO
        OPEN      PATITEM1,"newitemn"
        OPEN      PATITEM2,"newitemn"
        TRAPCLR   IO
        MOVE      " ,New File",DIM10
.
.               PRINT VIA ALPHA SORT KEY
.
ALPHANO MOVE            SP20,KEY15
        CALL            RDSITEM2
        MOVE            "60",CLNO
.
        DISPLAY    *P10:24,*EL,"Alpha Sort : ":
                   *P30:24,"Item Number : ";
.
NEXTALP CALL            RDKITEM2
        BRANCH     OVRCD OF ENDP 
.
        CALL            DISPDA
        DISPLAY         *P24:24,*V2LON,IALPHA,*P50:24,ITEMNO;
        GOTO            NEXTALP
.
NONEW   CALL            IBACLOCK
        CALL            HEAD132
        DISPLAY         *P1:24,*EL,*B:
                        "New File Does Not Exist. ";
        CALL            HITENTER
        PRINT           *N,*1,"New File Does Not Exist"
.
        NORETURN
        GOTO            START
.
.******************************************************************************
.*      NORGST - Print MBS Items by GST Code                                  *
.******************************************************************************
NORGST    MOVE      "ZZZZZZZZZZZ",ENDGST
          MOVE      SP10,STRTGST
.
.          MATCH     "IBARSH",PGM
.          GOTO      NORGST20 IF EQUAL
.
          CALL      KSTGST00              * Keyin Starting GST Code
.
          CALL      KEDGST00              * Keyin Ending GST Code
          CALL      CONTQST
          BRANCH    CEXIT,NORGST20,NORGST,NORGST99
.
NORGST20  CALL      PROC0000              * Read items and write to tempfile
.
          CALL      PRTGST00              * Print report 
.
NORGST99  GOTO      START
+
.******************************************************************************
.*      KSTGST00                                                              *
.******************************************************************************
KSTGST00  DISPLAY   *P1:4,*EF,"Starting GST Code : ",*V2LON;
.
          MOVE      TWENTY1,CCOL
          MOVE      FOUR,CVERT
.
          MOVE      TWO,CKEYTYP
          MOVE      SP10,IBGSCODE
          OPEN      IBAGSTA1,"ibagstaf"
          OPEN      IBAGSTA2,"ibagstaf"
          CALL      KIBGSA00
          MATCH     UNDLN6,IBGSCODE
          IF        @EQUAL
            MOVE      "Start",STRTDESC
            MOVE      SP10,STRTGST
            DISPLAY   *P21:4,*V2LON,STRTDESC
            GOTO      KSTGST99
          ELSE
            MOVE      IBGSDESC,STRTDESC
          ENDIF
.
          MOVE      IBGSCODE,STRTGST
          DISPLAY   *P21:4,*V2LON,STRTGST,SP2,STRTDESC
.
.
KSTGST99  RETURN
+
.******************************************************************************
.*      KEDGST00                                                              *
.******************************************************************************
KEDGST00  DISPLAY   *P1:5,*EF,"Ending GST Code : ",*V2LON;
.
          MOVE      TWENTY1,CCOL
          MOVE      FIVE,CVERT
.
          MOVE      SP10,IBGSCODE
          OPEN      IBAGSTA1,"ibagstaf"
          OPEN      IBAGSTA2,"ibagstaf"
          CALL      KIBGSA00
          MATCH     UNDLN6,IBGSCODE
          IF        @EQUAL
            MOVE      "Finish",ENDGDESC
            MOVE      "ZZZZZZZZZZZ",ENDGST
            DISPLAY   *P21:5,*V2LON,ENDGDESC
            GOTO      KEDGST90
          ELSE
            MOVE      IBGSDESC,ENDGDESC
          ENDIF
. 
          MOVE      IBGSCODE,ENDGST
          DISPLAY   *P21:5,*V2LON,ENDGST,SP2,ENDGDESC
.
KEDGST90  MATCH     ENDGST,STRTGST
          GOTO      KEDGST99 IF LESS
          GOTO      KEDGST99 IF EQUAL
.
          DISPLAY   *P1:24,*V2LON,"Ending GST Code must be greater than ":
                    "Starting Code.  ",*HOFF;
          CALL      HITENTER
          GOTO      KEDGST00
.
KEDGST99  RETURN
+
.******************************************************************************
.*      PROC0000   - loop through patitemn and write to tempfile              *
.******************************************************************************
PROC0000  CALL      CREA0000
          OPEN      PATITEM1,"patitemn"
          PACK      KEY9,SP10
          CALL      RDSITEM1
PROC1000  CALL      RDKITEM1
          BRANCH    OVRCD,PROC9999
.
          MATCH     STRTGST,PTITGSTC
          GOTO      PROC1000 IF LESS
.
          MATCH     PTITGSTC,ENDGST
          GOTO      PROC1000 IF LESS
.
          MOVE      ITEMNO,XXMBS 
          MOVE      PTITGSTC,XXGSTC 
          MOVE      PTITGSTA,XXGSTA 
          MOVE      IALPHA,XXALPH
          MOVE      IDESC,XXDESC 
          MOVE      QIAMT,XXMFEE 
          MOVE      IMISGRP,XXMGRP 
          MOVE      ICLSS,XXSCLS 
          MOVE      IBAND1,XXBD01 
          MOVE      IBAND2,XXBD02 
          MOVE      IBAND3,XXBD03 
          MOVE      IBAND4,XXBD04 
          MOVE      IBAND5,XXBD05 
          MOVE      IBAND6,XXBD06 
          MOVE      IBAND7,XXBD07 
          MOVE      IBAND8,XXBD08 
          MOVE      IBAND9,XXBD09 
          MOVE      IBAND10,XXBD10 
          MOVE      IBAND11,XXBD11 
          MOVE      IBAND12,XXBD12 
          MOVE      IBAND13,XXBD13 
          MOVE      IBAND14,XXBD14 
          MOVE      IBAND15,XXBD15 
          MOVE      IBAND16,XXBD16 
          MOVE      IBAND13,XXBD14 
.
          PACK      KEY15,XXGSTC,XXMBS,SP10
          CALL      WRTEMP1
          GOTO      PROC1000
.
PROC9999  RETURN
+
.*****************************************************************************
.*                         CREA0000                                          *
.*                  Create a temp file                                       *
.*****************************************************************************
CREA0000  CALL      KILL0000                * Kill tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    " 118 U1-6,7-15",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * build the tempfile
          OPEN      TEMPFILE,FNAMEA
.
CREA9999  RETURN
+
.*****************************************************************************
.*                         KILL0000                                          *
.*                   Kill  a temp file                                       *
.*****************************************************************************
KILL0000  CLOSE     TEMPFILE                * close tempfile
          CLEAR     CMDLINE                 * clear command line
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * delete the tempfile
.
KILL9999  RETURN
+
.*****************************************************************************
.*                         PRNT0000                                          *
.*            Print the report in order of gst code                          *
.*****************************************************************************
PRTGST00  PACK      KEY15,SP20
          CALL      RSTEMP1
PRTGST10  CALL      RKTEMP1
          BRANCH    OVRCD,PRTGST99
.
          IF        LINENO=35|LINENO=0 
            IF        CPAGENO=!0
            PRINT     *1,"--------------------------------------------------":
                       "--------------------------------------------------":
                       "--------------------------------"
            ENDIF
            GOTO      PRTGST15
          ELSE
            GOTO      PRTGST20
          ENDIF
.
PRTGST15  MOVE      ZERO,LINENO
          CALL      IBACLOCK
          CALL      HEAD132
          PRINT     *20,"Starting GST Code : ",STRTDESC:
                    *80,"Ending GST Code : ",ENDGDESC
          PRINT     *1,"--------------------------------------------------":
                     "--------------------------------------------------":
                     "--------------------------------"
          PRINT     *1,"|  GST",*9,"| GST",*14,"| Item",*26,"| Alpha":
                    *33,"| Description",*61,"| MBS",*77,"|Msc":
                    *81,"|Sug",*86,"|Tht",*92,"|Tht",*98,"|Tht":
                    *104,"|Tht",*110,"|Tht",*116,"|Tht",*122,"|Tht":
                    *128,"|Tht",*132,"|"
.
          PRINT     *1,"| Code",*9,"| App",*14,"|",*26,"|",*33,"|":
                    *61,"| Fee",*77,"|Grp",*81,"|Clas",*86,"|1/":
                    *92,"|2/",*98,"|3/",*104,"|4/",*110,"|5/",*116,"|6/":
                    *122,"|7/",*128,"|8/",*132,"|"
.
          PRINT     *1,"|",*9,"|",*14,"|",*26,"|",*33,"|":
                    *61,"|",*77,"|",*81,"|",*86,"|  9":
                    *92,"| 10",*98,"| 11",*104,"| 12",*110,"| 13",*116,"| 14":
                    *122,"| 15",*128,"| 16",*132,"|"
          PRINT     *1,"--------------------------------------------------":
                     "--------------------------------------------------":
                     "--------------------------------"
.
          ADD       EIGHT,LINENO
.
PRTGST20  PRINT     *1,"| ",XXGSTC,*9,"| ",XXGSTA,*14,"| ",XXMBS:
                    *26,"|",XXALPH,*33,"|",XXDESC,*61,"|",XXMFEE:
                    *77,"|",XXMGRP,*81,"|",XXSCLS,*86,"|",XXBD01:
                    *92,"|",XXBD02,*98,"|",XXBD03,*104,"|",XXBD04:
                    *110,"|",XXBD05,*116,"|",XXBD06,*122,"|",XXBD07:
                    *128,"|",XXBD08,*132,"|"
.
          PRINT     *1,"|",*9,"|",*14,"|",*26,"|",*33,"|",*61,"|":
                    *77,"|",*81,"|",*86,"|",XXBD09,*92,"|",XXBD10:
                    *98,"|",XXBD11,*104,"|",XXBD12,*110,"|",XXBD13:
                    *116,"|",XXBD14,*122,"|",XXBD15,*128,"|",XXBD16,*132,"|"
.
          ADD       ONE,LINENO
          IF        LINENO=55
            PRINT     *1,"--------------------------------------------------":
                       "--------------------------------------------------":
                       "--------------------------------"
          ENDIF
.
          GOTO      PRTGST10
.
PRTGST99  PRINT     *1,"--------------------------------------------------":
                     "--------------------------------------------------":
                     "--------------------------------"
          PRINT     *1,"*** End of Report ***"
          RETURN
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     COMPARE      ZERO,CPAGENO
         GOTO         NOITEM IF EQUAL
.
         CALL         PAGEND
         PRINT        *N,"*** END OF REPORT ***"
         GOTO         START
+
.
.     PRINT ACTUAL DATA 
.
DISPDA    IF        IBCNUGST=2
            BRANCH    OPTION OF BASIC,BASIC,BASIC,THTRE,THTRE,BASIC,BASIC
          ELSE
            BRANCH    OPTION OF BASIC,BASIC,THTRE,THTRE,BASIC,BASIC
          ENDIF
.
.         Basic Report
.
BASIC     COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      SP10,DMSC
          MOVE      DNO,DMSC
          MOVE      ZERO,FORM1
          MOVE      PTITMSCH,FORM1
          LOAD      DMSC,FORM1,DYES
.
          MOVE      IDESC,ITDESC
          PRINT     *1,"! ",ITEMNO,*13,"! ",IALPHA,*22,"! ",ITDESC;
          IF        IBCNUGST=2
            PRINT   *53,"| ",PTITGSTA,*57,"|",PTITGSTC;
          ENDIF
          PRINT     *65,"!",QIAMT,*81,"!",IMISGRP:
                    *86,"!",ICLSS,*91,"!",IBAND1,*96,"!",IBAND2:
                    *101,"!",IBAND3,*106,"!",IBAND4,*111,"!",IBAND5:
                    *116,"!",IBAND6,*121,"!",IBAND7,*126,"!",IBAND8:
                    *132,"!":
                    *N,*1,"!",*13,"!",*22,"!";
          IF        IBCNUGST=2
            PRINT     *53,"|",*57,"|";
          ENDIF
          PRINT     *65,"! ",DMSC,*81,"!":
                    *86,"!",*91,"!",IBAND9,*96,"!",IBAND10:
                    *101,"!",IBAND11,*106,"!",IBAND12,*111,"!",IBAND13:
                    *116,"!",IBAND14,*121,"!",IBAND15,*126,"!",IBAND16:
                    *132,"!"
          ADD       TWO,CLNO 
.  
DISPEND   RETURN
.
.         Theatre Charge Report
.
THTRE     MOVE      IAMT,DIM5
          LOAD      DIM5 USING THCFLG OF IBAND1,IBAND2,IBAND3,IBAND4:
                                         IBAND5,IBAND6,IBAND7,IBAND8:
                                         IBAND9,IBAND10,IBAND11,IBAND12:
                                         IBAND13,IBAND14,IBAND15,IBAND16
.
.         Get the day case rate
.
          PACK      DIM13 WITH ZERO,SP2,SP9,ANSD
          CALL      GETTHR
.
          ADD       TFHF1,TFPAT1
          MOVE      TFPAT1,DAYRATE
.
          LOAD      DAYRATE USING OVRCD OF SP10
.
.         Get the normal rate
.
          PACK      DIM13 WITH ZERO,SP2,SP10
          CALL      GETTHR
.
          ADD       TFHF1,TFPAT1
          MOVE      TFPAT1,THTRFEE
.
          LOAD      THTRFEE USING OVRCD OF SP10
.
          MOVE      SP20,TDESC
          MATCH     SP3,ICLSS
          GOTO      PDET2 IF EQUAL
.
          PACK      KEY5 WITH CODEA,ICLSS
          CALL      RDCODE1
.
.         Print the rates
.
PDET2     COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      IDESC,ITDESC
          PRINT     *1,"! ",ITEMNO,*13,"! ",IALPHA,*22,"! ",ITDESC;
          IF        IBCNUGST=2
            PRINT     *53,"|",PTITGSTA,*57,"|",PTITGSTC;
          ENDIF
          PRINT     *65,"!   ",THTRFEE,*80,"!   ",DAYRATE:
                    *95,"! ",ICLSS,SP1,TDESC,*122,"!"
          ADD       ONE,CLNO
          RETURN
.
.         Subroutine to get the theatre fee record
.
GETTHR    PACK      KEY24 WITH DIM13,DIM5
          CALL      RDTFEE1
          COMPARE   ZERO,OVRCD
          RETURN    IF EQUAL
.
.         If we are using banding, then not found
.
          COMPARE   ZERO,THCFLG
          GOTO      OVERCOND IF NOT EQUAL
.
.         Check if item is in the theatre item file
.
          CALL      RDSTFEE1
NXTTLP    CALL      RDKTFEE1
          BRANCH    OVRCD OF OVERCOND
.
          PACK      DIM13A WITH TFCLM,TFHFUND,PTTFHTYP,TFDAYC
.
          MATCH     DIM13A,DIM13
          GOTO      OVERCOND IF NOT EQUAL
.
          MATCH     DIM5,TFENDMBS
          GOTO      NXTTLP IF LESS
.
          RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    IF        IBCNUGST=2
            BRANCH    OPTION OF PG1,PG1,PG1,PG2,PG2,PG1,PG1
          ELSE
            BRANCH    OPTION OF PG1,PG1,PG2,PG2,PG1,PG1
          ENDIF
.
PG1       PRINT     *1,"*------------------------------------------":
                       "-------------------------------------------":
                       "---------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
PG2       PRINT     *1,"*------------------------------------------":
                       "-------------------------------------------":
                       "-----------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      
          COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          IF        IBCNUGST=2
            BRANCH    OPTION,HDNAM1,HDNAM1,HDNAM1,HDNAM2,HDNAM2,HDNAM1,HDNAM1
          ELSE
            BRANCH    OPTION,HDNAM1,HDNAM1,HDNAM2,HDNAM2,HDNAM1,HDNAM1
          ENDIF
.
HDNAM1    MOVE      "MBS ITEMS  REGISTER  LISTING",PRGNAM
          GOTO      HDCNT
.
HDNAM2    MOVE      "MBS ITEMS  CHARGES  LISTING",PRGNAM
.
HDCNT     IF        IBCNUGST=2
            BRANCH    OPTION OF PRNTC,PRNTA,PRNTG,PRNTC,PRNTA,PRNTC,PRNTA
          ELSE
            BRANCH    OPTION OF PRNTC,PRNTA,PRNTC,PRNTA,PRNTC,PRNTA
          ENDIF
.
PRNTA     PACK      CPHDROPT,ASEQ,DIM10 
          GOTO      PRTCONT
.
PRNTC     PACK      CPHDROPT,NSEQ,DIM10 
          GOTO      PRTCONT
.
PRNTG     PACK      CPHDROPT,GSEQ,DIM10
.
PRTCONT   CALL      HEAD132 
          CALL      PAGEND
.
          IF        IBCNUGST=2
            BRANCH    OPTION,HD1,HD1,HD1,HD2,HD2,HD1,HD1
          ELSE
            BRANCH    OPTION,HD1,HD1,HD2,HD2,HD1,HD1
          ENDIF
.
HD1       PRINT     *1,"! Item",*13,"! Alpha",*22,"! Description";
          IF        IBCNUGST=2
            PRINT     *53,"|GST",*57,"|  GST";
          ENDIF
          PRINT     *65,"!  MBS Fee",*81,"!Msc":
                    *86,"!Sug",*91,"!Tht",*96,"!Tht",*101,"!Tht":
                    *106,"!Tht",*111,"!Tht",*116,"!Tht":
                    *121,"!Tht",*126,"!Tht":
                    *132,"!",*N:
                    *1,"!",*13,"!",*22,"!";
          IF        IBCNUGST=2
            PRINT     *53,"|App",*57,"| Code";
          ENDIF 
          PRINT     *65,"!  Msc Sv Ch",*81,"! Grp":
                    *86,"! Cls",*91,"! 1/ ",*96,"! 2/ ",*101,"! 3/ ":
                    *106,"! 4/ ",*111,"! 5/ ",*116,"! 6/ ",*121,"! 7/ ":
                    *126,"! 8/  ",*132,"!":
                    *132,"!",*N:
                    *1,"!",*13,"!",*22,"!";
          IF        IBCNUGST=2
            PRINT     *53,"|",*57,"|";
          ENDIF
          PRINT     *65,"!     ",*81,"!   ":
                    *86,"! ",*91,"!  9",*96,"!  10",*101,"!  11":
                    *106,"!  12",*111,"!  13",*116,"!  14",*121,"!  15":
                    *126,"!  16",*132,"!"
          CALL      PAGEND
.
          MOVE      SEVEN,CLNO
          RETURN
.
.         Heading for charge list
.
HD2       PRINT     *1,"! Item",*13,"! Alpha",*22,"! Description";
          IF        IBCNUGST=2
            PRINT     *53,"|GST",*57,"|  GST";
          ENDIF
          PRINT     *65,"! Default Rate",*80,"! Daycase Rate":
                    *95,"! Admission Type",*122,"!":
                    *N,*1,"|",*13,"|",*22,"|",*53,"|App",*57,"| Code":
                     *65,"|",*80,"|",*95,"|",*122,"|"
          CALL      PAGEND
.
          MOVE      SIX,CLNO
          RETURN
+
.*****************************************************************************
.*                   tempfile I/O                                            *
.*****************************************************************************
.         Fisrt index
.
RSTEMP1   READ      TEMPFILE,KEY15;;
          RETURN
.
RATEMP1   RESET     KEY15
          MOVE      ZERO,OVRCD
          READ      TEMPFILE,KEY15;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE   ZERO,OVRCD
          RESET  KEY15
          READ   TEMPFILE,KEY15;XXGSTC,XXMBS,XXGSTA,XXALPH,XXDESC,XXMFEE,XXMGRP:
                             XXSCLS,XXBD01,XXBD02,XXBD03,XXBD04,XXBD05,XXBD06:
                             XXBD07,XXBD08,XXBD09,XXBD10,XXBD11,XXBD12,XXBD13:
                             XXBD14,XXBD15,XXBD16
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXGSTC,XXMBS,XXGSTA,XXALPH,XXDESC,XXMFEE,XXMGRP:
                             XXSCLS,XXBD01,XXBD02,XXBD03,XXBD04,XXBD05,XXBD06:
                             XXBD07,XXBD08,XXBD09,XXBD10,XXBD11,XXBD12,XXBD13:
                             XXBD14,XXBD15,XXBD16
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE  TEMPFILE,KEY15;XXGSTC,XXMBS,XXGSTA,XXALPH,XXDESC,XXMFEE,XXMGRP:
                             XXSCLS,XXBD01,XXBD02,XXBD03,XXBD04,XXBD05,XXBD06:
                             XXBD07,XXBD08,XXBD09,XXBD10,XXBD11,XXBD12,XXBD13:
                             XXBD14,XXBD15,XXBD16
          RETURN
.
UPTEMP1   UPDATE    TEMPFILE;XXGSTC,XXMBS,XXGSTA,XXALPH,XXDESC,XXMFEE,XXMGRP:
                             XXSCLS,XXBD01,XXBD02,XXBD03,XXBD04,XXBD05,XXBD06:
                             XXBD07,XXBD08,XXBD09,XXBD10,XXBD11,XXBD12,XXBD13:
                             XXBD14,XXBD15,XXBD16
          RETURN
.
ENDIT    CALL      KILL0000
          CHAIN      PGM
.
         INC       STDHLPCD
         INC       STDKWSCD
         INC       IBAGKIKY
         INC       IBAGSTKY
          INC       TFILENAM                     * Generate Temp File Name
         INC       IBAGSTIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
         INC       PATCODIO/INC
         INC       PATITMIO/INC
         INC       PATTFEIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
         INCLUDE    STD001IO
.

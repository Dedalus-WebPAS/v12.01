.******************************************************************************
.* System         : Patient Billing                                           *
.* Program        : IBAADT05                                                  *
.* Description    : Upload CMBS and Provisional DRG Data (patprdaf.dat)       *
.******************************************************************************
.* Date           : 20/07/2018                                                *
.* Author         : Don Nguyen                                                *
.* Function       : Upload CMBS and Provisional DRG Data                      *
.* Modifications  :                                                           *
.*                                                                            *
.*      V11.00.01   04/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*      V10.12.00   20/07/2018  Don Nguyen       TSK 0851002                  *
.*                  Created new program                                       *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATDGWFD/INC       * DRG Codes Table
          INC       PATITMFD/INC       * Patient Item Details Table
          INC       PATPRDFD/INC       * Provisional DRG via CMBS Item Table
.
.
.         CMBS & Provisional DRG Upload file
.
UPLDFTXT  FILE      HL7, FIXED=4000    * Upload file for CMBS & Provisional DRG
.
.Name     Type     Size     Description
.----     ----     ----     -----------
XVARCMBS  DIM         9     CMBS Item Code
XVARPDRG  DIM         4     Provisional DRG 
XVARWGHT  DIM         3     Weighted Value
XVARPSEX  DIM         1     Patient Sex        
XVARSDAY  DIM         1     Patient Sex        
XVARAGEF  DIM         2     Patient Sex        
XVARAGET  DIM         3     Patient Sex        
XVARACTV  DIM         1     Patient Sex        
.
.
. ----- Program Variables -----
.
CMDLINE   DIM       100
ERRNUMB   FORM      6
FILENAME  DIM       8
FNAMEINP  DIM       150
RECADDT   FORM      6             * Records added
RECNUMB   FORM      6             * Records processed
USERID    DIM       10
PATHNAME  DIM       150
.
XFLD0001  DIM       9             * CMBS Item Code
XFLD0002  DIM       4             * Provisional DRG
XFLD0003  DIM       3             * Weighted Value
XFLD0004  DIM       1             * Patient Sex
XFLD0005  DIM       1             * Sameday Only Visit
XFLD0006  DIM       2             * Valid Age Range From (years)
XFLD0007  DIM       3             * Valid Age Range To (years)
XFLD0008  DIM       1             * Active
.
.
CRETURN   INIT      015           * carriage return character
.
PRGID     INIT      "IBAADT05"
PRGNAM    INIT      "Upload CMBS and Provisional DRG Data"
VERSION   INIT      "V12.01.00"
.
.******************************************************************************
.*                      MAIN0000                                              *
.*                  Main Program                                              *
.******************************************************************************
MAIN0000  CALL      INIT0000           * Initailize variables & open files
          BRANCH    EXIT,MAIN9100
.
          CALL      KOPT0000           * Keyin Option
          BRANCH    EXIT,MAIN9999
.
          CALL      KFIL0000           * Keyin data file path (tab delimited)
          BRANCH    EXIT,MAIN9200
.
          CALL      KUSR0000           * Keyin User ID
          BRANCH    EXIT,MAIN0000
.
          CALL      KPTH0000           * Keyin Pathname (for report output)
          BRANCH    EXIT,MAIN0000
.
          CALL      PRHD0000           * Print Header
.
          CALL      LOAD0000           * Process Upload File
          GOTO      MAIN9999
.
MAIN9100  CALL      HEAD132
          PRINT     *N,"Initialization failed - can't open ",FILENAME
          GOTO      MAIN9999
.
MAIN9200  CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
          GOTO      MAIN9999
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                           INIT0000                                         *
.*                  Initialize Variables & Open Files                         *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"patdgwaf"
          OPEN      PATDGWA1,"patdgwaf"
.
          DISPLAY   *P64:24,"patprdaf"
          MOVE      "patprdaf",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATPRDA1,"patprdaf"
          TRAPCLR   IO
          BRANCH    OVRCD,INIT9100
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9100  MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                           KOPT0000                                         *
.*                  Keyin Option                                              *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Upload CMBS and Provisional DRG Data":
                    *P1:8,"Select Option : ";
.
KOPT1000  KEYIN     *P17:8,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                           KFIL0000                                         *
.*                  Keyin Upload File Path                                    *
.******************************************************************************
KFIL0000  MOVE      ZERO,EXIT
          PACK      FNAMEINP,SP70,SP70,SP70
.
          DISPLAY   *P20:10,*EF,"CMBS and Provisional DRG Data File";
.
          KEYIN     *P1:12,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEINP
.
          PACK      FNAMEINP,FNAMEINP,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEINP
          GOTO      KFIL9000 IF EQUAL
.
          SCAN      ".txt",FNAMEINP
          IF        !@EQUAL
            SQUEEZE   FNAMEINP
            ENDSET    FNAMEINP
            APPEND    ".txt",FNAMEINP    * Add ".txt" if not already there
            APPEND    SP70,FNAMEINP
            RESET     FNAMEINP
          ENDIF
          RESET     FNAMEINP
.
.         Open file if it exists
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      UPLDFTXT,FNAMEINP
          TRAPCLR   IO
          BRANCH    OVRCD,KFIL8000
.
          CALL      GFMT0000           * Reformat upload file (tab delimited)
.
          MOVE      ZERO,EXIT
          GOTO      KFIL9999
.
KFIL8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KFIL0000
.
KFIL9000  MOVE      ONE,EXIT
.
KFIL9999  RETURN
+
.******************************************************************************
.*                           GFMT0000                     Called by: KFIL0000 *
.*                  Reformat tab delimited input file to pipe delimited       *
.******************************************************************************
GFMT0000  CLEAR     CMDLINE
          APPEND    "convpipe.us1 ",CMDLINE
          APPEND    FNAMEINP,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
GFMT9999  RETURN
+
.******************************************************************************
.*                           KUSR0000                                         *
.*                  Keyin User ID                                             *
.******************************************************************************
KUSR0000  KEYIN     *P1:14,*EL,"Keyin User ID          : ":
                    *P26:14,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                           KPTH0000                                         *
.*                  Keyin Path Name                                           *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P1:16,*EL,"Keyin Path Name        : ":
                    *P26:16,*V2LON,PATHNAME;
          PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
.
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                           LOAD0000                                         *
.*                  Load CMBS & Provisional DRG upload data                   *
.******************************************************************************
LOAD0000  MOVE      ZERO,RECNUMB
          MOVE      ZERO,RECADDT
          MOVE      ZERO,ERRNUMB
.
          OPEN      UPLDFTXT,FNAMEINP
LOAD1000  READ      UPLDFTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008
          GOTO      LOAD9000 IF OVER
.
          MATCH     SP70,XFLD0001
          GOTO      LOAD1000 IF EQUAL
          GOTO      LOAD1000 IF EOS
.
          MATCH     SP70,XFLD0002
          GOTO      LOAD1000 IF EQUAL
          GOTO      LOAD1000 IF EOS
.
          ADD       ONE,RECNUMB
.
          UNPACK    SP70,XVARCMBS,XVARPDRG,XVARWGHT,XVARPSEX,XVARSDAY:
                         XVARAGEF,XVARAGET,XVARACTV
.
          PACK      XVARCMBS,XFLD0001,SP70
          PACK      XVARPDRG,XFLD0002,SP70
          PACK      XVARWGHT,XFLD0003,SP70
          RJUSTIFY  XVARWGHT
          PACK      XVARPSEX,XFLD0004,SP70
          PACK      XVARSDAY,XFLD0005,SP70
          PACK      XVARAGEF,XFLD0006,SP70
          RJUSTIFY  XVARAGEF
          PACK      XVARAGET,XFLD0007,SP70
          RJUSTIFY  XVARAGET
          PACK      XVARACTV,XFLD0008,SP70
.
          CALL      VALD0000                * Validate data fields
          BRANCH    EXIT,LOAD1000
.
          CALL      MVPRDF00                * move values to file fields
          PACK      KEY13,PTPDCMBS,PTPDPDRG,SP70
          CALL      RAPTPRD1
          IF        OVRCD=1
            CALL      WRPTPRD1              * write new record
            ADD       ONE,RECADDT           * increment records added total
          ELSE
            CALL      PREC0000              * Print record details
            PRINT     *90,"Record already exists"
            ADD       ONE,ERRNUMB
          ENDIF
.
          GOTO      LOAD1000
.
.
LOAD9000  CLOSE     UPLDFTXT
.
          PRINT     *N,*1,"Number of Records in txt file : ",RECNUMB:
                    *N,*1,"Number of Records Added       : ",RECADDT:
                    *N,*1,"Number of Records with Error  : ",ERRNUMB
.
LOAD9999  RETURN
+
.******************************************************************************
.*                           VALD0000                                         *
.*                  Validate Data Fields                                      *
.******************************************************************************
VALD0000  MATCH     SP70,XVARCMBS
          GOTO      VALD8100 IF EQUAL
.
.         Get valid MBS code
          PACK      KEY9,XVARCMBS,SP70
          PACK      KEY17,KEY9,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,VALD8100
.
.         Get last valid DRG code
          MATCH     SP70,XVARPDRG
          GOTO      VALD8200 IF EQUAL
.
          MOVE      "999",KEY3
          PACK      KEY7,XVARPDRG,KEY3,SP10
          CALL      RSPTDGW1
          CALL      RPPTDGW1
          BRANCH    OVRCD,VALD8200
.
          MATCH     PTDWDRGC,XVARPDRG
          GOTO      VALD8200 IF NOT EQUAL
.
.         Validate Weighted Value
          MATCH     SP70,XVARWGHT
          GOTO      VALD8300 IF EQUAL
          GOTO      VALD8300 IF EOS
          TYPE      XVARWGHT                * Numeric?
          GOTO      VALD8300 IF NOT EQUAL
.
.         Validate Sex code
          MATCH     SP70,XVARPSEX
          IF        !@EQUAL
            MOVE      XVARPSEX,DIM1
            REP       "M_F_R_U_I_",DIM1
            SCAN      "_",DIM1
            GOTO      VALD8400 IF NOT EQUAL
          ENDIF
.
.         Validate Sameday Only value
          MATCH     SP70,XVARSDAY
          IF        !@EQUAL
            MOVE      XVARSDAY,DIM1
            REP       "0 1 ",DIM1
            MATCH     SP1,DIM1
            GOTO      VALD8500 IF NOT EQUAL
          ENDIF
.
.         Validate Age From value
          MATCH     SP70,XVARAGEF
          IF        !@EQUAL
            TYPE      XVARAGEF                * Numeric?
            GOTO      VALD8600 IF NOT EQUAL
          ENDIF
.
.         Validate Age To value
          MATCH     SP70,XVARAGET
          IF        !@EQUAL
            TYPE      XVARAGET                * Numeric?
            GOTO      VALD8700 IF NOT EQUAL
          ENDIF
.
.         Validate Active value
          MATCH     SP70,XVARACTV
          IF        !@EQUAL
            MOVE      XVARACTV,DIM1
            REP       "0 1 ",DIM1
            MATCH     SP1,DIM1
            GOTO      VALD8800 IF NOT EQUAL
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      VALD99999
.
VALD8100  CALL      PREC0000                  * Print record details
          PRINT     *90,"Invalid CMBS Item Code: ",XVARCMBS 
          GOTO      VALD9100
.
VALD8200  CALL      PREC0000                  * Print record details
          PRINT     *90,"Invalid Provisional DRG Code: ",XVARPDRG
          GOTO      VALD9100
.
VALD8300  CALL      PREC0000                  * Print record details
          PRINT     *90,"Invalid Weighted Value: ",XVARWGHT
          GOTO      VALD9100
.
VALD8400  CALL      PREC0000                  * Print record details
          PRINT     *90,"Invalid Sex Code: ",XVARPSEX
          GOTO      VALD9100
.
VALD8500  CALL      PREC0000                  * Print record details
          PRINT     *90,"Invalid Sameday Value: ",XVARSDAY
          GOTO      VALD9100
.
VALD8600  CALL      PREC0000                  * Print record details
          PRINT     *90,"Invalid Age From Value: ",XVARAGEF
          GOTO      VALD9100
.
VALD8700  CALL      PREC0000                  * Print record details
          PRINT     *90,"Invalid Age To Value: ",XVARAGET
          GOTO      VALD9100
.
VALD8800  CALL      PREC0000                  * Print record details
          PRINT     *90,"Invalid Active Value: ",XVARACTV
          GOTO      VALD9100
.
VALD9100  MOVE      ONE,EXIT
          ADD       ONE,ERRNUMB
VALD9999  RETURN
+
.******************************************************************************
.*                           MVPRDF00                                         *
.*                  Move work variables to file fields                        *
.******************************************************************************
MVPRDF00  MOVE      XVARCMBS,PTPDCMBS
          MOVE      XVARPDRG,PTPDPDRG
          MOVE      XVARWGHT,PTPDWGHT
          MOVE      XVARPSEX,PTPDPSEX
          MOVE      XVARSDAY,PTPDSDAY
          MOVE      XVARAGEF,PTPDAGEF
          MOVE      XVARAGET,PTPDAGET
          MOVE      XVARACTV,PTPDACTV

MVPRDF99  RETURN
+
.******************************************************************************
.*                           PREC0000                                         *
.*                  Print record data                                         *
.******************************************************************************
PREC0000  PRINT     *1,XVARCMBS,*12,XVARPDRG,*29,XVARWGHT,*45,XVARPSEX:
                    *50,XVARSDAY,*60,XVARAGEF,*70,XVARAGET,*80,XVARACTV;
PREC9999  RETURN
+
.******************************************************************************
.*                           PRHD0000                                         *
.*                  Print the report details                                  *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Date               : ",CDATE:
                    *N,"Time               : ",CTIMEIS:
                    *N,"User ID            : ",USERID:
                    *N,"Data Load file     : ",PATHNAME:
                    *N,*N,*1,"CMBS Code",*12,"Provisional DRG":
                         *29,"Weighted Value",*45,"Sex":
                         *50,"Sameday",*60,"Age From":
                         *70,"Age To",*80,"Active":
                         *90,"Error Description":
                       *N,*1,"---------",*12,"---------------":
                         *29,"--------------",*45,"---":
                         *50,"-------",*60,"--------":
                         *70,"------",*80,"------":
                         *90,"-----------------"
.
PRHD9999  RETURN
+
.******************************************************************************
          INC       STD001IO
          INC       PATITMRD
.
          INC       PATDGWIO/INC   * DRG Codes Table
          INC       PATITMIO/INC   * Patient Item Table
          INC       PATPRDIO/INC   * Provisional DRG via CMBS Item Table
.
.
.

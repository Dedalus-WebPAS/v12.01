.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAPMS23                                             *
.*                                                                            *
.*     FUNCTION:         PATIENT DAY STATISTICS                               *
.*                                                                            *
.*     DATE WRITTEN:     11/04/90                                             *
.*                                                                            *
.*     AUTHOR:           DIG                                                  *
.*                                                                            *
.*     MODIFICATIONS:    Adapted from IBAPAT45                                *
.*                                                                            *
.******************************************************************************
.* V12.00.01 20/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V10.04.01 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.02.02 27/04/2011  Saroeun Soeur CAR 241405                      *
.*                   fixed non multi hospital                                 *
.*        V10.02.01 15/04/2011 Saroeun Soeur  CAR 241405                      *
.*                  added hospital filter for multi hospital                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.04.01  19/01/2005  Peter Vela  CAR 56908                         *
.*                  Ported over from 6.12                                     *
.******************************************************************************
.*        V6.11.02  01/07/2003  CAR 39148   Steve Downing                     *
.*                  Fixed dates being merged into one date                    *
.******************************************************************************
.*        V6.07.B01 11/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD MATADBIO MATTRAFD & MATTRAIO    *
.*                    V6.06.02  28/09/1999  Steve Downing                     *
.*                              Mods to use DAYSEP                            *
.*                    V6.06.01  19/01/1999  Jill Habasque                     *
.*                              Added test for From Date to be less than today*
.*                    V6.05.04  18/11/1998  Nicole Harrington                 *
.*                              Changed to use century in DMYCON/JULCON       *
.*                    V6.05.03  28/10/1998  Nicole Harrington                 *
.*                              Changed to disp. century and use HEAD132      *
.*                    V6.04.02  30/09/97 J.Tan      SRF 622127                *
.*                              Mods to print data either no of adms & beddays*
.*                              are not zero                                  *
.*                     V6.04.01 25/03/97 J.Tan                                *
.*                              Changes for casemix funding                   *
.*                       V5.01  01/02/91 i chung          SRF 107664          *
.*                              Added "Totals Only" option                    *
.*                       V5.02  15/04/91 J.Tan            SRF 108360          *
.*                              Fixed printing totals                         *
.*                       V5.03  10/09/92 J.Tan           SRF 116979           *
.*                              Fixed to include patients with a blank adm.   *
.*                              type (Unknown type)                           *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       PATCODFD/INC
          INC       PATDAYFD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC                                       *C-56908
          INC       PATTRNFD/INC     * for onleave date
          INC       HL7BMTFD/INC
          INC       PATHSPFD/INC     * hospital details
          INC       PMSVX1FD/INC     * patient visit extension

.
.         Temporary files used by report         
.
PATFNAME  DIM       8
.
PATMP1XX  IFILE     SQL, FIXED=9, KEY="u1-8"
PSTIDZ    IFILE     SQL, FIXED=35, KEY="U1-11"
.
.----------------------------------------------------------------------------
.NAME    TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
.----    ----   ------   --------  ----------   -----------
ZDATE    DIM         8          8           1   DATE OF STATISTICS (CCYYMMDD)
ZTYPE    DIM         3          3           9   ADMISSION TYPE CODE
.
ZADMNS   FORM        6          4          12   NUMBER OF ADMISSIONS
ZDSCHS   FORM        6          4          16   NUMBER OF DSCHARGES
ZBDAYS   FORM        8          5          20   NUMBER OF BED DAYS
ZDCASES  FORM        7          5          25   NUMBER OF DAY CASES
ZSPAR    FORM        7          5          30   SPARE
.
.End of Record                             35
.
SZDATE    DIM       8        DATE OF STATISTICS (CCYYMMDD)
SZTYPE    DIM       3        ADMISSION TYPE CODE
.----------------------------------------------------------------------------
.
.         Work area                
.
ANS1      DIM       1
ANS2      DIM       2
.
CMATRN    FORM      1
CMDLINE   DIM       50
CODEA     INIT      "A "
COUNT     FORM      2
.
DATEFLG   FORM      1
DDTYPE    DIM       16
DIM4      DIM       4
DIM6      DIM       6
DIM80     DIM       80
DFRODATE  DIM       10
DTODATE   DIM       10
.
FCDATE    DIM       10
FNAMEI    DIM       8
.
MDATE     DIM       8
MFADM     FORM      6
MFBED     FORM      6
MFDIS     FORM      6
MMADM     FORM      6
MMBED     FORM      6
MMDIS     FORM      6
MSP2      INIT      "M  "
.
NBNAMEI   DIM       8
NINE5     INIT      "99999"
NINE8     INIT      "99999999"
.
OPCND     FORM      1
PSTROL    FILE      ASCII, FIXED=256
.
RECFLG    FORM      1
SEC       FORM      2
SP12      INIT      "            "
STATUS    FORM      1
STRTFLG   FORM      1
.
TIMEIS    DIM       8
TMPADMN   DIM       8
VERT      FORM      2
.
.         date and time variables         
.
A5TIME    DIM       5
D5TIME    DIM       5
DHHMM     DIM       5
MIN1      FORM      1
PHDAYS    FORM      4
.
AJDAY     FORM      3           * julian admission day
CJDAY     FORM      3           * julian discharge day
.
FDAY      DIM       2
FMON      DIM       2
FYEAR     DIM       2
FCENT     DIM       2
.
IDAY1     FORM      2
IMON1     FORM      2
IYEAR1    FORM      2
ICENT1    FORM      2
.
IDAY2     FORM      2
IMON2     FORM      2
IYEAR2    FORM      2
ICENT2    FORM      2
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
XCENT2    DIM       2
.
SDAYMON   DIM       4
SYEAR     DIM       4
.
TDAY      DIM       2
TDAYMON   DIM       4
TMON      DIM       2
TYEAR     DIM       4
TCENT     DIM       4
.
FORM82    FORM      8.2
FROMDATE  DIM       8        * from date in (ccyymmdd) format
TODATE    DIM       8        * to date in (ccyymmdd) format
IDATET    DIM       8
IDATEF    DIM       8
JDAYS     FORM      3
JYEAR     FORM      2
LYR       FORM      1
NPERIOD   FORM      6
.
.         printing variables                
.
NADMNS    FORM      6
NDSCHS    FORM      6
NBDAYS    FORM      8
NDCASES   FORM      7
.
TADMNS    FORM      6
TDSCHS    FORM      6
TBDAYS    FORM      8
TDCASES   FORM      7
.
FRCCYY    FORM      4
TOCCYY    FORM      4
DYEAR     DIM       2
DCENT     DIM       2
DCCYY     DIM       4
.
HLBMDGUP  FORM      1         1       Write Update PMI details to Datagate 1=Yes
HLBMDGUA  FORM      1         2       Write Update Alias details to Datagate
HLBMDGAD  FORM      1       105       Write Admission details to Datagate
HLBMDGDS  FORM      1       106       Write Discharge details to Datagate
HLBMDGTR  FORM      1       107       Write Transfer details to Datagate
HLBMDGRG  FORM      1       108       Write New Pt. Registration to Datagate
.
HOSNAME   DIM       50
HOSCODE   DIM       3
IBCNMHOS  FORM      1         * multiple hospital indicator
.
PRGID     INIT      "IBAPMS23"
PRGNAM    INIT      "PATIENT DAY STATISTICS"
VERSION   INIT      "V12.01.00"
.
.         start of program                
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,EIGHTY;*1,HLBMDGUP,*2,HLBMDGUA,*105,HLBMDGAD:
                                    *106,HLBMDGDS,*107,HLBMDGTR,*108,HLBMDGRG
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PATFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEI
.
.         Start of Program         
.
START     MOVE      ZERO,CPAGENO
          MOVE      SP1,ANS
.
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Detailed Report":
                    *P1:6,*V2LON,TWO,*HOFF," = Totals Only":
                    *P1:8,"Select Option :";
.
START1    KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF KDDATE,KDDATE
          BEEP
          GOTO      START1
.
.         Keyin date range
.
KDDATE    DISPLAY   *P1:14,*EF,"Enter From Date : ":
                    *P1:16,"Enter To   Date : "
.
.         ** From Date
.
KADAT1    UNPACK    SP6 INTO CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          MOVE      "20",CCOL
          MOVE      TEN4,CVERT
          DISPLAY   *P20:16,*EL,*P20:14,*EL
.
          CALL      KEYDATE
          BRANCH    OVRCD OF START
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          CALL      PACDATE
          MOVE      CPCDATE,DFRODATE
          PACK      XDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",XDATE
          MATCH     FROMDATE,XDATE
          IF        @LESS           
            DISPLAY   *P1:24,*EL,*B,*V2LON,"From Date must be less than or ":
                                         "equal to today's date.  ",*HOFF;
            CALL      HITENTER
            GOTO      KADAT1
          ENDIF
          UNPACK    FROMDATE,XCENT1,XYEAR1,XMON1,XDAY1
.
.         ** To Date
.
KADAT2    MOVE      TEN6,CVERT
          PACK      TODATE,SP10
          UNPACK    SP6 INTO CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD OF ENDAD2R
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          CALL      PACDATE
          MOVE      CPCDATE,DTODATE
          UNPACK    TODATE INTO XCENT2,XYEAR2,XMON2,XDAY2
          GOTO      CHTODT
.
ENDAD2R   MOVE      FROMDATE,TODATE
          UNPACK    TODATE INTO XCENT2,XYEAR2,XMON2,XDAY2
          UNPACK    TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DTODATE
.
.         Check if date range is ok or not
.
CHTODT    PACK      XDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",XDATE
          MATCH     TODATE,XDATE
          GOTO      INVDATS IF LESS
.
          MATCH     FROMDATE,TODATE        * 'To' date must be >= 'From' date
          GOTO      INVDATG IF LESS              
.
          GOTO      REKEY
.
INVDATS   DISPLAY   *P1:24,*EL,*B,*V2LON,"To Date must be less than or ":
                                         "equal to today's date.  ",*HOFF;
          CALL      HITENTER
          GOTO      KADAT2
.
INVDATG   DISPLAY   *P1:24,*EL,*B,*V2LON,"To Date must be greater than or ":
                                         "equal to From Date.  ",*HOFF;
          CALL      HITENTER
          GOTO      KADAT1
.
.         Accept date range ?
.
REKEY     CALL      KHOS0000            * keyin hospital (jay)
          BRANCH    EXIT,REKEY
.
          KEYIN     *P1:24,*EF,"Proceed with current options? (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS
          GOTO      REKEY1 IF NOT EQUAL
          DISPLAY   *P1:24,*EL
          GOTO      KADAT1
.
REKEY1    MATCH     ANSY,ANS
          GOTO      CORDATE IF EQUAL
          BEEP
          GOTO      REKEY
.
.         display date range accepted
.
CORDATE   BRANCH    OPTION,CORDATE1
          DISPLAY   *P30:6;
          GOTO      CORDATE2
.
CORDATE1  DISPLAY   *P30:5;
.
CORDATE2  DISPLAY   "from ",*V2LON,DFRODATE:
                    *HOFF," to ":
                    *V2LON,DTODATE,*P1:10,*EF
.
.         Calculate the number of days in the period
.
          DAYSEP    FROMDATE,TODATE,NPERIOD
          ADD       ONE,NPERIOD
.
.         Create temporary indexed files
.
          DISPLAY   *P1:24,*EL,*P17:24,*V2LON,"** Creating Temp Indexed":
                    " Files - ",*BLINKON,"Please wait",*HOFF,*V2LON," **";
.
          CALL      INDZD
.
.         Kill the temp file if it already exists 
.
          CALL      KILLIDF
.
.         Build new temp indexed file 
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          APPEND    " 9 u1-8",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      PATMP1XX,PATFNAME
.
.         Data extraction
.
          CALL      DAYA0000        * load the temp file from the patdayaf file
.
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report 1":
                    *HOFF,*V2LON," **",*HOFF:
                    *P35:24,"Admission :",*P60:24,"Scanning :";
.
.         Position on the temp file 
.
          MOVE      SP8,KEY8
          CALL      RSTMPR1
.
.         Read through the indexed temp file and process each admission 
.
NEXTADM   CALL      RKTMPR1
          BRANCH    OVRCD,READDA
.
          MOVE      TMPADMN,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RDPTMIS1             * read the admissions file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     ADATE,TODATE         * make sure admission is in date range
          GOTO      NEXTADM IF LESS
.
          DISPLAY   *P71:24,AADMNO;
          MOVE      SP8,DDATE
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,AADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      NEXTADM IF NOT EQUAL   
              ELSE
                GOTO      NEXTADM   
              ENDIF
            ENDIF
          ENDIF
.
          BRANCH    ASTAT,NEXTADM,CONTADM,CONTDIS,CONTADM,NEXTADM
.
CONTDIS   MOVE      AADMNO,KEY8
          CALL      RDDSCH1              * read discharge file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     FROMDATE,DDATE       * make sure discharge is in date range
          GOTO      NEXTADM IF LESS        
.
.         We have a valid admission so extract details & write to summary file 
.
CONTADM   DISPLAY   *P47:24,*V2LON,AADMNO;
          CALL      TRANS
          GOTO      NEXTADM
+
.**********************************************************************
.*                            DAYA0000                                *
.*     Process patdayaf Data writing to temp indexed file patmp1xx    *
.**********************************************************************
.
DAYA0000  UNPACK    TODATE,CCENT,CYEAR,TDAYMON
          PACK      TYEAR,CCENT,CYEAR
          REP       " 0",TYEAR
.
          UNPACK    FROMDATE,CCENT,CYEAR,SDAYMON
          PACK      SYEAR,CCENT,CYEAR
          REP       " 0",SYEAR
.
          DISPLAY   *P1:24,*EL,*P14:24,*V2LON,"** Initializing the Temp ":
                    "Indexed File - ",*BLINKON,"Please wait",*HOFF,*V2LON," **";
.
.------ close previous day file and set up the name of the next one ------
.
DAYA1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      DAYA4900 IF IO        * trap if file does not exist
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10    * position on daily patient list file
          CALL      RSPTDAY1
.
.------ read through the patient daily list file ------
.
DAYA2000  CALL      RKPTDAY1
          BRANCH    OVRCD,DAYA5000
.
          MATCH     TYEAR,SYEAR            * see if the current file is the one
          GOTO      DAYA3000 IF NOT EQUAL    for the last year in the date range
.
          MATCH     PTDYDATE,TDAYMON       * if so, make sure the month and day
          GOTO      DAYA9999 IF LESS         are still in range
.
.------ Only need to write to the temporary file if the admission number -----
.------ is not already there ------
.
DAYA3000  MOVE      PTDYADMN,KEY8
          CALL      RDTMPR1                * read the temp file
          BRANCH    OVRCD,DAYA4000
          GOTO      DAYA2000
.
.------ write a new record to the temp file ------
.
DAYA4000  CALL      WRTMPR1
          GOTO      DAYA2000
.
.------ need to pop the return address off the stack if I/O error trapped -----
.
DAYA4900  NORETURN
.
.------ either file does not exist or we have reached the end of the ------
.------ current file ------
.
DAYA5000  MATCH     TYEAR,SYEAR             * test if we have reached the end 
          GOTO      DAYA9999 IF NOT LESS      year in the date range
.
          MOVE      SYEAR,FORM4             * if not, increment the start year
          ADD       ONE,FORM4                 to the next year and process
          MOVE      FORM4,SYEAR               the file for this year
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
.
          GOTO      DAYA1000
.
DAYA9999  RETURN
+
.**********************************************************************
.
.         Subroutine to process the transfer file for this person
.
TRANS     MOVE      FROMDATE,IDATEF
          MOVE      SP1,STMOVE
          MOVE      ATYPE,STATYPE
.
          PACK      KEY30 WITH AADMNO,SP20
          CALL      RDSTRAN2
NEXTTRN   CALL      RDKTRAN2
          BRANCH    OVRCD OF ENDTRN
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,TADMN,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      NEXTTRN IF NOT EQUAL
              ELSE
                GOTO      NEXTTRN
              ENDIF
            ENDIF
          ENDIF
.          
          MATCH     TADMN,AADMNO
          GOTO      ENDTRN IF NOT EQUAL
.
.         TMOVE means A=Admitted
.                     D=Discharged
.                     C=Changed Details
.                     L=Leave Hospital
.                     R=Return to Hospital
.
          MOVE      TDATE,IDATET
.
.         If this is an Admission or Return record, then just save the details
.
          CMATCH    ANSA,TMOVE
          GOTO      SAVETRN IF EQUAL
.
          CMATCH    ANSR,TMOVE
          GOTO      SAVETRN IF EQUAL
.
.         If this is not a Change record, then write/display a record
.
          CMATCH    ANSC,TMOVE
          GOTO      FNDTRN IF NOT EQUAL
.
.         If the Admission type has not changed, then ignore the record
.
          MATCH     TATYPE,STATYPE
          GOTO      NEXTTRN IF EQUAL
.
.         If this records date is greater than or equal to the starting date,
.         then generate an accomodation record.
.
FNDTRN    MATCH     IDATEF,IDATET
          CALL      CALC IF NOT LESS
.
.         If this is the discharge record, then we have finished
.
          CMATCH    ANSD,TMOVE
          RETURN    IF EQUAL
.
.         Save the Admission type
.
SAVETRN   MOVE      TATYPE,STATYPE
          MOVE      TMOVE,STMOVE
          MOVE      TDATE,IDATEF
.
.         Is this starting date less than the start date of the period?
.
          MATCH     FROMDATE,IDATEF
          GOTO      NEXTTRN IF NOT LESS
.
          MOVE      FROMDATE,IDATEF
          GOTO      NEXTTRN
.
.         End of this admissions records in transfer file. No Discharge record
.
ENDTRN    COMPARE   FOUR,ASTAT
          RETURN    IF EQUAL            * Last record was an "L" record
.
          MOVE      "999999",IDATET     * Dummy Value
          MOVE      "999999",TDATE      * Dummy Value
          MOVE      SP1,TMOVE           * Dummy Value
.
          MATCH     IDATEF,IDATET
          CALL      CALC IF NOT LESS
          RETURN
+
.
.         calculates the statistics for the date range specified - Option 1
.
CALC      MOVE      STATYPE,ZTYPE
.
          MATCH     IDATEF,IDATET
          GOTO      NEXTZ IF NOT EQUAL
.
.         The only same day periods we are interested in are those that
.         include the "A" record and/or the "D" record.
.
.         As a special case, we are only interested in the one with the
.         "D" record if this is a day case (eg A-C, C-D should ignore the
.         A-C for day cases).
.
          CMATCH    ANSD,TMOVE
          GOTO      NEXTZ IF EQUAL
.
          MATCH     ADATE,DDATE
          RETURN    IF EQUAL
.
          CMATCH    ANSA,STMOVE
          RETURN    IF NOT EQUAL
.
.         Loop over the date range, and for each day calculate the stats
.
NEXTZ     MATCH     IDATEF,TODATE
          RETURN    IF LESS
.
.         initialize the data values for this date
.
          MOVE      ZERO,NADMNS
          MOVE      ZERO,NDCASES
          MOVE      ZERO,NDSCHS
          MOVE      ONE,NBDAYS
.
.         Check if this is the last date in the period
.
          MATCH     IDATEF,TDATE
          GOTO      CNTAA IF NOT EQUAL
.
.         Check if this is the Discharge record
.
          CMATCH    ANSD,TMOVE
          GOTO      CNTAAD IF EQUAL
.
.         Check if this is an On-leave record or a Change record
.
          CMATCH    ANSC,TMOVE
          GOTO      CNTAAL IF EQUAL
.
          CMATCH    ANSL,TMOVE
          GOTO      CNTAAL IF EQUAL
          GOTO      CNTAA
.
.         Discharged today - not a bed day
.
CNTAAD    MOVE      ONE,NDSCHS
.
.         If we went on leave today, or if we changed admission type today
.         then don't count this as a bed day (for change in admission type,
.         today is the start of the next period).
.
CNTAAL    MOVE      ZERO,NBDAYS
.
.         Check if this is the admission date
.
CNTAA     UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      XDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0" IN XDATE
.
          MATCH     XDATE,IDATEF
          GOTO      WRDDD IF NOT EQUAL
.
.         Patient admitted today. Check if they were also discharged today
.
          MATCH     ADATE,DDATE
          GOTO      CHKAMVE IF NOT EQUAL
.
.         Day case - Note test above which means that we are processing the
.                    "D" record. This guarentee's this code is only run once.
.
          MOVE      ONE,NDCASES
          MOVE      ZERO,NBDAYS
          MOVE      ONE,NADMNS
          GOTO      WRDDD
.
.         Make sure this is the period that starts with the A record
.         This is to allow for the case A-L-R all on the admission date
.
CHKAMVE   CMATCH    ANSA,STMOVE
          GOTO      WRDDD IF NOT EQUAL
.
          MOVE      ONE,NADMNS
.
.         Update statistics file
.
WRDDD     MOVE      IDATEF,ZDATE
          PACK      KEY11 WITH ZDATE,ZTYPE
          CALL      RDZIND
          BRANCH    OVRCD OF WRZZZ
.
          ADD       NDSCHS,ZDSCHS
          ADD       NDCASES,ZDCASES
          ADD       NADMNS,ZADMNS
          ADD       NBDAYS,ZBDAYS
          CALL      UPZIND
          GOTO      CNTACCR
.
.         Write a new record to statistics file
.
WRZZZ     MOVE      NADMNS,ZADMNS
          MOVE      NDSCHS,ZDSCHS
          MOVE      NBDAYS,ZBDAYS
          MOVE      NDCASES,ZDCASES
          CALL      WRZIND
.
.         Calculate the next date, and check if it is still in date range
.
CNTACCR   MATCH     IDATEF,IDATET
          RETURN    IF EQUAL
.
          CALL      CALCDTE
.
.         Check if this date is in range
.
          MATCH     IDATEF,IDATET
          RETURN    IF LESS
.
          GOTO      NEXTZ
.
.         Recalculate date as date plus 1 day.
.
CALCDTE   UNPACK    IDATEF,CCENT,CYEAR,CMON,CDAY
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON
.
.         Add one to the julian date, and convert back to yymmdd
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          PACK      IDATEF,CC,YY,MM,DD
          REP       " 0",IDATEF
          UNPACK    IDATEF,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FCDATE
          REP       " 0",FCDATE
          RETURN
+
.
.         Read and print data from temp indexed file
.
READDA    CALL      HEADA
          CALL      ZEROT
          MOVE      ZERO,STRTFLG
          MOVE      ZERO,RECFLG
.
          COMPARE   TWO,OPTION
          GOTO      READDA1 IF EQUAL
          DISPLAY   *P35:24,*EL,*P40:24,"Generating for";
.
READDA1   MOVE      SP8,SZDATE
          MOVE      SP3,SZTYPE
.
NEXTT     PACK      KEY11 WITH SZDATE,SZTYPE
          CALL      RDSZIND
          CALL      RDKZIND
          BRANCH    OVRCD OF ENDREAD
.
          MATCH     NINE8,ZDATE
          GOTO      ENDREAD IF EQUAL
          GOTO      DATA1
.
ENDREAD   MOVE      TWO,STRTFLG
          GOTO      PRTHD1
.
DATA1     BRANCH    STRTFLG OF NEXT1
          MOVE      ZDATE,SZDATE
          MOVE      ZTYPE,SZTYPE          * type save here
          MOVE      ONE,STRTFLG
.
NEXT1     MATCH     ZDATE,SZDATE
          GOTO      PRTHD1 IF NOT EQUAL
.
NEXT11    CALL      PRNTDX
          GOTO      NEXTT
.
.         Print detail line(s) for each day
.
PRNTDX    COMPARE   TWO,OPTION                  * "Totals Only" option ?
          GOTO      ENDPDX1 IF EQUAL            * yes
.
.         Individual totals
.
          MOVE      ONE,RECFLG
          MOVE      SP20,TDESC
          PACK      KEY5 WITH CODEA,ZTYPE
          CALL      RDCODE1
          MOVE      TDESC,DDTYPE
.
          COMPARE   "55",CLNO
          GOTO      PRNTDX1 IF LESS
          CALL      HEADA
          GOTO      PRNTDX2
.
PRNTDX1   COMPARE   ZERO,DATEFLG
          GOTO      PRNTDX3 IF NOT EQUAL
.
PRNTDX2   UNPACK    ZDATE INTO XCENT,XYEAR,XMON,XDAY
          MOVE      XCENT,ICENT
          MOVE      XDAY,IDAY
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
.
          DISPLAY   *P55:24,*V2LON,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
          PRINT     *20,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *32,"| ",DDTYPE:
                    *51,"| ",ZBDAYS,*65,"| ",ZADMNS:
                    *80,"| ",ZDSCHS,*95,"| ",ZDCASES,*110,"|"
.
          MOVE      ONE,DATEFLG
          GOTO      ENDPDX
.
PRNTDX3   PRINT     *20,"|",*32,"| ",DDTYPE:
                    *51,"| ",ZBDAYS,*65,"| ",ZADMNS:
                    *80,"| ",ZDSCHS,*95,"| ",ZDCASES,*110,"|"
.
ENDPDX    ADD       ONE,CLNO
          ADD       ZADMNS,TADMNS
          ADD       ZDSCHS,TDSCHS
          ADD       ZBDAYS,TBDAYS
          ADD       ZDCASES,TDCASES
.
ENDPDX1   MOVE      ZADMNS,NADMNS
          MOVE      ZDSCHS,NDSCHS
          MOVE      ZBDAYS,NBDAYS
          MOVE      ZDCASES,NDCASES
.
          MOVE      ZDATE,SZDATE          * date save here
          MOVE      ZTYPE,SZTYPE          * type save here
          MOVE      NINE8,ZDATE
          PACK      KEY11 WITH ZDATE,SZTYPE
          CALL      RDZIND
          BRANCH    OVRCD OF WRZ999
.
          ADD       NADMNS,ZADMNS
          ADD       NDSCHS,ZDSCHS
          ADD       NBDAYS,ZBDAYS
          ADD       NDCASES,ZDCASES
          CALL      UPZIND
          RETURN
.
WRZ999    MOVE      NADMNS,ZADMNS
          MOVE      NDSCHS,ZDSCHS
          MOVE      NBDAYS,ZBDAYS
          MOVE      NDCASES,ZDCASES
          CALL      WRZIND
          RETURN
+
.
.         end of temp indexed file
.
PRTHD1    COMPARE   TWO,OPTION
          GOTO      PRTHD2 IF EQUAL
.
          IF        TBDAYS = 0 & TADMNS = 0 & TDSCHS = 0
            GOTO      PRTHD2
          ENDIF
.
          CALL      PRNTOT1
          CALL      PAGSEP
.
PRTHD2    BRANCH    STRTFLG OF NEXT11,ENDP
          GOTO      NEXT11
.
.         print sub-total for each day
.
PRNTOT1   COMPARE   "57",CLNO
          GOTO      PRNTOT11 IF LESS
          CALL      HEADA
          GOTO      PRNTOT12
.
PRNTOT11  CALL      PAGTOT
          PRINT     *20,"|";
          GOTO      PRNTOT13
.
PRNTOT12  PRINT     *20,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
PRNTOT13  PRINT     *32,"|          TOTAL":
                    *51,"| ",TBDAYS,*65,"| ",TADMNS:
                    *80,"| ",TDSCHS,*95,"| ",TDCASES,*110,"|"
.
          ADD       ONE,CLNO
          CALL      ZEROT
          RETURN
+
.
.         Headings routine for Report 1
.
HEADA     CALL      IBACLOCK
          COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          CALL      HEAD132
          ADD       ONE,CLNO
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital Id :",HOSNAME
            ADD       ONE,CLNO 
          ENDIF
.
          PRINT     *40,"From : ",DFRODATE:
                    *58,"To : ",DTODATE,*N
.
          CALL      PAGEND
.
          COMPARE    TWO,OPTION
          GOTO       HEADA1 IF EQUAL
.
          PRINT     *20,"| Date",*32,"| Patient Class.",*51,"| Beds Days":
                    *65,"| Admissions",*80,"| Discharges":
                    *95,"| Day Cases",*110,"|"
          GOTO      HEADA2
.
HEADA1    PRINT     *20,"|",*32,"| Patient Class.",*51,"| Beds Days":
                    *65,"| Admissions",*80,"| Discharges":
                    *95,"| Day Cases",*110,"|"
.
HEADA2    CALL      PAGEND
          MOVE      EIGHT,CLNO
          RETURN
+
.
.         Over condition found..print last line of -- and display message
.
ENDP      COMPARE   TWO,OPTION
          GOTO      ENDP2 IF EQUAL
.
ENDP1    
.         COMPARE   ZERO,ZBDAYS
.
          COMPARE   ZERO,RECFLG
          GOTO      NODATA IF EQUAL
.
ENDP2     CALL      ZEROT
          PACK      KEY11 WITH NINE5,SP10      * to make sure we include a blank
          CALL      RDSZIND                   * adm.type
.
NEXTEDP   CALL      RDKZIND
          BRANCH    OVRCD OF ENDP3
.
          MATCH     NINE8,ZDATE
          GOTO      ENDP3 IF NOT EQUAL
.
          MOVE      SP20,TDESC
          PACK      KEY5 WITH CODEA,ZTYPE
          CALL      RDCODE1
          MOVE      TDESC,DDTYPE
.
          COMPARE   "55",CLNO
          GOTO      NEXTEDP1 IF LESS
          CALL      HEADA
          GOTO      NEXTEDP2
.
NEXTEDP1  COMPARE   ZERO,DATEFLG
          GOTO      TOTPRT2 IF NOT EQUAL
.
.         print grand totals
.
NEXTEDP2  PRINT     *20,"|  TOTALS",*32,"| ",DDTYPE:
                    *51,"| ",ZBDAYS,*65,"| ",ZADMNS:
                    *80,"| ",ZDSCHS,*95,"| ",ZDCASES,*110,"|"
.
          MOVE      ONE,DATEFLG
          GOTO      CNTTTE
.
TOTPRT2   PRINT     *20,"|",*32,"| ",DDTYPE:
                    *51,"| ",ZBDAYS,*65,"| ",ZADMNS:
                    *80,"| ",ZDSCHS,*95,"| ",ZDCASES,*110,"|"
.
CNTTTE    ADD       ONE,CLNO
.
          ADD       ZADMNS,TADMNS
          ADD       ZDSCHS,TDSCHS
          ADD       ZBDAYS,TBDAYS
          ADD       ZDCASES,TDCASES
          GOTO      NEXTEDP
.
.         No data available
.
NODATA    DISPLAY   *P1:24,*EL,*B,*V2LON,*P23:24:
                    "*** No Day Statistics available ***",*W3;
.
          GOTO      ENDP3B
+
.
.         print final total and daily average
.
ENDP3     BRANCH    OPTION,ENDP3A
          IF        TBDAYS = 0 & TADMNS = 0
            GOTO      NODATA
          ENDIF
.
ENDP3A    COMPARE   "51",CLNO
          GOTO      ENDP3A1 IF LESS
          CALL      HEADA
          GOTO      ENDP3A2
.
ENDP3A1   CALL      PAGASK
.
ENDP3A2   PRINT     *20,"|TOTAL ",DFRODATE:
                    " to ",DTODATE:
                    *51,"| ",TBDAYS,*65,"| ",TADMNS:
                    *80,"| ",TDSCHS,*95,"| ",TDCASES,*110,"|"
.
.         Calculate daily average
.
          MOVE      TBDAYS,FORM82
          DIV       NPERIOD,FORM82
.
          PRINT     *20,"|  Daily Average",*51,"| ",FORM82,*65,"|",*80,"|":
                    *95,"|",*110,"|"
          CALL      PAGASK
.
ENDP3B    PRINT     *N,*N,"  Note: The Bed Days column does not include":
                          " day cases":
                    *N,*N,"  *** End of Report 1 ***"
.
ENDP4     CLOSE     PSTIDZ
          CALL      KILLIDF
          CALL      KILLIDZ
          GOTO      START
.
.         Create an indexed file based on port
.         For accumulating admission type totals
.         Grand total has zdate=999999
.
INDZD     MOVE      ZERO,ZADMNS
          MOVE      ZERO,ZDSCHS
          MOVE      ZERO,ZBDAYS
          MOVE      ZERO,ZDCASES
.
          MOVE      ZERO,STATUS
.
.         Check if previously exists..if so deleted it
.
          CALL      KILLIDZ
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 35 u1-11",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          MOVE      ONE,STATUS
          EXECUTE   CMDLINE,TASKID
          BRANCH    STATUS OF INDZFI
.
          MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B:
                    "Indexed File ",*V2LON,FNAMEI,*HOFF," already exists.  ";
          CALL      HITENTER
          RETURN
+
.
INDZFI    MOVE      ZERO,OPCND
          TRAP      NOINDZ IF IO
.
          OPEN      PSTIDZ,FNAMEI
          TRAPCLR   IO
ENDC      RETURN
+
.
NOINDZ    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B:
                    "Temp Indexed File ",*V2LON,FNAMEI,*HOFF," not found.  ";
          CALL      HITENTER
          TRAPCLR   IO
ENDINDZ   RETURN
+
.
.         Deleting an indexed file based on port
.
KILLIDF
          CLEAR     CMDLINE
          CLOSE     PATMP1XX
          APPEND    "iserase ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
.
KILLIDZ   CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.
.         Last line on the page
.
PAGEND    PRINT     *20,"*----------------------------------------------":
                    "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
.         Page Separator
.
PAGTOT    PRINT     *20,"|           |                  |---------------":
                    "-------------------------------------------|"
          ADD       ONE,CLNO
          RETURN
.
.         Page Separator
.
PAGSEP    PRINT     *20,"|----------------------------------------------":
                    "-------------------------------------------|"
          ADD       ONE,CLNO
          RETURN
.
.         Total Separator
.
PAGASK    PRINT     *20,"|==============================================":
                    "===========================================|"
          ADD       ONE,CLNO
          RETURN
+
.
.         Initialize total variables
.
ZEROT     MOVE      ZERO,TADMNS
          MOVE      ZERO,TDSCHS
          MOVE      ZERO,TBDAYS
          MOVE      ZERO,TDCASES
          MOVE      ZERO,DATEFLG
          RETURN
.
.         end of temp indexed file
.
PRTHD1M   COMPARE   TWO,OPTION
          GOTO      PRTHD2M IF EQUAL
.
          IF        TBDAYS = 0 & TADMNS = 0
            GOTO      MATBEND
          ENDIF
.
          CALL      PRNTOT2
          CALL      PAGSEP
.
PRTHD2M   BRANCH    STRTFLG OF NEXT11M,MATBEND
          GOTO      NEXT11M
.
MATBEND   RETURN
+
.
.         Print sub-total for individual day
.
PRNTOT2   COMPARE   "55",CLNO
          GOTO      PRNTOT21 IF LESS
          CALL      HEADB
          GOTO      PRNTOT22
.
PRNTOT21  CALL      PAGTOT
          PRINT     *20,"|";
          GOTO      PRNTOT23
.
PRNTOT22  PRINT     *20,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
PRNTOT23  PRINT     *32,"|          TOTAL":
                    *51,"| ",TBDAYS,*65,"| ",TADMNS:
                    *80,"| ",TDSCHS,*95,"|",*110,"|"
.
          ADD       ONE,CLNO
          CALL      ZEROT
          RETURN
+
.
.         Headings routine for Report 2
.
HEADB     CALL      IBACLOCK
          COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          CALL      HEAD132
          PRINT     *45,"From : ",DFRODATE:
                    *68,"To : ",DTODATE,*N
.
          CALL      PAGEND
.
          COMPARE   TWO,OPTION
          GOTO      HEADB1 IF EQUAL
.
          PRINT     *20,"| Date",*32,"|",*51,"| Beds Days":
                    *65,"| Admissions",*80,"| Discharges":
                    *95,"|",*110,"|"
          GOTO      HEADB2
.
HEADB1    PRINT     *20,"|",*32,"|",*51,"| Beds Days":
                    *65,"| Admissions",*80,"| Discharges":
                    *95,"|",*110,"|"
.
HEADB2    CALL      PAGEND
          MOVE      EIGHT,CLNO
          RETURN
+
.
.         Temp Indexed files I/O Routines
.
WRZIND    MOVE      ZERO,OVRCD
          RESET     KEY11
          WRITE     PSTIDZ,KEY11;KEY11,ZDSCHS,ZADMNS,ZBDAYS,ZDCASES,ZSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RDZIND    MOVE      ZERO,OVRCD
          RESET     KEY11
          READ      PSTIDZ,KEY11;ZDATE,ZTYPE,ZDSCHS,ZADMNS,ZBDAYS:
                    ZDCASES,ZSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKZIND   MOVE      ZERO,OVRCD
          READKS    PSTIDZ;ZDATE,ZTYPE,ZDSCHS,ZADMNS,ZBDAYS,ZDCASES,ZSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSZIND   MOVE      ZERO,OVRCD
          RESET     KEY11
          READ      PSTIDZ,KEY11;;
          GOTO      OVERCOND IF OVER
          RETURN
.
DEZIND    RESET     KEY11
          DELETE    PSTIDZ,KEY11
          RETURN
.
UPZIND    RESET     KEY11
          UPDATE    PSTIDZ;ZDATE,ZTYPE,ZDSCHS,ZADMNS,ZBDAYS,ZDCASES,ZSPAR
          RETURN
+
.
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKS    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKP    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          WRITE     PATMP1XX,KEY8;KEY8
          RETURN
.
UPTMPR1   UPDATE    PATMP1XX;TMPADMN
          RETURN
.
DETMPR1   RESET     KEY8
          DELETE    PATMP1XX,KEY8
          RETURN
+
.
ENDIT     CHAIN     PGM
          STOP
.
.------------------------------------------------------------------------------
.         KHOS0000
.         Enter The Hospital 
.------------------------------------------------------------------------------
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:21,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TWENTY1,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:21,*V2LON,PTHSNAME;
          PACK      HOSCODE,PTHSHOSP,SP70
          PACK      HOSNAME,PTHSNAME,SP70
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
.
.==============================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INCLUDE   PATDAYIO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       HL7BMTIO/INC
          INC       PATHSPIO/INC
          INC       PMSVX1IO/INC
.         INC       HL7BMTTR
          INC       PATHSPKY

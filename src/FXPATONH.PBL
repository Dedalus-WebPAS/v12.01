.*****************************************************************************
.* Program   :   FXPATONH                                                    *
.* Desc      :   Fixed Hold Invoice status for HL7 Receiver                  *
.*****************************************************************************
.* Date      :   04/04/2025                                                  *
.* Author    :   J.Tan                                                       *
.* Function  :   This program will patient invoice hold invoioce to fix      *
.*               status of records from HL7 Receiver                         *
.* Mods      :                                                               *
.*        V11.05.00 04/04/2025  J.Tan            TSK 0958935                 *
.*                  Created program                                          *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATONHFD/INC
          INC       PATCODFD/INC
          INC       WEBSECFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
FORM8     FORM      8
READCNT   FORM      8
COUNTER   FORM      8
CURRDATE  DIM       8
.
PRGID     INIT      "FXPATONH"
PRGNAM    INIT      "Fixit for Patinet Hold Invoice"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN0500:      * proceed with report
                            MAIN0500       * proceed with fixit
.
MAIN0500  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0500:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patonhaf";
          OPEN      PATONHA1,"patonhaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Report Only":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run report only
                            OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through hold invoice record from HL7 receiver                 *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,READCNT
          MOVE      ZERO,COUNTER                 * initialise update count
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      HEAD0000                     * print report header
.
          DISPLAY   *P1:24,*EL,"Record ";
.
          MOVE      ZERO,FORM8
          PACK      KEY24,SP70
          CALL      RSPTONH1                     * position on submitted recs.
PROC1000  CALL      RKPTONH1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          ADD       ONE,FORM8
          DISPLAY   *P12:24,*EL,FORM8
.
          MATCH     SP70,PTONREAH
          GOTO      PROC1000 IF EQUAL
.
          MOVE      "rh",KEY2
          PACK      KEY5,KEY2,PTONREAH
          CALL      RDCODE1
          BRANCH    OVRCD,PROC1000
.
          MATCH     "C",TCINDC4
          GOTO      PROC1000 IF NOT EQUAL        * NOT Code Focus
.
          MATCH     SP70,PTONURID
          GOTO      PROC1000 IF EQUAL
.
          MATCH     "HL7RECVR",PTONURID
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY10,PTONURID,SP70
          CALL      RDWBSE1
          COMPARE   ZERO,OVRCD
          GOTO      PROC1000 IF EQUAL            * valid user id
.
          ADD       ONE,READCNT
          CALL      DISP0000                     * print data
.
          BRANCH    COPTION,PROC1000             * report only
.
          MOVE      "HL7RECVR  ",PTONURID        * updated by
.
          MOVE      SP70,KEY20
          MATCH     "U",PTONHACT
          IF        @EQUAL
            MOVE      "L",PTONHACT               * Updated by HL7 Receiver
          ENDIF
.
          MATCH     "D",PTONHACT
          IF        @EQUAL
            MOVE      "M",PTONHACT               * Deleted by HL7 Receiver
          ENDIF
.
          ADD       ONE,COUNTER
          CALL      UPPTONH1
          GOTO      PROC1000
.
PROC9000  IF        COPTION = 1
            DISPLAY   *P1:24,*EL,"Report completed.  ";
          ELSE
            DISPLAY   *P1:24,*EL,"Fixit completed.  ",*V2LON,COUNTER,*HOFF:
                      " records updated.  ";
          ENDIF
          CALL      HITENTER
          IF        COUNTER <> 0
            CALL      LINE0000
          ENDIF
.
          PRINT     *N,*1,"*** Record Updated : ",COUNTER:
                    *N,*1,"*** End of Report ***"
.
PROC9999  RETURN
+
.****************************************************************************
.*                            DISP0000                 Called by:           *
.*                  Valid record so print it                                *
.****************************************************************************
.
DISP0000  UNPACK    PTONTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,PTONVISN,*12,CPCDATE,*24,PTONTTIM,*34,PTONURID;
.
          UNPACK    PTONHLIN,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *46,PTONREAH,*54,PTONHACT,*62,CPCDATE
.
          ADD       ONE,CLNO                     * increment line count
.
DISP9999  RETURN
+
.****************************************************************************
.*                            HEAD0000                 Called by: PROC0000  *
.*                       Print page heading                                 *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,"Visit No.",*12,"Trans.Date",*24,"Time",*34,"User Id":
                    *46,"Reason",*54,"Status",*62,"Hold Date"
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      EIGHT,CLNO                   * increment line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: PROC0000  *
.*                      Draw line across page                     HEAD0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "----------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATONHIO/INC
          INC       PATCODIO/INC
          INC       WEBSECIO/INC

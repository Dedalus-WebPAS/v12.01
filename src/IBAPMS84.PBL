.******************************************************************************
.* System         : Patient Management System                                 *
.* Program        : IBAPMS84.PBL                                              *
.* Name           : Health Fund Eligibility Check                             *
.******************************************************************************
.* Date           : 17/02/2009                                                *
.* Author         : Davin Sloan                                               *
.* Function       : Request an Eligibility Check for all patients that match  *
.*                  the criteria entered                                      *
.* Modifications  :                                                           *
.******************************************************************************
.*        V11.04.04 15/03/2024  J.Tan          Task 0919335                   *
.*                  Mod to check HF History file for HF Group                 *
.*        V11.04.03 15/03/2024  J.Tan          Task 0919335                   *
.*                  Mod to check HF History file for HF Group                 *
.*        V11.04.02 04/03/2024  PJ Le Febour       0929044dc                  *
.*                  WOEW0000 - populate WBOCCUID with USERKYIN                * 
.*                  WOEB0000 - populate WBOCCUID with USERKYIN                * 
.*                  WOEW0000 - populate WBOCUUID WBOCUDTE WBOCUTIM before call* 
.*                             WRWBOEC1                                       * 
.*                  HOEB0000 - populate WBOCUUID WBOCUDTE WBOCUTIM before call* 
.*                             UPWBOEC1                                       * 
.*                  HOEW0000 - populate WBOCUUID WBOCUDTE WBOCUTIM before call* 
.*                             UPWBOEC1                                       * 
.*        V11.04.01 04/12/2023  Thanh T            0919562                    *
.*                  Recompiled for WEBELFFD changes                           * 
.******************************************************************************
.*        V11.03.02 20.03.2023  DA Sarkies         0909393                    *
.*                  Added hospital name as key to theatre booking
.*        V11.03.01 19/12/2022  Peter Vela         0927981                    *
.*                  Added DMBS0000 loop though theatre mbs items and send     *
.*                  a OECW request for each                                   *
.*        V11.02.01 12/07/2022  Peter Vela         0884414                    *
.*                  Added WebServices Theatre Bookings Only Bulk OECW PROV3000*
.*        V11.01.01 12/07/2021  Peter Vela     TSK 0908845                    *
.*                  Added input for user id                                   *
.*        V11.00.03 01/09/2020  Peter Vela     TSK 0895969                    *
.*                  Added WebServices OECW                                    *
.*        V11.00.02 27/08/2020  Peter Vela     TSK 0884414                    *
.*                  Modified key in hospital in KHOS0000                      *
.*        V11.00.01 10/08/2020  Peter Vela     TSK 0884414                    *
.*                  Removed validation for PTELSPEA                           *
.*        V10.14.01 01/03/2019  Peter Vela     TSK 0865640                    *
.*                  Fixed VOEC0000 to check for PMVXPILC or AMBS              *
.*        V10.08.01 19/10/2016  Peter Vela     TSK 0827811                    *
.*                  Recompiled for CLPMSOEC                                   *
.*        V9.12.03  14/08/2009  Peter Vela     CAR 203723                     *
.*                  Added validation for eclipse participant patfx1af.ptfxspar*
.*        V9.12.02  13/08/2009  Peter Vela     CAR 202224                     *
.*                  Added Participant and Capability Validation               *
.*        V9.12.01  22/07/2009  Peter Vela     CAR 202023                     *
.*                  Initialised OECRPRNT to 1 for option 2                    *
.*        V9.11.00  21/04/2009  Davin          CAR 183976                     *
.*                  Created program - Scheduled Health Fund Eligibility Check *
.******************************************************************************
+
          INC       STD001FD
.
          INC       BOKRC1FD/INC         *For patient booking
          INC       BOKRX1FD/INC
          INC       OPRDEAFD/INC
          INC       OPRMBSFD/INC
          INC       OPRSESFD/INC
.
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Control file
          INC       PATDDHFD/INC            * HF history file
          INC       PATFN1FD/INC            * Health fund file
          INC       PATFX1FD/INC            * Health fund extension file
          INC       PATHSPFD/INC            * hospital file
          INC       PATMA1FD/INC            * Master file
          INC       PATMI1FD/INC            * Admission file
          INC       PATPARFD/INC            * Eclipse participants
          INC       PATPCPFD/INC            * Eclipse participants capabilities
          INC       PATELGFD/INC            * Eligibility file
          INC       PMSCCDFD/INC
          INC       PMSPX2FD/INC 
          INC       PMSVX1FD/INC            * Visit Extension file
          INC       PMSOECFD/INC            * Online Eligibility Check file
          INC       PMSOESFD/INC
          INC       PMSELFFD/INC
          INC       WEBOECFD/INC
          INC       WEBOESFD/INC
          INC       WEBELFFD/INC
          INC       WEBPARFD/INC            * WebServices participants
          INC       WEBPCPFD/INC            * WebServices participants cap
.
. ----- Program Variables -----
.
CATCT     INIT      "ct"
CURDATE   DIM       8
CMDLINE   DIM       80
CNCEFLAG  FORM      1
CNCWFLAG  FORM      1
DIM8      DIM       8
DIM20     DIM       20
DSPEDFND  DIM       20                      * Display ending health fund
DSPSTFND  DIM       20                      * Display starting haelth fund
ENDCLAIM  DIM       3
ENDDATE   DIM       8
USERKYIN  DIM       10
ENDFND    DIM       6                       * Ending health fund
ERRCT     FORM      5                       * total errors (exclusions)
ERRMSG    DIM       75                      * Error message
GELNFLAG  FORM      1
GELWFLAG  FORM      1
IBCNMHOS  FORM      1
OECRECKY  DIM       9[99]                   * array of visits for OEC
OECWINDC  FORM      1
OECCOUNT  FORM      3
OECRPRNT  FORM      1                       * OECR print indicator (1=yes)
PATNAME   DIM       30
RECCNT    FORM      5                       * Total Preadmns Meeting Criteria
REPUNIQ   INIT      "yzxywxvwuvtustrsqrpqopnomnlmkljkijhighfgefdecdbcab"
SAVCLAIM  DIM       3
SAVKEY11  DIM       11
STRCLAIM  DIM       3
STRTDATE  DIM       8                       * Start date (ccyymmdd)
STRTFND   DIM       6                       * Starting health fund
TILDA40   INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
WRTCT     FORM      5                       * total requests sent
.
. ----- WEBOECFD save variables -----
.
SVOCVISN  DIM       8  
SVOCCNTR  DIM       3  
SVOCSTAT  DIM       2  
SVOCURNO  DIM       8  
SVOCARID  DIM       20 
SVOCTRID  DIM       24 
SVOCRQDT  DIM       8  
SVOCETYP  DIM       2  
SVOCERRC  DIM       4  
SVOCERRD  DIM       500
SVOCCDTE  DIM       8  
SVOCCTIM  DIM       8  
SVOCUDTE  DIM       8  
SVOCUTIM  DIM       8  
SVOCHOSP  DIM       3  
SVOCELEG  DIM       8  
SVOCMSID  DIM       36 
.
. ----- WEBELFFD save variables -----
.
SVEFELGN  DIM       8  
SVEFURNO  DIM       8  
SVEFOECC  DIM       1  
SVEFTITL  DIM       4  
SVEFFNAM  DIM       40 
SVEFSNAM  DIM       40 
SVEFHFGN  DIM       40 
SVEFHFSN  DIM       40 
SVEFGEND  DIM       1  
SVEFBDAT  DIM       8  
SVEFEADT  DIM       8  
SVEFISTY  DIM       3  
SVEFPLOS  DIM       3  
SVEFDINT  DIM       3  
SVEFPEAI  DIM       1  
SVEFEMAD  DIM       1  
SVEFPRIC  DIM       3  
SVEFPRIM  DIM       9  
SVEFCLTY  DIM       3  
SVEFHFND  DIM       6  
SVEFHFNT  DIM       8  
SVEFHFMN  DIM       19 
SVEFDIAG  DIM       50 
SVEFSRV1  DIM       9  
SVEFSRV2  DIM       9  
SVEFSRV3  DIM       9  
SVEFSRV4  DIM       9  
SVEFSRV5  DIM       9  
SVEFCUID  DIM       10 
SVEFCDAT  DIM       8  
SVEFCTIM  DIM       8  
SVEFDVAN  DIM       10 
SVEFVISN  DIM       8  
SVEFCNTR  DIM       3  
SVEFFLAG  DIM       1  
SVEFTRID  DIM       24 
SVEFMSID  DIM       36 
.
SAVEAMBS  DIM       9
.
. ----- Program Constants -----
.
FINISH    INIT      "Finish"
PIPE      INIT      "|"
START     INIT      "Start"
ZERO8     INIT      "0000    "
.
PRGID     INIT      "IBAPMS84"
PRGNAM    INIT      "Health Fund Eligibility Check"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9999
.
          BRANCH    OPTION,ML5000,ML2000    * option 1 (list) or 2 (scheduled)
.                          array  adm loop
.
          GOTO      ML1000
.
ML2000    CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,ML1000
.
ML3000    CALL      KCLM0000                * Keyin the claim code range
          BRANCH    EXIT,ML2000
.
ML3500    CALL      KFND0000                * Keyin the health fund
          BRANCH    EXIT,ML3000
.
          CALL      KHOS0000                * Keyin hospital id
.
          CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3500,ML9999
          GOTO      ML9999
.
ML4000    CALL      PROC0000                * Process the files (option 2)
          CALL      TAIL0000                * Print report tail
          GOTO      ML9999
.
ML5000    CALL      PROV0000                * Process the files (option 1)
          GOTO      ML9999
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000                                 *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patfx1af";
          OPEN      PATFX1A1,"patfx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsccdaf";
          OPEN      PMSCCDA1,"pmsccdaf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patelgaf";
          OPEN      PATELGA1,"patelgaf"
.
          DISPLAY   *P64:24,"patparaf";
          OPEN      PATPARA1,"patparaf"
.
          DISPLAY   *P64:24,"patpcpaf";
          OPEN      PATPCPA1,"patpcpaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmsoecaf";
          OPEN      PMSOECA1,"pmsoecaf"
.
          DISPLAY   *P64:24,"pmsoesaf";
          OPEN      PMSOESA1,"pmsoesaf"
.
          DISPLAY   *P64:24,"pmselfaf";
          OPEN      PMSELFA1,"pmselfaf"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,"bokrx1af";
          OPEN      BOKRX1A1,"bokrx1af"
.
          DISPLAY   *P64:24,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"oprmbsaf"
          OPEN      OPRMBSA1,"oprmbsaf"
.
          DISPLAY   *P64:24,"oprsesaf"
          OPEN      OPRSESA1,"oprsesaf"
.
          MOVE      ONE,OECWINDC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      WEBOECA1,"weboecaf"
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      ZERO,OECWINDC
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      WEBOESA1,"weboesaf"
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      ZERO,OECWINDC
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      WEBELFA1,"webelfaf"
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      ZERO,OECWINDC
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      WEBPARA1,"webparaf"
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      ZERO,OECWINDC
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      WEBPCPA1,"webpcpaf"
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
.
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE 
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND29;*61,PTCNWSIN,*71,PTCNOECW,*72,PTCNOECE
.
          MOVE      SP70,USERKYIN
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          MOVE      ZERO,CPAGENO            * Reset the page no
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Visit Number":
                    *P1:6,*V2LON,"2",*HOFF," = By Date Range":
                    *P1:8,"Select Option : ";
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000                                  *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      "17",CCOL
          DISPLAY   *P1:4,*EF
          DISPLAY   *P1:9,"Starting Date : ",*EF:
                    *P1:10,"Ending Date   : ";
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "9",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          CALL      PACDATE                 * Format the start date
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,CPCDATE;
.
          MATCH     CURDATE,STRTDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Start date must not be in the past.  ";
            CALL      HITENTER 
            GOTO      DATE1000
          ENDIF
.
. ----- Keyin end date -----
.
DATE2000  MOVE      "10",CVERT
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          CALL      PACDATE                 * Format the end date
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,CPCDATE;
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must be after the start date.  ";
            CALL      HITENTER 
            GOTO      DATE2000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.**********************************************************************
.*                             KFND0000                               *
.*                keyin range of health funds                         *
.**********************************************************************
KFND0000  DISPLAY   *P1:15,"Starting Health Fund :",*EF:
                    *P1:16,"Ending   Health Fund :";
          MOVE      "24",ECOL
.
. ----- Starting Health Fund -----
.
KFND1000  MOVE      "15",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          MOVE      SP6,CKYIDEF6            * No default
          MOVE      ZERO,CKYIMAND           * Keyin not mandatory
          MOVE      ONE,CNEWDS              * Don't return for keyin
          CALL      PATFNDKY                * Keyin the health fund
          BRANCH    EXIT,KFND2000,KFND9500
.                        Space    Exit
.
          MOVE      HCODE,STRTFND
          MOVE      HFNAME,DSPSTFND
          DISPLAY   *P24:EVERT,*EL,*V2LON,STRTFND,*P31:EVERT,*HOFF,DSPSTFND;
          GOTO      KFND3000
.
KFND2000  MOVE      HCODE,STRTFND
          PACK      DSPSTFND,START,SP20
          DISPLAY   *P24:EVERT,*EL,*V2LON,DSPSTFND;
.
. ----- Ending Health Fund -----
.
KFND3000  MOVE      "16",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          MOVE      STRTFND,CKYIDEF6        * Default
          MOVE      ZERO,CKYIMAND           * Keyin not mandatory
          MOVE      ONE,CNEWDS              * Don't return for keyin
          CALL      PATFNDKY                * Keyin the health fund
          BRANCH    EXIT,KFND4000,KFND1000
.                        Space    Exit
.
          MOVE      HCODE,ENDFND
          MOVE      HFNAME,DSPEDFND
          DISPLAY   *P24:EVERT,*EL,*V2LON,ENDFND,*P31:EVERT,*HOFF,DSPEDFND;
          GOTO      KFND5000
.
KFND4000  MOVE      Z70,ENDFND
          PACK      DSPEDFND,FINISH,SP20
          DISPLAY   *P24:EVERT,*EL,*V2LON,DSPEDFND;
.
KFND5000  MATCH     STRTFND,ENDFND
          GOTO      KFND9000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Ending health fund must be after starting ":
                                  "health fund. ";
          CALL       HITENTER
          GOTO       KFND3000
.
KFND9000  MOVE      ZERO,EXIT
          GOTO      KFND9999
.
KFND9500  MOVE      ONE,EXIT
KFND9999  RETURN
+
.**********************************************************************
.*                             KCLM0000                               *
.*                keyin range of claim codes                          *
.**********************************************************************
KCLM0000  MOVE       ZERO,EXIT
          MOVE       SP20,STRCLAIM
          MOVE       SP20,ENDCLAIM
.
          DISPLAY    *P1:12,*EF,"From Claim Code   :":
                         *P1:13,"To   Claim Code   :";
.
          MOVE      "12",EVERT
          MOVE      "20",ECOL               * keyin position
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM9000,KCLM2000
          MOVE      ACODE,STRCLAIM
          GOTO      KCLM3000
.
KCLM2000  DISPLAY   *P24:12,*EL,"Start"
.
KCLM3000  MOVE      "13",EVERT
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM0000,KCLM4000
          MOVE      ACODE,ENDCLAIM
          GOTO      KCLM9999
.
KCLM4000  DISPLAY   *P24:13,*EL,"End"
          GOTO      KCLM9999
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.********************************************************************
.         Enter selected Hospital code
.********************************************************************
KHOS0000  
.
.         COMPARE   ONE,IBCNMHOS
.         GOTO      KHOS9000 IF NOT EQUAL
.
          DISPLAY   *P1:18,*EL,"Hospital: ";
.
          MOVE      "18",EVERT
          MOVE      "11",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS3000,KHOS3000
.
KHOS2000  DISPLAY   *P20:18,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS3000  MOVE      "All Hospitals",PTHSNAME
          DISPLAY   *P20:18,*V2LON,PTHSNAME;
.
KHOS9000  MOVE      SP10,PTHSHOSP
.
KHOS9999  RETURN
+
.********************************************************************
.         Check selected Hospital code (if multi hospital)
.********************************************************************
CHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      CHOS9000 IF NOT EQUAL
.
          PACK      PTHSHOSP,PTHSHOSP,SP70
          MATCH     SP10,PTHSHOSP           * All Hospitals
          GOTO      CHOS9000 IF EQUAL
.
          MATCH     PTHSHOSP,PMVXMHOS       * check hospital
          GOTO      CHOS9500 IF NOT EQUAL
.
CHOS9000  MOVE      ZERO,EXIT
          GOTO      CHOS9999
.
CHOS9500  MOVE      ONE,EXIT
CHOS9999  RETURN
+
.******************************************************************************
.*                                  PROC0000                                  *
.*                       Process Admission File (option 2)                    *
.******************************************************************************
PROC0000  MOVE      ONE,OECRPRNT
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
          MOVE      ZERO,CLNO
          CALL      HEAD0000                * Print report header
.
.         Get all preadmissions within the entered date range, claim type
.         range and health fund range.
.
          MOVE      ZERO,RECCNT                  * total meeting above criteria
          MOVE      ZERO,ERRCT                   * total validation exclusions
          MOVE      ZERO,WRTCT                   * requests
          PACK      KEY9,ONE,SP70
PROC0500  CALL      RDSMISS2
PROC1000  CALL      RDKMISS2                * Loop through preadmissions
          BRANCH    OVRCD,PROC9999
.
          DISPLAY   *P12:24,*V2LON,DAADMNO;
.
          MATCH     "1",DASTAT
          GOTO      PROC9999 IF NOT EQUAL   * preadmission
.
          MATCH     STRTDATE,ADATE
          GOTO      PROC1000 IF LESS        * admit date before start date
.
          MATCH     ADATE,ENDDATE
          GOTO      PROC1000 IF LESS        * admit date after end date
.
          CALL      RCLM0000                * check range of claim codes
          BRANCH    EXIT,PROC1000
.
          CALL      RFND0000                * check range of health funds
          BRANCH    EXIT,PROC1000
.
          MOVE      AADMNO,KEY8
          CALL      RDPMVX11                * read pmsvx1 before hospital check
          IF        OVRCD=1
            MOVE      SP70,PMVXIDUS
            MOVE      SP70,PMVXPILC
            MOVE      SP70,PMVXMHOS
            MOVE      SP70,PMVXUDT6
            MOVE      SP70,PMVXUDIN
          ENDIF
.
          CALL      CHOS0000                * check hospital
          BRANCH    EXIT,PROC1000
.
          ADD       ONE,RECCNT              * increment record count
.
          CALL      VOEC0000                * Validate eligibility criteria
          BRANCH    EXIT,PROC1000
.
          CALL      WOEC0000                * Write record to OEC file
          GOTO      PROC1000
.
PROC9999  RETURN
+
.******************************************************************************
.         Validate eligibility criteria for OEC
.******************************************************************************
. Exclude all preadmissions not meeting the following criteria:
.
VOEC0000  MATCH     "1",PTCNWSIN
          GOTO      VOEC0010 IF NOT EQUAL
.
          MATCH     "1",PTCNOECW
          GOTO      VOEC0010 IF NOT EQUAL
.
          COMPARE   ONE,OECWINDC
          GOTO      VOEC0010 IF NOT EQUAL
.
          CALL      VOEW0000
          BRANCH    EXIT,VOEC9500          
.
          GOTO      VOEC9000 
.
VOEC0010  MOVE      AURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "No PMI details exist for this admission",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
          MOVE      AURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            MOVE      "No PMI details exist for this admission",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
          MATCH     SP70,PSNAME            * surname set?
          IF        @EQUAL
            MOVE      "No surname exists on PMI",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
          MATCH     SP70,PGNAME            * given name set?
          IF        @EQUAL
            MOVE      "No given name exists on PMI",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
          MATCH     SP70,PBDATE            * DOB set?
          IF        @EQUAL
            MOVE      "No date of birth exists on PMI",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
          MATCH     SP70,AFUNDH            * Health fund set ?
          IF        @EQUAL
            MOVE      "No health fund exists on admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
.
          ELSE
.
            PACK      KEY6,AFUNDH,SP70
            CALL      RDPTFX11
            IF        OVRCD=1
              GOTO      VOEC1000
            ELSE
              MATCH     SP70,PTFXECLP
              IF        @EQUAL
                GOTO      VOEC1000
              ELSE
                PACK      KEY3,PTFXECLP,SP70
                CALL      RDPTPAR1                * participant code on file ?
                IF        OVRCD=1
                  GOTO      VOEC1000
                ELSE
                  PACK      KEY9,PTPRCODE,ANSO,ANSE,ANSC,SP70
                  CALL      RSPTPCP1                     * OEC capability ?
                  CALL      RKPTPCP1
                  IF        OVRCD=1
                    MOVE      "Participant does not have OEC capability",ERRMSG
                    CALL      PERR0000
                    GOTO      VOEC9500
                  ELSE
                    MATCH     PTPRCODE,PTPPCODE
                    IF        !@EQUAL
                      MOVE      "Participant does not have OEC capability",ERRMSG
                      CALL      PERR0000
                      GOTO      VOEC9500
                    ELSE
                      MATCH     "OEC",PTPPCPID
                      IF        !@EQUAL
                        MOVE      "Participant does not have OEC capability",ERRMSG
                        CALL      PERR0000
                        GOTO      VOEC9500
                      ELSE
                        GOTO      VOEC2000
                      ENDIF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
.
VOEC1000    PACK      KEY14,AFUNDH,ZERO8
            CALL      RDFUND1 
            IF        OVRCD=1
              MOVE      "No health fund record exists for this admission",ERRMSG
              CALL      PERR0000
              GOTO      VOEC9500
            ELSE    
              MOVE      "02",KEY2               * check HF history for HF Group
              PACK      KEY17,AFUNDH,KEY2,SP70
              CALL      PATDDHRD
.
              MATCH     SP70,PTHFHGRP
              IF        @EQUAL 
                MOVE      "No health fund group / participant exists on health fund table",ERRMSG
                CALL      PERR0000
                GOTO      VOEC9500
              ELSE
                PACK      KEY3,PTHFHGRP,SP70
                CALL      RDPTPAR1                * participant code on file ? 
                IF        OVRCD=1
                  MOVE      "Participant does not exist in participant table",ERRMSG
                  CALL      PERR0000
                  GOTO      VOEC9500 
                ELSE
                  PACK      KEY9,PTPRCODE,ANSO,ANSE,ANSC,SP70
                  CALL      RSPTPCP1                     * OEC capability ?
                  CALL      RKPTPCP1
                  IF        OVRCD=1
                    MOVE      "Participant does not have OEC capability",ERRMSG
                    CALL      PERR0000
                    GOTO      VOEC9500
                  ELSE
                    MATCH     PTPRCODE,PTPPCODE
                    IF        !@EQUAL
                      MOVE      "Participant does not have OEC capability",ERRMSG
                      CALL      PERR0000
                      GOTO      VOEC9500
                    ELSE
                      MATCH     "OEC",PTPPCPID
                      IF        !@EQUAL
                        MOVE      "Participant does not have OEC capability",ERRMSG
                        CALL      PERR0000
                        GOTO      VOEC9500
                      ENDIF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
.
          ENDIF
.
VOEC2000  MATCH     SP70,AFNDTB            * Health fund table set ?
          IF        @EQUAL
            MOVE      "No health fund table exists on admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
          MATCH     SP70,ADATE             * Admission date set ?
          IF        @EQUAL
            MOVE      "No admission date exists on admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
          MATCH     SP70,ACLAIM            * claim type set ?
          IF        @EQUAL
            MOVE      "No claim type exists on admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
.         MATCH     SP70,ADIAG1            * admitting diagnosis set ?
.         IF        @EQUAL
.           MATCH     SP70,AMBS            * admitting mbs set ?
.           IF        @EQUAL
.             MOVE      "No Presenting Diagnosis OR MBS Item exists on admission table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEC9500
.           ENDIF
.         ENDIF
.
          MATCH     SP70,PMVXIDUS      * Intended stay (sameday) indicator set?
          IF        @EQUAL
            MOVE      "Intended stay (sameday indicator) not set on visit extension table",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
          MATCH     SP70,PMVXPILC      * Presenting Illness Code CAT (il) ?
          IF        @EQUAL
            MATCH     SP70,AMBS            * admitting mbs set ?
            IF        @EQUAL
              MOVE      "No Presenting Illness Code OR MBS Item exists on visit/admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
            ENDIF
          ENDIF
.
          MOVE      AADMNO,KEY8
          CALL      RDPTELG1
          IF        OVRCD=1
            MOVE      "No eligibility details exist for this admission",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
          MATCH     "1",PTELCMPL          * eligibility Check Complete ?
          IF        @EQUAL
            MOVE      "Eligibility Check Complete - no request sent",ERRMSG
            CALL      PERR0000
            GOTO      VOEC9500
          ENDIF
.
.         MATCH     SP70,PTELSPEA          * Pre-Existing Ailment indicator set?
.         IF        @EQUAL
.           MOVE      "PEA Indicator not set on eligibility table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEC9500
.         ENDIF
.
.         MATCH     SP70,PMVXIDUS      * Intended stay (sameday) indicator set?
.         IF        @EQUAL
.           MOVE      "Intended stay (sameday indicator) not set on visit extension table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEC9500
.         ENDIF
.
.         MATCH     SP70,PMVXPILC      * Presenting Illness Code CAT (il) ?
.         IF        @EQUAL
.           MOVE      "Presenting Illness Code CAT (il) not set on visit extension table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEC9500
.         ENDIF
.
VOEC9000  MOVE      ZERO,EXIT               * send OEC
          GOTO      VOEC9999
.
VOEC9500  MOVE      ONE,EXIT                * don't send OEC
VOEC9999  RETURN
+
.******************************************************************************
.         Write to OEC table (pmsoec)
.******************************************************************************
WOEC0000  MATCH     "1",PTCNWSIN
          GOTO      WOEC0010 IF NOT EQUAL
.
          MATCH     "1",PTCNOECW
          GOTO      WOEC0010 IF NOT EQUAL
.
          COMPARE   ONE,OECWINDC
          GOTO      WOEC0010 IF NOT EQUAL
.
          CALL      WOEW0000
.
          GOTO      WOEC9000
.
WOEC0010  MATCH     "1",PTCNOECE
          GOTO      WOEC9999 IF EQUAL
.
          CALL      GNXOEC00
          CALL      CLPMSOEC              * Write new pmsoecaf record
.
          MOVE      AADMNO,PMOEVISN
          MOVE      KEY3,PMOECNTR
          MOVE      " 0",PMOESTAT
          MOVE      AURNO,PMOEURNO
          MOVE      AADMNO,PMOEARID
          PACK      PMOEARID,PMOEARID,SP70
          LJUSTIFY  PMOEARID
          MOVE      SP70,PMOETRID
          CALL      IBACLOCK
          PACK      PMOERQDT,CCC,CYY,CMM,CDD
          REP       " 0",PMOERQDT
          MOVE      SP70,PMOEETYP
          MOVE      SP70,PMOEERRC
          MOVE      SP70,PMOEERRD
          PACK      PMOECDTE,CCC,CYY,CMM,CDD
          REP       " 0",PMOECDTE
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,PMOECTIM
          MOVE      SP70,PMOEUDTE
          MOVE      SP70,PMOEUTIM
          MOVE      PMVXMHOS,PMOEHOSP
          MOVE      SP70,PMOESPAR
.
          PACK      KEY11,AADMNO,KEY3,SP70
          CALL      RAPMOEC1
          IF        OVRCD=1
            CALL      WRPMOEC1
          ELSE
            GOTO      WOEC0000
          ENDIF
.
          CALL      HOEC0000
.
WOEC9000  ADD       ONE,WRTCT               * requests sent counter
.
WOEC9999  RETURN
+
.*******************************************************************************
.        Get Next pmsoecaf report counter
.*******************************************************************************
GNXOEC00  MOVE      "  1",KEY3
          PACK      KEY11,AADMNO,Z70
          CALL      RSPMOEC1
          CALL      RPPMOEC1
          BRANCH    OVRCD,GNXOEC99
.
          MATCH     DAADMNO,PMOEVISN
          GOTO      GNXOEC99 IF NOT EQUAL
.
          SQUEEZE   PMOECNTR
          MOVE      PMOECNTR,F3
.
          ADD       ONE,F3
          MOVE      F3,KEY3
.
GNXOEC99  RETURN
+
.******************************************************************************
.*                                  HEAD0000                                  *
.*                             Print Report Header                            *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print report header
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital : ",PTHSNAME
            ADD       ONE,CLNO
          ENDIF
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"Date Range From : ",CPCDATE;
.
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *85,"End Date   : ",CPCDATE
.
          MOVE      "Start               ",TDESC
          MATCH     SP70,STRCLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,STRCLAIM
            CALL      RDCODE1
          ENDIF
          PRINT     *40,"Claim Type From : ",TDESC;
.
          MOVE      "Finish              ",TDESC
          MATCH     SP70,ENDCLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,ENDCLAIM
            CALL      RDCODE1
          ENDIF
          PRINT     *85,"Claim Type To   : ",TDESC
.
          PRINT     *40,"Health Fund From : ",DSPSTFND;
.
          PRINT     *85,"Health Fund To   : ",DSPEDFND
.
          CALL      UND132
          PRINT     *1,"| U/R No. ",*11,"| Adm.No. ",*21,"| Patient Name":
                    *54,"| Error Message",*132,"|"
          CALL      UND132
          ADD       "4",CLNO
.
HEAD9999  RETURN
+
.******************************************************************************
.*                                  TAIL0000                                  *
.*                              Print Report Tail                             *
.******************************************************************************
TAIL0000  CALL      UND132
.
          PRINT     *N,*1,"Total Preadmissions Meeting Criteria : ",RECCNT:
                    *N,*1,"Total Number of Validation Errors    : ",ERRCT:
                    *N,*N,*1,"Total Eligibility Requests Sent      : ",WRTCT
.
          PRINT     *N,*N,"*** End of Report ***"
.
          MOVE      ZERO,EXIT
.
TAIL9999  RETURN
+
.*****************************************************************************
.*                                  PERR0000                                 *
.*                             Print Error Message                           *
.* Requires: ERRMSG - error description                                      *
.*           AADMNO - admission number                                       *
.*           AURNO  - U/R number                                             *
.*****************************************************************************
PERR0000  COMPARE   ONE,OECRPRNT                 * print error report ?
          GOTO      PERR9999 IF NOT EQUAL        * no
.
          COMPARE   "55",CLNO                    * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * Patients name
.
          PACK      ERRMSG,ERRMSG,SP30,SP30
          PRINT     *1,PIPE,*2,AURNO,*11,PIPE,*12,AADMNO,*21,PIPE,*23,PATNAME:
                    *54,PIPE,*56,ERRMSG,*132,PIPE
          ADD       ONE,CLNO
.
          ADD       ONE,ERRCT             * Increment the error counter
.
PERR9999  RETURN
+
.******************************************************************************
.         Check Range of Claim codes
.******************************************************************************
RCLM0000  MATCH     SP20,STRCLAIM
          IF        !@EQUAL
            MATCH     STRCLAIM,ACLAIM         * Check with Start claim code
            GOTO      RCLM9000 IF LESS
          ENDIF
.
          MATCH     SP20,ENDCLAIM
          IF        !@EQUAL
            MATCH     ACLAIM,ENDCLAIM         * Check with End of Claim code
            GOTO      RCLM9000 IF LESS
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      RCLM9999
.
RCLM9000  MOVE      ONE,EXIT
RCLM9999  RETURN
+
.******************************************************************************
.         Check Range of Health Funds
.******************************************************************************
RFND0000  MATCH     SP20,STRTFND
          IF        !@EQUAL
            MATCH     STRTFND,AFUNDH        * Check with Start fund
            GOTO      RFND9000 IF LESS
          ENDIF
.
          MATCH     Z70,ENDFND
          IF        !@EQUAL
            MATCH     AFUNDH,ENDFND         * Check with End fund
            GOTO      RFND9000 IF LESS
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      RFND9999
.
RFND9000  MOVE      ONE,EXIT
RFND9999  RETURN
+
.******************************************************************************
.*                                 KCOD0000                                   *
.*                         Keyin codes                                        *
.******************************************************************************
KCOD0000  KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,ACODE:
                    *PECOL:EVERT,*DV,ACODE
          ENDSET    ACODE
          APPEND    SP3,ACODE
          RESET     ACODE
.
          MATCH     SP3,ACODE
          GOTO      KCOD8000 IF EQUAL       * nothing
.
          PACK      KEY5,ANSC,ANSL,ACODE
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      KCOD1000 IF EQUAL
          MOVE      ONE,EXIT
          GOTO      KCOD9999
.
KCOD1000  DISPLAY   *P30:EVERT,*V2LON,TDESC;
          MOVE      ZERO,EXIT
          GOTO      KCOD9999
.
KCOD8000  MOVE      TWO,EXIT
KCOD9999  RETURN
+
.******************************************************************************
.*                                  PROV0000                                  *
.*                 Process array to get visits from keystrokes (option 1)     *
.******************************************************************************
PROV0000  MOVE      ONE,OECCOUNT
          WHILE     OECCOUNT <= 99
            MOVE      SP70,OECRECKY[OECCOUNT]    * Clear list array
            ADD       ONE,OECCOUNT
          DO
.
          KEYIN     OECRPRNT                * print report flag (1=yes)
          KEYIN     STRTDATE                * start of date range
          KEYIN     ENDDATE                 * end of date range
          KEYIN     USERKYIN                * user id
          CALL      KHOS0000                * Keyin hospital id
.
          MOVE      SP70,STRCLAIM           * start claim type
          MOVE      SP70,ENDCLAIM           * end claim type
          PACK      DSPSTFND,START,SP20     * start health fund
          PACK      DSPEDFND,FINISH,SP20    * end health fund
.
          IF        OECRPRNT=1
            MOVE      ZERO,CPAGENO            * initialise print variables
            MOVE      ZERO,CNOUNDLN
            CALL      IBACLOCK
            MOVE      ZERO,CLNO
            CALL      HEAD0000                * Print report header
          ENDIF
.
          MOVE      ZERO,RECCNT                  * total meeting criteria
          MOVE      ZERO,ERRCT                   * total validation exclusions
          MOVE      ZERO,WRTCT                   * requests
.
.         Receive list array input from web front-end via keyin until
.         a blank record is received (end of array)
.
          MOVE      ONE,OECCOUNT
PROV1000  KEYIN     OECRECKY[OECCOUNT]
          PACK      OECRECKY[OECCOUNT],OECRECKY[OECCOUNT],SP70
.
          MATCH     SP70,OECRECKY[OECCOUNT]    * last array record ?
          GOTO      PROV9000 IF EQUAL          * yes
.
          PACK      KEY8,OECRECKY[OECCOUNT],SP70
          CALL      RDMISS1                 * read (pre)admission record
          BRANCH    OVRCD,PROV3000
.
          MOVE      AADMNO,KEY8
          CALL      RDPMVX11                * read pmsvx1 before hospital check
          IF        OVRCD=1
            MOVE      SP70,PMVXIDUS
            MOVE      SP70,PMVXPILC
            MOVE      SP70,PMVXMHOS
            MOVE      SP70,PMVXUDT6
            MOVE      SP70,PMVXUDIN
          ENDIF
.
          CALL      CHOS0000                * check hospital
          BRANCH    EXIT,PROV2000
.
          ADD       ONE,RECCNT              * increment record count
.
          CALL      VOEC0000                * Validate eligibility criteria
          BRANCH    EXIT,PROV2000
.
          CALL      WOEC0000                * Write record to OEC file
.
          CALL      DMBS0000                * Loop through theatre mbs items
.                                           * And send OECWs for each
.
PROV2000  ADD       ONE,OECCOUNT
          GOTO      PROV1000
.
PROV3000  PACK      KEY8,OECRECKY[OECCOUNT],SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,PROV4000
.
          PACK      KEY8,OECRECKY[OECCOUNT],SP70
          CALL      RDBKRX11                * Read booking extn file
          BRANCH    OVRCD,PROV4000
.
          PACK      KEY31,DBKREBOO,SP70
          CALL      RSOPDEA2
          CALL      RKOPDEA2
          BRANCH    OVRCD,PROV4000
.
          MATCH     DBKREBOO,OPDAADMN
          GOTO      PROV4000 IF NOT EQUAL
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,PROV4000 
.
          PACK      KEY11,DBKREBOO,Z70
          CALL      RSWBOEC1
          CALL      RPWBOEC1
          BRANCH    OVRCD,PROV4000
.
          MATCH     DBKREBOO,WBOCVISN
          GOTO      PROV4000 IF NOT EQUAL
.
          MATCH     SP70,WBOCELEG
          GOTO      PROV4000 IF EQUAL
          GOTO      PROV4000 IF EOS
.
          PACK      KEY8,WBOCELEG,SP70
          CALL      RDWBELF1 
          BRANCH    OVRCD,PROV4000
.
          CALL      CLSVOC00
          CALL      MVSVOC00
.
          CALL      CLSVEF00
          CALL      MVSVEF00
.
          CALL      CHOB0000                       * check hospital
          BRANCH    EXIT,PROV4000
.
          ADD       ONE,RECCNT                     * increment record count
.
          CALL      VOEB0000                * Validate eligibility criteria
          BRANCH    EXIT,PROV2000
.
          CALL      WOEB0000                * Write record to OEC file 
.
PROV4000  ADD       ONE,OECCOUNT
          GOTO      PROV1000
.
PROV9000  IF        OECRPRNT=1
            CALL      TAIL0000                * Print report tail
          ENDIF
.
PROV9999  RETURN
+
.-------------------------------------------------------------
. Write OEC History Record to pmselfaf (OEC Free Format table)
.-------------------------------------------------------------
HOEC0000  CALL      CLPMSELF
.
          PACK      SAVKEY11,PMOEVISN,PMOECNTR,SP70
.
HOEC1000  CALL      GELN0000           * Generate Eligibility Number
          BRANCH    GELNFLAG,HOEC9999
.
          MOVE      SAVKEY11,KEY11
          CALL      RDPMOEC1
.
          MOVE      PMOEURNO,PMEFURNO
          MOVE      ZERO,PMEFOECC
          MOVE      PTITL,PMEFTITL
          PACK      PMEFFNAM,PMPXFNAM,SP70
          PACK      PMEFSNAM,PTMASNAM,SP70
          MOVE      SP70,PMEFHFGN
          MOVE      SP70,PMEFHFSN
          MOVE      PSEX,PMEFGEND
          MOVE      PBDATE,PMEFBDAT
          MOVE      ADATE,PMEFEADT
          MOVE      PMVXIDUS,PMEFISTY
          MOVE      ASTAY,PMEFPLOS
          MOVE      PMVXUDIN,PMEFDINT
          MOVE      PTELSPEA,PMEFPEAI
          MOVE      PTELEADM,PMEFEMAD
.
          MOVE      PMVXPILC,PMEFPRIC
.
          MOVE      AMBS,PMEFPRIM
.
          MOVE      ACLAIM,PMEFCLTY
          MOVE      AFUNDH,PMEFHFND
          MOVE      AFNDTB,PMEFHFNT
          MOVE      AFNDMM,PMEFHFMN
          MOVE      ADIAG1,PMEFDIAG
.
          MATCH     SP70,USERKYIN
          IF        @EQUAL
            MOVE      "iba       ",PMEFCUID
          ELSE
            MOVE      USERKYIN,PMEFCUID
          ENDIF
.
          CALL      IBACLOCK
          PACK      PMEFCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMEFCDAT
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,PMEFCTIM
.
          CALL      CNCE0000                       * DVA conc card on file ?
          IF        EXIT=1
            PACK      PMEFDVAN,PREPAT,SP20         * No
          ELSE
            MOVE      DIM20,PMEFDVAN               * yes
          ENDIF
.
          MOVE      PMOEVISN,PMEFVISN
          MOVE      PMOECNTR,PMEFCNTR
          MOVE      ZERO,PMEFFLAG
          PACK      PMEFSPAR,SP70,SP70
.
          PACK      KEY8,PMEFELGN,SP70
          CALL      RAPMELF1
          IF        OVRCD=0
            GOTO      HOEC1000
          ENDIF
.
          CALL      WRPMELF1
.
          PACK      KEY11,PMOEVISN,PMOECNTR,SP70
          CALL      RDPMOEC1
          IF        OVRCD=0
            MOVE      PMEFELGN,PMOEELEG
            CALL      UPPMOEC1
          ENDIF
.
HOEC9999  RETURN
+
.*****************************************************************************
.*                              CNCE0000           Called by: WPAT0000       *
.*     See if the patient has concession card of DVA type on file            *
.* Requires: PURNO - U/R number                                              *
.* Returns:  EXIT  0 = valid concession card found                           *
.*                 1 = no valid concession card found                        *
.*           DIM20 - Concession Card Number                                  *
.*****************************************************************************
.
CNCE0000  PACK      KEY19,PURNO,TILDA40
          CALL      RSPMCCD1                     * position on U/R
CNCE0500  CALL      RPPMCCD1                     * read previous record
          BRANCH    OVRCD,CNCE9100               * eof - nothing found
.
          MATCH     PURNO,PMCDURNO               * same U/R still ?
          GOTO      CNCE9100 IF NOT EQUAL        * no - nothing found
.
.         Check if the card type is DVA (Cat ct, Indicator 1)
.
          PACK      KEY5,CATCT,PMCDCTYP
          CALL      RDCODE1                      * code on file ?
          BRANCH    OVRCD,CNCE0500               * no - ignore record
.
          MATCH     SP1,TCINDC1                  * blank indicator 1 ?
          GOTO      CNCE0500 IF EQUAL            * yes - ignore record
.
          TYPE      TCINDC1                      * numeric indicator 1 ?
          GOTO      CNCE0500 IF NOT EQUAL        * no - ignore record
.
          MATCH     "3",TCINDC1                  * DVA card type ?
          GOTO      CNCE0500 IF NOT EQUAL        * no - ignore record
.
.         A DVA concession card has been found, so check
.         if there is an expiry date
.
          MATCH     SP8,PMCDEXDT                 * expiry date blank ?
          GOTO      CNCE1000 IF EQUAL            * yes - assume valid
.
.         There is an expiry date for the card, so check that the
.         card is current
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          MATCH     DIM8,PMCDEXDT                * card valid for date ?
          GOTO      CNCE9100 IF LESS             * no - nothing found
.
.         Concession card is current
.
CNCE1000  MATCH     SP20,PMCDCNUM                * card number blank ?
          GOTO      CNCE9100 IF EQUAL            * yes - nothing found
.
          MOVE      PMCDCNUM,DIM20               * load concession card number
.
          MOVE      ZERO,CNCEFLAG
          GOTO      CNCE9999
.
CNCE9100  MOVE      ONE,CNCEFLAG
.
CNCE9999  RETURN
+
.-----------------------------------------------------------
.        Generate Free Format OEC Eligibility Number
.-----------------------------------------------------------
GELN0000  MOVE      ZERO,GELNFLAG
.
          MOVE      "118",PRXCODE
          CALL      GETSLK00
          READ      CONTROLF,HUND18;*117,PTCNELGN
.
GELN0100  MOVE      PTCNELGN,PMEFELGN
          UNPACK    PTCNELGN,ANS,KEY7
          MATCH     "a",ANS
          GOTO      GELN1150 IF LESS
          MOVE      ZERO,F7
          MOVE      KEY7,F7
.
GELN1100  COMPARE   "9999999",F7               * at the end of the group
          GOTO      GELN1110 IF NOT EQUAL
.
          MATCH     "z",ANS
          GOTO      GELN9000 IF EQUAL
.
          REP       REPUNIQ,ANS
          MOVE      ZERO,F7
.
GELN1110  ADD       ONE,F7
          MOVE      F7,D7
          REP       " 0",D7
.
          PACK      KEY8,ANS,D7,SP70
          CALL      RAPMELF1
          IF        OVRCD=ONE
            PACK      KEY11,ANS,D7,SP1,SP1,ONE,SP70
            CALL      RAPMOEC1
            IF        OVRCD=ONE
              PACK      KEY14,ANS,D7,SP1,SP1,ONE,SP70
              CALL      RSPMOES1
              CALL      RKPMOES1
              BRANCH    OVRCD,GELN9500
.
              PACK      KEY8,ANS,D7,SP70
              MATCH     KEY8,PMOSVISN
              GOTO      GELN9500 IF NOT EQUAL
            ENDIF
          ENDIF
.
          GOTO      GELN1100
.
. no records
.
GELN1150  MOVE      ONE,F7
          MOVE      "a",ANS
.
          MOVE      F7,D7
          REP       " 0",D7
.
          PACK      KEY8,ANS,D7,SP70
          GOTO      GELN9500
.
GELN9000  MOVE      ONE,GELNFLAG                  * Passcode allocation full
          GOTO      GELN9999
.
GELN9500  MOVE      KEY8,PTCNELGN
          WRITAB    CONTROLF,HUND18;*117,PTCNELGN
.
          MATCH     "00000000",PMEFELGN
          GOTO      GELN0100 IF EQUAL
.
GELN9999  CALL      RELSLK00
          RETURN
+
.******************************************************************************
.         Write to OECW table (weboec)
.******************************************************************************
WOEW0000  CALL      GNXOEW00
          CALL      CLWEBOEC              * Write new weboecaf record
.
          MOVE      AADMNO,WBOCVISN
          MOVE      KEY3,WBOCCNTR
          MOVE      " 0",WBOCSTAT
          MOVE      AURNO,WBOCURNO
          MOVE      AADMNO,WBOCARID
          PACK      WBOCARID,WBOCARID,SP70
          LJUSTIFY  WBOCARID
          MOVE      SP70,WBOCTRID
          CALL      IBACLOCK
          PACK      WBOCRQDT,CCC,CYY,CMM,CDD
          REP       " 0",WBOCRQDT
          MOVE      SP70,WBOCETYP
          MOVE      SP70,WBOCERRC
          MOVE      SP70,WBOCERRD
          PACK      WBOCCDTE,CCC,CYY,CMM,CDD
          REP       " 0",WBOCCDTE
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,WBOCCTIM
          MOVE      SP70,WBOCUDTE
          MOVE      SP70,WBOCUTIM
          MOVE      PMVXMHOS,WBOCHOSP
          MOVE      SP70,WBOCELEG
          MOVE      SP70,WBOCMSID
          MOVE      SP70,WBOCSPAR
.
          MOVE      USERKYIN,WBOCCUID
          MOVE      SP70,WBOCUUID
.
          PACK      KEY11,AADMNO,KEY3,SP70
          CALL      RAWBOEC1
          IF        OVRCD=1

            CALL      IBACLOCK
            PACK      WBOCUDTE,CCC,CYY,CMM,CDD
            REP       " 0",WBOCUDTE
            MOVE      CTIMEIS,WBOCUTIM
            MOVE      USERKYIN,WBOCUUID

            CALL      WRWBOEC1
          ELSE
            GOTO      WOEW0000
          ENDIF
.
          CALL      HOEW0000
.
          ADD       ONE,WRTCT               * requests sent counter
.
WOEW9999  RETURN
+
.*******************************************************************************
.        Get Next weboecaf report counter
.*******************************************************************************
GNXOEW00  MOVE      "  1",KEY3
          PACK      KEY11,AADMNO,Z70
          CALL      RSWBOEC1
          CALL      RPWBOEC1
          BRANCH    OVRCD,GNXOEW99
.
          MATCH     DAADMNO,WBOCVISN
          GOTO      GNXOEW99 IF NOT EQUAL
.
          SQUEEZE   WBOCCNTR
          MOVE      WBOCCNTR,F3
.
          ADD       ONE,F3
          MOVE      F3,KEY3
.
GNXOEW99  RETURN
+
.--------------------------------------------------------------
. Write OECW History Record to webelfaf (OECW Free Format table)
.--------------------------------------------------------------
HOEW0000  CALL      CLWEBELF
.
          PACK      SAVKEY11,WBOCVISN,WBOCCNTR,SP70
.
HOEW1000  CALL      GELW0000           * Generate Eligibility Number
          BRANCH    GELWFLAG,HOEW9999
.
          MOVE      SAVKEY11,KEY11
          CALL      RDWBOEC1
.
          MOVE      WBOCURNO,WBEFURNO
          MOVE      ZERO,WBEFOECC
          MOVE      PTITL,WBEFTITL
          PACK      WBEFFNAM,PMPXFNAM,SP70
          PACK      WBEFSNAM,PTMASNAM,SP70
          MOVE      SP70,WBEFHFGN
          MOVE      SP70,WBEFHFSN
          MOVE      PSEX,WBEFGEND
          MOVE      PBDATE,WBEFBDAT
          MOVE      ADATE,WBEFEADT
          MOVE      PMVXIDUS,WBEFISTY
          MOVE      ASTAY,WBEFPLOS
          MOVE      PMVXUDIN,WBEFDINT
          MOVE      PTELSPEA,WBEFPEAI
          MOVE      PTELEADM,WBEFEMAD
.
          MOVE      PMVXPILC,WBEFPRIC
.
          MOVE      AMBS,WBEFPRIM
.
          MOVE      ACLAIM,WBEFCLTY
          MOVE      AFUNDH,WBEFHFND
          MOVE      AFNDTB,WBEFHFNT
          MOVE      AFNDMM,WBEFHFMN
          MOVE      ADIAG1,WBEFDIAG
.
          MATCH     SP70,USERKYIN
          IF        @EQUAL
            MOVE      "iba       ",WBEFCUID
          ELSE
            MOVE      USERKYIN,WBEFCUID
          ENDIF
.
          CALL      IBACLOCK
          PACK      WBEFCDAT,CCC,CYY,CMM,CDD
          REP       " 0",WBEFCDAT
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,WBEFCTIM
.
          CALL      CNCW0000                       * DVA conc card on file ?
          IF        EXIT=1
            PACK      WBEFDVAN,PREPAT,SP20         * No
          ELSE
            MOVE      DIM20,WBEFDVAN               * yes
          ENDIF
.
          MOVE      WBOCVISN,WBEFVISN
          MOVE      WBOCCNTR,WBEFCNTR
          MOVE      ZERO,WBEFFLAG
          PACK      WBEFSPAR,SP70,SP70
.
          PACK      KEY8,WBEFELGN,SP70
          CALL      RAWBELF1
          IF        OVRCD=0
            GOTO      HOEW1000
          ENDIF
.
          CALL      WRWBELF1
.
          PACK      KEY11,WBOCVISN,WBOCCNTR,SP70
          CALL      RDWBOEC1
          IF        OVRCD=0
            MOVE      WBEFELGN,WBOCELEG

            CALL      IBACLOCK
            PACK      WBOCUDTE,CCC,CYY,CMM,CDD
            REP       " 0",WBOCUDTE
            MOVE      CTIMEIS,WBOCUTIM
            MOVE      USERKYIN,WBOCUUID
            CALL      UPWBOEC1
          ENDIF
.
HOEW9999  RETURN
+
.-----------------------------------------------------------
.        Generate Free Format OECW Eligibility Number
.-----------------------------------------------------------
GELW0000  MOVE      ZERO,GELWFLAG
.
          MOVE      "118",PRXCODE
          CALL      GETSLK00
          READ      CONTROLF,HUND18;*117,PTCNELGN
.
GELW0100  MOVE      PTCNELGN,WBEFELGN
          UNPACK    PTCNELGN,ANS,KEY7
          MATCH     "a",ANS
          GOTO      GELW1150 IF LESS
          MOVE      ZERO,F7
          MOVE      KEY7,F7
.
GELW1100  COMPARE   "9999999",F7               * at the end of the group
          GOTO      GELW1110 IF NOT EQUAL
.
          MATCH     "z",ANS
          GOTO      GELW9000 IF EQUAL
.
          REP       REPUNIQ,ANS
          MOVE      ZERO,F7
.
GELW1110  ADD       ONE,F7
          MOVE      F7,D7
          REP       " 0",D7
.
          PACK      KEY8,ANS,D7,SP70
          CALL      RAWBELF1
          IF        OVRCD=ONE
            PACK      KEY11,ANS,D7,SP1,SP1,ONE,SP70
            CALL      RAWBOEC1
            IF        OVRCD=ONE
              PACK      KEY14,ANS,D7,SP1,SP1,ONE,SP70
              CALL      RSWBOES1
              CALL      RKWBOES1
              BRANCH    OVRCD,GELW9500
.
              PACK      KEY8,ANS,D7,SP70
              MATCH     KEY8,WBOSVISN
              GOTO      GELW9500 IF NOT EQUAL
            ENDIF
          ENDIF
.
          GOTO      GELW1100
.
. no records
.
GELW1150  MOVE      ONE,F7
          MOVE      "a",ANS
.
          MOVE      F7,D7
          REP       " 0",D7
.
          PACK      KEY8,ANS,D7,SP70
          GOTO      GELW9500
.
GELW9000  MOVE      ONE,GELWFLAG                  * Passcode allocation full
          GOTO      GELW9999
.
GELW9500  MOVE      KEY8,PTCNELGN
          WRITAB    CONTROLF,HUND18;*117,PTCNELGN
.
          MATCH     "00000000",WBEFELGN
          GOTO      GELW0100 IF EQUAL
.
GELW9999  CALL      RELSLK00
          RETURN
+
.*****************************************************************************
.*                              CNCW0000           Called by: WPAT0000       *
.*     See if the patient has concession card of DVA type on file            *
.* Requires: PURNO - U/R number                                              *
.* Returns:  EXIT  0 = valid concession card found                           *
.*                 1 = no valid concession card found                        *
.*           DIM20 - Concession Card Number                                  *
.*****************************************************************************
.
CNCW0000  PACK      KEY19,PURNO,TILDA40
          CALL      RSPMCCD1                     * position on U/R
CNCW0500  CALL      RPPMCCD1                     * read previous record
          BRANCH    OVRCD,CNCW9100               * eof - nothing found
.
          MATCH     PURNO,PMCDURNO               * same U/R still ?
          GOTO      CNCW9100 IF NOT EQUAL        * no - nothing found
.
.         Check if the card type is DVA (Cat ct, Indicator 1)
.
          PACK      KEY5,CATCT,PMCDCTYP
          CALL      RDCODE1                      * code on file ?
          BRANCH    OVRCD,CNCW0500               * no - ignore record
.
          MATCH     SP1,TCINDC1                  * blank indicator 1 ?
          GOTO      CNCW0500 IF EQUAL            * yes - ignore record
.
          TYPE      TCINDC1                      * numeric indicator 1 ?
          GOTO      CNCW0500 IF NOT EQUAL        * no - ignore record
.
          MATCH     "3",TCINDC1                  * DVA card type ?
          GOTO      CNCW0500 IF NOT EQUAL        * no - ignore record
.
.         A DVA concession card has been found, so check
.         if there is an expiry date
.
          MATCH     SP8,PMCDEXDT                 * expiry date blank ?
          GOTO      CNCW1000 IF EQUAL            * yes - assume valid
.
.         There is an expiry date for the card, so check that the
.         card is current
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          MATCH     DIM8,PMCDEXDT                * card valid for date ?
          GOTO      CNCW9100 IF LESS             * no - nothing found
.
.         Concession card is current
.
CNCW1000  MATCH     SP20,PMCDCNUM                * card number blank ?
          GOTO      CNCW9100 IF EQUAL            * yes - nothing found
.
          MOVE      PMCDCNUM,DIM20               * load concession card number
.
          MOVE      ZERO,CNCWFLAG
          GOTO      CNCW9999
.
CNCW9100  MOVE      ONE,CNCWFLAG
.
CNCW9999  RETURN
+
.-----------------------------------------------------------
.        Clear WEBOECFD save variables
.-----------------------------------------------------------
.
CLSVOC00  MOVE      SP70,SVOCVISN
          MOVE      SP70,SVOCCNTR
          MOVE      SP70,SVOCSTAT
          MOVE      SP70,SVOCURNO
          MOVE      SP70,SVOCARID
          MOVE      SP70,SVOCTRID
          MOVE      SP70,SVOCRQDT
          MOVE      SP70,SVOCETYP
          MOVE      SP70,SVOCERRC
          PACK      SVOCERRD,SP70,SP70,SP70,SP70,SP70:
                             SP70,SP70,SP70,SP70,SP70
          MOVE      SP70,SVOCCDTE
          MOVE      SP70,SVOCCTIM
          MOVE      SP70,SVOCUDTE
          MOVE      SP70,SVOCUTIM
          MOVE      SP70,SVOCHOSP
          MOVE      SP70,SVOCELEG
          MOVE      SP70,SVOCMSID
.
CLSVOC99  RETURN
+
.-----------------------------------------------------------
.        Clear WEBELFFD save variables
.-----------------------------------------------------------
.
CLSVEF00  MOVE      SP70,SVEFELGN
          MOVE      SP70,SVEFURNO
          MOVE      SP70,SVEFOECC
          MOVE      SP70,SVEFTITL
          MOVE      SP70,SVEFFNAM
          MOVE      SP70,SVEFSNAM
          MOVE      SP70,SVEFHFGN
          MOVE      SP70,SVEFHFSN
          MOVE      SP70,SVEFGEND
          MOVE      SP70,SVEFBDAT
          MOVE      SP70,SVEFEADT
          MOVE      SP70,SVEFISTY
          MOVE      SP70,SVEFPLOS
          MOVE      SP70,SVEFDINT
          MOVE      SP70,SVEFPEAI
          MOVE      SP70,SVEFEMAD
          MOVE      SP70,SVEFPRIC
          MOVE      SP70,SVEFPRIM
          MOVE      SP70,SVEFCLTY
          MOVE      SP70,SVEFHFND
          MOVE      SP70,SVEFHFNT
          MOVE      SP70,SVEFHFMN
          MOVE      SP70,SVEFDIAG
          MOVE      SP70,SVEFSRV1
          MOVE      SP70,SVEFSRV2
          MOVE      SP70,SVEFSRV3
          MOVE      SP70,SVEFSRV4
          MOVE      SP70,SVEFSRV5
          MOVE      SP70,SVEFCUID
          MOVE      SP70,SVEFCDAT
          MOVE      SP70,SVEFCTIM
          MOVE      SP70,SVEFDVAN
          MOVE      SP70,SVEFVISN
          MOVE      SP70,SVEFCNTR
          MOVE      SP70,SVEFFLAG
          MOVE      SP70,SVEFTRID
          MOVE      SP70,SVEFMSID
.
CLSVEF99  RETURN
+
.-----------------------------------------------------------
.        Move  WEBOECFD to save variables
.-----------------------------------------------------------
.
MVSVOC00  MOVE      WBOCVISN,SVOCVISN
          MOVE      WBOCCNTR,SVOCCNTR
          MOVE      WBOCSTAT,SVOCSTAT
          MOVE      WBOCURNO,SVOCURNO
          MOVE      WBOCARID,SVOCARID
          MOVE      WBOCTRID,SVOCTRID
          MOVE      WBOCRQDT,SVOCRQDT
          MOVE      WBOCETYP,SVOCETYP
          MOVE      WBOCERRC,SVOCERRC
          MOVE      WBOCERRD,SVOCERRD
          MOVE      WBOCCDTE,SVOCCDTE
          MOVE      WBOCCTIM,SVOCCTIM
          MOVE      WBOCUDTE,SVOCUDTE
          MOVE      WBOCUTIM,SVOCUTIM
          MOVE      WBOCHOSP,SVOCHOSP
          MOVE      WBOCELEG,SVOCELEG
          MOVE      WBOCMSID,SVOCMSID
.
MVSVOC99  RETURN
+
.-----------------------------------------------------------
.        Move WEBELFFD to save variables
.-----------------------------------------------------------
.
MVSVEF00  MOVE      WBEFELGN,SVEFELGN
          MOVE      WBEFURNO,SVEFURNO
          MOVE      WBEFOECC,SVEFOECC
          MOVE      WBEFTITL,SVEFTITL
          MOVE      WBEFFNAM,SVEFFNAM
          MOVE      WBEFSNAM,SVEFSNAM
          MOVE      WBEFHFGN,SVEFHFGN
          MOVE      WBEFHFSN,SVEFHFSN
          MOVE      WBEFGEND,SVEFGEND
          MOVE      WBEFBDAT,SVEFBDAT
          MOVE      WBEFEADT,SVEFEADT
          MOVE      WBEFISTY,SVEFISTY
          MOVE      WBEFPLOS,SVEFPLOS
          MOVE      WBEFDINT,SVEFDINT
          MOVE      WBEFPEAI,SVEFPEAI
          MOVE      WBEFEMAD,SVEFEMAD
          MOVE      WBEFPRIC,SVEFPRIC
          MOVE      WBEFPRIM,SVEFPRIM
          MOVE      WBEFCLTY,SVEFCLTY
          MOVE      WBEFHFND,SVEFHFND
          MOVE      WBEFHFNT,SVEFHFNT
          MOVE      WBEFHFMN,SVEFHFMN
          MOVE      WBEFDIAG,SVEFDIAG
          MOVE      WBEFSRV1,SVEFSRV1
          MOVE      WBEFSRV2,SVEFSRV2
          MOVE      WBEFSRV3,SVEFSRV3
          MOVE      WBEFSRV4,SVEFSRV4
          MOVE      WBEFSRV5,SVEFSRV5
          MOVE      WBEFCUID,SVEFCUID
          MOVE      WBEFCDAT,SVEFCDAT
          MOVE      WBEFCTIM,SVEFCTIM
          MOVE      WBEFDVAN,SVEFDVAN
          MOVE      WBEFVISN,SVEFVISN
          MOVE      WBEFCNTR,SVEFCNTR
          MOVE      WBEFFLAG,SVEFFLAG
          MOVE      WBEFTRID,SVEFTRID
          MOVE      WBEFMSID,SVEFMSID
.
MVSVEF99  RETURN
+
.******************************************************************************
.         Check selected Hospital code (if multi hospital) Theatre Booking Only
.******************************************************************************
CHOB0000  COMPARE   ONE,IBCNMHOS
          GOTO      CHOB9000 IF NOT EQUAL
.
          PACK      PTHSHOSP,PTHSHOSP,SP70
          MATCH     SP10,PTHSHOSP           * All Hospitals
          GOTO      CHOB9000 IF EQUAL
.
          MATCH     PTHSHOSP,OPSEHOSP       * check hospital
          GOTO      CHOB9500 IF NOT EQUAL
.
CHOB9000  MOVE      ZERO,EXIT
          GOTO      CHOB9999
.
CHOB9500  MOVE      ONE,EXIT
CHOB9999  RETURN
+
.******************************************************************************
.         Validate eligibility criteria for OECW
.******************************************************************************
. Exclude all preadmissions not meeting the following criteria:
.
VOEW0000  MOVE      AURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "No PMI details exist for this admission",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
          MOVE      AURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            MOVE      "No PMI details exist for this admission",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
          MATCH     SP70,PSNAME            * surname set?
          IF        @EQUAL
            MOVE      "No surname exists on PMI",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
          MATCH     SP70,PGNAME            * given name set?
          IF        @EQUAL
            MOVE      "No given name exists on PMI",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
          MATCH     SP70,PBDATE            * DOB set?
          IF        @EQUAL
            MOVE      "No date of birth exists on PMI",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
          MATCH     SP70,AFUNDH            * Health fund set ?
          IF        @EQUAL
            MOVE      "No health fund exists on admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
.
          ELSE
.
            PACK      KEY6,AFUNDH,SP70
            CALL      RDPTFX11
            IF        OVRCD=1
              GOTO      VOEW1000
            ELSE
              MATCH     SP70,PTFXECLP
              IF        @EQUAL
                GOTO      VOEW1000
              ELSE
                PACK      KEY3,PTFXECLP,SP70
                CALL      RDWBPAR1                * participant code on file ?
                IF        OVRCD=1
                  GOTO      VOEW1000
                ELSE
                  PACK      KEY9,WBPRCODE,ANSO,ANSE,ANSC,SP70
                  CALL      RSWBPCP1                     * OEC capability ?
                  CALL      RKWBPCP1
                  IF        OVRCD=1
                    MOVE      "Participant does not have OEC capability",ERRMSG
                    CALL      PERR0000
                    GOTO      VOEW9500
                  ELSE
                    MATCH     WBPRCODE,WBPCCODE
                    IF        !@EQUAL
                      MOVE      "Participant does not have OECW capability",ERRMSG
                      CALL      PERR0000
                      GOTO      VOEW9500
                    ELSE
                      MATCH     "OEC",WBPCCPID
                      IF        !@EQUAL
                        MOVE      "Participant does not have OECW capability",ERRMSG
                        CALL      PERR0000
                        GOTO      VOEW9500
                      ELSE
                        GOTO      VOEW2000
                      ENDIF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
.
VOEW1000    PACK      KEY14,AFUNDH,ZERO8
            CALL      RDFUND1
            IF        OVRCD=1
              MOVE      "No health fund record exists for this admission",ERRMSG
              CALL      PERR0000
              GOTO      VOEW9500
            ELSE
              MOVE      "02",KEY2               * check HF history for HF Group
              PACK      KEY17,AFUNDH,KEY2,SP70
              CALL      PATDDHRD
.
              MATCH     SP70,PTHFHGRP
              IF        @EQUAL
                MOVE      "No health fund group / participant exists on health fund table",ERRMSG
                CALL      PERR0000
                GOTO      VOEW9500
              ELSE
                PACK      KEY3,PTHFHGRP,SP70
                CALL      RDWBPAR1                * participant code on file ?
                IF        OVRCD=1
                  MOVE      "Participant does not exist in participant table",ERRMSG
                  CALL      PERR0000
                  GOTO      VOEW9500
                ELSE
                  PACK      KEY9,WBPRCODE,ANSO,ANSE,ANSC,SP70
                  CALL      RSWBPCP1                     * OEC capability ?
                  CALL      RKWBPCP1
                  IF        OVRCD=1
                    MOVE      "Participant does not have OECW capability",ERRMSG
                    CALL      PERR0000
                    GOTO      VOEW9500
                  ELSE
                    MATCH     WBPRCODE,WBPCCODE
                    IF        !@EQUAL
                      MOVE      "Participant does not have OECW capability",ERRMSG
                      CALL      PERR0000
                      GOTO      VOEW9500
                    ELSE
                      MATCH     "OEC",WBPCCPID
                      IF        !@EQUAL
                        MOVE      "Participant does not have OECW capability",ERRMSG
                        CALL      PERR0000
                        GOTO      VOEW9500
                      ENDIF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
.
          ENDIF
.
VOEW2000  MATCH     SP70,AFNDTB            * Health fund table set ?
          IF        @EQUAL
            MOVE      "No health fund table exists on admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
          MATCH     SP70,ADATE             * Admission date set ?
          IF        @EQUAL
            MOVE      "No admission date exists on admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
          MATCH     SP70,ACLAIM            * claim type set ?
          IF        @EQUAL
            MOVE      "No claim type exists on admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
.         MATCH     SP70,ADIAG1            * admitting diagnosis set ?
.         IF        @EQUAL
.           MATCH     SP70,AMBS            * admitting mbs set ?
.           IF        @EQUAL
.             MOVE      "No Presenting Diagnosis OR MBS Item exists on admission table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEW9500
.           ENDIF
.         ENDIF
.
          MATCH     SP70,PMVXIDUS      * Intended stay (sameday) indicator set?
          IF        @EQUAL
            MOVE      "Intended stay (sameday indicator) not set on visit extension table",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
          MATCH     SP70,PMVXPILC      * Presenting Illness Code CAT (il) ?
          IF        @EQUAL
            MATCH     SP70,AMBS            * admitting mbs set ?
            IF        @EQUAL
              MOVE      "No Presenting Illness Code OR MBS Item exists on visit/admission table",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
            ENDIF
          ENDIF
.
          MOVE      AADMNO,KEY8
          CALL      RDPTELG1
          IF        OVRCD=1
            MOVE      "No eligibility details exist for this admission",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
          MATCH     "1",PTELCMPL          * eligibility Check Complete ?
          IF        @EQUAL
            MOVE      "Eligibility Check Complete - no request sent",ERRMSG
            CALL      PERR0000
            GOTO      VOEW9500
          ENDIF
.
.         MATCH     SP70,PTELSPEA          * Pre-Existing Ailment indicator set?
.         IF        @EQUAL
.           MOVE      "PEA Indicator not set on eligibility table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEW9500
.         ENDIF
.
.         MATCH     SP70,PMVXIDUS      * Intended stay (sameday) indicator set?
.         IF        @EQUAL
.           MOVE      "Intended stay (sameday indicator) not set on visit extension table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEW9500
.         ENDIF
.
.         MATCH     SP70,PMVXPILC      * Presenting Illness Code CAT (il) ?
.         IF        @EQUAL
.           MOVE      "Presenting Illness Code CAT (il) not set on visit extension table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEW9500
.         ENDIF
.
VOEW9000  MOVE      ZERO,EXIT               * send OEC
          GOTO      VOEW9999
.
VOEW9500  MOVE      ONE,EXIT                * don't send OEC
VOEW9999  RETURN
+
.******************************************************************************
.         Validate eligibility criteria for OECW Theatre Bookings only
.******************************************************************************
. Exclude all theatre bookings not meeting the following criteria:
.
VOEB0000  MOVE      BKREURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "No PMI details exist for this theatre booking",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
          ENDIF
.
          MOVE      BKREURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            MOVE      "No PMI details exist for this theatre booking",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
          ENDIF
.
          MATCH     SP70,PSNAME            * surname set?
          IF        @EQUAL
            MOVE      "No surname exists on PMI",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
          ENDIF
.
          MATCH     SP70,PGNAME            * given name set?
          IF        @EQUAL
            MOVE      "No given name exists on PMI",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
          ENDIF
.
          MATCH     SP70,PBDATE            * DOB set?
          IF        @EQUAL
            MOVE      "No date of birth exists on PMI",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
          ENDIF
.
          MATCH     SP70,WBEFHFND          * Health fund set ?
          IF        @EQUAL
            MOVE      "No health fund exists on webelfaf table",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
.
          ELSE
.
            PACK      KEY6,WBEFHFND,SP70
            CALL      RDPTFX11
            IF        OVRCD=1
              GOTO      VOEB1000
            ELSE
              MATCH     SP70,PTFXECLP
              IF        @EQUAL
                GOTO      VOEB1000
              ELSE
                PACK      KEY3,PTFXECLP,SP70
                CALL      RDWBPAR1                * participant code on file ?
                IF        OVRCD=1
                  GOTO      VOEB1000
                ELSE
                  PACK      KEY9,WBPRCODE,ANSO,ANSE,ANSC,SP70
                  CALL      RSWBPCP1                     * OEC capability ?
                  CALL      RKWBPCP1
                  IF        OVRCD=1
                    MOVE      "Participant does not have OEC capability",ERRMSG
                    CALL      PERR0000
                    GOTO      VOEB9500
                  ELSE
                    MATCH     WBPRCODE,WBPCCODE
                    IF        !@EQUAL
                      MOVE      "Participant does not have OECW capability",ERRMSG
                      CALL      PERR0000
                      GOTO      VOEB9500
                    ELSE
                      MATCH     "OEC",WBPCCPID
                      IF        !@EQUAL
                        MOVE      "Participant does not have OECW capability",ERRMSG
                        CALL      PERR0000
                        GOTO      VOEB9500
                      ELSE
                        GOTO      VOEB2000
                      ENDIF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
.
VOEB1000    PACK      KEY14,WBEFHFND,ZERO8
            CALL      RDFUND1
            IF        OVRCD=1
              MOVE      "No health fund record exists for webelfaf",ERRMSG
              CALL      PERR0000
              GOTO      VOEB9500
            ELSE
              MOVE      "02",KEY2               * check HF history for HF Group
              PACK      KEY17,WBEFHFND,KEY2,SP70
              CALL      PATDDHRD
.
              MATCH     SP70,PTHFHGRP
              IF        @EQUAL
                MOVE      "No health fund group / participant exists on health fund table",ERRMSG
                CALL      PERR0000
                GOTO      VOEB9500
              ELSE
                PACK      KEY3,PTHFHGRP,SP70
                CALL      RDWBPAR1                * participant code on file ?
                IF        OVRCD=1
                  MOVE      "Participant does not exist in participant table",ERRMSG
                  CALL      PERR0000
                  GOTO      VOEB9500
                ELSE
                  PACK      KEY9,WBPRCODE,ANSO,ANSE,ANSC,SP70
                  CALL      RSWBPCP1                     * OEC capability ?
                  CALL      RKWBPCP1
                  IF        OVRCD=1
                    MOVE      "Participant does not have OECW capability",ERRMSG
                    CALL      PERR0000
                    GOTO      VOEB9500
                  ELSE
                    MATCH     WBPRCODE,WBPCCODE
                    IF        !@EQUAL
                      MOVE      "Participant does not have OECW capability",ERRMSG
                      CALL      PERR0000
                      GOTO      VOEB9500
                    ELSE
                      MATCH     "OEC",WBPCCPID
                      IF        !@EQUAL
                        MOVE      "Participant does not have OECW capability",ERRMSG
                        CALL      PERR0000
                        GOTO      VOEB9500
                      ENDIF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
.
          ENDIF
.
VOEB2000  MATCH     SP70,WBEFHFNT          * Health fund table set ?
          IF        @EQUAL
            MOVE      "No health fund table exists on webelfaf table",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
          ENDIF
.
          MATCH     SP70,WBEFEADT          * Admission date set ?
          IF        @EQUAL
            MOVE      "No admission date exists on webelfaf table",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
          ENDIF
.
          MATCH     SP70,WBEFCLTY          * claim type set ?
          IF        @EQUAL
            MOVE      "No claim type exists on webelfaf table",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
          ENDIF
.
.         MATCH     SP70,WBEFDIAG          * admitting diagnosis set ?
.         IF        @EQUAL
.           MATCH     SP70,WBEFPRIM        * admitting mbs set ?
.           IF        @EQUAL
.             MOVE      "No Presenting Diagnosis OR MBS Item exists on webelfaf table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEB9500
.           ENDIF
.         ENDIF
.
          MATCH     SP70,WBEFISTY      * Intended stay (sameday) indicator set?
          IF        @EQUAL
            MOVE      "Intended stay (sameday indicator) not set on webelfaftable",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
          ENDIF
.
          MATCH     SP70,WBEFPRIC      * Presenting Illness Code CAT (il) ?
          IF        @EQUAL
            MATCH     SP70,WBEFPRIM        * admitting mbs set ?
            IF        @EQUAL
              MOVE      "No Presenting Illness Code OR MBS Item exists on webelfaf table",ERRMSG
            CALL      PERR0000
            GOTO      VOEB9500
            ENDIF
          ENDIF
.
.          MOVE      DBKREBOO,KEY8
.          CALL      RDPTELG1
.          IF        OVRCD=1
.            MOVE      "No eligibility details exist for this theatre booking",ERRMSG
.            CALL      PERR0000
.            GOTO      VOEB9500
.          ENDIF
..
.          MATCH     "1",PTELCMPL          * eligibility Check Complete ?
.          IF        @EQUAL
.            MOVE      "Eligibility Check Complete - no request sent",ERRMSG
.            CALL      PERR0000
.            GOTO      VOEB9500
.          ENDIF
.
.         MATCH     SP70,PTELSPEA          * Pre-Existing Ailment indicator set?
.         IF        @EQUAL
.           MOVE      "PEA Indicator not set on eligibility table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEB9500
.         ENDIF
.
.         MATCH     SP70,PMVXIDUS      * Intended stay (sameday) indicator set?
.         IF        @EQUAL
.           MOVE      "Intended stay (sameday indicator) not set on visit extension table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEB9500
.         ENDIF
.
.         MATCH     SP70,WBEFPRIC      * Presenting Illness Code CAT (il) ?
.         IF        @EQUAL
.           MOVE      "Presenting Illness Code CAT (il) not set on webelfaf table",ERRMSG
.           CALL      PERR0000
.           GOTO      VOEB9500
.         ENDIF
.
VOEB9000  MOVE      ZERO,EXIT               * send OEC
          GOTO      VOEB9999
.
VOEB9500  MOVE      ONE,EXIT                * don't send OEC
VOEB9999  RETURN
+
.******************************************************************************
.         Write to OECW table (weboec) Theatre Bookings only
.******************************************************************************
.
WOEB0000  MATCH     "1",PTCNWSIN
          GOTO      WOEB9999 IF NOT EQUAL
.
          MATCH     "1",PTCNOECW
          GOTO      WOEB9999 IF NOT EQUAL
.
          COMPARE   ONE,OECWINDC
          GOTO      WOEB9999 IF NOT EQUAL
.
          CALL      GNXOEB00
          CALL      CLWEBOEC              * Write new weboecaf record
.
          MOVE      SVOCVISN,WBOCVISN
          MOVE      KEY3,WBOCCNTR
          MOVE      " 0",WBOCSTAT
          MOVE      SVOCURNO,WBOCURNO
          MOVE      SVOCARID,WBOCARID
          PACK      WBOCARID,WBOCARID,SP70
          LJUSTIFY  WBOCARID
          MOVE      SP70,WBOCTRID
          CALL      IBACLOCK
          PACK      WBOCRQDT,CCC,CYY,CMM,CDD
          REP       " 0",WBOCRQDT
          MOVE      SP70,WBOCETYP
          MOVE      SP70,WBOCERRC
          MOVE      SP70,WBOCERRD
          PACK      WBOCCDTE,CCC,CYY,CMM,CDD
          REP       " 0",WBOCCDTE
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,WBOCCTIM
          MOVE      SP70,WBOCUDTE
          MOVE      SP70,WBOCUTIM
          MOVE      SVOCHOSP,WBOCHOSP
          MOVE      SP70,WBOCELEG
          MOVE      SP70,WBOCMSID
          MOVE      SP70,WBOCSPAR
.
          MOVE      USERKYIN,WBOCCUID
          MOVE      SP70,WBOCUUID
.
          PACK      KEY11,SVOCVISN,KEY3,SP70
          CALL      RAWBOEC1
          IF        OVRCD=1
.
            CALL      IBACLOCK
            PACK      WBOCUDTE,CCC,CYY,CMM,CDD
            REP       " 0",WBOCUDTE
            MOVE      CTIMEIS,WBOCUTIM
            MOVE      USERKYIN,WBOCUUID

            CALL      WRWBOEC1
          ELSE
            GOTO      WOEB0000
          ENDIF
.
          CALL      HOEB0000
.
          ADD       ONE,WRTCT               * requests sent counter
.
.
WOEB9999  RETURN
+
.*******************************************************************************
.        Get Next weboecaf report counter Theatre Bookings Only
.*******************************************************************************
GNXOEB00  MOVE      "  1",KEY3
          PACK      KEY11,SVOCVISN,Z70
          CALL      RSWBOEC1
          CALL      RPWBOEC1
          BRANCH    OVRCD,GNXOEB99
.
          MATCH     SVOCVISN,WBOCVISN
          GOTO      GNXOEB99 IF NOT EQUAL
.
          SQUEEZE   WBOCCNTR
          MOVE      WBOCCNTR,F3
.
          ADD       ONE,F3
          MOVE      F3,KEY3
.
GNXOEB99  RETURN
+
.-------------------------------------------------------------------------------
. Write OECW History Record to webelfaf (OECW Free Format table) Theatre Booking
.-------------------------------------------------------------------------------
.
HOEB0000  CALL      CLWEBELF
.
          PACK      SAVKEY11,WBOCVISN,WBOCCNTR,SP70
.
HOEB1000  CALL      GELW0000           * Generate Eligibility Number
          BRANCH    GELWFLAG,HOEB9999
.
          MOVE      SAVKEY11,KEY11
          CALL      RDWBOEC1
.
          MOVE      WBOCURNO,WBEFURNO
          MOVE      ZERO,WBEFOECC
          MOVE      SVEFTITL,WBEFTITL
          PACK      WBEFFNAM,SVEFFNAM,SP70
          PACK      WBEFSNAM,SVEFSNAM,SP70
          MOVE      SP70,WBEFHFGN
          MOVE      SP70,WBEFHFSN
          MOVE      SVEFGEND,WBEFGEND
          MOVE      SVEFBDAT,WBEFBDAT
          MOVE      SVEFEADT,WBEFEADT
          MOVE      SVEFISTY,WBEFISTY
          MOVE      SVEFPLOS,WBEFPLOS
          MOVE      SVEFDINT,WBEFDINT
          MOVE      SVEFPEAI,WBEFPEAI
          MOVE      SVEFEMAD,WBEFEMAD
.
          MOVE      SVEFPRIC,WBEFPRIC
.
          MOVE      SVEFPRIM,WBEFPRIM
.
          MOVE      SVEFCLTY,WBEFCLTY
          MOVE      SVEFHFND,WBEFHFND
          MOVE      SVEFHFNT,WBEFHFNT
          MOVE      SVEFHFMN,WBEFHFMN
          MOVE      SVEFDIAG,WBEFDIAG
.
          MATCH     SP70,USERKYIN
          IF        @EQUAL
            MOVE      "iba       ",WBEFCUID
          ELSE
            MOVE      USERKYIN,WBEFCUID
          ENDIF
.
          CALL      IBACLOCK
          PACK      WBEFCDAT,CCC,CYY,CMM,CDD
          REP       " 0",WBEFCDAT
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,WBEFCTIM
.
          CALL      CNCB0000                       * DVA conc card on file ?
          IF        EXIT=1
            PACK      WBEFDVAN,SVEFDVAN,SP20         * No
          ELSE
            MOVE      DIM20,WBEFDVAN               * yes
          ENDIF
.
          MOVE      WBOCVISN,WBEFVISN
          MOVE      WBOCCNTR,WBEFCNTR
          MOVE      ZERO,WBEFFLAG
          PACK      WBEFSPAR,SP70,SP70
.
          PACK      KEY8,WBEFELGN,SP70
          CALL      RAWBELF1
          IF        OVRCD=0
            GOTO      HOEB1000
          ENDIF
.
          CALL      WRWBELF1
.
          PACK      KEY11,WBOCVISN,WBOCCNTR,SP70
          CALL      RDWBOEC1
          IF        OVRCD=0
            MOVE      WBEFELGN,WBOCELEG
            CALL      IBACLOCK
            PACK      WBOCUDTE,CCC,CYY,CMM,CDD
            REP       " 0",WBOCUDTE
            MOVE      CTIMEIS,WBOCUTIM
            MOVE      USERKYIN,WBOCUUID
            CALL      UPWBOEC1
          ENDIF
.
HOEB9999  RETURN
+
.*****************************************************************************
.*                              CNCB0000           Called by: WPAT0000       *
.*     See if the patient has concession card of DVA type on file            *
.* Requires: SVEFURNO - U/R number                                           *
.* Returns:  EXIT  0 = valid concession card found                           *
.*                 1 = no valid concession card found                        *
.*           DIM20 - Concession Card Number                                  *
.*****************************************************************************
.
CNCB0000  PACK      KEY19,SVEFURNO,TILDA40
          CALL      RSPMCCD1                     * position on U/R
CNCB0500  CALL      RPPMCCD1                     * read previous record
          BRANCH    OVRCD,CNCB9100               * eof - nothing found
.
          MATCH     SVEFURNO,PMCDURNO               * same U/R still ?
          GOTO      CNCB9100 IF NOT EQUAL        * no - nothing found
.
.         Check if the card type is DVA (Cat ct, Indicator 1)
.
          PACK      KEY5,CATCT,PMCDCTYP
          CALL      RDCODE1                      * code on file ?
          BRANCH    OVRCD,CNCB0500               * no - ignore record
.
          MATCH     SP1,TCINDC1                  * blank indicator 1 ?
          GOTO      CNCB0500 IF EQUAL            * yes - ignore record
.
          TYPE      TCINDC1                      * numeric indicator 1 ?
          GOTO      CNCB0500 IF NOT EQUAL        * no - ignore record
.
          MATCH     "3",TCINDC1                  * DVA card type ?
          GOTO      CNCB0500 IF NOT EQUAL        * no - ignore record
.
.         A DVA concession card has been found, so check
.         if there is an expiry date
.
          MATCH     SP8,PMCDEXDT                 * expiry date blank ?
          GOTO      CNCB1000 IF EQUAL            * yes - assume valid
.
.         There is an expiry date for the card, so check that the
.         card is current
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          MATCH     DIM8,PMCDEXDT                * card valid for date ?
          GOTO      CNCB9100 IF LESS             * no - nothing found
.
.         Concession card is current
.
CNCB1000  MATCH     SP20,PMCDCNUM                * card number blank ?
          GOTO      CNCB9100 IF EQUAL            * yes - nothing found
.
          MOVE      PMCDCNUM,DIM20               * load concession card number
.
          MOVE      ZERO,CNCWFLAG
          GOTO      CNCB9999
.
CNCB9100  MOVE      ONE,CNCWFLAG
.
CNCB9999  RETURN
+
.*****************************************************************************
.*                              DMBS0000                                     *
.*              Create OECW for Theatre Extra MBS Items                      *
.*   Loop through theatre and send OECWs for each theatre MBS item           *
.*****************************************************************************
DMBS0000  MOVE      AMBS,SAVEAMBS
.
          PACK      KEY31,AADMNO,SP70
          CALL      RSOPDEA2
DMBS1000  CALL      RKOPDEA2
          BRANCH    OVRCD,DMBS9999
.
          MATCH     DAADMNO,OPDAADMN
          GOTO      DMBS9999 IF NOT EQUAL
.
          MATCH     "3",DOPDASTA
          GOTO      DMBS1000 IF EQUAL          * Cancelled
.
          MATCH     SP70,OPDAPROV
          IF        !@EQUAL & !@EOS
            MOVE      OPDAPROV,AMBS
            CALL      WOEC0000
          ENDIF
.
          MATCH     SP70,OPDAPRV2
          IF        !@EQUAL & !@EOS
            MOVE      OPDAPRV2,AMBS
            CALL      WOEC0000
          ENDIF
.
          MATCH     SP70,OPDAPRV3
          IF        !@EQUAL & !@EOS
            MOVE      OPDAPRV3,AMBS
            CALL      WOEC0000
          ENDIF
.
          PACK      KEY13,OPDAUNIQ,SP70
          CALL      RSOPMBS1
DMBS2000  CALL      RKOPMBS1
          BRANCH    OVRCD,DMBS1000
.
          MATCH     OPDAUNIQ,OPMBUNIQ
          GOTO      DMBS1000 IF NOT EQUAL
.
          MATCH     SP70,OPMBMBSI
          IF        !@EQUAL & !@EOS
            MOVE      OPMBMBSI,AMBS
            CALL      WOEC0000
          ENDIF
.
          GOTO      DMBS2000
.
DMBS9999  MOVE      SAVEAMBS,AMBS
          RETURN
+
          INC       STD001IO
          INC       CLBOKRC1
          INC       CLBOKRX1
          INC       CLOPRDEA
          INC       CLOPRSES
          INC       CLPMSOEC
          INC       CLPMSOES
          INC       CLPMSELF
          INC       CLWEBOEC
          INC       CLWEBOES
          INC       CLWEBELF
          INC       PATFNDKY                * Keyin the health fund
          INC       PATHSPKY
          INC       PATDDHRD
.
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       OPRDEAIO/INC
          INC       OPRMBSIO/INC
          INC       OPRSESIO/INC
.
          INC       PATCODIO/INC            * Codes file
          INC       PATDDHIO/INC            * HF history file
          INC       PATFN1IO/INC            * Health fund file
          INC       PATFX1IO/INC            * Health fund extension file
          INC       PATHSPIO/INC            * hospital file
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC            * Admission file
          INC       PATELGIO/INC            * Eligibility file
          INC       PATPARIO/INC            * Eclipse participants
          INC       PATPCPIO/INC            * Eclipse participants capabilities
          INC       PMSCCDIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC            * Visit Extension file
          INC       PMSOECIO/INC            * Online Eligibility Check file
          INC       PMSOESIO/INC
          INC       PMSELFIO/INC            
          INC       WEBOECIO/INC
          INC       WEBOESIO/INC
          INC       WEBELFIO/INC
          INC       WEBPARIO/INC            * WebServices participants
          INC       WEBPCPIO/INC            * WebServices participants cap
+
.


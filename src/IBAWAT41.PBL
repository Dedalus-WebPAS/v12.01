.******************************************************************************
.* System        :  Waiting Lists                                             *
.* Program       :  IBAWAT41.PBL                                              *
.* Description   :  Indicator Procedure Report                                *
.******************************************************************************
.* Date          :  25/07/1990                                                *
.* Author        :  Justin Coates                                             *
.* Function      :  Print all patients with a one (unscheduled) or            *
.*                  two (scheduled) or three (pre-admitted) status            *
.* Modifications :                                                            *
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.03  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.******************************************************************************
.
.         IBAWAT41.PBL        Indicator Procedure Report
.         ============        ==========================
.         
.         KSEQ0000 - Determine if to produce the report or exit
.
.         REPT0000 - Produce the report
.                    Loop through the second index of the treatment file
.                    and write to a temp file all the patients with a status
.                    of one, two or three.
.                    When finished reading treatment file, produce the report
.                    by looping through each entry of the temp file and 
.                    displaying the details
.
.
          INC       STD001FD                * common variables
          INC       PATCODFD/INC            * codes file
          INC       PATDO1FD/INC            * doctors file
          INC       PATMA1FD/INC            * master file
          INC       WATCONFD/INC            * WL control file
          INC       WATMESFD/INC            * WL comments file
          INC       WATPROFD/INC            * WL procedure file
          INC       WATTR1FD/INC            * WL treatment file
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
. ------ program variables ------
.
CMDLINE   DIM       42                      * line for 'execute' command
CNTPATTL  FORM      5                       * total no. of patients in report
DIM9      DIM       9                       * spare variable
QUPER     FORM      1                       * ? performed flag
.
. ------ variables for the report ------
.
PRDESC    DIM       53                      * procedure code/description
PRNAME    DIM       33                      * patients name
PRUNIT    DIM       23                      * current unit code/description
PRDOCT    DIM       29                      * current doct code/description
PRSEX     DIM       8                       * patients sex             *C-49016
PRCOMM1   DIM       30                      * line one of comments
PRCOMM2   DIM       30                      * line two of comments
.
. ------ variables needed for the temporary index file ------
.
TEMPFILE  IFILE     SQL, FIXED=149, KEY="U1-8,9-17,18-19"
.
.Name     Type      Length  Phys  Sta-Fin
.----     ----      ------  ----  -------
XWMURNO   DIM       8        8      1-8     * patients UR number
XWMCODE   DIM       9        9      9-17    * procedure code
DXWMCNT   DIM       2        2     18-19    * procedure code count as a dim
XWMDESC   DIM       50       50    20-69    * procedure description
XWMDOCTO  DIM       6        6     70-75    * doctors description
XWMUNIT   DIM       3        3     76-78    * unit/clinic code
XWATEXT   DIM       70       70    79-148   * comments
.
.End of Record                    149-149
.
XWMCNT    FORM      2                       * procedure code count as a form
.
. ------ program constants ------
.
CATWG     INIT      "WG"                    * procedure category
CATWU     INIT      "WU"                    * U/C category
DASH      INIT      "-"                     * a dash
FILPREC   INIT      "wat41t"                * prefix for temp file clinic
PREFDOCT  INIT      "Doctor: "              * Doctor Code prefix for report
PREFUNIT  INIT      "Unit: "                * Unit/Clinic prefix for report
PRINTLIM  FORM      "55"                    * no. lines to print per page
.
PRGID     INIT      "IBAWAT41"
PRGNAM    DIM       26                      * set up in init0000
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000                * initialization and open files
.
ML0500    CALL      KSEQ0000                * enter the report type
          BRANCH    MOPTION,ML1000          * do the report
.
          GOTO      ML9999
.
. ------ produce the report ------
.
ML1000    CALL      REPT0000                * produce report
          GOTO      ML9999
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  DISPLAY   *P1:1,*EF               * clear screen
          OPEN      CONTROLF,"controlf"     * control file
          READ      CONTROLF,THIRTY7;*122,WTCNTPUC,*185,WTCNTPDS
.
          CLEAR     PRGNAM
          APPEND    "INDICATOR ",PRGNAM
          APPEND    WTCNTPUC,PRGNAM
          APPEND    " REPORT",PRGNAM
          RESET     PRGNAM
.
          CALL      DISPHEAD                * display program heading
          CALL      IBACLOCK                * get current date/time
          CALL      TFILENAM
.
          OPEN      PATCODE1,"patcodes"     * open codes file
          OPEN      PATDO1A1,"patdo1af"     * open doctors file
          OPEN      PATMA1A1,"patma1af"     * open master file
          OPEN      PATMX1A1,"patmx1af"     * open master extension file
          OPEN      WATMESA1,"watmesaf"     * WL comments file
          OPEN      WATPROA1,"watproaf"     * WL procedure file
          OPEN      WATTR1A2,"wattr1af"     * WL treatment file
.
          MOVE      ONE,CNEWDS              * new ? option
.
INIT9999  RETURN
+
.*********************************************************************
.*                  KSEQ0000                    Called by : ML0500   *
.*        Enter the sequence to report by                            *
.*        Returns : MOPTION = 0   exit selected                      *
.*                  MOPTION = 1   report selected                    *
.*********************************************************************
KSEQ0000  DISPLAY   *P1:3,*EF:
                    *V2LON,*P1:4,"0",*P1:5,"1",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Print Report":
                    *P1:7,"Select Option : "
.
KSEQ1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION:
                    *P17:7,*DV,MOPTION 
.
          BRANCH    MOPTION,KSEQ9999        * one entered
.
          COMPARE   ZERO,MOPTION            * was 0 entered
          GOTO      KSEQ9999 IF EQUAL       * yes
.
          BEEP
          GOTO      KSEQ1000                * invalid selection
.
KSEQ9999  RETURN
+
.*********************************************************************
.*                  REPT0000                    Called by : ML1000   *
.*        Generates the report                                       *
.*********************************************************************
REPT0000  CALL      CRTP0000                * create temp file
          DISPLAY   *P1:24,"Obtaining the valid entries.",*EL
.
. ------ start looping through 2nd index of treatment file ------
.
REPT1000  PACK      KEY19,SP8,SP9,SP2       * start at beginning
          CALL      RDSTREA2                * positional read
.
. -----------------------------------------
. ------ next read on treatment file ------
. -----------------------------------------
.
REPT2000  CALL      RDKTREA2                * next read
          BRANCH    OVRCD,REPT5000          * finished report
.
          DISPLAY   *P70:24,WMURNO          * display UR to show working
.
          MATCH     ZEROUR,WMURNO           * is it a zero UR number
          GOTO      REPT2000 IF EQUAL       * yes, so ignore
.
          MATCH     "0",WMURNO              * is it the other zero UR number
          GOTO      REPT2000 IF EQUAL       * yes, so ignore
.
. ------ check that have the correct status of 1 or 2 or 3 ------
.
          BRANCH    WMSTAT,REPT4000,REPT4000,REPT4000
.                          unched    sched   pre-adm  
.
          GOTO      REPT2000
.
. ------ have a correct record so write a temp file record ------
.
REPT4000  PACK      KEY21,WMURNO,WMCODE,DWMCNT,SP1,ONE * get first comments line
          PACK      WATEXT,SP30,SP30,SP10   * clear incase read fails
          CALL      RDMESA1                 * read comments file
          MOVE      WMCNT,DWMCNT            * set up as a dim
          PACK      KEY19,WMURNO,WMCODE,DWMCNT
          WRITE     TEMPFILE,KEY19;WMURNO,WMCODE,DWMCNT,WMDESC,WMDOCTOR,WMUNIT:
                                   WATEXT
.
          DISPLAY   *P70:24,*V2LON,WMURNO   * highlight UR to show it is valid
.
          GOTO      REPT2000                * read next record
.
. -----------------------------------------------------------
. ------ have finished looping through treatment file, ------
. ------ start doing the report                        ------
. -----------------------------------------------------------
.
REPT5000  PACK      KEY19,SP20              * key to position at start
          READ      TEMPFILE,KEY19;;        * positional read
          CALL      IBACLOCK                * get current date and time
          MOVE      ZERO,CPAGENO            * initialize page number
          MOVE      ZERO,CNTPATTL           * clear total count
          DISPLAY   *P1:24,"Producing the report now.",*EL
          CALL      NEWP0000                * print the heading
.
. ------ read next record of temp file ------
.
REPT5100  READKS    TEMPFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWATEXT
          GOTO      REPT9000 IF OVER
          MOVE      DXWMCNT,XWMCNT          * get form value back
.
. ------ obtain the current information to display ------
.
REPT5500  PACK      KEY8,XWMURNO            * UR number as key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,REPT5100          * invalid UR number
          MOVE      PTITL,PACTITLE          * title
          MOVE      PGNAME,PACGNAME         * given name
          MOVE      PSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
          PACK      PRNAME,PACFNAME         * store the name
          RESET     PRNAME
.
. ------ format the date of birth ------
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * format the date
.
. ------ format the sex ------
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,PRSEX
.* ******************************************   End of Changes         *C-49016
.
. ------ format the procedure group and description ------
.
REPT5600  PACK      KEY9,XWMCODE            * procedure code as key
          CALL      RDPROA1                 * read procedure file
          PACK      KEY5,CATWG,WPGRP        * key of procedure group
          CALL      RDCODE1                 * read codes file to get grp desc.
          MOVE      TDESC,DIM9              * shorten the description
          PACK      PRDESC,DIM9,SP1,XWMDESC * format the full description
.
. ------ format the doctors code and name ------
.
          PACK      KEY6,XWMDOCTO           * doctors code as key
          CALL      RDDOCT1                 * read doctors file
          PACK      PRDOCT,PREFDOCT,DTITL   * set up name
          ENDSET    PRDOCT                  * get to last character
.
. ------ remove the blanks from title ------
.
RETP5700  MATCH     SP1,PRDOCT              * is the last char blank ??
          GOTO      RETP5800 IF NOT EQUAL   * no
.
          BUMP      PRDOCT,-1               * get preceeding char
          GOTO      RETP5700
.
RETP5800  MOVE      DGNAME,ANS              * get first initial
          APPEND    DOT,PRDOCT              * add dot
          APPEND    ANS,PRDOCT              * add initial
          APPEND    DOT,PRDOCT              * add dot
          APPEND    DSNAME,PRDOCT           * add surname 
          RESET     PRDOCT
.
. ------ format the unit description ------
.
          PACK      KEY5,CATWU,XWMUNIT      * unit code as key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNIT,PREFUNIT,TDESC   * set up description
.
. ------ format the comments ------
.
          PACK      PRCOMM1,XWATEXT,SP30    * get first line of comments
          RESET     XWATEXT,THIRTY1         * reset to 31st char.
          PACK      PRCOMM2,XWATEXT,SP30    * get second line of comments
.
. ------ print the line see if it will fit on page ------
.
          COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      REPT6000 IF NOT LESS    * no
.
          CALL      NEWP0000                * print new page heading
.
REPT6000  CALL      PLIN0000                * print the line
          GOTO      REPT5100                * read next record
.
. ------ report is finished ------
.
REPT9000  PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Grand Total Patients Printed ",SP20,SP4," : ":
                    CNTPATTL:
                    *N,*N,*N,"***  END OF REPORT ****"
.
          CALL      DETP0000                * delete the temp file
.
REPT9999  RETURN
+
.*********************************************************************
.*                  NEWP0000                    Called by : REPT0000 *
.*        Prints the new page heading for a report                   *
.*********************************************************************
NEWP0000  MOVE      ZERO,CNOUNDLN           * underlines at end of page
.
NEWP0500  CALL      HEAD132                 * do the new heading
          MOVE      THREE,CVERT             * set line counter
          CALL      PTBH0000                * print a table heading
.
NEWP9999  RETURN
+
.*********************************************************************
.*                  PLIN0000                    Called by : REPT0000 *
.*        Prints a single data line of the report                    *
.*        ie. the information about a patients procedure             *
.*        Paras/Returns : CNTPATCL    count of pats                  *
.*                        CNTPATTL    count of total patients        *
.*                        CVERT       current row number             *
.*********************************************************************
PLIN0000  PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,"|",XWMURNO,*13,PRNAME,*47,"|",*48,PRDESC,*101,"|":
                    PRCOMM1,*132,"|":
                    *N,"|",*13,"Sex : ",PRSEX,*27,"D.O.B. : ",CPCDATE,*47,"|":
                    *48,PRDOCT,*78,PRUNIT,*101,"|",PRCOMM2,*132,"|"
.
          DISPLAY   *P70:24,XWMURNO         * print UR to show it is working
.
          ADD       THREE,CVERT             * add to line counter
          ADD       ONE,CNTPATTL            * add to total pat. count
.
PLIN9999  RETURN
+
.*********************************************************************
.*                  PTBH0000                    Called by : NEWP0000 *
.*        Prints a new table heading                        REPT0000 *
.*        Needed at a new page                                       *
.*        Paras/Returns : CVERT      current row number              *
.*********************************************************************
PTBH0000  PRINT     *N,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*1,"|",*3,"U/R",*13,"Personal Details",*47,"| ":
                    *49,"Medical Details",*101,"| Comments",*132,"|"
.
          ADD       THREE,CVERT             * add to line count
.
PTBH9999  RETURN
+
.*********************************************************************
.*                  CRTP0000                    Called by : REPT0000 *
.*        Create temporary file                                      *
.*********************************************************************
CRTP0000  CALL      DETP0000                    * delete the temp file
          DISPLAY   *V2LON,*BLINKON,*P1:24,"Please Wait.",*EL
          CLEAR     CMDLINE               
          APPEND    "isbuild ",CMDLINE    
          APPEND    TFILNAME,CMDLINE        
          APPEND    " 149 U1-8,9-17,18-19",CMDLINE  
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID              * create temp file
.
          OPEN      TEMPFILE,TFILNAME        
          DISPLAY   *P1:24,"Producing the report now.....",*EL
.
CRTP9999  RETURN
+
.*********************************************************************
.*                  DETP0000                    Called by : REPT0000 *
.*        Delete temporary file                                      *
.*********************************************************************
DETP0000  CLEAR     CMDLINE                 * clear the command line
          APPEND    "iserase ",CMDLINE      * create command
          APPEND    TFILNAME,CMDLINE        * add file name
          RESET     CMDLINE
          CLOSE     TEMPFILE                * close temp files so iserase
          EXECUTE   CMDLINE,TASKID          * execute the delete command
.
          DISPLAY   *P1:24,"Temporary file deleted.",*EL
.
DETP9999  RETURN
+
.
. ------ I/O routines needed ------
.
          INC       STD001IO                * standard IO routines
          INC       SEXDSCIO
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       PATMA1IO/INC            * master file
          INC       WATMESIO/INC            * WL comments file
          INC       WATPROIO/INC            * WL procedure file
          INC       WATTR1IO/INC            * WL treatment file
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC


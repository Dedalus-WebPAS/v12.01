.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   IBAOUT37                                                  *
.* Desc      :   Transport Report                                          *
.***************************************************************************
.* Date      :   29/09/2009                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will print the transport report              *
.* Modifications  :                                                        *
.***************************************************************************
.*        V11.02.01 09/02/2022  Thanh T        TSK 0905641                 *
.*                  Recompiled as OUTHEDFD changes                         *
.*        V10.13.01 21/09/2018  Ebon Clements  TSK 0834684                 *
.*                  Added IP visits to transport list                      *
.*        V10.11.01 18/07/2017  Richa Phogat   TSK 0834684                 *
.*                  Replaced table VISCMTFD by VISUCMFD                    *
.*        V10.07.01 02/02/2016  Ebon Clements  TSK 0813050                 *
.*                  Changed LOOPCNTR to a FORM 6                           *
.*        V10.05.01 15/07/2014 Ebon Clements  CAR 261630                   *
.*                  Removed port number from temp file name                *
.*        V10.02.01 22/06/2011 Ebon Clements  CAR 242062                   *
.*                  Added display of clinic location                       *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                   *
.*                  Added pmsvx1af                                         *
.*        V9.12.05  10/02/2010  Ebon Clements CAR 214121                   *
.*                  Added print U/R once per day option                    *
.*        V9.12.04  15/01/2010  Ebon Clements CAR 214121                   *
.*                  Added current inpatient filter                         *
.*        V9.12.03  10/12/2009  Ebon Clements CAR 210390                   *
.*                  Added booking status filter                            *
.*        V9.12.02  12/11/2009  Ebon Clements CAR 202769                   *
.*                  Removed isadd                                          *
.*        V9.12.01  09/10/2009  Ebon Clements CAR 202769                   *
.*                  Added print of claim code description                  *
.*        V9.12.00  29/09/2009  Ebon Clements CAR 202769                   *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       IBACONFD/INC
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTHEDFD/INC
          INC       OUTSITFD/INC
          INC       OUTSESFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATIN1FD/INC
          INC       PATWVEFD/INC
          INC       PMSTSPFD/INC
          INC       PATWC1FD/INC
          INC       PATWMAFD/INC
          INC       TFILEVAR/INC
          INC       VISUCMFD/INC
          INC       WEBERRFD/INC
.
.         Temporary File Definition
.         -------------------------
.
.         Filename : out37axx.dat          (xx = port id)
.
REPTEMP1  IFILE SQL, FIXED=46, KEY="U1-8,9-11"
REPTEMP2  IFILE SQL, FIXED=46, KEY="U12-14,15-22,23-27,1-8,9-11"
REPTEMP3  IFILE SQL, FIXED=46, KEY="U28-35,15-22,23-27,1-8,9-11"
REPTEMP4  IFILE SQL, FIXED=46, KEY="U12-14,15-22,36-40,1-8,9-11"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TEMPVISN  DIM     8          8            1         Visit Number
TEMPUNIQ  DIM     3          3            9         Transport Unique Number
TEMPCONT  DIM     3          3            12        Contractor from (Cat OB)
TEMPDATE  DIM     8          8            15        Visit Date
TEMPTIME  DIM     5          5            23        Visit Time
TEMPURNO  DIM     8          8            28        U/R Number
TEMPIBTM  DIM     5          5            36        Inbound Time (First Visit)
TEMPOBTM  DIM     5          5            41        Outbound Time (Last Visit)
.
.End of Record                            46
.
.********************************************************************
.
.         File Variables
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
ISADD     INIT      "isadd "
TEMPFILE  DIM       8
UKEY1     INIT      " 46 U1-8,9-11"                   * Primamy key
UKEY2     INIT      " U12-14,15-22,23-27,1-8,9-11"    * Index 2
UKEY3     INIT      " U28-35,15-22,23-27,1-8,9-11"    * Index 3
UKEY4     INIT      " U12-14,15-22,36-40,1-8,9-11"    * Index 4
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
AIDEDESC  DIM       13
BOOKDESC  DIM       10
BOOKSTAT  DIM       1
CMDLINE   DIM       200
CATAP     INIT      "ap"
CATCT     INIT      "CT"
CATPN     INIT      "pn"
CATPO     INIT      "po"
CATSA     INIT      "SA"
CLAIMCOD  DIM       3
CLAMDESC  DIM       20
CLAMNUMB  DIM       20
CODE      DIM       2
ONCEOPTN  DIM       1
ONCEDESC  DIM       3
DISPLOCN  DIM       20
DSPEDDTE  DIM       10                      * Display end date (dd/mm/ccyy)
DSPSTDTE  DIM       10                      * Display start date (dd/mm/ccyy)
ECOL1     FORM      2
ENDDATE   DIM       8                       * End date (ccyymmdd)
ENDDCLAM  DIM       3
ENDDCONT  DIM       3
ENDDTIME  DIM       5
FRCLAIMD  DIM       20
FRCONTDS  DIM       20
HOUR      FORM      2
INBCOMMT  DIM       30
INPTSTAT  DIM       1
INPTDESC  DIM       3
LOOPCNTR  FORM      6
MIN       DIM       2
MINUTES   FORM      3
MINTIME   FORM      2
OUTCOMMT  DIM       30
INSUNAME  DIM       21
MOBLDESC  DIM       10
SITE      DIM       6
PATFNAME  DIM       20
SAVECONT  DIM       3
SAVEDATE  DIM       8
SAVEURNO  DIM       8
SPECDESC  DIM       20
STRTDATE  DIM       8                       * Start date (ccyymmdd)
STRTCLAM  DIM       3
STRTCONT  DIM       3
TIME      FORM      3
TOTALPAT  FORM      5
TOCLAIMD  DIM       20
TOCONTDS  DIM       20
.
PRGID     INIT      "IBAOUT37"
PRGNAM    INIT      "Transport Report"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      DATE0000               * Keyin the date range
          BRANCH    EXIT,ML1000
.
          CALL      KCLM0000               * Keyin the claim code
          BRANCH    EXIT,ML1000
.
          CALL      KCON0000               * Keyin the contractor code
          BRANCH    EXIT,ML1000
.
          CALL      KBOK0000               * Keyin the booking status filter
          BRANCH    EXIT,ML1000
.
          CALL      KCUR0000               * Keyin current IP filter
          BRANCH    EXIT,ML1000
.
          CALL      KONE0000               * Keyin print U/R once per day
          BRANCH    EXIT,ML1000
.
          CALL      KHOS0000               * Keyin hospital
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CREA0000               * create temp file
.
          CALL      REPT0000               * process report
          CALL      INPT0000               * process inpatients
          CALL      ONCE0000               * process print U/R once per day
          CALL      PRNT0000               * print report
.
          CALL      KILL0000               * remove temp file
          GOTO      ML0100
          
.
ML9999    CHAIN     PGM                    * chain back to program
.
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN3;*188,CMABINS,*194,CVETINS
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmstspaf";
          OPEN      PMSTSPA1,"pmstspaf"
          OPEN      PMSTSPA2,"pmstspaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patwmabf";
          OPEN      PATWMAB1,"patwmabf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patwc1af";
          OPEN      PATWC1A1,"patwc1af"
.
          DISPLAY   *P64:24,"patwvetf";
          OPEN      PATWVET1,"patwvetf"
.
          DISPLAY   *P64:24,"visucmaf";
          OPEN      VISUCMA1,"visucmaf"
.
          DISPLAY   *P64:24,"patin1af";
          OPEN      PATIN1A1,"patin1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A8,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
.
          PACK      CFNAME WITH UID,FILSESA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTSESA1,CFNAME
.
          PACK      CFNAME WITH UID,FILSESA6
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTSESA6,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA1 
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA3 
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA3,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA6 
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA6,CFNAME
.
          PACK      CFNAME WITH UID,FILBB1A1 
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBB1A1,CFNAME
.
          PACK      CFNAME WITH UID,FILHEDA1 
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTHEDA1,CFNAME
.
INIT9999  RETURN
.
.******** ********************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Transport Report"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.******************************************************************************
.*                                  DATE0000                                  *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      ZERO,EXIT
          MOVE      "18",CCOL
          DISPLAY   *P1:9,"Starting Date : ",*EF:
                    *P1:10,"Ending Date   : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "9",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9900          * Nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          CALL      PACDATE                 * Format the start date
          MOVE      CPCDATE,DSPSTDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPSTDTE;
.
. ----- Keyin end date -----
.
DATE2000  MOVE      "10",CVERT
          UNPACK    SP70,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          CALL      PACDATE                 * Format the end date
          MOVE      CPCDATE,DSPEDDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPEDDTE;
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must be after the start date. ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
          GOTO      DATE9999
.
DATE9900  MOVE      ONE,EXIT
DATE9999  RETURN
.
.
.******** ********************************************************************
.*                                KBOK0000                                  *
.*                        keyin the booking status filter                   *
.****************************************************************************
.
KBOK0000  MOVE      SP70,BOOKSTAT
          KEYIN     *P1:15,*EL,"Booking Status : ",BOOKSTAT
.         
          PACK      BOOKSTAT,BOOKSTAT,SP70
.         
          MATCH     SP70,BOOKSTAT
          IF        @EQUAL
            MOVE      "All",BOOKDESC
            DISPLAY   *P20:15,BOOKDESC
            GOTO      KBOK9000
          ENDIF
.         
          MATCH     "1",BOOKSTAT
          IF        @EQUAL
            MOVE      "Not Booked",BOOKDESC
            DISPLAY   *P20:15,BOOKDESC
            GOTO      KBOK9000
          ENDIF
.         
          MATCH     "2",BOOKSTAT
          IF        @EQUAL
            MOVE      "Booked",BOOKDESC
            DISPLAY   *P20:15,BOOKDESC
            GOTO      KBOK9000
          ENDIF
.         
          BEEP
          GOTO      KBOK0000
.
KBOK9000  MOVE      ZERO,EXIT
          GOTO      KBOK9999
.
KBOK9500  MOVE      ONE,EXIT
.
KBOK9999  RETURN
.           
.******** ********************************************************************
.*                                KCUR0000                                  *
.*                        keyin the current IP filter                       *
.****************************************************************************
KCUR0000  MOVE      SP70,INPTSTAT
          KEYIN     *P1:16,*EL,"Current IP : ",INPTSTAT
.           
          PACK      INPTSTAT,INPTSTAT,SP70
.
          MATCH     SP70,INPTSTAT
          IF        @EQUAL
            MOVE      "All",INPTDESC
            DISPLAY   *P20:16,INPTDESC
            GOTO      KCUR9000
          ENDIF
.
          MATCH     "1",INPTSTAT
          IF        @EQUAL
            MOVE      "No",INPTDESC
            DISPLAY   *P20:16,INPTDESC
            GOTO      KCUR9000
          ENDIF
.
          MATCH     "2",INPTSTAT
          IF        @EQUAL
            MOVE      "Yes",INPTDESC
            DISPLAY   *P20:16,INPTDESC
            GOTO      KCUR9000
          ENDIF
.
          BEEP
          GOTO      KCUR0000
.
KCUR9000  MOVE      ZERO,EXIT
          GOTO      KCUR9999
.
KCUR9500  MOVE      ONE,EXIT
.
KCUR9999  RETURN
.
.****************************************************************************
.*                                KONE0000                                  *
.*                        keyin print U/R once per day                      *
.****************************************************************************
KONE0000  MOVE      SP70,ONCEOPTN
          KEYIN     *P1:17,*EL,"Print U/R Once Per Day : ",ONCEOPTN
.
          PACK      ONCEOPTN,ONCEOPTN,SP70
.
          MATCH     SP70,ONCEOPTN
          IF        @EQUAL
            MOVE      ZERO,ONCEOPTN
          ENDIF
.
          MATCH     "0",ONCEOPTN
          IF        @EQUAL
            MOVE      "No",ONCEDESC
            DISPLAY   *P26:17,ONCEDESC
            GOTO      KONE9000
          ENDIF
.
          MATCH     "1",ONCEOPTN
          IF        @EQUAL
            MOVE      "Yes",ONCEDESC
            DISPLAY   *P26:17,ONCEDESC
            GOTO      KONE9000
          ENDIF
.
          BEEP
          GOTO      KONE0000
.
KONE9000  MOVE      ZERO,EXIT
          GOTO      KONE9999
.
KONE9500  MOVE      ONE,EXIT
.
KONE9999  RETURN
.
.******************************************************************************
.*                                  KHOS0000                                  *
.*                      Keyin hospital ID                                     *
.******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          MOVE       SP70,PTHSHOSP
          DISPLAY    *P1:18,*EL,"Hospital Id   : ";
          MOVE       TEN8,ECOL
          MOVE       TEN8,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ONE,CKYIMAND           * Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS9000,KHOS9000
.
KHOS3000  DISPLAY   *P22:18,*V2LON,PTHSNAME;
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
.
.**********************************************************************
.*                             KCLM0000                               *
.*                keyin range of claim codes                          *
.**********************************************************************
KCLM0000  MOVE       ZERO,EXIT
          MOVE       SP70,FRCLAIMD
          MOVE       SP70,TOCLAIMD
          MOVE       SP70,STRTCLAM
          MOVE       SP70,ENDDCLAM
          MOVE       ZERO,CKYIMAND           *Not Mandatory
.
          DISPLAY    *P1:11,*EF,"From Claim Code   :":
                     *P1:12,"To   Claim Code   :";
.
          MOVE      "11",EVERT
          MOVE      "20",ECOL               * keyin position
          MOVE      "30",ECOL1              * Display description position
          MOVE      SP3,ACODE
          MOVE      "CL",CODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM1000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,STRTCLAM
          MOVE      TDESC,FRCLAIMD
          GOTO      KCLM5000
.
KCLM1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          MOVE      "START",FRCLAIMD
          MOVE      ZERO,EXIT
.
KCLM5000  MOVE      "12",EVERT
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM6000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,ENDDCLAM
          MOVE      TDESC,TOCLAIMD
          GOTO      KCLM9999
.
KCLM6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          MOVE      "END  ",TOCLAIMD
          MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
.
.**********************************************************************
.*                             KCON0000                               *
.*                keyin range of contractor codes                     *
.**********************************************************************
KCON0000  MOVE       ZERO,EXIT
          MOVE       SP70,FRCONTDS
          MOVE       SP70,TOCONTDS
          MOVE       SP70,STRTCONT
          MOVE       SP70,ENDDCONT
          MOVE       ZERO,CKYIMAND           *Not Mandatory
.
          DISPLAY    *P1:13,*EF,"From Contractor   :":
                     *P1:14,"To   Contractor   :";
.
          MOVE      "13",EVERT
          MOVE      "20",ECOL               * keyin position
          MOVE      "30",ECOL1              * Display description position
          MOVE      SP3,ACODE
          MOVE      "OB",CODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCON1000,KCON9000  * Nothing,exit entered
          MOVE      ACODE,STRTCONT
          MOVE      TDESC,FRCONTDS
          GOTO      KCON5000
.
KCON1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          MOVE      "START",FRCONTDS
          MOVE      ZERO,EXIT
.
KCON5000  MOVE      "14",EVERT
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCON6000,KCON9000  * Nothing,exit entered
          MOVE      ACODE,ENDDCONT
          MOVE      TDESC,TOCONTDS
          GOTO      KCON9999
.
KCON6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          MOVE      "END  ",TOCONTDS
          MOVE      ZERO,EXIT
          GOTO      KCON9999
.
KCON9000  MOVE      ONE,EXIT
KCON9999  RETURN
.
.******************************************************************************
.*                                 KCOD0000                                   *
.*                         Keyin codes                                        *
.******************************************************************************
KCOD0000  CALL      KEYINCOD
          BRANCH    EXIT,KCOD1000:          * Nothing entered
                         KCOD2000           * Exitchar entered
.
KCOD1000  DISPLAY   *PECOL1:EVERT,*V2LON,TDESC;
KCOD2000  RETURN
.
.**************************************************************************
.*                               REPT0000                                 *
.* Select appointments                                                    *
.**************************************************************************
REPT0000  PACK      KEY25,SITE,STRTDATE,SP70
          CALL      RDSSESA6
REPT0500  CALL      RDKSESA6
          BRANCH    OVRCD,REPT9999
.
          MATCH     SITE,OSESITE
          GOTO      REPT9999 IF NOT EQUAL
.
          MATCH     OSEDATE,ENDDATE
          GOTO      REPT9999 IF LESS
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,REPT0500
.
          IF        IBCNMHOS=1
            MATCH     SP70,PTHSHOSP
            IF        !@EQUAL
              MATCH     PTHSHOSP,OTHEHOSP
              GOTO      REPT0500 IF NOT EQUAL
            ENDIF
          ENDIF
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
          CALL      RDSBOKA1
REPT1000  CALL      RDKBOKA1
          BRANCH    OVRCD,REPT0500
.
          MATCH     OSESITE,OBASITE
          GOTO      REPT0500 IF NOT EQUAL
.
          MATCH     OSECLIN,OBACLIN
          GOTO      REPT0500 IF NOT EQUAL
.
          MATCH     OSEDATE,OBADATE
          GOTO      REPT0500 IF NOT EQUAL
.
          MATCH     OSESTRT,OBASTRT
          GOTO      REPT0500 IF NOT EQUAL
.
.                    Status  Booked   N/Used   Ex.Slot  Attended
          BRANCH    OBASTAT,REPT2000,REPT1000,REPT1000,REPT2000
          GOTO      REPT1000
.
REPT2000  MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1                 * Clinic Session Booking Table B
          BRANCH    OVRCD,REPT1000
.
          MATCH     SP70,STRTCLAM           * From claim code
          IF        !@EQUAL
            MATCH     STRTCLAM,OBACOMP
            GOTO      REPT1000 IF LESS
          ENDIF
.
          MATCH     SP70,ENDDCLAM           * To claim code
          IF        !@EQUAL
            MATCH     OBACOMP,ENDDCLAM
            GOTO      REPT1000 IF LESS
          ENDIF
.
          PACK      KEY17,OBAURNO,TWO,SP20
          CALL      RSPTMI18
          CALL      RKPTMI18
          BRANCH    OVRCD,REPT2500          * not on file
.
          MATCH     OBAURNO,AURNO           * same U/R Number?
          GOTO      REPT2500 IF NOT EQUAL   * No
.
          GOTO      REPT2600
.
REPT2500  CALL      CLPATMIS
.
REPT2600  MATCH     "1",INPTSTAT            * Exclude current IP
          IF        @EQUAL
            COMPARE   TWO,ASTAT
            GOTO      REPT1000 IF EQUAL
          ENDIF
.
          MATCH     "2",INPTSTAT            * Current IP Only
          IF        @EQUAL
            COMPARE   TWO,ASTAT
            GOTO      REPT1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY19,OBAURNO,OBAOUTNO,SP70
          CALL      RSPMTSP1
REPT4000  CALL      RKPMTSP1                * Transport
          BRANCH    OVRCD,REPT1000
.
          MATCH     PMTSURNO,OBAURNO
          GOTO      REPT1000 IF NOT EQUAL
.
          MATCH     PMTSVISN,DOBAOUTN
          GOTO      REPT1000 IF NOT EQUAL
.
          MATCH     SP70,BOOKSTAT
          GOTO      REPT5000 IF EQUAL
.
          MATCH     "2",BOOKSTAT
          IF        @EQUAL
            MATCH     "1",PMTSBOOK               * Only select booked
            GOTO      REPT4000 IF NOT EQUAL
          ELSE
            MATCH     "1",PMTSBOOK               * Skip if booked
            GOTO      REPT4000 IF EQUAL
          ENDIF
.
REPT5000  MATCH     SP70,STRTCONT           * From contractor code
          IF        !@EQUAL
            MATCH     STRTCONT,PMTSTRNS
            GOTO      REPT4000 IF LESS
          ENDIF
.
          MATCH     SP70,ENDDCONT           * To contractor code
          IF        !@EQUAL
            MATCH     PMTSTRNS,ENDDCONT
            GOTO      REPT4000 IF LESS
          ENDIF
.
          PACK      TEMPVISN,OBAOUTNO,SP70
          PACK      TEMPUNIQ,PMTSUNIQ,SP70
          PACK      TEMPCONT,PMTSTRNS,SP70
          PACK      TEMPDATE,OBADATE,SP70
          PACK      TEMPTIME,OBATIME,SP70
          PACK      TEMPURNO,OBAURNO,SP70
          MOVE      SP70,TEMPIBTM
          MOVE      SP70,TEMPOBTM
.
          PACK      KEY11,TEMPVISN,TEMPUNIQ,SP70
          CALL      RATEMP1                 * write to Temp file
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
.
          GOTO      REPT4000
.
REPT9999  RETURN
.
.**************************************************************************
.*                               INPT0000                                 *
.* Select IP visists                                                      *
.**************************************************************************
INPT0000  PACK      KEY35,STRTDATE,SP70
          CALL      RSPMTSP2
INPT1000  CALL      RKPMTSP2                * Transport
          BRANCH    OVRCD,INPT9999
.
          MATCH     PMTSTDAT,ENDDATE
          GOTO      INPT9999 IF LESS
.
          PACK      KEY8,PMTSVISN,SP70           * IP Visits Only
          CALL      RDMISS1
          BRANCH    OVRCD,INPT1000
.
          IF        IBCNMHOS=1
            MATCH     SP70,PTHSHOSP
            IF        !@EQUAL
              MATCH     PTHSHOSP,PMVXMHOS
              GOTO      INPT1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     SP70,STRTCLAM           * From claim code
          IF        !@EQUAL
            MATCH     STRTCLAM,ACLAIM
            GOTO      INPT1000 IF LESS
          ENDIF
.
          MATCH     SP70,ENDDCLAM           * To claim code
          IF        !@EQUAL
            MATCH     ACLAIM,ENDDCLAM
            GOTO      INPT1000 IF LESS
          ENDIF
.
          MATCH     "1",INPTSTAT            * Exclude current IP
          IF        @EQUAL
            COMPARE   TWO,ASTAT
            GOTO      INPT1000 IF EQUAL
          ENDIF
.
          MATCH     "2",INPTSTAT            * Current IP Only
          IF        @EQUAL
            COMPARE   TWO,ASTAT
            GOTO      INPT1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,BOOKSTAT
          GOTO      INPT5000 IF EQUAL
.
          MATCH     "2",BOOKSTAT
          IF        @EQUAL
            MATCH     "1",PMTSBOOK               * Only select booked
            GOTO      INPT1000 IF NOT EQUAL
          ELSE
            MATCH     "1",PMTSBOOK               * Skip if booked
            GOTO      INPT1000 IF EQUAL
          ENDIF
.
INPT5000  MATCH     SP70,STRTCONT           * From contractor code
          IF        !@EQUAL
            MATCH     STRTCONT,PMTSTRNS
            GOTO      INPT1000 IF LESS
          ENDIF
.
          MATCH     SP70,ENDDCONT           * To contractor code
          IF        !@EQUAL
            MATCH     PMTSTRNS,ENDDCONT
            GOTO      INPT1000 IF LESS
          ENDIF
.
          PACK      TEMPVISN,AADMNO,SP70
          PACK      TEMPUNIQ,PMTSUNIQ,SP70
          PACK      TEMPCONT,PMTSTRNS,SP70
          PACK      TEMPDATE,ADATE,SP70
          PACK      TEMPTIME,ATIME,SP70
          PACK      TEMPURNO,AURNO,SP70
          MOVE      SP70,TEMPIBTM
          MOVE      SP70,TEMPOBTM
.
          PACK      KEY11,TEMPVISN,TEMPUNIQ,SP70
          CALL      RATEMP1                 * write to Temp file
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
.
          GOTO      INPT1000
.
INPT9999  RETURN
.
.**************************************************************************
.*                               ONCE0000                                 *
.* Loop through the temp file and only keep the first booking for a       *
.* U/R for each day. Calculate the IB and OB appointment times            *
.* from the first and last visit for the day                              *
.**************************************************************************
ONCE0000  MATCH     "1",ONCEOPTN
          GOTO      OPTN9999 IF NOT EQUAL
.
          MOVE      SP70,SAVEURNO
          MOVE      SP70,SAVEDATE
.
          PACK      KEY32,SP70
          CALL      RSTEMP3
ONCE1000  CALL      RKTEMP3
          BRANCH    OVRCD,ONCE2000  
.
          MATCH     SAVEURNO,TEMPURNO
          IF        !@EQUAL
            MOVE      TEMPURNO,SAVEURNO
            MOVE      TEMPDATE,SAVEDATE
            GOTO      ONCE1000
          ENDIF
.
          MATCH     SAVEDATE,TEMPDATE
          IF        !@EQUAL
            MOVE      TEMPURNO,SAVEURNO
            MOVE      TEMPDATE,SAVEDATE
            GOTO      ONCE1000
          ENDIF
.
          PACK      KEY32,TEMPURNO,TEMPDATE,TEMPTIME,TEMPVISN,TEMPUNIQ
          CALL      DETEMP3
          GOTO      ONCE1000
.
ONCE2000  PACK      KEY32,SP70
          CALL      RSTEMP3
ONCE3000  CALL      RKTEMP3
          BRANCH    OVRCD,ONCE9999
.
          MOVE      SP70,TEMPIBTM
          MOVE      SP70,TEMPOBTM
.
          PACK      KEY8,TEMPVISN,SP70
          CALL      RDMISS1
          COMPARE   ONE,OVRCD      
          GOTO      ONCE6000 IF NOT EQUAL
.
          PACK      KEY36,TEMPURNO,TEMPDATE,SP70 
          CALL      RDSBOKA3
ONCE4000  CALL      RDKBOKA3
          BRANCH    OVRCD,ONCE5000
.
          MATCH     TEMPURNO,OBAURNO
          GOTO      ONCE5000 IF NOT EQUAL
.
          MATCH     TEMPDATE,OBADATE
          GOTO      ONCE5000 IF NOT EQUAL
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,ONCE4000
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,ONCE4000
.
          MATCH     SP70,TEMPIBTM
          IF        @EQUAL
            MOVE      OBATIME,TEMPIBTM
          ENDIF
.
          MATCH     TEMPIBTM,OBATIME
          IF        @LESS
            MOVE      OBATIME,TEMPIBTM
          ENDIF
.
          CALL      STDUR000                * Calculate OB Time
.         
          MATCH     SP70,TEMPOBTM
          IF        @EQUAL
            MOVE      ENDDTIME,TEMPOBTM
          ENDIF
.
          MATCH     ENDDTIME,TEMPOBTM
          IF        @LESS
            MOVE      ENDDTIME,TEMPOBTM
          ENDIF
.
          GOTO      ONCE4000
.
ONCE5000  CALL      UPTEMP3
          GOTO      ONCE3000
.
ONCE6000  MOVE      ATIME,TEMPIBTM
          CALL      UPTEMP3
          GOTO      ONCE3000
.
ONCE9999  RETURN
.
.**************************************************************************
.                         STDUR000  
.              Calculate booking end time        
.**************************************************************************
STDUR000  PACK      KEY5,ANSC,ANSV,OBAVISIT,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      ONE,TCFORM6
          ENDIF
          MOVE      ZERO,LOOPCNTR
.           
          UNPACK    OBATIME,KEY2,KEY1,MIN   * HH:MM
STDUR100  MOVE      KEY2,HOUR               * HH to Form
          MOVE      MIN,MINUTES             * MM to Form
.
          ADD       OTHEDURN,MINUTES   * Add Duration time to minutes
          ASSIGN    MINUTES-SIXTY,TIME
          IF        TIME >= 0
            IF        TIME >= 60
              MOVE      TIME,MINUTES
              ASSIGN    MINUTES-SIXTY,TIME
              MOVE      TIME,MINTIME
              MOVE      MINTIME,MIN
              ADD       TWO,HOUR                 * Add Two Hours
            ELSE
              MOVE      TIME,MINTIME
              MOVE      MINTIME,MIN
              ADD       ONE,HOUR                 * Add One Hour
            ENDIF
          ELSE
            MOVE      MINUTES,MINTIME            * No Hour Change
            MOVE      MINTIME,MIN
          ENDIF
.
          PACK      ENDDTIME,HOUR,COLON,MIN  * pack time save variable (HH:MM)
          REPLACE   " 0",ENDDTIME            * replace spaces with zero
.
          ADD       ONE,LOOPCNTR
.
          COMPARE   TCFORM6,LOOPCNTR
          IF        @LESS
            UNPACK    ENDDTIME,KEY2,KEY1,MIN   * HH:MM
            GOTO      STDUR1000
          ENDIF
.
STDUR999  RETURN
.
.**************************************************************************
.*                               PRNT0000                                 *
.* Print report in date, time, ward and bed order                         *
.**************************************************************************
.
PRNT0000  MOVE      "99",CLNO
          MOVE      ZERO,CPAGENO
          MOVE      Z70,SAVECONT
.
          MATCH     "1",ONCEOPTN
          IF        @EQUAL
            PACK      KEY27,SP70
            CALL      RSTEMP4 
          ELSE
            PACK      KEY27,SP70
            CALL      RSTEMP2 
          ENDIF
PRNT1000  MATCH     "1",ONCEOPTN
          IF        @EQUAL
            CALL      RKTEMP4  
          ELSE
            CALL      RKTEMP2  
          ENDIF
          BRANCH    OVRCD,PRNT9000
.
          MATCH     TEMPCONT,SAVECONT
          CALL      SUBH0000 IF NOT EQUAL
.
          COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      KEY19,TEMPURNO,TEMPVISN,TEMPUNIQ,SP70
          CALL      RDPMTSP1 
          BRANCH    OVRCD,PRNT1000
.
          PACK      KEY8,TEMPVISN,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,PRNT1500
.
          CALL      CLOUTBOA
          CALL      CLOUTBB1
          CALL      CLOUTHED
          MOVE      ACLAIM,CLAIMCOD
.
          MOVE      SP70,DISPLOCN
          MATCH     SP70,PMVXAPWD
          IF        !@EQUAL
            PACK      KEY5,CATAP,PMVXAPWD,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,DISPLOCN
            ENDIF
          ENDIF
.
          GOTO      PRNT2000
.
PRNT1500  PACK      KEY36,TEMPVISN,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,PRNT1000
.
          MATCH     TEMPVISN,DOBAOUTN
          GOTO      PRNT1000 IF NOT EQUAL
.
          PACK      KEY8,TEMPVISN,SP70
          CALL      RDBOKB1 
          BRANCH    OVRCD,PRNT1000
.
          MOVE      OBACOMP,CLAIMCOD
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,PRNT1000
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,PRNT1000
.
          MOVE      SP70,DISPLOCN
          MATCH     SP70,OTHELTYP
          IF        !@EQUAL
            PACK      KEY5,CATCT,OTHELTYP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTHELTYP,DISPLOCN
            ELSE
              MOVE      TDESC,DISPLOCN
            ENDIF
          ENDIF

PRNT2000  PACK      KEY8,TEMPURNO
          CALL      RDMAST1
          BRANCH    OVRCD,PRNT1000
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      SP4,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATFNAME              * shorten name
.
          UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      SP70,MOBLDESC
          MATCH     SP70,PMTSMOBL
          IF        !@EQUAL
            PACK      KEY5,CATPN,PMTSMOBL,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,MOBLDESC
            ENDIF
          ENDIF
.
          MOVE      SP70,AIDEDESC
          MATCH     SP70,PMTSAIDS
          IF        !@EQUAL
            PACK      KEY5,CATPO,PMTSAIDS,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,AIDEDESC
            ENDIF
          ENDIF
.
          MOVE      SP70,SPECDESC
          MATCH     SP70,PMTSSPEC
          IF        !@EQUAL
            PACK      KEY5,CATSA,PMTSSPEC,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,SPECDESC
            ENDIF
          ENDIF
.
          MOVE      SP70,INBCOMMT
          PACK      KEY17,TEMPVISN,ZERO,ZERO,SEVEN,TEMPUNIQ,SP2,ONE,SP70
          CALL      RDVSUCM1
          IF        OVRCD<>1
            MOVE      VSUCLINE,INBCOMMT
          ENDIF
.
          MOVE      SP70,OUTCOMMT
          PACK      KEY17,TEMPVISN,ZERO,ONE,ONE,TEMPUNIQ,SP2,ONE,SP70
          CALL      RDVSUCM1
          IF        OVRCD<>1
            MOVE      VSUCLINE,OUTCOMMT
          ENDIF
.
          CALL      GCLM0000
.
          CALL      UND132
.
.         Patient Line 1
.
          MATCH     "1",ONCEOPTN
          IF        @EQUAL
            MOVE      TEMPIBTM,KEY5
          ELSE
            MOVE      TEMPTIME,KEY5
          ENDIF
.
          PRINT     *1,"|IB ",CPCDATE," @ ",KEY5,*23,"| ",PATFNAME:
                    *46,"| ",PMTSPAD1,*83,"| ",MOBLDESC,*95,"| ",AIDEDESC:
                    *110,"| ",SPECDESC,*132,"|"
.
.         Patient Line 2
.
          MATCH     "1",ONCEOPTN
          IF        @EQUAL
            MOVE      TEMPOBTM,KEY5
          ELSE
            MOVE      PMTSRTTM,KEY5
          ENDIF
.
          PRINT     *1,"|OB            @ ",KEY5,*23,"| ",TEMPURNO:
                    *46,"| ",PMTSPAD2,*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
.         Patient Line 3
.
          STRIP     PMTSPAD3
          PACK      KEY35,PMTSPAD3,COMMA,SP1,PMTSPPOS
          MOVE      SP70,KEY10
          MATCH     "1",PMTSBOOK
          IF        @EQUAL
            MOVE      "Booked",KEY10
          ENDIF
.
          PRINT     *1,"|",KEY10,*23,"|":
                    *46,"| ",KEY35,*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
.         Patient Line 4
.
          PRINT     *1,"|",DISPLOCN,*23,"|":
                    *46,"| ",INBCOMMT,*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
.         Patient Line 5 
.
          PRINT     *1,"|",CLAMDESC,*23,"|":
                    *46,"| ",*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
.         Patient Line 6
.
          PRINT     *1,"|",CLAMNUMB,*23,"|":
                    *46,"| ",PMTSRAD1,*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
.         Patient Line 7
.
          PRINT     *1,"|",INSUNAME,*23,"|":
                    *46,"| ",PMTSRAD2,*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
.         Patient Line 8
.
          STRIP     PMTSRAD3
          PACK      KEY35,PMTSRAD3,COMMA,SP1,PMTSRPOS
          PRINT     *1,"|",*23,"| ":
                    *46,"| ",KEY35,*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
.         Patient Line 9
.
          PRINT     *1,"|",*23,"|":
                    *46,"| ",OUTCOMMT,*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
          ADD       ONE,TOTALPAT
          ADD       NINE,CLNO
.
          GOTO      PRNT1000     
.
PRNT9000  CALL      UND132
          PRINT     *1,"| No. of Patients : ",TOTALPAT,*132,"|"
          CALL      UND132
.
PRNT9999  RETURN
.
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *30,"From : ",DSPSTDTE," to ",DSPEDDTE
          PRINT     *30,"From Claim Type : ",FRCLAIMD," to ",TOCLAIMD
          PRINT     *30,"From Contractor : ",FRCONTDS," to ",TOCONTDS
          PRINT     *30,"Booking Status : ",BOOKDESC
          PRINT     *30,"Current IP : ",INPTDESC
          PRINT     *30,"Print U/R Once Per Day : ",ONCEDESC
.
          IF        IBCNMHOS=1
            PRINT     *30,"Hospital : ",PTHSNAME
          ENDIF
.
          IF        IBCNMHOS=1
            MOVE     TEN1,CLNO
          ELSE
            MOVE     TEN,CLNO
          ENDIF
.
HEAD9999  RETURN
.
.*********************************************************************
.         print contractor sub heading
.*********************************************************************
SUBH0000  MATCH     Z70,SAVECONT
          IF        !@EQUAL
            CALL      UND132
            PRINT     *1,"| No. of Patients : ",TOTALPAT,*132,"|"
          ENDIF
.
          MOVE      ZERO,TOTALPAT
.
          CALL      HEAD0000                * Start on new page
          CALL      UND132
.
          MOVE      TEMPCONT,SAVECONT       * Save contractor
.
          MOVE      SP70,TDESC
          MATCH     SP70,SAVECONT
          IF        !@EQUAL
            PACK      KEY5,ANSO,ANSB,TEMPCONT
            CALL      RDCODE1
          ENDIF

          PRINT     *1,"| Transport Contractor : ",TDESC,*132,"|"
          ADD       ONE,CLNO
.
          CALL      UND132
.
	  PRINT     *1,"|Appointment Date/Time",*23,"| Patient Name":
                    *46,"| Pick Up Address",*83,"| Mobility":
                    *95,"| Aides",*110,"| Special Arrangements",*132,"|"
.
	  PRINT     *1,"|Return Time",*23,"|":
                    *46,"| Comments",*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
	  PRINT     *1,"|",*23,"|":
                    *46,"| Return Address",*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
	  PRINT     *1,"|Claim Details",*23,"|":
                    *46,"| Comments",*83,"|":
                    *95,"|",*110,"|",*132,"|"
.
          ADD       FOUR,CLNO
.
SUBH9999  RETURN
.
.------------------------------------------------------------
. Get claim details from the original visit
.------------------------------------------------------------
GCLM0000  MOVE      SP70,CLAMNUMB
          MOVE      SP70,INSUNAME
          MOVE      SP70,CLAMDESC
.
          MATCH     SP70,CLAIMCOD           * Visits claim code
          GOTO      GCLM9999 IF EQUAL
.
.         Set the claim type indicator if this is a claim
.
          PACK      KEY5 WITH ANSC,ANSL,CLAIMCOD
          CALL      RDCODE1
          BRANCH    OVRCD OF GCLM9999
.
          MOVE      TDESC,CLAMDESC
.
.         Check the indicators for a W, M, or V (these are the claims)
.
          MOVE      ZERO,FORM2
GCLM3000  ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      GCLM9999 IF NOT LESS
.
          LOAD      ANS USING FORM2 OF TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
          CMATCH    "W",ANS
          GOTO      GCLM4000 IF EQUAL
          CMATCH    "M",ANS
          GOTO      GCLM5000 IF EQUAL
          CMATCH    "V",ANS
          GOTO      GCLM6000 IF EQUAL
          GOTO      GCLM3000
.
GCLM4000  OPEN      PATWC1A1,"patwc1af"
          MOVE      TEMPVISN,KEY8
          CALL      RDWCOM1            * read previous Workers Comp details
          BRANCH    OVRCD,GCLM9999
.
          MOVE      WCCLMNO,CLAMNUMB
.
          MOVE      WCICODE,KEY6
          CALL      RDINSR1
          IF        OVRCD<>1
            MOVE      INAME,INSUNAME
          ENDIF
.
          GOTO      GCLM9999
.
GCLM5000  MOVE      TEMPVISN,KEY8
          CALL      RDWMAB1            * read previous TAC details
          BRANCH    OVRCD,GCLM9999
.
          MOVE      MREFNO,CLAMNUMB
.
          MOVE      CMABINS,KEY6
          CALL      RDINSR1
          IF        OVRCD<>1
            MOVE      INAME,INSUNAME
          ENDIF
          GOTO      GCLM9999
.
GCLM6000  MOVE      TEMPVISN,KEY8
          CALL      RDWVET1
          BRANCH    OVRCD,GCLM9999
.
          MOVE      VCLMNO,CLAMNUMB
.
          MOVE      CVETINS,KEY6
          CALL      RDINSR1
          IF        OVRCD<>1
            MOVE      INAME,INSUNAME
          ENDIF
          GOTO      GCLM9999
.
GCLM9999  RETURN
.
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY1,UKEY2,UKEY3,UKEY4
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      REPTEMP1,TEMPFILE            * open temp index file
          OPEN      REPTEMP2,TEMPFILE            * open temp index file
          OPEN      REPTEMP3,TEMPFILE            * open temp index file
          OPEN      REPTEMP4,TEMPFILE            * open temp index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     REPTEMP1                     * close file
          CLOSE     REPTEMP2                     * close index 1
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
.
RSTEMP1   READ      REPTEMP1,KEY11;;
          RETURN
.
RATEMP1   MOVE      ZERO,OVRCD
          READ      REPTEMP1,KEY11;ANSS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    REPTEMP1;TEMPVISN,TEMPUNIQ,TEMPCONT,TEMPDATE,TEMPTIME:
                             TEMPURNO,TEMPIBTM,TEMPOBTM
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     REPTEMP1,KEY11;TEMPVISN,TEMPUNIQ,TEMPCONT,TEMPDATE,TEMPTIME:
                                   TEMPURNO,TEMPIBTM,TEMPOBTM
          RETURN
.
RSTEMP2   READ      REPTEMP2,KEY27;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    REPTEMP2;TEMPVISN,TEMPUNIQ,TEMPCONT,TEMPDATE,TEMPTIME:
                             TEMPURNO,TEMPIBTM,TEMPOBTM
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP3   READ      REPTEMP3,KEY32;;
          RETURN
.
RKTEMP3   MOVE      ZERO,OVRCD
          READKS    REPTEMP3;TEMPVISN,TEMPUNIQ,TEMPCONT,TEMPDATE,TEMPTIME:
                             TEMPURNO,TEMPIBTM,TEMPOBTM
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP3   UPDATE    REPTEMP3;TEMPVISN,TEMPUNIQ,TEMPCONT,TEMPDATE,TEMPTIME:
                             TEMPURNO,TEMPIBTM,TEMPOBTM
          RETURN 
.
DETEMP3   RESET     KEY32
          DELETE    REPTEMP3,KEY32
          RETURN
.
RSTEMP4   READ      REPTEMP4,KEY27;;
          RETURN    
.         
RKTEMP4   MOVE      ZERO,OVRCD
          READKS    REPTEMP4;TEMPVISN,TEMPUNIQ,TEMPCONT,TEMPDATE,TEMPTIME:
                             TEMPURNO,TEMPIBTM,TEMPOBTM
          GOTO      OVERCOND IF OVER
          RETURN
.
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       CLOUTBOA
          INC       CLOUTBB1
          INC       CLOUTHED
          INC       KEYINCOD
          INC       STD001IO
          INC       PATDSCOD
          INC       PATHSPKY
          INC       CLPATMIS
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTHEDIO/INC
          INC       OUTSITIO/INC
          INC       OUTSESIO/INC
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATIN1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATWC1IO/INC
          INC       PATWMAIO/INC
          INC       PATWVEIO/INC
          INC       PMSTSPIO/INC
          INC       VISUCMIO/INC
          INC       WEBERRIO/INC

.***************************************************************************
.* System    :   HIC                                                       *
.* Program   :   FXHICTOT                                                  *
.* Desc      :   Fix HIC Batch Total                                       *
.***************************************************************************
.* Date      :   08/12/2006                                                *
.* Author    :   Steve Armstrong                                           *
.* Function  :   This program will loop through all HIC batches and check  *
.*               that the claim totals equal the batch total.  If not,     *
.*               the batch total will be updated.                          *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       HICBTCFD/INC
          INC       HICCLMFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
BATCHTOT  FORM      12.2
.
PRGID     INIT      "FXHICTOT"
PRGNAM    INIT      "Fix HIC Batch Totals"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"hicbtcaf";
          OPEN      HICBTCA1,"hicbtcaf"
.
          DISPLAY   *P64:24,"hicclmaf";
          OPEN      HICCLMA7,"hicclmaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*         Validate the batch total against the claim totals              *
.**************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER                 * initialise record count
          DISPLAY   *P1:22,*EF,"Records updated: ",*V2LON,COUNTER;
          DISPLAY   *P1:24,*EL,"Processing:";
.
.         Loop through the batch file
.
          PACK      KEY33,SP30,SP30
          CALL      RSHCBTC1                     * position at start of file
PROC0500  CALL      RKHCBTC1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
.         A batch record has been found, so get all the claim records for
.         the batch and total each claim.
.
          DISPLAY   *P13:24,*EL,*V2LON,HCBTBTCH;
          MOVE      ZERO,BATCHTOT                * initialise batch total
          PACK      KEY15,HCBTBTCH,SP70
          CALL      RSHCCLM7                     * position on batch
PROC1000  CALL      RKHCCLM7                     * read next record
          BRANCH    OVRCD,PROC2000               * eof - get next batch
.
          MATCH     HCCLBTCH,HCBTBTCH            * same batch number still ?
          GOTO      PROC2000 IF NOT EQUAL        * yes
.
          ADD       HCCLCAMN,BATCHTOT            * increment batch total
          GOTO      PROC1000                     * get next claim for batch
.
.         All the claims for the batch have been totalled, now see if
.         the claims balance against the batch total - if not update the
.         batch total
.
PROC2000  COMPARE   BATCHTOT,HCBTCAMT            * claim same as batch total ?
          IF        !@EQUAL
            ADD       ONE,COUNTER                * increment counter
            MOVE      BATCHTOT,HCBTCAMT          * update batch total
            CALL      UPHCBTC1
            DISPLAY   *P18:22,*EL,*V2LON,COUNTER;
          ENDIF
          GOTO      PROC0500
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit completed.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       HICBTCIO/INC
          INC       HICCLMIO/INC

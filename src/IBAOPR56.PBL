.******************************************************************************
.* System   : THEATRE                                                         *
.* Program  : IBAOPR56                                                        *
.* Desc.    : Operating Surgeon Usage Report                                  *
.******************************************************************************
.* Date     : 08/02/1990                                                      *
.* Author   : Bill Dew                                                        *
.* Mods.    :                                                                 *
.*        V10.04.01 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*            V5.01.01  21/01/91 ROD                                          *
.*                      Converted to Release 5.01                             *
.*                      15/02/91 ROD                                          *
.*                      Added default to end date                             *
.*            V5.01.02  27/05/91 i chung           SRF 108687                 *
.*                      Fixed incorrect Grand Total figures                   *
.*            V5.01.03  03/07/1991  Justin Coates  SRF 109236                 *
.*                      Allow for blank surgeon codes on oprsouaf             *
.*            V5.01.04  20/07/92  ROD                                         *
.*                      Display/Print date for period                         *
.*                                                                            *
.******************************************************************************
.
.$$$$$$
.
.   Operating Surgeon Usage Report - IBAOPR56
.   -----------------------------------------
.
.                        <<< Program Description >>>
.   This program enables operator to show statistics of USED sessions
.   throughout the theatre system over user defined monthly period and 
.   doctor type.
.
.   - END
.
.$$$$$$
+
.
          INC       STD001FD
          INC       IBASEQFD/INC       * Sequential Numbers File
          INC       TFILEVAR/INC       * Generate Temp File Name
          INC       WEBERRFD/INC       * Web Server Error Logging
.
          INC       OPRCONFD/INC       * system parameters
          INC       OPRSOUFD/INC       * surgeon operation usage
          INC       PATCOMM/INC        * variables for PATCOMN3
          INC       PATCODFD/INC       * codes file
          INC       PATDO1FD/INC       * doctors details
          INC       PATDRGFD/INC       * period date range
+
.*****************************************************************************
.                           VARIABLE DECLARATIONS
.*****************************************************************************
OPRTMPF1  IFILE     SQL, FIXED=21, KEY="u1-3,4-9,10-12"
.    1-3 is doctor type  4-9 doctor code  10-12 operation type
.
CODE      DIM       2                  * codes file 
COUNTER   FORM      3
.
DATE1     DIM       7
DATE2     DIM       7
DATEDES1  DIM       20
DATEDES2  DIM       20
DELCOMM   DIM       20                 * delete command execute dos kill
DOCTDES1  DIM       25                 * doctor description start 1
DOCTDES2  DIM       25                 * doctor description start 2
DOCT1     DIM       3                  * starting code for report
DOCT2     DIM       3                  * ending code for report
DOCTCODE  DIM       6                  * doctor code
DOCTNAME  DIM       25                 * doctors name
DOCTTYPE  DIM       3                  * doctor type
DOSCMND1  DIM       80                 * execute DOS command
.
ENDCR     DIM       6                  * ending code range
FDATE1    DIM       6                  * month and year century
FDATE2    DIM       6                  * month and year century
INDEX     FORM      2                  * a multipurpose indextor
.
.
LASTDOC   DIM       6                  * last doctor code printed
LINE80    DIM       80                 * multi purpose 80 char line buffer
.
NAMESAVE  DIM       30                 * save doctors name
NORMDATE  DIM       10                 * session date display format for users
NUMOPS    FORM      6                  * number of operations
OPERTYPE  DIM       3                  * operation type
.
SAVFDATE  DIM       10
SAVTDATE  DIM       10
SCRNFLAG  FORM      1                  * signals redisplays vars
STARTCR   DIM       6                  * starting code range
.
TEMPNAM1  DIM       8                  * temporary file name
TIMEUSED  FORM      6                  * time used in operations
TODAY     DIM       8                  * todays file date
.
.-------------------------------< CONSTANTS >---------------------------------
.
OPTNDES1  INIT      "Print Surgeon Operation Usage Report"
NUM40     FORM      "40"
NUM55     FORM      "55"
TYPEHEAD  INIT      "Dr Type : "
Z6        INIT      "ZZZZZZ"
Z9        INIT      "ZZZZZZZZZ"
Z12       INIT      "ZZZZZZZZZZZZ"
.
PRGID     INIT      "IBAOPR56"
PRGNAM    INIT      "Operating Surgeon Usage Report"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.                         MAIN LINE Rock and Roll
.*****************************************************************************
ML0000    CALL      INIT0000           * initialisation routine
.MLQQ      SPLOPEN   "data"
.
ML1000    CALL      MENU0000           * get option
          BRANCH    OPTION,ML1111
.
ML9999    CALL      DELTEMP1           * clean up any tempfiles remaining
.MLQQQ     SPCLOSE   "data"
          CHAIN     PGM                * chain back to calling menu
.
.---- print report range doctor type ----
.
ML1111    CALL      CLRV0000           * clear work variables
          CALL      REDP1111           * display srceen prompts
          CALL      MNTH0000           * get month range for report 
          BRANCH    EXIT,ML1000        * enter on first date EXIT=1
          CALL      DTYP0000           * get doctor type range
          CALL      CONTQST            * yes no cancel to continue
          BRANCH    CEXIT,ML5555,ML1111,ML1000
.
ML5555    CALL      PSUR0000           * Print Surgeon Usage report
          GOTO      ML1000
+
.*****************************************************************************
.                                 INIT0000
.*****************************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,*EL,"opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P58:24,"oprdrgaf";
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P58:24,"oprsouaf";
          OPEN      OPRSOUA1,"oprsouaf"
.
          DISPLAY   *P58:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P58:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REPLACE   " 0",TODAY         * set todays date
          MOVE      ONE,CNEWDS         * set the new ? routines
.
.      -- Setup DOSCMND1 : 
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPNAM1
.
          PACK      DOSCMND1,SP30,SP20
          CLEAR     DOSCMND1
          APPEND    "isbuild ",DOSCMND1
          APPEND    TEMPNAM1,DOSCMND1
          APPEND    " 21 u1-3,4-9,10-12",DOSCMND1
          RESET     DOSCMND1
.
INIT9999  RETURN
+
.*****************************************************************************
.                                 CLRV0000
.    Clear all main variables with spaces or zeros
.*****************************************************************************
CLRV0000  PACK      LINE80,SP30,SP30,SP20
          UNPACK    LINE80,DOCTCODE,DOCTTYPE,DOCT1,DOCT2,DOCTDES1,DOCTDES2
          UNPACK    LINE80,DATE1,DATE2,DOCTNAME,DATEDES1,DATEDES2
          UNPACK    LINE80,STARTCR,ENDCR
CLRV9999  RETURN
+
.*****************************************************************************
.                                 MENU0000
.    Menu driver routine
.*****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",OPTNDES1:
                    *P1:7,"Select Option :";
.
MENU1111  KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      MENU9999 IF EQUAL
          BRANCH    OPTION,MENU9000
          BEEP
          GOTO      MENU1111
MENU9000
MENU9999  RETURN
+
.*****************************************************************************
.*                                REDP0000                                   *
.*                    screen re-display after ? option                       *
.*****************************************************************************
REDP0000  BRANCH    SCRNFLAG,REDP9999
          MOVE      ONE,SCRNFLAG       * reset the redisplay flag
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",OPTNDES1:
                    *P1:7,"Select Option : ",*V2LON,OPTION;
.
REDP1111  DISPLAY   *P1:11,*EF,"Start Date        : ":
                    *V2LON,DATE1,*HOFF,*P31:11,DATEDES1:
                    *P1:12,"End   Date        : ":
                    *V2LON,DATE2,*HOFF,*P31:12,DATEDES2;
.
REDP2222  DISPLAY   *P1:14,*EF,"Start Doctor Type : ":
                    *V2LON,STARTCR,*HOFF,*P31:14,DOCTDES1:
                    *P1:15,"End   Doctor Type : ":
                    *V2LON,ENDCR,*HOFF,*P31:15,DOCTDES2;
.
REDP9999  RETURN
+
.*****************************************************************************
.                                 MNTH0000
.    Month range of calender year
.    EXIT=1 if return hit on first date range.
.*****************************************************************************
MNTH0000  MOVE      TWENTY1,CCOL               * setup screen position 1st keyin
          MOVE      TEN1,CVERT
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      SP2,CMON
          CALL      GDAT0000                   * get month and year calender
          BRANCH    EXIT,MNTH9999
          PACK      LINE80,DRGYR,DRGNUM,DRGLDSC,KEY6
          UNPACK    LINE80,FDATE1,DATEDES1,KEY4,CMON
          PACK      DATE1,CMON,SLASH,KEY4
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P31:11,CPCDATE;           * display date
          MOVE      CPCDATE,SAVFDATE
MNTH5555
          MOVE      TWENTY1,CCOL
          MOVE      TEN2,CVERT
          MOVE      ICENT,CCENT
          MOVE      IYEAR,CYEAR
          MOVE      IMON,CMON
          REP       " 0",CMON
          CALL      GDAT0000                   * get end date range
          BRANCH    EXIT,MNTH0000
          PACK      LINE80,DRGYR,DRGNUM,DRGLDSC,KEY6
          UNPACK    LINE80,FDATE2,DATEDES2,KEY4,CMON
          PACK      DATE2,CMON,SLASH,KEY4
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P31:12,CPCDATE;           * display date
          MOVE      CPCDATE,SAVTDATE
.
          MATCH     FDATE1,FDATE2
          GOTO      MNTH9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** End Date must be greater than or = Start Date **   ";
          CALL      HITENTER
          GOTO      MNTH5555
.
MNTH9999  RETURN
+
.*****************************************************************************
GDAT0000  
          DISPLAY   *PCCOL:CVERT,*EL;
          CALL      KEYPER             * keyin the period
          BRANCH    OVRCD,GDAT8888
          PACK      KEY6,CCENT,CYEAR,CMON
          REPLACE   " 0",KEY6          * replace blanks with zeros
          CALL      RDDRGA3            * validate date against calender year
          BRANCH    OVRCD,GDAT1111
          GOTO      GDAT8888
.
GDAT1111  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Date not found in Date Range file **    ";
          CALL      HITENTER
          GOTO      GDAT0000
GDAT8888
          MOVE      OVRCD,EXIT
GDAT9999  RETURN
+
.*****************************************************************************
.                                 DTYP0000
.    this routine gets a pair of information concerning start and end keys
.    and their descriptions.  
.*****************************************************************************
DTYP0000  MOVE      ONE,SCRNFLAG
.
DTYP1000  DISPLAY   *P21:14,UNDLN3,*P21:14;
.
DTYP1111  CALL      GDOC0000           * get item code for item1 and do ?
          MOVE      DOCTTYPE,DOCT1
.
          MATCH     SP3,DOCT1
          GOTO      DTYP2222 IF NOT EQUAL
          MOVE      "Start",STARTCR
          MOVE      SP30,DOCTDES1
          CALL      REDP0000           * test for redisplay
          GOTO      DTYP4444           * get item2
DTYP2222  
          COMPARE   ZERO,SCRNFLAG
          GOTO      DTYP3333 IF EQUAL
          DISPLAY   *P21:14,*EL,*V2LON,DOCT1;
.
DTYP3333  CALL      VDOC0000           * validate the DOCT1 entered
          BRANCH    EXIT,DTYP1000,DTYP1111
          CALL      REDP0000           * test for redisplay
          MOVE      DOCTTYPE,STARTCR   * copy the code for display purposes
          MOVE      TDESC,DOCTDES1     * copy description into work desc
DTYP4444                               * enter item2 info
          DISPLAY   *P21:14,*V2LON,STARTCR,*HOFF,*P31:14,DOCTDES1;
DTYP5000
          DISPLAY   *P21:15,UNDLN3,*P21:15;
DTYP5555  
          CALL      GDOC0000           * get item 2 and do ? routines 
          MOVE      DOCTTYPE,DOCT2
          MATCH     SP3,DOCT2          * test for return hit means END
          GOTO      DTYP6000 IF NOT EQUAL
          MOVE      "ZZZ",DOCT2        * set item to highest ascii value
          MOVE      "Finish",ENDCR     * set description for end code
          MOVE      SP30,DOCTDES2      * clear description doctor 2
          CALL      REDP0000           * test for redisplay
          GOTO      DTYP7777
DTYP6000
          COMPARE   ZERO,SCRNFLAG
          GOTO      DTYP6666 IF EQUAL
          DISPLAY   *P21:15,*EL,*V2LON,DOCT2;
.
DTYP6666  CALL      VDOC0000           * validate DOCT2
          BRANCH    EXIT,DTYP5000,DTYP5555
          MATCH     DOCT1,DOCT2        * test item2 appears after item1 in file
          GOTO      DTYP8000 IF LESS 
          CALL      REDP0000           * test for redisplay
          MOVE      DOCTTYPE,ENDCR     * copy the code for display reasons
          MOVE      TDESC,DOCTDES2     * copy description into work desc
DTYP7777
          DISPLAY   *P21:15,*V2LON,ENDCR,*HOFF,*P31:15,DOCTDES2;
          GOTO      DTYP9999
.
DTYP8000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** End Doc. Type must be > or = Start Doc. Type **    ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,DTYP5000
          GOTO      DTYP5555
.
DTYP9999  RETURN
+
.*****************************************************************************
.                                 GDOC0000
.    get doctor type store item in DOCTTYPE and checks for ? routines
.*****************************************************************************
GDOC0000  COMPARE   ZERO,SCRNFLAG
          GOTO      GDOC4444 IF EQUAL
GDOC1000
          KEYIN     *V2LON,DOCTTYPE
          ENDSET    DOCTTYPE
          APPEND    SP3,DOCTTYPE
          RESET     DOCTTYPE
.
          CMATCH    "?",DOCTTYPE
          GOTO      GDOC9999 IF NOT EQUAL
          PACK      CODE,ANSD,ANST
          CALL      PATCODDS           * ? routine for menu codes
          MOVE      ZERO,SCRNFLAG
GDOC4444
          DISPLAY   *P1:24,*EL,"Doctor Type : ___",*P15:24;
          GOTO      GDOC1000
.
GDOC9999  RETURN
.*****************************************************************************
.                                 VDOC0000
.    SCRNFLAG   0   1
.    EXIT       2   1   0 if data is valid
.*****************************************************************************
VDOC0000  PACK      KEY5,ANSD,ANST,DOCTTYPE,SP5
          CALL      RDCODE1            * test menu code against codes file
          BRANCH    OVRCD,VDOC8888
          GOTO      VDOC9990
.
VDOC8888  DISPLAY   *P1:24,*B,*EL,*V2LON,"** Doctor Type does not exist **    ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,VDOC8899
          MOVE      TWO,EXIT           * exit=2 if scrnflag=0 ? routine
          GOTO      VDOC9999
.
VDOC8899  MOVE      ONE,EXIT           * exit=1 if no ? routine used
          GOTO      VDOC9999
.
VDOC9990  MOVE      ZERO,EXIT          * exit=0 input valid
VDOC9999  RETURN
+
.*****************************************************************************
.                                 PSUR0000
.    Print Surgeon Usage Report first write data from Surgeon usage file
.    tempfile which adds up the details
.
PSUR0000  CALL      OPENTMP1
          CALL      STAT0000           * add up totals into tempfile
          CALL      PRNT0000
.
PSUR9999  RETURN
+
.*****************************************************************************
.                                 STAT0000
.    do stats by adding up all operations and time used in operations
.*****************************************************************************
STAT0000  PACK      KEY19,FDATE1,SP20
          CALL      RSOPSOU1
.
STAT2222  CALL      RKOPSOU1
          BRANCH    OVRCD,STAT9999
.
          MATCH     OPSODATE,FDATE2    * test if reached end of date range
          GOTO      STAT9999 IF LESS
.
          MOVE      "***",DRTYPE
          MOVE      "******",DCODE
          MATCH     SP6,OPSODOCT
          GOTO      STAT3333 IF EQUAL       * no code on file
.
          PACK      KEY6,OPSODOCT,SP6
          CALL      RDDOCT1            * get doctors details and type
          BRANCH    OVRCD,STAT2222
.
          MATCH     DOCT1,DRTYPE       * test doctor is within doct type range
          GOTO      STAT2222 IF LESS
          MATCH     DRTYPE,DOCT2       * test outside doct type range
          GOTO      STAT2222 IF LESS
.
STAT3333  PACK      KEY12,DRTYPE,DCODE,OPSOOTYP
          CALL      UPDT0000           * update single doctor subtotal
          PACK      KEY12,DRTYPE,DCODE,Z6
          CALL      UPDT0000           * update single doctor total
          PACK      KEY12,DRTYPE,Z6,OPSOOTYP
          CALL      UPDT0000           * update operation type subtotals
          PACK      KEY12,DRTYPE,Z9
          CALL      UPDT0000           * update subtotals for doctor types
          PACK      KEY12,Z9,OPSOOTYP
          CALL      UPDT0000           * update grandtotals for all types
          PACK      KEY12,Z12
          CALL      UPDT0000           * update grandtotal
          GOTO      STAT2222           * go and get another
.
STAT9999  RETURN
+
.*****************************************************************************
.                                 UPDT0000
.    update information to tempfile by checking if record on file then
.    add totals and update else write new record
.*****************************************************************************
UPDT0000  RESET     KEY12
          READ      OPRTMPF1,KEY12;DOCTTYPE,DOCTCODE,OPERTYPE,NUMOPS,TIMEUSED
          GOTO      UPDT8000 IF OVER
          ADD       OPSONOPR,NUMOPS
          ADD       OPSOTIME,TIMEUSED
          UPDATE    OPRTMPF1;DOCTTYPE,DOCTCODE,OPERTYPE,NUMOPS,TIMEUSED
          GOTO      UPDT9999
UPDT8000
          WRITE     OPRTMPF1,KEY12;KEY12,OPSONOPR,OPSOTIME
UPDT9999  RETURN
+
.*****************************************************************************
.                                 PRNT0000
.    Print operation usage report
.*****************************************************************************
PRNT0000  DISPLAY   *P1:24,"Doctor code :",*P50:24,*EL,*V2LON,"*** Printing ***"
          MOVE      ZERO,CPAGENO          * zero the page counter
          CALL      HEAD0000              * print report heading
          MOVE      SP6,LASTDOC           * set the last doctor code
          MOVE      ZERO,SCRNFLAG
.
          MOVE      SP20,KEY12
          READ      OPRTMPF1,KEY12;;      * begin reading the stats tempfile
PRNT2222
          READKS    OPRTMPF1;DOCTTYPE,DOCTCODE,OPERTYPE,NUMOPS,TIMEUSED
          GOTO      PRNT9999 IF OVER
.
          MATCH     Z6,DOCTTYPE           * test for grand total
          GOTO      PRNT2266 IF NOT EQUAL
          BRANCH    SCRNFLAG,PRNT3333
          MOVE      "GRAND TOTAL             ",DOCTNAME
          MOVE      DOCTNAME,NAMESAVE
          MOVE      ONE,SCRNFLAG          * use SCRNFLAG to abort the code later
          GOTO      PRNT3333
PRNT2266
          MATCH     LASTDOC,DOCTCODE      * test for change in doctor code
          GOTO      PRNT3333 IF EQUAL
          MOVE      DOCTCODE,LASTDOC      * set the current doctor code
          MATCH     Z9,DOCTCODE           * test for a doctor type total
          GOTO      PRNT3000 IF NOT EQUAL
.
          MOVE      "No Doctor Type",TDESC  * default code
          MATCH     "***",DOCTTYPE
          GOTO      PRNT2900 IF EQUAL       * no code
.
          MOVE      "Unknown Code",TDESC    * default doc type desc to unknown
          PACK      KEY5,ANSD,ANST,DOCTTYPE
          CALL      RDCODE1               * get doctor type description
.
PRNT2900  PACK      DOCTNAME,TYPEHEAD,TDESC     * set the operation type
          PACK      NAMESAVE,TYPEHEAD,TDESC
          GOTO      PRNT3333
PRNT3000
          DISPLAY   *P15:24,DOCTCODE;
          CALL      DOCN0000              * get doctor description
          MOVE      DOCTNAME,NAMESAVE
PRNT3333      
          MATCH     Z9,OPERTYPE           * test for subtotals for operas & time
          GOTO      PRNT4444 IF NOT EQUAL
          PRINT     "|",*29,"|----------------------|------------|------------|"
          PRINT     "|",*29,"| Total",*52,"|   ",NUMOPS,"   |   ":
                              TIMEUSED,"   |"
          CALL      DRAWLINE
          ADD       THREE,CLNO
.
          MOVE      SP30,DOCTNAME
          PACK      KEY9,DOCTCODE,OPERTYPE
          MATCH     KEY9,Z9
          GOTO      PRNT2222 IF NOT EQUAL
          MATCH     Z6,DOCTTYPE           * test for grandtotal & end of report
          GOTO      PRNT3388 IF NOT EQUAL
.
          PRINT     *N,"*** End of Report ***",*N,*N,*N:
                    "Number of Operations = Total ":
                    "Number of operations performed for each operation type",*N:
                    "Total Oper. duration = ":
                    "Operation performance time in minutes"
.
          DISPLAY   *P1:24,*EL,*V2LON,*P24:24:
                    "** Report Generation Completed **",*W2;
          GOTO      PRNT9999
PRNT3388
          PRINT     *N;                   * print a divider bet. sets of output
          CALL      DRAWLINE
          ADD       TWO,CLNO
          GOTO      PRNT2222
PRNT4444
          COMPARE   CLNO,NUM55            * test for end of page
          CALL      NPAG0000 IF LESS
.
          MOVE      "No Operation Type",TDESC
.
          PACK      OPERTYPE,OPERTYPE,SP3
          MATCH     SP3,OPERTYPE
          GOTO      PRNT6000 IF EQUAL
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSO,ANSY,OPERTYPE
          CALL      RDCODE1               * get operation type
PRNT6000
          PRINT     "| ",DOCTNAME," | ",TDESC," |   ":
                    NUMOPS,"   |   ",TIMEUSED,"   |"
          MOVE      SP30,DOCTNAME
          ADD       ONE,CLNO
          GOTO      PRNT2222
.
PRNT9999  RETURN
+
.*****************************************************************************
.    Next page prints new page header and resets the doctors name or doctor
.    type description
.
NPAG0000  CALL      DRAWLINE
          CALL      HEAD0000
          MOVE      NAMESAVE,DOCTNAME
NPAG9999  RETURN
+
.*****************************************************************************
.    setup the doctors name in DOCTNAME and get description to operation type
.*****************************************************************************
DOCN0000  MOVE      "No Doctor Code",DOCTNAME           * clear doctors name 
          MATCH     "******",DOCTCODE
          GOTO      DOCN9999 IF EQUAL       * no code 
.
          MOVE      "Unknown Code ",DOCTNAME           * clear doctors name 
          PACK      KEY6,DOCTCODE,SP6
          CALL      RDDOCT1                 * get doctor details
          BRANCH    OVRCD,DOCN9999
.
          MOVE      ONE,PACFRMT             * title name surname of doctor
          MOVE      DSNAME,PACSNAME         * use pacdate to fix doctors name
          MOVE      DGNAME,ANS              * get first letter in given name
          PACK      PACGNAME,ANS,SP30    
          MOVE      DTITL,PACTITLE
          CALL      PACNAME                 * pack up doctors name
          MOVE      PACFNAME,DOCTNAME
DOCN9999  RETURN
+
.*****************************************************************************
.                                 HEAD0000
.*****************************************************************************
HEAD0000  ADD       ONE,CPAGENO             * increment the page counter
          CLOCK     TIME,CTIMEIS            * get current system time
.
          PRINT     *F,*N,PRGID,*20,CNAME,*65,"DATE : ",CDATE:
                    *N,VERSION,*20,PRGNAM,*65,"TIME : ",CTIMEIS:
                    *N,*65,"PAGE :      ",CPAGENO
.
          PRINT     *20,"Start Date : ",DATE1,SP3,SAVFDATE,*N:
                    *20,"End   Date : ",DATE2,SP3,SAVTDATE,*N:
                    *13,"Doctor Type range : ",*+,STARTCR," to ",ENDCR,*N
.
          CALL      DRAWLINE
          PRINT     "| Operating Surgeon",*29,"| Operation Type",*52:
                    "| Number of  | Total Oper.|",*N:
                    "|",*29,"|",*52,"| Operations |  Duration  |"
          CALL      DRAWLINE
          MOVE      TEN1,CLNO               * set the printed line counter
HEAD9999  RETURN
+
.******************************************************************************
DRAWLINE  PRINT     "--------------------------------------------------------":
                    "----------------------"
          RETURN
+
.*****************************************************************************
.                                 OPENTMP1
.*****************************************************************************
OPENTMP1  DISPLAY   *P1:24,*EL,*V2LON,"Pre-processing....  ",*BLINKON:
                                      "Please wait";
          CALL      DELTEMP1               * delete the temp file
          EXECUTE   DOSCMND1,TASKID        * temp index file for item code order
          OPEN      OPRTMPF1,TEMPNAM1      * open the tempfile file pointer
          DISPLAY   *P1:24,*EL;            * clear notice to users
          RETURN
+
.*****************************************************************************
DELTEMP1  CLOSE      OPRTMPF1              * close 1st and 2nd index
          CLEAR      DELCOMM
          APPEND     "iserase ",DELCOMM 
          APPEND     TEMPNAM1,DELCOMM 
          RESET      DELCOMM 
          EXECUTE    DELCOMM,TASKID
          RETURN
+
          INC       STD001IO
          INC       PATCODKY                   * codes file ? routine
          INC       PATCOMN3                   * KEYPER routine
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       OPRSOUIO/INC               * surgeon operation usage
          INC       PATDRGIO/INC               * period date range
          INC       PATCODIO/INC               * patient codes 
          INC       PATDO1IO/INC               * doctors details

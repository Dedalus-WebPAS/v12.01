.***************************************************************************
.* System    :   Private Practice                                          *
.* Program   :   FXPRIDTR                                                  *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   15/04/2024                                                *
.* Author    :   J.Tan  Task 0941662                                       *
.* Function  :   This program will change PRDTAMNT = Item amount x Quantity*
.*               and PRDTGSTM = Item GST amount x quantity                 *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PRIDTRFD/INC
          INC       PRIINVFD/INC
          INC       PRIFCIFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNT     FORM      5
LOOPFLAG  FORM      1
.
PRGID     INIT      "FXPRDTRC"
PRGNAM    INIT      "Update Total Item & GST Amount"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PRIDTRA4,"pridtraf"
          OPEN      PRIINVO1,"priinvof"
          OPEN      PRIFCIA3,"prifciaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run":
                    *P1:6,*V2LON,TWO,*HOFF,". Run & Update"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  MOVE      "999",CLNO
          MOVE      ZERO,LOOPFLAG
          MOVE      ZERO,COUNT
.
          DISPLAY   *P1:24,*EL,"Processing pridtraf : ";
.
          PACK      KEY22,SP70
          CALL      RSPRDT4
PROC1000  CALL      RKPRDT4
          BRANCH    OVRCD,PROC5000
.
          ADD       ONE,COUNT
          DISPLAY   *P23:24,COUNT;
.
          COMPARE   ONE,PRDTRTYP          * Item record type ?
          GOTO      PROC1000 IF NOT EQUAL
    
          COMPARE   TWO,PRDTSERV
          GOTO      PROC1000 IF LESS      * single item
          COMPARE   ZERO,PRDTIAMT
          GOTO      PROC1000 IF EQUAL
.
          COMPARE   PRDTAMNT,PRDTIAMT     * check Total amount against Item amt
          GOTO      PROC1000 IF NOT EQUAL
.
          MOVE      PRDTINVN,KEY8
          CALL      PRTL0000
          GOTO      PROC1000
.
.         Check Credit note file
.
PROC5000  ADD       ONE,LOOPFLAG
          MOVE      ZERO,COUNT
          DISPLAY   *P1:24,*EL,"Processing prifciaf : ";
.
          MOVE      SP70,KEY33
          CALL      RSPRFCI3
PROC6000  CALL      RKPRFCI3
          BRANCH    OVRCD,PROC9000
.
          ADD       ONE,COUNT
          DISPLAY   *P23:24,COUNT;
.
          COMPARE   ONE,PRFCRTYP          * Item record type ?
          GOTO      PROC6000 IF NOT EQUAL
.
          COMPARE   TWO,PRFCNUMS
	  GOTO      PROC6000 IF LESS      * single item
          COMPARE   ZERO,PRFCITAM
          GOTO      PROC6000 IF EQUAL
.
          COMPARE   PRFCIAMT,PRFCITAM     * check Total amount against Item amt
          GOTO      PROC6000 IF NOT EQUAL
.
          MOVE      PRFCINVN,KEY8
          CALL      PRTL0000
          GOTO      PROC6000
.
PROC9000  DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.**************************************************************************
.*                               PRTL0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
PRTL0000  MOVE      SP70,PRINDATE
          CALL      RDPRIN1                 * read the P.P. invoice file
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          UNPACK    PRINDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          IF        LOOPFLAG=1
            PRINT     *1,PRFCINVN,*12,PRFCDEBT,*23,PRFCINUM,"/",PRFCSUBI:
                      *38,PRFCMSFL,*45,PRFCNUMS,*50,PRFCITAM,*65,PRFCIAMT:
                      *82,PRFCCGST,*100,CPCDATE;
          ELSE
            PRINT     *1,PRDTINVN,*12,PRDTDEBT,*23,PRDTITMN,"/",PRDTSUBN:
                      *38,PRDTMFLG,*45,PRDTSERV,*50,PRDTIAMT,*65,PRDTAMNT:
                      *82,PRDTGSTM,*100,CPCDATE;
          ENDIF
.
          IF        LOOPFLAG=1
            PRINT     *112,"*";       * Credit note
          ENDIF
          PRINT     " "
.
          ADD       ONE,CLNO
.
          COMPARE   TWO,COPTION
          GOTO      PRTL9999 IF NOT EQUAL * Run only
.
          IF        LOOPFLAG=1
            MOVE      PRFCITAM,PRFCIAMT
            MULT      PRFCNUMS,PRFCIAMT      * Total Amount
            MULT      PRFCNUMS,PRFCCGST      * Total GST Amount
            CALL      UPPRFCI3
          ELSE
            MOVE      PRDTIAMT,PRDTAMNT
            MULT      PRDTSERV,PRDTAMNT      * Total Amount
            MULT      PRDTSERV,PRDTGSTM      * Total GST Amount
            CALL      UPPRDT4
          ENDIF
.
PRTL9999  RETURN
+
.**************************************************************************
.*                               HEAD0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
HEAD0000  CALL      HEAD132
.
          CALL      UND132
          PRINT     *1,"Inv.No",*12,"U/R No.",*23,"Item":
                    *38,"Mult",*45," Serv",*54,"Item Amt":
                    *65,"   Total Amt",*82,"   GST Amt",*100,"Invoice Date"
          CALL      UND132
          ADD       "2",CLNO
.
HEAD9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PRIDTRIO/INC
          INC       PRIINVIO/INC
          INC       PRIFCIIO/INC

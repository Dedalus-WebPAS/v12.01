.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   CONVOUT                                                   *
.* Desc      :   Load Conversion data for Outpatients and Allied Health    *
.***************************************************************************
.* Date      :   05/06/2001                                                *
.* Author    :   Mike Laming                                               *
.* Function  :   This program will read the conversion files convahrf.txt  *
.*               and convoutp.txt to load the Allied Health Referrals and  *
.*               Outpatients data to ALLREFAF and PMSEVTAF                 *
.* Mods      :                                                             *
.*     V12.00.01 01/07/2025  J.Tan           TSK 0961623                   *
.*               Mod for Alphanumeric visit number                         *
.*     V10.03.01 17/11/2011  Mike Laming     CAR 240184                    *
.*               Changes to IBAPOSTF Post Code table - added State to Keys *
.*     V9.11.03  07/04/2009  Peter Vela         CAR 183976                 *
.*               Moved spaces to PMVXPILC before WRPMVX11                  *
.*     V9.11.02  17/02/2009  Peter Vela         CAR 183976                 *
.*               Moved spaces to PMVXPOEC before WRPMVX11                  *
.*     V9.11.01  08/01/2009  Steve Armstrong    CAR 185927                 *
.*               Removed isadd on temp file and included extra index in    *
.*               isbuild (now that CMDLINE has been used instead of KEY127 *
.*               and extended to 500 in length).                           *
.***************************************************************************
.*     V9.09.01  04/01/2008  J.Tan         CAR 135498                      *
.*               Mods for NZ MHINC Extract                                 *
.***************************************************************************
.*     V9.05.B01 20/02/2006  Ebon Clements     CAR 76341                   *
.*               Added referral and encounter file audit                   *
.***************************************************************************
.*     V9.03.01  30/03/2003  Lina Vo      CAR 37504                        *
.*               Write SLA/ASGC code from ibapostf to visit extension table*
.***************************************************************************
.*     V9.02.14  20/02/2002  Ebon Clements                                 *
.*               Move XVVISN for a form before writing to pmsevtaf         *
.*     V9.02.13  05/02/2002  J Habasque                                    *
.*               Added check for merged U/R's                              *
.*     V9.02.12  02/02/2002  J Habasque                                    *
.*               Changed XEVISN to be XEREFN                               *
.*     V9.02.11  02/02/2002  J Habasque                                    *
.*               Added move of spaces to XERHCP                            *
.*     V9.02.10  31/01/2002  J Habasque                                    *
.*               Added check for ovrcd=1 after rdmast1                     *
.*     V9.02.09  31/01/2002  J Habasque                                    *
.*               Changed to read pmshcpaf instead of patdo1af              *
.*     V9.02.08  25/01/2002  J.Tan                                         *
.*               Mods to not to write to outcli and patdoc for invalid doc *
.*     V9.02.07  23/01/2002  J.Tan                                         *
.*               Mods to read site file                                    *
.*     V9.02.06  16/01/2002  J.Tan                                         *
.*               Mods PVISYST to " 1" instead of "1"                       *
.*     V9.02.05  07/12/2001  Ebon Clements                                 *
.*               Added variable to A/Health for orthotics job number       *
.*     V9.02.04  26/10/2001  Mike Laming                                   *
.*               Add TRAP FORMAT code to catch input data errors           *
.*     V9.02.03  16/10/2001  Mike Laming                                   *
.*               Change Outpatients PVISYST from 3 to 1 (Event Coding)     *
.*     V9.02.02  16/10/2001  Mike Laming                                   *
.*               Add fields for Referral No., and Refer's Name             *
.*               Change Problem Codes to Diagnosis Codes                   *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC                
          INC       IBAPOSFD/INC                 * Postcode file        
          INC       PATMA1FD/INC                 * Patient Master file
          INC       PATVISFD/INC                 * Visit Details
          INC       PMSVX1FD/INC                 * Visit Extension 
          INC       PMSEVTFD/INC                 * Patient Event file
          INC       PATDO1FD/INC                 * Patient Doctor Table
          INC       PMSHCPFD/INC                 * Patient Healthcare Profess
          INC       PATCODFD/INC                 * Patient Codes Table
          INC       ALLREFFD/INC                 * Allied Health Referral 
          INC       OUTCTYFD/INC                 * Outpatients Clinic Type
          INC       OUTCLIFD/INC                 * Outpatients Clinic ID
          INC       OUTSITFD/INC                 * Outpatients Site Table
.
.
.Layout of convahrf.txt
.---------------------
AHRFPD   FILE      ASCII, FIXED=143                                    *C-90202
.
.Name     Type    Length  Pos  Description
.----     ----    ------  ---  -----------
XEVISN    DIM       8      1    Visit Number
XEREFN    DIM       8      9    Referral Number
XEURNO    DIM       8      17   U/R Number
XERDAT    DIM       8      25   Date of Referral (CCYYMMDD)    (Date of Letter)
XESRCE    DIM       3      33   Source of Referral  (Categ LQ) (Referral Source)
XERHCP    DIM       6      36   Referring HCP (pmshcpaf)       (Referring Doc)
XERNAM    DIM       30     42   Refer's Name                   (Ref. Person)
XEHCP     DIM       6      72   Responsible HCP (pmshcpaf)     (Responsible HCP)
XEUNIT    DIM       3      78   Unit/Specialty   (Category AC) (Unit)
XEPRTY    DIM       3      81   Priority (Category TP)         (Priority)
XECOMP    DIM       3      84   Compensation Cod (Category CL) (Claim Code)
XECTYP    DIM       6      87   Clinic Type                    (Clinic Type)
XEDIA1    DIM       9      93   Diagnosis Code 1               |
XEDIA2    DIM       9      102  Diagnosis Code 2               | were XEPROn
XEDIA3    DIM       9      111  Diagnosis Code 3               |
XELINK    DIM       8      120  Linked Visit Number 
XEEDEP    DIM       3      128  Department Code                (Department)
XEJOBN    FORM      12     131  Job Number
.
.        End of record     143
.
.
.Layout of convoutp.txt
.---------------------
OUTPATD   FILE      ASCII, FIXED=135
.
.Name     Type    Length  Pos  Description
.----     ----    ------  ---  -----------
XVURNO    DIM       8      1    U/R Number
XVEVNT    DIM       3      9    Event Type (Category EV) 
XVADTE    DIM       8      12   Admission/Attendance Date (ccyymmdd
XVATIM    DIM       5      20   Admission/Attendance Time (hh:mm:ss) (to 8 byte)
XVBTYP    DIM       3      25   Booking Type (Cat S)       (Source of Referral)
XVDDTE    DIM       8      28   Discharge/Depart Date (ccyymmdd)
XVDTIM    DIM       5      36   Discharge/Depart Time (hh:mm:ss)  (Convert to 8)
XVACON    DIM       6      41   Attending Consultant (pmshcpaf)  (Doc.Seen Code)
XNANAM    DIM       25     47   Doctor's Name   (When needed to add to PATDO1FD)
XVATYP    DIM       3      72   Appointment Type (Cat CV)
XVASTT    DIM       1      75   Attendance Status
XVCCOD    DIM       3      76   Claim Code  (Cat CL)
XVCTYP    DIM       6      79   Clinic Type (outctyaf)
XVCLID    DIM       6      85   Clinic Id   (outcliaf)
XNCNAM    DIM       30     91   Clinic Name (when needed to add to OUTCLIFD)
XVVISN    DIM       8      121  Visit Number
XNRCNC    DIM       3      129  Reason for cancellation (Category CB)  to where?
XVUNIT    DIM       3      132  Unit (Department)       (Category AC)
.        End of record     135
.
TEMP      FILE      ASCII,FIXED=256
.
ANERROR   FORM      1
BYPAHRF   FORM      1
BYPOUTPT  FORM      1
CMDLINE   DIM       500
COUNT     FORM      6
COUNTIN   FORM      6
COUNTERR  FORM      6
COUNTREJ  FORM      6
COUNTAWR  FORM      6
COUNTAUP  FORM      6
COUNTEWR  FORM      6
COUNTEUP  FORM      6
COUNTVWR  FORM      6
COUNTVUP  FORM      6
COUNT1    FORM      3
DATAERR   FORM      1                                                  *I-90204
DISPVAR   FORM      6
NEWVIS    FORM      1
WHICHPAS  FORM      1
.
CH12      DIM       12
CURDTE    DIM       8
LINNO     DIM       3
PRTPT1    DIM       35
PRTPT2    DIM       92
SECS      INIT      ":00" 
.                    ....*....1....*....2....*....3....*....4..
SP127     INIT      "                                          ":
                    "                                          ":
                    "                                           "
WRKDATE   DIM       10
WDD       DIM       2
WMM       DIM       2
WCC       DIM       2
WYY       DIM       2
.
DASH      INIT      "-"
LALL      INIT      "ALL   "
LOUT      INIT      "OUT   "
LCL       INIT      "CL"
LTP       INIT      "TP"
.
PRGID     INIT      "CONVOUT"
PRGNAM    INIT      "Load Allied Health Refs & Outpatients"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and check files
          BRANCH    EXIT,ML9999            * if conversion files don't exist
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
. ------- CALL      CLER0000               * Clear the files before proceeding
. ------- BRANCH    EXIT,ML9999
.
          CALL      OPEN0000               * Open file
          CALL      PROC0000               * process the details
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and check files. *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      AHRFPD,"convahrf"
          MOVE      OVRCD,BYPAHRF
          TRAPCLR   IO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTPATD,"convoutp"
          MOVE      OVRCD,BYPOUTPT
          TRAPCLR   IO
.
          MOVE      ZERO,EXIT
.                                           * If no conversion files exist
          IF        BYPAHRF=1 & BYPOUTPT=1
            DISPLAY   *P1:23,*EL,*B,"Files convoutp.txt and convahrf.txt ":
                      " do not exist - Process Terminated":  
                      *P1:24,"Hit <",*V2LON,"ENTER",*HOFF,"> to continue ";
            KEYIN     ANS;
            MOVE      ONE,EXIT
          ENDIF 
.
          PACK      CURDTE,CCC,CYY,CMM,CDD  * Set up the current date for use
          REPLACE   " 0",CURDTE
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.****************************************************************************

OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,"This program loads Outpatient and/or Allied Health":
                    " Referral data":
                    *P15:5,"from the convoutp & convahrf files";
          KEYIN     *P1:24,*EL,"Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN0000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  CLER0000                                  *
.*                      Clear The Files Before Proceeding                     *
.******************************************************************************
CLER0000  KEYIN     *P1:24,*EL,*B,"Clear the files before proceeding (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      CLER1000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      CLER9000 IF EQUAL
.
          BEEP
          GOTO      CLER0000
.
CLER1000  COMPARE   ONE,BYPAHRF
          GOTO      CLER2000 IF EQUAL
          
          DISPLAY   *P1:24,*EL,"Checking pmsevtaf",*W;
          EXECUTE   "ldat pmsevtaf.dat > temp.txt",TASKID
          CALL      FFLP0000                * Find file path
          BRANCH    EXIT,CLER9500
.
          DISPLAY   *P1:24,*EL,"Saving patgsrnf",*W;
          EXECUTE   "cp `ldat pmsevtaf.dat` savevtaf.dat",TASKID
          EXECUTE   "cp `ldat pmsevtaf.idx` savevtaf.idx",TASKID
.
          DISPLAY   *P1:24,*EL,"Deleting pmsevtaf",*W;
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    KEY99,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          DISPLAY   *P1:24,*EL,"Creating pmsevtaf",*W;
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    KEY99,CMDLINE
          APPEND    " 230 UC1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
CLER2000  COMPARE   ONE,BYPOUTPT
          GOTO      CLER9000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"Checking allrefaf",*W;
          EXECUTE   "ldat allrefaf.dat > temp.txt",TASKID
          CALL      FFLP0000                * Find file path
          BRANCH    EXIT,CLER9500
.
          DISPLAY   *P1:24,*EL,"Saving patalrtf",*W;
          EXECUTE   "cp `ldat allrefaf.dat` savrefaf.dat",TASKID
          EXECUTE   "cp `ldat allrefaf.idx` savrefaf.idx",TASKID
.
          DISPLAY   *P1:24,*EL,"Deleting allrefaf",*W;
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    KEY99,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          DISPLAY   *P1:24,*EL,"Creating allrefaf",*W;
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    KEY99,CMDLINE
          APPEND    " 710 UC1-8 UC9-16,1-8 UC17-17,129-136,1-8",CMDLINE
          APPEND    " UC172-174,17-17,250-259,1-8",CMDLINE
          APPEND    " UC250-259,17-17,129-136,1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
CLER9000  MOVE      ZERO,EXIT
          GOTO      CLER9999
.
CLER9500  MOVE      ONE,EXIT
CLER9999  RETURN
+
.******************************************************************************
.*                                  FFLP0000                                  *
.*                               Find File Path                               *
.******************************************************************************
FFLP0000  CLOSE     TEMP
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,FFLP3000
.
          READ      TEMP,SEQ;KEY99
          GOTO      FFLP3000 IF OVER
.
          ENDSET    KEY99
FFLP1000  CMATCH    ".",KEY99
          GOTO      FFLP2000 IF EQUAL
.
          BUMP      KEY99,-1
          GOTO      FFLP1000 IF NOT EOS
          GOTO      FFLP3000
.
FFLP2000  BUMP      KEY99,-1
          LENSET    KEY99
          RESET     KEY99
          PACK      KEY99,KEY99,SP127
          STRIP     KEY99
          GOTO      FFLP9000
.
FFLP3000  DISPLAY   *P1:24,*EL,*B,"Error clearing files.  Files not cleared.  ";
          CALL      HITENTER
          GOTO      FFLP9500
.
FFLP9000  MOVE      ZERO,EXIT
          GOTO      FFLP9999
.
FFLP9500  MOVE      ONE,EXIT
FFLP9999  RETURN
+
.**************************************************************************
.*                               OPEN0000              Called by: ML0000  *
.*                              Open Files                                *
.**************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD                   * 
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.                                                * Visit Details
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
.                                                * Doctor Table 
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.                                                * Referring Doctor Table 
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.                                                * Codes Table
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.                                                * Clinic Type
          DISPLAY   *P64:24,"outctyaf";
          OPEN      OUTCTYA1,"outctyaf"
.                                                * Clinic Id
          DISPLAY   *P64:24,"outcliaf";
          OPEN      OUTCLIA1,"outcliaf"
.                                                * Site     
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.
          COMPARE   ONE,BYPAHRF
          GOTO      OPEN1000 IF EQUAL
.                                                * Allied Health Referral
          DISPLAY   *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"
.
OPEN1000  COMPARE   ONE,BYPOUTPT
          GOTO      OPEN9999 IF EQUAL
.                                                * Patient Events
          DISPLAY   *P64:24,"pmsevtaf";
          OPEN      PMSEVTA1,"pmsevtaf"
.
OPEN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                     Process the conversion files                       *
.**************************************************************************
.
.
PROC0000  CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      "100",CLNO              * Force 1st page Heading
          MOVE      ONE,CNOUNDLN
.
          CALL      HEAD0000
          MOVE      ZERO,COUNTIN
          MOVE      ZERO,COUNTERR
          MOVE      ZERO,COUNTREJ
          MOVE      ZERO,COUNTAUP
          MOVE      ZERO,COUNTAWR
          MOVE      ZERO,COUNTVUP
          MOVE      ZERO,COUNTVWR
.
          MOVE      SP10,OSTSITE
          MOVE      SP10,KEY6 
          CALL      RDSSITA1                 * Read Site Table
          CALL      RDKSITA1
          IF        OVRCD=1
            MOVE      " - Missing record from Site Table",PRTPT2
            CALL      PRNT0000
            MOVE      ONE,ANERROR
            MOVE      SP30,PRTPT1
            GOTO      PROC9999
          ENDIF
.
          COMPARE   ONE,BYPAHRF
          GOTO      PROC5000 IF EQUAL
.
          PRINT     *1,"|",*37,"|",*50,"'convahrf.txt'",*132,"|"
.
          MOVE      ONE,COUNTIN
          MOVE      ONE,COUNT
          DISPLAY   *P1:14,"* Processing ",*V2LON,"convahrf.txt ",*HOFF," *":
                    *P1:16,"Started : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:18,"U/R Number : ":
                    *P1:19,"S          : ":
                    *P1:20,"S          : ":
                    *P1:21,*EL,"Records    : ";
.
          MOVE      ZERO,COUNTIN
          MOVE      ZERO,COUNTERR
          MOVE      ZERO,COUNTREJ
.
PROC1000  MOVE      ZERO,OVRCD
          MOVE      ZERO,DATAERR                                       *I-90204
          TRAP      FERR0000 IF FORMAT                                 *I-90204
.
. -----   READ      AHRFPD,SEQ;XEVISN,XEURNO,XERDAT,XESRCE,XERHCP:      D-90202
. -----                        XEHCP,XEUNIT,XEPRTY,XECOMP,XECTYP:       D-90202
. -----                        XEPRO1,XEPRO2,XEPRO3,XELINK,XEEDEP       D-90202
          READ      AHRFPD,SEQ;XEVISN,XEREFN,XEURNO,XERDAT:            *I-90202
                               XESRCE,XERHCP,XERNAM,XEHCP:             *I-90202
                               XEUNIT,XEPRTY,XECOMP,XECTYP:            *I-90202
                               XEDIA1,XEDIA2,XEDIA3,XELINK,XEEDEP:     *I-90202
                               XEJOBN
.
          TRAPCLR   FORMAT                                             *I-90204
.
          GOTO      PROC4000 IF OVER        * Check for EOF
          BRANCH    DATAERR,PROC1000        * Get next record          *I-90204
.
          ADD       ONE,COUNTIN
          ADD       ONE,COUNT
          ASSIGN    COUNT % 100,DISPVAR
          IF        DISPVAR = 0
            DISPLAY   *P14:18,*V2LON,XEURNO:
                      *P14:19,*V2LON,XEREFN:
.                     *P14:20,*V2LON,XRGNAM:
                      *P14:21,COUNT;
          ENDIF
.
          MOVE      ONE,WHICHPAS
          MOVE      XEURNO,KEY8
          CALL      CHEK0000                * Check against Patient Master
.
          BRANCH    ANERROR,PROC1000
          CALL      EDAA0000                * Edit Allied Health Ref fields
.
          BRANCH    ANERROR,PROC1000
.
          CALL      UPAA0000                * Add to Allied Health Referral
          GOTO      PROC1000
.
PROC4000  PRINT     *1,"|",*37,"|",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTIN:
                    " 'convahrf.txt' records read",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTERR:
                    " errors detected ",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTREJ:
                    " records rejected",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTAWR:
                    " Allied Health Referrals created",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTAUP:
                    " Allied Health Referrals updated",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTVWR:
                    " Visit records created",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTAUP:
                    " Visit records updated",*132,"|"
          ADD       EIGHT,CLNO
.
          MOVE      ZERO,COUNTIN
          MOVE      ZERO,COUNTERR
          MOVE      ZERO,COUNTREJ
          MOVE      ZERO,COUNTVUP
          MOVE      ZERO,COUNTVWR
.
PROC5000  COMPARE   ONE,BYPOUTPT
          GOTO      PROC9999 IF EQUAL
.
          PRINT     *1,"|",*37,"|",*132,"|":
                    *N,*1,"|",*37,"|",*50,"'convoutp.txt'",*132,"|"
          ADD       TWO,CLNO
          MOVE      ZERO,COUNTEWR
          MOVE      ZERO,COUNTEUP
.
          MOVE      ONE,COUNT
          DISPLAY   *P1:14,"* Processing ",*V2LON,"convoutp.txt ",*HOFF," *":
                    *P1:16,"Started : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:18,"U/R Number : ":
                    *P1:19,"A          : ":
                    *P1:20,"A          : ":
                    *P1:21,*EL,"Records    : ";
.
PROC6000  MOVE      ZERO,OVRCD
          MOVE      ZERO,DATAERR                                       *I-90204
          TRAP      FERR2000 IF FORMAT                                 *I-90204
.
          READ      OUTPATD,SEQ;XVURNO,XVEVNT,XVADTE,XVATIM,XVBTYP,XVDDTE:
                                XVDTIM,XVACON,XNANAM,XVATYP,XVASTT,XVCCOD:
                                XVCTYP,XVCLID,XNCNAM,XVVISN,XNRCNC,XVUNIT
.
          TRAPCLR   FORMAT                                             *I-90204
.
          GOTO      PROC9000 IF OVER        * Check for EOF
          BRANCH    DATAERR,PROC6000        * Get next record          *I-90204
.
          ADD       ONE,COUNTIN
          ADD       ONE,COUNT
          ASSIGN    COUNT % 100,DISPVAR
          IF        DISPVAR = 0
            DISPLAY   *P14:18,*V2LON,XVURNO:
.                     *P14:19,*V2LON,XLCATG:
.                     *P14:20,*V2LON,XLCODE:
                      *P14:21,COUNT;
          ENDIF
.
          MOVE      TWO,WHICHPAS
          MOVE      XVURNO,KEY8
          CALL      CHEK0000                * Check against Patient Master
.         
          BRANCH    ANERROR,PROC6000
          CALL      EDBB0000                * Edit Outpatients fields
.
          BRANCH    ANERROR,PROC6000
.
          CALL      UPBB0000                * Add to Events & Visit Tables
          GOTO      PROC6000
.
PROC9000  PRINT     *1,"|",*37,"|",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTIN:
                    " 'convoutp.txt' records read",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTERR:
                    " errors detected ",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTREJ:
                    " records rejected",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTEWR:
                    " Patient Events created",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTEUP:
                    " Patient Events updated",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTVWR:
                    " Visit records created",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTAUP:
                    " Visit records updated",*132,"|"
.
PROC9999  MOVE      ZERO,OVRCD
          CLOCK     TIME,CTIMEIS
          CALL      UND132
          PRINT     *N,*N,"**** End of Report ****";
.
          DISPLAY   *P20:21,*V2LON,COUNT;
          KEYIN     *P40:16,"Finished : ",*V2LON,*DV,CTIMEIS:
                    *P1:24,*EL,*HOFF,"No more records.  Tap ",*V2LON,"<ENTER>":
                    *HOFF," to exit ... ",*EOFF,ANS;
          RETURN
+
.**************************************************************************
.*                               CHEK0000              Called by: PROC0000*
.*                Check that U/R Number is on-file & valid                *
.**************************************************************************
.
CHEK0000  MOVE      ZERO,ANERROR
.
.                                    Set up the Key data for any Error lines
          IF        WHICHPAS =1
            UNPACK    XERDAT,WCC,WYY,WMM,WDD
            PACK      WRKDATE,WDD,DASH,WMM,DASH,WCC,WYY
            PACK      PRTPT1,SP3,XEURNO,SP2,WRKDATE,SP2,XEREFN,SP30
          ELSE
            UNPACK    XVADTE,WCC,WYY,WMM,WDD
            PACK      WRKDATE,WDD,DASH,WMM,DASH,WCC,WYY
            PACK      PRTPT1,SP3,XVURNO,SP2,WRKDATE,SP30
          ENDIF
.
.
          RESET     KEY8
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      ONE,ANERROR
            IF        WHICHPAS = 1
              MOVE      "U/R not found for this Referral",PRTPT2
            ELSE
              MOVE      "U/R not found for this Outpatient Event",PRTPT2    
            ENDIF
            CALL      PRNT0000
            ADD       ONE,COUNTREJ
            GOTO      CHEK9999
          ENDIF
.         
          COMPARE   ZERO,PSTAT              * valid U/R ?
          GOTO      CHEK9999 IF EQUAL
.
          COMPARE   ONE,PSTAT
          IF        @EQUAL
            MOVE      PPSNAME,XEURNO
            PACK      KEY8,XEURNO,SP70
            GOTO      CHEK0000
          ENDIF
. 
          MOVE      ONE,ANERROR
          IF        WHICHPAS = 1
            MOVE      "U/R not valid for this Referral",PRTPT2
          ELSE
            MOVE      "U/R not valid for this Outpat.Event",PRTPT2
          ENDIF
          CALL      PRNT0000
          ADD       ONE,COUNTREJ
.
CHEK9999  RETURN
+
.**************************************************************************
.*                               EDAA0000              Called by: PROC0000*
.*                Edit Allied Health Referrals data fields                *
.**************************************************************************
.
EDAA0000  MOVE      XEHCP,KEY10
          CALL      RAPMHCP1                * Read the HCP Table
.
          COMPARE   ZERO,OVRCD
          GOTO      EDAA1000 IF EQUAL
          PACK      PRTPT2,XEHCP,PRTPT2
          MOVE      " - Doctor ID not on HCP Table",PRTPT2
          CALL      PRNT0000
. ------- MOVE      ONE,ANERROR
	  MOVE      SP30,PRTPT1
.
EDAA1000  PACK      KEY12,OSTSITE,XECTYP
          CALL      RDCTYA1                 * Read Clinic Type Table
.
          COMPARE   ZERO,OVRCD
          GOTO      EDAA2000 IF EQUAL
          MOVE      " - Clinic Type not on Clinic Type Table",PRTPT2
          PACK      PRTPT2,XECTYP,PRTPT2
          CALL      PRNT0000
. ------- MOVE      ONE,ANERROR
	  MOVE      SP30,PRTPT1
.
EDAA2000  MOVE      SP10,XERHCP
.         MOVE      XERHCP,KEY10
.         CALL      RAPMHCP1                * Read HCP (Referring Doc) Table
.
.         COMPARE   ZERO,OVRCD
.         GOTO      EDAA3000 IF EQUAL
.         MOVE      " - Ref.Doctor ID not on HCP Table",PRTPT2
.         PACK      PRTPT2,XERHCP,PRTPT2
.         CALL      PRNT0000
. ------- MOVE      ONE,ANERROR
	  MOVE      SP30,PRTPT1
.
EDAA3000  PACK      KEY5,LCL,XECOMP
          CALL      RDCODE1                 * Read Codes Table for Cat "CL"
.
          COMPARE   ZERO,OVRCD
          GOTO      EDAA4000 IF EQUAL
          MOVE      " - Claim/Compo Code not in CL Code Table",PRTPT2
          PACK      PRTPT2,XECOMP,PRTPT2
          CALL      PRNT0000
. ------- MOVE      ONE,ANERROR
	  MOVE      SP30,PRTPT1
.
EDAA4000  PACK      KEY5,LTP,XEPRTY
          CALL      RDCODE1                 * Read Codes Table for Cat "TP"
.
          COMPARE   ZERO,OVRCD
          GOTO      EDAA9000 IF EQUAL
          MOVE      " - Priority Code not in TP Code Table",PRTPT2
          PACK      PRTPT2,XEPRTY,PRTPT2
          CALL      PRNT0000
. ------- MOVE      ONE,ANERROR
	  MOVE      SP30,PRTPT1
.
EDAA9000  COMPARE   ZERO,ANERROR
          GOTO      EDAA9999 IF EQUAL
          MOVE      "   ***********   This record will not be loaded",PRTPT2
          CALL      PRNT0000
.
EDAA9999  RETURN
+
.**************************************************************************
.*                               EDBB0000              Called by: PROC0000*
.*                     Edit Outpatients data fields                       *
.**************************************************************************
.
EDBB0000  MOVE      XVACON,KEY10
          CALL      RAPMHCP1                * Read the HCP Table
.
          COMPARE   ZERO,OVRCD
          GOTO      EDBB1000 IF EQUAL
.
.         MOVE      XVACON,DCODE
.         MOVE      XNANAM,DSNAME
.         MOVE      ONE,DRSTAT
.         CALL      WRDOCT1
.
          MOVE      " - Doctor ID not on HCP Table ",PRTPT2
          PACK      PRTPT2,XVACON,PRTPT2
          CALL      PRNT0000
	  MOVE      SP30,PRTPT1
          GOTO      EDBB3000
.
EDBB1000  PACK      KEY12,OSTSITE,XVCLID
          PACK      KEY20,OSTSITE,XVCLID,CURDTE
.
          CALL      RDCLIA1                 * Read/or for Pos.on Partial Key
          COMPARE   ZERO,OVRCD
          GOTO      EDBB3000 IF EQUAL
.
EDBB2100  CALL      RDPCLIA1                * Read Previous with a valid date
          COMPARE   ONE,OVRCD
          GOTO      EDBB2200 IF EQUAL
.
          PACK      CH12,OCASITE,OCACLIN
          MATCH     KEY12,CH12
          GOTO      EDBB2100 IF EQUAL
.
EDBB2200  MOVE      OSTSITE,OCASITE
          MOVE      XVCLID,OCACLIN
          MOVE      CURDTE,OCADATE
          MOVE      SP6,OCADOCT     
          MOVE      XNCNAM,OCADESC
          MOVE      SP6,OCACCONS    
          MOVE      ONE,OTCLIACT
          MOVE      SP4,OCASPARE
          PACK      KEY20,OSTSITE,XVCLID,CURDTE
          CALL      WRCLIA1                 * Add Clinic Id to Table
.
EDBB3000  PACK      KEY5,LCL,XVCCOD
          CALL      RDCODE1                 * Read Codes Table for Cat "CL"
.
          COMPARE   ZERO,OVRCD
          GOTO      EDBB9000 IF EQUAL
          MOVE      " - Claim/Compo Code not in CL Code Table",PRTPT2
          PACK      PRTPT2,XVCCOD,PRTPT2
          CALL      PRNT0000
. ------- MOVE      ONE,ANERROR
	  MOVE      SP30,PRTPT1
.
EDBB9000  COMPARE   ZERO,ANERROR
          GOTO      EDBB9999 IF EQUAL
          CALL      PRNT0000
.
EDBB9999  RETURN
+
.**************************************************************************
.*                               UPAA0000              Called by: PROC0000*
.*              Write Allied Health Ref data to allrefaf and patvisaf     *
.**************************************************************************
.
UPAA0000  CALL      CLALLREF                * Clear all allrefaf variables
.
          MOVE      XEREFN,KEY8                                    
          CALL      RDALREF1                * Check if this record exists
.
          MOVE      XEREFN,ALREVISN
          MOVE      XEVISN,ALREUFR2                                    *I-90202
          MOVE      XEURNO,ALREURNO  
          MOVE      ONE,ALRESTAT 
          MOVE      XERDAT,ALREDACT    
          MOVE      XELINK,ALRELINK
          MOVE      XESRCE,ALRESRCE
          MOVE      XERHCP,ALRERHCP
          MOVE      XERNAM,ALREUFR1                                    *I-90202
          MOVE      XEHCP,ALREHCP
          MOVE      XEUNIT,ALREUNIT
          MOVE      XEPRTY,ALREPRTY
          MOVE      XECTYP,ALRECTYP
          MOVE      XERDAT,ALRERDAT
          MOVE      XERDAT,ALREDREC
. -----   MOVE      XEPRO1,ALREPRO1                                     D-90202
. -----   MOVE      XEPRO2,ALREPRO2                                     D-90202
. -----   MOVE      XEPRO3,ALREPRO3                                     D-90202
          MOVE      XEDIA1,ALREDIA1                                    *I-90202
          MOVE      XEDIA2,ALREDIA2                                    *I-90202
          MOVE      XEDIA3,ALREDIA3                                    *I-90202
          MOVE      XEEDEP,ALREDEPT
          MOVE      XEJOBN,ALREUNO2
          MOVE      XECOMP,ALRECOMP
          MOVE      CURDTE,ALRECDAT
          MOVE      CTIMEIS,ALRECTIM
          MOVE      SP10,ALRECUID
.
          CALL      ARMHI000                * Set to 'Unsent' - MHINC extract
          IF        OVRCD = 0
            CALL      UPALREF1              * Update AHR record
            ADD       ONE,COUNTAUP
          ELSE
            CALL      WRALREF1              * Write new Allied Health Referral
            ADD       ONE,COUNTAWR
          ENDIF
.
. ------- Check for an existing Visit Details & Extension -------
.
          MOVE      XEREFN,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1                 * read the patient visit file
.                                           
          IF        OVRCD = 0
            MOVE      ZERO,NEWVIS
            CALL      RDPMVX11              * read patient extension record
          ELSE
            MOVE      ONE,NEWVIS
.           CALL      CLVIS000
          ENDIF
.                                             Set up a Visit Details record
          MOVE      XEURNO,PVIURNO
          MOVE      XERDAT,PVIDATE
          MOVE      XEREFN,PVIBILL
          MOVE      PVIBILL,DPVIBILL
          MOVE      "9",PVITYPE
          MOVE      XEHCP,PVIDOCT
          MOVE      "2",PVISTAT
          MOVE      " 2",PVISYST
.                                             Set up the Extension record
          MOVE      PVIBILL,PMVXVISN
          MOVE      PVIDATE,PMVXVSDT
          MOVE      XEHCP,PMVXDOCA
          MOVE      XEHCP,PMVXDOCT
          MOVE      XERHCP,PMVXRHC1
          MOVE      ZERO,PMVXPWGT
          MOVE      ZERO,PMVXPHGT
          MOVE      PPOST,PMVXPOST
.
          MOVE      PPOST,D8
          SQUEEZE   D8
          MOVE      D8,F4
          MOVE      F4,D4
          REP       " 0",D4
          PACK      PPOST,D4,SP10
          PACK      KEY56,PPOST,PSUBRB,SP10,PADD4,SP70                *C-240184
          CALL      RDIBPOS1
          IF        OVRCD=0
            MOVE      IBPOASGC,PMVXASGC
          ELSE                                                        *I-240184
            MOVE      SP10,PMVXASGC
            PACK      KEY56,PPOST,PSUBRB,SP70
            CALL      RSIBPOS1
            CALL      RKIBPOS1
            BRANCH    OVRCD,UPAA7000
            MATCH     PPOST,IBPOPCOD
            IF        @EQUAL
              MATCH     PSUBRB,IBPOSUBR
              IF        @EQUAL
                MOVE      IBPOASGC,PMVXASGC
              ENDIF
            ENDIF
          ENDIF                                                 * end *I-240184
.
UPAA7000  COMPARE   ONE,NEWVIS
          IF        @EQUAL
            CALL      WRVISA1               * Write a new Visit Record
            ADD       ONE,COUNTVWR
            MOVE      SP70,PMVXPOEC
            MOVE      SP70,PMVXPILC
            CALL      WRPMVX11
          ELSE
            CALL      UPVISA1               * Update a Visit Record
            ADD       ONE,COUNTVUP
            CALL      UPPMVX11
          ENDIF
.
UPAA9999  RETURN
+
.******************************************************************************
.         MHINC - Add Referral for MH Department and Primary Ref.
.******************************************************************************
ARMHI000  PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,ARMHI999
.
          MATCH     "M",TCINDC4
          GOTO      ARMHI999 IF NOT EQUAL      * Not a Mental Health department
.
          MATCH     "1",ALREUYN4
          GOTO      ARMHI999 IF NOT EQUAL      * Not Primary Referral
.
          MOVE      "0",ALREMOHR               * Unsent
.
ARMHI999  RETURN
+
.**************************************************************************
.*                               UPBB0000              Called by: PROC0000*
.*              Write an Outpatients record to PMSEVTAF and PATVISAF      *
.**************************************************************************
.
UPBB0000  SQUEEZE   XVVISN
          TYPE      XVVISN
          IF        @EQUAL
            MOVE      ZERO,F8
            MOVE      XVVISN,F8               * Convert visit number to a form
            MOVE      F8,KEY8                 
          ELSE
            PACK      KEY8,XVVISN,SP70
          ENDIF
          CALL      RDPMEVT1                * Read the Event Record
 
          MOVE      KEY8,PMEVVISN           * Visit number 
          MOVE      XVEVNT,PMEVEVNT
          MOVE      XVADTE,PMEVADTE
. ------  MOVE      XVATIM,PMEVATIM                                     D-90201
          PACK      PMEVATIM,XVATIM,SECS                               *I-90201
          MOVE      XVACON,PMEVACON
          MOVE      XVUNIT,PMEVUNIT
          MOVE      XVDDTE,PMEVDDTE
. ------  MOVE      XVDTIM,PMEVDTIM                                     D-90201
          PACK      PMEVDTIM,XVDTIM,SECS                               *I-90201
          MOVE      XVCTYP,PMEVCTYP
          MOVE      XVCLID,PMEVCLID
          MOVE      XVCCOD,PMEVCCOD
          MOVE      XVATYP,PMEVATYP
          MOVE      XVASTT,PMEVASTT
          MOVE      XVBTYP,PMEVBTYP
.                                           
          IF        OVRCD = 0
            CALL      UPPMEVT1              * update Event record          
            ADD       ONE,COUNTEUP
          ELSE
            CALL      WRPMEVT1
            ADD       ONE,COUNTEWR
          ENDIF
.
. ------- Check for an existing Visit Details & Extension -------
.
          MOVE      XVVISN,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1                 * read the patient visit file
.                                           
          IF        OVRCD = 0
            MOVE      ZERO,NEWVIS
            CALL      RDPMVX11              * read patient extension record
          ELSE
            MOVE      ONE,NEWVIS
.           CALL      CLVIS000
          ENDIF
.                                             Set up a Visit Details record
          MOVE      XVURNO,PVIURNO
          MOVE      XVADTE,PVIDATE
          MOVE      XVVISN,PVIBILL
          MOVE      PVIBILL,DPVIBILL
          MOVE      "9",PVITYPE
          MOVE      XVACON,PVIDOCT
          MOVE      OSTSITE,PVISITE
          MOVE      "2",PVISTAT
. ------  MOVE      " 3",PVISYST                                       D-90203
          MOVE      " 1",PVISYST                                      *I-90203
.                                             Set up the Extension record
          MOVE      PVIBILL,PMVXVISN
          MOVE      PVIDATE,PMVXVSDT
          MOVE      XVACON,PMVXDOCA
          MOVE      XVACON,PMVXDOCT
          MOVE      SP10,PMVXRHC1
          MOVE      ZERO,PMVXPWGT
          MOVE      ZERO,PMVXPHGT
.
          COMPARE   ONE,NEWVIS
          IF        @EQUAL
            CALL      WRVISA1               * Write a new Visit Record
            ADD       ONE,COUNTVWR
            MOVE      SP70,PMVXPOEC
            MOVE      SP70,PMVXPGID
            MOVE      SP70,PMVXPILC
            CALL      WRPMVX11
          ELSE
            CALL      UPVISA1               * Update a Visit Record
            ADD       ONE,COUNTVUP
            CALL      UPPMVX11
          ENDIF
.
UPBB9999  RETURN
+
.******************************************************************************
.*                                     FERR0000                               *
.*            Print TRAP data errors in "convoutp.txt" & "convahrf.txt"        *
.******************************************************************************
.
FERR0000  MOVE      ONE,DATAERR
          PRINT     *1,"|",XEURNO," Visit ",XEREFN,*37,"| ":
                    "Data error occurred in this record - Record No. :":
                    COUNTIN,"   - Record bypassed",*132,"|"
          READ      AHRFPD,SEQ;XEVISN,XEREFN       * Bypass rest of record
          GOTO      FERR9999
.                   
FERR2000  MOVE      ONE,DATAERR
          PRINT     *1,"|",XVURNO," Event ",XVEVNT,*37,"| ":
                    "Data error occurred in this record - Record No. :":
                    COUNTIN,"   - Record bypassed",*132,"|"
          READ      OUTPATD,SEQ;XVURNO      * Bypass rest of record
.
FERR9999  RETURN
+
.******************************************************************************
.*                                     PRNT0000                               *
.*                                   Print A Line                             *
.******************************************************************************
PRNT0000  ADD       ONE,CLNO
.
          IF        CLNO>=58
            CALL      HEAD0000              * print the page header
          ENDIF
.
          PRINT     *1,"|",PRTPT1,*37,"|",PRTPT2,*132,"|"
          ADD       ONE,COUNTERR
.
PRNT9999  RETURN
.
.******************************************************************************
.*                                    HEAD0000                                *
.*                  Routine to print the error report page header             *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * print the page header
.
          CALL      UND132
          PRINT     *1,"|    U/R No.    Date      Referral  | Error Message":
                    *132,"|"
          CALL      UND132
.       
          MOVE      ONE,CLNO
.
HEAD9999  RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       IBAPOSIO/INC                 * Postcode file        
          INC       PATMA1IO/INC                 * patient master file
          INC       PATVISIO/INC                 * Visit Details
          INC       PMSVX1IO/INC                 * Visit Extension
          INC       PMSEVTIO/INC                 * Patient Event file
          INC       PATDO1IO/INC                 * Patient Doctor Table
          INC       PMSHCPIO/INC                 * Patient Healthcare Profess
          INC       PATCODIO/INC                 * Patient Codes Table
          INC       ALLREFIO/INC                 * Allied Health Referral 
          INC       OUTCTYIO/INC                 * Outpatients Clinic Type
          INC       OUTCLIIO/INC                 * Outpatients Clinic Id
          INC       OUTSITIO/INC                 * Outpatients Site Table :q
          INC       CLALLREF                     * Clear for A/Health referral

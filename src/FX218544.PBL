.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   FX218544                                                    *
.* Desc      :   Fixit to load Cat A DVA service codes into pmsdvaaf         *
.*****************************************************************************
.* Date      :   15/07/1997                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will load Cat A DVA service codes into pmsdvaaf*
.*               and where the first character is an "X", a leading "H" will *
.*               be added.                                                   *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCODFD/INC
          INC       PMSDVAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
.
DIM4      DIM       4
.
.
.
PRGID     INIT      "FX218544"
PRGNAM    INIT      "Load Cat A DVA Service Codes"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsdvaaf";
          OPEN      PMSDVAA1,"pmsdvaaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*         Loop through the Category A codes looking for Nat. HCP Equiv.     *
.*         that is not blank and is not set up for HCP Extract.              *
.*         For each one found, write a new pmsdvaaf record.                  *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Loading codes:";
          MOVE      ZERO,COUNTER                 * initialise counter
.         
          PACK      KEY5,ANSA,SP5
          CALL      RDSCODE1                     * position on Cat A
PROC0500  CALL      RDKCODE1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.         
          MATCH     "A ",TCODE                   * Cat A still ?
          GOTO      PROC9000 IF NOT EQUAL        * no - finished
.         
          DISPLAY   *P16:24,*V2LON,TCODE,ACODE;
.
          MATCH     SP4,PTCDNHCP                 * blank Nat. HCP Equiv. ?
          GOTO      PROC0500 IF EQUAL            * yes - ignore record
.
.         If the Nat. HCP Equiv. is set up for use with the HCP extract,
.         then the value in this field will be "Q" (Qualified Baby).
.
          MOVE      PTCDNHCP,DIM4
          SQUEEZE   DIM4                         * remove spaces
          TYPE      DIM4                         * numeric value ?
          GOTO      PROC1000 IF EQUAL            * yes - treat as DVA Serv. Code
.
          MOVELPTR  DIM4,FORM1                   * get number of characters
          COMPARE   ONE,FORM1                    * 1-character field ?
          IF        @EQUAL
            MATCH     ANSQ,DIM4                  * yes - set up for HCP ?
            GOTO      PROC0500 IF EQUAL          * yes - ignore record
          ENDIF
.
.         The Nat. HCP Equiv. is not set up for HCP extract,
.         so assume this is a DVA Service Code and load up the pmsdvaaf
.         fields and write a new record.
.
PROC1000  MATCH     ANSX,PTCDNHCP                * 1st character "X" ?
          IF        @EQUAL
            PACK    PMDVSERV,ANSH,PTCDNHCP,SP10
          ELSE 
            PACK    PMDVSERV,PTCDNHCP,SP10
          ENDIF     
.         
          PACK      PMDVACAT,ANSA,SP1
          MOVE      ACODE,PMDVACOD
          PACK      PMDVSPAR,SP30,SP20
.         
          PACK      KEY5,PMDVACAT,PMDVACOD       
          CALL      RAPMDVA1
          IF        OVRCD = 1
            ADD       ONE,COUNTER                * increment counter
            CALL      WRPMDVA1                   * write pmsdvaaf record
          ENDIF
.         
          MOVE      SP4,PTCDNHCP                 * clear Nat. HCP Equiv.
          CALL      UPCODE1                      * update record
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:22,*EF,*V2LON,COUNTER,*HOFF," records written to ":
                    "pmsdvaaf":
                    *P1:24,"Fixit completed.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PMSDVAIO/INC

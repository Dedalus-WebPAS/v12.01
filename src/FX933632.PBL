.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX933632                                                  *
.* Desc      :   Write clinician to patecoaf if mandatory and blank        *
.*                                                                         *
.* V11.03.00 21/09/2023 Ebon Clements  TSK 0933632                         *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
          INC       MRTCONFD/INC
          INC       PATCONFD/INC
          INC       OPRDEAFD/INC
          INC       OPRSESFD/INC
          INC       PATECOFD/INC
          INC       PATMI1FD/INC
          INC       PATICOFD/INC
          INC       PATDSCFD/INC
          INC       PMSHCPFD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
COUNT     FORM      8
CURRDATE  DIM       8
DIM15     DIM       15
ENDDDATE  DIM       8
PRNTDATE  DIM       10
PRTODATE  DIM       10       * Display date from
PRFRDATE  DIM       10       * Display date to

STRTDATE  DIM       8
.
PRGID     INIT      "FX933632"
PRGNAM    INIT      "Populate blank patecoaf mandatory clinician"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      DATE0000               * Keyin the date range
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      PATECOA1,"patecoaf"
          OPEN      PATDSCH2,"patdschf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHCPA4,"pmshcpaf"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINTY8;*149,PTCNGDV2
          READ      CONTROLF,TEN;*105,PTCNU50G                         *I-50344
          READ      CONTROLF,HUND05;*104,PTCNDGET,*147,CMDISM,*158,PTCNDGPV:
                                    *173,PTCNICUT:
                                    *217,PTCNGDV4,*227,PTCNGDV5       *C-105244
          READ      CONTROLF,HUND09;*151,PTCNGDV3

          READ      CONTROLF,NINTY7;*245,MRCNDFPR,*173,MRCNOPDT
          CALL      OPICO1
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Update Blank Clinicians":
                    *P1:6,*V2LON,TWO,*HOFF,". Print Only - No Update"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000    MOVE       FALSE,EXIT
            DISPLAY    *P1:10,*EF,"Discharge Date From :":
                           *P1:11,"Discharge Date To   :";
.
            MOVE      "10",CVERT
            MOVE      "23",CCOL
            CALL      IBACLOCK
            PACK      CURRDATE,CCC,CYY,CMM,CDD
            REP       " 0",CURRDATE
            MOVE      CCC,CCENT
            MOVE      CYY,CYEAR
            MOVE      CMM,CMON
            MOVE      CDD,CDAY

            CALL      KEYDATE                          * enter the from date
            PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",STRTDATE                    * from date
.
            MATCH     "20230701",STRTDATE
            IF        @LESS
              DISPLAY    *P1:24,*EL,*B,"Fix can't be run before 1 July 2023":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MOVE       "11",CVERT
            MOVE       "23",CCOL
            CALL      KEYDATE                          * enter the from date
            PACK      ENDDDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",ENDDDATE                    * from date
.
            MATCH     ENDDDATE,CURRDATE
            IF        @LESS
              DISPLAY    *P1:24,*EL,*B,"Fix can't be run for a future date":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MATCH      STRTDATE,ENDDDATE
            GOTO       DATE9999 IF NOT LESS
.
            DISPLAY    *P1:24,*EL,*B,"2nd Range Must Be Greater Than 1st ":
                       "Range - Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       DATE0000
.
DATE9999    RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  MOVE      ZERO,COUNT
          CALL      HEAD0000
.
          PACK      KEY16,STRTDATE,SP70
          CALL      RDSDSCH2
PROC1000  CALL      RDKDSCH2                * Read discharges
          BRANCH    OVRCD,PROC9000
.
          MATCH     DDATE,ENDDDATE          * Exit if after to date
          GOTO      PROC9000 IF LESS
.
          PACK      KEY13,DDADMNO,SP70
          CALL      RSPTECO1
          CALL      RKPTECO1                * Find first procedure
          BRANCH    OVRCD,PROC1000
.
          MATCH     DDADMNO,DPTEOADM        * Same visit
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP70,PTEOCLIN           * Skip visit clinician populated
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      KEY9,PTEOCODE,SP10
          CALL      RDPTICO1                * Read ICD10 operation
          BRANCH    OVRCD,PROC1000
.
          MATCH     "1",PT0ODTMN             * PT0ODTMN 1 = not Mandatory
          GOTO      PROC1000 IF EQUAL        * Skip if clinician not mandatory
.
          ADD       ONE,COUNT
          IF        (COUNT%100)=0
            DISPLAY   *P14:15,*V2LON,PTEOADMN:
                      *P14:16,PTEOEPNO:
                      *P14:17,PTEOCNT:
                      *P14:18,COUNT;
          ENDIF
.
          PACK      KEY31,PTEOADMN,SP70
          CALL      RSOPDEA2
PROC3000  CALL      RKOPDEA2                * Read fist theatre record 
          BRANCH    OVRCD,PROC4000          * for this admission
.
          MATCH     OPDAADMN,DPTEOADM       * correct admission
          GOTO      PROC4000 IF NOT EQUAL
.
          COMPARE   FOUR,OPDASTAT           * Skip cancelled
          GOTO      PROC3000 IF NOT EQUAL
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1                 * Read theatre session
          BRANCH    OVRCD,PROC4000          
.
.         Populate clinician here
.
          MATCH     "1",MRCNDFPR      * Clinician from Theatre Session
          IF        @EQUAL
            MATCH     SP70,OPSECLIN           * Skip blank
            GOTO      PROC4000 IF EQUAL
.
            PACK      KEY10,OPSECLIN,SP70     * Valid HCP
            CALL      RDPMHCP1
            BRANCH    OVRCD,PROC4000
.
            PACK      PTEOCLIN,OPSECLIN,SP70  * Populate clinician
            GOTO      PROC5000
          ENDIF
.
          MATCH     "2",MRCNDFPR      * Clinician from Default Team
          IF        @EQUAL
            MATCH     SP70,OPSEDCC1           * Skip blank
            GOTO      PROC4000 IF EQUAL
.
            PACK      KEY10,OPSEDCC1,SP70     * Valid HCP
            CALL      RDPMHCP1
            BRANCH    OVRCD,PROC4000
.
            PACK      PTEOCLIN,OPSEDCC1,SP70  * Populate clinician
            GOTO      PROC5000
          ENDIF
.
PROC4000  PACK      KEY8,DADMNO,SP70
          CALL      RDMISS1                   * Read admission to get 
          BRANCH    OVRCD,PROC1000            * attending doctor
.
          MATCH     SP70,ADOCTA               * No attending doctor
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY16,ADOCTA,SP70
          CALL      RSPMHCP4                  * Read HCP file by linked
          CALL      RKPMHCP4                  * doctor
          BRANCH    OVRCD,PROC1000
.
          MATCH     ADOCTA,PMHCLDOC           * Correct linked doctor
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      PTEOCLIN,PMHCHCPC,SP70    * Populate clinician
.
PROC5000  IF        COPTION=1
            CALL      UPPTECO1                * Update patecoaf
          ENDIF
.
          CALL      PRNT0000                  * Print patient
.
          GOTO      PROC1000
.
PROC9000  CALL      UND80
.
          PRINT     *N,"End of Report"
          DISPLAY   *P1:24,"Finished ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.*********************************************************************
. Print heading
.*********************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD80
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE         * formatted start date
.
          UNPACK    ENDDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted start date

          PRINT     *20,"Discharges From : ",PRFRDATE," to ",PRTODATE
          ADD       ONE,CLNO
.
          IF        COPTION=1
            PRINT     *20,"Update Records : Yes"
          ELSE
            PRINT     *20,"Update Records : No"
          ENDIF
.
          ADD       ONE,CLNO
.
          CALL      UND80
.
          PRINT     *1,"|Disc. Date",*12,"|Visit ":
                    *21,"|U/R",*30,"|Proc",*40,"|Op Date":
                    *51,"|Clinician",*80,"|"
.
          ADD       ONE,CLNO
.
          CALL      UND80
.
HEAD9999  RETURN
+
.*********************************************************************
. Print line
.*********************************************************************
PRNT0000  UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRNTDATE         * formatted discharge date
.
          UNPACK    PTEODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CPCDATE         * formatted op date
.
          MOVE      PMHCSNAM,DIM15
.
          PRINT     *1,"|",PRNTDATE,*12,"|",DADMNO:
                    *21,"|",DURNO,*30,"|",PTEOCODE:
                    *40,"|",CPCDATE,*51,"|",PTEOCLIN," - ",DIM15,*80,"|"
PRNT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       OPRDEAIO/INC
          INC       OPRSESIO/INC
          INC       PATECOIO/INC
          INC       PATMI1IO/INC
          INC       PATICOIO/INC
          INC       PATDSCIO/INC
          INC       PMSHCPIO/INC
          INC       PMSVX1IO/INC


. ***************************************************************
. *                                                             *
. *                 P R O G R A M  S U M M A R Y                *
. *                 - - - - - - -  - - - - - - -                *
. *                                                             *
. *     Program Name   :  FINDLINK                              *
. *                                                             *
. *     Function       :  Create systable link from DPATH       *
. *                                                             *
. *     Modifications  :                                        *
. *       V9.03.01  09/09/2003  CAR 43237   Mike Laming                       *
. *                 Modify to handle "systables" of either 177 or 561 chars   *
. *                 - change in Infomix SE                                    *
. *                                                                           *
. *                       V5.01 26.05.89 B.G.Ackland (I.B.A.)   *
. *                             Remove Need For Linking systable*
. *                                                             *
. *                       V5.02 26.06.90 B.G.Ackland (I.B.A.)   *
. *                             Remove Need isadd U1-17         *
. *                                                             *
. *                    V5.01.01 02.07.90 H. Crick (I.B.A.)      *
. *                             Recompiled for v5.01            *
. *                                                             *
. *                    V6.00.03 12.03.93 B.G.Ackland (I.B.A.)   *
. *                             *RV on Database                 *
. *                                                             *
. *                    C1.00.01 26.11.94 B.G.Ackland (I.B.A.)   *
. *                             Add Prefix Check Option         *
. *                                                             *
. *                    C2.00.01 03.06.94 B.G.Ackland (I.B.A.)   *
. *                             Option to Change Owner to IBA   *
. *                                                             *
. *                * A T T E N T I O N *                        *
. *                ---------------------                        *
. *        This   program   is  the   property  of              *
. *        Information Builders Australia  Pty Ltd              *
. *        and must not be utilised,  ENQified  or              *
. *        copied in any manner whatsoever without              *
. *        the  express   written   permission  of              *
. *        Information Builders Australia Pty Ltd.              *
. *                                                             *
. ***************************************************************
.
          INC       STD001FD
          INC       SYSTABFD/INC
.
TABLELIN  FILE      ASCII, FIXED=256
.
CHGOWN    DIM       1
LLCNT     FORM      2
LONGRECD  FORM      1                                                  *I-90301
DIM3      DIM       3
PATH      DIM       25
SQLDIR    DIM       40
OPENSYS   DIM       50
LDAT      INIT      "linktab "
LDATEND   INIT      " >tablelin.txt"
COMMAND   DIM       80
.
FILEPRE   DIM       8
PRECHECK  FORM      1
SP17      INIT      "                 "
SP18      INIT      "                  "
SP40      INIT      "                                        "
SP50      INIT      "                                                  "
.
PRGID     INIT      "FINDLINK"
PRGNAM    INIT      "Link Informix Tables According to DPATH"
VERSION   INIT      "V12.01.00"
.
          CALL      DISPHEAD
          MOVE      SP40,SQLDIR
MAIN1000  KEYIN     *P1:3,*EF,*P1:6,*EL,"(Spaces to Exit)":
                    *P1:5,*EL,"Enter Systable Path : ",*V2LON,*RV,SQLDIR
          ENDSET    SQLDIR
          APPEND    SP40,SQLDIR
          RESET     SQLDIR
          MATCH     SP40,SQLDIR
          GOTO      MAIN9999 IF EQUAL
.
          CALL      OPNSYS00     * Open Systables
          BRANCH    OVRCD,MAIN1000
.
MAIN1500  KEYIN     *P23:5,*EL,*V2LON,*DV,SQLDIR,*HOFF,*P65:5,"Ok (":
                    *V2LON,"y",*HOFF,"/",*V2LON,"n",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "N",ANS
          GOTO      MAIN1000 IF EQUAL
          MATCH     "Y",ANS
          GOTO      MAIN1500 IF NOT EQUAL
.
MAIN2000  KEYIN     *P1:9,*EL,"(Spaces for All)":
                    *P1:8,*EL,"File Prefix to Link : ",*V2LON,*RV,FILEPRE
          ENDSET    FILEPRE
          APPEND    SP40,FILEPRE
          RESET     FILEPRE
          MOVE      ZERO,PRECHECK
          MATCH     SP40,FILEPRE
          IF        @EQUAL
            MOVE      ONE,PRECHECK
          ENDIF
          STRIP     FILEPRE
          MOVE      "N",CHGOWN
          KEYIN     *P1:11,*EL,"Change Owner to iba (y/n) ? ",*V2LON,*RV,CHGOWN
          REP       UPPLOW,CHGOWN
          KEYIN     *P1:24,*EL,"Ok to Run (":
                    *V2LON,"y",*HOFF,"/",*V2LON,"n",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "N",ANS
          GOTO      MAIN1000 IF EQUAL
          MATCH     "Y",ANS
          GOTO      MAIN2000 IF NOT EQUAL
.
          PACK      KEY26,SP10,SP10,SP6
          DISPLAY   *P1:23,*EF,"Table      : ":
                    *P1:24,"Linked to  : ";
.
          IF        LONGRECD = 0
            CALL      RSSYTB1               * read 177 byte record
          ELSE                                                         *I-90301
            CALL      RSSYTX1               * read 561 byte record     *I-90301
          ENDIF                                                        *I-90301
.
MAIN3000  MOVE      ZERO,OVRCD                                         *C-90301
          IF        LONGRECD = 0
            CALL      RKSYTB1               * read 177 byte record
          ELSE                                                         *I-90301
            CALL      RKSYTX1               * read 561 byte record     *I-90301
          ENDIF                                                        *I-90301
.
          BRANCH    OVRCD,MAIN9000
.
          MOVE      SYTBNAME,DIM3
          MATCH     "sys",DIM3             * ignore all system files
          GOTO      MAIN3000 IF EQUAL
          BRANCH    PRECHECK,MAIN3500
          MATCH     FILEPRE,SYTBNAME
          GOTO      MAIN3000 IF NOT EQUAL
.
MAIN3500  STRIP     SYTBNAME
          PACK      COMMAND,LDAT,SYTBNAME,LDATEND
          EXECUTE   COMMAND,TASKID
          OPEN      TABLELIN,"tablelin"
.....     READ      TABLELIN,ZERO;SYTBPATH                              D-90301
          READ      TABLELIN,ZERO;SYTBPATX,SYTBPAT2,SYTBPAT3           *I-90301
.
          ENDSET    SYTBPATX                                           *C-90301
          APPEND    SP50,SYTBPATX                                      *C-90301
          RESET     SYTBPATX                                           *C-90301
          MATCH     SP50,SYTBPATX                                      *C-90301
          GOTO      MAIN3000 IF EQUAL
.
          MOVE      SYTBPATX,SYTBPATH                                  *I-90301
          DISPLAY   *P14:23,*V2LON,SYTBNAME:
                    *P14:24,*V2LON,SYTBPATH;
          CLOSE     TABLELIN
          MATCH     "Y",CHGOWN
          IF        @EQUAL
            MOVE      "iba      ",SYTBOWN
          ENDIF
.....     UPDATE    SYSTABLE;SYTBNAME,SYTBOWN,SYTBPATH                  D-90301
          IF        LONGRECD = 0
            CALL      UPSYTB1               * update 177 byte record   *I-90301
          ELSE                                                         *I-90301
            CALL      UPSYTX1               * update 561 byte record   *I-90301
          ENDIF                                                        *I-90301
          GOTO      MAIN3000
.
MAIN9000  KEYIN     *P1:24,*EL,"*** Linking Completed. Tap Enter. ***",ANS
          GOTO      MAIN1000
MAIN9999  CHAIN     PGM
.--------------------------------
. Open Systables
.--------------------------------
OPNSYS00  STRIP     SQLDIR
          CLEAR     OPENSYS
          APPEND    SQLDIR,OPENSYS
          APPEND    "systables",OPENSYS
          RESET     OPENSYS
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      SYSTABLE,OPENSYS
          TRAPCLR   IO
.
.* ****************************************** Start of Addition        *I-90301
          MOVE      ZERO,LONGRECD
          IF        OVRCD = 1
            MOVE      ZERO,OVRCD
            MOVE      ONE,LONGRECD
            TRAP      OVERCOND IF IO
            OPEN      SYSTABLX,OPENSYS
            TRAPCLR   IO
          ENDIF
.* ****************************************** End of Addition          *I-90301
.
          IF        OVRCD=1
            KEYIN     *P1:24,"Could NOT Open ",*DV,OPENSYS," Tap Enter.",ANS
          ENDIF
          RETURN
.
          INC       SYSTABIO/INC
          INC       STD001IO
.

.*****************************************************************************
.* System    :   Inpatient                                                   *
.* Program   :   EADHSXML                                                    *
.* Desc      :   Process an eAdmission change doc loader XML File            *
.*****************************************************************************
.* Date      :   28/03/2013                                                  *
.* Author    :   Saroeun Soeur                                               *
.* Function  :   This program will open an eAdmission XML File and           *
.*               load the details into pmsehbaf and pmsedbaf                 *
.* Mods      :                                                               *
.* V12.01.01     25/11/2025 Nikitha Bhaskar  TSK 0969892                     *
.*               Recompiled for PMSEDBXD                                     *
.* V12.00.02     29/10/2025 Ebon Clements    TSK 0968424                     *
.*               Set value of OBECURNO when eAdmission is not linked to      *
.*               to a visit - RDHD2000                                       *
.* V12.00.01     19/05/2025 Ebon Clements    TSK 0883549                     *
.*               Populate record created time and updated time               *
.* V10.15.01     30/10/2019 Ebon Clements    TSK 0883549                     *
.*               Link the document to the eAdmission if the eAdmission       *
.*               is not linked to a visit.                                   *
.* V10.11.02     18/12/2017 Peter Vela       TSK 0850270                     *
.*               Recompiled for PMSEDBXD                                     *
.* V10.11.01     04.09.2017 Ebon Clements    TSK 0844706                     *
.*               Fixed correspondence id generation before OBSECOFD write    *
.* V10.05.01     14.10.2014 Peter Vela       CAR 301518                      *
.*               Added Clinical Documents audit functionality OBSAUDFD       *
.* v10.04.02     22.04.2014 Patrick Adair    Car 298587                      *
.*               only store history date for type 999 documents              *
.* v10.04.01     06.02.2014 Patrick Adair    Car 293445                      *
.*               amended to allow addition of documents to processed patient *
.*                                           Car 293780                      *
.*               amended to set clinical documents icon flag                 *
.* v10.03.03     01.08.2013 Patrick Adair    Car 289389                      *
.*               recompiled for changes to PMSXMLFD/INC                      *
.* v10.03.02     02.07.2013 Patrick Adair    Car 280425                      *
.*               recompiled to pick up correct OBSECOFD                      *
.* v10.03.01     23.04.2013          SAROEUN SOEUR  280425                   *
.*               changed to work on command line                             *
.* Version   :   v10.03.00                                                   *
.*****************************************************************************
          INC       STD002FD
          INC       PATCONFD/INC
          INC       PATMA1FD/INC
          INC       PMSEDBXD/INC
          INC       PMSEDBFD/INC        *eAdmission pending detail table
          INC       PMSEERFD/INC        *eAdmission pending error table
          INC       PMSEHBFD/INC        *eAdmission pending header table
          INC       TFILEVAR/INC 
          INC       WEBERRFD/INC 
          INC       PMSXMLFD/INC
          INC       OBSAUDFD/INC
          INC       OBSPCOFD/INC
          INC       OBSECOFD/INC
          INC       OBSPCTFD/INC
.
. FILE DESCRIPTION INCLUDES
. -------------------------
FLATFILE  FILE      ASCII,FIXED=4000             * XML File
.
. eAdmission Delimited
. -------------------------
EAHDSTXT  FILE      HL7,FIXED=1000
.
.Name     Type      Size      Start
XMHBHOSP  DIM       3         Hospital (pathspaf)
XMHDOCID  DIM       3         document  id
XMHBEAID  DIM       20        eAdmission ID
XMHBMURN  DIM       8         mrn
XMHDCTID  DIM       3         document type id
XMHDCTYP  DIM       3         document type
XMHLSMDT  DIM       8         last modified date

. LOCAL VARIABLE DEFINITION
. -------------------------
CMDLINE   DIM       200
DATSTRNG  DIM       300
DIM100    DIM       100
F20       FORM      20
LASTCHAR  DIM       1
LINCOUNT  FORM      8
MESSGKEY  INIT      "1                   "
ADMSURNM  INIT      "PatientSurname"
TAGSTRNG  DIM       300
TYPEFLAG  FORM      1             * information type flag
TODAY     DIM       8
DIM8      DIM       8
CWEBCLOC  DIM       4
.                                    0 = start tag value
.                                    1 = end tag value
.                                    2 = data value
SECTION   FORM      2
.
HEADERFN  DIM       8     * Header Temp File Name
DETAILFN  DIM       8     * Deatil Temp File Name
.
FILENAM1  DIM       20
FILENAM2  DIM       20
NWFILENM  DIM       100
TMPSTRNG  DIM       4000
XMLFLNAM  DIM       80
FORM20    FORM      20
.
.
. PROGRAM CONSTANTS
. -----------------
ANSUR     INIT      "UR"
ANS001    INIT      "001"
ENDSTRNG  INIT      ">"
PIPE      INIT      "|"
SEDSTRNG  INIT      "sed 's/<\/[^>]*>/&\n/g'"
TILDA     INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
PDF       INIT      ".pdf"
DASH      INIT      "-"
.
.
PRGID     INIT      "EADHSXML"
PRGNAM    INIT      "eAdmission document history XML load"
VERSION   INIT      "V12.01.01"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      SETX0000
          CALL      INIT0000               * initialisation and open files
.
MAIN0500  CALL      GFIL0000               * get filename 
          BRANCH    EXIT,MAIN9999          * nothing input
.
          CALL      CFIL0000               * copy file
          BRANCH    EXIT,MAIN9999          * unable to copy
.
          CALL      OPEN0000               * open text file
          BRANCH    EXIT,MAIN9999          * unable to open .txt file
.
          CALL      PROC0000               * process
.
MAIN9999  STOP                             * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSXMLA1,"pmsxmlaf"
          OPEN      OBSAUDA1,"obsaudaf"
          OPEN      OBSPCOA1,"obspcoaf"
          OPEN      OBSECOA1,"obsecoaf"
          OPEN      OBSPCTA1,"obspctaf"
          OPEN      PMSEHBA6,"pmsehbaf"
.
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,HEADERFN
          REP       " 0",HEADERFN
.
INIT9999  RETURN
+
.*****************************************************************************
.*                               OPEN0000              Called by: MAIN0000   *
.*            Open the copied ".txt" file prior to processing                *
.*****************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EAHDSTXT,HEADERFN            * open text file
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Header File not found.  ";
            CALL      HITENTER
            GOTO      OPEN9100
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9100  MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9200  DISPLAY   *P1:24,"Invalid Field";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9300  DISPLAY   *P1:24,"Field not found for this message id";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*             Loop through the XML Response file and display the data       *
.*****************************************************************************
.
PROC0000  CALL      RDHD0000
          CLOSE     EAHDSTXT,DELETE        * Delete Temporary File
.
PROC9999  RETURN
+
.*****************************************************************************
.         write eAdmission header to table
.*****************************************************************************
RDHD0000  READ      EAHDSTXT,SEQ;XMHBHOSP,XMHDOCID,XMHBEAID,XMHBMURN:
                                 XMHDCTID,XMHDCTYP,XMHLSMDT
.
          READ      CONTROLF,NINTY1;*219,CWEBCLOC
          PACK      NWFILENM,SP70,SP70
.
RDHD1000  PACK      KEY40,XMHBEAID,SP70
          CALL      RAPMEHB6
RDHD2000  CALL      RKPMEHB6
          BRANCH    OVRCD,RDHD9999
.
          MATCH     PMHBEAID,XMHBEAID
          GOTO      RDHD9999 IF NOT EQUAL
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
          PACK      TODAY,DIM8,SP70
          CLOCK     TIME,CTIMEIS
.
          MATCH     PMHBVISN,SP70
          IF        @EQUAL
                                                  * write to obsecoaf new
            PACK    KEY24,XMHBEAID,TILDA
            CALL    RSOBECO1
            CALL    RPOBECO1
            IF      OVRCD=1
              MOVE    "   1",OBECCPID
            ELSE
              MATCH   XMHBEAID,OBECEAID
              IF      @EQUAL
                MOVE    OBECCPID,FORM4
                ADD     ONE,FORM4
                MOVE    FORM4,OBECCPID
              ELSE
                MOVE    "   1",OBECCPID
              ENDIF
            ENDIF  
. 
            PACK    OBECEAID,XMHBEAID

            MATCH   XMHLSMDT,SP70
            IF      @EQUAL
              PACK    OBECINDT,SP70
            ELSE
              PACK    OBECINDT,XMHLSMDT
            ENDIF
.
            PACK    OBECCTYP,XMHDCTID
            PACK    OBECINTM,CTIMEIS
            PACK    OBECINUS,SP70
            PACK    OBECFEXT,PDF        
            PACK    OBECURNO,PMHBURNO,SP70
            PACK    OBECSLID,CWEBCLOC
.
            CALL    WROBECO1
            REP     " 0",OBECEAID
            REP     " 0",OBECCPID
            PACK    KEY4,CWEBCLOC,SP70
            CALL    RDOBPT1
            STRIP   OBPTSDIR
            PACK    NWFILENM,OBPTSDIR,OBECEAID,DASH,OBECCPID,SP70
            STRIP   NWFILENM
.
            CALL    MVDC0000             * move temp to episodeId+correspodent.pdf
.
            PACK    PMHBLDTY,XMHDCTID
            MATCH   "999",PMHBLDTY
            IF      @EQUAL
              PACK    PMHBLDDT,TODAY
            ENDIF
            PACK    PMHBUDAT,TODAY
            PACK    PMHBUTIM,CTIMEIS
.
            CALL    UPPMEHB6           
.
          ELSE
            PACK    KEY20,PMHBURNO,PMHBVISN,TILDA
            CALL    RSOBPC1
            CALL    RPOBPC1
            IF      OVRCD=1
              MOVE    "   1",OBPCCPID
            ELSE
              MATCH   PMHBURNO,OBPCURNO
              IF      @EQUAL
                MATCH   PMHBVISN,OBPCCVIS
                IF      @EQUAL
                  MOVE    OBPCCPID,FORM4
                  ADD     ONE,FORM4
                  MOVE    FORM4,OBPCCPID
                ELSE
                  MOVE    "   1",OBPCCPID
                ENDIF
              ELSE
                MOVE    "   1",OBPCCPID
              ENDIF
            ENDIF  
. 
            PACK    OBPCURNO,PMHBURNO
            PACK    OBPCCVIS,PMHBVISN,SP70
            PACK    OBPCINDT,XMHLSMDT
            PACK    OBPCCTYP,XMHDCTID
            PACK    OBPCINTM,CTIMEIS
            PACK    OBPCINUS,SP70
            PACK    OBPCFEXT,PDF
            PACK    OBPCMDEL,SP70
            PACK    OBPCSLID,CWEBCLOC
            PACK    OBPCSLEV,SP70
            PACK    OBPCCOM1,SP70
            PACK    OBPCCOM2,SP70
            PACK    OBPCCFRM,SP70
            PACK    OBPCCTOO,SP70
            PACK    OBPCUDF1,SP70
            PACK    OBPCUDF2,SP70
            PACK    OBPCUYN1,SP70
            PACK    OBPCUYN2,SP70
            PACK    OBPCUDD1,SP70
            PACK    OBPCUDD2,SP70
            PACK    OBPCDTLU,DIM8
            PACK    OBPCTMLU,CTIMEIS
            PACK    OBPCLUID,SP70
            PACK    OBPCACAT,SP70
            PACK    OBPCACOD,SP70
            PACK    OBPCEAID,PMHBEAID
.
            CALL    WROBPC1
.
            MOVE    ANSA,OBAUTYPE           * Add
            CALL    OBSAUD00                * Clinical Documents Audit
.
            PACK    KEY24,OBPCEAID,OBPCCPID
            CALL    RAOBECO1
            IF      OVRCD<>1
              GOTO    RDHD3000
            ENDIF
. 
            PACK    OBECEAID,OBPCEAID
            PACK    OBECCPID,OBPCCPID
            PACK    OBECINDT,XMHLSMDT
            PACK    OBECCTYP,XMHDCTID
            PACK    OBECINTM,CTIMEIS
            PACK    OBECINUS,SP70
            PACK    OBECFEXT,PDF
            PACK    OBECSLID,OBPCSLID
            PACK    OBECURNO,OBPCURNO
            PACK    OBECSPAR,SP70
.
            CALL    WROBECO1
.
RDHD3000    REP     " 0",OBPCCVIS
            REP     " 0",OBPCCPID
            PACK    KEY4,CWEBCLOC,SP70
            CALL    RDOBPT1
            STRIP   OBPTSDIR
            PACK    NWFILENM,OBPTSDIR,OBECEAID,DASH,OBECCPID,SP70
            MATCH   OBPCCVIS,SP70
            IF      !@EQUAL      
              PACK    NWFILENM,OBPTSDIR,OBPCCVIS,DASH,OBPCCPID,SP70
            ENDIF
            STRIP   NWFILENM
            CALL    MVDC0000                       * move temp to urn+corepodent.pdf
.
            PACK    PMHBLDTY,XMHDCTID
            MATCH   "999",PMHBLDTY
            IF      @EQUAL
              PACK    PMHBLDDT,TODAY
            ENDIF
            PACK    PMHBUDAT,TODAY
            PACK    PMHBUTIM,CTIMEIS
.
            CALL    UPPMEHB6
.
            PACK      KEY8,PMHBURNO           * validate UR
            CALL      RDMAST1
            IF        OVRCD<>1
              MATCH     "1",PTMXSIN4
              IF        !@EQUAL
                PACK      KEY8,PMHBURNO         * validate UR
                CALL      RLPTMAS1
                IF        OVRCD=0
                  MOVE      "1",PTMXSIN4
                  CALL      UPMAST1
                  CALL      UUPTMAS1
                ENDIF
              ENDIF
            ENDIF
.
          ENDIF
.
RDHD9999  RETURN
.****************************************************************************
.*                                                                          *
.*        MVDC0000                                                          *
.****************************************************************************
MVDC0000  
          CLEAR     CMDLINE
          APPEND    "eadhspdf.us1 ",CMDLINE
          APPEND    HEADERFN,CMDLINE
          APPEND    ".pdf",CMDLINE
          APPEND    " ",CMDLINE
          APPEND    NWFILENM,CMDLINE           * filename 
          APPEND    ".pdf",CMDLINE           * filename 
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MATCH     "0       ",TASKID
          IF        @EQUAL
            MOVE      ZERO,EXIT
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Unable to move pdf. ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
.
MVDC9999  RETURN
+
.*****************************************************************************
.*                          CFIL0000               Called by: MAIN0000       *
.*          Copy the XML file to a ".txt" file so TBL can open it            *
.* Requires: XMLFLNAM - eAdmission XML Response File name                    *
.* Returns : TEMPFILE - temporary ".txt" filename                            *
.*           EXIT  0 = file successfully copied                              *
.*                 1 = unable to copy file                                   *
.*****************************************************************************
.
.
CFIL0000  STRIP     XMLFLNAM
          CLEAR     CMDLINE
          APPEND    "eadhsxml.us1 ",CMDLINE
          APPEND    HEADERFN,CMDLINE
          APPEND    " ",CMDLINE
          APPEND    HEADERFN,CMDLINE
          APPEND    ".pdf ",CMDLINE           * filename 
          APPEND    XMLFLNAM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MATCH     "0       ",TASKID
          IF        @EQUAL
            MOVE      ZERO,EXIT
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Unable to convert XML file.  ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                          GFIL0000               Called by: MAIN0000       *
.*                       Keyin the response file name                        *
.* Returns:  XMLFLNAM - eAdmission XML Response File name                     *
.*           EXIT  0 = valid filename input                                  *
.*                 1 = nothing input                                         *
.*****************************************************************************
.
GFIL0000  KEYIN     *P1:10,*EL,"Filename (including path): ":
                    *P28:10,XMLFLNAM
.
          PACK      XMLFLNAM,XMLFLNAM,SP30
          MATCH     SP30,XMLFLNAM
          GOTO      GFIL9100 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      GFIL9999
.
GFIL9100  MOVE      ONE,EXIT
.
GFIL9999  RETURN
+
.------------------------------------------------------------
. Clinical Documents Audit: Add, Update, View and Delete
. Input - OBAUTYPE (A,U,V,D)
.------------------------------------------------------------
OBSAUD00  MOVE      OBPCURNO,OBAUURNO
          MOVE      OBPCCVIS,OBAUVISN
          MOVE      OBPCCPID,OBAUCORR
          CALL      IBACLOCK
          PACK      OBAUDATE,CCC,CYY,CMM,CDD
          REP       " 0",OBAUDATE
          PACK      OBAUTIME,CTIMEIS
          MOVE      SP70,OBAUWEBU
          MOVE      SP70,OBAUREAS
          PACK      OBAUSPAR,SP70
.
          MATCH     ANSU,OBAUTYPE
          IF        @EQUAL
            MOVE      OBPCUDF1,OBAUREAS
          ENDIF
.
          PACK      KEY47,OBAUURNO,OBAUVISN,OBAUCORR,OBAUDATE:
                          OBAUTIME,OBAUWEBU,OBAUTYPE,SP70
          CALL      RAOBAUD1
          IF        OVRCD=1
            CALL      WROBAUD1
          ENDIF
.
OBSAUD99  RETURN
+
.=========================================================================
. I/O Includes
.=========================================================================
          INC       STD002IO
          INC       TFILENAM     
          INC       PATMA1IO/INC
          INC       PMSEDBXM/INC
          INC       PMSEERIO/INC        *eAdmission pending error table
          INC       WEBERRIO/INC
          INC       PMSXMLIO/INC
          INC       PMSEHBIO/INC
          INC       PMSEDBIO/INC
          INC       OBSAUDIO/INC
          INC       OBSPCOIO/INC
          INC       OBSECOIO/INC
          INC       OBSPCTIO/INC
.

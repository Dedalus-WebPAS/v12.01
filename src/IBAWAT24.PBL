.******************************************************************************
.* System   :  Waiting Lists                                                  *
.* Program  :  IBAWAT24                                                       *
.* Desc     :  Waiting Lists Unit Report                                      *
.******************************************************************************
.* Date     :  11/09/89                                                       *
.* Author   :  S O'Gorman                                                     *
.* Mods     :                                                                 *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.05.01 05/01/2015  Ebon Clements     CAR 261630                  *
.*                  Removed port number from temp file name                   *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*        V5.07.B01 12/01/1999  Nicole Harrington 507 rebounds - Error 33     *
.*                  Fixed printing of dates                                   *
.******************************************************************************
.*        V5.04.02  25/06/1997  Howellsy         505                  *
.*                  Use PATDOCKY instead of PATDOCDS                  *
.*        V5.04.01  11/07/1996  Paul Howells     SRF 641678           *
.*                  fixed the packing of the key before writing to the*
.*                  temp file for option 2.                           *
.**********************************************************************
.*             V5.01.02  18/09/1989 Steve O'Gorman                    *
.*                       Added Reason Diagnosis                       *
.*                       Only allowed Patients with WMSTAT = 1        *
.*                       Get Procedure Description from WATTREAF File *
.*             V5.01.03  29/09/1989 Monika Ohleiter                   *
.*                       Changed to print in date order, check status *
.*                       of records printed SRF - 102463              *
.*             V5.01.04  13/10/1989 Karin Peak                        *
.*                       Added unit/consultant/priority/date sequence *
.*                       SRF 102699                                   *
.*             V5.01.05  26/10/1989 Karin Peak                        *
.*                       Fixed I*D 10402 occurred on 2nd time through *
.*                       SRF 102840                                   *
.*             V5.01.06  30/10/1989 Karin Peak                        *
.*                       Fixed I*L 04755 occurred on 2nd time through *
.*                       SRF 102884                                   *
.*             V5.01.07  31/10/1989 S.Barcham                         *
.*                       Fixed I*D & I*I                              *
.*                       Added D.O.B & Totals                         *
.*                       Reversed Priority                            *
.*                       SRF 102923 , 102940 , 102941                 *
.*             V5.01.08  05.01.1990  D.Di.Paola  SRF 103460           *
.*                       Fixed report so that if no details exist the *
.*                       it will print header details & end of report.*
.*                       Fixed printing of date and page no.          *
.*             V5.01.09  22.02.1990  D.Di.Paola  SRF 104027           *
.*                       Fixed I*D on 2nd option by adding procedure  *
.*                       code as part of the key.                     *
.*             V5.01.10  23/02/90  E.Phan                             *
.*                       Recompiled for WATPROFD.                     *
.*             V5.01.11  02/03/90  E.Phan  SRF 103515                 *
.*                       Fixed I*L in creating/deleting temp file     *
.*             V5.01.12  14.03.90  D.Di.Paola    SRF 104253           *
.*                       Fixed bug in printing date of birth when     *
.*                       comments exist for a patient as it uses diff *
.*                       print routine.......                         *
.*             V5.01.20  06/07/1990 Bill Dew                          *
.*                       Make changes for Wellington : addition of    *
.*                       class field.  I truncated the procedure desc *
.*                       to 27 chars to squeeze class in.  This is the*
.*                       first waiting list program that I didnt have *
.*                       re-write or make major changes unlike the    *
.*                       others.  I tip my hat to the programmers here*
.*                       Thumbs UP. Good work.                        *
.*                       12/07/1990 Bill Dew                          *
.*                       Include pending patients in report           *
.*	      V5.01.21   02/08/1990 Judy Clark                        *
.*                       Changed print of name from title,given name, *
.*                       Surname to Surname,title,given name          *
.*            V5.01.22   15/08/90 Justin Coates                       *
.*                       Changed the printing position of 'Comments'  *
.*            V5.01.121 27/11/90 Glen Hobby                           *
.*                      Recompiled for changes to                     *
.*                      PATMX1FD                                      *
.*            V5.01.122 29/01/1992  Graeme Williams                   *
.*                      Replace WMPCAT with WTWMCLSS on the report    *
.*        V5.01.123 26/05/1994 Ian Rutt                               *
.*                  Fixed Global Recompile Bugs                       * 
.*        V5.03.01  31/10/1995  Steve Armstrong  SRF 640919           *
.*                  Re-wrote keyin routines for unit and consultant   *
.*                                                                    *
.**********************************************************************
.
          INC        STD001FD
.
          INC       IBASEQFD/INC
          INC        PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       TFILEVAR/INC
          INC        WATCONFD/INC
          INC        WATMESFD/INC
          INC        WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WEBERRFD/INC
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
CODETP    INIT      "TP"
CODEWU    INIT      "WU"
.
OPTDESC1  INIT      "by Unit/Cons./Date "
OPTDESC2  INIT      "by Unit/Cons./Pr'ty/Date "
.
HEDDESC1  INIT      "UNIT/CONSULTANT/DATE SEQUENCE           "
HEDDESC2  INIT      "UNIT/CONSULTANT/PRIORITY/DATE SEQUENCE  "
.
LINE65    INIT      "-----------------------------------":
                    "------------------------------"
PIPE      INIT      "|"
.
.
VALDCHRS  INIT      "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ"
+
.**********************************************************************
.                        TEMPFILE VARIABLES
.**********************************************************************
SORT1     INIT       "U1-3,68-73,77-84,4-11,57-65,66-67"
SORT2     INIT       "U1-3,68-73,74-76,77-84,4-11,57-65,66-67"
.
SORTSTR   DIM        50
TEMPFILE  IFILE     SQL, FIXED=178, KEY=SORTSTR
.
. ----- tempfile consists of following vars
.                length     physical   start
.
TEMPUNIT  DIM        3         3          1      unit code
TEMPURNO  DIM        8         8          4      U/R number
TEMPNAME  DIM        45        45        12      patient name for print
TEMPPROC  DIM        9         9         57      procedure code
TEMPCNT   FORM       2         2         66      unique count
TEMPCONS  DIM        6         6         68      doctor code
NEWPRIO   DIM        3         3         74      priority
TEMPLDTE  DIM        8         8         77      wmdate1 ccyymmdd
TEMPDESC  DIM        27        27        85      procedure description
TEMPREAS  DIM        40        40       112      reason for treatment
TEMPDOB   DIM        8         8        152      Date of Birth (ccyymmdd)
TEMPPTY   DIM        3         3        160      priority
TEMPCLSS  DIM        3         3        163
TEMPFILL  DIM        12        12       166      filler
.
. -----                  End of Record  178    -----
.
DTEMPCNT  DIM        2
.
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CMDLINE   DIM        60
CODE      DIM         2
COMCOUNT  FORM        2
CONSDESC  DIM        20
CONSULT   DIM         6
CONTOT    FORM        6
.
DIM3      DIM         3
DIM8      DIM         8
DIM50     DIM        50
DIM55     DIM        55
DOCCODE   DIM         6
.
FIRSTCH   FORM        1
FIRSTREC  FORM        1
FNAME1    DIM         8
FROMDATE  DIM         8
FRSTUNIT  FORM        1
.
GRANDTOT  FORM        6
.
HEDNDESC  DIM        40
.
INDEX     FORM        2
.
.
LINE      FORM        2
.
OPTNDESC  DIM        25
PTCNDCQS  FORM        1
.
RECCOUNT  FORM        4
.
SAVECONS  DIM         6
SAVEUNIT  DIM         3
.
TEMP55    DIM        55
TODATE    DIM         8
TODAY     DIM         8
.
UNIT      DIM         3
UNITDESC  DIM        20
.
PRGID     INIT      "IBAWAT24"
PRGNAM    INIT      "Waiting Lists Unit Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000
.MLQQ      SPLOPEN   "data"
          CALL       INIT0000               * initialization 
.
ML1000    CALL       CLR0000                * Clear Variables
          CALL       SCRN0000               * Main Option Screen
          BRANCH     EXIT,ML9999
ML1100
          CALL      UNIT0000           * Get Unit Range
          BRANCH    EXIT,ML1000
          CALL      CONS0000           * Get Consultant Range
          BRANCH    EXIT,ML1000        * exitchar input
          CALL      DATE0000           * Get Date Range
          CALL      CONT0000           * Ok to Continue (Y/N/C)
          BRANCH    EXIT,ML1100,ML1000
          CALL      PROC0000           * Process Records in wattr1af
          CALL      PRNT0000           * print report
          GOTO      ML1000
ML9999
          CALL      KILL0000           * erase tempfiles
.MLQQQ     SPLCLOSE  "data"
          CHAIN      PGM
.**********************************************************************
.*                           INIT0000                                 *
.**********************************************************************
INIT0000   CALL       DISPHEAD
.
           DISPLAY   *P57:24,"Opening patcodes"
           OPEN      PATCODE1,"patcodes"
.
           DISPLAY   *P65:24,"patdo1af"
           OPEN      PATDO1A1,"patdo1af"
           OPEN      PATDO1A2,"patdo1af"
.
           DISPLAY   *P65:24,"patma1af"
           OPEN      PATMA1A1,"patma1af"
.
           DISPLAY   *P65:24,"patmx1af"
           OPEN      PATMX1A1,"patmx1af"
.
           DISPLAY   *P65:24,"watmesaf"
           OPEN      WATMESA1,"watmesaf"
.
           DISPLAY   *P65:24,"watproaf"
           OPEN      WATPROA1,"watproaf"
.
           DISPLAY   *P65:24,"wattr1af"
           OPEN      WATTR1A3,"wattr1af"
.
           DISPLAY   *P65:24,"controlf"
           OPEN      CONTROLF,"controlf"
.
           READ      CONTROLF,TWENTY1;*247,PTCNDCQS
           READ      CONTROLF,THIRTY7;*121,HREVPRIO,*185,WTCNTPDS
           CLOSE     CONTROLF
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME1
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.**********************************************************************
.*                           CLR0000                                  *
.**********************************************************************
CLR0000   PACK       UNITDESC,SP30,SP30
          PACK       CONSDESC,SP30,SP30
          MOVE       SP6,CONSULT
          MOVE       SP6,TEMPCONS
.
          RETURN
.
.****************************************************************************** 
.*                            SCRN0000                                *
.*                       Main Option Screen                           *
.********************************************************************** 
.
SCRN0000  MOVE       FALSE,EXIT
          CALL       DISPHEAD
          DISPLAY    *P1:3,*EF:
                     *P1:4,*V2LON," 0",*HOFF," = Exit":
                     *P1:5,*V2LON," 1",*HOFF:
                                  " = Unit/Consultant/Date Sequence":
                     *P1:6,*V2LON," 2",*HOFF:
                                  " = Unit/Consultant/Priority/Date Sequence":
                     *P1:8,"Select Option : _";
.
SCRN0100  KEYIN      *P17:8,*EL,*V2LON,OPTION;
.
          COMPARE    ZERO,OPTION
          GOTO       SCRN9000 IF EQUAL
.
          BRANCH     OPTION,SCRN1000,SCRN2000
.
          BEEP
          GOTO       SCRN0100
.
SCRN1000  LOAD       OPTNDESC,OPTION,OPTDESC1,OPTDESC2
          LOAD       HEDNDESC,OPTION,HEDDESC1,HEDDESC2
          MOVE       SORT1,SORTSTR
          PACK       KEY60,PRGNAM,SP1,OPTNDESC
          CALL       DISPLIN2
          GOTO       SCRN9999
.
SCRN2000  LOAD       OPTNDESC,OPTION,OPTDESC1,OPTDESC2
          LOAD       HEDNDESC,OPTION,HEDDESC1,HEDDESC2
          MOVE       SORT2,SORTSTR
          PACK       KEY60,PRGNAM,SP1,OPTNDESC
          CALL       DISPLIN2
          GOTO       SCRN9999
.
SCRN9000  MOVE       TRUE,EXIT
SCRN9999  RETURN
+
.****************************************************************************
.*                                  UNIT0000           Called by: ML0000    *
.*                     get unit code                                        *
.*         Returns: EXIT  0 = OK                                            *
.*                        1 = exitchar entered                              *
.****************************************************************************
.
UNIT0000  MOVE      SP3,UNIT
          KEYIN     *P1:10,*EF,"Unit          : ",*DV,UNDLN3:  * get code
                    *P17:10,*V2LON,UNIT:
                    *P17:10,*DV,UNIT
.
          CALL      CHKU0000                * see what was entered
          BRANCH    EXIT,UNIT1000:          * ? entered
                         UNIT7500:          * valid code
                         UNIT7500:          * nothing entered
                         UNIT9000           * exitchar
.
          GOTO      UNIT0000                * invalid code
.
UNIT1000  MOVE      ZERO,HLEF
          CALL      GETSCR00
          MOVE      CODEWU,CODE
UNIT1200  CALL      PATCODDS                * display codes on file
.
UNIT1500  KEYIN     *P1:24,*EL,"Unit : ",*DV,UNDLN3:
                    *P8:24,*V2LON,UNIT:
                    *P8:24,*DV,UNIT
.
          CALL      CHKU0000                * see what was entered
          BRANCH    EXIT,UNIT1200:          * ? entered
                         UNIT7000:          * valid code
                         UNIT7000:          * nothing entered
                         UNIT8000           * exitchar
.
          GOTO      UNIT1500
.
UNIT7000  CALL      PUTSCR00                * restore screen
          DISPLAY   *P17:10,*V2LON,UNIT
UNIT7500  DISPLAY   *P29:10,UNITDESC
          MOVE      ZERO,EXIT
          GOTO      UNIT9999
.
UNIT8000  CALL      FRESCR00
UNIT9000  MOVE      ONE,EXIT
.
UNIT9999  RETURN
+
.***************************************************************************
.*                                 CHKU0000            Called by: UNIT0000 *
.*                      See what was input                                 *
.*      Returns: EXIT  0 = invalid                                         *
.*                     1 = "?"                                             *
.*                     2 = valid                                           *
.*                     3 = nothing                                         *
.*                     4 = exitchar                                        * 
.***************************************************************************
.
CHKU0000  PACK      UNIT,UNIT,SP3
.
          MATCH     SP3,UNIT                     * anything entered ?
          GOTO      CHKU9000 IF EQUAL            * no
.
          MATCH     EXITCHAR,UNIT                * exitchar entered ?
          GOTO      CHKU7000 IF EQUAL            * yes
.
          MATCH     QUEST,UNIT                   * ? entered ?
          GOTO      CHKU8000 IF EQUAL            * yes
.
.         Code entered
.
          PACK      KEY5,CODEWU,UNIT    
          CALL      RDCODE1                      * code on file ?
          BRANCH    OVRCD,CHKU6000               * no
.
          MOVE      TDESC,UNITDESC
          MOVE      TWO,EXIT                     * code entered
          GOTO      CHKU9999
.
CHKU6000  DISPLAY   *P1:24,*EL,*B,"Unit not on file. ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      CHKU9999
.
CHKU7000  MOVE      FOUR,EXIT
          GOTO      CHKU9999
.
CHKU8000  MOVE      ONE,EXIT
          GOTO      CHKU9999
.
CHKU9000  MOVE      "ALL                       ",UNITDESC
          MOVE      THREE,EXIT
.
CHKU9999  RETURN
+
.****************************************************************************
.*                                  CONS0000           Called by: ML0000    *
.*                     get consultant code                                  *
.*         Returns: EXIT  0 = OK                                            *
.*                        1 = exitchar entered                              *
.****************************************************************************
.
CONS0000  MOVE      SP6,CONSULT
          DISPLAY   *P1:11,*EL,"Consultant    : "
          MOVE      "17",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,CONS7000,CONS9000
.
          MOVE      DCODE,CONSULT
          PACK      KEY6,CONSULT
          CALL      FCON0000                     * format doctor's name
          GOTO      CONS7500
.
CONS7000  MOVE      "ALL                       ",CONSDESC
.
CONS7500  DISPLAY   *P29:11,CONSDESC
          MOVE      ZERO,EXIT
          GOTO      CONS9999
.
CONS9000  MOVE      ONE,EXIT
.
CONS9999  RETURN
+
CHKC0000  PACK      CONSULT,CONSULT,SP6
.
          MATCH     SP6,CONSULT                  * anything entered ?
          GOTO      CHKC9000 IF EQUAL            * no
.
          MATCH     EXITCHAR,CONSULT             * exitchar entered ?
          GOTO      CHKC7000 IF EQUAL            * yes
.
          MATCH     QUEST,CONSULT                * ? entered ?
          GOTO      CHKC8000 IF EQUAL            * yes
.
.         Code entered
.
          PACK      KEY6,CONSULT
          CALL      RDDOCT1                      * code on file ?
          BRANCH    OVRCD,CHKC6000               * no
.
          CALL      FCON0000                     * format doctor's name
.
          MOVE      TWO,EXIT                     * code entered
          GOTO      CHKC9999
.
CHKC6000  DISPLAY   *P1:24,*EL,*B,"Consultant not on file. ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      CHKC9999
.
CHKC7000  MOVE      FOUR,EXIT
          GOTO      CHKC9999
.
CHKC8000  MOVE      ONE,EXIT
          GOTO      CHKC9999
.
CHKC9000  MOVE      "ALL                       ",CONSDESC
          MOVE      THREE,EXIT
.
CHKC9999  RETURN
+
.******************************************************************************
.*        Keyin from / to date                                                *
.*        date on list (=wmdate1) should be between these dates               *
.******************************************************************************
.
DATE0000  DISPLAY    *P1:13,*EF:
                     *P1:13,"From  Date  :":
                     *P1:14,"To    Date  :"
.
          UNPACK     SP6 INTO CYEAR,CMON,CDAY
          MOVE       CCC,CCENT
.
DATE7000  MOVE       TEN5,CCOL
          MOVE       TEN3,CVERT
.
          MOVE       "40",ECOL
          MOVE       CVERT,EVERT
.
          CALL       KEYDATE
          BRANCH     CQUEST,DATE7000
.
          MATCH      SP2,CDAY
          GOTO       DATE7000 IF EQUAL
.
          PACK       FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP        " 0",FROMDATE
          MATCH      FROMDATE,TODAY
          GOTO       DATE8200 IF LESS
.
DATE7500  UNPACK     SP6 INTO CYEAR,CMON,CDAY
.
          MOVE       TEN5,CCOL
          MOVE       TEN4,CVERT
          MOVE       "40",ECOL
          MOVE       CVERT,EVERT
.
          MOVE       CCC,CCENT
.
          CALL       KEYDATE
          BRANCH     CQUEST,DATE7500
.
          MATCH      SP2,CDAY
          GOTO       DATE7500 IF EQUAL
.
          PACK       TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP        " 0",TODATE
.
          MATCH      FROMDATE,TODATE
          GOTO       DATE8000 IF LESS
.
          MATCH      TODATE,TODAY
          GOTO       DATE8100 IF LESS
.
          GOTO       DATE9999
.
DATE8000  DISPLAY   *P1:24,*B,"TO date must not be less than FROM date.  ",*EL;
          CALL      HITENTER
          GOTO      DATE7500
.
DATE8100  DISPLAY   *P1:24,*B,"TO date must not be into the future.  ",*EL;
          CALL      HITENTER
          GOTO      DATE7500
.
DATE8200  DISPLAY   *P1:24,*B,"FROM date must not be into the future.  ",*EL;
          CALL      HITENTER
          GOTO      DATE0000
.
DATE9000  MOVE       TRUE,EXIT
.
DATE9999  RETURN
.
.********************************************************************** 
.*                            CONT0000                                *
.*                  Ok to Continue (Y/N/C) Routine                    *
.********************************************************************** 
.
CONT0000  MOVE       FALSE,EXIT
.
          CALL       CONTQST
.
          MOVE       ANS,FORM1
          BRANCH     FORM1,CONT1000,CONT1000,CONT1000
.
          BEEP
          GOTO       CONT0000
.
CONT1000  LOAD       EXIT,FORM1,ZERO,ONE,TWO
.
          RETURN
.
.********************************************************************** 
.*                            FCON0000                                *
.*                     Format Consultant's Name                       *
.********************************************************************** 
.
FCON0000    CLEAR      CONSDESC
            CALL       RDDOCT1                        * read doctor file
            BRANCH     OVRCD,FCON7000
            MOVE       DGNAME,ANS
            PACK       DIM50,DTITL,SP1,ANS,DOT,SP1,DSNAME
.
            CALL       COMP0000                       * compress blanks  
.           CALL       CASE0000                       * convert to lower case
            MOVE       DIM50,CONSDESC
            GOTO       FCON9999
.
.------ doctors name does not exist ------
.
FCON7000    PACK       CONSDESC,SP30,SP30
.
FCON9999    RETURN
+
.**********************************************************************
.*                              COMP0000                              *
.*       Routine to compress excessive blanks in DIM50                *
.**********************************************************************
COMP0000    MOVE       DIM50,DIM55
            PACK       TEMP55,SP20,SP20,SP10,SP5
            MATCH      TEMP55,DIM55                 * test line for spaces
            GOTO       COMP9999 IF EQUAL
            CLEAR      DIM50
.
COMP1000    CMATCH     SP1,DIM55
            GOTO       COMP5000 IF NOT EQUAL        * compress leading blanks
            BUMP       DIM55 
            GOTO       COMP1000 IF NOT EOS
            GOTO       COMP8000
.
COMP5000    MOVE       DIM55,TEMP55
            MOVE       TEMP55,DIM55
            ENDSET     DIM55
.
COMP5100    CMATCH     SP1,DIM55
            GOTO       COMP7000 IF NOT EQUAL        * compress trailing blanks
            BUMP       DIM55,-1
            GOTO       COMP7000 IF EOS
            GOTO       COMP5100
.
COMP7000    LENSET     DIM55
            RESET      DIM55
.
COMP7100    MOVE       DIM55,ANS
            APPEND     ANS,DIM50
            BUMP       DIM55                        * compress multiple blanks
            GOTO       COMP8000 IF EOS              *   from in-between words
            CMATCH     SP1,DIM55                   
            GOTO       COMP7100 IF NOT EQUAL
            MOVE       DIM55,ANS
            APPEND     ANS,DIM50
.     
COMP7200    BUMP       DIM55
            CMATCH     SP1,DIM55
            GOTO       COMP7100 IF NOT EQUAL
            GOTO       COMP7200
.
COMP8000    RESET      DIM50
            GOTO       COMP9999
.
COMP9999    RETURN
+
.**********************************************************************
.*                             CASE0000                               *
.*  Routine to convert all except first character of each word to     *
.*        lower case                                                  *
.**********************************************************************
CASE0000    MOVE       ONE,FIRSTCH
            RESET      DIM50
.
CASE1000    CMATCH     SP1,DIM50
            GOTO       CASE5000 IF EQUAL            * match char to space
            BRANCH     FIRSTCH,CASE2000
            MOVE       DIM50,ANS
            RESET      UPPLOW
            SCAN       ANS,UPPLOW                   * test to see if char is
            GOTO       CASE1100 IF EQUAL            *   a letter
            MOVE       ONE,FIRSTCH
            GOTO       CASE3000                     * any char but a letter 
.                                                   *   found
.
CASE1100    OR         040,DIM50                    * actual conversion to
.                                                   *   lower case (believe it
.                                                   *   or not!)  
.
CASE2000    MOVE       ZERO,FIRSTCH                 * not on first char any
.                                                   *   more
CASE3000    BUMP       DIM50
            GOTO       CASE1000 IF NOT EOS          * move to next char
            RESET      DIM50                        * end of line reached
            GOTO       CASE9999
.
CASE5000    MOVE       ONE,FIRSTCH                  * first char was a space
            GOTO       CASE3000
.
CASE9999    RETURN
.
.********************************************************************** 
.*                            PROC0000                                *
.*                          Process Data                              *
.    Comment 12/07/1990 Bill Dew : This is nifty code especially the reverse
.    order concerning routine, semi and urgent priority codes.  However
.    PROC could be better if it had MORE comments.  Wellington WANT their
.    pending patients to be printed last.  This will be done by extracting
.    all pending records into a separate tempfile.
.********************************************************************** 
.
PROC0000  CALL       CREA0000               * Create Temp File
.
          MOVE      ZERO,RECCOUNT      * zero record counter 
.
          PACK       KEY27,FROMDATE,SP30 
          CALL       RDSTREA3
PROC1000
          CALL       RDKTREA3
....  DISPLAY   *P1:3,*DBON,"PROC>",WMURNO,WMCODE,WMCNT,"|",WMSTAT,"|",WMDATE1:
....                    "|",WMUNIT,"|",WMDOCTOR,"|",WMPTY,"|"
          BRANCH     OVRCD,PROC9000
.
          DISPLAY    *P1:24,"Scanning ",*V2LON,WMURNO,*HOFF:
                            SP2,WTCNTPDS," ",*V2LON,WMCODE;
.
          MATCH      WMDATE1,TODATE
          GOTO       PROC9000 IF LESS
.
          COMPARE    ONE,WMSTAT             * Only Waiting List Patients
          GOTO       PROC1000 IF NOT EQUAL
.
          MATCH      SP3,UNIT               * All Units Chosen
          GOTO       PROC2000 IF EQUAL
.
          MATCH      UNIT,WMUNIT
          GOTO       PROC1000 IF NOT EQUAL
.
PROC2000  MATCH      SP9,CONSULT            * All Consultants Chosen
          GOTO       PROC3000 IF EQUAL
.
          MATCH      CONSULT,WMDOCTOR
          GOTO       PROC1000 IF NOT EQUAL
.
PROC3000  CALL       FNAM0000               * Format Patient's Name
.
          MOVE       WMUNIT,TEMPUNIT
          MOVE       WMURNO,TEMPURNO
          MOVE       WMCODE,TEMPPROC
          MOVE       WMCNT,TEMPCNT
          MOVE       WMDOCTOR,TEMPCONS
          MOVE       WMPTY,TEMPPTY
          MOVE       WMPTY,NEWPRIO
          MOVE       WMDATE1,TEMPLDTE
          MOVE       WMDESC,TEMPDESC
          MOVE       WMREASON,TEMPREAS
          MOVE       WTWMCLSS,TEMPCLSS
          MOVE       PBDATE,TEMPDOB
          MOVE       SP30,TEMPFILL
.
          BRANCH    OPTION,PROC3400,PROC3600
PROC3400
          PACK      KEY36,TEMPUNIT,TEMPCONS,TEMPLDTE,TEMPURNO,TEMPPROC:
                          TEMPCNT,SP20,SP20
          CALL      WRTEMP1
          ADD       ONE,RECCOUNT
          GOTO      PROC1000
PROC3600
          BRANCH     HREVPRIO,PROC3605 * test for reverse priority
          GOTO       PROC3700
PROC3605
          CLEAR      NEWPRIO           * Reverse Priority codes 
          RESET      TEMPPTY
.
PROC3610  MOVE       TEMPPTY TO ANS
          RESET      VALDCHRS
          SCAN       ANS,VALDCHRS
          GOTO       PROC3650 IF NOT EQUAL
.
          MOVEFPTR   VALDCHRS,FORM2
          MOVE       THIRTY7,INDEX
          SUB        FORM2,INDEX
          RESET      VALDCHRS,INDEX
          MOVE       VALDCHRS,ANS
.
PROC3650  APPEND     ANS,NEWPRIO
          BUMP       TEMPPTY
          GOTO       PROC3610 IF NOT EOS
          RESET      NEWPRIO
PROC3700
          PACK      KEY39,TEMPUNIT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPURNO:
                          TEMPPROC,TEMPCNT,SP20,SP20
.... DISPLAY   *P1:16,*DBON,"PROC> key39 [",KEY39,"]";
.... KEYIN     ANS
          CALL      WRTEMP1
          ADD       ONE,RECCOUNT
          GOTO      PROC1000
.
PROC9000  COMPARE    ZERO,RECCOUNT
          GOTO       PROC9999 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"No Records Found - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
PROC9999  RETURN
.********************************************************************** 
.*                            FNAM0000                                *
.*                   Format the Patient's Name                        *
.********************************************************************** 
.
FNAM0000  CLEAR     TEMPNAME
          PACK      TEMPNAME,SP30,SP30
          MOVE      WMURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,FNAM9999
.
          CLEAR     DIM50
          APPEND    PSNAME,DIM50
          APPEND    SP1,DIM50
          APPEND    COMMA,DIM50
          APPEND    PTITL,DIM50
          APPEND    SP1,DIM50
          APPEND    PGNAME,DIM50
          RESET     DIM50
.
          CALL      COMP0000
          MOVE      DIM50,TEMPNAME
.
FNAM9999  RETURN
.
.********************************************************************** 
.*                            CREA0000                                *
.*                       Create the Temp File                         *
.********************************************************************** 
CREA0000  CALL       KILL0000
          PACK       CMDLINE,SP30,SP30
          CLEAR      CMDLINE
          APPEND     "isbuild ",CMDLINE
          APPEND     FNAME1,CMDLINE
          APPEND     " 178 ",CMDLINE
          APPEND     SORTSTR,CMDLINE
          RESET      CMDLINE
          EXECUTE    CMDLINE,TASKID
          OPEN       TEMPFILE,FNAME1
.
CREA9999  RETURN
.********************************************************************** 
.*                            KILL0000                                *
.*                        Kill the Temp File                          *
.********************************************************************** 
KILL0000  CLOSE      TEMPFILE
          PACK       CMDLINE,SP30,SP30
          CLEAR      CMDLINE
          APPEND     "iserase ",CMDLINE
          APPEND     FNAME1,CMDLINE
          RESET      CMDLINE
          EXECUTE    CMDLINE,TASKID
.
KILL9999  RETURN
.********************************************************************** 
.*                            PRNT0000                                *
.*                   Print Records in Temp File                       *
.********************************************************************** 
PRNT0000
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,GRANDTOT
          CALL      CLRTMPVR           * clear tempfile variables
          LOAD      HEDNDESC,OPTION,HEDDESC1,HEDDESC2
          MOVE      ZERO,CONTOT
          MOVE      "60",LINE
          MOVE      ONE,FRSTUNIT                * set first unit
          MOVE      FALSE,FIRSTREC              * initialise first record flag
          UNPACK    SP30,SAVECONS,SAVEUNIT
.
          BRANCH     OPTION,PRNT0400,PRNT0600
.
PRNT0400  PACK       KEY36,SP30,SP30
          GOTO       PRNT0800
.
PRNT0600  PACK       KEY39,SP30,SP30
.
PRNT0800  CALL       RSTEMP1
PRNT1000  CALL       RKTEMP1
.PRNTQQ1   DISPLAY   *P1:3,"PRNT> eof ",OVRCD,TEMPUNIT,TEMPURNO,TEMPNAME:
.                    TEMPPROC,DTEMPCNT,TEMPCONS;
.PRNTQQK1  KEYIN     ANS
          BRANCH     OVRCD,PRNT9000
.
          MOVE       SP30,TDESC
          MOVE       SP30,DIM8
          PACK       KEY5,CODETP,TEMPPTY
          CALL       RDCODE1
          MOVE       TDESC,DIM8
.
          UNPACK     SP30,CDAY,CMON,CYEAR,CCENT
          UNPACK     TEMPLDTE,CCENT,CYEAR,CMON,CDAY
          UNPACK     TEMPDOB,XCENT,XYEAR,XMON,XDAY
.
          BRANCH     FRSTUNIT,PRNT2000
.
          MATCH      TEMPUNIT,SAVEUNIT
          GOTO       PRNT1500 IF EQUAL
.
          PRINT      *N,*N,"Total Patients : ",CONTOT
          ADD        CONTOT TO GRANDTOT
          MOVE       ZERO TO CONTOT
.
          CALL       HEAD0000
          GOTO       PRNT2000
.
PRNT1500  MATCH      TEMPCONS,SAVECONS
          GOTO       PRNT2000 IF EQUAL
.
          PRINT      *N,*N,"Total Patients : ",CONTOT
          ADD        CONTOT TO GRANDTOT
          MOVE       ZERO TO CONTOT
          CALL       HEAD0000
PRNT2000
          CALL      PREC0000
.
          CALL      COMM0000           * Print Comments for this Patient
.
          CALL      REAS0000           * Print Reason for Procedure
.
          MOVE       ZERO,FRSTUNIT
          MOVE       TEMPUNIT,SAVEUNIT
          MOVE       TEMPCONS,SAVECONS
.
.--------- first record read successful therefore set flag to to true ------
.
          MOVE       TRUE,FIRSTREC          * set first record flag 
.
          GOTO       PRNT1000               * read next record 
.
.--------- end of file, branch on first record flag, if any records exist ----
.
PRNT9000  BRANCH     FIRSTREC,PRNT9999      * "0" = No details on file
.                                           * "1" = Records exist.. continue
.
          CALL       NRPT0000               * print report header
.
.-------- No more records, print totals ------
.
PRNT9999  
          PRINT      *N,*N,"Total Patients : ",CONTOT
          ADD        CONTOT TO GRANDTOT
          RETURN
.********************************************************************** 
.                                 PREC0000  
.    Print a single record
.*******************************************************************************
PREC0000  
          COMPARE    LINE,FIFTY5
          CALL       HEAD0000 IF LESS
.
          PRINT     *N,*1,PIPE,SP1,TEMPURNO:
                    SP1,PIPE,SP1,TEMPNAME:
                    *57,PIPE,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    PIPE,TEMPPROC:
                    PIPE,TEMPDESC:
                    PIPE,DIM8:
                    PIPE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    "| ",TEMPCLSS," |";
.
          ADD        ONE TO CONTOT
          PRINT      *N,ASK,LINE65,LINE65,ASK;
          ADD        TWO,LINE
          DISPLAY    *P1:24,*EL,"Printing ",*V2LON,TEMPURNO;
PREC9999  RETURN
.******************************************************************************
CLRTMPVR
          MOVE       SP30,TEMPUNIT
          MOVE       SP30,TEMPURNO
          MOVE       SP30,TEMPPROC
          MOVE       SP30,TEMPCNT
          MOVE       SP30,TEMPCONS
          MOVE       SP30,TEMPPTY
          MOVE       SP30,NEWPRIO
          MOVE       SP30,TEMPLDTE
          PACK       TEMPDESC,SP30,SP30
          PACK       TEMPREAS,SP30,SP30
          MOVE       SP30,TEMPCLSS
          MOVE       SP30,TEMPDOB
          MOVE       SP30,TEMPFILL
          RETURN
.********************************************************************** 
.*                            HEAD0000                                *
.*                         Report Heading                             *
.********************************************************************** 
HEAD0000  CLOCK    TIME,CTIMEIS               * clock system time
          CALL     IBACLOCK
.
          CALL     HEAD132
          PRINT    *40,HEDNDESC;
.
          MATCH    SP3,UNIT
          GOTO     HEAD1000 IF EQUAL
.
          PRINT    *N,*40,"UNIT       : ",UNIT,SP5,UNITDESC;
          GOTO     HEAD2000
.
HEAD1000  MOVE     SP30,UNITDESC
.
          PACK     KEY5,CODEWU,TEMPUNIT
          CALL     RDCODE1
.
          MOVE     TDESC,UNITDESC
.
          PRINT    *N,*40,"UNIT       : ",TEMPUNIT,SP5,UNITDESC;
.
HEAD2000  PACK     DIM50,SP30,SP30
          MOVE     SP30,CONSDESC
.
          MATCH    SP6,TEMPCONS
          GOTO     HEAD3333 IF EQUAL
          PACK     KEY6,TEMPCONS
          CALL     RDDOCT1
.
          CALL     FCON0000               * Format Consultant's Name
HEAD3333
          PRINT    *N,*N,*1," Consultant : ",TEMPCONS,SP5,CONSDESC 
HEAD4000
          PRINT    ASK,LINE65,LINE65,ASK:
                   *N,*1,PIPE,SP1,"U/R No":
                     *12,PIPE,SP1,"Patient's Name":
                     *57,PIPE,SP1,"D.O.B.":
                     *68,PIPE,WTCNTPDS:   
                     *78,PIPE,WTCNTPDS," Description":
                    *106,"|Priority":
                    *115,"|On List":
                    *126,"|Class|":
                   *N,ASK,LINE65,LINE65,ASK;
.
          MOVE      TEN3,LINE
          RETURN
.********************************************************************** 
.*                            NRPT0000                                *
.*                         Report Heading                             *
.********************************************************************** 
NRPT0000  CLOCK    TIME,CTIMEIS               * clock system time
          CALL     IBACLOCK
.
          CALL     HEAD132
          PRINT    *40,HEDNDESC;
.
          MOVE     SP20,UNITDESC
          MATCH    SP3,UNIT
          GOTO     NRPT1000 IF EQUAL
.
          MOVE     SP30,TDESC
          PACK     KEY5,CODEWU,UNIT
          CALL     RDCODE1
          BRANCH   OVRCD,NRPT1000
          MOVE     TDESC,UNITDESC
.
NRPT1000  PRINT    *N,*40,"UNIT       : ",UNIT,SP5,UNITDESC;
.
          MOVE     SP30,CONSDESC
          MATCH    SP6,CONSULT
          GOTO     NRPT2000 IF EQUAL
.
          PACK     KEY6,CONSULT           * read doctors file
          CALL     RDDOCT1
          BRANCH   OVRCD,NRPT2000
          CALL     FCON0000               * Format Consultant's Name
NRPT2000  
          PRINT    *N,*N,*1," Consultant : ",CONSULT,SP5,CONSDESC 
.
          PRINT    ASK,LINE65,LINE65,ASK:
                   *N,*1,PIPE,SP1,"U/R No":
                     *12,PIPE,SP1,"Patient's Name":
                     *59,PIPE,SP1,"D.O.B.":
                     *68,PIPE,WTCNTPDS:
                     *78,PIPE,WTCNTPDS," Description":
                    *112,PIPE,"Priority":
                    *121,PIPE,"On List":
                    *132,PIPE:
                   *N,ASK,LINE65,LINE65,ASK;
.
          RETURN
.********************************************************************** 
.*                            COMM0000                                *
.*                    Print Comments for Patient                      *
.********************************************************************** 
.
COMM0000  MOVE      ZERO,COMCOUNT
.
          PACK      KEY21,TEMPURNO,TEMPPROC,TEMPCNT
          CALL      RDSMESA1
COMM1000  CALL      RDKMESA1
          BRANCH    OVRCD,COMM9999
.
          MATCH     TEMPURNO,WAURNO
          GOTO      COMM9999 IF NOT EQUAL
.
          MATCH     TEMPPROC,WAPROC
          GOTO      COMM9999 IF NOT EQUAL
.
          COMPARE   TEMPCNT,WACNT
          GOTO      COMM9999 IF NOT EQUAL
.
          COMPARE   LINE,FIFTY5
          GOTO      COMM2000 IF NOT LESS    * will fit on the page
.
          COMPARE   ZERO,COMCOUNT           * Underline already exists
          GOTO      COMM1500 IF EQUAL
.
          PRINT     *N,ASK,LINE65,LINE65,ASK; * underlines at end of page
.
COMM1500  
          CALL      HEAD0000
.
          PRINT     *N,*1,PIPE,SP1,TEMPURNO:
                    SP1,PIPE,SP1,TEMPNAME:
                    PIPE,XDAY,SLASH,XMON,SLASH,XYEAR:
                    PIPE,TEMPPROC:
                    PIPE,TEMPDESC:
                    PIPE,DIM8:
                    PIPE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    "| ",TEMPCLSS," |":
                    *N,ASK,LINE65,LINE65,ASK;
          ADD       TWO,LINE
.
COMM2000  COMPARE   ONE,WACNT2              * is it the first comments line ??
          GOTO      COMM2050 IF NOT EQUAL   * no it isnt
.
. ------ Modified 15/08/90 Justin Coates                ------
. ------ first comments line so print the word comments ------
.
          PRINT     *N,"| Comments : ",*14,WATEXT,*132,PIPE;
          GOTO      COMM2100
.
COMM2050  PRINT     *N,PIPE,SP10,SP2,WATEXT,*132,PIPE;
.
COMM2100  ADD       ONE,LINE
          ADD       ONE,COMCOUNT
.
          GOTO      COMM1000
COMM9999  RETURN
.
.********************************************************************** 
.*                            REAS0000                                *
.*                   Print Reason for Procedure                       *
.********************************************************************** 
.
REAS0000  PRINT     *N,PIPE,*132,PIPE,*N:
                    PIPE,SP10,SP2,"Reason / Diagnosis : ":
                    TEMPREAS,*132,PIPE;
.
          PRINT     *N,ASK,LINE65,LINE65,ASK;
.
          ADD       THREE,LINE
.
          RETURN
+
.********************************************************************** 
.*                   I/O ROUTINES FOR THE TEMP FILE                   *
.**********************************************************************
.
RATEMP1   MOVE      ZERO,OVRCD
          BRANCH    OPTION,RATMP1,RATMP2
.
RATMP1    RESET     KEY36
          READ      TEMPFILE,KEY36;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RATMP2    RESET     KEY39
          READ      TEMPFILE,KEY39;ANS
          GOTO      OVERCOND IF OVER
          RETURN  
.-----------------------------------------
RSTEMP1   MOVE      ZERO,OVRCD
          BRANCH    OPTION,RSTMP1,RSTMP2
.
RSTMP1    RESET     KEY36
          READ      TEMPFILE,KEY36;;
          RETURN  
.
RSTMP2    RESET     KEY39
          READ      TEMPFILE,KEY39;;
          RETURN  
.-----------------------------------------
RDTEMP1   MOVE      ZERO,OVRCD
          BRANCH    OPTION,RDTMP1,RDTMP2
.
RDTMP1    RESET     KEY36
          READ      TEMPFILE,KEY36;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                                   DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                                   TEMPREAS,TEMPDOB,TEMPPTY,TEMPCLSS,TEMPFILL
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPCNT,TEMPCNT
          RETURN  
.
RDTMP2    RESET     KEY39
          READ      TEMPFILE,KEY39;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                                   DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                                   TEMPREAS,TEMPDOB,TEMPPTY,TEMPCLSS,TEMPFILL
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPCNT,TEMPCNT
          RETURN  
.-----------------------------------------
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                             DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                             TEMPREAS,TEMPDOB,TEMPPTY,TEMPCLSS,TEMPFILL
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPCNT,TEMPCNT
          RETURN  
.-----------------------------------------
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMPFILE;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                             DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                             TEMPREAS,TEMPDOB,TEMPPTY,TEMPCLSS,TEMPFILL
          GOTO      OVERCOND IF OVER
          MOVE      DTEMPCNT,TEMPCNT
          RETURN  
.-----------------------------------------
WRTEMP1   BRANCH    OPTION,WRTMP1,WRTMP2
.
WRTMP1    RESET     KEY36
          MOVE      TEMPCNT,DTEMPCNT
          WRITE     TEMPFILE,KEY36;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                                   DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                                   TEMPREAS,TEMPDOB,TEMPPTY,TEMPCLSS,TEMPFILL
          RETURN  
.
WRTMP2    RESET     KEY39
          MOVE      TEMPCNT,DTEMPCNT
          WRITE     TEMPFILE,KEY39;TEMPUNIT,TEMPURNO,TEMPNAME,TEMPPROC:
                                   DTEMPCNT,TEMPCONS,NEWPRIO,TEMPLDTE,TEMPDESC:
                                   TEMPREAS,TEMPDOB,TEMPPTY,TEMPCLSS,TEMPFILL
.
          RETURN  
.-----------------------------------------
DETEMP1   BRANCH    OPTION,DETMP1,DETMP2
.
DETMP1    RESET     KEY36
          DELETE    TEMPFILE,KEY36
          RETURN
.
DETMP2    RESET     KEY39
          DELETE    TEMPFILE,KEY39
          RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC        STD001IO
          INC       PATCODKY
          INC       PATDOCKY
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       WATMESIO/INC
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WEBERRIO/INC

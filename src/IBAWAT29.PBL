.******************************************************************************
.* System    :  WAITING LIST                                                  *
.* Program   :  IBAWAT29                                                      *
.* Desc      :  Scheduled Admissions Report                                   *
.* Function  :  List of patients scheduled for treatment (by doctor, by unit, *
.*              by ward or, by scheduled admission date)                      *
.******************************************************************************
.* Author    :  N.S.                                                          *
.* Date      :  18/10/1988                                                    *
.* Mods.     :                                                                *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.04.01  01/03/2005  Peter Vela    CAR 57547                       *
.*                  Recompiled for PATVADFD and PATVADIO                      *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V9.02.01  24/10/2001  Dean Cramer                                   *
.*                  Make so MediCare No prints.                               * 
.*        V5.08.03  28/09/2000  Tonii                                         *
.*                  Fixed the keyin line for Doctor Code, was displaying 2    *
.*                  lines higher than it was meant to be                      *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                   Fixed global recompile bugs                              *
.******************************************************************************
.*        V5.07.01  16/08/1999 J.Tan                                          *
.*                  Added transfer destination description                    *
.*                  22/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*        V5.04.03  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*        V5.04.02  18/12/1996  Howellsy         SRF 642436                   *
.*                  Use WATCNTCN to derive the number of cancellations        *
.*        V5.04.01  03/10/1996  Howellsy          EOC & ACC                   *
.*                  Check for Unsched to use WMSTAT=1 Not WMBOOK=0            *
.******************************************************************************
.*        V5.00     23/05/1989  MO                                            *
.*                  DIM8, report changes                                      *
.*        V5.01     03/06/1989  S.Barcham         SRF 100809                  *
.*                  Recompiled for locking in PATIOMAS                        *
.*        V5.02     10/06/1989  M.Ohleiter        SRF 101031                  *
.*                  Fixed heading and doctor display                          *
.*        V5.03     15/06/1989  M.Ohleiter        SRF 100926                  *
.*                  Included '?' option on unit and doctor                    *
.*        V5.01.01  19/06/1989  M.Ohleiter                                    *
.*                  Changes for new release 5.01                              *
.*                  09/07/1989  J.Clark                                       *
.*                  Added Key26 variable added to PATIODOC by SOG             *
.*        V5.01.10  31/07/1990  Justin Coates                                 *
.*                  Added two extra report orders - Ward Sequence & Scheduled *
.*                  Admission Date sequence.                                  *
.*                  Also changed temp file structure.                         *
.*                  Changed print routines.                                   *
.*                  Made code format standard.                                *
.*                  24/08/1990  Bill Dew                                      *
.*                  Print Patients booking number since its is a scheduled    *
.*                  admission list summary.  Programmers should pay more      *
.*                  attention to using tempfiles and the method they are      *
.*                  engineered into a program.                                *
.*                  I know the idea of using tempfiles suck but if you        *
.*                  want to do anything useful with TBL, you need them.       *
.*                  Useful tempfile debug routines are PROCQQ1 and PROCQQ2.   *
.*        V5.01.121 25/10/1990  Justin Coates     SRF 106885                  *
.*                  Removed check on current date for FROM and TO dates       *
.*        V5.01.122 07/11/1990  Karin Peak                                    *
.*                  Fixed the spelling of clinic                              *
.*        V5.01.123 14/11/1990  i chung                                       *
.*                  Recompiled for WATCATIO.INC - incorrect Keys size         *
.*        V5.01.124 24/01/1991  Justin Coates                                 *
.*                  Changed to use BOKRC1FD (rather than BOKMASFD)            *
.*        V5.01.125 02/03/93  Neeriem "I Hate Support" Dye                    *
.*                  Reformat print for new address lengths                    *
.*        V5.01.126 27/03/1993  i chung           (Dudley change)             *
.*                  Added user security check                                 *
.*        V5.01.127 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD           * common variables
          INC       PATCOMM/INC        * for 'gperd'
.
          INC       BOKRC1FD/INC       * booking file
          INC       IBASEQFD/INC
          INC       PATCODFD/INC       * codes file
          INC       PATCONFD/INC       * patient control file
          INC       PATDO1FD/INC       * doctor file
          INC       PATDRGFD/INC       * date range file
          INC       PATMA1FD/INC       * patient master file
          INC       PATWR1FD/INC       * ward file
          INC       TFILEVAR/INC
          INC       WATCATFD/INC       * W/L category change file
          INC       WATCONFD/INC       * W/L control file
          INC       WATRTDFD/INC       * Transfer destination file
          INC       WATSEAFD/INC       * W/L security access for ALL file
          INC       WATSEIFD/INC       * W/L security access to individual file
          INC       WATTR1FD/INC       * W/L pat. treatment file
          INC       WATWTAFD/INC       * W/L movement analysis file
          INC       WEBERRFD/INC
+
.
. ------ program variables ------
.
CMDLINE   DIM       50                 * line for execute statement
COL       FORM      2
.
DESCUNIT  DIM       20                 * unit name entered
DESCWARD  DIM       20                 * ward entered description
DIM3      DIM       3                  * PATDOCDS
DOCNAM    DIM       30                 * doctor name display
DOCOPTN   FORM      1                  * used in fdoc
DOCTNAM   DIM       30                 * doctor's name in keyin
.
FNAME1    DIM       8                  * filename of temp file
FROMDATE  DIM       8                  * From Date (ccyymmdd)
.
QUPER     FORM      1                  * ? performed flag
SRTKEY    DIM       30                 * which key to write using
TODATE    DIM       8                  * To Date (ccyymmdd)
TODAY     DIM       8                  * current date
VERT      FORM      2
WATEMP    IFILE     SQL, FIXED=255, KEY=SRTKEY
.
. ------ needed to get the actual code - description at change of unit/doct/ward
.
CODE      DIM       2
DATE20    DIM       10                 * latest booking cancelled date
DATE30    DIM       10                 * WMDATE3 (dd/mm/ccyy)
DATE40    DIM       10                 * Date of Birth (dd/mm/ccyy)
DESCOPT   DIM       33                 * option description in heading
DOCTOR    DIM       6                  * doctor code keyed in
.
DIM7      DIM       7                  * ward part one for print
DIM8      DIM       8                  * date part two for print
DIM10     DIM       10                 * date part two for print
DIM14     DIM       14                 * doct part one for print
DIM26     DIM       26                 * unit part two for print
DIM27     DIM       27                 * date part one for print
DIM31     DIM       31                 * doct part two for print
DIM38     DIM       38                 * ward part two for print
.
KEY21OR   DIM       21                 * booking cancellation original data
KEY21NE   DIM       21                 * booking cancellation read in data 
UNIT      DIM       3                  * unit keyed in
WARD      DIM       3                  * ward keyed in
.
. ------ variables associated with the printout ------
.
CNTPATCL  FORM      5                   * unit/doct/ward patient count
CNTPATTL  FORM      5                   * total patient count
NEEDNP    FORM      "48"                * when need a new page b4 table
PRINTLIM  FORM      "55"                * when need a new page
PREVTYPE  DIM       8                   * used when new clin etc needed
ADDRESS   DIM       50                  * print address
PRCANC    DIM       16                  * print cancellation reason
PRCLIN    DIM       30                  * print unit/clinic
PRDESC    DIM       35                  * print procedure description
PRDOCT    DIM       35                  * print doctor
PRFROM    DIM       42                  * print from date
PRTO      DIM       42                  * print to date
PNAME     DIM       50                  * print name
PRPRTY    DIM       18                  * print priority description
PRSEX     DIM       8                   * print sex male  /female      *C-49016
PRUNDO1   DIM       45                  * current unit/doct/ward
.
. ------ program constants ------
.
ALL       INIT      "ALL"
CATBC     INIT      "BC"               * catagory BC (booking cancellation)
CATTP     INIT      "TP"               * catagory TP (priority)
CATWU     INIT      "WU"               * catagory WU (unit)
.
DASH      INIT      "-"
DESCOPT1  INIT      "Unit/Clinic Code Sequence"
DESCOPT2  INIT      "Doctor Code Sequence"
DESCOPT3  INIT      "Ward Sequence"
DESCOPT4  INIT      "Scheduled Admission Date Sequence"
DOCCODE   DIM        6
.
PREFFROM  INIT      "From Scheduled Admission Date : "
PREFTO    INIT      "To   Scheduled Admission Date : "
PREFUNCL  INIT      "Unit/Clinic Code : "
PREFDOCT  INIT      "Doctor Code : "
PREFWARD  INIT      "Ward : "
PREFDATE  INIT      "Scheduled Admission Date : "
.
SORT1     INIT      "U78-80,70-77,1-8,9-17,18-19" * sort on unit/clinic
SORT2     INIT      "U81-86,70-77,1-8,9-17,18-19" * sort on doctor code
SORT3     INIT      "U87-89,1-8,9-17,18-19"       * sort on ward
SORT4     INIT      "U70-77,119-138,1-8,9-17,18-19" * sort on date/surname
Z8        INIT      "zzzzzzzz"
.
THREE7    FORM      "37"
.
XWTWMHOS  FORM      3
XWTWMPAT  FORM      3
XWTWMOTH  FORM      3
.
PRGID     INIT      "IBAWAT29"
PRGNAM    INIT      "Scheduled Admission List"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
ML0000    CALL      INIT0000                * open files     
.
ML0100    HLINE     *G37,2,52,80            * clear display subheading
          PACK      CPHDROPT,SP20           * clear print subheading
          CALL      OPTN0000                * select options
          BRANCH    EXIT,ML9999             * exit chosen
.
. ---- By Unit or Doctor or Ward or Date -----
.
ML1000    LOAD      DESCOPT,MOPTION,DESCOPT1,DESCOPT2,DESCOPT3,DESCOPT4
          LOAD      CPHDROPT,MOPTION,DESCOPT1,DESCOPT2,DESCOPT3,DESCOPT4
          DISPLAY   *P52:2,*V2LON,"- ",*+,DESCOPT,SP1;
.
ML1500    CALL      KDAT0000                * keyin From/To Date
          BRANCH    EXIT,ML0100             * no From Date entered
.
          CALL      CODE0000                * keyin unit/doctor/ward code
.
          CALL      CONTQST                 * ok to continue ?
          BRANCH    CEXIT,ML2000,ML1500,ML0100
.                          Yes    No    Cancel
.
ML2000    CALL      TEMP0000                * create tempfile
          CALL      CLRV0000                * clear vars patmas,wait,docfd
.
. ------ produce the temp file for the report ------
.
          CALL      PROC0000               * read wattrefd
.
. ------ produce the report ------
.
          CALL      PRTT0000               * produce the report
          GOTO      ML0100
.
ML9999    CHAIN     PGM
+
.***************************************************************************
.*                  INIT0000
.*        Initialisation , opening files                                   *
.***************************************************************************
INIT0000  CALL      DISPHEAD                * display program heading
          CALL      IBACLOCK                * get current date
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THREE7;*211,WTCNWLSC
          READ      CONTROLF,FIFTY9;*220,PTCNUADT
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA2,"watcataf"
.
          IF        WTCNWLSC = 1
            DISPLAY   *P64:24,"watseaaf";
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P64:24,"watseiaf";
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P64:24,"watwtaaf";
          OPEN      WATWTAA1,"watwtaaf"
.
          MOVE      ONE,CNEWDS
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD   * current date
          REP       " 0",TODAY              * zero fill
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME1
.
INIT9999  RETURN
+
.**************************************************************************
.*                  OPTN0000                     Called by : ML0000       *
.*        Display the options and select choice                           *
.*        Returns : MOPTION       the sort sequence                       *
.**************************************************************************
OPTN0000  MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
.
          CALL      SCRO0000                * display main option screen
          DISPLAY   *P1:10,"Select Option : ";
.
OPTN1000  KEYIN     *P17:10,*EL,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION            * was return hit ??
          GOTO      OPTN9000 IF EQUAL       * yes. exit
.
          BRANCH    MOPTION,OPTN8000,OPTN8000,OPTN8000,OPTN8000
          BEEP                              * invalid answer
          GOTO      OPTN1000
.
OPTN8000  MOVE      FALSE,EXIT              * valid answer
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT               * exit chosen
.
OPTN9999  RETURN
+
.******************************************************************************
.*                  SCRO0000                     Called by : OPTN0000         *
.*          Main option screen                                                *
.******************************************************************************
SCRO0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",DESCOPT1:
                    *P1:6,*V2LON,TWO,*HOFF," = ",DESCOPT2:
                    *P1:7,*V2LON,THREE,*HOFF," = ",DESCOPT3:
                    *P1:8,*V2LON,FOUR,*HOFF," = ",DESCOPT4;
SCRO9999  RETURN
+
.******************************************************************************
.*                  KDAT0000                     Called by : ML0000           *
.*        Keyin from / to date                                                *
.*        fromdate < sched.admiss.date = wmdate3 < todate                     *
.******************************************************************************
KDAT0000  DISPLAY   *P1:11,*EF:
                    *P1:13,"From Scheduled Admission Date :":
                    *P1:14,"To   Scheduled Admission Date :";
.
. ------ enter the From Date ------
.
          MOVE      "33",CCOL               * column for input
.
KDAT1000  DISPLAY   *P33:13,*EL,*P33:14,*EF;
          MOVE      TEN3,CVERT              * row for input
          MOVE      "50",ECOL               * error column
          MOVE      CVERT,EVERT
.
kDAT1100  UNPACK    SP6 INTO CYEAR,CMON,CDAY
          MOVE      CCC,CCENT               * default to current century
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT1100
          BRANCH    OVRCD OF KDAT9000       * nothing entered so exit
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          CALL      PACDATE                 * format the date
          PACK      PRFROM,PREFFROM,CPCDATE * set up print options
.
. ------ enter the To Date ------
.
          MOVE      TEN4,CVERT              * row for input
          MOVE      "50",ECOL
          MOVE      CVERT,EVERT
.
KDAT2000  UNPACK    SP6,CYEAR,CMON,CDAY     * no default
          MOVE      CCC,CCENT               * default to current century
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT2000
          BRANCH    OVRCD OF KDAT1000       * no to date hit, re-enter from date
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
. ------ check that To Date >= From Date ------
.
          MATCH     FROMDATE,TODATE         * is To Date < From Date ??
          GOTO      KDAT3000 IF LESS        * yes, give error message
.
          CALL      PACDATE                 * format the date
          PACK      PRTO,PREFTO,CPCDATE     * set up print options
          MOVE      FALSE,EXIT              * valid answer
          GOTO      KDAT9999
.
KDAT3000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Date must be greater than or equal From Date **   ";
          CALL      HITENTER
          GOTO      KDAT2000                * re-enter to date
.
KDAT9000  MOVE      TRUE,EXIT               * exit chosen
.
KDAT9999  RETURN
+
.**************************************************************************
.*                  CODE0000                     Called by : ML0000       *
.*        Keyin unit (option1) or doctor (option 2) or ward (option 3)    *
.*        Returns : UNIT          the unit entered or,                    *
.*                  DOCTOR        the doctor code entered, or             *
.*                  WARD          the ward entered                        *
.**************************************************************************
CODE0000  BRANCH    MOPTION,CODE1000,CODE2000,CODE3000,CODE9999
.
CODE1000  CALL      KUCC0000                * enter the unit/clinic code
          GOTO      CODE9999
.
CODE2000  CALL      KDOC0000                * enter the doctors code
          GOTO      CODE9999
.
CODE3000  CALL      KWRD0000                * enter the ward
          GOTO      CODE9999
.
CODE9999  RETURN                            * when option four chosen
+
.*********************************************************************
.*                  KUCC0000                    Called by : UNCL0000 *
.*        Keyin the Unit/Clinic Code                                 *
.*        Returns : UNIT          the unit/clinic code               *
.*                  DESCUNIT      the unit/clinic description        *
.*********************************************************************
KUCC0000  MOVE      ONE,QUPER               * "?" not performed
          MOVE      TEN7,CVERT              * row for input
          MOVE      "20",CCOL               * column for input
.
          DISPLAY   *P1:17,"Unit/Clinic Code : ";
.
KUCC1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN3,*PCCOL:CVERT,*V2LON,UNIT:
                    *PCCOL:CVERT,*DV,UNIT;
          ENDSET    UNIT
          APPEND    SP3,UNIT
          RESET     UNIT
.
          MATCH     SP3,UNIT                * was anything entered ?
          GOTO      KUCC4000 IF EQUAL       * no
.
          CMATCH    QUEST,UNIT              * was "?" entered ?
          GOTO      KUCC2000 IF NOT EQUAL   * no
.
. ------ "?" entered ------
.
          PACK      CODE,CATWU              * set up category for codes file
          CALL      PATCODDS                * "?" routine
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      "24",CVERT              * input on line 24
          MOVE      "20",CCOL               * input in col 20
          PACK      UNIT,SP3                * clear the code
          DISPLAY   *P1:24,"Unit/Clinic Code : ",*EL;
          GOTO      KUCC1000

. ------ validate what was entered ------
.
KUCC2000  PACK      KEY5,CATWU,UNIT         * key = cat and value entered
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,KUCC3000          * invalid entry
          MOVE      TDESC,DESCUNIT          * set up description
          GOTO      KUCC5000
.
. ------ ERROR: invalid code entered ------
.
KUCC3000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Invalid Unit/Clinic Code **    ";
          CALL      HITENTER
          PACK      UNIT,SP3                * clear the code
.
          BRANCH    QUPER,KUCC1000          * "?" not entered
          DISPLAY   *P1:24,"Unit/Clinic Code : ",*EL;
          GOTO      KUCC1000
.
. ------ only enter hit ------
.
KUCC4000  BRANCH    QUPER,KUCC45000
          CALL      RDIS0000
.
KUCC4500  PACK      UNIT,ALL,SP1       * set up default value
          PACK      DESCUNIT,SP20      * clear description
          GOTO      KUCC8000
.
. ------ valid entry ------
.
KUCC5000  BRANCH    QUPER,KUCC8000     * "?" not entered
          CALL      RDIS0000           * redisplay the screen
.
KUCC8000  DISPLAY   *P1:17,"Unit/Clinic Code : ",*V2LON,UNIT:
                    *P27:17,*HOFF,DESCUNIT;
.
KUCC9999  RETURN
+
.*********************************************************************
.*                  KDOC0000                    Called by : CODE0000 *
.*        Keyin the Doctors Code                                     *
.*        Returns : DOCTOR        the doctor code                    *
.*                  DOCNAM        the doctor description             *
.*********************************************************************
KDOC0000  MOVE      TEN7,CVERT              * row for input
          MOVE      "15",CCOL               * column for input
.
          DISPLAY   *P1:17,"Doctor Code : ";
.
          MOVE      SP10,DOCTOR
          MOVE      "15",ECOL
          MOVE      "17",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KDOC4000,KDOC0000
.
          MOVE      DCODE,DOCTOR
.
KDOC2000  PACK      KEY6,DOCTOR             * key = cat and value entered
          CALL      FDOT0000                * format doctor's name
          PACK      DOCNAM,PACFNAME         * store the name
          GOTO      KDOC8000
.
. ------ only enter hit ------
.
KDOC4000  PACK      DOCTOR,ALL,SP3          * set up default value
          PACK      DOCNAM,SP20             * clear description
          GOTO      KDOC8000
.
. ------ valid entry ------
.
KDOC8000  DISPLAY   *P1:17,"Doctor Code : ",*V2LON,DOCTOR:
                    *P25:17,*HOFF,DOCNAM;
.
KDOC9999  RETURN
+
.*********************************************************************
.*                  KWRD0000                    Called by : CODE3000 *
.*        Keyin the ward code                                        *
.*        Returns : WARD          the ward code                      *
.*                  DESCWARD      the ward description               *
.*********************************************************************
KWRD0000  MOVE      TEN7,CVERT              * row for input
          MOVE      ONE,QUPER               * set "?" as not performed
          DISPLAY   *P1:17,*EL,"Ward : ",*EL;
.
KWRD1000  KEYIN     *P8:CVERT,*DV,UNDLN3,*P8:CVERT,*V2LON,WARD:
                    *P8:CVERT,*V2LON,*DV,WARD;
          RESET     WARD                    * was anything entered ??
          GOTO      KWRD4000 IF EOS         * no
          ENDSET    WARD
          APPEND    SP3,WARD
          RESET     WARD
.
          MATCH     SP3,WARD                * spaces entered ?
          GOTO      KWRD4000 IF EQUAL       * yes
.
          CMATCH    QUEST,WARD              * was "?" entered ??
          GOTO      KWRD2000 IF NOT EQUAL   * no, so validate entry
.
. ------ ? routine ------
.
          CALL      PATWRDDS                * "?" routine on wards file
          MOVE      ZERO,QUPER              * set "?" as performed
          MOVE      "24",CVERT              * input on line 24
          PACK      WARD,SP3                * clear the code
          DISPLAY   *P1:24,"Ward : ",*EL;
          GOTO      KWRD1000
.
. ------ validate what was entered ------
.
KWRD2000  PACK      KEY6,WARD,SP3        
          CALL      RDWARD1                 * read wards file
          BRANCH    OVRCD,KWRD3000          * invalid unit entered
          MOVE      WBDESC,DESCWARD         * set up description
          GOTO      KWRD5000
.
. ------ invalid unit entered ------
.
KWRD3000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Ward **    ";
          CALL      HITENTER
.
          BRANCH    QUPER,KWRD1000          * "?" not entered
          DISPLAY   *P1:24,"Ward : ",*EL;
          GOTO      KWRD1000
.
. ------ only return hit so display all ------
.
KWRD4000  BRANCH    QUPER,KWRD4500
          CALL      RDIS0000
.
KWRD4500  PACK      WARD,ALL,SP1
          PACK      DESCWARD,SP30
          GOTO      KWRD8000
.
. ------ valid entry ------
.
KWRD5000  BRANCH    QUPER,KWRD8000          * "?" not entered
          CALL      RDIS0000                * redisplay the screen
.
KWRD8000  DISPLAY   *P1:17,"Ward : ",*V2LON,WARD,*HOFF,*P15:17,DESCWARD;
.
KWRD9999  RETURN                            * ward input return
+
.*****************************************************************************
.*                  RDIS0000                                                 *
.*        Redisplays the screen after ? option                               *
.*****************************************************************************
RDIS0000  CALL       SCRO0000                    * display main option screen
          DISPLAY    *P1:10,"Select Option : ",*V2LON,MOPTION,*HOFF:
                     *P1:13,"From Scheduled Admission Date : ":
                     *P1:14,"To   Scheduled Admission Date : ";
.
          UNPACK     FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE                     * format the date
          DISPLAY    *P33:13,*V2LON,CPCDATE;     * the From Date
.
          UNPACK     TODATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE
          DISPLAY    *P33:14,*V2LON,CPCDATE;     * the To Date
          RETURN              
+
.**************************************************************************
.*                  TEMP0000                     Called by : ML1000       *
.*        Open tempfile for unit sequence   :                             *
.*                          doctor sequence :                             *
.*                          ward sequence   :                             *
.*                          admiss. date seq:                             *
.*        Kill indexed temp files, and create new indexed tempfiles       *
.**************************************************************************
TEMP0000  DISPLAY   *P1:24,*EL,*V2LON,"Creating Temporary Indexed file.  ":
                    *BLINKON,"Please wait...",*W;
.
. ----- Kill indexed tempfile -----
.
          CLOSE     WATEMP                  * close temp file
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
. ----- Create indexed tempfile -----
.
          LOAD      SRTKEY,MOPTION,SORT1,SORT2,SORT3,SORT4
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 255 ",CMDLINE
          APPEND    SRTKEY,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file
.
          OPEN      WATEMP,FNAME1           * Open temp file
TEMP9999  RETURN
+          
.*****************************************************************************
.*                  CLRV0000                                                 *
.*        Clear PATMA1FD variables                                           *
.*****************************************************************************
CLRV0000  MOVE      SP30,PURNO
          MOVE      SP30,WMURNO
CLRV9999  RETURN
+
.****************************************************************************
.*                  PROC0000                     Called by : ML0000         *
.*        Read WATTR1FD only patients with treatment status =2 are valid    *
.*        Option 1 (=By unit )                                              *
.*        Option 2 (=By doctor)                                             *
.*        Option 3 (=By ward)                                               *
.*        Option 4 (=By date/surname)                                       *
.****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,*V2LON,*P28:24,"***  Extracting Data  ***",*W:
                    *HOFF,*P16:24,*EL,"U/R Number : ",*P47:24,"Scanning : ";
          PACK      KEY19,SP20              * key for positional read
          CALL      RDSTREA1                * positional read
.
. ------ next read on treatment file ------
.
PROC1000  CALL      RDKTREA1                * next read
          BRANCH    OVRCD TO PROC9999       * at end of search
          DISPLAY   *P58:24,*EL,*V2LON,WMURNO;
.
          COMPARE   TWO,WMSTAT              * is the status two ??
          GOTO      PROC1000 IF NOT EQUAL   * status is not 2, read next record
.
          MATCH     SP8,WMDATE3             * is there a date ??
          GOTO      PROC1000 IF EQUAL       * no, so invalid
.
          MATCH     FROMDATE,WMDATE3        * is date less than from date ??
          GOTO      PROC1000 IF LESS        * yes, so invalid
.
          MATCH     WMDATE3,TODATE          * is to date less than date ??
          GOTO      PROC1000 IF LESS        * yes, so invalid
.
          PACK      KEY8,WMURNO             * U/R Number as key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,PROC1000          * invalid UR so ignore
.
. ------ read the booking file using the booking number to get ward ------
.
          PACK      KEY8,WMBOOK,SP8         * booking number as key
          CALL      RDBKREC1                * read booking file
          BRANCH    OVRCD,PROC1000          * invalid booking number
.
. ------ decide which option we are using for extra checks ------
.
          BRANCH    MOPTION,PROC2000,PROC2100,PROC2200,PROC3000
.
. ------ check on unit/clinic code ------
.
PROC2000  MATCH     ALL,UNIT
          GOTO      PROC3000 IF EQUAL       * print all units
.
          MATCH     UNIT,WMUNIT
          GOTO      PROC1000 IF NOT EQUAL   * unit not right
          GOTO      PROC3000                * a valid unit
.
. ------ check on doctors code ------
.
PROC2100  MATCH     ALL,DOCTOR 
          GOTO      PROC3000 IF EQUAL       * print all doctors      
.
          MATCH     DOCTOR,WMDOCTOR
          GOTO      PROC1000 IF NOT EQUAL   * doctor not right
          GOTO      PROC3000                * a valid doctors code
.
. ------ check on ward code ------
.
PROC2200  MATCH     ALL,WARD                * are we using all wards ??
          GOTO      PROC3000 IF EQUAL       * yes, print all wards
.
          MATCH     WARD,BKREWARD           * is it the correct ward ??
          GOTO      PROC1000 IF NOT EQUAL   * ward not correct
.
. ------  check if user has security access to this record  ------
.
PROC3000  COMPARE   ONE,WTCNWLSC
          GOTO      PROC4000 IF NOT EQUAL
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          COMPARE   ZERO,OVRCD
          GOTO      PROC4000 IF EQUAL       * All access
          PACK      KEY13,PASSCODE,WMDOCTOR,WMUNIT
          CALL      RDWTSEI2
          BRANCH    OVRCD,PROC1000          * no access
.
. ------ continue to obtain data ------
.
PROC4000  CALL      WATCNTCN                * obtain booking canc. stats
          MOVE      XWTWMHOS,WTWTHOSP
          MOVE      XWTWMPAT,WTWTPAT
          MOVE      XWTWMOTH,WTWTOTH
.
. ------ write the temp file record ------
.
          BRANCH    MOPTION,PROC5000,PROC5100,PROC5200,PROC5300
.
. ------ write using unit/clinic code key ------
.
PROC5000  PACK      KEY30,WMUNIT,WMDATE3,WMURNO,WMCODE,DWMCNT
          RESET     KEY30
          WRITE     WATEMP,KEY30;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                 WMDOCTOR,BKREWARD,WMPTY,WMSTAY,WMTIME,WMREMOVE:
                                 WMDATE4,WMUDEF2,WTWTHOSP,WTWTPAT,WTWTOTH:
                                 PSNAME,BKREBOOK
          DISPLAY   *P29:24,*V2LON,WMURNO;
          GOTO      PROC1000
.
. ------ write using doctor code key ------
.
PROC5100  PACK      KEY33,WMDOCTOR,WMDATE3,WMURNO,WMCODE,DWMCNT
          RESET     KEY33
          WRITE     WATEMP,KEY33;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                 WMDOCTOR,BKREWARD,WMPTY,WMSTAY,WMTIME,WMREMOVE:
                                 WMDATE4,WMUDEF2,WTWTHOSP,WTWTPAT,WTWTOTH:
                                 PSNAME,BKREBOOK
          DISPLAY   *P29:24,*V2LON,WMURNO;
          GOTO      PROC1000
.
. ------ write using ward code key ------
.
PROC5200  PACK      KEY22,BKREWARD,WMURNO,WMCODE,DWMCNT
          RESET     KEY22
          WRITE     WATEMP,KEY22;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                 WMDOCTOR,BKREWARD,WMPTY,WMSTAY,WMTIME,WMREMOVE:
                                 WMDATE4,WMUDEF2,WTWTHOSP,WTWTPAT,WTWTOTH:
                                 PSNAME,BKREBOOK
          DISPLAY   *P29:24,*V2LON,WMURNO;
          GOTO      PROC1000
.
. ------ write using date/surname code key ------
.
PROC5300  PACK      KEY47,WMDATE3,PSNAME,WMURNO,WMCODE,DWMCNT
          RESET     KEY47
          WRITE     WATEMP,KEY47;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                 WMDOCTOR,BKREWARD,WMPTY,WMSTAY,WMTIME,WMREMOVE:
                                 WMDATE4,WMUDEF2,WTWTHOSP,WTWTPAT,WTWTOTH:
                                 PSNAME,BKREBOOK
          DISPLAY   *P29:24,*V2LON,WMURNO;
          GOTO      PROC1000                * read next record
.
PROC9999  RETURN
+
.****************************************************************************
.*                  PRTT0000                                                 *
.*        Produce the report                                                *
.****************************************************************************
PRTT0000  DISPLAY   *P1:24,*EL,*V2LON,*P27:24,"***  Generating Report  ***",*W:
                    *HOFF,*P26:24,*EL,"Printing U/R Number : "; 
          MOVE      ZERO,CNTPATTL          * clear total count
          MOVE      ZERO,CNTPATCL          * clear progressive count
          MOVE      ZERO,CPAGENO           * initialize page counter
          CALL      IBACLOCK               * get current date and time
.
. ------ do the initial read on the temp file ------
.
PRTT1000  BRANCH    MOPTION,PRTT1100,PRTT1200,PRTT1300,PRTT1400
.
. ------ initial read on unit/clinic index ------
.
PRTT1100  PACK      KEY30,SP30
          READ      WATEMP,KEY30;;          * positional read
          READKS    WATEMP;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT,WMDOCTOR:
                           BKREWARD,WMPTY,WMSTAY,WMTIME,WMREMOVE,WMDATE4:
                           WMUDEF2,WTWTHOSP,WTWTPAT,WTWTOTH,PSNAME,BKREBOOK
          GOTO      PRTT6000 IF OVER
          MOVE      DWMCNT,WMCNT
.
. ------ set up the first units name ------
.
.PRTTQQ1   DISPLAY   *P1:3,"PRTT>",WMURNO,WMCODE,WMCNT," bkrebook ",BKREBOOK;
.PRTTQQK1  KEYIN     ANS
          PACK      KEY5,CATWU,WMUNIT       * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNCL,WMUNIT,SP1,DASH,SP1,TDESC
          PACK      PREVTYPE,WMUNIT,SP5     * set up previous unit
          GOTO      PRTT2000
.
. ------ initial read on doctor code index ------
.
PRTT1200  PACK      KEY33,SP30,SP3
          READ      WATEMP,KEY33;;          * positional read
          READKS    WATEMP;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT,WMDOCTOR:
                           BKREWARD,WMPTY,WMSTAY,WMTIME,WMREMOVE,WMDATE4:
                           WMUDEF2,WTWTHOSP,WTWTPAT,WTWTOTH,PSNAME,BKREBOOK
          GOTO      PRTT6000 IF OVER
          MOVE      DWMCNT,WMCNT
.
. ------ set up the first docts name ------
.
          PACK      KEY6,WMDOCTOR           * doctor as the key
          CALL      FDOT0000                * format doctor's name
          PACK      PRUNDO1,PREFDOCT,WMDOCTOR,SP1,DASH,SP1,PACFNAME
          PACK      PREVTYPE,WMDOCTOR,SP2   * set up previous doct
          GOTO      PRTT2000
.
. ------ initial read on ward index ------
.
PRTT1300  PACK      KEY22,SP20,SP2
          READ      WATEMP,KEY22;;          * positional read
          READKS    WATEMP;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT,WMDOCTOR:
                           BKREWARD,WMPTY,WMSTAY,WMTIME,WMREMOVE,WMDATE4:
                           WMUDEF2,WTWTHOSP,WTWTPAT,WTWTOTH,PSNAME,BKREBOOK
          GOTO      PRTT6000 IF OVER
          MOVE      DWMCNT,WMCNT
.
. ------ set up the first wards name ------
.
          PACK      KEY6,BKREWARD,SP3       * ward as the key
          CALL      RDWARD1                 * read ward file
          PACK      PRUNDO1,PREFWARD,BKREWARD,SP1,DASH,SP1,WBDESC
          PACK      PREVTYPE,BKREWARD,SP5   * set up previous ward
          GOTO      PRTT2000
.
. ------ initial read on date/surname index ------
.
PRTT1400  PACK      KEY47,SP30,SP20
          READ      WATEMP,KEY47;;          * positional read
          READKS    WATEMP;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT,WMDOCTOR:
                           BKREWARD,WMPTY,WMSTAY,WMTIME,WMREMOVE,WMDATE4:
                           WMUDEF2,WTWTHOSP,WTWTPAT,WTWTOTH,PSNAME,BKREBOOK
          GOTO      PRTT6000 IF OVER
          MOVE      DWMCNT,WMCNT
.
. ------ set up the first date ------
.
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      PRUNDO1,PREFDATE,CPCDATE,SP10
          PACK      PREVTYPE,WMDATE3        * set up previous date
          GOTO      PRTT2000
.
. ------ have the first units data so now print the first page heading ------
.
PRTT2000  CALL       NEWP0000               * print a new page
          GOTO       PRTT4000               * print the information
.
. ------------------------------------
. ------ next read on temp file ------
. ------------------------------------
PRTT3000  READKS    WATEMP;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT,WMDOCTOR:
                           BKREWARD,WMPTY,WMSTAY,WMTIME,WMREMOVE,WMDATE4:
                           WMUDEF2,WTWTHOSP,WTWTPAT,WTWTOTH,PSNAME,BKREBOOK
          GOTO      PRTT8000 IF OVER
          MOVE      DWMCNT,WMCNT
          CALL      DIFF0000                * determine if new clinic
.
. ------ obtain the current information to display ------
.
PRTT4000  CALL      FINF0000                * get the ingo
.
. ------ print the line but first see if it will fit on page ------
.
          COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      PRTT5000 IF NOT LESS    * no
          CALL      NEWP0000                * print new page heading
.
PRTT5000  CALL      PPAT0000                * print the line
          GOTO      PRTT3000                * read next record
.
. ------ there is no data obtained for this report ------
.
PRTT6000  CALL      NEWP0000                * print a page heading
          PRINT        *1,"*-----------------------------------------------":
                          "------------------------------------------------":
                          "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values.";
          GOTO      PRTT9000
.
. --------------------------------
. ------ report is finished ------
. --------------------------------
PRTT8000  CALL      DONE0000                * print end of report
.
PRTT9000  PRINT     *N,*N,*N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*V2LON,*P22:24:
                    "***  Report Generation Completed  ***",*W;
.
. ----- Kill indexed tempfile -----
.
          CLOSE     WATEMP                  * close temp file
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      "SCHEDULED ADMISSION LIST  ",PRGNAM  * get back name
PRTT9999  RETURN
+
.*********************************************************************
.*                  FDOT0000                    Called by : lots     *
.*        Obtain the doctor's name and format it                     *
.*        Paras   : KEY6          contains the doctor's code         *
.*        Returns : PACFNAME      doctors name (65 chars)            *
.*********************************************************************
FDOT0000  PACK      PACFNAME,SP30,SP30,SP5  * clear the variable
          CALL      RDDOCT1                 * read doct file
          BRANCH    OVRCD,FDOT9999          * invalid code
          MOVE      DTITL,PACTITLE          * title
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
FDOT9999  RETURN
+
.*********************************************************************
.*                  FINF0000                    Called by : PROC0000 *
.*        Obtain and format all info on patient                      *
.*********************************************************************
FINF0000  PACK      KEY8,WMURNO             * U/R Number as key
          CALL      RDMAST1                 * read master file
          MOVE      PTITL,PACTITLE          * title
          MOVE      PGNAME,PACGNAME         * given name
          MOVE      PSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
          PACK      PNAME,PACFNAME          * store the name
          RESET     PNAME
          CALL      ADDR0000                * format the address
          MOVE      WMDESC,PRDESC           * set up procedure description
.
. ------ format the scheduled admission date ------
.
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE30
.
. ------ format the date of birth ------
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE40
.
. ------ format the priority ------
.
          MOVE      SP20,TDESC              * clear tdesc
          PACK      KEY5,CATTP,WMPTY        * key of priority
          CALL      RDCODE1                 * read codes file
          MOVE      TDESC,PRPRTY            * set up priority
.
. ------ format the doctor's name ------
.
          PACK      KEY6,WMDOCTOR           * doctor's name as key
          CALL      FDOT0000                * get doctor's name
          MOVE      PACFNAME,PRDOCT         * set up doctor's name
.
. ------ format the unit/clinic ------
.
          PACK      KEY5,CATWU,WMUNIT       * key of unit
          CALL      RDCODE1                 * read codes file
          PACK      PRCLIN,WMUNIT,SP1,TDESC
FINF9999  RETURN
+
.**********************************************************************
.*                             ADDR0000                               *
.*                     Pack Patients Address                          *
.**********************************************************************
ADDR0000  PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
          RETURN
+
.*********************************************************************
.*                  DIFF0000                    Called by : PRTT0000 *
.*        see if a new clinic/doct/ward has been found               *
.*********************************************************************
DIFF0000  BRANCH    MOPTION,DIFF1000,DIFF2000,DIFF3000,DIFF4000
.
DIFF1000  MATCH     PREVTYPE,WMUNIT         * is previous same as current ??
          GOTO      DIFF9999 IF EQUAL       * yes, don't need a new heading
.
. ---------------------------------------------------------------
. ------ have a new clinic type (only applies when ALL)    ------
. ------ do underlines for last clinic, give patient count ------
. ------ set up new clinic name, do new table heading      ------
. ------ update the previous clinic with the new one       ------
. ---------------------------------------------------------------
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *N,"Total Patients printed for ",DIM26," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new units details ------
.
          PACK      KEY5,CATWU,WMUNIT       * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNCL,WMUNIT,SP1,DASH,SP1,TDESC * store new unit
          RESET     PRUNDO1
          PACK      PREVTYPE,WMUNIT,SP5     * update previous unit
          GOTO      DIFF8000
.
. ------ check on doctors codes ------
.
DIFF2000  MATCH     PREVTYPE,WMDOCTOR       * is previous same as current ??
          GOTO      DIFF9999 IF EQUAL       * yes, dont need a new heading
.
. ---------------------------------------------------------------
. ------ have a new doctor code (only applies when ALL)    ------
. ------ do underlines for last doctor, give patient count ------
. ------ set up new doctor name, do new table heading      ------
. ------ update the previous doctor with the new one       ------
. ---------------------------------------------------------------
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *N,"Total Patients printed for ",DIM31," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new docts details ------
.
          PACK      KEY6,WMDOCTOR           * doctor as the key
          CALL      FDOT0000                * get doctor's name
          PACK      PRUNDO1,PREFDOCT,WMDOCTOR,SP1,DASH,SP1,PACFNAME
          RESET     PRUNDO1
          PACK      PREVTYPE,WMDOCTOR,SP2   * update previous doct
          GOTO      DIFF8000
.
. ------ check on previous ward ------
.
DIFF3000  MATCH     PREVTYPE,BKREWARD       * is previous same as current ??
          GOTO      DIFF9999 IF EQUAL       * yes, dont need a new heading
.
. ---------------------------------------------------------------
. ------ have a new ward   code (only applies when ALL)    ------
. ------ do underlines for last ward  , give patient count ------
. ------ set up new ward   name, do new table heading      ------
. ------ update the previous ward   with the new one       ------
. ---------------------------------------------------------------
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM7,DIM38     * get the two parts
          PRINT     *N,"Total Patients printed for ",DIM38," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new wards details ------
.
          PACK      KEY6,BKREWARD,SP3         * ward as the key
          CALL      RDWARD1                 * get ward name
          PACK      PRUNDO1,PREFWARD,BKREWARD,SP1,DASH,SP1,WBDESC
          RESET     PRUNDO1
          PACK      PREVTYPE,BKREWARD,SP5     * update previous doct
          GOTO      DIFF8000
.
. ------ check on previous date ------
.
DIFF4000  MATCH     PREVTYPE,WMDATE3        * is previous same as current ??
          GOTO      DIFF9999 IF EQUAL       * yes, dont need a new heading
.
. ---------------------------------------------------------------
. ------ have a new admission date                         ------
. ------ do underlines for last date  , give patient count ------
. ------ set up new admiss date, do new table heading      ------
. ------ update the previous date   with the new one       ------
. ---------------------------------------------------------------
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM27,DIM10,DIM8 * get the date separately
          PRINT     *N,"Total Patients printed for ",DIM10," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new dates details ------
.
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE30
          PACK      PRUNDO1,PREFDATE,DATE30,SP8
          RESET     PRUNDO1
          PACK      PREVTYPE,WMDATE3        * update previous date
.
. ------ see if there is enough room on the page for new table heading ------
.
DIFF8000  COMPARE   CVERT,NEEDNP            * will table fit on page ??
          GOTO      DIFF8500 IF NOT LESS    * yes, print new table heading only
          CALL      NEWP0000                * print new page and table heading
          GOTO      DIFF9999
.
DIFF8500  CALL      PTBH0000                * print new table
.
DIFF9999  RETURN
+
.*********************************************************************
.*                  NEWP0000                    Called by : PRTT0000 *
.*        Prints the new page heading for a report          DORP0000 *
.*        Para's  : CPHRDOPT      sub heading                        *
.*********************************************************************
NEWP0000  MOVE      ZERO,CNOUNDLN           * underlines at end of page
          MOVE      "SCHEDULED ADMISSION LIST -",PRGNAM
          RESET     PRGNAM
.
          CALL      HEAD132                 * do the new heading
          PRINT     *1,PRFROM               * From Date, search type
          PRINT     *1,PRTO                 * To Date
.
. ------ print the table heading ------
.
          MOVE      FIVE,CVERT               * set line counter
          CALL      PTBH0000                * print a table heading
NEWP9999  RETURN
+
.*********************************************************************
.*                  PPAT0000                    Called by : PRTT0000 *
.*        Prints a single data line of the report           DORP0000 *
.*        ie. the information about a patients procedure             *
.*        Updates : CNTPATCL      count of pats                      *
.*                  CNTPATTL      count of total patients            *
.*                  CVERT         current row number                 *
.*********************************************************************
PPAT0000  PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,"|",WMURNO,*11,PNAME,*61,"|",PRDESC:
                   *97,"|Scheduled : ",DATE30,*124,BKREBOOK,*132,"|"
.
. -------------------------------------------
. ------ print line two about patient ------
. -------------------------------------------
          PRINT      *1,"|",*11,ADDRESS,*61,"|",PRDOCT:
                    *97,"|Priority  : ",WMPTY,SP1,PRPRTY,*132,"|"
.
. -------------------------------------------
. ------ print line three about patient ------
. -------------------------------------------
          PRINT      *1,"|",*11,"Home ",PTELEP,SP1,"Bus ",PTELEB:
                    *61,"|Unit : ",PRCLIN,*97,"|";
.
. ------ see if there is a booking cancellation reason ------
.
          MATCH     SP3,WMREMOVE            * is there a booking canc. reason ??
          GOTO      PPAT1000 IF EQUAL       * no, don't display anything
.
. ------ display the stats on booking cancellations ------
.
          PRINT     "Cancel  : Hos=",WTWTHOSP," Pat=",WTWTPAT:
                    " Oth=",WTWTOTH;
          GOTO      PPAT1000
.
PPAT1000  PRINT     *132,"|"
.
. -------------------------------------------
. ------ print line four about patient ------
. -------------------------------------------
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,PRSEX
.* ******************************************   End of Changes         *C-49016
.
PPAT2000  PRINT      *1,"|",*11,"Sex: ",PRSEX,*28,"D.O.B.: ",DATE40:
                    *47,"M/C:",PMEDI,*61,"|Stay :",WMSTAY," Days Duration ":
                    WMTIME," Min|";
.
. ------ determine if a there is a latest booking cancellation ------
.
          MATCH     SP3,WMREMOVE            * is there a booking canc ??
          GOTO      PPAT3000 IF EQUAL       * no.
.
          UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * format the cancellation date
          MOVE      CPCDATE,DATE20          * set up the date
          MOVE      SP20,TDESC              * default if read fails
          PACK      KEY5,CATBC,WMREMOVE     * key of booking canc reason
          CALL      RDCODE1                 * read codes file
          MOVE      TDESC,PRCANC            * set up reason
.
          PACK      KEY19,WMURNO,WMCODE,DWMCNT
          PROC      DTRNDEST               * to get the transfer destination
          MOVE      TDESC,PRCANC
.
          PRINT     "Latest  : ",DATE30," ",PRCANC;
.
PPAT3000  PRINT     *132,"|"
          DISPLAY   *P70:24,WMURNO;         * display U/R Number to show working
.
. ------ increment the counters ------
.
          ADD       FIVE,CVERT              * add to line counter
          ADD       ONE,CNTPATCL            * add to patient count for clinic
          ADD       ONE,CNTPATTL            * add to total pat. count
.
PPAT9999  RETURN
+
.*********************************************************************
.*                  PTBH0000                    Called by : NEWP0000 *
.*        Prints a new table heading                        PRTT0000 *
.*        Needed at a new page or for a new unit/clinic     DORP0000 *
.*        Paras/Returns : CVERT      current row number              *
.*********************************************************************
PTBH0000  PRINT     *N,PRUNDO1:
                    *N,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*1,"| U/R No ",*11,"Personal Details ":
                      *61,"|Medical Details ",*97,"|Waiting List Details":
                     *124,"Book No.",*132,"|"
          ADD       FOUR,CVERT              * add to line count
PTBH9999  RETURN
+
.*********************************************************************
.*                  DONE0000                    Called by : PRTT0000 *
.*        Finish off the report                                      *
.*********************************************************************
DONE0000  BRANCH    MOPTION,DONE1000,DONE2000,DONE3000,DONE4000
.
. ------ finish for unit/clinic ------
.
DONE1000  UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients printed for ",DIM26," : ",CNTPATCL:
                    *N,*N,"Grand Total Patients printed ",SP20,SP4," : ":
                    CNTPATTL
          GOTO      DONE9999
.
. ------ finish for doctor code ------
.
DONE2000  UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients printed for ",DIM31," : ",CNTPATCL:
                    *N,*N,"Grand Total Patients printed ",SP20,SP9," : ":
                    CNTPATTL
          GOTO      DONE9999
.
. ------ finish for ward ------
.
DONE3000  UNPACK    PRUNDO1,DIM7,DIM38      * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients printed for ",DIM38," : ",CNTPATCL:
                    *N,*N,"Grand Total Patients printed ",SP30,SP6," : ":
                    CNTPATTL
          GOTO      DONE9999
.
. ------ finish for date/surname ------
.
DONE4000  UNPACK    PRUNDO1,DIM27,DIM10,DIM8 * get the date separately
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients printed for ",DIM10," : ",CNTPATCL:
                    *N,*N,"Grand Total Patients printed ",SP8," : ":
                    CNTPATTL
.
DONE9999  RETURN
+
.******************************************************************************
PROCQQ1   DISPLAY   *P1:2,"Data after write ":
                    *P1:3,"WMURNO  [",WMURNO,"]":
                    *P1:4,"WMCODE  [",WMCODE,"]":
                    *P1:5,"DWMCNT  [",DWMCNT,"]":
                    *P1:6,"WMDESC  [",WMDESC,"]":
                    *P1:7,"WMDATE3 [",WMDATE3,"]":
                    *P1:8,"WMUNIT  [",WMUNIT,"]":
                    *P1:9,"WMDOCTOR[",WMDOCTOR,"]":
                    *P1:10,"BKREWARD  [",BKREWARD,"]":
                    *P1:11,"WMPTY   [",WMPTY,"]":
                    *P1:12,"WMTIME  [",WMTIME,"]":
                    *P1:13,"WMREMOVE[",WMREMOVE,"]":
                    *P1:14,"WMDATE4 [",WMDATE4,"]":
                    *P1:15,"WMUDEF2 [",WMUDEF2,"]":
                    *P1:16,"WTWTHOSP[",WTWTHOSP,"]":
                    *P1:17,"WTWTPAT [",WTWTPAT,"]":
                    *P1:18,"PSNAME  [",PSNAME,"]":
                    *P1:19,"BKREBOOK  [",BKREBOOK,"]";
          RETURN
.
PROCQQ2   DISPLAY   *P40:2,"Data after read":
                    *P40:3,"WMURNO  [",WMURNO,"]":
                    *P40:4,"WMCODE  [",WMCODE,"]":
                    *P40:5,"DWMCNT  [",DWMCNT,"]":
                    *P40:6,"WMDESC  [",WMDESC,"]":
                    *P40:7,"WMDATE3 [",WMDATE3,"]":
                    *P40:8,"WMUNIT  [",WMUNIT,"]":
                    *P40:9,"WMDOCTOR[",WMDOCTOR,"]":
                    *P40:10,"BKREWARD  [",BKREWARD,"]":
                    *P40:11,"WMPTY   [",WMPTY,"]":
                    *P40:12,"WMTIME  [",WMTIME,"]":
                    *P40:13,"WMREMOVE[",WMREMOVE,"]":
                    *P40:14,"WMDATE4 [",WMDATE4,"]":
                    *P40:15,"WMUDEF2 [",WMUDEF2,"]":
                    *P40:16,"WTWTHOSP[",WTWTHOSP,"]":
                    *P40:17,"WTWTPAT [",WTWTPAT,"]":
                    *P40:18,"PSNAME  [",PSNAME,"]":
                    *P40:19,"BKREBOOK  [",BKREBOOK,"]";
PROCQQK1  KEYIN     ANS
          RETURN
+
.*****************************************************************************
.
          INC       STD001IO       
          INC       PATCOMN3           * for 'gperd'
.
          INC       PATCODKY           * "?" routine on codes file
          INC       PATDOCKY           * "?" routine on doctors file
          INC       PATWRDDS           * "?" routine on wards file
          INC       TRANDEST           * Transfer destination
          INC       TFILENAM
          INC       SEXDSCIO
.
          INC       BOKRC1IO/INC       * booking file
          INC       IBASEQIO/INC
          INC       PATDRGIO/INC       * date range file
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATWR1IO/INC       * ward file
.
          INC       WATCATIO/INC       * W/L category change file
          INC       WATTR1IO/INC
          INC       WATRTDIO/INC       * Transfer destination file
          INC       WATSEAIO/INC       * W/L security access for ALL file
          INC       WATSEIIO/INC       * W/L security access to individual file
          INC       WATWTAIO/INC       * W/L movement analysis file
          INC       WATCNTCN           * W/L Cancellations           
          INC       WEBERRIO/INC
+

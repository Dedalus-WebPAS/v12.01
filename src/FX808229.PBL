.***************************************************************************
.* System    :   Legacy Visits                                             *
.* Program   :   FX808229                                                  *
.* Desc      :   Fix U/R number in Legacy Visit and Alternate ID files     *
.***************************************************************************
.* Date      :   24/05/2017                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   Validate the U/R number for all records in the legacy     *
.*           :   visit/alternate id file. If the U/R is merged update it   *
.*           :   with the current U/R                                      *
.* Modifications  :                                                        *
.*        V10.11.02 04/09/2017  Ebon Clements TSK 0844674                  *
.*                  Added print of records                                 *
.*        V10.11.01 02/08/2017  Ebon Clements TSK 0808229                  *
.*                  Display U/R number during processing                   *
.*        V10.10.00 25/05/2017  Ebon Clements TSK 0808229                  *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       LEGALTFD/INC
          INC       PATMA1FD/INC
          INC       PMSAIDFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
NEWURNUM  DIM       8
OLDURNUM  DIM       8
RECCOUNT  FORM      8
SAVKEY31  DIM       31
SAVKEY32  DIM       32
.
PRGID     INIT      "FX808229"
PRGNAM    INIT      "Fix U/R in legacy visit/alternate id if merged"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    IF        COPTION=1 | COPTION=2
            CALL      LEGA0000             * process legaltaf
          ELSE
            CALL      PMAI0000             * process pmsaidaf
          ENDIF
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"legaltaf";
          OPEN      LEGALTA1,"legaltaf"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmsaidaf"
          OPEN      PMSAIDA1,"pmsaidaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run (legaltaf)":
                    *P1:6,*V2LON,TWO,*HOFF,". Run and Update (legaltaf)":
                    *P1:7,*V2LON,THREE,*HOFF,". Run (pmsaidaf)":
                    *P1:8,*V2LON,FOUR,*HOFF,". Run and Update (pmsaidaf)"
.
OPTN0500  KEYIN     *P1:10,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:10,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000:   * run clean-up
                            OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               LEGA0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
LEGA0000  CALL      IBACLOCK
          MOVE      "Fix U/R in legacy visit if merged",PRGNAM
          CALL      HEAD80
          MOVE      ZERO,RECCOUNT          * Number of records updated
.
          CALL      UND80
          PRINT     *1,"|",*30,"File : legaltaf",*80,"|"
.
          CALL      UND80
          PRINT     *1,"| U/R",*12,"| Date",*23,"| Time",*34,"| Number",*45,"|"
          CALL      UND80
.
          PACK      KEY32,SP70
          CALL      RSLGALT1
LEGA1000  CALL      RKLGALT1               * Loop legacy visit file
          BRANCH    OVRCD,LEGA9000
.
          MOVE      LGATURNO,OLDURNUM      * Set Old U/R
          MOVE      LGATURNO,NEWURNUM      * Set New U/R
          PACK      SAVKEY32,LGATURNO,LGATADAT,LGATATIM,LGATEVNO,SP70 * Save Key
.
LEGA2000  PACK      KEY8,NEWURNUM,SP70
          CALL      RDMAST1                * Read PMI 
          BRANCH    OVRCD,LEGA1000
.
          DISPLAY   *P10:20,"U/R: ",PURNO
.
          IF        PSTAT=1 | PSTAT=2
            MOVE      PPSNAME,NEWURNUM     * Merged
            GOTO      LEGA2000
          ENDIF
.
          MATCH     NEWURNUM,OLDURNUM      * U/R is not merged so 
          GOTO      LEGA1000 IF EQUAL      * get next record
.
          PACK      KEY32,NEWURNUM,LGATADAT,LGATATIM,LGATEVNO,SP70
          CALL      RALGALT1               * Does new record exist
          BRANCH    OVRCD,LEGA3000
.
          IF        COPTION=2            
            CALL      DELGALT1             * Delete duplicate from new U/R
          ENDIF
.
LEGA3000  PACK      KEY32,OLDURNUM,LGATADAT,LGATATIM,LGATEVNO,SP70
          CALL      RDLGALT1               * Re read orginal record
          BRANCH    OVRCD,LEGA1000
.
          ADD       ONE,RECCOUNT           * Number of records updated
          PRINT    *1,"| ",LGATURNO,*12,"| ",LGATADAT,*23,"| ",LGATATIM:
                    *34,"| ",LGATEVNO,*45,"|"
.
          MOVE      NEWURNUM,LGATURNO      * Update with new U/R
          IF        COPTION=2            
            CALL      UPLGALT1             * Option 2 - Report and Update
          ENDIF
.
          PACK      KEY8,NEWURNUM,SP70
          CALL      RDMAST1                * Read PMI
          BRANCH    OVRCD,LEGA1000
.
          MOVE      ONE,PTMXSIN5           * Legacy visits exist
.
          IF        COPTION=2              
            CALL      UPMAST1              * Option 2 - Report and Update
          ENDIF
.
          PACK      KEY32,SAVKEY32,SP70    * Re position on save key after 
          CALL      RSLGALT1               * update
          GOTO      LEGA1000
.
LEGA9000  IF        COPTION=2
            DISPLAY   *P1:24,*EL,"Total legaltaf Records Updated : ":
                      *V2LON,RECCOUNT,*HOFF,SP5;
            CALL      UND80
            PRINT     *N,*1,"Total legaltaf Records Updated : ",RECCOUNT
          ELSE
            DISPLAY   *P1:24,*EL,"Total legaltaf Records with Old U/R : ":
                      *V2LON,RECCOUNT,*HOFF,SP5;
            CALL      UND80
            PRINT     *N,*1,"Total legaltaf Records with Old U/R : ",RECCOUNT
          ENDIF
.
          CALL      HITENTER
.
LEGA9999  RETURN
+
.**************************************************************************
.*                               PMAI0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PMAI0000  CALL      IBACLOCK
          MOVE      "Fix U/R in alternate id if merged",PRGNAM
          CALL      HEAD80
          MOVE      ZERO,RECCOUNT          * Number of records updated
.
          PRINT     *1,"|",*30,"File : pmsaidaf",*80,"|"
.
          CALL      UND80
          PRINT     *1,"| U/R",*12,"| Type",*18,"| Id",*41,"|"
          CALL      UND80
.
          PACK      KEY31,SP70
          CALL      RSPMAID1
PMAI1000  CALL      RKPMAID1               * Loop Alternate ID file
          BRANCH    OVRCD,PMAI9000
.
          MOVE      PMAIURNO,OLDURNUM      * Set Old U/R
          MOVE      PMAIURNO,NEWURNUM      * Set New U/R
          PACK      SAVKEY31,PMAIURNO,PMAITYPE,PMAIALID,SP70 * Save Key
.
PMAI2000  PACK      KEY8,NEWURNUM,SP70
          CALL      RDMAST1                * Read PMI
          BRANCH    OVRCD,PMAI1000
.
          DISPLAY   *P10:20,"U/R: ",PURNO
.
          IF        PSTAT=1 | PSTAT=2
            MOVE      PPSNAME,NEWURNUM     * Merged
            GOTO      PMAI2000
          ENDIF
.
          MATCH     NEWURNUM,OLDURNUM      * U/R is not merged so
          GOTO      PMAI1000 IF EQUAL      * get next record
.
          PACK      KEY31,NEWURNUM,PMAITYPE,PMAIALID,SP70
          CALL      RAPMAID1               * Does new record exist
          BRANCH    OVRCD,PMAI3000
.
          IF        COPTION=4
            CALL      DEPMAID1             * Delete duplicate from new U/R
          ENDIF
.
PMAI3000  PACK      KEY31,OLDURNUM,PMAITYPE,PMAIALID,SP70
          CALL      RDPMAID1               * Re read orginal record
          BRANCH    OVRCD,PMAI1000
.
          ADD       ONE,RECCOUNT           * Number of records updated
          PRINT     *1,"| ",PMAIURNO,*12,"| ",PMAITYPE,*18,"| ",PMAIALID:
                    *41,"|"
.
          MOVE      NEWURNUM,PMAIURNO      * Update with new U/R
          IF        COPTION=4
            CALL      UPPMAID1             * Option 4 - Report and Update
          ENDIF
.
          PACK      KEY31,SAVKEY31,SP70    * Re position on save key after
          CALL      RSPMAID1               * update
          GOTO      PMAI1000
.
PMAI9000  IF        COPTION=4
            DISPLAY   *P1:24,*EL,"Total pmsaidaf Records Updated : ":
                      *V2LON,RECCOUNT,*HOFF,SP5;
            CALL      UND80
            PRINT     *N,*1,"Total pmsaidaf Records Updated : ",RECCOUNT
          ELSE
            DISPLAY   *P1:24,*EL,"Total pmsaidaf Records with Old U/R : ":
                      *V2LON,RECCOUNT,*HOFF,SP5;
            CALL      UND80
            PRINT     *N,*1,"Total pmsaidaf Records with Old U/R : ",RECCOUNT
          ENDIF
          CALL      HITENTER
.
PMAI9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       LEGALTIO/INC
          INC       PATMA1IO/INC
          INC       PMSAIDIO/INC

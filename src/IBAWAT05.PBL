.***************************************************************************
.* System    :   WAITING LISTS                                             *
.* Program   :   IBAWAT05                                                  *
.* Desc      :   Procedure Codes Maintenance                               *
.***************************************************************************
.* Date      :   26/9/88                                                   *
.* Author    :   Thai-Phuong La                                            *
.* Comments  :   N.S. add ? option proc.codes                              *
.* Mods      :                                                             *
.*         V5.07.01 21/10/1999  Jill Habasque                              *
.*                  Added WTWPACTV - Active/Inactive indicator             *
.*         V5.05.02 18/03/1998  Davin  506                                 *
.*                  Changed to keyin WTWPDRGC - DRG code                   *
.*         V5.05.01 19/09/1997  Steve Armstrong                            *
.*                  Changed to keyin WTWPHDPE - Procedure HDP Equivalent   *
.***************************************************************************
. *              V5.04.01 14/04/1997  howellsy   RHA                       *
. *                       added wprhat, wpchet                             *
.***************************************************************************
.*               L.P. 10/04/89 Screen cleanup (Version 5.01)               *
.*               V5.01.01 18/06/89 M.Ohleiter                              *
.*                        Changes for new release 5.01                     *
.*                        Changed wording SRF 101395                       *
.*               V5.01.02 02.10.89 L.P.                                    *
.*                        Recompiled with new WATAUXFD.INC(V5.01) SRF102514*
.*               V5.01.03 05.10.89 K.Peak                                  *
.*                        Replaced uppercase audit to lowercase SRF 102601 *
.*               V5.01.04 23.02.90  E.Phan                                 *
.*                        Replaced WPTYPE with WPESTLS                     *
.*               V5.01.10 24/05/90 Paul Duncan                             *
.*                        Converted to new system specs                    *
.                         26/06/1990 Bill Dew
.                         Fixed minor screen displays and audits for
.                         procedure file.  WATIOAUX is no more.
.*               V5.01.11 03/08/90 Justin Coates                           *
.*                        Added the use of WTCNTPUC                        *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
          INC       WATPROFD/INC
          INC       WATCONFD/INC
          INC       PATCODFD/INC
.******************************************************************************
.                           VARIABLE DECLARATION
ACTDESC   DIM       3        * Inactive description - Yes/No
AUDIFLAG  FORM      1        * audit flag 1=A 2=B 3=C 4=D 5=delete B
CHNGFLAG  FORM      1        * CHaNGe FLAG  check if any changes occurred.
DISPACT   FORM      1
.
SEL       FORM      1
PTCNURHA  FORM      1
FORM6     FORM      6
.
.
PCODE     DIM       9          * work variable : procedure code
DESC      DIM       50         *                 description
TIME      FORM      5          *                 proc. time
ESTLS     FORM      3          *                 record estimated length of stay
GROUP     DIM       3          *                 proc. group
.
GRPDESC   DIM       20
CODEWG    INIT      "WG"
.
CODE      DIM       2
SCRNFLAG  FORM      1        * signals redisplays
.
COL       FORM      2
VERT      FORM      2
.
PRGID     INIT      "IBAWAT05"
VERSION   INIT      "V12.01.00"
PRGNAM    DIM       21                 * it's value is set up in INIT0000
.
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999
          BRANCH    OPTION,ML1000,ML2000,ML3000
.
. ---- add a procedure code ----
.
ML1000    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format
          CALL      GPRO0000               * input procedure code
          BRANCH    EXIT,ML0100
          CALL      ACPT0000               * accept initial values
          BRANCH    EXIT,ML1000        * exit char hit
ML1100    CALL      SEL0000                * select field to modify
          BRANCH    EXIT,ML1000
          CALL      WRT0000                * write to file
          GOTO      ML1000
.
. ---- modify a procedure code ----
.
ML2000    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format
          CALL      GPRO0000               * input procedure code
          BRANCH    EXIT,ML0100
          CALL      DISP0000               * display details
ML2100    CALL      SEL0000                * select field to modify
          BRANCH    EXIT,ML2000
          CALL      UPD0000                * update record
          GOTO      ML2000
.
. ---- delete a procedure code ----
.
ML3000    CALL      CLR0000            * clear input variables
          CALL      SCR0000            * display screen format
          CALL      GPRO0000           * input procedure code
          BRANCH    EXIT,ML0100
          CALL      DISP0000           * display detail
          CALL      DELQST
          BRANCH    CEXIT,ML3100,ML3000,ML0100
ML3100    CALL      DEL0000            * delete this record
          GOTO      ML3000
ML9999
          CHAIN     PGM
          STOP
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
INIT0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY7;*43,HWAUDA,*122,WTCNTPUC,*185,WTCNTPDS
          READ      CONTROLF,SEVENTY9;*138,PTCNURHA
.
. ------  set up either TREATMENT or PROCEDURE value in program name ------
.
          CLEAR     PRGNAM
          APPEND    WTCNTPUC,PRGNAM
          APPEND    " Maintenance",PRGNAM
          RESET     PRGNAM
          CALL      DISPHEAD
          MOVE      ONE,CNEWDS         * set new code display routines
.
          DISPLAY   *P56:24,"Opening watproaf";
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA2,"watproaf"
          OPEN      WATPROA3,"watproaf"
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P64:24,"controlf";
.
          BRANCH    HWAUDA,INIT1111
          DISPLAY   *P64:24,"wataudwp";
          OPEN      WATAUDWP,"wataudwp"
INIT1111
INIT9999  RETURN
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  MOVE      FALSE,EXIT
          HLINE     *G37,2,70,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Insert":
                    *P1:6,*V2LON,"2",*HOFF," = Change":
                    *P1:7,*V2LON,"3",*HOFF," = Delete"
.
. ---- select and validate option selected ----
.
OPTN1000  KEYIN     *P1:9,"Select Option : ",*P17:9,*DV,UNDLN1,*P17:9:
                    *V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL                 * exit
          COMPARE   FOUR,OPTION
          GOTO      OPTN1000 IF NOT LESS           * invalid option, re-enter
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT
OPTN9999  RETURN
.
.**************************************************************************
.*                                  CLR0000                               *
.*                            clear global variables                      *
.**************************************************************************
CLR0000   UNPACK    SP30,PCODE,GROUP
          PACK      DESC,SP30,SP30
          MOVE      ZERO,TIME
          MOVE      ZERO,ESTLS
          MOVE      ZERO,FORM6
          MOVE      "     0",WPRHAT
          MOVE      "     0",WPCHET
          MOVE      SP4,WTWPHDPE
          MOVE      SP6,WTWPDRGC
          MOVE      ZERO,WTWPACTV
CLR9999   RETURN
.*******************************************************************************
.                                 GPRO0000
.                        input procedure code
.    Created By Bill Dew 27/06/1990
.*******************************************************************************
GPRO0000
          MOVE      SP10,PCODE
          MOVE      ONE,SCRNFLAG
          DISPLAY   *P25:4,*EL,UNDLN9,*P25:4;
GPRO1111
          KEYIN     *V2LON,PCODE
          ENDSET    PCODE
          APPEND    SP9,PCODE
          RESET     PCODE
.
          MATCH     SP9,PCODE          * test for a return hit
          GOTO      GPRO9990 IF EQUAL
          MATCH     QUEST,PCODE        * test for a ? routine
          GOTO      GPRO3333 IF EQUAL
.
          CALL      VPRO0000           * validate procedure code to option
          BRANCH    EXIT,GPRO0000,GPRO4444
          BRANCH    SCRNFLAG,GPRO2222
          CALL      SCR0000            * get screen display back
GPRO2222
          DISPLAY   *P25:4,*V2LON,PCODE,*HOFF
          MOVE      ZERO,EXIT
          GOTO      GPRO9999
GPRO3333
          BRANCH    OPTION,GPRO8888
          MOVE      ONE,DISPACT
          CALL      WATDSPRO           * display all procedure codes
          MOVE      ZERO,SCRNFLAG
GPRO4444  DISPLAY   *P1:24,*EL,WTCNTPDS," code : ",UNDLN9,*P18:24;
          GOTO      GPRO1111           * keyin procedure code
GPRO8888
          DISPLAY   *P1:24,*EL,"The ? is an illegal code in insert mode.  ";
          CALL      HITENTER
          GOTO      GPRO0000
GPRO9990
          MOVE      TRUE,EXIT
GPRO9999  RETURN
.*******************************************************************************
.                                 VPRO0000
. ---- validate the input procedure code by reading WATPROA1 ----
.*******************************************************************************
VPRO0000
          PACK      KEY9,PCODE,SP9
          CALL      RDPROA1
          BRANCH    OVRCD,VPRO8000
.
          BRANCH    OPTION,VPRO8222,VPRO1111,VPRO1111
VPRO1111
          MOVE      ZERO,EXIT          * procedure code OK
          GOTO      VPRO9999
VPRO8000
          BRANCH    OPTION,VPRO1111,VPRO8111,VPRO8111
VPRO8111
          DISPLAY   *P1:24,*EL,WTCNTPDS," code does not exist.  ";
          GOTO      VPRO8888
VPRO8222
          DISPLAY   *P1:24,*EL,WTCNTPDS," code already exists.  ";
VPRO8888
          CALL      HITENTER
          MOVE      TWO,EXIT           * exit=1 if scrnflag=1
          LOAD      EXIT,SCRNFLAG,ONE  * exit=2 if scrnflag=0
VPRO9999  RETURN
.**************************************************************************
.*                                  SCR0000                               *
.*                         display screen format                          *
.**************************************************************************
SCR0000   BRANCH    OPTION,SCR1000,SCR2000,SCR3000
.
SCR1000   DISPLAY   *P70:2,*V2LON," Insert "
          MOVE      DNO,ACTDESC
          GOTO      SCR4000
.
SCR2000   DISPLAY   *P70:2,*V2LON," Change "
          GOTO      SCR4000
.
SCR3000   DISPLAY   *P70:2,*V2LON," Delete "
.
SCR4000   DISPLAY   *P1:4,*EF,*P5:4,WTCNTPDS," Code    : ",UNDLN9:
                    *P1:6,*V2LON,"1",*HOFF,". Description        : ":
                    *P1:7,*V2LON,"2",*HOFF,". Estimated Time     : ":
                    *P24:7,"       Minutes ":
                    *P1:8,*V2LON,"3",*HOFF,". Est Length of Stay : ":
                    *P31:8,"Days":
                    *P1:9,*V2LON,"4",*HOFF,". Group              : ":
                    *P1:10,*V2LON,"5",*HOFF,". HDP Equivalent     : ";
. 
          COMPARE   ONE,OPTION
          IF        !@EQUAL
             MOVE      SP8,ACTDESC
          ENDIF
.
          IF        PTCNURHA = 1
            DISPLAY   *P1:11,*V2LON,"6",*HOFF,". RHA Threshold Level: ":
                      *P1:12,*V2LON,"7",*HOFF,". CHE Threshold Level: ":
                      *P1:13,*V2LON,"8",*HOFF,". DRG Code           : ":
                      *P1:14,*V2LON,"9",*HOFF,". Inactive           : ",ACTDESC;
          ELSE
            DISPLAY   *P1:11,*V2LON,"6",*HOFF,". DRG Code           : ":
                      *P1:12,*V2LON,"7",*HOFF,". Inactive           : ",ACTDESC;
          ENDIF
.
SCR9999   RETURN
.*************************************************************************
.*                                  ACPT0000                             *
.*   INPUT mode only :      accept initial values                        *
.*************************************************************************
ACPT0000
          KEYIN     *P25:6,*DV,UNDLN30,*DV,UNDLN20:
                    *P25:6,*V2LON,DESC:
                    *P25:6,*DV,DESC
.
          ENDSET    DESC
          APPEND    SP30,DESC
          RESET     DESC
.
          MATCH     SP30,DESC
          GOTO      ACPT0000 IF EQUAL
.
          KEYIN     *P25:7,*DV,UNDLN5:
                    *P25:7,*V2LON,TIME:
                    *P25:7,*DV,TIME
ACPT0050 
          KEYIN     *P27:8,*DV,UNDLN3," Days":
                    *P27:8,*V2LON,ESTLS:
                    *P27:8,*DV,ESTLS
          COMPARE   ZERO,ESTLS
          GOTO      ACPT0100 IF EQUAL        * Zero is ok
          GOTO      ACPT0100 IF NOT LESS     * More than zero is ok
          KEYIN     *P1:24,*EL,*B,"Estimated length of stay cannot ":
                    "be less than zero.  ";
          CALL      HITENTER
          GOTO      ACPT0050
ACPT0055  DISPLAY   *P52:8,*V2LON,"** Invalid code **",*W,*HOFF,*P52:8,*EL
          GOTO      ACPT0050
.
ACPT0100  
          CALL      GROP0000           * get group code
          BRANCH    EXIT,ACPT9999
.
          CALL      HDPE0000           * Get the HDP Equivalent
          BRANCH    EXIT,ACPT9999
.
          COMPARE   ONE,PTCNURHA
          GOTO      ACPT5000 IF NOT EQUAL
.
          CALL      RHAT0000           * RHA Threshold
          BRANCH    EXIT,ACPT9999
.
          CALL      CHET0000           * CHE Threshold
          BRANCH    EXIT,ACPT9999
.
ACPT5000  CALL      CDRG0000           * Get DRG code
          BRANCH    EXIT,ACPT9999
.
          CALL      INAC0000           * Active/Inactive Code
          BRANCH    EXIT,ACPT9999
.
ACPT9999  RETURN
.******************************************************************************
.* RHAT0000 - Keyin RHA Threshold Level                             
.******************************************************************************
RHAT0000  MOVE      "     0",FORM6 
          MOVE      WPRHAT,FORM6 
          KEYIN     *P25:11,*V2LON,*DV,FORM6:   
                    *P25:11,*RV,FORM6:
                    *P25:11,*DV,FORM6;
.
          COMPARE   ZERO,FORM6      * test less than zero
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Cannot be Less Than Zero. ";
            CALL      HITENTER
            GOTO      RHAT0000
          ENDIF
.
          MOVE      FORM6,WPRHAT
.
RHAT9999  RETURN
.******************************************************************************
.* CHET0000 - Keyin CHE Threshold Level                             
.******************************************************************************
CHET0000  MOVE      "     0",FORM6 
          MOVE      WPCHET,FORM6 
          KEYIN     *P25:12,*V2LON,*DV,FORM6:   
                    *P25:12,*RV,FORM6:
                    *P25:12,*DV,FORM6;
.
          COMPARE   ZERO,FORM6      * test less than zero
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Cannot be Less Than Zero. ";
            CALL      HITENTER
            GOTO      CHET0000
          ENDIF
.
          MOVE      FORM6,WPCHET
.
CHET9999  RETURN
.**************************************************************************
.*                                   SEL0000                              *
.*                         select field to modify or post                 *
.    Upon POST if CHNGFLAG=0 then no assume no changes were made, EXIT=1
.                 same as CANCEL and delete the B audit.
.
.              if CHNGFLAG=1 then change was made EXIT=0
.**************************************************************************
SEL0000   
          MOVE      ZERO,CHNGFLAG      * set no changes yet
          BRANCH    OPTION,SEL0011     * write B audit in option 2 change mode
          MOVE      TWO,AUDIFLAG       * set to write a B audit
          CALL      AUDWLPRO
SEL0011
          CALL      MAINQST  
.
. ---- keyin field to change ----
.
          COMPARE   "-1",CCITEM                    * post record
          GOTO      SEL9000 IF EQUAL
.
          COMPARE   ZERO,CCITEM                    * post record
          GOTO      SEL9500 IF EQUAL
.
          MOVE      ONE,CHNGFLAG       * set a change is made
          IF        PTCNURHA =1
            BRANCH    CCITEM,SEL0100,SEL0200,SEL0300,SEL0400,SEL0500:
                             SEL0600,SEL0700,SEL0800,SEL0900
          ELSE
            BRANCH    CCITEM,SEL0100,SEL0200,SEL0300,SEL0400,SEL0500:
                             SEL0800,SEL0900
          ENDIF
.
          GOTO      SEL0011
.
. ---- change description ----
.
SEL0100  
          KEYIN     *P25:6,*EL,*DV,UNDLN30,*DV,UNDLN20:
                    *P25:6,*V2LON,DESC:
                    *P25:6,*DV,DESC;
          GOTO      SEL0011
.
. ---- change estimated time ----
.
SEL0200 
          KEYIN     *P25:7,*EL,*DV,UNDLN5," Minutes ":
                    *P25:7,*V2LON,TIME:
                    *P25:7,*DV,TIME;
          GOTO      SEL0011
.
. ---- change estimated length of stay ----
.
SEL0300  
          KEYIN     *P27:8,*EL,*DV,UNDLN3," Days":
                    *P27:8,*V2LON,ESTLS:
                    *P27:8,*DV,ESTLS
          GOTO      SEL0011
.
. ---- change group ----
.
SEL0400   
          CALL      GROP0000           * get group code
          BRANCH    EXIT,SEL9999       * exitchar hit EXIT=1 EXIT=1
          GOTO      SEL0011
.
.         Change HDP Equivalent
.
SEL0500   CALL      HDPE0000           * GEt the HDP Equivalent
          BRANCH    EXIT,SEL9999
          GOTO      SEL0011
.
SEL0600   CALL      RHAT0000           * RHA Threshold
          BRANCH    EXIT,SEL9999
          GOTO      SEL0011
.
SEL0700   CALL      CHET0000           * RHA Threshold
          BRANCH    EXIT,SEL9999
          GOTO      SEL0011
.
....      change DRG code
.
SEL0800   CALL      CDRG0000           * change DRG code
          BRANCH    EXIT,SEL9999
          GOTO      SEL0011
.
SEL0900   CALL      INAC0000           * change Active/Inactive indicator
          BRANCH    EXIT,SEL9999
          GOTO      SEL0011
.
SEL9000   
          BRANCH    OPTION,SEL9222
          MOVE      FIVE,AUDIFLAG      * delete B audit due to cancel
          CALL      AUDWLPRO
SEL9222   MOVE      TRUE,EXIT          * Cancel
          GOTO      SEL9999
.
SEL9500   
          BRANCH    OPTION,SEL9555     * if insert mode then EXIT=0
          COMPARE   ZERO,CHNGFLAG      * test if any changes were made.
          GOTO      SEL9000 IF EQUAL   * No changes then treat as cancel
SEL9555   MOVE      FALSE,EXIT         * Post
.
SEL9999   RETURN
.*****************************************************************************
.                                 GROP0000
.    get Procedure group code
.    Created by Bill Dew 26/06/1990 because the original method was crap.
.*****************************************************************************
GROP0000 
          MOVE      ONE,SCRNFLAG
          DISPLAY   *P25:9,*EL,UNDLN3,*P25:9;
GROP1111
          KEYIN     *V2LON,GROUP
          ENDSET    GROUP
          APPEND    SP3,GROUP
          RESET     GROUP
.
          MATCH     SP3,GROUP
          GOTO      GROP8800 IF EQUAL
.
          MATCH     EXITCHAR,GROUP
          GOTO      GROP9990 IF EQUAL
.
          CMATCH    QUEST,GROUP
          GOTO      GROP6666 IF NOT EQUAL
          MOVE      CODEWG,CODE
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG
GROP4444
          DISPLAY   *P1:24,*EL,"Group Code : ___",*P14:24;
          GOTO      GROP1111
GROP6666
          PACK      KEY5,CODEWG,GROUP  * check group code
          CALL      RDCODE1
          BRANCH    OVRCD,GROP8888
          MOVE      TDESC,GRPDESC
          BRANCH    SCRNFLAG,GROP7000  * test to redisplay screen after ?
          CALL      SCR0000
          DISPLAY   *P25:4,*V2LON,PCODE,*P25:6,DESC,*P25:7,TIME,*P27:8,ESTLS
GROP7000
          DISPLAY   *P25:9,*EL,*V2LON,GROUP,*HOFF,*P31:9,GRPDESC
          MOVE      ZERO,EXIT
          GOTO      GROP9999
.
GROP8800  DISPLAY   *P1:24,*EL,*B,"Group code is mandatory.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,GROP0000
          GOTO      GROP4444
.
GROP8888  DISPLAY   *P1:24,*EL,*B,"Group code not on file.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,GROP0000
          GOTO      GROP4444
GROP9990
          MOVE      TRUE,EXIT
GROP9999  RETURN
+
.******************************************************************************
.*                                  HDPE0000              Called by: ACPT0000 *
.*                          Keyin The HDP Equivalent               & SEL0000  *
.******************************************************************************
HDPE0000  MOVE      WTWPHDPE,KEY4
          MATCH     SP4,KEY4
          IF        @EQUAL
            KEYIN     *P25:10,*DV,UNDLN4:
                      *P25:10,*V2LON,KEY4:
                      *P25:10,*DV,KEY4;
          ELSE
            KEYIN     *P25:10,*DV,KEY4:
                      *P25:10,*V2LON,*RV,KEY4:
                      *P25:10,*DV,KEY4;
          ENDIF
.
          ENDSET    KEY4
          APPEND    SP4,KEY4
          RESET     KEY4
.
          CMATCH    EXITCHAR,KEY4
          GOTO      HDPE9500 IF EQUAL       * Exitchar entered, exit
.
          MOVE      KEY4,WTWPHDPE
.
HDPE9000  MOVE      ZERO,EXIT
          GOTO      HDPE9999
.
HDPE9500  MOVE      ONE,EXIT
HDPE9999  RETURN
+
.******************************************************************************
.*                                  CDRG0000              Called by: ACPT0000 *
.*                             Keyin The DRG Code                  & SEL0000  *
.******************************************************************************
CDRG0000  MOVE      WTWPDRGC,KEY6
          COMPARE   ONE,PTCNURHA
          GOTO      CDRG1000 IF EQUAL            * using RHA booking system
.
          MOVE      "11",CVERT
          GOTO      CDRG2000
.
CDRG1000  MOVE      "13",CVERT
.
CDRG2000  MATCH     SP6,KEY6
          IF        @EQUAL
            KEYIN     *P25:CVERT,*DV,UNDLN6:
                      *P25:CVERT,*V2LON,KEY6:
                      *P25:CVERT,*DV,KEY6;
          ELSE
            KEYIN     *P25:CVERT,*DV,KEY6:
                      *P25:CVERT,*V2LON,*RV,KEY6:
                      *P25:CVERT,*DV,KEY6;
          ENDIF
.
          ENDSET    KEY6
          APPEND    SP6,KEY6
          RESET     KEY6
.
          CMATCH    EXITCHAR,KEY6
          GOTO      CDRG9500 IF EQUAL       * Exitchar entered, exit
.
          MOVE      KEY6,WTWPDRGC
.
CDRG9000  MOVE      ZERO,EXIT
          GOTO      CDRG9999
.
CDRG9500  MOVE      ONE,EXIT
CDRG9999  RETURN
+
.******************************************************************************
.*                                  INAC0000              Called by: ACPT0000 *
.*                             Keyin The Active/Inactive           & SEL0000  *
.******************************************************************************
INAC0000  MOVE      ZERO,DIM1                    * Default to active
          MOVE      WTWPACTV,DIM1
.
          MOVE      DNO,ACTDESC
          COMPARE   ONE,WTWPACTV
          IF        @EQUAL
            MOVE      DYES,ACTDESC
          ENDIF
.
          COMPARE   ONE,PTCNURHA
          GOTO      INAC1000 IF EQUAL            * using RHA booking system
.
          MOVE      "12",CVERT
          GOTO      INAC2000
.
INAC1000  MOVE      "14",CVERT
.
INAC2000  DISPLAY   *P25:CVERT,*V2LON,ACTDESC 
          KEYIN     *P25:CVERT,*EL,*V2LON,DIM1
          RESET     DIM1
.
          CMATCH    EXITCHAR,DIM1
          GOTO      INAC9500 IF EQUAL       * Exitchar entered, exit
.
          REP       UPPLOW,DIM1
          MATCH     "Y",DIM1
          IF        @EQUAL
            MOVE      DYES,ACTDESC
            MOVE      ONE,WTWPACTV
            GOTO      INAC8000
          ENDIF
.
          MATCH      "N",DIM1
          IF         @EQUAL
            MOVE       DNO,ACTDESC
            MOVE       ZERO,WTWPACTV
          ELSE
            GOTO       INAC0000
          ENDIF
INAC8000  DISPLAY   *P25:CVERT,*V2LON,ACTDESC
.
INAC9000  MOVE      ZERO,EXIT
          GOTO      INAC9999
.
INAC9500  MOVE      ONE,EXIT
INAC9999  RETURN
+
.****************************************************************************
.*                              WRT0000                                   *
.*                          write a new record                            *
.**************************************************************************
WRT0000
          DISPLAY   *P1:24,*EL,*P60:24,"*** POSTED ***"
          CALL      TRAN0000           * tranfer program vars to IO vars
          PACK      KEY9,WPCODE
          CALL      WRPROA1
.
          MOVE      ONE,AUDIFLAG       * write a A audit
          CALL      AUDWLPRO
WRT9999   RETURN
.*************************************************************************
TRAN0000
          MOVE      PCODE,WPCODE
          MOVE      DESC,WPDESC
          MOVE      TIME,WPTIME
          MOVE      GROUP,WPGRP
          MOVE      ESTLS,WPESTLS
TRAN9999  RETURN
.*************************************************************************
.*                                DISP0000                               *
.* disp details corresponding to proc code and save values read from file*
.*************************************************************************
DISP0000
          MOVE      WPDESC,DESC
          MOVE      WPTIME,TIME
          MOVE      WPESTLS,ESTLS
          MOVE      WPGRP,GROUP
          MOVE      SP30,GRPDESC
.
          MATCH     SP3,WPGRP                  * no grp code, hence no
          GOTO      DISP1111 IF EQUAL          * description will be print
          MOVE      "Unknown Group Code",TDESC
          PACK      KEY5,CODEWG,WPGRP
          CALL      RDCODE1
          MOVE      TDESC,GRPDESC
DISP1111
          DISPLAY   *P25:6,*V2LON,WPDESC:
                    *P25:7,WPTIME:
                    *P27:8,WPESTLS:
                    *P25:9,WPGRP,*HOFF,*P31:9,GRPDESC:
                    *P25:10,*V2LON,WTWPHDPE;
          MOVE      DNO,ACTDESC
          IF        WTWPACTV=1
            MOVE      DYES,ACTDESC
          ENDIF
          IF        PTCNURHA = 1
            DISPLAY   *P25:11,*V2LON,WPRHAT:
                      *P25:12,WPCHET:
                      *P25:13,WTWPDRGC:
                      *P25:14,ACTDESC;
          ELSE
            DISPLAY   *P25:11,*V2LON,WTWPDRGC:
                      *P25:12,*V2LON,ACTDESC;
          ENDIF
.
DISP9999  RETURN
.***************************************************************************
.*                               UPD0000                                   *
.*  update record and insert key if part of key has been modified.         *
.***************************************************************************
UPD0000
          DISPLAY   *P1:24,*EL,*P60:24,"*** POSTED ***"
          CALL      TRAN0000           * tranfers Program vars to IO vars
.
          CALL      UPPROA1            * update procedure details
.
          MOVE      THREE,AUDIFLAG     * write a C audit
          CALL      AUDWLPRO
UPD9999   RETURN
.****************************************************************************
.*                              DEL0000                                     *
.****************************************************************************
DEL0000
          DISPLAY   *P1:24,*EL,*P60:24,"*** DELETED ***"
          CALL      DEPROA1
.
          MOVE      FOUR,AUDIFLAG      * write a D record 
          CALL      AUDWLPRO
.
DEL9999   RETURN
.****************************************************************************
.                        AUDIT ROUTINES for WAT01
.                 Created by Bill Dew on the 31/05/1990
.
.    AUDIFLAG : 1   Write a A audit to file
.               2   Write a B audit to file
.               3   Write a C audit to file
.               4   Write a D audit to file
.               5   Delete  B audit to file
.****************************************************************************
.                                 AUDWLPRO
.    Audits for patient waiting list procedure details.
.    Modified 22/08/1990 Justin Coates
.             stopped it reclocking time for an after change audit
.****************************************************************************
AUDWLPRO
.         DISPLAY   *P1:3,*EL,"HWAUDA ",HWAUDA
          BRANCH    HWAUDA,WLPRO999
.
          COMPARE   FIVE,AUDIFLAG      * test to delete B audit
          GOTO      WLPRO600 IF EQUAL
.
          COMPARE   THREE,AUDIFLAG     * is it an after change audit ??
          GOTO      WLPRO100 IF EQUAL  * yes it is so dont get new time
.
          CALL      IBACLOCK           * get current date
          PACK      WTWPAUDD,CCC,CYY,CMM,CDD
          REP       " 0",WTWPAUDD
          CLOCK     TIME,WTWPAUDT      * clock current time
.
WLPRO100  MOVE      ONE,WTWPAUDS       * set print status
          MOVE      PASSCODE,WTWPAUDO  * get user ID
          MOVE      PORT,WTWPAUDP      * get port number
.
          BRANCH    AUDIFLAG,WLPRO200,WLPRO200,WLPRO200,WLPRO200,WLPRO600
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      WLPRO999
WLPRO200
          MOVE      AUDIFLAG,WTWPAUDR
          REPLACE   "1A2B3C4D",WTWPAUDR
          PACK      KEY19,WTWPAUDD,WTWPAUDT,WTWPAUDP,WTWPAUDR  
          CALL      AWPROA1            * write to audit file
          GOTO      WLPRO999
WLPRO600                               * Delete B audit
          PACK      KEY19,WTWPAUDD,WTWPAUDT,WTWPAUDP,ANSB
          CALL      ADPROA1            * delete audit file
WLPRO999  RETURN
.****************************************************************************
          INC       WATPROIO/INC
          INC       WATDSPRO
          INC       PATCODIO/INC
          INC       PATCODKY
          INC       STD001IO
.****************************************************************************

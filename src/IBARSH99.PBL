. ***********************************************************************
. *                    P R O G R A M  S U M M A R Y                     *
. *                    -------------  -------------                     *
. *                                                                     *
. *   PROGRAM NAME  :   IBARSH99                                        *
. *                                                                     *
. *   FUNCTION      :   Chaining Program For Scheduling                 *
. *                                                                     *
. *   MODIFICATIONS :                                                   *
. *   V11.02.01     05/04/2022 Ebon Clements  TSK 0911828               *
. *                 Set scheduled report completed date and time        *
.*                  WBSCCDAT and WBSCCTIM                               *
. *   V10.02.01     29/03/2011 Jill Parkinson CAR 239574                *
. *                 Increased printer field                             *
. *                     V9.02.03 28.08.2002 Sandra Barcham              *
. *                              Fix date srf 22974                     *
. *                     V9.02.02 26.04.2002 Bronko Sosic                *
. *                              Changed the read of ibaprtaf so that   *
. *                              it now strips all leading zeros        * 
. *                     V9.02.01 28.02.2002 Jill Habasque               *
. *                              Changed errors to display              *
. *                     V9.00.02 29.06.2001 B.G.Ackland                 *
. *                              Change to WEBSCHFD for TRU64 Cluster   *
. *                     V9.00.01 22.06.2001  B.G.Ackland                *
. *                              Fixed CPRTFLG to 0                     *
. *                                                                     *
. *                     C2.00.01 12/04/2000  B.G.Ackland & Pat Dirito   *
. *                              Created Program                        *
. *                                                                     *
. ***********************************************************************
          INC       STD001FD
          INC       IBAPRNFD/INC
          INC       CHECKDAT/INC
          INC       WEBSCHFD/INC     * Report Server Schedule Table
.
.********************************************************************** 
.*                            Variables                               *
.********************************************************************** 
PRTEXT    INIT     "/prt"
LOCKCNT   FORM      2
CMDLINE   DIM       50
FLAG      FORM      1
FNAMER    DIM       8
TFILE     FILE      ASCII, FIXED=256
VERT      FORM      2
PRINTER   DIM       6
DIM1A     DIM       1
DIM1B     DIM       1
DIM1C     DIM       1
DIM1D     DIM       1
DIM1E     DIM       1
DIM1F     DIM       1
.---------------------------------------------------------------------
PRGID     INIT      "IBARSH99"
PRGNAM    INIT      "Print Handling for the Scheduler"
VERSION   INIT      "V12.01.00"
.---------------------------------------------------------------------
MAIN0000  MOVE      "  1",CNOCOPY          * default number of copies
          OPEN      IBACODE1,"ibacodef"
          OPEN      CONTROLF,"controlf"
          OPEN      IBASPOL1,"ibaspolf"
          OPEN      IHAPASS1,"ihapassf"
          OPEN      IBAPRTA1,"ibaprtaf"
          OPEN      WEBSCHA1,"webschaf"
.
.      *  Read Schedule File using CCMENU as KEY8
.
          MOVE      CCMENU,KEY8
          CALL      RDWBSC1
          BRANCH    OVRCD,MAIN1920  
.
          MOVE      ZERO,CPRTFLG       * No Printer Selection in Programs
.
.         * Setup No Copies Based on Schedule Record     
.         * Setup Spool File Name Based on Schedule Record  (Description)
.
          MOVE      WBSCPRIN,PRINTER    *  Set Printer 
.
          CALL      SPLEMPTY
          BRANCH    OVRCD,MAIN9100,MAIN9200
.
. NOTE following to use indicator from schedule record  ie Print Report = No
.
          MATCH     "S",PRINTER
          GOTO      MAIN2000 IF EQUAL
.
MAIN1000  MOVE      WBSCNCOP,KEY3
          SQUEEZE   KEY3
          MOVE      ZERO,F3
          MOVE      KEY3,F3
          IF        F3<>0
            MOVE      F3,CNOCOPY
          ENDIF
.
. bronko
          UNPACK    PRINTER,DIM1A,DIM1B,DIM1C,DIM1D,DIM1E,DIM1F
          MATCH     "0",DIM1A
          GOTO      MAIN1060 IF NOT EQUAL
.
          MOVE      SP1,DIM1A
.
          MATCH     "0",DIM1B
          GOTO      MAIN1050 IF NOT EQUAL
.
          MOVE      SP1,DIM1B
.
          MATCH     "0",DIM1C
          GOTO      MAIN1050 IF NOT EQUAL
.
          MOVE      SP1,DIM1C
.
          MATCH     "0",DIM1D
          GOTO      MAIN1050 IF NOT EQUAL
.
          MOVE      SP1,DIM1D
.
          MATCH     "0",DIM1E
          GOTO      MAIN1050 IF NOT EQUAL
.
          MOVE      SP1,DIM1E
.
MAIN1050  PACK      PRINTER,DIM1A,DIM1B,DIM1C,DIM1D,DIM1E,DIM1F
.
MAIN1060  PACK      KEY6,PRINTER,SP70
          CALL      RDPRTA1
          BRANCH    OVRCD,MAIN1900
          COMPARE   ZERO,PRTAVAL
          GOTO      MAIN1910 IF NOT EQUAL
.
          MOVE      PRINTER,CDEFPRT
          CALL      CLSPRINT                     * Print & delete spool file
.
.     * Read and Update Schedule as Report Status Complete & Printed (9)
.
          MOVE      ZERO,LOCKCNT
MAIN1100  MOVE      CCMENU,KEY8
          CALL      RLWBSC1          * Readlock Record
          BRANCH    OVRCD,MAIN1920,MAIN1930
          MOVE      NINE,WBSCSTAT    * Set to 9-Complete & Printed
.
          CALL      IBACLOCK
          PACK      WBSCCDAT,CCC,CYY,CMM,CDD  * Completed Date
          REP       " 0",WBSCCDAT 
          CLOCK     TIME,WBSCCTIM             * Completed Time
.
          CALL      UPWBSC1          * Update Record
          CALL      UUWBSC1          * Unlock Record
.
          GOTO      MAIN9999
.
MAIN1900  DISPLAY   "ERROR: Invalid Printer Selection",PRINTER
          GOTO      MAIN2000
.
MAIN1910  DISPLAY   "ERROR: Printer Not Available",PRINTER
          GOTO      MAIN2000
.
MAIN1920  DISPLAY   "ERROR: Invalid Schedule No",KEY8
          GOTO      MAIN2000
.
MAIN1930  ADD       ONE,LOCKCNT
          IF        LOCKCNT<20
            GOTO      MAIN1100
          ENDIF
          DISPLAY   "ERROR: Schedule No. Locked",KEY8
          GOTO      MAIN2000
.
MAIN2000  PACK      CSPDESC,SP30,SP30
          MOVE      SPLFILE,KEY8
          CALL      RDSPOL1
          IF        OVRCD=0
            CALL      DESPOL1
          ENDIF
.
          PACK      CSPDESC,SPLFILE,PRTEXT,SP30
          PACK      CSPDATE,CCC,CYY,CMM,CDD
          REP       " 0",CSPDATE
          CLOCK     TIME,CSPTIME
          MOVE      PASSCODE,CSPOPER
          MOVE      SPLFILE,CSPNAME
          MOVE      SPLFILE,KEY8
          MOVE      CPROGLEV,CSPLEVE
          MOVE      CDEPCDE,CSPDEPT
          CALL      WRSPOL1
.
.
.      * Read and Update Schedule as Report Status Complete & Spooled (8)
.          & update Spool File Name
.
          MOVE      ZERO,LOCKCNT
MAIN3000  MOVE      CCMENU,KEY8
          CALL      RLWBSC1             * Readlock Record
          BRANCH    OVRCD,MAIN9999,MAIN3100
          MOVE      EIGHT,WBSCSTAT      * Set to 8-Complete & Spooled
          MOVE      CSPDESC,WBSCSPOO    * Set Spool File Name
.
          CALL      IBACLOCK
          PACK      WBSCCDAT,CCC,CYY,CMM,CDD  * Completed Date
          REP       " 0",WBSCCDAT
          CLOCK     TIME,WBSCCTIM             * Completed Time
.
          CALL      UPWBSC1             * Update Record
          CALL      UUWBSC1             * Unlock Record
.
          GOTO      MAIN9999
.
MAIN3100  ADD       ONE,LOCKCNT
          IF        LOCKCNT<20
            GOTO      MAIN3000
          ENDIF
          DISPLAY   "ERROR: Schedule No. Locked",KEY8
          GOTO      MAIN2000
.
MAIN9100  CALL      SPLDELET                     * Delete spool file
.
. Read and Update Schedule as Report Status Complete & No Output (7)
.
MAIN9200  MOVE      ZERO,LOCKCNT
MAIN9210  MOVE      CCMENU,KEY8
          CALL      RLWBSC1             * Readlock Record
          BRANCH    OVRCD,MAIN9999,MAIN9300
          MOVE      SEVEN,WBSCSTAT      * Set to 7-Complete & no output
.
          CALL      IBACLOCK
          PACK      WBSCCDAT,CCC,CYY,CMM,CDD  * Completed Date
          REP       " 0",WBSCCDAT
          CLOCK     TIME,WBSCCTIM             * Completed Time
.
          CALL      UPWBSC1             * Update Record
          CALL      UUWBSC1             * Unlock Record
          GOTO      MAIN9999
.
MAIN9300  ADD       ONE,LOCKCNT
          IF        LOCKCNT<20
            GOTO      MAIN9210
          ENDIF
          DISPLAY   "ERROR: Schedule No. Locked",KEY8
          GOTO      MAIN2000
.
MAIN9999  SHUTDOWN  "Complete "
.
          INC       IBAPRINT
          INC       IBAPRNIO/INC
          INC       WEBSCHIO/INC        * Report Server Schedule Table
.
          INC       STD001IO

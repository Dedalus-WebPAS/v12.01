.*****************************************************************************
.* System    :   Allied Health                                               *
.* Program   :   FX321334                                                    *
.* Desc      :   Fixed for orphaned waiting allhdtaf records                 *
.*****************************************************************************
.* Date      :   03/09/2015                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through allhdtaf from a start date   *
.*               looking for waiting records on or before that date.  Any    *
.*               records found will have their status updated to "98".       *
.* Mods      :                                                               *
.*        V10.14.01 17/04/2019  Steve Armstrong  TSK 0868837                 *
.*                  Recompiled for changes to ALLHDTFD.                      *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLHDTFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CURRDATE  DIM       8
RECCOUNT  FORM      8
STARTDAT  DIM       8
.
.
. PROGRAM CONSTANTS
. -----------------
TILDA42   INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
.
.
.
PRGID     INIT      "FX321334"
PRGNAM    INIT      "Fixit for Orphaned allhdtaf Waiting Records"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      SDAT0000               * get start date
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allhdtaf";
          OPEN      ALLHDTA4,"allhdtaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                             SDAT0000                Called by: MAIN0000  *
.*                       Get fixit start date                               *
.* Returns: STARTDAT - Start fixit date (ccyymmdd)                          *
.****************************************************************************
.
SDAT0000  DISPLAY   *P1:15,*EF,"Starting Date :"
          MOVE      ONE,CCENTRY
          MOVE      TEN5,CVERT
          MOVE      TEN7,CCOL
.
          CALL      IBACLOCK                     * get current date/time
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      SP2,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,SDAT0000
          PACK      STARTDAT,CCENT,CYEAR,CMON,CDAY
.
          MATCH     STARTDAT,CURRDATE            * date in future ?
          GOTO      SDAT9000 IF NOT LESS         * no
.
          DISPLAY   *P1:24,*EL,*B,"Date cannot be in the future.  ";
          CALL      HITENTER
          GOTO      SDAT0000
.
SDAT9000  DISPLAY   *P1:23,*EF,"All waiting records on or before this date ":
                    "will be updated to a status of 98.";
.
SDAT9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*          Loop back through allhdtaf looking for waiting records           *
.*          on or prior to the starting date.                                *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,RECCOUNT                * Initialise counter
.
PROC0500  PACK      KEY42,SP1,ZERO,STARTDAT,TILDA42
          CALL      RSALHDT4                     * position after start date
          CALL      RPALHDT4                     * read previous record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          ADD       ONE,RECCOUNT                 * increment counter
          MOVE      "98",ALHDSTAT                * load new status
          CALL      UPALHDT4                     * update record
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:24,*EL,*V2LON,RECCOUNT,*HOFF," records updated.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLHDTIO/INC

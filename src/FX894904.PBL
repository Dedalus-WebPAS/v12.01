.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   FX894904                                                  *
.* Desc      :   Write missinge EDWARD UPI records                         *
.***************************************************************************
.* Date      :   29/07/2020                                                *
.* Author    :   Jill Parkinson                                            *
.* Function  :   This program will loop EDWARD UPI alternate id records    *
.*               and write trigger information to pmsedwaf if required     *
.* Modifications  :                                                        *
.* V11.00.00 29/07/2020 Jill Parkinson Task 0894904                        *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCODFD/INC
          INC       PMSAIDFD/INC
          INC       PMSEDWFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECNUM    FORM      8
.
PRGID     INIT      "FX894904"
PRGNAM    INIT      "Fix EDWARD UPI trigger records"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PMSAIDA1,"pmsaidaf"
          OPEN      PMSEDWA2,"pmsedwaf"
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  DISPLAY   *P1:15,*EL,"Processing Alternate ID for UR : ";
.
          PACK      KEY31,SP70
          CALL      RSPMAID1
PROC1000  CALL      RKPMAID1                       * Loop alternate id
          BRANCH    OVRCD,PROC9000
.
          DISPLAY   *P25:15,*EL,*V2LON,PMAIURNO
.
          PACK      KEY5,ANSA,ANSI,PMAITYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PROC1000
          MATCH     TCINDC4,ANSU
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      KEY27,PMAIURNO,ONE,SIX,SP70
          CALL      RSPMEDW2
          CALL      RKPMEDW2
          BRANCH    OVRCD,PROC2000
.
          MATCH     PMAIURNO,PMEWURNO
          GOTO      PROC2000 IF NOT EQUAL
.
          MATCH     "16",PMEWTYPE
          GOTO      PROC1000 IF EQUAL
.
PROC2000  PACK      PMEWURNO,PMAIURNO
          MOVE      "16",PMEWTYPE
          PACK      PMEWOLDV,SP70
          PACK      PMEWNEWV,PMAIURNO,PMAITYPE,PMAIALID,SP70
          CALL      IBACLOCK
          PACK      PMEWCDAT,PMAICRDT
          REP       " 0",PMEWCDAT
          PACK      PMEWCTIM,PMAICRTM
          PACK      PMEWCUID,PMAICRUD
          PACK      PMEWTYPE,PMEWTYPE,SP70
          PACK      PMEWSPAR,SP70,SP70
.
          PACK      KEY27,PMEWURNO,PMEWTYPE,PMEWCDAT,PMEWCTIM,SP70
          CALL      RAPMEDW2
          IF        OVRCD=1
            CALL      WRPMEDW2
          ELSE
            CALL      UPPMEDW2
          ENDIF
.
          PRINT     *1,"Added record for ",PMAIURNO,*27,PMAIALID
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:20,*EF,"UPI EDWARD Update Complete.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PMSAIDIO/INC
          INC       PMSEDWIO/INC

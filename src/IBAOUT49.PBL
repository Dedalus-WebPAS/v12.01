.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT49.PBL                                              *
.* Name           : DAILY BOOKING SCHEDULE REPORT                             *
.******************************************************************************
.* Author         : N. SPIJKMAN                                               *
.* Date Written   : 26/07/88                                                  *
.* Modifications  :                                                           *
.******************************************************************************
.*        V12.00.01 30/05/2025 Jacob Jackson     TSK 0955096                  *
.*                  Alphanumeric changes                                      *
.*        V11.02.01 09/02/2022  Thanh T          TSK 0905641                  *
.*                  Recompiled as OUTMA1FD changes                            *
.*        V11.00.01 02/04/2020  J.Tan             TSK 0868813                 *
.*                  Changed PKNAME to DIM80                                   *
.*        V10.07.01 29/10/2015  Ebon Clements CAR 268970                      *
.*                  Removed open of OUTCLIA2                                  *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*             V5.08.B01  13/03/2000  Steve Armstrong   5.08 Mods             *
.*                        Mods for changes to OUTCLIFD (effective date)       *
.******************************************************************************
.*             V5.07.B01  02/11/1998  Jim Stathopoulos                        *
.*                        507 Changes                                         *
.******************************************************************************
.*                 V5.01  01/03/89 J.Tan    (I.B.A.)                          *
.*                       Added comments on heading                            *
.*                        SRF 4727                                            *
.*                 V5.02  25/04/89 Graeme Williams                            *
.*                        Use PATPRAM for 0 U/R bookings                      *
.*                        SRF 5648                                            *
.*                 V5.03  08.05.89  E.Phan  (I.B.A.)                          *
.*                        Changed prgnam for clarity.                         *
.*                 V5.04  22.05.89 S.Barcham(I.B.A.)                          *
.*                        Fixed Report                                        *
.*                        SRF100669                                           *
.*                 V5.05  24/05/89 Graeme Williams (I.B.A)                    *
.*                        Fixed up and check some code                        *
.*                        SRF 100689                                          *
.*                 V5.06  02.06.89  E.Phan  (I.B.A.)                          *
.*                        CE/IN/CL used to print PTYPE,                       *
.*                        OBACOMP, OBACLASS.  Changed to                      *
.*                        OBACOMP, OBAINS, OBACLASS.                          *
.*                 V5.07  03.06.89 S.Barcham(I.B.A.)                          *
.*                        Split OUTBOK & OUTMAS includes                      *
.*                        SRF 100751                                          *
.*                 V5.08  05.06.89 S.Barcham(I.B.A.)                          *
.*                        Recompiled for locking in                           *
.*                        PATIOMAS            SRF 100809                      *
.*                 V5.09  24.06.89 S.Barcham(I.B.A.)                          *
.*                        Recompiled for OUTBB1FD                             *
.*                 V5.10  03/07/89 DIG                                        *
.*                        Changed to read source of                           *
.*                        referral from OUTSITAF file.                        *
.*              V5.01.01  16/07/89 K.Peak                                     *
.*                        Changed Adm & U/R to 8 characters                   *
.*              V5.01.02  12/09/89 K.Peak                                     *
.*                        Removed CE/IN/CL from report                        *
.*              V5.01.03  22/09/89 Graeme Williams                            *
.*                        Fixed inifinite loop in Ok to Cnt                   *
.*              V5.01.04  11/07/90 Paul Duncan                                *
.*                        recomp for OUTBOA & OUTBOB audits                   *
.*        V5.01.121 27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to  PATMX1FD                       *
.*        V5.01.122 01/08/1991    Justin Coates  Quote 6994 SRF 109232        *
.*                  Suppress extended slots if desired                        *
.*        V5.01.123 04/10/91 Steve O'Gorman                                   *
.*                  Recompiled for Change to OUTMA1FD                         *
.*        V5.01.124 07/12/1992  Graeme Williams            (UK Changes)       *
.*                  Added OTCNR49A to specify report format                   *
.*        V5.01.125 02/03/93  ROD                                             *
.*                  Recompiled for Change to OUTMA1FD                         *
.*        V5.01.126 03.03.93 Neeriem "I Hate Support" Dye                     *
.*                  Modify to print full address                              *
.*        V5.01.127 10/03/93  Steve O'Gorman                                  *
.*                  Modify to be hospital based                               *
.*        V5.01.128 18/03/1993    Justin Coates                               *
.*                  fixed a few printing bugs                                 *
.*        V5.01.129 28/04/93 J.Tan                                            *
.*                  Move right justified zero to UR                           *
.*        V5.01.130 27/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.131 14/07/1994 Rob Leonard   SRF 440996                       *
.*                  Fixed hospital keyin                                      *
.******************************************************************************
.
.         FD's required
.
          INC       STD001FD
          INC       OUTBOAFD/INC      * Clinic Session Booking file A
          INC       OUTBB1FD/INC      * Clinic Session Booking file B
          INC       OUTCLIFD/INC      * Outpatient Clinics
          INC       OUTCONFD/INC      * Outpatient Control file
          INC       OUTCTYFD/INC      * Outpatient Clinic Type file
          INC       OUTMA1FD/INC      * Clinic Master file A
          INC       OUTMABFD/INC      * Clinic Master file B
          INC       OUTSITFD/INC      * Site Code file
          INC       PATCODFD/INC      * system codes file
          INC       PATCONFD/INC      * Patient control file
          INC       PATDO1FD/INC      * Doctor file
          INC       PATHSPFD/INC      * Hospital File
          INC       PATMA1FD/INC      * Patient Master file
          INC       PATPR1FD/INC      * Pre Admission file
          INC       PATRE1FD/INC      * Person Responsible for Account
.
.------------------------------------------------------------------------------
.                   Variables
.------------------------------------------------------------------------------
.
ADDT1     DIM       35
ADDT2     DIM       35
ATT       INIT      "** ATTENDED **           "
BOK       INIT      "** BOOKED **             "
.
COL       FORM      2
CODE      DIM       2
CODES     INIT      "S "
CAN       INIT      "** CANCELLED **          "
.
DIM40     DIM       40      * pack patientname / pack doctorname
DIMC1     DIM       1       * case notes
DESCTYP   DIM       30      * clinictype description
DESCID    DIM       30      * clinic ID description
DATE      DIM       8       * date keyed in
DDAY      DIM       9
DSRCDESC  DIM       20
DTRNDESC  DIM       20
DLNGDESC  DIM       20
.
EXT       INIT      "** EXTENDED SLOT **      "
.
IBCNMHOS  FORM      1
.
NBK       INIT      "** NOT BOOKED **         "
NATT      INIT      "** NON ATTENDANCE **     "
.
OUT       INIT      "out"
.
PNAME     DIM       22      * patients name
PNAMER    DIM       25      * patients name
PBNAMER   DIM       23      * For print option "Booking Name"
.
SITE      DIM       6       * store IDDD
SP40      INIT      "                                        "
.
PRGNAM    INIT      "Daily Booking Schedule"
PRGID     INIT      "IBAOUT49"
VERSION   INIT      "V12.01.00"
+
.------------------------------------------------------------------------------
.                   MAINLINE
.------------------------------------------------------------------------------
ML0000    CALL      INIT0000                * initialization
          BRANCH    EXIT,ML9999             * invalid site code
.
ML1000    CALL      OPTN0000                * enter option
          BRANCH    EXIT,ML9999             * exit
.
ML2000    CALL      KDAT0000                * enter date
          BRANCH    EXIT,ML1000             * nothing entered
.
          CALL      PRNT0000                * print report
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,INIT9100          * invalid site code
.
          PACK      CFNAME WITH UID,FILBOKA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME WITH UID,FILBB1A1
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTBB11
.
          PACK      CFNAME WITH UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME WITH UID,FILMA1A2
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTMA12
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patre1af";
          OPEN      PATRE1A1,"patre1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,THIRTY1;*155,OTCNSUPX,*204,OTCNR49A
          MOVE      IDDD,SITE
          MOVE      ZERO,EXIT
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          CALL      HOSP0000                * Get Hospital Code if Required
          GOTO      INIT9999
.
INIT9100  DISPLAY   *P1:24,*B,"Invalid Site Code.  ",*EL;
          CALL      HITENTER 
          MOVE      ONE,EXIT
.
INIT9999  RETURN
+
.*********************************************************************
.*        HOSP0000                               Called by ML0000    *
.*        Get hospital code                                          *
.*********************************************************************
HOSP0000  IF        IBCNMHOS=1
            DISPLAY   *P1:11,"    Hospital    : "
            MOVE      "11",EVERT
            MOVE      "19",ECOL
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
            OPEN      PATHSPA1,"pathspaf"
            CALL      PATHSPKY
            CLOSE     PATHSPA1
            DISPLAY   *P25:CVERT,PTHSNAME;
          ENDIF
          RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        Enter whether or not to continue                           *
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Print a Single Date, all Clinics"
.
OPTN1000  KEYIN     *P2:7,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9100 IF EQUAL       * exit
.
          BRANCH    OPTION,OPTN9000         * valid
.
          BEEP
          GOTO      OPTN1000 
.
.         set exit flag
.
OPTN9000  MOVE      ZERO,EXIT               * continue
          GOTO      OPTN9999
.
OPTN9100  MOVE      ONE,EXIT                * exit
.
OPTN9999  RETURN
+
.------------------------------------------------------------------------------
.                   KDAT0000
.         Input date for clinic schedule report
.------------------------------------------------------------------------------
KDAT0000  MOVE      SP8,DATE
          DISPLAY   *P1:9,*EL," Enter Date : "
.
          UNPACK    DATE INTO CCENT,CYEAR,CMON,CDAY
          MOVE      "15",CCOL
          MOVE      "9",CVERT
.
          MOVE      ZERO,CHIGHLT
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9100          * nothing entered
.
          PACK      DATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATE
.
          CALL      CONTQST
          BRANCH    CEXIT,KDAT9000,KDAT0000,KDAT9100
.
.         set exit flags
.
KDAT9000  MOVE      ZERO,EXIT               * continue
          GOTO      KDAT9999
.
KDAT9100  MOVE      ONE,EXIT                * exit
.
KDAT9999  RETURN
+
.------------------------------------------------------------------------------
.                   PRNT0000
.         Print the report
.------------------------------------------------------------------------------
PRNT0000  MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          CLOCK     TIME,CTIMEIS
.
          DISPLAY   *P1:24,*EL:
                    *P8:24,"Clin type :":
                    *P27:24,"Clin ID :":
                    *P44:24,"Date :":
                    *P65:24,"Slot :";
.
.         Read Clinic Master File A
.
          PACK      KEY24 WITH SITE,SP30
          CALL      RDSMASA2
.
PRNT1000  CALL      RDKMASA2
          BRANCH    OVRCD TO PRNT8000
.
          MATCH     OMASITE,SITE                 * match site id
          GOTO      PRNT8000 IF NOT EQUAL
.
          IF        IBCNMHOS=1
            MATCH     PTHSHOSP,OTMAHOSP          * Check for Correct Hospital
            GOTO      PRNT1000 IF NOT EQUAL
          ENDIF
.
.         Read Clinic Session Booking File A
.
          PACK      KEY28 WITH SITE,OMACLIN,DATE,OMASTART,SP30
          CALL      RDSBOKA1
.
PRNT2000  CALL      RDKBOKA1
          BRANCH    OVRCD OF PRNT1000
.
          DISPLAY   *P20:24,*V2LON,OBACTYP:
                    *P37:24,OBACLIN:
                    *P51:24,OBADATE:
                    *P72:24,OBASLOT
.
          MATCH     OBASITE,SITE          * match booking site to site
          GOTO      PRNT7000 IF NOT EQUAL
.
          MATCH     OBACLIN,OMACLIN       * match clinic id
          GOTO      PRNT7000 IF NOT EQUAL
.
          MATCH     OBADATE,DATE          * match booking date to date entered
          GOTO      PRNT7000 IF NOT EQUAL
.
          MATCH     OBASTRT,OMASTART      * match start time
          GOTO      PRNT7000 IF NOT EQUAL
.
          MATCH     OBACTYP,OMATYP        * match clinic type
          GOTO      PRNT7000 IF NOT EQUAL
.
          COMPARE   OBADAY,OMADAY         * compare day indicator
          GOTO      PRNT7000 IF NOT EQUAL
.
. ------- see if to suppress extended slots -------
.
          COMPARE   THREE,OBASTAT
          GOTO      PRNT3000 IF NOT EQUAL   * not extended slot
.
          COMPARE   ONE,OTCNSUPX
          GOTO      PRNT3000 IF NOT EQUAL   * not checking for suppression
.
          MATCH     ANSY,OTMASUPX
          GOTO      PRNT2000 IF EQUAL       * suppress the slot
.
PRNT3000  COMPARE   ZERO,OBASTAT          * check whether slot status is booked
          GOTO      PRNT6100 IF EQUAL
.
.         Read Clinic Session Booking File B
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD OF PRNT6100
.
          MATCH     SP8,OBAURNO
          GOTO      PRNT5000 IF NOT EQUAL
.
.         Read Pre Admission Master File 
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD OF PRNT2000       * invalid visit number
          GOTO      PRNT6000
.
.         Read Patient Master File 
.
PRNT5000  MOVE      ZERO,PCASE
          MOVE      OBAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF PRNT2000       * invalid UR number
.
PRNT6000  CALL      FNAM0000           * find patient name
          MOVE      DIM40,PNAMER
.
          MATCH     OBAOUTNO,ZEROVISN
          GOTO      PRNT6100 IF EQUAL
.
.         Read Person Responsible for Account File 
.
          MOVE      OBAOUTNO,KEY8           
          CALL      RDRESP1
          BRANCH    OVRCD OF PRNT6100
          GOTO      PRNT6500
.
PRNT6100  PACK      PKNAME,SP70,SP20
          MOVE      SP30,PKADD1
          MOVE      SP30,PKADD2
          MOVE      SP4,PKPOST
          MOVE      SP20,PKTELEB
          MOVE      SP20,PKSUBR
.
PRNT6500  CALL      PREC0000                * print report line
          GOTO      PRNT2000                * get next outboafd record
.
PRNT7000  MOVE      "80",CLNO
          GOTO      PRNT1000                * get next outmaafd record
.
.         End of Report
.
PRNT8000  COMPARE   ZERO,CPAGENO
          GOTO      PRNT9000 IF EQUAL       * no data displayed
.
          CALL      LINE0000
          PRINT     *N,*N,"** End of Report **",*N
.
          DISPLAY   *P1:24,*EL,*V2LON,*P20:24:
                    "** Report Generation Completed **";
          GOTO      PRNT9999
.
.         No Data Found
.
PRNT9000  DISPLAY   *P1:24,*EL,*V2LON,*B,*P20:24:
                    "** No Scheduled date found **",*W2
.
PRNT9999  RETURN
+
.------------------------------------------------------------------------------
.                   PREC0000
.         Print records
.------------------------------------------------------------------------------
.
PREC0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          COMPARE   TEN2,CLNO                 * new page --> no underline print
          GOTO      PREC0100 IF EQUAL
.
          IF        OTCNR49A = 0
            PRINT     *1,"|",*63,UNDLN30,UNDLN30,UNDLN9,*132,"|"
          ENDIF
.
          IF        OTCNR49A = 1
            PRINT     *1,"|",*60,UNDLN30,UNDLN30,UNDLN12,*132,"|"
          ENDIF
.
PREC0100  MOVE      SP1,DIMC1
          LOAD      DIMC1 BY PCASE FROM CCNOTES   * case notes
.
.         Standard format
.
          IF        OTCNR49A = 0
            PRINT     "|",*3,OBASLOT,*7,OBATIME:
                      *13,OBAVISIT;
          ENDIF
.
.         Dudley format (visit type on next line)
.
          IF        OTCNR49A = 1
            PRINT     "|",*3,OBASLOT,*7,OBATIME;
          ENDIF
.
          MOVE      SP20,DSRCDESC
.
          COMPARE   ZERO,OBASTAT         * If not booked then don't print
          GOTO      PREC2000 IF EQUAL
.
          UNPACK    PBDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          IF        OTCNR49A = 0
            PRINT     *17,OBAURNO,DIMC1,*27,CPCDATE;
          ENDIF
          IF        OTCNR49A = 1
            PRINT     *13,OBAURNO,DIMC1,*23,CPCDATE;
          ENDIF
.
          MATCH     OBASRCR,SP3
          IF        !@EQUAL
            MOVE      SP20,TDESC
            PACK      KEY5 WITH OSTCATG,OBASRCR
            CALL      RDCODE1
            MOVE      TDESC,DSRCDESC
          ENDIF
.
PREC2000  MOVE      NBK,PBNAMER
          LOAD      PBNAMER,OBASTAT,PNAMER,CAN,EXT,ATT,NATT
.
          BRANCH    OTCNR49A,PREC1000
.
.         Standard format
.
          SETLPTR   PKADD1,30
          SETLPTR   PADD2,25
          SETLPTR   PTELEP,11
          SETLPTR   PKADD2,30
          SETLPTR   PSUBRB,15
          SETLPTR   PPOST,5
          SETLPTR   PKSUBR,18
          UNPACK    PKNAME,KEY30
          PRINT     *38,PBNAMER,*63,PNAMER,*101,KEY30,*132,"|":
                    *N,"|",*38,DSRCDESC,*63,PADD1:
                    *101,*+,PKADD1,*132,"|":
                    *N,"|",*63,PADD2,*89,PTELEP,*101,PKADD2,*132,"|":
                    *N,"|",*38,OBACOMM,*63,PSUBRB,*79,PPOST,*84,PMEDI:
                    *101,PKSUBR,*119,PKPOST,*132,"|":
                    *N,"|",*101,PKTELEB,*132,"|"             * telep, not teleb
.
          ADD       SIX,CLNO                 * 5 lines plus sep. printed above
          CALL      CLRV0000
          GOTO      PREC9999
.
.         Dudley format
.
PREC1000  
          UNPACK    PKNAME,KEY30
          STRIP     PADD4
          PACK      ADDT1,PADD4,SP1,PPOST,SP40
          STRIP     PKADD4
          PACK      ADDT2,PKADD4,SP1,PKPOST,SP40
.
          PRINT     *34,PBNAMER,*60,PNAMER,*96,KEY30,*132,"|":
                    *N,"|",*7,OBAVISIT,*34,DSRCDESC,*60,PADD1,*96,PKADD1:
                    *132,"|":
                    *N,"|",*34,OBACOMP,*38,OBAINS,*42,OBACLASS:
                    *46,OTBBTRAN,*50,OTBBLNGI:
                    *60,PADD2,*96,PKADD2,*132,"|":
                    *N,"|",*60,PSUBRB:
                    *96,PKSUBR,*132,"|":
                    *N,"|",*60,ADDT1:
                    *96,ADDT2,*132,"|":
                    *N,"|",*34,OBACOMM,*60,PPOST," (P) ",PTELEP:
                    *96,PKPOST," (B) ",PKTELEB,*132,"|"
.
          ADD       "7",CLNO                 * 5 lines plus sep. printed above
          CALL      CLRV0000
          GOTO      PREC9999
.
PREC9999  RETURN
+
.------------------------------------------------------------------------------
.                   HEAD0000
.         Print report header
.------------------------------------------------------------------------------
HEAD0000  LOAD      DDAY USING OBADAY OF DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          UNPACK    OBADATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          CALL      HEAD132
.
          IF         IBCNMHOS=1
            PRINT     *N,*40,"Hospital  : ",PTHSHOSP,SP3,PTHSNAME
            ADD       TWO,CLNO
          ENDIF
.
          PRINT     *N,*40,"On ",DDAY,*53,CPCDATE,*64,"at ",OMASTART
          ADD       ONE,CLNO
.
          MOVE      "Invalid clinic type           ",DESCTYP
          PACK      KEY12 WITH SITE,OMATYP
          CALL      RDCTYA1
          BRANCH    OVRCD OF HEAD1000
.
          MOVE      OCTDESC,DESCTYP
.
HEAD1000  MOVE      "Invalid doctor name           ",DESCID
.
.         Read Clinic File 
.
          PACK      KEY20,SITE,OMACLIN,OMADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OMACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          MOVE      OCADESC,DESCID     * Save the clinic description
          BRANCH    OVRCD,HEAD2000
.
          MATCH     SP6,OCADOCT
          GOTO      HEAD1500 IF EQUAL
.
.         Read Doctor File 
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF HEAD1500
.
          MOVE      DGNAME,ANS
          CALL      CLIN0000         * find clinic name
          MOVE      DIM40,DESCID     * clinic id description
          GOTO      HEAD2000
.
HEAD1500  MOVE      OCADESC,DESCID   * clinic id description
.
HEAD2000  PRINT     *N,*1,"Clinic Type : ",OMATYP,SP3,DESCTYP:
                    *N,*1,"Clinic Id   : ",OMACLIN,SP3,DESCID
.
          ADD       THREE,CLNO
          BRANCH    OTCNR49A,HEAD2100
.
.         Standard format
.
          CALL      LINE0000
          PRINT     *1,"|",*2,"Slot",*7,"Time ",*13,"Vis",*17,"U/R No.  ":
                    *29,"D.O.B.  ",*38,"Booking Name/ Referral":
                    *63,"PMI Details ":
                    *101,"Person Fin. Resp.":
                    *125,"Nxt/Bk",*132,"|"
          CALL      LINE0000
          PRINT     "|",*132,"|"
          ADD       TWO,CLNO
          GOTO      HEAD9999
.
.         Dudley format
.
HEAD2100  CALL      LINE0000
          PRINT     *1,"|",*2,"Slot",*7,"Time ",*13,"U/R No.  ":
                    *25,"D.O.B.  ",*34,"Booking Details":
                    *60,"PMI Details ":
                    *96,"Person Fin. Resp.":
                    *125,"Nxt/Bk",*132,"|"
          CALL      LINE0000
          PRINT     "|",*132,"|"
          ADD       TWO,CLNO
.
HEAD9999  RETURN
+
.------------------------------------------------------------------------------
.                   LINE0000
.         Print page Separator
.------------------------------------------------------------------------------
LINE0000  PRINT     "*------------------------------------------":
                    "-------------------------------------------":
                    "---------------------------------------------*"
          ADD       ONE,CLNO
.
LINE9999  RETURN
+
.------------------------------------------------------------------------------
.                   FNAM0000
.         Find Patient Name
.------------------------------------------------------------------------------
FNAM0000  PACK      DIM40 WITH SP20,SP20
          MOVE      PSNAME,DIM40
          ENDSET    DIM40
          CALL      BNAM0000
          LENSET    DIM40
          APPEND    ", ",DIM40
          APPEND    PGNAME,DIM40
          CALL      BNAM0000
          APPEND    ", ",DIM40
          APPEND    PTITL,DIM40
          RESET     DIM40
          CMATCH    DIM40," "
          GOTO      FNAM9999 IF NOT EQUAL
          MOVE      SP30,DIM40
.
FNAM9999  RETURN
.
.------------------------------------------------------------------------------
.                   BNAM0000
.         Remove spaces in patients name
.------------------------------------------------------------------------------
BNAM0000  CMATCH    SP1,DIM40
          GOTO      BNAM9999 IF NOT EQUAL
          BUMP      DIM40 BY -1
          GOTO      BNAM0000 IF NOT EOS
.
BNAM9999  RETURN
+
.------------------------------------------------------------------------------
.                   CLIN0000
.         Find Clinic Name
.------------------------------------------------------------------------------
CLIN0000  PACK      DIM40 WITH SP20,SP20
          MOVE      DTITL,DIM40
          ENDSET    DIM40
          CALL      BCLI0000
          LENSET    DIM40
          APPEND    SP1,DIM40
          APPEND    ANS,DIM40
          CALL      BCLI0000
          APPEND    ". ",DIM40
          APPEND    DSNAME,DIM40
          RESET     DIM40
          CMATCH    DIM40," "
          GOTO      CLIN9999 IF NOT EQUAL
.
          MOVE      SP30,DIM40
.
CLIN9999  RETURN
.
.------------------------------------------------------------------------------
.                   BCLI0000
.         Remove spaces in clinic description
.------------------------------------------------------------------------------
BCLI0000  CMATCH    SP1,DIM40
          GOTO      BCLI9999 IF NOT EQUAL
          BUMP      DIM40 BY -1
          GOTO      BCLI0000 IF NOT EOS
.
BCLI9999  RETURN
+
.------------------------------------------------------------------------------
.                   CLRV0000
.         Initialise Variables
.------------------------------------------------------------------------------
CLRV0000  MOVE      SP6,OBACLIN             * outpatient variables
          MOVE      SP8,OBADATE
          MOVE      SP5,OBASTRT
          MOVE      ZERO,OBASLOT
          MOVE      ZERO,OBADAY
          MOVE      SP5,OBATIME
          MOVE      SP6,OBACTYP
          MOVE      SP3,OBAVISIT
          MOVE      SP30,OCTDESC
          MOVE      SP30,OCADESC
          MOVE      SP3,OBASRCR
          MOVE      ZEROVISN,OBAOUTNO
          MOVE      ZEROUR,OBAURNO
          MOVE      SP3,OBACOMP
          MOVE      SP3,OBACLASS
          MOVE      SP3,OBAINS
          MOVE      ZERO,OBASTAT
          MOVE      SP3,OBAATTN
          MOVE      SP6,OBADOCT
          MOVE      SP30,OBACOMM
          MOVE      SP3,OBACAT
.
          MOVE      SP20,PSNAME             * PMI variables
          MOVE      SP30,PGNAME
          MOVE      SP4,PTITL
          MOVE      SP8,PBDATE
          MOVE      SP20,PPSNAME
          PACK      PADD1,SP20,SP20
          PACK      PADD2,SP20,SP20
          PACK      PSUBRB,SP20,SP20
          PACK      PADD4,SP20,SP20
          MOVE      SP20,PPOST
          MOVE      SP20,PTELEP
          MOVE      SP1,PSEX
          MOVE      SP1,PMSTAT
          MOVE      ZERO,PSAGE
          MOVE      SP3,PCONT
          MOVE      SP3,PREG
          MOVE      SP3,PTYPE
          MOVE      SP20,POCCUP
          MOVE      SP10,PMEDI
.
          MOVE      SP1,DIMC1               * other variables
          MOVE      SP20,TDESC
          MOVE      SP30,PBNAMER
          MOVE      SP30,PNAMER
.
CLRV9999  RETURN
+
.------------------------------------------------------------------------------
.         I/O Routines
.------------------------------------------------------------------------------
.
          INC       STD001IO
          INC       OUTBOAIO/INC           * clinic booking A IO
          INC       OUTBB1IO/INC           * clinic booking B IO
          INC       OUTCLIIO/INC           * clinic ID IO
          INC       OUTCTYIO/INC           * clinic type IO
          INC       OUTMA1IO/INC           * Master IO
          INC       OUTSITIO/INC           * site IO
          INC       PATCODIO/INC           * codes IO
          INC       PATDO1IO/INC           * doctor IO
          INC       PATHSPIO/INC           * hospital file
          INC       PATMA1IO/INC           * patient master IO
          INC       PATPR1IO/INC           * pre admission IO
          INC       PATRE1IO/INC           * person responsible for account IO
.
          INC       PATHSPKY
.............................................................................

.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   IBAADT15                                                  *
.* Desc      :   Casemix Funding Listing                                   *
.***************************************************************************
.* Date      :   31/07/96                                                  *
.* Author    :   J.Tan                                                     *
.* Mods.     :                                                             *
.*        V10.03.01 13/05/2013 J.Tan         CAR 282824                    *
.*                  Added Transfer Trim Point                              *
.*        V10.01.01 27/01/2011 Ebon Clements CAR 233050                    *
.*                  Removed old/new functionality                          *
.*        V9.12.01  14/12/09 J.Tan        CAR 212419                       *
.*                  Mods to allow selecting Contract ID from New file      *
.*        V5.08.03  24/10/2000  Tonii  609 mods                            *
.*                  Mods to print new fields PTCMXLOS and PTCMDIFG, which  *
.*                  were added in PATCMXFD                                 *
.*        V5.08.02  16/08/2000  Caleb Sharman                              *
.*                  Changed Health Fund variables to be 8 chars            *
.*        V5.08.01  06/06/2000  Jill Habasque                              *
.*                  Changed from key10 to key14 for the health fund file   *
.*               V6.05.01 26/10/1998  Nicole Harrington                    *
.*                        Changed to display century                       *
.***************************************************************************
          INC       STD001FD
          INC       PATCMXFD/INC  
          INC       PATCMCFD/INC
          INC       PMSCIDFD/INC
          INC       PATFN1FD/INC
          INC       PATCODFD/INC
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CONTRTID  DIM       6
CODE      DIM       2
DASH      INIT      "-"
DIM15A    DIM       15A
DIM15     DIM       15
DIM18     DIM       18
ECOL1     FORM      2
FRCLM     DIM       5
TOCLM     DIM       5
STRCLM    DIM       3
ENDCLM    DIM       3
ZERO4     INIT      "0000"
ZERO8     INIT      "0000    "
.
PRGNAM    INIT      "Casemix Funding Master Listing"
PRGID     INIT      "IBAADT15"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * initialization
.                                       * open files
.                                       * display header
.
ML1000    CALL      OPTN0000            * select option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      OPFL0000            * Open file
          BRANCH    EXIT,ML1000
.
          CALL      KCLM0000            * Keyin range of claim code
          BRANCH    EXIT,ML1000
.
          CALL      CNID0000            * Select Contract ID
.
ML2400    CALL      CONTQST             * OK to continue ?
          BRANCH    CEXIT,ML3000,ML1000,ML9999
          BEEP
          GOTO      ML2400
.
ML3000    CALL      PRIN0000            * print all details
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000    
          CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening ";
.
          DISPLAY   *P60:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P60:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P60:24,"pmscidaf";
          OPEN      PMSCIDA1,"pmscidaf"
.
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          DISPLAY    *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF," = Claim Code Sequence";
.
OPTN1000  KEYIN      *P1:7,*EL,"Please Select : ",*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9000 IF EQUAL
.
          BRANCH     OPTION,OPTN9999
          BEEP
          GOTO       OPTN1000
.
OPTN9000  MOVE       TRUE,EXIT
OPTN9999  RETURN
+
.**********************************************************************
.*                             OPFL0000                               *
.*                Open casemix funding file                           *
.**********************************************************************
OPFL0000  OPEN      PATCMXA2,"patcmxaf"                           
.
OPFL9999  RETURN
+
.**********************************************************************
.*                             KCLM0000                               *
.*                keyin range of claim codes                          *
.**********************************************************************
KCLM0000  MOVE       ZERO,EXIT
          UNPACK     SP10,FRCLM,TOCLM
          MOVE       SP20,STRCLM
          MOVE       SP20,ENDCLM
.
          DISPLAY    *P1:10,*EF,"From Claim Code   :":
                     *P1:11,"To   Claim Code   :";
.
          MOVE      "10",EVERT
          MOVE      "20",ECOL               * keyin position
          MOVE      "30",ECOL1              * Display description position
          MOVE      SP3,ACODE
          MOVE      "CL",CODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM1000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,STRCLM  
          MOVE      STRCLM,FRCLM
          GOTO      KCLM5000
.
KCLM1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          MOVE      "START",FRCLM
          MOVE      ZERO,EXIT
.
KCLM5000  MOVE      "11",EVERT
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM6000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,ENDCLM  
          MOVE      ENDCLM,TOCLM
          GOTO      KCLM9999
.
KCLM6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          MOVE      "END  ",TOCLM
          MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.******************************************************************************
.*                                 KCOD0000              Called By : ML0000   *
.*                         Keyin codes                                        *
.******************************************************************************
KCOD0000  CALL      KEYINCOD
          BRANCH    EXIT,KCOD1000:          * Nothing entered
                         KCOD2000           * Exitchar entered
.
KCOD1000  DISPLAY   *PECOL1:EVERT,*V2LON,TDESC;
KCOD2000  RETURN
+
.******************************************************************************
.*                                 CNID0000              Called By : ML0000   *
.*                          Select Contract ID                                *
.******************************************************************************
.
CNID0000  MOVE      SP6,CONTRTID
          KEYIN     *P1:12,*EL,"Keyin Contract ID : ",*V2LON,CONTRTID
          ENDSET    CONTRTID
          APPEND    SP70,CONTRTID
          RESET     CONTRTID
.
.         PACK      KEY24,CONTRTID,SP70
.         CALL      RSPTCMX2
.         CALL      RKPTCMX2
.         BRANCH    OVRCD,CNID9000
.
.         MATCH     CONTRTID,PTCMCNID
.         GOTO      CNID9000 IF NOT EQUAL
.
          GOTO      CNID9999
.
CNID9000  DISPLAY   *P1:24,*EL,*B,"Invalid Contract ID. ";
          CALL      HITENTER
          GOTO      CNID0000
.
CNID9999  RETURN
.
.**********************************************************************
.*                             PRIN0000                               *
.*                Read file and print all details                     *
.**********************************************************************
PRIN0000  MOVE       ZERO,CPAGENO
          CLOCK      TIME,CTIMEIS
          MOVE       "60",CLNO
.
          DISPLAY   *P10:24,"Printing Claim Code : ":
                     *P35:24,*EL,*V2LON,PTCMCLMN,*P42:24,"- ",PTCMHFND;
.
          PACK       KEY24,CONTRTID,STRCLM,SP20
          CALL       RSPTCMX2                      * position to start of file
PRIN1000  CALL       RKPTCMX2
          BRANCH     OVRCD,PRIN8000
.
          DISPLAY    *P35:24,*EL,*V2LON,PTCMCLMN,*P42:24,"- ",PTCMHFND;
.
          MATCH      CONTRTID,PTCMCNID
          GOTO       PRIN8000 IF NOT EQUAL
.
          MATCH      SP3,ENDCLM                    * check if end claim entered?
          GOTO       PRIN2000 IF EQUAL
.
          MATCH      PTCMCLMN,ENDCLM               * within selected range?
          GOTO       PRIN8000 IF LESS              * no.
.
PRIN2000  CALL       PREC0000                       * print a Record  
          GOTO       PRIN1000
.
PRIN8000  COMPARE    ZERO,CPAGENO
          GOTO       PRIN9000 IF EQUAL
.
          CALL       UND132
          PRINT      *N,*N,"*** END OF REPORT  ***";
          GOTO       PRIN9999
.
PRIN9000  DISPLAY   *P1:24,*EL,*B,"No Data Available. ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.**********************************************************************
.*                                HEAD0000                            *
.*                          Print page header                         *
.**********************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *40,"Claim Code from :",FRCLM:
                           " to : ",TOCLM,*N:
                    *40,"    Contract ID :",CONTRTID
.
          CALL      UND132
          PRINT     *01,"| Claim Code   ":
                    *23,"| Health Fund":
                    *48,"| Casemix Code":
                    *63,"| Contract":
                    *89,"| Expected LOS":
                    *104,"| Detailed Inv.":
                    *119,"| Act.":
                    *125,"| Point":
                    *132,"|"
.
          ADD       FOUR,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.**********************************************************************
.*                                PREC0000                             *
.*                   Routine to print a PRECord                        *
.**********************************************************************
PREC0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTCMCLMN
          CALL      RDCODE1
          PACK      DIM15,TDESC
.
          PRINT     *01,"| ",PTCMCLMN,"-",DIM15,*23,"| ";
.
          MOVE      SP20,DIM15A
          MATCH     SP6,PTCMHFND
          GOTO      PREC1000 IF EQUAL
.
          PACK      KEY14,PTCMHFND,ZERO8,SP10
          CALL      RDFUND1
          PACK      DIM15A,HFNAME
          PRINT     *25,PTCMHFND,"-",DIM15A;
.
PREC1000  MOVE      SP70,DIM18
          MATCH     SP70,PTCMCNID
          GOTO      PREC2000 IF EQUAL
.
          PACK      KEY6,PTCMCNID,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,PREC2000
.
          PACK      DIM18,SP1,DASH,SP1,PMCIDESC

.
PREC2000  PRINT     *48,"| ",PTCMCASM:
                    *63,"| ",PTCMCNID,DIM18:
                    *89,"|   ",PTCMXLOS;
.
          IF        PTCMDIFG = 1
            PRINT     *104,"|     ",DYES;
          ELSE
            PRINT     *104,"|     ",DNO;
          ENDIF
.
          PACK      KEY9,PTCMCASM,SP10
          CALL      RDPTCMC1
          IF        PTCAACTV = 1
            PRINT     *119,"| ",DYES;
          ELSE
            PRINT     *119,"| ",DNO;
          ENDIF
.
          PRINT     *125,"| ",PTCMTRPT:
                    *132,"|"
          ADD       ONE,CLNO
.
PREC9999   RETURN
+
.
          INC       STD001IO
          INC       PATDSCOD
          INC       KEYINCOD
.
          INC       PATCMXIO/INC
          INC       PATCMCIO/INC
          INC       PATFN1IO/INC
          INC       PATCODIO/INC
          INC       PMSCIDIO/INC

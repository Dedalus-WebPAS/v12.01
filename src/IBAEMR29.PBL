.***************************************************************************
.* System    :   Emergency                                                 *
.***************************************************************************
.* Program   :   IBAEMR29                                                  *
.* Desc      :   Patient Location Report                                   *
.***************************************************************************
.* Date      :   16/10/2012                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will print the emergency location report     *
.*           :                                                             *
.* Modifications  :                                                        *
.*        V10.03.00 16/10/2012  Ebon Clements    CAR 266929                *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
          INC       TFILEVAR/INC
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       EMRHISFD/INC
          INC       EMRLOCFD/INC
          INC       EMRSITFD/INC
          INC       EMRUNKFD/INC
          INC       EMRVISFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       IBASEQFD/INC
          INC       WEBERRFD/INC
.
.         Temporary File Definition
.         -------------------------
.
.         Filename : CSCXXXXX.dat
.
REPTEMP1  IFILE   SQL, FIXED=52, KEY="U1-3,4-11,12-19,20-27"
.
.Name     Type     Size    Physical  Start  Desc
.----     ----     ----    --------  -----  ---------------
XXXXLOCN  DIM       3      3         1      Location Code
XXXXURNO  DIM       8      8         4      U/R
XXXXIDAT  DIM       8      8         12     Date Into Location
XXXXITIM  DIM       8      8         20     Time into Location
XXXXODAT  DIM       8      8         28     Date Out of Location
XXXXOTIM  DIM       8      8         36     Time Out of Location
XXXXVISN  DIM       8      8         44     ED Visit Number
.
.End of Record                       52
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
TEMPFILE  DIM       8
UKEY      INIT      "  52 U1-3,4-11,12-19,20-27"
.

.
. LOCAL VARIABLE DEFINITION
. -------------------------
CMDLINE   DIM       127
DIM2A     DIM       2
DIM2B     DIM       2
DIM2C     DIM       2
FINISHED  FORM      1
FROMDATE  DIM       8
LOCATION  DIM       3
LOCNDESC  DIM       25
PRNTNAME  DIM       34
PRNTIDAT  DIM       10
PRNTITIM  DIM       8
PRNTODAT  DIM       10
PRNTOTIM  DIM       8
TODATE    DIM       9
SAVELOCN  DIM       3
SITECODE  DIM       3
SITEDESC  DIM       25
.
PRGID     INIT      "IBAEMR29"
PRGNAM    INIT      "Patient Location Report"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
.
          CALL      KSIT0000               * keyin the ED site code
          BRANCH    EXIT,MAIN1000
.
          CALL      KDAT0000               * keyin the date range
          CALL      KLOC0000               * keyin the location code
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN3000:        * yes
                          MAIN1000:        * no
                          MAIN1000         * cancel
.
MAIN3000  CALL      CREA0000               * create report tempfile
          CALL      LDAT0000               * load tempfile by visit date
.
          CALL      PRNT0000               * process
          CALL      KILL0000               * delete report tempfile
.
MAIN9999  CHAIN     PGM                    * chain back to program
.
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialisation                               *
.****************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
.
          DISPLAY   *P64:24,"emrhisaf";
          OPEN      EMRHISA1,"emrhisaf"
.
          DISPLAY   *P64:24,"emrlocaf";
          OPEN      EMRLOCA1,"emrlocaf"
.
          DISPLAY   *P64:24,"emrsitaf";
          OPEN      EMRSITA1,"emrsitaf"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA6,"emrvisaf"
.
          DISPLAY   *P64:24,"emrunkaf";
          OPEN      EMRUNKA1,"emrunkaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"ibaseqaf";
          OPEN      IBASEQA1,"ibaseqaf"
.
INIT9999  RETURN
.
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Patient Location Report"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.****************************************************************************
. Keyin the site code
.****************************************************************************
KSIT0000  KEYIN     *P1:9,*EF,"ED Site Code  : ",SITECODE;
.
          MOVE      SP70,SITEDESC
          PACK      SITECODE,SITECODE,SP70
.
          MATCH     SP70,SITECODE                * Exit if blank site code
          GOTO      KSIT9500 IF EQUAL
.
          PACK      KEY3,SITECODE,SP70
          CALL      RDEMSIT1
          BRANCH    OVRCD,KSIT9000
.
          MOVE      EMSTDESC,SITEDESC
          DISPLAY   *P21:9,SITEDESC
          MOVE      ZERO,EXIT
          GOTO      KSIT9999
.
KSIT9000  DISPLAY   *P1:24,*EL,*B,"Invalid ED Site Code.  ";
          CALL      HITENTER
          GOTO      KSIT0000
.
KSIT9500  MOVE      ONE,EXIT
.
KSIT9999  RETURN
.
.********************************************************************
. Keyin the date range
.********************************************************************
KDAT0000  DISPLAY   *P1:10,*EF:
                    *P1:10,"Arrival Date From :":
                    *P1:11,"Arrival Date To   :"
.
KDAT1000  CALL      IBACLOCK 
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY 
.
          MOVE      TWENTY1,CCOL
          MOVE      TEN,CVERT
.
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT0000
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          MOVE      TWENTY1,CCOL
          MOVE      TEN1,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT1000
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT8000 IF LESS
.
          GOTO      KDAT9999
.
KDAT8000  DISPLAY   *P1:24,*B,*V2LON:
                    "** FROM Date Greater Than TO Date ** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      KDAT0000
.
KDAT9999  RETURN
.
.****************************************************************************
. Keyin the location code    
.****************************************************************************
KLOC0000  KEYIN     *P1:12,*EF,"Location Code : ",LOCATION;
.
          MOVE      SP70,LOCNDESC
          PACK      LOCATION,LOCATION,SP70
.
          MATCH     SP70,LOCATION                * Exit if blank site code
          GOTO      KLOC8000 IF EQUAL
.
          PACK      KEY3,LOCATION,SP70
          CALL      RDEMLOC1
          BRANCH    OVRCD,KLOC9000
.
          MOVE      EMLODESC,LOCNDESC
          DISPLAY   *P21:12,LOCNDESC
          GOTO      KLOC9999
.
KLOC8000  MOVE      "All",LOCNDESC
          DISPLAY   *P17:12,LOCNDESC
          GOTO      KLOC9999
.
KLOC9000  DISPLAY   *P1:24,*EL,*B,"Invalid Location Code.  ";
          CALL      HITENTER
          GOTO      KLOC0000
.
KLOC9999  RETURN
.
.********************************************************************
. Load ed visits and location details                              
.********************************************************************
LDAT0000  PACK      KEY27,SITECODE,FROMDATE,SP70
          CALL      RSEMVIS6
LDAT1000  CALL      RKEMVIS6                     * Read ED visits
          BRANCH    OVRCD,LDAT9999
.
          MATCH     EMVISITE,SITECODE            * Correct ED Site
          GOTO      LDAT9999 IF NOT EQUAL
.
          MATCH     EMVIDATE,TODATE              * End if after arrival date to
          GOTO      LDAT9999 IF LESS
.
          COMPARE   FOUR,EMVISTAT                * Skip cancelled ed visit
          GOTO      LDAT1000 IF EQUAL
.
          MOVE      SP70,SAVELOCN                * Clear save location
          MOVE      ZERO,FINISHED
.
          PACK      KEY22,DEMVIADM,SP70
          CALL      RSEMHIS1
LDAT2000  CALL      RKEMHIS1                     * Read ED history
          BRANCH    OVRCD,LDAT8000
.
          MATCH     EMHIVIS,DEMVIADM             * Correct ED visit
          GOTO      LDAT8000 IF NOT EQUAL
.
LDAT3000  MATCH     SP70,SAVELOCN
          IF        @EQUAL
            MATCH     SP70,EMHILOC                 * Skip of location blank
            GOTO      LDAT2000 IF EQUAL
.
            MATCH     SP70,LOCATION                * Location filter
            IF        !@EQUAL
              MATCH     LOCATION,EMHILOC
              GOTO      LDAT2000 IF NOT EQUAL
            ENDIF
.
            MOVE      EMHILOC,SAVELOCN           * Save location being processed
            PACK      XXXXLOCN,EMHILOC           * Save location
            PACK      XXXXIDAT,EMHIDAT           * Save date into location
            PACK      XXXXITIM,EMHITIM           * Save time into location
            GOTO      LDAT2000   
          ELSE
            MATCH     EMHILOC,SAVELOCN           * Patient is still in the same
            GOTO      LDAT2000 IF EQUAL          * location
          ENDIF
.
          PACK      XXXXODAT,EMHIDAT,SP70        * Save date out of location
          PACK      XXXXOTIM,EMHITIM,SP70        * Save time out of location
          MOVE      ZERO,FINISHED                * Not last history recods
          GOTO      LDAT8500
.
LDAT8000  MATCH     SP70,SAVELOCN                * Nothing to report for this 
          GOTO      LDAT1000 IF EQUAL            * visit
.
          MOVE      ONE,FINISHED                 * Yes last history recods
.
LDAT8500  IF        EMVISTAT<>1
            PACK      XXXXODAT,EMHIDAT,SP70      * Discharge date of location
            PACK      XXXXOTIM,EMHITIM,SP70      * Save time out of location
          ENDIF
.
          PACK      XXXXURNO,EMVIURNO            * Temp file U/R
          PACK      XXXXVISN,DEMVIADM            * Temp file visit
          PACK      KEY27,XXXXLOCN,XXXXURNO,XXXXIDAT,XXXXITIM,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
.
          MOVE      SP70,SAVELOCN                * Clear save location
          UNPACK    SP70,XXXXLOCN,XXXXURNO,XXXXIDAT,XXXXITIM
          UNPACK    SP70,XXXXODAT,XXXXOTIM,XXXXVISN
          IF        FINISHED=1
            MOVE      ZERO,FINISHED
            GOTO      LDAT1000
          ENDIF
          GOTO      LDAT3000
.
LDAT9999  RETURN
.
.**************************************************************************
. Print the report
.**************************************************************************
PRNT0000  CALL      HEAD0000                     * Print page heading
.
          PACK      KEY27,SP70
          CALL      RSTEMP1
PRNT1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRNT9000
.
          CALL      PLIN0000                     * Print report line
          GOTO      PRNT1000
.
PRNT9000  CALL      ENDD0000
.
PRNT9999  RETURN
.
.***************************************************************************
. Print report line
.***************************************************************************
PLIN0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      "Invalid U/R",PACFNAME
          PACK      KEY8,XXXXURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PLIN1000
.
          PACK      KEY8,XXXXURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PLIN1000
.
          GOTO      PLIN2000
.
PLIN1000  PACK      KEY8,XXXXVISN,SP70
          CALL      RDEMUNK1
          BRANCH    OVRCD,PLIN2000
.
          MOVE      EMUNDET2,PGNAME
          MOVE      EMUNDET1,PSNAME
          MOVE      SP70,PTITL
.
PLIN2000  MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PRNTNAME
.
          PACK      KEY3,XXXXLOCN,SP70
          CALL      RDEMLOC1
          IF        OVRCD=1
            MOVE      XXXXLOCN,EMLODESC
          ENDIF
.
          MOVE      SP70,PRNTIDAT
          MATCH     SP70,XXXXIDAT
            IF        !@EQUAL
            UNPACK    XXXXIDAT INTO CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,PRNTIDAT
          ENDIF
.
          MOVE      SP70,PRNTITIM
          MATCH     SP70,XXXXITIM
          IF        !@EQUAL
            UNPACK    XXXXITIM,DIM2A,DIM2B,DIM2C
            PACK      PRNTITIM,DIM2A,COLON,DIM2B,COLON,DIM2C
          ENDIF
.
          MOVE      SP70,PRNTODAT
          MATCH     SP70,XXXXODAT
          IF        !@EQUAL
            UNPACK    XXXXODAT INTO CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,PRNTODAT
          ENDIF
.
          MOVE      SP70,PRNTOTIM
          MATCH     SP70,XXXXOTIM
          IF        !@EQUAL
            UNPACK    XXXXOTIM,DIM2A,DIM2B,DIM2C
            PACK      PRNTOTIM,DIM2A,COLON,DIM2B,COLON,DIM2C
          ENDIF
.
          PRINT     *1,"| ",EMLODESC:
                    *29,"| ",XXXXURNO:
                    *40,"| ",PRNTNAME:
                    *77,"| ",XXXXVISN:
                    *88,"| ",PRNTIDAT,SP1,PRNTITIM:
                    *110,"| ",PRNTODAT,SP1,PRNTOTIM:
                    *132,"|"
.
PLIN7000  ADD       ONE,CLNO
.
PLIN9999  RETURN
.
.***************************************************************************
. Print a new page 
.***************************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *40,"Ed Site : ",SITEDESC
.
          UNPACK    FROMDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"Arrival Date From : ",CPCDATE," to ";
.
          UNPACK    TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE
.
          MATCH     SP70,LOCATION
          IF        @EQUAL
            PRINT     *40,"Locations : ",LOCNDESC
          ELSE
            PRINT     *40,"Location : ",LOCNDESC
          ENDIF
.
          CALL      UND132
.
          PRINT     *1,"| Location":
                    *29,"| U/R":
                    *40,"| Patient Name":
                    *77,"| Visit":
                    *88,"| Date/Time into":
                    *110,"| Date/Time out":
                    *132,"|"
.
          PRINT     *1,"|":
                    *29,"|":
                    *40,"|":
                    *77,"|":
                    *88,"| Location":
                    *110,"| of Location":
                    *132,"|"
.
          CALL      UND132
          MOVE      TEN,CLNO
.
HEAD9999  RETURN
.
.***************************************************************************
. Print end of report 
.***************************************************************************
ENDD0000  CALL      UND132
.
          PRINT     *N,*55,"***  End of Report  ***"
.
ENDD9999  RETURN
.
.*********************************************************************
.* Create a new temporary file                                       *
.*********************************************************************
CREA0000  CALL      TFILENAM                     * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      REPTEMP1,TEMPFILE            * open temp index file
.
CREA9999  RETURN
.
.****************************************************************************
.* Close and erase the temporary file                                       *
.****************************************************************************
KILL0000  CLOSE     REPTEMP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999  RETURN
.
.****************************************************************************
.*        IO ROUTINE FOR TEMPORARY FILE                                     *
.****************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          READ      REPTEMP1,KEY27;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   READ      REPTEMP1,KEY27;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    REPTEMP1;XXXXLOCN,XXXXURNO,XXXXIDAT,XXXXITIM:
                             XXXXODAT,XXXXOTIM,XXXXVISN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     REPTEMP1,KEY27;XXXXLOCN,XXXXURNO,XXXXIDAT,XXXXITIM:
                             XXXXODAT,XXXXOTIM,XXXXVISN
          RETURN
.
. **************************************************************************
.         I/O Includes                                                     *
.***************************************************************************
.
          INC       STD001IO
          INC       TFILENAM
.
          INC       EMRHISIO/INC
          INC       EMRLOCIO/INC
          INC       EMRSITIO/INC
          INC       EMRUNKIO/INC
          INC       EMRVISIO/INC
          INC       IBASEQIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       WEBERRIO/INC
.

. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAHOS23                                            *
. *                                                                           *
. *     FUNCTION:         Exceptions Report                                   *
. *                                                                           *
. *     DATE WRITTEN:     29.07.88                                            *
. *                                                                           *
. *     AUTHOR:           S.Barcham                                           *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
. *       V12.00.01 16/05/2025  Ebon Clements  TSK 0955096                    *
. *                 Alphanueric visit number changes                          *
. *       V10.13.01 05/12/2018  Don Nguyen     TSK 0838558                    *
. *                 Deleted temp file (PSTROL) after processing               *
. *       V10.10.02 05/04/2017 Ania P CAR 260631                              *
. *                 Removed use of PORT                                       *
. *       V10.01.01 03/02/2011 Jill Parkinson  CAR 233088                     *
. *                 Added pmsvx1af                                            *
. *       V9.09.00  24/01/2008  Jill Habasque  CAR 159423                     *
. *                 Ported from 5.15                                          *
. *       V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *      V5.07.B02  28/01/1999  Nicole Harrington                             *
. *                 507 rebounds                                              *
. *      V5.07.B01  10/11/1998  Davin  507                                    *
. *                 Mods for 507                                              *
.******************************************************************************
. *       V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
. *                 Changed temp file name "pstidx" to standard format        *
.******************************************************************************
. *                       V3.02 19/08/88 G.Williams                           *
. *                             Add claims details                            *
. *                       V5.00 17/05/89 DIG                                  *
. *                             Changed to standard and                       *
. *                             modified print for 8 digit                    *
. *                             admission numbers                             *
. *                       V5.01 27/05/89 J.Tan                                *
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMI1FD).     SRF 100750                    *
. *                       V5.02 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                       V5.03 17/06/89 J.Tan                                *
. *                             Removed Standby file                          *
. *                             SRF 101101                                    *
. *                    V5.01.01 05/07/89 M.Ohleiter                           *
. *                             Changes for 8 char ur and                     *
. *                             admiss no, standard - 5.01                    *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *                                                                           *
. *****************************************************************************
.
.
          INC       STD001FD
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATMA1FD/INC
          INC       PATWR1FD/INC
          INC       PATWC1FD/INC
          INC       PATWMAFD/INC
          INC       PATWVEFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.               VARIABLES DEFINITIONS
.
PSTROL    FILE      ASCII, FIXED=256
.
TEMPFILE  IFILE     SQL, FIXED=96, KEY="u1-19"
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.ACLAIM         DIM        3            3           1
XDAADMNO        DIM        8            8           4
DUNIQUE         DIM        8            8          12
.AWARD          DIM        3            3          20
.ABED           DIM        3            3          23
.PGNAME         DIM       25           25          26
.PSNAME         DIM       20           20          51
.AFUNDH         DIM        6            6          71
.AFNDTB         DIM        4            4          77
.AFNDMM         DIM       10           10          81
.ATYPE          DIM        3            3          91
.ADYHOSP        FORM       2            2          94
. ----- EOR 96 -----
.
.
FNAMER    DIM       8
FNAMEI    DIM       8
.
CMDLINE   DIM       50
CODEA     INIT      "A "
CODECL    INIT      "CL"
CUDATE    DIM       8
.
DIM10     DIM       10
DIM11     DIM       11
.
INSDETS   DIM       25
.
JYEAR     FORM      2
.
LNO       FORM      2
LYEAR     FORM      "366"
.
OPCND     FORM      1
OYEAR     FORM      "365"
.
PAGENO    FORM      3
.
S18       FORM      "18"
STATUS    FORM      1
.
TIMEIS    DIM       8
.
UNIQUE    FORM      8
.
WSTAT     FORM      1
.
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
.
YTDATE    DIM       8
.
PRGID     INIT      "IBAHOS23"
PRGNAM    INIT      "Exceptions Report"
VERSION   INIT      "V12.01.00"
+ 
.         START OF PROGRAM 
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSVX1A2,"pmsvx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A4,"patwr1af"
.
          DISPLAY   *P64:24,"patwc1af";
          OPEN      PATWC1A1,"patwc1af"
.
          DISPLAY   *P64:24,"patwmabf";
          OPEN      PATWMAB1,"patwmabf"
.
          DISPLAY   *P64:24,"patwvetf";
          OPEN      PATWVET1,"patwvetf"
.
          DISPLAY   *P64:24,"patwvetf";
          OPEN      PATWVET1,"patwvetf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER

.        START OF PROGRAM
.
START     DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Claim Number Sequence"
.
          KEYIN     *P1:7,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF STPROC
          BEEP
          DISPLAY   *P30:7,*EL,*V2LON,"Invalid selection",*W2;
          GOTO      START
.
.            PROCESS START
.
STPROC    CALL      INDZD
          CLOCK     TIME,TIMEIS
          MOVE      ONE,UNIQUE
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Current Admissions **",*W;
          DISPLAY   *P1:24,*EL,*P20:24,"Current Admission No :":
                    *P55:24,"Scanning :";
          MOVE      "2",WSTAT
          CALL      ADMFN
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing On Leave Admissions **",*W;
          DISPLAY   *P1:24,*EL,*P20:24,"On Leave Admission No :":
                    *P55:24,"Scanning :";
          MOVE      "4",WSTAT
          CALL      ADMFN
.
          GOTO      REPORT
.
.               READ ADMISSION FILE

ADMFN   PACK       KEY9 WITH WSTAT,SP8
        CALL       RDSMISS2
.
NEXT5   CALL       RDKMISS2
        COMPARE    ONE,OVRCD
        RETURN     IF EQUAL
.
        COMPARE    WSTAT,ASTAT
        RETURN     IF NOT EQUAL
.
        DISPLAY    *P72:24,AADMNO;
.
        MOVE        AURNO,KEY8
        CALL        RDMAST1
        BRANCH      OVRCD OF NOMAST
.
        MATCH       SP3 TO AWARD
        GOTO        WRITETM IF NOT EQUAL
.
.       Get the ward/bed standby
.
        MOVE        AADMNO,WSTBY
        PACK        KEY14,WSTBY,SP6
        CALL        RDSWARD4
        CALL        RDKWARD4
        BRANCH      OVRCD TO WRITETM
.
        MATCH       AADMNO TO WSTBY
        GOTO        WRITETM IF NOT EQUAL
.
        MOVE        WWARD TO AWARD
        MOVE        WBED TO ABED
.
WRITETM CALL        WRTEMP
        ADD         ONE,UNIQUE
        GOTO        NEXT5
.
NOMAST   DISPLAY    *P1:24,*EL,*B,*V2LON:
                    "** Adm.no : ",AADMNO," has No Master File ",*W2:
                    *P1:24,*EL;
        RETURN
.
.              PRINT REPORT
.
REPORT   DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                    "** Generating Report **",*W1;
         DISPLAY    *P1:24,*EL,*P20:24,"Printing :";
.
         MOVE       ZERO TO PAGENO
         MOVE       "60",LNO
         MOVE       "                   " TO KEY19
         CALL       RDSTEMP
.
NXTTEMP  CALL       RDKTEMP
         BRANCH     OVRCD OF ENDREP
         DISPLAY    *P40:24,*V2LON,AADMNO;
.
         PACK       INSDETS USING AFUNDH,SP2,AFNDTB,SP3,AFNDMM
.
         COMPARE    "55",LNO
         CALL       HEAD IF NOT LESS
.
         PACK       KEY5 WITH CODECL,ACLAIM
         CALL       RDCODE1
         BRANCH     OVRCD TO NXTT32
.
         MOVE       TDESC TO DIM11
.
.        Check if this is a claim
.
         MOVE       ZERO,FORM2
CLLP1    ADD        ONE,FORM2
         COMPARE    SIX,FORM2
         GOTO       NXTT32 IF EQUAL
.
         LOAD       ANS USING FORM2 OF TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
         CMATCH     ANSW,ANS
         GOTO       WCLM IF EQUAL
.
         CMATCH     ANSM,ANS
         GOTO       MCLM IF EQUAL
.
         CMATCH     ANSV,ANS
         GOTO       VCLM IF EQUAL
.
         GOTO       CLLP1
.
.        Workcare claim
.
WCLM     MOVE       AADMNO,KEY8
         CALL       RDWCOM1
         BRANCH     OVRCD OF NOCLMD
.
         MOVE       "CLAIM DETAILS ON FILE    ",INSDETS
         GOTO       NXTT32
.
.        T.A.C. claim
.
MCLM     MOVE       AADMNO,KEY8
         CALL       RDWMAB1
         BRANCH     OVRCD OF NOCLMD
.
         MOVE       "CLAIM DETAILS ON FILE    ",INSDETS
         GOTO       NXTT32
.
.        Veteran Affairs claim
.
VCLM     MOVE       AADMNO,KEY8
         CALL       RDWVET1
         BRANCH     OVRCD OF NOCLMD
.
         PACK       INSDETS WITH SP5,VCLMNO
         GOTO       NXTT32
.
.        No claim details on file
.
NOCLMD   MOVE       SP30,INSDETS
.
.           Get the admission type
.
NXTT32   MOVE       SP10,DIM10
         PACK       KEY5 WITH CODEA,ATYPE
         CALL       RDCODE1
         BRANCH     OVRCD OF NOATYP
.
         MOVE       TDESC,DIM10
.
NOATYP   PRINT      *1,"! ",DIM11,*15,"!",AADMNO,*24,"! ",PSNAME," ":
                    PGNAME,*74,"! ",AWARD,"/",ABED,*84,"! ",DIM10:
                    *97,"!   ",ADYHOSP,*104,"! ",INSDETS,*132,"!"
.
         ADD        ONE,LNO
         GOTO       NXTTEMP
.
ENDREP   COMPARE   ZERO TO PAGENO
         CALL      PRTLN IF NOT EQUAL
.
         PRINT     *N,*N,*1,"**** End Of Report ****";
.
         DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                   "** Report Generation Completed **",*W2;
         CALL       KILLIDZ
         GOTO      START
+
.                 HEADING
.
HEAD      COMPARE   ZERO TO PAGENO
          CALL      PRTLN IF NOT EQUAL
.
          ADD       ONE,PAGENO
          CALL      IBACLOCK
          CALL      HEAD132
.
          CALL      PRTLN
          PRINT     *1,"! Claim Type",*15,"! Admiss",*24,"! Patients Name":
                    *74,"! Ward/",*84,"! Admission",*97,"! Days":
                    *104,"! Health Fund Details",*132,"!":
                    *N,*1,"!",*15,"! Number",*24,"!",*74,"! Bed",*84:
                    "! Type",*97,"! Hosp",*104,"! Code",*114,"Table",*121:
                    "Membership",*132,"!"
          CALL      PRTLN
          MOVE      NINE,LNO
.
          RETURN
.
PRTLN     PRINT     *1,"*----------------------------------------":
                    "----------------------------------------------":
                    "--------------------------------------------*"
          RETURN
.
.       Create an indexed file based on port
.       for accumulating Admission Type totals
.
INDZD   MOVE        ZERO,STATUS
.
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
.
        CALL          KILLIDZ
.
        PREP        PSTROL,FNAMER
        WRITE       PSTROL,SEQ;"isbuild ",FNAMEI," 96 u1-19"
        WEOF        PSTROL,SEQ
.
        MOVE        ONE,STATUS
.
        CLEAR       CMDLINE
        APPEND      "sh ",CMDLINE
        APPEND      FNAMER,CMDLINE
        APPEND      ".txt",CMDLINE
        RESET       CMDLINE
.
        DISPLAY    *P1:10,*EF,*P1:10
        EXECUTE     CMDLINE,TASKID
.
        CLOSE       PSTROL,DELETE
.
        MOVE        ZERO,OPCND
        TRAP        NOINDZ IF IO
        OPEN	    TEMPFILE,FNAMEI
        TRAPCLR     IO
ENDC    RETURN
.
NOINDZ  MOVE        ONE,OPCND
        DISPLAY     *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMEI," Not Found **",*W4;
        TRAPCLR     IO
        RETURN
+
.
.       Deleting an indexed file based on port
.
KILLIDZ CLOSE      TEMPFILE
.
        MOVE       ZERO,STATUS
.
        DISPLAY    *P1:24,*EL,*P20:24,*V2LON:
                   "** Deleting Indexed File **",*W;
.
        CLEAR      CMDLINE
        APPEND     "iserase ",CMDLINE
        APPEND     FNAMEI,CMDLINE
        RESET      CMDLINE
        EXECUTE    CMDLINE,TASKID
        DISPLAY    *P1:10,*EF,*P1:10
        RETURN
.
.
.           I/O FOR THE TEMP FILE
.
WRTEMP   MOVE      AADMNO,XDAADMNO
         MOVE      UNIQUE,DUNIQUE
         PACK      KEY19 WITH ACLAIM,XDAADMNO,DUNIQUE
         WRITE     TEMPFILE,KEY19;ACLAIM,XDAADMNO,DUNIQUE,AWARD,ABED,PGNAME:
                                  PSNAME,AFUNDH,AFNDTB,AFNDMM,ATYPE,ADYHOSP
         RETURN
.
RDKTEMP  MOVE      ZERO,OVRCD
         READKS    TEMPFILE;ACLAIM,XDAADMNO,DUNIQUE,AWARD,ABED,PGNAME:
                            PSNAME,AFUNDH,AFNDTB,AFNDMM,ATYPE,ADYHOSP
         GOTO      OVERCOND IF OVER
         MOVE      DUNIQUE,UNIQUE
         MOVE      XDAADMNO,AADMNO
         RETURN
.
RDSTEMP  RESET     KEY19
         READ      TEMPFILE,KEY19;;
         RETURN
.
         INC       STD001IO
         INC       TFILENAM
.
         INC       PATCODIO/INC
         INC       PATMA1IO/INC
         INC       PATMI1IO/INC
         INC       PMSVX1IO/INC
         INC       PATWR1IO/INC
         INC       PATWC1IO/INC
         INC       PATWMAIO/INC
         INC       PATWVEIO/INC
         INC       IBASEQIO/INC
         INC       WEBERRIO/INC
.
ENDIT    CHAIN      PGM
         STOP

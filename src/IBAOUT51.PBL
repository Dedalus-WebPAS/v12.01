.*****************************************************************************
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:     IBAOUT51                                            *
.*                                                                           *
.*     FUNCTION:         CLINIC ID MASTER LISTING                            *
.*                                                                           *
.*     DATE WRITTEN:     22.06.88                                            *
.*                                                                           *
.*     MODIFICATIONS:                                                        *
.*                                                                           *
.*     V10.07.01  29/10/2015  Ebon Clements CAR 268970                       *
.*                Removed open of OUTCLIA                                    *
.*     V9.02.01   15/04/2002  Dean Cramer                                    *
.*                Port from 5.09 to 9.02                                     *
.*     V5.08.B01  06/03/2000  Steve Armstrong                                *
.*                Mods for effective date field                              *
.*****************************************************************************
.*     V5.07.B01  Nicole Harrington  507 rebound 095                         *
.*                Mods to use HEAD132                                        *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       OUTCLIFD/INC
          INC       OUTCONFD/INC
.
          INC       PATDO1FD/INC
.
.
. LOCAL VARIABLES
. ---------------
DOCDESC   DIM       20
.
END6      DIM       6
.
FIRST     DIM       1
.
START6    DIM       6
.
XDIM6     DIM       6
.
YDIM6     DIM       6
.
.
. PROGRAM CONSTANTS
. -----------------
FSTREC    INIT      "START "
LN55      FORM      "55"
LSTREC    INIT      "FINISH"
.
.
PRGID     INIT      "IBAOUT51"
PRGNAM    INIT      "Clinic Id Register"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          MOVE      IDDD,OCASITE        /* Site code held in common */
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
.
          PACK      CFNAME WITH UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Clinic ID Sequence"
.
BEGIN     KEYIN     *P1:7,*EF,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION,SDISEA
          BEEP
          GOTO      BEGIN
.
SDISEA    DISPLAY   *P1:11,"Keyin Clinic ID From   :":
                    *P1:13,"             ID To     :"
          MOVE      SP6,START6
          MOVE      SP6,END6
.
KEYST     KEYIN     *P29:13,*EL,*P29:11,*EL,*V2LON,START6,*P29:11,*DV,START6
          RESET     START6
          GOTO      CONT1 IF NOT EOS
.
          MOVE      SP6,START6
          DISPLAY   *P29:11,*V2LON,FSTREC
          GOTO      CONT2
.
CONT1     ENDSET    START6
          APPEND    SP6,START6
          RESET     START6
.
          PACK      KEY20,OCASITE,START6,SP20
          CALL      RDSCLIA1
          CALL      RDKCLIA1
          IF        OVRCD = 0
            MATCH     START6,OCACLIN
            GOTO      CONT2 IF EQUAL
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,"Clinic ID not on file.  ";
          CALL      HITENTER
          GOTO      KEYST
.
CONT2     KEYIN     *P29:13,*V2LON,END6
          RESET     END6
          GOTO      CONT3 IF NOT EOS
.
          MOVE      SP6,END6
          DISPLAY   *P29:13,*V2LON,LSTREC
          GOTO      CONFIRM
.
CONT3     PACK      END6,END6,SP6
.
          MATCH     START6,END6
          GOTO      CONT3A IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Must be greater than FROM ID.  ";
          CALL      HITENTER
          GOTO      KEYST
.
CONT3A    PACK      KEY20,OCASITE,END6,SP20
          CALL      RDSCLIA1
          CALL      RDKCLIA1
          IF        OVRCD = 0
            MATCH     END6,OCACLIN
            GOTO      CONFIRM IF EQUAL
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,"Clinic ID does not exist.  ";
          CALL      HITENTER
          GOTO      CONT2
.
CONFIRM   KEYIN     *P1:24,*EL,"Ok to Continue (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          CMATCH    ANSN,ANS
          GOTO      BEGIN IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      CONT4 IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.
CONT4     DISPLAY   *P1:24,*EL,"Printing:";
.
          CALL      HEADSET
.
          MOVE      ZERO,CPAGENO
.
          MATCH     SP6,START6
          IF        @EQUAL
            PACK      KEY20,OCASITE,SP20
          ELSE
            PACK      KEY20,OCASITE,START6,SP20
          ENDIF
          CALL      RDSCLIA1
LOOP1     MOVE      SP20,DOCDESC
          CALL      RDKCLIA1
          BRANCH    OVRCD,EOF
.
          MATCH     IDDD,OCASITE
          GOTO      EOF IF NOT EQUAL
.
          MATCH     SP6,END6
          GOTO      CONT6 IF EQUAL
.
          MATCH     OCACLIN,END6
          GOTO      EOF IF LESS
.
CONT6     DISPLAY   *P11:24,*V2LON,OCACLIN;
.
          COMPARE   ZERO,CPAGENO
          CALL      PRINTH IF EQUAL
.
          COMPARE   LN55,CLNO
          CALL      PRINTH IF NOT LESS
.
          MATCH     SP30,OCADESC
          GOTO      NODOCT IF NOT EQUAL
.
          PACK      KEY6,OCADOCT
          CALL      RDDOCT1
          MOVE      DGNAME,ANS
          PACK      DOCDESC,ANS,DOT,DSNAME
.
NODOCT    CALL      PRINTD
          GOTO      LOOP1
.
.
EOF       COMPARE   ZERO,CPAGENO
          GOTO      NOREC IF EQUAL
.
          PRINT     *23,"*----------------------------------------":
                    "----------------------------------------------------*":
                    *N,*N,*1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,"Generation Completed.  ";
          CALL      HITENTER
          GOTO      BEGIN
.
NOREC     DISPLAY   *P1:24,*EL,*B,"No Clinic Id selected.  ";
          CALL      HITENTER
          GOTO      BEGIN
.
PRINTH    COMPARE   ZERO,CPAGENO
          GOTO      PG1 IF EQUAL
.
          PRINT     *23,"|",*37,"|",*50,"|",*81,"|",*116,"|",*N:
                    *23,"*----------------------------------------":
                    "----------------------------------------------------*"
.
PG1       CALL      IBACLOCK    
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
          PRINT     *N,*40,"Clinic Id Sequence From ",XDIM6," To ",YDIM6,*N:
                    *N,*23,"*-------------------------------------------":
                    "-------------------------------------------------*":
                    *N,*23,"|  Clinic Id  | Eff. Date  |  Doctor Code ":
                    *81,"|  Clinic Id Description",*116,"|":
                    *N,*23,"*-------------------------------------------":
                    "-------------------------------------------------*"
.
          MOVE      "9",CLNO
          RETURN
.
.
PRINTD    UNPACK    OCADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *23,"|",*26,OCACLIN,*37,"|",*39,CPCDATE,*50,"|":
                    *53,OCADOCT,*61,DOCDESC,*81,"|",*84,OCADESC,*116,"|"
          ADD       ONE TO CLNO
          RETURN
.
.
HEADSET   MOVE      SP6,XDIM6
          MOVE      SP6,YDIM6
.
          MOVE      START6,XDIM6
          MOVE      END6,YDIM6
.
          MATCH     SP6,START6
          GOTO      CONT8 IF NOT EQUAL
          MOVE      FSTREC,XDIM6
.
CONT8     MATCH     SP6,END6
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM6
          RETURN
.
          INC       STD001IO
.
          INC       OUTCLIIO/INC
          INC       PATDO1IO/INC
.
ENDIT     CHAIN     PGM
          STOP
.

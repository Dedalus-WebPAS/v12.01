.******************************************************************************
.* System   : WAITING LIST                                                    *
.* Program  : IBAWAT34                                                        *
.* Desc     : Admissions Summary                                              *
.******************************************************************************
.* Author   : Paul Duncan                                                     *
.* Date     : 31/05/1990                                                      *
.* Comments :                                                                 *
.* Mods     :                                                                 *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*                      24/07/90 Bill Dew                                     *
.*                      Fix minor screen handling bugs of keyin fields        *
.*                      Replace date range routine DRTN                       *
.*                      Convert wattr1af 3rd index loop to bokmasaf 4 index   *
.*                      Fix minor printout problems                           *
.*            V5.01.10  03/08/90 Justin Coates                                *
.*                      Added the use of WTCNTPDS : Treatment/Procedure       *
.*                      12/10/90 Bill Dew                                     *
.*                      Fix report bugs found in r5 testing                   *
.*            V5.01.11  27/10/90 Bill Dew                                     *
.*                      Fix EXTR0000 routine to loop wattr1af on 2nd key for  *
.*                      Admitted and discharged patients and check their      *
.*                      scheduled admission dates (WMDATE3) to be within the  *
.*                      specified date ranges.   Also clear maximum after each*
.*                      subtotal.  Add Cancellation routine to calculate      *
.*                      cancelled patients.                                   *
.*            V5.01.12  14/11/90 i chung                                      *
.*                      Recompile for WATCATIO.INC - incorrect Keys size      *
.*            V5.01.13  23/05/91  Steve O'Gorman                              *
.*                      Fixed Errors found by the New Compiler                *
.*        V5.01.14  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*                                                                            *
.******************************************************************************
.$$$$$
.$$$$$
.************************************************************************
.*                        FD INCLUDES                                   *
.************************************************************************
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC
          INC       WATCATFD/INC
          INC       WATCONFD/INC
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WEBERRFD/INC
+
.************************************************************************
.*                        CONSTANTS                                     *
.************************************************************************
CODEBC    INIT      "BC"
CODETP    INIT      "TP"
CODEWU    INIT      "WU"
CODEWG    INIT      "WG"
.
DESCOPT1  DIM       35            * it's actual value is set up in INIT0000
DESCOPT2  INIT      "Unit/Clinic Code Sequence          "
DESCOPT3  INIT      "Doctor Code Sequence               "
DESCOPT4  DIM       35            * it's actual value is set up in INIT0000
DESCOPT5  INIT      "Unit/Clinic Seq                    "
DESCOPT6  INIT      "Doctor Sequence                    "
.
KEYOPT1   INIT      "u10-12,13-21,7-9,1-6"
KEYOPT2   INIT      "u7-9,13-21,1-6,10-12"
KEYOPT3   INIT      "u1-6,13-21,7-9,10-12"
.
.************************************************************************
.*                        VARIABLES                                     *
.************************************************************************
CMDLINE   DIM       80
CODE      DIM       2
.
DESCOPT   DIM       35               * display option
DIM3      DIM       3
DIM8      DIM       8
DISPDATE  DIM       10               * print date
DOCNAM    DIM       15               * print doctors name
DOCTCODE  DIM       6
DOCCODE   DIM       6
DOLIST    FORM      5
.
NUM365    FORM      "365"
FRDATE    DIM       8
.
HOSTOT    FORM      5                * totals of CANHOS's
HOSTTT    FORM      5                * totals of totals of CANHOS's
.
JDOLIST   FORM      3
.
.
MAXTOT    FORM      6                * total of the means
MAXTTT    FORM      6                * total of total of the means
MEAN      FORM      6
MEANTOT   FORM      6                * total of the maximums
MEANTTT   FORM      6                * total of total of the maximums
.
OTHTOT    FORM      4                * total of the CAMOTH's
OTHTTT    FORM      4                * total of total of the CAMOTH's
OVERTOT   FORM      7                * total of the overdues
OVERTTT   FORM      7                * total of the overdues
.
PATCNT    FORM      4                * printed patient count
PATNAME   DIM       29               * print patients name
PATTOT    FORM      5                * total of the CANPAT's
PATTTT    FORM      5                * total of total of the CANPAT's
PFRDATE   DIM       10               * print from date
PTODATE   DIM       10               * print to date
PRUNIT    DIM       3
PRDOCT    DIM       6
PRGRP     DIM       3
PRINT34   DIM       34
PROCDESC  DIM       33               * print proc. description
PROCTOT   FORM      6                * print proc. total      
PTCNDCQS  FORM      1
.
RANLIM1   FORM      3                * print range limit
RANLIM2   FORM      3                * print range limit
RANLIM3   FORM      3                * print range limit
RANLIM4   FORM      3                * print range limit
RANLIM5   FORM      3                * print range limit
RANLIM6   FORM      4                * print range limit
.
STRTFLAG  FORM      1
.
TEMPKEY   DIM       40
TEMPFIL1  IFILE     SQL, FIXED=79, KEY=TEMPKEY
.file variables
.
.WMDOCTOR DIM       6
.WMUNIT   DIM       3
.WPGRP    DIM       3
.WMCODE   DIM       9
RANGE1    FORM      5
RANGE2    FORM      5
RANGE3    FORM      5
RANGE4    FORM      5
RANGE5    FORM      5
RANGE6    FORM      5
RANGE7    FORM      5
TOTDAY    FORM      6
MAX       FORM      6
WITHIN    FORM      7
OVERDUE   FORM      7
CANHOS    FORM      5
CANPAT    FORM      5
CANOTH    FORM      4
TEMPFIL2  IFILE     SQL, FIXED=22, KEY="u1-9"
PROCCODE  DIM       9
PROCSUM   FORM      10
PROCNUM   FORM      10    
.EOF                 1
....................
TODATE    DIM       8
TOTRAN1   FORM      5
TOTRAN2   FORM      5
TOTRAN3   FORM      5
TOTRAN4   FORM      5
TOTRAN5   FORM      5
TOTRAN6   FORM      5
TOTRAN7   FORM      5
TTTRAN1   FORM      5
TTTRAN2   FORM      5
TTTRAN3   FORM      5
TTTRAN4   FORM      5
TTTRAN5   FORM      5
TTTRAN6   FORM      5
TTTRAN7   FORM      5
TPROCTOT  FORM      6                * total of the procdure totals
TTPROCTT  FORM      6                * total of the procdure totals
.
UNITCODE  DIM       3
UNITDESC  DIM       20               * print unit description
.
WATTEMP1  DIM       8
WATTEMP2  DIM       8
WITHTOT   FORM      7                * total of withins
WITHTTT   FORM      7                * total of withins
WMDOCNAM  DIM       18
.
.************************************************************************
PRGID     INIT      "IBAWAT34"
PRGNAM    INIT      "Admissions Summary"
VERSION   INIT      "V12.01.00"
.************************************************************************
.*                         MAINLINE                                     *
.************************************************************************
ML0000
.MLQQ      SPLOPEN   "data"
          CALL      INIT0000             * Display header and open files
.
ML1000    CALL      OPTN0000             * select option
          BRANCH    MOPTION,ML2000,ML2000,ML2000
.
          GOTO      ML9999               * exit chosen
.
ML2000    CALL      SCRR0000             * date screen
ML2100    CALL      DRNG0000             * Get date range
ML3000    BRANCH    MOPTION,ML6000,ML4000,ML5000
.
ML4000    CALL      UNIT0000             * keyin unit code
          GOTO      ML6000
.
ML5000    CALL      DOCT0000             * get doctor code
          GOTO      ML6000
.
ML6000    CALL      CONTQST              * ok to continue?
          BRANCH    CEXIT,ML7000,ML2000,ML1000
.
ML7000    CALL      EXTR0000             * extract records and put in tempfile
.
          CALL      OUT0000              * produce printout
          GOTO      ML1000
ML9999    CALL      KILL0000             * Delete temp file
.MLQQQ     SPLCLOSE  "data"
          CHAIN     PGM
.************************************************************************
.*                         INIT0000                                     *
.*               Display Header and open files                          *
.*                   called by : ML0000                                 *
.************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"opening controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,THIRTY7;*140,WTCNTRG1,WTCNTRG2,WTCNTRG3,WTCNTRG4:
                                          WTCNTRG5,WTCNTRG6:
                                     *122,WTCNTPUC,*185,WTCNTPDS
          DISPLAY   *P60:24,"opening patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P60:24,"opening patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P68:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P68:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
          DISPLAY   *P68:24,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          DISPLAY   *P68:24,"watcataf"
          OPEN      WATCATA2,"watcataf"
.
. ------  set up Treatment/Procedure value  ------
.
          CLEAR     DESCOPT1
          APPEND    WTCNTPDS,DESCOPT1
          APPEND    " Group Code Sequence      ",DESCOPT1
          RESET     DESCOPT1

          CLEAR     DESCOPT4
          APPEND    WTCNTPDS,DESCOPT4
          APPEND    " Group Seq                ",DESCOPT4
          RESET     DESCOPT4
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,WATTEMP1
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,WATTEMP2
.
INIT9999  RETURN
+
.************************************************************************
.*                        OPTN0000                                      *
.*             select option                                            *
.*                   called by : ML1000                                 *
.************************************************************************
OPTN0000  CALL      SCRN0000           * Display screen
.
OPTN1000  KEYIN     *P17:9,*DV,UNDLN1,*P17:9,*V2LON,MOPTION,*DV,*P17:9,MOPTION
          BRANCH    MOPTION,OPTN2000,OPTN2000,OPTN2000
.
          COMPARE   ZERO,MOPTION        * exit chosen
          GOTO      OPTN9999 IF EQUAL
          BEEP                         * invalid entry
          GOTO      OPTN1000
.
OPTN2000  LOAD      DESCOPT,MOPTION,DESCOPT4,DESCOPT5,DESCOPT6
          MOVE      "- ",CPHDROPT
          APPEND    DESCOPT,CPHDROPT
.
          DISPLAY   *P49:2,*V2LON,"- ",*+,DESCOPT,SP1    * display screen head
          GOTO      OPTN9999
.
OPTN9999  RETURN
.************************************************************************
.*                        SCRN0000                                      *
.*                Display main options screen                           *
.*                   called by : OPTN0000                               *
.************************************************************************
SCRN0000  HLINE     *G37,2,49,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",DESCOPT1:
                    *P1:6,*V2LON,TWO,*HOFF," = ",DESCOPT2:
                    *P1:7,*V2LON,THREE,*HOFF," = ",DESCOPT3:
                    *P1:9,"Select Option : "
.
SCRN9999  RETURN
.************************************************************************
.*                         SCRR0000                                     *
.*             Screen for date keyins                                   *
.*                   called by : ML1000                                 *
.************************************************************************
SCRR0000  DISPLAY   *P1:11,*EF,"From Scheduled Admission Date : ":
                    *P1:12,"To   Scheduled Admission Date :"
.
SCRR9999  RETURN
.************************************************************************
.*                        DTRN0000->DRNG0000                            *
.*           Input the admission date range required                    *
.*                   called by : ML1000               
.    Modified 25/07/1990 Bill Dew
.************************************************************************
DRNG0000
          UNPACK    SP20,PFRDATE,PTODATE
          UNPACK    SP20,FRDATE,TODATE
          UNPACK    SP10,CDAY,CMON,CCENT,CYEAR
.
          MOVE      CCC,CCENT
          MOVE      "33",CCOL               * set up position for keydate
          MOVE      TEN1,CVERT
          MOVE      ZERO,CHIGHLT
          CALL      KEYDATE            * get start date
.
          COMPARE   ZERO,OVRCD         * test for return hit
          GOTO      DRNG1111 IF EQUAL
          MOVE      "Start    ",PFRDATE     * set start if return hit
          DISPLAY   *PCCOL:CVERT,*V2LON,PFRDATE
          GOTO      DRNG2222
DRNG1111
          PACK      FRDATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",FRDATE
          PACK      PFRDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REPLACE   " 0",PFRDATE
DRNG2222
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      TEN2,CVERT         * line for keydate
          CALL      KEYDATE            * get second date range
.
          COMPARE   ZERO,OVRCD         * test for return hit
          GOTO      DRNG3333 IF EQUAL
          MOVE      "99999999",TODATE  * set filedate to highest value
          MOVE      "Finish    ",PTODATE
          DISPLAY   *PCCOL:CVERT,*V2LON,PTODATE
          GOTO      DRNG4444
DRNG3333
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",TODATE
          PACK      PTODATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REPLACE   " 0",PTODATE
DRNG4444
          MATCH     FRDATE,TODATE      * test dates are correct sequence
          GOTO      DRNG9999 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,"The To date must be after From date.  ";
          CALL      HITENTER
          GOTO      DRNG0000
DRNG9999  RETURN
.************************************************************************
.*                         UNIT0000                                     *
.*             Keyin the unit that will be printed                      *
.*                   called by : ML4000                                 *
.************************************************************************
UNIT0000  DISPLAY   *P1:14,"Unit/Clinic Code : "
          MOVE      ONE,STRTFLAG           * come back to here if error
.
UNIT1000  KEYIN     *P20:14,*DV,UNDLN3,*P20:14,*V2LON,UNITCODE:
                    *P20:14,*DV,UNITCODE
.
          ENDSET    UNITCODE
          APPEND    SP10,UNITCODE
          RESET     UNITCODE
.
          MATCH     SP3,UNITCODE                * nothing entered?
          GOTO      UNIT8000 IF EQUAL
.
          MATCH     QUEST,UNITCODE              * ?
          GOTO      UNIT4000 IF NOT EQUAL
.
UNIT2000  MOVE      CODEWU,CODE                 * set up category
          MOVE      ONE,CNEWDS                  * new ?
          MOVE      ZERO,STRTFLAG               * come back to here
          CALL      PATCODDS
.
          DISPLAY   *P1:24,"Group Code : "
UNIT3000  KEYIN     *P14:24,*EL,*DV,UNDLN3,*P14:24,UNITCODE
.
          ENDSET    UNITCODE
          APPEND    SP10,UNITCODE
          RESET     UNITCODE
.
          MATCH     SP3,UNITCODE                * nothing entered?
          GOTO      UNIT7000 IF EQUAL
.
          MATCH     QUEST,UNITCODE              * ?
          GOTO      UNIT2000 IF EQUAL
.
UNIT4000  PACK      KEY5,CODEWU,UNITCODE
          CALL      RDCODE1
          BRANCH    OVRCD,UNIT6000              * not on file
.
          BRANCH    STRTFLAG,UNIT5000           * do we have to redisplay
.          
          CALL      SCRN0000                    * redisplay screen
          CALL      SCRR0000
          DISPLAY   *P17:9,*V2LON,MOPTION:
                    *P33:11,*V2LON,PFRDATE:
                    *P33:12,*V2LON,PTODATE:
                    *P1:14,*HOFF,"Unit/Clinic Code : ":
                    *P20:14,*V2LON,UNITCODE,*P35:14,TDESC
          GOTO      UNIT9999
.
UNIT5000  DISPLAY   *P35:14,*V2LON,TDESC
          MOVE      TDESC,UNITDESC
          GOTO      UNIT9999
.
UNIT6000  DISPLAY   *P1:24,"Invalid code. ";
          CALL       HITENTER
          BRANCH     STRTFLAG,UNIT1000          * were do we go back to
          GOTO       UNIT3000
UNIT7000  
          CALL      SCRN0000                    * redisplay for nothing entered
          CALL      SCRR0000
          DISPLAY   *P17:9,*V2LON,MOPTION
          DISPLAY   *P33:11,*V2LON,PFRDATE:
                    *P33:12,*V2LON,PTODATE:
                    *P1:14,*HOFF,"Unit/Clinic Code : "
          GOTO      UNIT0000
.
UNIT8000
          DISPLAY   *P20:14,*V2LON,*EL,"All"
.
UNIT9999  RETURN
.************************************************************************
.*                         DOCT0000                                     *
.*             Keyin the DOCT that will be printed                      *
.*                   called by : ML4000                                 *
.************************************************************************
DOCT0000  DISPLAY   *P1:14,"Doctor Code : "      
          MOVE      ONE,STRTFLAG           * come back to here if error
.
          MOVE      SP10,DOCTCODE
          MOVE      "20",ECOL
          MOVE      "14",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,DOCT8000,DOCT0000
.
          MOVE      DCODE,DOCTCODE
.
DOCT4000  PACK      KEY6,DOCTCODE
          MOVE      DSNAME,PACSNAME             * format doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCNAM
          DISPLAY   *P35:14,*V2LON,DOCNAM
          GOTO      DOCT9999
.
DOCT8000  DISPLAY   *P15:14,*V2LON,*EL,"All"
.
DOCT9999  RETURN
.************************************************************************
.*                        EXTR0000                                      *
.*            Extract values and put in tempfile                        *
.*                   called by : ML1000                                 *
.    Modified 27/09/1990 Bill Dew
.************************************************************************
EXTR0000  CALL      CREA0000
          DISPLAY   *P1:24,*EL,"Extracting record : "
          MOVE      ZERO,STRTFLAG
.
          PACK      KEY20,FOUR,SP20    * position fptr at first admitted record
          CALL      RDSTREA2
EXTR1000
          CALL      RDKTREA2
          BRANCH    OVRCD,EXTR7000
.
.      accept only a status of four or five
.
          BRANCH    WMSTAT,EXTR7000,EXTR7000,EXTR7000,EXTR1500,EXTR1500,EXTR7000
          GOTO      EXTR1000
.
EXTR1500  DISPLAY   *P21:24,*V2LON,WMURNO,WMCODE,WMCNT;
.EXTRQQK2  KEYIN     ANS
.
          MOVE      WMBOOK,KEY8    
          CALL      RDMISS1            * get admission date
          BRANCH    OVRCD,EXTR1000     * no admission record -data error -ignor
.
          PACK      DIM8,ADATE     * setup century into admission date
          MATCH     FRDATE,DIM8        * admission date must be in date range
          GOTO      EXTR1000 IF LESS
          MATCH     DIM8,TODATE
          GOTO      EXTR1000 IF LESS
          MOVE      ONE,STRTFLAG
.EXTRQQ1   DISPLAY   *P1:16,"EXTR>",WMURNO,WMCODE,WMCNT,"|",WMBOOK,"|":
.                    WMDATE1,"|",DIM8,"|";
.EXTRQQK1  KEYIN     ANS
.
          MOVE      SP3,WPGRP
          MOVE      WMCODE,KEY9
          CALL      RDPROA1            * get procedure group code
.
          BRANCH    MOPTION,EXTR2000,EXTR3000,EXTR4000
.
EXTR2000  PACK      KEY21,WPGRP,WMCODE,WMUNIT,WMDOCTOR
.
          GOTO      EXTR5000
.
EXTR3000  PACK      KEY21,WMUNIT,WMCODE,WMDOCTOR,WPGRP 
          MATCH     SP3,UNITCODE         * printing all?
          GOTO      EXTR5000 IF EQUAL
          MATCH     UNITCODE,WMUNIT      * if not - is this the right code
          GOTO      EXTR1000 IF NOT EQUAL * no - go back
          GOTO      EXTR5000
.
EXTR4000  PACK      KEY21,WMDOCTOR,WMCODE,WMUNIT,WPGRP
          MATCH     SP6,DOCTCODE         * printing all?
          GOTO      EXTR5000 IF EQUAL
          MATCH     DOCTCODE,WMDOCTOR    * if not - is this the right code
          GOTO      EXTR1000 IF NOT EQUAL * no - go back
.
EXTR5000  CALL      RDTMPR1
          BRANCH    OVRCD,EXTR6000     * new record
          CALL      CDOL0000           * calculate days on list for patient
          CALL      UPDT0000           * update correct field 
          CALL      UPDY0000           * update total days
          CALL      MAX0000            * update the maximum
          CALL      WITH0000           * update WITHIN
          CALL      CANC0000           * calcalate cancelled patients
          CALL      UPTMPR1            * update with first key
          GOTO      EXTR1000
.
EXTR6000  CALL      SETF0000           * set fileds ready to write new record
          CALL      CDOL0000           * calculate days on list for patient
          CALL      UPDT0000           * update correct field
          CALL      UPDY0000           * update total days
          MOVE      DOLIST,MAX         * The dolist becomes the max upon write
          CALL      WITH0000           * update WITHIN
          CALL      CANC0000           * calcalate cancelled patients
          CALL      WRTMPR1            * write with the first key
          GOTO      EXTR1000
.
EXTR7000  BRANCH    STRTFLAG,EXTR9999  * any written to tempfile?
          DISPLAY   *P1:24,"No patients admitted.  ";
          CALL      HITENTER
          GOTO      EXTR9999
.
EXTR9999  RETURN
.************************************************************************
.*                        CDOL0000                                      *
.*              Calculate number of days on list for patient            *
.*                   called by : EXTR1500                               *
.************************************************************************
CDOL0000  
          UNPACK    WMDATE1,XCENT,XYEAR,XMON,XDAY
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      XCENT,CC
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON                       * get julien date
          MOVE      JULDAY,JDOLIST
.
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                       * get julien date
.
          MATCH     XYEAR,CYEAR                  * same year?
          GOTO      CDOL1000 IF EQUAL
.
          MOVE      ZERO,DOLIST
          MOVE      CYEAR,DOLIST
          MOVE      XYEAR,FORM2
          SUB       FORM2,DOLIST                 * no - how many years then
          MULT      NUM365,DOLIST                   * convert to days
          ADD       NUM365,DOLIST
          SUB       JDOLIST,DOLIST               * sub the bit of the first year
                                                 * that they werent on the list
          ADD       JULDAY,DOLIST                * add part of the last year 
                                                 * that they were
.
          SUB       NUM365,DOLIST                  * if so sub one
          GOTO      CDOL9999
.
CDOL1000  MOVE      JULDAY,DOLIST                * same year - easy
          SUB       JDOLIST,DOLIST
          GOTO      CDOL9999
CDOL9999
.CDOLQQ1   DISPLAY   *P1:17,"CDOL>",DOLIST;
.CDOLQQK1  KEYIN     ANS
          RETURN
.************************************************************************
.*                         UPDT0000                                     *
.*            Update the field in the range of the patient              *
.*                   called by : EXTR0000                               *
.************************************************************************
UPDT0000  COMPARE   DOLIST,WTCNTRG6              * in the range 7?
          GOTO      UPDT1000 IF NOT LESS
.
          ADD       ONE,RANGE7
          GOTO      UPDT9999
.         
UPDT1000  COMPARE   DOLIST,WTCNTRG5              * in the range 6?
          GOTO      UPDT2000 IF NOT LESS
.
          ADD       ONE,RANGE6
          GOTO      UPDT9999          
.         
UPDT2000  COMPARE   DOLIST,WTCNTRG4              * in the range 5?
          GOTO      UPDT3000 IF NOT LESS
.
          ADD       ONE,RANGE5
          GOTO      UPDT9999          
.         
UPDT3000  COMPARE   DOLIST,WTCNTRG3              * in the range 4?
          GOTO      UPDT4000 IF NOT LESS
.
          ADD       ONE,RANGE4
          GOTO      UPDT9999          
.         
UPDT4000  COMPARE   DOLIST,WTCNTRG2              * in the range 3?
          GOTO      UPDT5000 IF NOT LESS
.
          ADD       ONE,RANGE3
          GOTO      UPDT9999          
.         
UPDT5000  COMPARE   DOLIST,WTCNTRG1              * in the range 2?
          GOTO      UPDT6000 IF NOT LESS
.
          ADD       ONE,RANGE2
          GOTO      UPDT9999          
.         
UPDT6000  
          ADD       ONE,RANGE1                  * must be in range 1
.
UPDT9999
.UPDTQQ1   DISPLAY   *P1:18,"UPDT>",RANGE1,RANGE2,RANGE3,RANGE4,RANGE5,RANGE6:
.                    RANGE7
.UPDTQQK1  KEYIN     ANS
          RETURN
.************************************************************************
.*                         UPDY0000                                     *
.*             Update the total days on list                            *
.*                   called by : EXTR1500                               *
.************************************************************************
UPDY0000  ADD       DOLIST,TOTDAY
.
UPDY9999  RETURN 
.************************************************************************
.*                         MAX0000                                      *
.*                 Update maximum if needed                             *
.*                   called by : EXTR1500                              *
.************************************************************************
MAX0000  COMPARE    MAX,DOLIST             * is this number of days greater 
         GOTO       MAX9999 IF LESS
.
         MOVE       DOLIST,MAX 
.
MAX9999  RETURN
.************************************************************************
.*                         WITH0000                                     *
.*     update the WITHIN variable in the tempfile for all patients      *
.*     that were on the waiting list less than the overdue time         *
.*                   called by : EXTR1500                               *
.************************************************************************
WITH0000  PACK      KEY5,CODETP,WMPTY
          CALL      RDCODE1
          MATCH     TCINDC1,ANSR                 * for routine admission
          GOTO      WITH1000 IF NOT EQUAL
.
          COMPARE   DOLIST,WTCNRODY              * is it within the max time
          GOTO      WITH8000 IF LESS
.
          GOTO      WITH9000
.
WITH1000  MATCH     TCINDC1,ANSS                 * for semi-urgent admiss
          GOTO      WITH2000 IF NOT EQUAL
.
          COMPARE   DOLIST,WTCNSUDY              * is it swithin max time
          GOTO      WITH8000 IF LESS             * no - must be overdue
.
          GOTO      WITH9000                     * yes - go and add one to
.                                                  within variable
.
WITH2000  MATCH     TCINDC1,ANSU                 * for urgent adm.
          GOTO      WITH9999 IF NOT EQUAL
.
          COMPARE   DOLIST,WTCNURDY              * is it within max time
          GOTO      WITH8000 IF LESS             * no - must be overdue
.
          GOTO      WITH9000                     * yes - go and add one to
.                                                  within variable
.
WITH8000  ADD       ONE,OVERDUE
          GOTO      WITH9999
.
WITH9000  ADD       ONE,WITHIN
.
WITH9999  RETURN
.*******************************************************************************
.                                 CANC0000
.    Created by Bill Dew 27/07/1990
.    Read the watcataf for cancellation codes category BC and test indicators
.    for cancellations by patient hospital or other.
.    Patients are cancelled if there admission date is within the selected
.    date range and have been cancelled.
.*******************************************************************************
CANC0000
          PACK      KEY29,WMURNO,WMCODE,WMCNT,SP20
          CALL      RSWTCAT2           * position fptr in watcataf
CANC2222
          CALL      RKWTCAT2
          BRANCH    OVRCD,CANC9999
.
.CANCQQ2   DISPLAY   *P1:20,*EL,"CANC>",WTCAEDAT,"|",WTCAURNO,"|",WTCAPROC:
.                    "|",WTCACATF,"|",WTCACODT,"|";
          MATCH     WTCAURNO,WMURNO    * test same patient
          GOTO      CANC9999 IF NOT EQUAL
          MATCH     WTCAPROC,WMCODE    * test same procedure code 
          GOTO      CANC9999 IF NOT EQUAL
          COMPARE   WTCAPCNT,WMCNT     * test same procedure code counter
          GOTO      CANC9999 IF NOT EQUAL
          MATCH     CODEBC,WTCACATF    * test for a cancellation code BC
          GOTO      CANC2222 IF NOT EQUAL
          MATCH     FRDATE,WTCAEDAT    * test if greater than start date
          GOTO      CANC2222 IF LESS
          MATCH     WTCAEDAT,TODATE    * test if less than end date 
          GOTO      CANC2222 IF LESS
.
          PACK      KEY5,CODEBC,WTCACODT
          CALL      RDCODE1            * get 2nd indicator 
          BRANCH    OVRCD,CANC2222
.
.CANCQQ3   DISPLAY   *P70:20,WTCACODT," ",TCINDC2;
.CANCQQK3  KEYIN     ANS
          MATCH     ANSH,TCINDC2       * test cancelled by hospital
          GOTO      CANC5555 IF NOT EQUAL
          ADD       ONE,CANHOS         * increment cancelled by hosp counter
          GOTO      CANC2222
CANC5555
          MATCH     ANSP,TCINDC2       * test cancelled by patient
          GOTO      CANC6666 IF NOT EQUAL
          ADD       ONE,CANPAT         * increment cancelled by patient counter
          GOTO      CANC2222
CANC6666
          ADD       ONE,CANOTH         * increment canelled by other code
          GOTO      CANC2222
CANC9999  RETURN
.************************************************************************
.*                         SETF0000                                     *
.*            Set the fields for writing a new record                   *
.*                   called by : EXTR0000                               *
.************************************************************************
SETF0000
          MOVE      ZERO,RANGE1
          MOVE      ZERO,RANGE2
          MOVE      ZERO,RANGE3
          MOVE      ZERO,RANGE4
          MOVE      ZERO,RANGE5
          MOVE      ZERO,RANGE6
          MOVE      ZERO,RANGE7
          MOVE      ZERO,MAX
          MOVE      ZERO,WITHIN
          MOVE      ZERO,TOTDAY
          MOVE      ZERO,OVERDUE
          MOVE      ZERO,CANHOS 
          MOVE      ZERO,CANPAT 
          MOVE      ZERO,CANOTH 
SETF9999  RETURN
.************************************************************************
.*                         OUT0000                                      *
.*                       Produce the printout                           *
.*                   called by : ML1000                                 *
.************************************************************************
OUT0000   MOVE      ZERO,CPAGENO      * Set up heading variables
          MOVE      ZERO,PATCNT
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,"Printing Record : "
.
          MOVE      SP20,PRGRP
          MOVE      SP20,PRUNIT
          MOVE      SP20,PRDOCT
.

OUT1000   PACK      KEY21,SP30
          CALL      RSTMPR1
.
OUT1500   CALL      RKTMPR1
          BRANCH    OVRCD,OUT5000
.
          LOAD      DIM8,MOPTION,PRGRP,PRUNIT,PRDOCT
          MATCH     SP8,DIM8
          GOTO      OUT1900 IF EQUAL
.
          BRANCH    MOPTION,OUT1600,OUT1700,OUT1800
OUT1600
          MATCH     WPGRP,PRGRP
          GOTO      OUT1900 IF EQUAL
.
          CALL      PRST0000         * Print total for last group
          CALL      HEAD0000
          GOTO      OUT1900
.
OUT1700   MATCH     WMUNIT,PRUNIT
          GOTO      OUT1900 IF EQUAL
.
          CALL      PRST0000         * Print total for last group
          CALL      HEAD0000
          GOTO      OUT1900
.
OUT1800   MATCH     WMDOCTOR,PRDOCT
          GOTO      OUT1900 IF EQUAL
.
          CALL      PRST0000         * Print total for last doctor
          CALL      HEAD0000
          GOTO      OUT1900
.
OUT1900   DISPLAY   *P19:24,*V2LON,WMCODE
          COMPARE   "55",CLNO        * new page?
          CALL      HEAD0000 IF NOT LESS
.
          CALL      FRMT0000         * format data to print
.
          CALL      PRNT0000         * print a line of output
          BRANCH    MOPTION,OUT2000,OUT3000,OUT4000
OUT2000
          MOVE      WPGRP,PRGRP              * set up previous gropu code
          GOTO      OUT1500
.
OUT3000   MOVE      WMUNIT,PRUNIT            * set up previous procedure code
          GOTO      OUT1500
.
OUT4000   MOVE      WMDOCTOR,PRDOCT          * set up previous doctor code
          GOTO      OUT1500
.
OUT5000
          COMPARE   ZERO,CPAGENO
          GOTO      OUT5511 IF NOT EQUAL
          CALL      HEAD0000
          PRINT     *N,"No Patients Admitted.":
                    *N,*N,"*** End of Report ***"
          GOTO      OUT9999
OUT5511
          CALL      PRST0000                 * print sub-totals
          CALL      END0000                  * print grand totals
.
OUT9999   RETURN
.************************************************************************
.*                        HEAD0000                                      *
.*                 Produce printout heading                             *
.*                   called by : OUT0000,OUT1500,OUT2500                *
.************************************************************************
HEAD0000  
          CALL      HEAD132       
.
          BRANCH    MOPTION,HEAD1000,HEAD2000,HEAD3000
HEAD1000  PRINT     *40,"From Scheduled Admission Date : ",PFRDATE,*N:
                    *40,"To   Scheduled Admission Date : ",PTODATE,*N
          CALL      UND132
          GOTO      HEAD4000
.
HEAD2000
          PACK      KEY5,CODEWU,WMUNIT
          CALL      RDCODE1
          PRINT     *40,"From Scheduled Admission Date : ",PFRDATE,*N:
                    *40,"To   Scheduled Admission Date : ",PTODATE,*N:
                    "Unit/Clinic Code : ",WMUNIT," - ",TDESC
          CALL      UND132
          GOTO      HEAD4000
.
HEAD3000
          MOVE      WMDOCTOR,KEY6
          MOVE      "Unknown doctor",WMDOCNAM
          CALL      RDDOCT1 
          BRANCH    OVRCD,HEAD3100
          MOVE      DSNAME,PACSNAME     * format doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,WMDOCNAM
HEAD3100  PRINT     *40,"From Scheduled Admission Date : ",PFRDATE,*N:
                    *40,"To   Scheduled Admission Date : ",PTODATE,*N:
                    "Doctor Code : ",WMDOCTOR," - ",WMDOCNAM
          CALL      UND132
          GOTO      HEAD4000
.
HEAD4000  MOVE      WTCNTRG1,RANLIM1          * set up lower limits of ranges
          ADD       ONE,RANLIM1
          MOVE      WTCNTRG2,RANLIM2
          ADD       ONE,RANLIM2
          MOVE      WTCNTRG3,RANLIM3
          ADD       ONE,RANLIM3
          MOVE      WTCNTRG4,RANLIM4
          ADD       ONE,RANLIM4
          MOVE      WTCNTRG5,RANLIM5
          ADD       ONE,RANLIM5
          MOVE      WTCNTRG6,RANLIM6
          ADD       ONE,RANLIM6
          PRINT     "|",*36,"|",*43,"|",*59,"Distrubution",*85,"|":
                    *88,"Statistics",*99,"|",*104,"Priority",*115,"|":
                    *119,"Cancelled",*132,"|",*N,"|",*36,"|",*43,"|",*48,ZERO:
                    *49,"|",*52,RANLIM1,*55,"|",*58,RANLIM2,*61,"|",*64:
                    RANLIM3,*67,"|",*70,RANLIM4,*73,"|",*76,RANLIM5,*79:
                    "|",*81,RANLIM6,*85,"|",*99,"|",*115,"|",*132,"|",*N:
                    "|",*3,WTCNTPDS," Description",*36,"|",*38,"Total":
                    *43,"|",*46,WTCNTRG1,*49,"|",*52,WTCNTRG2,*55,"|",*58:
                    WTCNTRG3,*61,"|",*64,WTCNTRG4,*67,"|",*70,WTCNTRG5,*73:
                    "|",*76,WTCNTRG6,*79,"|",*83,"+",*85,"|",*88,"Mean":
                    *92,"|",*96,"Max",*99,"|",*102,"W/in",*107,"|",*109:
                    "O/due",*115,"|",*117,"Hos",*121,"|",*123,"Pat",*127,"|":
                    *129,"Oth",*132,"|" 
          CALL      UND132
.
          ADD      EIGHT,CLNO
.
HEAD9999  RETURN
.************************************************************************
.*                        FRMT0000                                      *
.*          Convert data to the format to print                         *
.*                   called by : OUT1500,OUT2500                        *
.************************************************************************
FRMT0000   MOVE     WMCODE,PROCDESC
           SCAN     SP6,PROCDESC
           APPEND   " - Unknown ",PROCDESC
           APPEND   WTCNTPDS,PROCDESC
           MOVE     WMCODE,KEY9         
           CALL     RDPROA1              * get proc. description
           BRANCH   OVRCD,FRMT9999
           MOVE     WPDESC,PROCDESC      * move it to the print variable
.
FRMT9999   RETURN
.************************************************************************
.*                        PRNT0000                                      *
.*              Print a line of output                                  *
.*                   called by : OUT1500,OUT2500                        *
.************************************************************************
PRNT0000  SUB       PROCTOT,PROCTOT
          ADD       RANGE1,PROCTOT
          ADD       RANGE2,PROCTOT
          ADD       RANGE3,PROCTOT
          ADD       RANGE4,PROCTOT
          ADD       RANGE5,PROCTOT
          ADD       RANGE6,PROCTOT
          ADD       RANGE7,PROCTOT
          DIV       PROCTOT,TOTDAY
          MOVE      TOTDAY,MEAN
          PRINT     "|",*3,PROCDESC,*36,"|",*37,PROCTOT:
                    *43,"|",*44,RANGE1,*49,"|",*50,RANGE2,*55,"|",*56:
                    RANGE3,*61,"|",*62,RANGE4,*67,"|",*68:
                    RANGE5,*73,"|",*74,RANGE6,*79,"|",*80:
                    RANGE7,*85,"|",*86,MEAN,*92,"|",*93:
                    MAX,*99,"|",*100,WITHIN,*107,"|",*108:
                    OVERDUE,*115,"|",*116,CANHOS,*121,"|":
                    *122,CANPAT,*127,"|",*128,CANOTH,*132,"|"
.
          ADD       ONE,CLNO                    * update line number
          ADD       RANGE1,TOTRAN1              * update all the total variables
          ADD       RANGE2,TOTRAN2
          ADD       RANGE3,TOTRAN3
          ADD       RANGE4,TOTRAN4
          ADD       RANGE5,TOTRAN5
          ADD       RANGE6,TOTRAN6
          ADD       RANGE7,TOTRAN7
          ADD       PROCTOT,TPROCTOT
          ADD       MEAN,MEANTOT
          ADD       MAX,MAXTOT
          ADD       WITHIN,WITHTOT
          ADD       OVERDUE,OVERTOT
          ADD       CANHOS,HOSTOT
          ADD       CANPAT,PATTOT
          ADD       CANOTH,OTHTOT
.
PRNT9999  RETURN
.************************************************************************
.*                        PRST0000                                      *
.*           Print Sub-total for change in group,doc or unit            *
.*                   called by :  OUT1000                               *
.************************************************************************
PRST0000  CALL      UND132
          BRANCH    MOPTION,PRST1000,PRST2000,PRST3000
.
PRST1000 
          CLEAR     PRINT34
          MOVE      "Unknown ",PRINT34      * 12/10/90 BD
          APPEND    WTCNTPDS,PRINT34
          APPEND    "Sub-Total",PRINT34
.
          PACK      KEY5,CODEWG,PRGRP
          CALL      RDCODE1
          BRANCH    OVRCD,PRST1500
          PACK      PRINT34,SP30,SP30
          CLEAR     PRINT34
          MOVE      TDESC,PRINT34
          SETLPTR   PRINT34,34
          RESET     PRINT34           
          SCAN      SP6,PRINT34
          APPEND    "Sub-Total",PRINT34
.
PRST1500  PRINT     "|",*3,PRINT34;
          GOTO      PRST4000
.
PRST2000  MOVE      "Unknown Unit",TDESC
          PACK      KEY5,CODEWU,PRUNIT
          CALL      RDCODE1
          PRINT     "|",*3,"Total ",PRUNIT," - ",TDESC;
          GOTO      PRST4000
.
PRST3000  MOVE      "Unknown Doctor",WMDOCNAM
          MOVE      PRDOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,PRST3500
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,WMDOCNAM
.
PRST3500  PRINT     "|",*3,"Total ",PRDOCT," - ",WMDOCNAM;
. 
PRST4000  PRINT     *36,"|",*37,TPROCTOT,*43,"|",*44,TOTRAN1,*49,"|":
                    *50,TOTRAN2,*55,"|",*56,TOTRAN3,*61,"|",*62,TOTRAN4:
                    *67,"|",*68,TOTRAN5,*73,"|",*74,TOTRAN6,*79,"|":
                    *80,TOTRAN7,*85,"|",*86,MEANTOT,*92,"|",*93,MAXTOT:
                    *99,"|",*100,WITHTOT,*107,"|",*108,OVERTOT,*115,"|":
                    *116,HOSTOT,*121,"|",*122,PATTOT,*127,"|",*128,OTHTOT:
                    *132,"|"
          CALL      UND132
          ADD       THREE,CLNO
.
          ADD       TOTRAN1,TTTRAN1     * update all the total-total variables
          ADD       TOTRAN2,TTTRAN2
          ADD       TOTRAN3,TTTRAN3
          ADD       TOTRAN4,TTTRAN4
          ADD       TOTRAN5,TTTRAN5
          ADD       TOTRAN6,TTTRAN6
          ADD       TOTRAN7,TTTRAN7
          ADD       TPROCTOT,TTPROCTT
          ADD       MEANTOT,MEANTTT
          ADD       MAXTOT,MAXTTT
          ADD       WITHTOT,WITHTTT
          ADD       OVERTOT,OVERTTT
          ADD       HOSTOT,HOSTTT 
          ADD       PATTOT,PATTTT
          ADD       OTHTOT,OTHTTT
.
          MOVE      "    0",TOTRAN1              * Clear all totals
          MOVE      "    0",TOTRAN2
          MOVE      "    0",TOTRAN3
          MOVE      "    0",TOTRAN4
          MOVE      "    0",TOTRAN5
          MOVE      "    0",TOTRAN6
          MOVE      "    0",TOTRAN7
          MOVE      "     0",TPROCTOT
          MOVE      "     0",MEANTOT
          MOVE      "     0",MAXTOT
          MOVE      "      0",WITHTOT
          MOVE      "      0",OVERTOT
          MOVE      "    0",HOSTOT
          MOVE      "    0",PATTOT
          MOVE      "   0",OTHTOT
.
PRST9999  RETURN
.************************************************************************
.*                        END0000                                       *
.*             Print total and end of report                            *
.*                   called by :  OUT4000                               *
.************************************************************************
END0000   
          PRINT     "|",*3,"Grand Total",*36,"|",*37,TTPROCTT,*43,"|":
                    *44,TTTRAN1,*49,"|",*50,TTTRAN2,*55,"|",*56,TTTRAN3,*61,"|":
                    *62,TTTRAN4,*67,"|",*68,TTTRAN5,*73,"|",*74,TTTRAN6,*79,"|":
                    *80,TTTRAN7,*85,"|",*86,MEANTTT,*92,"|",*93,MAXTTT,*99,"|":
                    *100,WITHTTT,*107,"|",*108,OVERTTT,*115,"|",*116,HOSTTT:
                    *121,"|",*122,PATTTT,*127,"|",*128,OTHTTT,*132,"|"
          CALL      UND132
          PRINT     *N,*N,"*** End of Report ***"
          MOVE      "    0",TTTRAN1     * update all the total-total variables
          MOVE      "    0",TTTRAN2
          MOVE      "    0",TTTRAN3
          MOVE      "    0",TTTRAN4
          MOVE      "    0",TTTRAN5
          MOVE      "    0",TTTRAN6
          MOVE      "    0",TTTRAN7
          MOVE      "     0",TTPROCTT
          MOVE      "     0",MEANTTT
          MOVE      "     0",MAXTTT
          MOVE      "      0",WITHTTT
          MOVE      "      0",OVERTTT
          MOVE      "    0",HOSTTT 
          MOVE      "    0",PATTTT
          MOVE      "   0",OTHTTT
END9999   RETURN
.**********************************************************************
.*                              CREA0000                              *
.*                   Create the Indexed Temp File                     *
.**********************************************************************
CREA0000  CALL      KILL0000                       * kill existing temp file
.
.------ Build new indexed temp file ------
.
          MOVE      SP30,TEMPKEY
          LOAD      TEMPKEY,MOPTION,KEYOPT1,KEYOPT2,KEYOPT3
          BRANCH    MOPTION,CREA1000,CREA2000,CREA3000
.
CREA1000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          APPEND    " 79 u10-12,13-21,7-9,1-6",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P60:24,"Opening tempfile"
          OPEN      TEMPFIL1,WATTEMP1
          GOTO      CREA9999
.
CREA2000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          APPEND    " 79 u7-9,13-21,1-6,10-12",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL1,WATTEMP1
          GOTO      CREA9999
.
CREA3000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          APPEND    " 79 u1-6,13-21,7-9,10-12",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL1,WATTEMP1
.
CREA9999  RETURN
+
.**********************************************************************
.*                              KILL0000                              *
.*                  Kill the Tempfile if it Already Exists            *
.**********************************************************************
KILL0000  CLEAR     CMDLINE
          CLOSE     TEMPFIL1
          APPEND    "iserase ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          CLOSE     TEMPFIL2
          APPEND    "iserase ",CMDLINE
          APPEND    WATTEMP2,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.************************************************************************
.*                    I/O Routines for Tempfile                         *
.************************************************************************
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          READ      TEMPFIL1,KEY21;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          READ      TEMPFIL1,KEY21;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          READ      TEMPFIL1,KEY21;WMDOCTOR,WMUNIT,WPGRP,WMCODE,RANGE1,RANGE2:
                                  RANGE3,RANGE4,RANGE5,RANGE6,RANGE7,TOTDAY,MAX:
                                  WITHIN,OVERDUE,CANHOS,CANPAT,CANOTH
          GOTO      OVERCOND IF OVER
          MOVE      DWMCNT,WMCNT
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          READKS    TEMPFIL1;WMDOCTOR,WMUNIT,WPGRP,WMCODE,RANGE1,RANGE2:
                            RANGE3,RANGE4,RANGE5,RANGE6,RANGE7,TOTDAY,MAX:
                            WITHIN,OVERDUE,CANHOS,CANPAT,CANOTH
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          WRITE     TEMPFIL1,KEY21;WMDOCTOR,WMUNIT,WPGRP,WMCODE,RANGE1,RANGE2:
                                  RANGE3,RANGE4,RANGE5,RANGE6,RANGE7,TOTDAY,MAX:
                                  WITHIN,OVERDUE,CANHOS,CANPAT,CANOTH
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMPR1  
          UPDATE    TEMPFIL1;WMDOCTOR,WMUNIT,WPGRP,WMCODE,RANGE1,RANGE2:
                            RANGE3,RANGE4,RANGE5,RANGE6,RANGE7,TOTDAY,MAX:
                            WITHIN,OVERDUE,CANHOS,CANPAT,CANOTH
          RETURN
.
.************************************************************************
.*                        I/O INCLUDES                                  *
.************************************************************************
          INC       STD001IO
          INC       PATDOCKY
          INC       PATCODKY
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       WATCATIO/INC
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WEBERRIO/INC

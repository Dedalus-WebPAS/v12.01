.----------------------------------------------------------------------
. Program       : NHIWEB96
.
. Function      : NHI/MWS Patient Search Server
.
. Modifications : 
.----------------------------------------------------------------------
. V11.04.01 09/05/2024  J.Tan           TSK 0944858
.           Changed SESSKEYS to DIM 22
. V10.15.01 20/02/2020  Peter Vela      TSK 0887845
.           Recompiled for NZHISWIO
. V10.14.01 30/05/2019  Thanh T.        TSK 0872539
.           Recompiled for NZHISWIO changes
.----------------------------------------------------------------------
. V10.06.02 26/08/2015  Jill Parkinson CAR 318680   
.           Recompiled for changes to NZHISWIO 
. V10.06.01 25/08/2015  Jill Parkinson CAR 320905   
.           Recompiled for changes to NZHISWIO 
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
. V10.03.04 30/06/2013  Davin             CAR 283202
.           Recompiled for changes to hl7 messaging
. V10.03.03 01/05/2013  Steve Armstrong   CAR 284376
.           Recompiled for changes to BRHLCOMN so that PMI messages
.           can use H7CIHOSP for Facility Code.
.           Recompiled for changes to DGCLICUP.
. V10.03.02 06/03/2013  Jill Parkinson CAR 281060
.           Changed NZSET000 to use DOB search if DOB is entered
.           Changed FIXGIV00 to allow 65 characters instead of 25 in total
. V10.03.01 16/05/2012  Steve Armstrong  CAR 256322
.           Recompiled for changes to NZHISWIO.
.----------------------------------------------------------------------
. V10.00.02 01/04/2010  Jill Parkinson CAR 218924
.           Fixed redirect in CONF000 if cancel selected from theatre
. V10.00.01 30/03/2010  Jill Parkinson CAR 218924
.           Copied theatflg and sesskeys from 9.08 
.----------------------------------------------------------------------
. V9.12.01  22/12/2009  Peter McMullen CAR 187251
.           Do not format pseudo month 00 (unknown)
.----------------------------------------------------------------------
. V9.11.02  20/02/2009  Peter McMullen CAR 189519 
.           Minor tweak to age calculation                         
. V9.11.01  03/12/2008  Jill Habasque CAR 184565
.           Mods to DOB search to set the age and range instead
.----------------------------------------------------------------------
. V9.10.01  27/11/2008  Jill Habasque CAR 184565
.           Mods to DOB search to not calculate the range
.----------------------------------------------------------------------
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
.----------------------------------------------------------------------
. V9.03.06  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      
.           - Add Sex Description routine SEXDSC00 with Code "R" - "Intersex"
. V9.03.05  24/03/2004  Jill Habasque 48462
.           Fixed output of error message when searching
. V9.03.04  29/10/2003 Sandra Barcham    43783
.           Recompiled for NZHISWIO and NZHISWUP
. V9.03.03  16/10/2003 Jill Habasque SRF 43358
.           Fixed redirect string                 
. V9.03.02  15/10/2003 Jill Habasque SRF 43358
.           Added gethcu if urnumber is entered
. V9.03.01  16/07/2003 Jill Habasque SRF 39340
.           Added tmotfrst and tmotnext
.----------------------------------------------------------------------
. V9.02.01  21/03/2003 Jill Habasque SRF 36883
.           Added read of PTCNNHTM
.                 V5.10.B02 22.03.2001 B.G.Ackland
.                          Fix DOB Search
.                 V5.08.03 20/09/2000 Katie Nelson
.                          509 Changes
.                 V5.08.02 12/09/2000 Katie Nelson
.                          Changed format to display birthdate as DD MON CCYY
.                 V5.08.01  16/08/2000  Caleb Sharman
.                          Changed Health Fund variables to be 8 chars
.                 V5.07.02 03.03.2000 B.G.Ackland
.                          Fix Age Validation
.
.                 V5.07.01 05.01.2000 B.G.Ackland
.                          Recompiled for New Standards
.
.                 V5.07.00 05.07.1999 K.C Nelson 
.                          Created Program
.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       NZHISIFD/INC
          INC       PATCONFD/INC
          INC       PATMA1FD/INC
          INC       NHIMASFD/INC
.         INC       PATMWSFD/INC
          INC       PATNIDFD/INC
          INC       PATCOMM/INC
          INC       TMPALIFD/INC
          INC       PATALRFD/INC
.
FORM3     FORM      3
.
.HTMLLINK  DIM       127
.
LOCALSTA  DIM       1
NMPNUMB   DIM       20
NZSRH001  FORM      1                       * Search Flag
NZSRH002  DIM       22                      * Search Continuation Pointer
NZSRH003  FORM      1                       * Search Type
NZSRH004  DIM       25                      * Search Surname
NZSRH005  DIM       60                      * Search Given Name
NZSRH006  DIM       11                      * Search Date of Birth
NZSRH007  FORM      3                       * Search Age 
NZSRH008  FORM      2                       * Search Age Range
NZSRH009  DIM       1                       * Search Sex 
.
NZGIVEN   DIM       25[15]
NZADDR    DIM       67[15]
.
LISTNMPI  DIM       7[8]     * NMPI No on Screen
NZARNMPI  DIM       7[15]    * NMPI No.
NZARSURN  DIM       25[15]   * Surname/Family name
NZARGIV1  DIM       20[15]   * Given Name 1
NZARGIV2  DIM       20[15]   * Given Name 2
NZARGIV3  DIM       20[15]   * Given Name 3
NZARPRNM  DIM       1[15]    * preferred name indicator
NZARDOB   DIM       8[15]    * Date of Birth
NZARSEX   DIM       1[15]    * Sex/Gender
.
NZARADD1  DIM       35[15]   * Address Line 1
NZARADD2  DIM       30[15]   * Address Line 2
NZARSUBR  DIM       30[15]   * Suburb
NZARCITY  DIM       30[15]   * City
NZARCTRY  DIM       30[15]   * Country
NZARDDTH  DIM       8[15]    * Date of Death
NZARDOMC  DIM       4[15]    * Domicile code
NZARRESI  DIM       1[15]    * Resident Status
NZARETHN  DIM       2[15]    * Ethnicity
NZARETH2  DIM       2[15]    * Ethnicity
NZARETH3  DIM       2[15]    * Ethnicity
NHIHCUID  DIM       7
.NZHISWRN  FORM      1
THEATFLG  FORM      1
SESSKEYS  DIM       22
COUNT     FORM      2
TOTALALI  FORM      3
ERETURND  FORM      1
.
.----------------------------------------------------------------------
PRGID     INIT      "NHIWEB96"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "NHI/MWS Patient Search Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
          BRANCH    EXIT,MAIN9999
          CALL      CONNECT                 * Start connection
          BRANCH    EXIT,MAIN9999
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          CALL      DISCNNCT  
          CALL      CONNECT                 * Start connection
          BRANCH    EXIT,MAIN1000
.
          BRANCH    REPORTNO,MAIN1100
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      SRHNHI00                * Search National Health Index
          BRANCH    EXIT,MAIN1000
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
.  Search National Health Index
.------------------------------------------------------------
SRHNHI00  MOVE      "NHI/MWS Patient Search",WEBTITLE  
          MATCH     SP70,URNUMBER
          GOTO      SRHNHI80 IF NOT EQUAL
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,SRHNHI99
.
          CALL      NZDSP000                * Get Array of Result Records
          BRANCH    EXIT,SRHNHI99
.
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ZERO,EXIT
          GOTO      SRHNHI99
.
SRHNHI80  CALL      CHKLC000
.
SRHNHI90  MOVE      ONE,EXIT
          MOVE      ZERO,NZSRH001
SRHNHI99  RETURN
.
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
CHKLC000  CALL      CLRNZIB0                * Clear National details 
          CALL      CLNHIMAS                * Clear Local details
          MATCH     SP70,URNUMBER           * Registration Form if Blank
          GOTO      CHKLC999 IF EQUAL       * Process Form
.
          UNPACK    URNUMBER,KEY1,KEY7
          MOVE      KEY7,NHIHCUID
          MOVE      NHIHCUID,NZIBNMPI
          CALL      CONNECT
          CALL      GETHCU                  * Read National Details
          BRANCH    OVRCD,CHKLC900
.
          PACK      KEY7,NHIHCUID,SP70
          CALL      RDNHMAS1                * Read Local NHI Details
          IF        OVRCD=0
            MOVE      NHMANMPI,NHIHCUID
            MOVE      NHMAURNO,URNUMBER
            MOVE      NHMAURNO,KEY8
            CALL      RDMAST1
            BRANCH    OVRCD,CHKLC920
            REP       " +",URNUMBER
            CLEAR     REDIRECT
            APPEND    "nhiweb99.pbl?reportno=1&template=002&urnumber=",REDIRECT
            APPEND    URNUMBER,REDIRECT
            RESET     REDIRECT
.
          ELSE
            REP       " +",NHIHCUID
            CLEAR     REDIRECT
            APPEND    "nhiweb99.pbl?reportno=1&template=004&nhihcuid=",REDIRECT
            APPEND    NHIHCUID,REDIRECT
            RESET     REDIRECT
            CALL      CONF0000
          ENDIF
.
          MOVE      NHMANMPI,NHIHCUID
          MOVE      NHMAURNO,URNUMBER
          MOVE      NHMAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,CHKLC920
          REP       " +",URNUMBER
          CLEAR     REDIRECT
          APPEND    "nhiweb99.pbl?reportno=1&template=002&urnumber=",REDIRECT
          APPEND    URNUMBER,REDIRECT
          RESET     REDIRECT
          COMPARE   ONE,THEATFLG
          IF        @EQUAL
            CLEAR     REDIRECT
            APPEND    "oprweb01.pbl?reportno=9&template=002&urnumber=",REDIRECT
            APPEND    URNUMBER,REDIRECT
            APPEND    "&sesskeys=",REDIRECT
            APPEND    SESSKEYS,REDIRECT
            RESET     REDIRECT
            CALL      PREDIR00
            GOTO      CHKLC999
          ENDIF
          CALL      RDIREC00
          GOTO      CHKLC999
.
CHKLC200  REP       " +",NHIHCUID
          CLEAR     REDIRECT
          APPEND    "nhiweb99.pbl?reportno=1&template=004&nhihcuid=",REDIRECT
          APPEND    NHIHCUID,REDIRECT
          APPEND    "&theatflg=",REDIRECT
          APPEND    THEATFLG,REDIRECT
          RESET     REDIRECT
          CALL      CONF0000
          GOTO      CHKLC999
.
CHKLC900  CALL      NZERR000
          CLEAR     REDIRECT
          APPEND    "nhiweb96.pbl?reportno=1&template=001",REDIRECT
          RESET     REDIRECT
          CALL      RDIREC00
          MOVE      ONE,EXIT
          GOTO      CHKLC999
.
CHKLC920  MOVE      "Invalid Patient Master Record.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKLC999
.
CHKLC999  RETURN
.
.------------------------------------------------------------------------------
.  Confirm registration
.------------------------------------------------------------------------------
CONF0000  CALL      OPENHTML
          BRANCH    EXIT,CONF9999
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                   "  var undefined;":
                   "  if (confirm(#"","\nRegister patient on the ":
                   " local system?#")) {";
          IF       THEATFLG=1
            WRITE  HTMLFILE;"  parent.location.href=#"",REDIRECT,"#"}";
          ELSE
            WRITE  HTMLFILE;"  location.href=#"",REDIRECT,"#"}";
          ENDIF
.
          WRITE    HTMLFILE;"  else {";
.
          IF       THEATFLG=1
            WRITE  HTMLFILE;"  location.href=#"nhiweb96.pbl?reportno=1&template=003";
          ELSE
            WRITE  HTMLFILE;"  location.href=#"nhiweb96.pbl?reportno=1&template=001";
          ENDIF
          WRITE    HTMLFILE;"#"}":
...               "      parent.PopUpScreen.style.display='none';  }":
..                 "    history.go(-1); }":
                   "}":
                   "</script>",012
.
          CALL      CLOSHTML
          GOTO      CONF9999
.
CONF9999  RETURN
.------------------------------------------------------------------------------
. PREDIR00 - Redirect Parent URL
.------------------------------------------------------------------------------
.
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
.                             " alert(#"",WEBTITLE,"#");":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
+
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  MOVE      ZERO,EXIT
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      NHIMASA1,"nhimasaf"
          OPEN      PATCODE1,"patcodes"
          READ      CONTROLF,HUND09;*177,PTCNNHTM
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,TEMPLATE
          MOVE      SP70,URNUMBER
          MOVE      SP70,NHIHCUID
          MOVE      ZERO,THEATFLG
          MOVE      ZERO,NZSRH001
          MOVE      SP70,NZSRH002
          MOVE      ZERO,NZSRH003
          MOVE      SP70,NZSRH004
          MOVE      SP70,NZSRH005
          MOVE      SP70,NZSRH006
          MOVE      ZERO,NZSRH007
          MOVE      ZERO,NZSRH008
          MOVE      SP70,NZSRH009
          MOVE      ZERO,ERETURND
          MOVE      "30",TMOTFRST
          MOVE      "30",TMOTNEXT
          MOVE      SP70,SESSKEYS

          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "theatflg=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,THEATFLG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            RJUSTIFY  URNUMBER
            PACK      URNUMBER,URNUMBER,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "sesskeys=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SESSKEYS
            PACK      SESSKEYS,SESSKEYS,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nzsrh",QRYNAME
          IF        @EQUAL
            CALL      SPNZS000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
SETPAR99  MOVE      "30",TMOTFRST
          MOVE      "30",TMOTNEXT
          RETURN
.------------------------------------------------------------
. Set NZSRH cgi parameters
.------------------------------------------------------------
SPNZS000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPNZS010,SPNZS020,SPNZS030,SPNZS040,SPNZS050:
                       SPNZS060,SPNZS070,SPNZS080,SPNZS090
.
          MOVE      ONE,EXIT
          GOTO      SPNZS999
.
SPNZS010  SQUEEZE   QRYDATA
          MOVE      QRYDATA,NZSRH001
          GOTO      SPNZS999 
.
SPNZS020  MOVE      QRYDATA,NZSRH002
          GOTO      SPNZS999 
.
SPNZS030  SQUEEZE   QRYDATA
          MOVE      QRYDATA,NZSRH003
          GOTO      SPNZS999 
.
SPNZS040  PACK      NZSRH004,QRYDATA,SP70
          GOTO      SPNZS999 
.
SPNZS050  PACK      NZSRH005,QRYDATA,SP70
          GOTO      SPNZS999 
.
SPNZS060  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00            * Set CMON from MTHNAM3
          PACK      NZSRH006,CCENT,CYEAR,CMON,CDAY
          GOTO      SPNZS999 
.
SPNZS070  SQUEEZE   QRYDATA
          MOVE      QRYDATA,NZSRH007
          GOTO      SPNZS999 
.
SPNZS080  SQUEEZE   QRYDATA
          MOVE      QRYDATA,NZSRH008
          GOTO      SPNZS999 
.
SPNZS090  MOVE      QRYDATA,NZSRH009
.
SPNZS999  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      NZSH0000
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. National Search Results
.------------------------------------------------------------
NZSH0000  BRANCH    FLDITMNO,NZSH0010,NZSH0020,NZSH0030,NZSH0040:
                             NZSH0050,NZSH0060,NZSH0070,NZSH0080:
                             NZSH0090,NZSH0100,NZSH0110,NZSH0120
.
          GOTO      NZSH9999
.
NZSH0010  WRITE     HTMLFILE;NZSRH003;
          GOTO      NZSH9999
.
NZSH0020  MATCH     SP70,NZSRH004
          GOTO      NZSH9999 IF EQUAL 
.
          WRITE     HTMLFILE;NZSRH004;
          GOTO      NZSH9999
.
NZSH0030  MATCH     SP70,NZSRH005
          GOTO      NZSH9999 IF EQUAL 
.
          WRITE     HTMLFILE;NZSRH005;
          GOTO      NZSH9999
.
NZSH0040  MATCH     SP70,NZSRH006
          GOTO      NZSH9999 IF EQUAL 
.
          UNPACK    NZSRH006,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT:
                    NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      NZSH9999
.
NZSH0050  COMPARE   ZERO,NZSRH007
          GOTO      NZSH9999 IF EQUAL 
          WRITE     HTMLFILE;NZSRH007;
          GOTO      NZSH9999
.
NZSH0060  WRITE     HTMLFILE;NZSRH008;
          GOTO      NZSH9999
.
NZSH0070  WRITE     HTMLFILE;NZIBNMRE;
          GOTO      NZSH9999
.
NZSH0080  WRITE     HTMLFILE;NZSRH009;
          GOTO      NZSH9999
.
NZSH0090  CALL      NEXT0000 
          GOTO      NZSH9999
.
NZSH0100  CALL      PREV0000
          GOTO      NZSH9999
.
NZSH0110  CALL      TABHED00
          GOTO      NZSH9999
.
NZSH0120  CALL      TABROW00 
          GOTO      NZSH9999
.
NZSH9999  RETURN
.------------------------------------------------------------
. Table Heading Row
.------------------------------------------------------------
TABHED00  WRITE     HTMLFILE;"<tr>":
                             "<td class=TableCol1><b> NMPI No </b></td>": 
                             "<td class=TableCol2><b> Patient </b></td>": 
                             "<td class=TableCol3><b> Sex </b></td>": 
                             "<td class=TableCol4><b> D.O.B </b></td>": 
                             "</tr>"
TABHED99  RETURN
.------------------------------------------------------------
. Table Details Row
.------------------------------------------------------------
TABROW00  COMPARE   ONE,ERETURND
          IF        @EQUAL
            WRITE     HTMLFILE;"<script type=#"text/javascript#">":
                               "alert(#"",NZIBERRD,"#");":
                               "</script>";
            GOTO      TABROW99
          ENDIF
.
          COMPARE   ZERO,NZSRH001
          GOTO      TABROW99 IF EQUAL
          SQUEEZE   NZIBNMRP
          MOVE      NZIBNMRP,FORM2
          COMPARE   ZERO,FORM2                    * a match found?
          GOTO      TABROW95 IF EQUAL
.
. loop until records are finished displaying
.
          SQUEEZE   NZIBNMRP
          MOVE      NZIBNMRP,NZFORM3B                rf 643903
          MOVE      ONE,TOTALCNT                  * initialise total counter
          WHILE     TOTALCNT<=NZFORM3B
            CALL      OUTROW00
            ADD       ONE,TOTALCNT
          DO
          GOTO      TABROW99
.
TABROW95  WRITE     HTMLFILE;"<tr>":
                        "<td colspan=4 align=center>":
                        "<b>NO RECORDS FOUND</td></tr>"
          GOTO      TABROW99
.
TABROW99  RETURN
.
. Output Table Row
.
OUTROW00  UNPACK    NZARDOB[TOTALCNT],CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          IF        F2 = 0
            MOVE      "00 ", MTHNAM3
          ELSE
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          ENDIF
          MOVE      SP8,DISPSEX
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      NZARSEX[TOTALCNT],DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DISPSEX
.* ******************************************   End of Changes         *C-49016
.
          MOVE      SP70,NHMAURNO
          MOVE      NZARNMPI[TOTALCNT],NHMANMPI
          MOVE      NZARNMPI[TOTALCNT],KEY7
          CALL      RDNHMAS1
          MOVE      OVRCD,LOCALSTA
          WRITE     HTMLFILE;"<tr valign=middle>":
                      "<td class=TableCol1>":
                      "<img src=../images/patdet.gif border=0 hspace=4 ":
                      "align=absmiddle onClick='PatientSelected(#"":
                      NHMANMPI,"#",#"",NHMAURNO,"#",#"",LOCALSTA,"#")'>":
                      NZARNMPI[TOTALCNT],"</td>": 
                      "<td class=TableCol2><b>":
                      NZARSURN[TOTALCNT],"</b>",", ":
                      NZARGIV1[TOTALCNT];
          MATCH     SP70,NZARGIV2[TOTALCNT]
          IF        !@EQUAL
            WRITE     HTMLFILE;", ",NZARGIV2[TOTALCNT];
          ENDIF
          MATCH     SP70,NZARGIV3[TOTALCNT]
          IF        !@EQUAL
            WRITE     HTMLFILE;", ",NZARGIV3[TOTALCNT];
          ENDIF
          WRITE     HTMLFILE;"<br>":
                      NZARADD1[TOTALCNT],NZARADD2[TOTALCNT]:
                      NZARSUBR[TOTALCNT],NZARCITY[TOTALCNT]:
                      NZARCTRY[TOTALCNT],NZARDOMC[TOTALCNT],"</td>": 
                      "<td class=TableCol3>":
                       DISPSEX,"</td>": 
                      "<td class=TableCol4>":
                       CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td></tr>"
          RETURN
.------------------------------------------------------------
. Link to Next Group
.------------------------------------------------------------
NEXT0000  BRANCH    NZSRH001,NEXT0100,NEXT0100
.          
          WRITE     HTMLFILE;"alert(#"No Search in Progress#");";
          GOTO      NEXT9999
.
NEXT0100  MATCH     SP70,NZIBNMRP
          IF        @EQUAL 
            WRITE     HTMLFILE;"alert(#"No more Patient Records#");";
            GOTO      NEXT9999
          ELSE
            WRITE     HTMLFILE;"NextPage(#"",NZIBCPTR,"#");";
          ENDIF
.
NEXT9999  RETURN
.------------------------------------------------------------
. Link to Previous Group
.------------------------------------------------------------
PREV0000  BRANCH    NZSRH001,PREV0100,PREV0200
.
          WRITE     HTMLFILE;"alert(#"No Search in Progress#");";
          GOTO      PREV9999
.
PREV0100  WRITE     HTMLFILE;"alert(#"No more Patient Records#");";
          GOTO      PREV9999
.
PREV0200  WRITE     HTMLFILE;"history.back();";
          GOTO      PREV9999
.
PREV9999  RETURN
.************************************************************************
.*  SRHHCU  :  Initiate a Surname search on the National database       *
.************************************************************************
SRHHCU    MOVE      ZERO,ERETURND
          MOVE      "5 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "294",WRLNGTH                 * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                          NZIBSTYP,NZIBSTIM,NZIBRLIM:
                          NZIBSURN,NZIBGIV1,NZIBGIV2,NZIBGIV3,NZIBPRNM,NZIBNSTY:
                          NZIBADD1,NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY:
                          NZIBASTY,NZIBMGEN,NZIBMDOB,NZIBMAGE,NZIBMPAG;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC                 * valid read?
          GOTO      SRHCU900 IF NOT EQUAL         * error
.
          MOVE      "30",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBNMFN,NZIBNMRE,NZIBNMRP:
                             NZIBCPTR;
.
          MOVE      "280",RDLNGTH                * FIFO record length (reply)
          MOVE      ONE,NZIBCNT
.
          SQUEEZE   NZIBNMRP
          MOVE      NZIBNMRP,FORM2
          WHILE     NZIBCNT <= FORM2
            READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZARNMPI[NZIBCNT]:
                               NZARSURN[NZIBCNT]:
                               NZARGIV1[NZIBCNT],NZARGIV2[NZIBCNT]:
                               NZARGIV3[NZIBCNT],NZARPRNM[NZIBCNT]:
                               NZARADD1[NZIBCNT],NZARADD2[NZIBCNT]:
                               NZARSUBR[NZIBCNT],NZARCITY[NZIBCNT]:
                               NZARCTRY[NZIBCNT],NZARDOB[NZIBCNT]:
                               NZARDDTH[NZIBCNT],NZARDOMC[NZIBCNT]:
                               NZARRESI[NZIBCNT],NZARETHN[NZIBCNT]:
                               NZARETH2[NZIBCNT],NZARETH3[NZIBCNT]:
                               NZARSEX[NZIBCNT]:
                               NZIBALNM,NZIBMWDN,NZIBMWST,NZIBMERG;
            ADD       ONE,NZIBCNT
          DO
.
          MOVE      ZERO,OVRCD                    * valid read
          GOTO      SRHCU999
.
SRHCU900  MOVE      ONE,ERETURND                 * an error was encountered
.
SRHCU999  RETURN
.************************************************************************
.*  NXTHCU  :  Continue a surname search on the National database       *
.************************************************************************
NXTHCU    MOVE      "6 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "59",WRLNGTH                  * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBCPTR,NZIBSTIM,NZIBRLIM,NZIBTSRC;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC                * valid read?
          GOTO      NXHCU900 IF NOT EQUAL        * error
.
          MOVE      "30",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBNMFN,NZIBNMRE,NZIBNMRP:
                             NZIBCPTR;
.
          MOVE      "280",RDLNGTH                * FIFO record length (reply)
          MOVE      ONE,NZIBCNT
          SQUEEZE   NZIBNMRP
          MOVE      NZIBNMRP,FORM2
          WHILE     NZIBCNT <= FORM2
            READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZARNMPI[NZIBCNT]:
                               NZARSURN[NZIBCNT]:
                               NZARGIV1[NZIBCNT],NZARGIV2[NZIBCNT]:
                               NZARGIV3[NZIBCNT],NZARPRNM[NZIBCNT]:
                               NZARADD1[NZIBCNT],NZARADD2[NZIBCNT]:
                               NZARSUBR[NZIBCNT],NZARCITY[NZIBCNT]:
                               NZARCTRY[NZIBCNT],NZARDOB[NZIBCNT]:
                               NZARDDTH[NZIBCNT],NZARDOMC[NZIBCNT]:
                               NZARRESI[NZIBCNT],NZARETHN[NZIBCNT]:
                               NZARETH2[NZIBCNT],NZARETH3[NZIBCNT]:
                               NZARSEX[NZIBCNT]:
                               NZIBALNM,NZIBMWDN,NZIBMWST,NZIBMERG;
            ADD       ONE,NZIBCNT
          DO
.
          MOVE      ZERO,OVRCD                    * valid read
          GOTO      NXHCU999
.
NXHCU900  MOVE      ONE,OVRCD                     * an error was encountered
.
NXHCU999  RETURN
.***************************************************************
.*  NZSET000  :  Set up vars for FIFO                          *
.***************************************************************
NZSET000  MATCH     SP70,NZSRH006             * use DOB search if DOB entered
          GOTO      NZSET100 IF NOT EQUAL
.
          MOVE      SP70,NZIBMDOB
          MOVE      "6",NZIBSTYP                * search type Range of Ages
          MOVE      NZSRH007,NZIBMAGE
          REP       " 0",NZIBMAGE
          MOVE      NZSRH008,NZIBMPAG
          REP       " 0",NZIBMPAG
          GOTO      NZSET200
.
NZSET100  MOVE      NZSRH006,NZIBMDOB
          MOVE      SP30,NZIBMAGE
          MOVE      "00",NZIBMPAG
          MOVE      "5",NZIBSTYP                * DOB Search
          GOTO      NZSET200
.
NZSET150  MOVE      SP30,NZIBMDOB
          MOVE      NZSRH007,NZIBMAGE
          REP       " 0",NZIBMAGE
          MOVE      ONE,NZSRH008
          MOVE      NZSRH008,NZIBMPAG
          REP       " 0",NZIBMPAG
.
NZSET200  BRANCH    NZSRH003,NZSET300,NZSET300,NZSET300,NZSET210,NZSET220:
                             NZSET230
NZSET210  MOVE      "7",NZIBSTYP                * search type Fast
          GOTO      NZSET300
NZSET220  MOVE      "8",NZIBSTYP                * search type Normal
          GOTO      NZSET300
NZSET230  MOVE      "9",NZIBSTYP                * search type Wide
.
NZSET300  
.
.  Need to read control file for this parameter PTCNMPTR
.  However I think it must be the 15 that is set as the init value.
.
.          MOVE      PTCNMPTR,NZIBRLIM                  * reply limit
.          REP       " 0",NZIBRLIM
.
          MOVE      NZSRH009,NZIBMGEN
          LOAD      NZIBNSTY,NZSRH003,ANSW,ANSS,ANSM,ANSF,ANSN,ANSD * Name Type
          PACK      NZIBADD1,SP30,SP5
          MOVE      SP30,NZIBADD2
          MOVE      SP30,NZIBSUBR
          MOVE      SP30,NZIBCITY
          MOVE      SP30,NZIBCTRY
          MOVE      SP1,NZIBASTY
          MOVE      "1",NZIBPRNM
.
          PACK      NZIBSURN,NZSRH004,SP70              * set up Name variables
          PACK      NZIBGIV1,NZSRH005,SP70
          MOVE      SP30,NZIBGIV2
          MOVE      SP30,NZIBGIV3
          CALL      FIXGIV00                      * Set up Given Name Variables
.
NZSET999  RETURN
.-------------------------------------------------
. Format Given Names
.-------------------------------------------------
FIXGIV00  MOVE      SP20,NZIBGIV1
          MOVE      SP20,NZIBGIV2
          MOVE      SP20,NZIBGIV3
          PACK      NZTMPD25,NZSRH005,SP70
          MOVE      ONE,EXIT                 * handy form field
.
.         Eliminate leading blanks
.
FIXGIV10  CMATCH    SP1,NZTMPD25
          IF        @EQUAL
            BUMP      NZTMPD25
            GOTO      FIXGIV10 IF NOT EOS
            GOTO      FIXGIV90               * entire name if blank
          ENDIF
          PACK      KEY60,NZTMPD25,SP70        * reset form pointer
          MOVE      KEY60,NZTMPD25
.
          SCAN      SP1,NZTMPD25               * Locate the first blank
          GOTO      FIXGIV20 IF NOT EQUAL
          BUMP      NZTMPD25,-1              * go back 1 to end of name
          MOVEFPTR  NZTMPD25,CCITEM          * another handly form field
          RESET     NZTMPD25                 * reset the form pointer
          SETLPTR   NZTMPD25,CCITEM          * set logical length to end of name
.
.         Save this name
.
          STORE     NZTMPD25,EXIT,NZIBGIV1,NZIBGIV2,NZIBGIV3
.
.         Check for any more names
.
          SETLPTR   NZTMPD25,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     NZTMPD25,CCITEM          * reset to this point
          PACK      KEY60,NZTMPD25,SP30      * reset form pointer
          MOVE      KEY60,NZTMPD25
.
          ADD       ONE,EXIT                 * look for next word
          BRANCH    EXIT,FIXGIV10,FIXGIV10,FIXGIV10
          GOTO      FIXGIV90
.
.         Rest of the string is one name
.
FIXGIV20  STORE     NZTMPD25,EXIT,NZIBGIV1,NZIBGIV2,NZIBGIV3
.
.         Pad names out with blanks
.
FIXGIV90  PACK      NZIBGIV1,NZIBGIV1,SP30
          PACK      NZIBGIV2,NZIBGIV2,SP30
          PACK      NZIBGIV3,NZIBGIV3,SP30
.
FIXGIV99  RETURN
.------------------------------------------------------------
. Display Table of Patient Records             
.------------------------------------------------------------
NZDSP000  MOVE      ZERO,NZIBREGO               * assume register option not sel
.
          BRANCH    NZSRH001,NZDSP010,NZDSP020
          MOVE      ZERO,EXIT
          GOTO      NZDSP999
.
NZDSP010  CALL      NZSET000                * Setup vars for FIFO         
          CALL      SRHHCU                        * initiate surname search
          BRANCH    OVRCD,NZDSP800                * error
          MOVE      ZERO,EXIT
          GOTO      NZDSP999
.
NZDSP020  MOVE      NZSRH002,NZIBCPTR             * continuation ptr
          MOVE      "N",NZIBTSRC                  * Terminate search flag = No
          CALL      NXTHCU                        * get next 15 records    
          BRANCH    OVRCD,NZDSP800                * error
          MOVE      ZERO,EXIT
          GOTO      NZDSP999
.
NZDSP800  CALL      NZERR000                     * display error message
          MOVE      ONE,EXIT
.
NZDSP999  RETURN
.***************************************************************************
.*  NZERR000  :  Display error message                                     *
.***************************************************************************
NZERR000  MOVE      NZIBERRD,NZIBDM40
          STRIP     NZIBDM40
          CLEAR     WEBTITLE
          APPEND    "Error: ",WEBTITLE
          APPEND    NZIBERRN,WEBTITLE
          APPEND    SP2,WEBTITLE
          APPEND    NZIBDM40,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
.
NZERR999  RETURN
.------------------------------------------------------------
.   Clear nhi details
.------------------------------------------------------------
CLRNZIB0  MOVE     SP70,NZIBSURN
          MOVE     SP70,NZIBGIV1
          MOVE     SP70,NZIBGIV2
          MOVE     SP70,NZIBGIV3
          MOVE     SP70,NZIBPRNM
          MOVE     SP70,NZIBADD1
          MOVE     SP70,NZIBADD2
          MOVE     SP70,NZIBSUBR
          MOVE     SP70,NZIBCITY
          MOVE     SP70,NZIBCTRY
          MOVE     SP70,NZIBDOB
          MOVE     SP70,NZIBDDTH
          MOVE     SP70,NZIBDOMC
          MOVE     SP70,NZIBRESI
          MOVE     SP70,NZIBETHN
          MOVE     SP70,NZIBETH2
          MOVE     SP70,NZIBETH3
          MOVE     SP70,NZIBSEX
.
.         Setting up default values
.
          MATCH     "u",NZIBSEX
          IF        @EQUAL
            PACK     NZIBSEX,SP70
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Clear Local NHI Details
.------------------------------------------------------------
CLNHIMAS  MOVE      SP70,NHMASURN
          MOVE      SP70,NHMAGIV1
          MOVE      SP70,NHMAGIV2
          MOVE      SP70,NHMAGIV3
          MOVE      SP70,NHMAPREI
          MOVE      SP70,NHMAADD1
          MOVE      SP70,NHMAADD2
          MOVE      SP70,NHMAADD3
          MOVE      SP70,NHMAADD4
          MOVE      SP70,NHMAADD5
          MOVE      SP70,NHMADOB
          MOVE      SP70,NHMADDTH
          MOVE      SP70,NHMADOMC
          MOVE      SP70,NHMARES
          MOVE      SP70,NHMAETH
          MOVE      SP70,NHMAETH2
          MOVE      SP70,NHMAETH3
          MOVE      SP70,NHMASEX
          PACK      NHMADAT,CCC,CYY,CMM,CDD
          CLOCK     TIME,NHMATIM
          MOVE      SP70,NHMASPA
.
          RETURN
.------------------------------------------------------------
          INC       NZHISWIO
          INC       NHIMASIO/INC
          INC       PATMA1IO/INC
          INC       PATMWSIO/INC
          INC       TMPALIIO/INC
.
          INC       IBAWEBCD
.

.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAPAT83                                             *
.*                                                                            *
.*     FUNCTION:         PRE-ADMISSIONS FOR THE DAY REPORT                    *
.*                                                                            *
.*     DATE WRITTEN:     22/08/1985                                           *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V9.09.01  22/10/2007  Davin         CAR 151920                      *
.*                  Changed to allow selection of 'All' hospitals for mult hos*
.*        V9.04.01  23/12/2004  Jill Habasque CAR 55353                       *
.*                  Changed to use pmsvx1af instead of bokrc1af for multi hosp*
.*        V9.02.01  14/11/2001  Steve Armstrong  API mods                     *
.*                  Changed PHOSP to use zero for "No".                       *
.******************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.B02 25/01/1999  Nicole Harrington 507 rebound 141             *
.*                  Mods to print admission date with century                 *
.                   09/11/1998  Glenn Berry      5.07 changes
.******************************************************************************
.* Release 5.02                                                               *
.*        V5.02.01  03/02/1995  Gabrielle       SRF 134358                    *
.*                  Fixed selecting pre-admissions for the keyin date only    *
.******************************************************************************
.
          INC       STD001FD   
.
          INC       BOKRC1FD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATWR1FD/INC
          INC       PMSVX1FD/INC
+
.
.         IBACONFD Variable
.
IBCNMHOS  FORM      1
.
.         Work Variables
.
ACTDATE   DIM       8
.
DIM30     DIM       30
DALERG    DIM       7
DALPHA    DIM       6
DANS      DIM       7
DANS7     DIM       10
DHHMM     DIM       5
DHOSP     DIM       7
DILLN     DIM       7
DNAME     DIM       15
.
MIN1      FORM      1
NAME      DIM       22                 * MFNAME.MSNAME
NOP       FORM      5
.
PAGENO    FORM      3
PGNAME17  DIM       17
PSNAME15  DIM       15
.
TODATE    DIM       8
.
XDAY1     DIM       2
XDAY2     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
.
.         Constants
.
DOCCP     INIT      "Occupation"
DSPEC     INIT      "Spec Cond "
.
PRGID     INIT      "IBAPAT83"
PRGNAM    INIT      "Pre Admissions For The Day Report"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*164,CSPECCO
          READ      CONTROLF,EIGHTY;*144,PTCNRP83
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
            DISPLAY   *P64:24,"patwr1af";
            OPEN      PATWR1A1,"patwr1af"
            DISPLAY   *P64:24,"bokrc1af";
            OPEN      BOKRC1A1,"bokrc1af"
          ENDIF
.
          MOVE      DOCCP,DANS7
          LOAD      DANS7 USING CSPECCO OF DOCCP,DSPEC
.
.         set up KEYDATE input requirements
.
          MOVE      ZERO,CCANLDTE
          MOVE      ZERO,CHIGHLT
+
.
.         START OF PROGRAM
.
          IF        IBCNMHOS = 1
            DISPLAY   *P1:5,*EL,"Hospital Id : ";
            MOVE      TEN5,ECOL
            MOVE      FIVE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
START       CALL      PATHSPKY
            BRANCH    EXIT,STARTB,ENDIT
            GOTO      STARTC
.
STARTB      MOVE       "All Hospitals",PTHSNAME
            MOVE       SP10,PTHSHOSP
.
STARTC      DISPLAY   *P22:5,*EL,PTHSNAME,*W2;
          ENDIF
.
.         MAIN  SCREEN
.
MAINSCRN  DISPLAY   *P1:6,*EF,*P1:8,"Keyin Admission Date : ";
.
.         Keyin Date
.
KADATE    DISPLAY   *P24:8,*EF;
          MOVE      "24",CCOL
          MOVE      "8",CVERT
          MOVE      CCC,CCENT
.
KADATE1   UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,KADATE1
          BRANCH    OVRCD,ENDIT
.
ENDAD1    PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          UNPACK    TODATE,XCENT1,XYEAR1,XMON1,XDAY1
.
.         Check if dates are ok or not
.
REKEY     KEYIN     *P1:24,*EF,"Date okay (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     "N" TO ANS
          GOTO      KADATE IF EQUAL
          MATCH     "Y" TO ANS
          GOTO      ADMNNO IF EQUAL
          BEEP
          GOTO      REKEY
+
.
.         PRINT VIA ADMISSION NUMBER
.
.         Get only Pre-Admissions... i.e. ASTAT=1
.
ADMNNO    MOVE      "60",CLNO
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,PAGENO
          MOVE      ZERO,NOP
.
          DISPLAY   *P1:24,*EL,*P27:24,*V2LON,"***  Generating Report  ***",*W2:
                    *P17:24,*EL,*HOFF,"Admission :",*P46:24,"Scanning :";
.
          PACK      KEY9 WITH ONE,SP8
          CALL      RDSMISS2
.
NEXTA     CALL      RDKMISS2
          BRANCH    OVRCD OF ENDP
.
          COMPARE   ONE,ASTAT
          GOTO      ENDP IF NOT EQUAL
          DISPLAY   *P57:24,*EL,AADMNO;
.
          COMPARE   ONE,IBCNMHOS
          GOTO      NEXTB IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP           * 'all' hospitals selected
          GOTO      NEXTB IF EQUAL
.
          PACK      KEY8,DAADMNO,SP10
          CALL      RDPMVX11
          BRANCH    OVRCD,NEXTA
.
          MATCH     PMVXMHOS,PTHSHOSP       * only want selected hospital
          GOTO      NEXTA IF NOT EQUAL
.
NEXTB     UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
          PACK      ACTDATE,XCENT,XYEAR,XMON,XDAY
.
          MATCH     TODATE,ACTDATE
          GOTO      NEXTA IF NOT EQUAL
.
          MOVE      AURNO,KEY8
.         MOVE      AURNO,URNUMBER
          CALL      RDMAST1
          COMPARE   ZERO,OVRCD
          GOTO      OKADM IF EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD OF NEXTA
.
.         MOVE      "   N/A",URNUMBER
.
OKADM     MOVE      SP30,DNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
.
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          MOVE      DSNAME,DALPHA
.
          MOVE      DUNK,DALERG
          MOVE      DUNK,DILLN 
          MOVE      DUNK,DHOSP 
          LOAD      DALERG FROM AALERG OF DYES,DNO
          LOAD      DILLN FROM AILLN OF DYES,DNO
          MOVE      DNO,DHOSP
          LOAD      DHOSP,PHOSP,DYES
.
          MOVE      PGNAME,PGNAME17
          MOVE      PSNAME,PSNAME15
          CALL      DISPDA
          DISPLAY   *P29:24,*V2LON,AADMNO;
          GOTO      NEXTA
.
NOADMN    DISPLAY   *P1:24,*EL,*B,*P20:24,*V2LON:
                    "***  No Pre-Admissions Available  ***",*W3;
          GOTO      MAINSCRN
+
.
.         OVER Condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,PAGENO  
          GOTO      NOADMN IF EQUAL
          CALL      PAGEND
          PRINT     *N,*6,"Number of Pre-Admissions printed : ",NOP:
                    *N,*N,*N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*V2LON,*P22:24:
                    "***  Report Generation Completed  ***",*W2;
          GOTO      MAINSCRN
+
.
.         PRINT ACTUAL DATA
.
DISPDA    IF        PTCNRP83=1
            CALL      DSPA0000              * do the Type 1 layout
            RETURN
          ENDIF
.                                           * do the Type 0 (normal) layout
          COMPARE   "55",CLNO
          CALL      HEAD     IF NOT LESS
.
          COMPARE   TWO,CSPECCO
          GOTO      DISPDA2 IF EQUAL
.
          PRINT      *1,"| ",AADMNO,*12,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *23,"|",ATIME,*32,"| ",PSNAME15," ",PGNAME17,*68,"| ",DHOSP:
                    *82,"| ",DILLN,*92,"| ",DNAME,*119,"|":
                     *N,"|",*12,"|",*23,"|",*32,"|",*68,"|",*82,"|":
                    *92,"|",*119,"|"
          ADD       ONE TO NOP
          ADD       TWO,CLNO
          RETURN
+
.
DISPDA2   PRINT      *1,"| ",AADMNO,*11,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *21,"|",ATIME,*30,"| ",PSNAME15," ",PGNAME17,*66,"| ",DHOSP:
                    *80,"| ",DILLN,*90,"| ",DNAME,*115,"| ",POCCUP,*132,"|":
                     *N,"|",*11,"|",*21,"|",*30,"|",*66,"|",*80,"|":
                    *90,"|",*115,"|",*132,"|"
          ADD       ONE TO NOP
          ADD       TWO,CLNO
          RETURN
+
.         Last Line on the page
.
PAGEND    IF        PTCNRP83=1
            CALL      UND132
            RETURN
          ENDIF
.
          COMPARE   TWO,CSPECCO
          GOTO      PAGEND2 IF EQUAL
.
          PRINT     "*---------------------------------------------------":
                    "-----------------------------------------":
                    "-------------------------*"
          ADD       ONE,CLNO
          RETURN
.
PAGEND2   PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.         Headings for Output
.
HEAD      IF        PTCNRP83=1
            CALL      PGEA0000               * page for layout type 1
            RETURN
          ENDIF
.                                            * page for layout type 0
          COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
          ADD       ONE,PAGENO
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          IF        IBCNMHOS = 1
            PRINT     *40,"Hospital : ",PTHSNAME,*N
          ENDIF
.
          PRINT     *40,"Pre-Admissions for : ":
                        XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1,*N
.
          CALL      PAGEND
.
          COMPARE   TWO,CSPECCO
          GOTO      HEAD2 IF EQUAL
.
          PRINT      *1,"|  Adm No.",*12,"|Adm Date  |Adm Time":
                    *32,"| Patient's Name",*68,"|Last 12 Mths",*82,"|Prev Illn":
                    *92,"| Attending Doctor",*119,"|"
          GOTO      ENDHEAD
.
HEAD2     PRINT      *1,"| Adm No.",*11,"|Adm Date",*21,"|Adm Time":
                    *30,"| Patient's Name",*66,"|Last 12 Mths",*80,"|Prev Illn":
                    *90,"| Attending Doctor",*115,"| Spec Cond.",*132,"|"
.
ENDHEAD   CALL      PAGEND
          IF        IBCNMHOS = 1
            MOVE      TEN,CLNO
          ELSE
            MOVE      EIGHT,CLNO
          ENDIF
          RETURN
+
.
.********************************************************************
.DSPA0000 - Display Layout Type 1
.********************************************************************
DSPA0000  COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      ADIAG1,DIM30
.
          PRINT      *1,"| ",PURNO,*12,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *23,"|",ATIME,*32,"| ",PSNAME15," ",PGNAME17:
                    *68,"| ",PSEX:
                    *74,"| ",PSAGE: 
                    *80,"| ",DIM30:
                    *113,"| ",DNAME:
                    *132,"|"
          ADD       ONE TO NOP
          ADD       ONE,CLNO
.
DSPA9999  RETURN
+
.
.********************************************************************
. PGEA0000 - New page for layout 1
.********************************************************************
PGEA0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          IF        IBCNMHOS = 1
            PRINT     *43,"Hospital : ",PTHSNAME,*N
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *43,"Pre-Admissions for : ":
                        XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1,*N
          ADD       ONE,CLNO
.
          CALL      UND132
.
          PRINT     *1,"|  U/R No.",*12,"| Adm Date |Adm Time",*32:
                    "| Patient's Name",*68,"| Sex | Age | Admitting Diagnosis":
                    *113,"| Attending Doctor |"
          ADD       ONE,CLNO
.
          CALL      UND132
.
PGEA9999  RETURN
+
.
.==============================================================================
ENDIT     CHAIN     PGM
          STOP
.==============================================================================
.
          INC       STD001IO    
.
          INC       PATHSPKY
.
          INC       BOKRC1IO/INC
          INC       PATHSPIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATWR1IO/INC
          INC       PMSVX1IO/INC
+

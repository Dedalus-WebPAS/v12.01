.************************************************************************
.*  Program   :  IBASYS63                                               *
.*  Function  :  PABX Extension file maintenance                        *
.*  Author    :  ROD   22/10/1996                                       *
.*  Mods      :                                                         * 
.*        V9.02.00  06/05/2002  J.Tan                                   *
.*                  Ported from r6.09                                   *
.*          C2.00.01 17/04/2000   Tonii    SRF 638489                   *
.*                   Opened CONTROLF file to fix I*C error              *
.************************************************************************
.*               V6.04.02 19/12/96  ROD                                 *
.*                        Added Phone Type and Access Level             *
.*               V6.04.03 24/12/96  ROD                                 *
.*                        Added Access Level Off                        *
.************************************************************************
.
          INC       STDGENDF
          INC       IBACHGFD/INC
          INC       IBAEXTFD/INC
          INC       PATCODFD/INC
          INC       DEBDBTFD/INC
          INC       DEBCODFD/INC
.
CODE      DIM       2
PTYPDESC  DIM       20
LVLDDESC  DIM       20
LEVLDESC  DIM       20
CFDBSQLA  DIM       20
CFDBSQLB  DIM       20
EOSFLG    FORM      1
SRVLDESC  DIM       20
PHONDESC  DIM       20
TYPEDESC  DIM       20
WODESC    INIT      "Ward Only"
WBDESC    INIT      "Ward/Bed"
TEDESC    INIT      "Tenant"
ARDESC    INIT      "Accounts Receivable"
.
PRGID     INIT      "IBASYS63"
PRGNAM    INIT      "PABX Extension file Maintenance"
VERSION   INIT      "V12.01.00"
+
.************************************************************************
.*  ML0000  :  Mainline                                                 *
.************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      SELOPTN
          BRANCH    OPTION,ML3000,ML4000,ML5000   * Insert, Change, Delete
          GOTO      ML9999
.
. --- Insert mode ---
.
ML3000    CALL      KEXT0000                      * keyin Extension code
          BRANCH    EXIT,ML1000,ML9999            * nothing input
          CALL      INSR0000                      * keyin fields Insert mode
          BRANCH    EXIT,ML3000                   * Exitchar
          CALL      CHNG0000                      * keyin fields Change mode
          BRANCH    EXIT,ML3000                   * Cancel selected
          CALL      POST0000                      * post
          GOTO      ML3000
.
. --- Change mode ---
.
ML4000    CALL      KEXT0000                      * keyin Extension code
          BRANCH    EXIT,ML1000,ML9999            * nothing input
          CALL      DISP0000                      * display details for change
          CALL      CHNG0000                      * keyin fields change mode
          BRANCH    EXIT,ML4000                   * Cancel selected
          CALL      POST0000                      * post
          GOTO      ML4000
.
. --- Delete mode ---
.
ML5000    CALL      KEXT0000                      * keyin Extension code
          BRANCH    EXIT,ML1000,ML9999            * nothing input
          CALL      DISP0000                      * display details for change
          CALL      DELE0000                      * delete record if desired
          BRANCH    EXIT,ML1000                   * Exit to menu (Cancel)
          GOTO      ML5000
.
ML9999    CHAIN     PGM
+
.************************************************************************
.*  INIT0000  :  Initialisation                                         *
.************************************************************************
INIT0000  CALL      DISPHEAD
.
          OPEN      IBACHGA1,"ibachgaf"
          OPEN      IBAEXTA1,"ibaextaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      DEBDBTA1,"debdbtaf"
          OPEN      DEBDBTA2,"debdbtaf"
          OPEN      CONTROLF,"controlf"        * Srf 638489
.
INIT9999  RETURN
+
.************************************************************************
.*  KEXT0000  :  Keyin extension code                                   *
.************************************************************************
KEXT0000  DISPLAY   *P1:4,*EF:
                    *P6:4,"Extension code : "
.
.  allow "?" on change and delete modes
.
          BRANCH   OPTION,KEXT1000,KEXT2000,KEXT2000
.
KEXT1000  KEYIN    *P23:4,*DV,UNDLN4:
                   *P23:4,*V2LON,IBEXEXTN
          PACK     IBEXEXTN,IBEXEXTN,SP4
          MATCH    SP4,IBEXEXTN
          GOTO     KEXT9500 IF EQUAL
          MATCH    "\",IBEXEXTN
          GOTO     KEXT9500 IF EQUAL
          MATCH    "?",IBEXEXTN
          GOTO     KEXT1000 IF EQUAL
          GOTO     KEXT3000
.
KEXT2000  MOVE     "23",ECOL
          MOVE     "4",EVERT
          MOVE     SP4,CKYIDEF4
          MOVE     ZERO,CKYIMAND
          MOVE     ZERO,EOSFLG
.
          CALL     KEYINEXT
          BRANCH   EXIT,KEXT9500,KEXT9600

.         DISPLAY  *P27:4,IBEXDESC
          GOTO     KEXT5000
.
. insert mode so code must not exist already
.
KEXT3000  MOVE      SP20,IBEXDESC
          MOVE      SP3,IBEXCHRG
          MOVE      ONE,IBEXETYP
          MOVE      SP8,IBEXDEBT
.
          PACK      KEY4,IBEXEXTN
          CALL      RDIBEXT1
          BRANCH    OVRCD,KEXT8000

          DISPLAY   *P1:24,*EL,*B,"Code already exists.  ";
          CALL      HITENTER
          GOTO      KEXT0000

. Change or Delete mode so code must exist 
.
KEXT5000  PACK      KEY4,IBEXEXTN
          CALL      RDIBEXT1

          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
            CALL      HITENTER
            GOTO      KEXT0000
          ENDIF

. everything is Ok!
.
KEXT8000  MOVE      ZERO,EXIT
          GOTO      KEXT9999
.
KEXT9500  MOVE      ONE,EXIT
          GOTO      KEXT9999
.
KEXT9600  MOVE      TWO,EXIT
          GOTO      KEXT9999
.
KEXT9999  RETURN
+
.************************************************************************
.*  KDES0000  :  keyin description                                      *
.************************************************************************
KDES0000  KEYIN     *P23:6,*DV,UNDLN20:
                    *P23:6,*V2LON,IBEXDESC
.
          PACK      IBEXDESC,IBEXDESC,SP30
.
          MATCH     SP20,IBEXDESC
          GOTO      KDES0000 IF EQUAL
.
KDES9999  RETURN
+
.************************************************************************
.*  KPHT0000  :  Keyin Phone Type                                       *
.************************************************************************
KPHT0000  MOVE      "23",ECOL
          MOVE      "10",EVERT
          MOVE      "XV",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND
          MOVE      ONE,CNEWDS
.
          CALL      PATCODKY
          BRANCH    EXIT,KPHT9999,KPHT9000
.
          MOVE      ACODE,IBEXPTYP
.
          DISPLAY   *P31:10,TDESC
.
. everything is Ok!
.
KPHT8000  MOVE      ZERO,EXIT
          GOTO      KPHT9999
.
KPHT9000  MOVE      ONE,EXIT
.
KPHT9999  RETURN
+
.************************************************************************
.*  KSRV0000  :  Keyin Service Level                                    *
.************************************************************************
KSRV0000  MOVE      "23",ECOL
          MOVE      "13",EVERT
          MOVE      "XX",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND
          MOVE      ONE,CNEWDS
.
          CALL      PATCODKY
          BRANCH    EXIT,KSRV9999,KSRV9000
.
          MOVE      ACODE,IBEXSRVL
.
          DISPLAY   *P31:13,TDESC
.
. everything is Ok!
.
KSRV8000  MOVE      ZERO,EXIT
          GOTO      KSRV9999
.
KSRV9000  MOVE      ONE,EXIT
.
KSRV9999  RETURN
+
.************************************************************************
.*  KLEV0000  :  Keyin Access Level On                                  *
.************************************************************************
KLEV0000  MOVE      "23",ECOL
          MOVE      "11",EVERT
          MOVE      "XW",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND
          MOVE      ONE,CNEWDS
.
          CALL      PATCODKY
          BRANCH    EXIT,KLEV9999,KLEV9000
.
          MOVE      ACODE,IBEXLEVL
.
          DISPLAY   *P31:11,TDESC
.
. everything is Ok!
.
KLEV8000  MOVE      ZERO,EXIT
          GOTO      KLEV9999
.
KLEV9000  MOVE      ONE,EXIT
.
KLEV9999  RETURN
+
.************************************************************************
.*  KLVD0000  :  Keyin Access Level Off                                 *
.************************************************************************
KLVD0000  MOVE      "23",ECOL
          MOVE      "12",EVERT
          MOVE      "XW",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND
          MOVE      ONE,CNEWDS
.
          CALL      PATCODKY
          BRANCH    EXIT,KLVD9999,KLVD9000
.
          MOVE      ACODE,IBEXLVLD
.
          DISPLAY   *P31:12,TDESC
.
. everything is Ok!
.
KLVD8000  MOVE      ZERO,EXIT
          GOTO      KLVD9999
.
KLVD9000  MOVE      ONE,EXIT
.
KLVD9999  RETURN
+
.************************************************************************
.*  KCHG0000  :  Keyin Charge code                                      *
.************************************************************************
KCHG0000  MOVE      "23",ECOL
          MOVE      "7",EVERT
          MOVE      "CY",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND
          MOVE      ONE,CNEWDS
.
          CALL      PATCODKY
          BRANCH    EXIT,KCHG9999,KCHG9000
.
          MOVE      ACODE,IBEXCHRG
.
          DISPLAY   *P31:7,TDESC
.
          PACK      KEY3,IBEXCHRG
          CALL      RDIBCHG1
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Charge Rate not set up for this ":
                      "charge code.  ";
            CALL      HITENTER
            GOTO      KCHG0000
          ENDIF
.
. everything is Ok!
.
KCHG8000  MOVE      ZERO,EXIT
          GOTO      KCHG9999
.
KCHG9000  MOVE      ONE,EXIT
.
KCHG9999  RETURN
+
.************************************************************************
.*  KTYP0000  :  keyin extension type                                   *
.************************************************************************
KTYP0000  DISPLAY   *P1:24,*EL,*V2LON,"1",*HOFF,"=Ward/Bed, ":
                    *V2LON,"2",*HOFF,"=Tenant, ":
                    *V2LON,"3",*HOFF,"=Accounts Receivable, ":
                    *V2LON,"4",*HOFF,"=Ward Only"
.
          MOVE      ONE,IBEXETYP                      * default
          KEYIN     *P23:8,*V2LON,*RV,IBEXETYP
.
          IF        IBEXETYP<1 | IBEXETYP>4
            BEEP
            GOTO      KTYP0000
          ENDIF
.
          LOAD      TYPEDESC,IBEXETYP,WBDESC,TEDESC,ARDESC,WODESC
          DISPLAY   *P1:24,*EL,*P31:8,TYPEDESC
.
          IF        IBEXETYP<>3
            DISPLAY   *P23:9,*EL
            MOVE      SP8,IBEXDEBT
          ENDIF
.
KTYP9999  RETURN
+
.************************************************************************
.*  KTEN0000  :  keyin Tenant No.                                       *
.************************************************************************
KTEN0000  MOVE      ONE,IBEXTENN                      * default
          KEYIN     *P23:14,*V2LON,*RV,IBEXTENN
.
          IF        IBEXTENN<1 | IBEXTENN>15
            BEEP
            GOTO      KTEN0000
          ENDIF
.
KTEN9999  RETURN
+
.************************************************************************
.*  KDEB0000  :  keyin debtor                                           *
.************************************************************************
KDEB0000  MOVE      SP8,IBEXDEBT
.
          COMPARE   THREE,IBEXETYP         * must be Accounts Receivable
          GOTO      KDEB9999 IF NOT EQUAL
.
          MOVE      "9",CVERT
          MOVE      "23",CCOL
          MOVE      ZERO,CKEYTYP
.
          CALL      KDBDBA00                     * keyin debtor code (DEBDBTKY)
          BRANCH    EXIT,KDEB8000,KDEB9000
.
          MOVE      DBDBDEB,IBEXDEBT
          DISPLAY   *P31:9,DBDBNA1
          MOVE      ZERO,EXIT
          GOTO      KDEB9999
.
KDEB8000  DISPLAY   *P23:9,*EL
          MOVE      SP8,IBEXDEBT
          MOVE      ZERO,EXIT
          GOTO      KDEB9999
.
KDEB9000  MOVE      ONE,EXIT
          GOTO      KDEB9999
.
KDEB9999  RETURN
+
.************************************************************************
.*  INSR0000  :  Keyin fields insert mode                               *
.************************************************************************
INSR0000  CALL      DISP0000                      * display screen
.
          CALL      KDES0000                      * keyin description
.
          CALL      KCHG0000                      * keyin charge type
          BRANCH    EXIT,INSR9000                 * EXITCHAR
.
          CALL      KTYP0000                      * keyin Extension Type
          CALL      KDEB0000                      * keyin Debtor
          BRANCH    EXIT,INSR9000                 * EXITCHAR
.
          CALL      KPHT0000                      * keyin Phone type
          BRANCH    EXIT,INSR9000                 * EXITCHAR
.
          CALL      KLEV0000                      * keyin Access Level On
          BRANCH    EXIT,INSR9000                 * EXITCHAR
.
          CALL      KLVD0000                      * keyin Access Level Off
          BRANCH    EXIT,INSR9000                 * EXITCHAR
.
          CALL      KSRV0000                      * keyin Service Level
          BRANCH    EXIT,INSR9000                 * EXITCHAR
.
          CALL      KTEN0000                      * keyin Tenant Number
.
          MOVE      ZERO,EXIT
          GOTO      INSR9999

INSR9000  MOVE      ONE,EXIT                      * EXITCHAR
.
INSR9999  RETURN
+
.************************************************************************
.*  CHNG0000  :  Keyin fields change mode                               *
.************************************************************************
CHNG0000  CALL      MAINQST                       * Select item, Post, Cancel
          COMPARE   ZERO,CCITEM
          GOTO      CHNG8000 IF EQUAL
          GOTO      CHNG9000 IF LESS
.
          BRANCH    CCITEM,CHNG1000,CHNG2000,CHNG3000,CHNG4000,CHNG5000:
                           CHNG6000,CHNG6200,CHNG6500,CHNG6800
.
          BEEP
          GOTO      CHNG0000
.
CHNG1000  CALL      KDES0000                      * keyin description
          GOTO      CHNG0000
.
CHNG2000  CALL      KCHG0000                      * keyin charge type
          BRANCH    EXIT,CHNG9000                 * EXITCHAR
          GOTO      CHNG0000
.
CHNG3000  CALL      KTYP0000                      * keyin extension type
          GOTO      CHNG0000
.
CHNG4000  CALL      KDEB0000                      * keyin debtor
          BRANCH    EXIT,CHNG9000                 * EXITCHAR
          GOTO      CHNG0000
.
CHNG5000  CALL      KPHT0000                      * keyin Phone type
          BRANCH    EXIT,INSR9000                 * EXITCHAR
          GOTO      CHNG0000
.
CHNG6000  CALL      KLEV0000                      * keyin Access Level On
          BRANCH    EXIT,INSR9000                 * EXITCHAR
          GOTO      CHNG0000
.
CHNG6200  CALL      KLVD0000                      * keyin Access Level Off
          BRANCH    EXIT,INSR9000                 * EXITCHAR
          GOTO      CHNG0000
.
CHNG6500  CALL      KSRV0000                      * keyin Service Level
          BRANCH    EXIT,INSR9000                 * EXITCHAR
          GOTO      CHNG0000
.
CHNG6800  CALL      KTEN0000                      * keyin Tenant Number
          GOTO      CHNG0000
.
CHNG8000  MOVE      ZERO,EXIT                     * Post selected
          GOTO      CHNG9999
.
CHNG9000  MOVE      ONE,EXIT                      * Cancel selected
.
CHNG9999  RETURN
+
.************************************************************************
.*  DISP0000  :  display details for change mode                        *
.************************************************************************
DISP0000  DISPLAY   *P1:6,*EF,*V2LON," 1",*HOFF,". Description     : ":
                    *P1:7,*V2LON," 2",*HOFF,". Charge Type     : ":
                    *P1:8,*V2LON," 3",*HOFF,". Extension Type  : ":
                    *P1:9,*V2LON," 4",*HOFF,". Debtor          : ":
                    *P1:10,*V2LON," 5",*HOFF,". Phone Type      : ":
                    *P1:11,*V2LON," 6",*HOFF,". Access Level On : ":
                    *P1:12,*V2LON," 7",*HOFF,". Access Level Off: ":
                    *P1:13,*V2LON," 8",*HOFF,". Service Level   : ":
                    *P1:14,*V2LON," 9",*HOFF,". Tenant No.      : "
.
          COMPARE   ONE,OPTION             * dont display fields if insert mode
          GOTO      DISP9999 IF EQUAL
.
          MOVE      SP20,SRVLDESC
          MATCH     SP3,IBEXSRVL
          IF        !@EQUAL
            MOVE      "<<Missing code>>",TDESC
            PACK      KEY5,ANSX,ANSX,IBEXSRVL
            CALL      RDCODE1
            MOVE      TDESC,SRVLDESC
          ENDIF
.
          MOVE      SP20,PHONDESC
          MATCH     SP3,IBEXPTYP
          IF        !@EQUAL
            MOVE      "<<Missing code>>",TDESC
            PACK      KEY5,ANSX,ANSV,IBEXPTYP
            CALL      RDCODE1
            MOVE      TDESC,PTYPDESC
          ENDIF
.
          MOVE      SP20,LEVLDESC
          MATCH     SP3,IBEXLEVL
          IF        !@EQUAL
            MOVE      "<<Missing code>>",TDESC
            PACK      KEY5,ANSX,ANSW,IBEXLEVL
            CALL      RDCODE1
            MOVE      TDESC,LEVLDESC
          ENDIF
.
          MOVE      SP20,LVLDDESC
          MATCH     SP3,IBEXLVLD
          IF        !@EQUAL
            MOVE      "<<Missing code>>",TDESC
            PACK      KEY5,ANSX,ANSW,IBEXLVLD
            CALL      RDCODE1
            MOVE      TDESC,LVLDDESC
          ENDIF
.
          MOVE      SP20,TDESC
          MATCH     SP3,IBEXCHRG
          IF        !@EQUAL
            MOVE      "<<Missing code>>",TDESC
            PACK      KEY5,ANSC,ANSY,IBEXCHRG
            CALL      RDCODE1
          ENDIF
.
          LOAD      TYPEDESC,IBEXETYP,WBDESC,TEDESC,ARDESC,WODESC
.
          MOVE      SP30,DBDBNA1
          PACK      KEY8,IBEXDEBT
          CALL      RDDBDB1
.
          DISPLAY   *P23:6,*V2LON,IBEXDESC:
                    *P23:7,IBEXCHRG,*HOFF,*P31:7,TDESC:
                    *P23:8,*V2LON,IBEXETYP,*HOFF,*P31:8,TYPEDESC:
                    *P23:9,*V2LON,IBEXDEBT,*HOFF,*P31:9,DBDBNA1:
                    *P23:10,*V2LON,IBEXPTYP,*HOFF,*P31:10,PTYPDESC:
                    *P23:11,*V2LON,IBEXLEVL,*HOFF,*P31:11,LEVLDESC:
                    *P23:12,*V2LON,IBEXLVLD,*HOFF,*P31:12,LVLDDESC:
                    *P23:13,*V2LON,IBEXSRVL,*HOFF,*P31:13,SRVLDESC:
                    *P23:14,*V2LON,IBEXTENN
.
DISP9999  RETURN
+
.************************************************************************
.*  POST0000  :  post data                                              *
.************************************************************************
POST0000  DISPLAY   *P1:24,*EL,*P35:24,"Posting...";
.
          BRANCH    OPTION,POST1000,POST2000     * insert or change mode?
.
. Insert mode
.
POST1000  PACK      KEY4,IBEXEXTN          
          CALL      WRIBEXT1
          GOTO      POST9999
.
. change mode
.
POST2000  CALL      UPIBEXT1
.
POST9999  RETURN
+
.************************************************************************
.*  DELE0000  :  delete record                                          *
.************************************************************************
DELE0000  CALL      DELQST
          BRANCH    CEXIT,DELE1000,DELE8000,DELE9000
.
DELE1000  DISPLAY   *P1:24,*EL,*P35:24,"Deleting...";
.
          PACK      KEY4,IBEXEXTN
          CALL      DEIBEXT1
.
DELE8000  MOVE      ZERO,EXIT
          GOTO      DELE9999
.
DELE9000  MOVE      ONE,EXIT
.
DELE9999  RETURN
.
.
          INC       STDGENCD
          INC       IBACHGIO/INC
          INC       IBAEXTIO/INC
          INC       PATCODIO/INC
          INC       DEBDBTIO/INC
          INC       DEBCODIO/INC
          INC       PATCODKY
          INC       KEYINEXT
          INC       DEBDBTKY
          INC       DEBDKIKY
          INC       IBAEXTDS
.

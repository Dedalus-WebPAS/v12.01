.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX121899                                                  *
.* Desc      :   This program will write a new record to pmsaidaf for      *
.*               all patients that currently have a value in               *
.*               don't have one for st george and caritas                  *
.***************************************************************************
.* Date      :   16.11.2007                                                *
.* Author    :   Sandra Barcham                                            *
.*                                                                         *
.* Modifications :                                                         *
.***************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PMSAIDFD/INC
          INC       PATMA1FD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CHRCOUNT  FORM      2
CODE      DIM       2
DIM8      DIM       8
DIM20     DIM       20
JUSTNO    FORM      2
LJUSCODE  DIM       80
RJUSCODE  DIM       80
SAVDIM20  DIM       20
RECNUM    FORM      5
TYPE      DIM       3
TYPEDESC  DIM       25
WRITTEN   FORM      5
.
PRGID     INIT      "FX121899"
PRGNAM    INIT      "Add Alternate ID to STG and CCH"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
          GOTO      ML0100
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process pmsaidaf
          DISPLAY   *P1:24,*EL,"Written : ",WRITTEN
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PMSAIDA1,"pmsaidaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,RECNUM
          MOVE      ZERO,WRITTEN
          PACK      KEY8,SP70
          CALL      RDSMAST1
PROC1000  CALL      RDKMAST1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,RECNUM                        * update record counter
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      DIM8,PURNO,SP70
          SCAN      "G",DIM8
          GOTO      PROC1200 IF EQUAL
.
          SCAN      "C",DIM8
          GOTO      PROC1300 IF EQUAL
.
          GOTO      PROC1000
.
PROC1200  MOVE      "SGH",PMAIHOSP
          MOVE      "SG ",PMAITYPE
          GOTO      PROC1500
.
PROC1300  MOVE      "CCH",PMAIHOSP
          MOVE      "CC ",PMAITYPE
.
PROC1500  PACK      DIM20,PURNO,SP70
          SQUEEZE   DIM20
          PACK      LJUSCODE,DIM20,SP70
          MOVE      "20",JUSTNO
          CALL      RJUS0000
          PACK      PMAIALID,RJUSCODE,SP70
.
          MOVE      ZERO,PMAIDISU
          PACK      PMAIURNO,PURNO,SP70
          PACK      PMAISPAR,SP70
.
          PACK      KEY31,PMAIURNO,PMAITYPE,PMAIALID,SP70
          CALL      RAPMAID1
          IF        OVRCD=1
            CALL      WRPMAID1
            PRINT     PURNO
            ADD       ONE,WRITTEN
          ENDIF
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
.******************************************************************************
.*                                  RJUS0000              Called by: GDAT0000 *
.*                            Right Justify The Code                          *
.******************************************************************************
RJUS0000  PACK      RJUSCODE,SP70,SP70
          RESET     LJUSCODE,JUSTNO
          MOVE      ZERO,CHRCOUNT
RJUS1000  CMATCH    SP1,LJUSCODE
          GOTO      RJUS2000 IF NOT EQUAL
.
          ADD       ONE,CHRCOUNT
          BUMP      LJUSCODE,-1
          GOTO      RJUS2000 IF EOS
.
          COMPARE   JUSTNO,CHRCOUNT
          GOTO      RJUS1000 IF LESS
.
RJUS2000  RESET     LJUSCODE
          CLEAR     RJUSCODE
          RESET     RJUSCODE,CHRCOUNT
          APPEND    LJUSCODE,RJUSCODE
          RESET     RJUSCODE
.
RJUS9000  MOVE      ZERO,EXIT
RJUS9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATCODIO/INC
          INC       PMSAIDIO/INC
          INC       PATMA1IO/INC

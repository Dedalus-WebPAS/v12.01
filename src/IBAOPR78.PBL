.**********************************************************************
.* System   : Operating Room System                                   *
.* Program  : IBAOPR78                                                *
.* Desc     : Equipment Usage Report                                  *
.**********************************************************************
.* Modifications                                                      *
.**********************************************************************
.* V11.04.01 24/04/2024  Thanh T            TSK 0923560               *
.*           Changed PRNT0000 to add hosp id when calling RDOPTRA1    *
.* V11.03.01 23/01/2023  Thanh T.           TSK 0919770               *
.*           Changed PRNT0000 to cater for adding Hospital field      *
.*           changes                                                  *
.**********************************************************************
.* V10.04.01 09/02/2014 Ania P       CAR 267230                       *
.*           Added OPEQACTV                                           *
.*  V9.04.00 16/03/2005 Lina Vo      CAR 56404                        * 
.*           Created New.                                             * 
.*                                                                    * 
.**********************************************************************
.
.$$$$$
.    Operating Room System Equipment Usage Report(IBAOPR78 )
.    -------------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPREQPA4
.               OPRTRYA1
.               PATCODE1
.               CONTROLF
.
.    - Clear Input Variables
.
.    - Display Screen Format
.
.    - Input Range 
.         Enter expiry date range 
.         Enter number of uses remaining range
.
.    - Read through appropriate file & Print Report
.
.    - END
.$$$$$
.
.*****************************************************************************
.
          INC       STD001FD
          INC       OPREQPFD/INC           * Equipment File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       OPRTRAFD/INC           * Tray Header File
          INC       PATCODFD/INC           * patient codes file
.
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
EQUIPTYP  DIM       15
CURRSTAT  DIM       15
USUALLOC  DIM       17
CURRNLOC  DIM       17
.
EQUIPCNT  FORM      4
.
STRTEXDT  DIM       8                  * starting Expiry date range
FNSHEXDT  DIM       8                  * ending Expiry date range
DSPSTRTD  DIM       10                 * starting Expiry date range
DSPFNSHD  DIM       10                 * ending Expiry date range
.
STRTUSRM  FORM      5                  * starting number of uses remaining range
FNSHUSRM  FORM      5                  * ending number of uses remaining range
DSPSTRTU  DIM       6                 * start description
DSPFNSHU  DIM       6                 * end description
.
.
.----------------------------------< CONSTANTS >--------------------------------
.
PRGID     INIT      "IBAOPR78"
PRGNAM    INIT      "Equipment Usage Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000     CALL      INIT0000               * initialization and open files
.
ML1000     CALL      MENU0000               * select option         
           BRANCH    OPTION,ML2000
           GOTO      ML9999
.
ML2000     CALL      CLRV0000               * clear input variables
           CALL      EDAT0000
           BRANCH    EXIT,ML1000
           CALL      UREM0000               * select range
           CALL      CONTQST
           BRANCH    CEXIT,ML3000,ML2000,ML1000
.
ML3000     CALL      PRNT0000               * print tray item master listing
           GOTO      ML1000
.
ML9999     CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening oprtryaf";
          OPEN      OPRTRYA1,"oprtryaf"
.
          DISPLAY   *P64:24,"opreqpaf";
          OPEN      OPREQPA4,"opreqpaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          MOVE      ONE,CNEWDS         * set using new ? routine
          MOVE      ONE,CNOUNDLN
INIT9999  RETURN
+
.**********************************************************************
.*                             CLRV0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLRV0000  UNPACK    SP70,STRTEXDT,FNSHEXDT
          UNPACK    SP70,DSPSTRTD,DSPFNSHD
          MOVE      ZERO,STRTUSRM
          MOVE      ZERO,FNSHUSRM
          UNPACK    SP70,DSPSTRTU,DSPFNSHU
          MOVE      ZERO,EQUIPCNT
CLRV9999  RETURN
+
.*****************************************************************************
.                                 MENU0000
.         Menu driver routine
.*****************************************************************************
.
MENU0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = By Expiry Date Range."
.
MENU1000  KEYIN     *P1:7,*EL,"Select Option : _",*P17:7,*V2LON,OPTION
          COMPARE   ZERO,OPTION
          GOTO      MENU9999 IF EQUAL
          BRANCH    OPTION,MENU9000
          BEEP
          GOTO      MENU1000
MENU9000
MENU9999  RETURN
+
.*********************************************************************
.*                  EDAT0000                    Called by : ML0500   *
.*        Enter the Expiry date ranges                               *
.*********************************************************************
.
EDAT0000  UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
          MOVE      "10",CVERT                * row
          MOVE      "27",CCOL                 * col
          DISPLAY   *P1:9,*EF:
                    *P4:10,"Starting Expiry date : ":
                    *P4:11,"Ending   Expiry date : "
.
          CALL      KEYDATE
          BRANCH    OVRCD,EDAT9000          * nothing entered
          BRANCH    CQUEST,EDAT0000
.
          PACK      STRTEXDT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTEXDT           * set up start date
.
          PACK      DSPSTRTD,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",DSPSTRTD           * set up start date
.
.         enter ending date (default to starting date)
.
EDAT2000  MOVE      "11",CVERT               * row
          UNPACK    STRTEXDT,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,EDAT9000          * nothing entered
          BRANCH    CQUEST,EDAT2000
.
          PACK      FNSHEXDT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FNSHEXDT           * set up end date
.
          PACK      DSPFNSHD,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",DSPFNSHD           * set up end date
.
          MATCH     STRTEXDT,FNSHEXDT
          GOTO      EDAT8000 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER
          GOTO      EDAT2000
.
EDAT8000  MOVE      ZERO,EXIT               * valid dates
          GOTO      EDAT9999
.
EDAT9000  MOVE      ONE,EXIT                * exit selected
.
EDAT9999  RETURN
+
.*********************************************************************
.*                  UREM0000                    Called by : ML0000   *
.*        Enter the Number Uses Remaining range                      *
.*********************************************************************
.
UREM0000  MOVE      ZERO,STRTUSRM
          MOVE      ZERO,FNSHUSRM
          MOVE      SP70,DSPSTRTU
          MOVE      SP70,DSPFNSHU
          DISPLAY   *P1:12,*EF:
                    *P4:13,"Starting Number of Uses Remaining : ":
                    *P4:14,"Ending   Number of Uses Remaining : "
.
          KEYIN     *P41:13,*DE,STRTUSRM
          IF        STRTUSRM=0
            MOVE        "Start",DSPSTRTU
          ELSE
            MOVE        STRTUSRM,DSPSTRTU
          ENDIF
          DISPLAY     *P41:13,DSPSTRTU
.
          KEYIN     *P41:14,*DE,FNSHUSRM
          IF        FNSHUSRM=0
            MOVE        "99999",FNSHUSRM
          ENDIF
          IF          FNSHUSRM=99999
            MOVE        "Finish",DSPFNSHU
          ELSE
            MOVE        FNSHUSRM,DSPFNSHU
          ENDIF
          DISPLAY     *P41:14,DSPFNSHU
.
          COMPARE   FNSHUSRM,STRTUSRM
          GOTO      UREM9999 IF EQUAL   
          GOTO      UREM9999 IF LESS    
.
          DISPLAY   *P1:24,*B,"Starting Number must be less Ending Number.",*EL;
          CALL      HITENTER
          GOTO      UREM0000
.
UREM9999  RETURN
+
.*****************************************************************************
.                                 PRNT0000
.         Print Equipment Usage Report           
.*****************************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,"Equipment code : ":
          *P60:24,*V2LON,"*** Printing ***"
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000           * print heading
.
          PACK      KEY26,STRTEXDT,SP70
          CALL      RSOPEQP4           * read from starting tray code range
PRNT2000  CALL      RKOPEQP4
          BRANCH    OVRCD,PRNT9000
.
          MATCH     "1",OPEQACTV
          GOTO      PRNT2000 IF NOT EQUAL

          MATCH     OPEQEEDT,FNSHEXDT  * test if fptr has reached end range
          GOTO      PRNT9000 IF LESS
.
          MOVE      OPEQMNOU,F5
          COMPARE   STRTUSRM,F5
          GOTO      PRNT2000 IF LESS
          COMPARE   F5,FNSHUSRM
          GOTO      PRNT2000 IF LESS
.
          IF        CLNO>55                                     
            CALL      UND132
            CALL      HEAD0000
          ENDIF
.
          DISPLAY   *P13:24,OPEQCODE   * show operator were doing something
.
          ADD       ONE,EQUIPCNT
.
          PACK      KEY5,ANSO,ANSE,OPEQTYPE
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,EQUIPTYP
          ELSE
            PACK      EQUIPTYP,OPEQTYPE,SP70
          ENDIF
.
          PACK      KEY5,ANSE,ANSR,OPEQSTAT
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,CURRSTAT
          ELSE
            PACK      CURRSTAT,OPEQSTAT,SP70
          ENDIF     
.
          PACK      KEY18,OPEQUSLO,OPEQHOSP,SP70
          CALL      RDOPTRA1           * get tray description
          IF        OVRCD=0
            MOVE      OPTHDESC,USUALLOC
          ELSE
            PACK      USUALLOC,OPEQUSLO,SP70
          ENDIF
.
          PACK      KEY18,OPEQCULO,OPEQHOSP,SP70
          CALL      RDOPTRA1           * get tray description
          IF        OVRCD=0
            MOVE      OPTHDESC,CURRNLOC
          ELSE
            PACK      CURRNLOC,OPEQCULO,SP70
          ENDIF
.
          MOVE      OPEQDESC,KEY24
          UNPACK    OPEQEEDT,CCENT,CYEAR,CMON,CDAY
          PACK      KEY10,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR 
.
          PRINT     *1,"|",OPEQCODE:
                    *17,"|",KEY24:
                    *42,"|",KEY10:
                    *53,"|  ",OPEQMNOU:
                    *64,"|",EQUIPTYP:
                    *80,"|",CURRSTAT:
                    *96,"|",USUALLOC:
                    *114,"|",CURRNLOC:
                    *132,"|"
          ADD       ONE,CLNO
          GOTO      PRNT2000
.
PRNT9000  CALL      UND132
          PRINT     *N,"Total Equipment : ",EQUIPCNT:
                    *N,*N,"***** End of Report *****"
.
PRNT9999  RETURN
+
.****************************************************************************** 
.                                 HEAD0000
.****************************************************************************** 
HEAD0000  CLOCK     TIME,CTIMEIS       * get current system time
          CALL      HEAD132
.
          PRINT     *N,*35,"Expiry Date Range             : ":
                        DSPSTRTD," To ",DSPFNSHD:
                    *N,*35,"Number of Use Remaining Range :   ":
                        DSPSTRTU," To ",DSPFNSHU:
                    *N
.
          CALL      UND132
          PRINT     *1,"|Code":
                    *17,"|Description":
                    *42,"|Expiry":
                    *53,"|No of Uses":
                    *64,"|Equipment":
                    *80,"|Current":
                    *96,"|Usual":
                    *114,"|Current":
                    *132,"|"
.
          PRINT     *1,"|":
                    *17,"|":
                    *42,"|Date":
                    *53,"|Remaining":
                    *64,"|Type":
                    *80,"|Status":
                    *96,"|Location":
                    *114,"|Location":
                    *132,"|"
          CALL      UND132
.
          ADD       SIX,CLNO         
.
HEAD9999  RETURN
.
.******************************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
          INC       STD001IO
          INC       PATCODIO/INC            * patient i/o for codes
          INC       OPRTRAIO/INC            * tray header i/o   
          INC       OPREQPIO/INC            * Equipment    
.**********************************************************************

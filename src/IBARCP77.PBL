.******************************************************************************
.* System    :   Common                                                       *
.* Program   :   IBARCP77                                                     *
.* Desc      :   Health Fund Remittance upload to Central Receipting          *
.******************************************************************************
.* Date      :   11/12/2007                                                   *
.* Author    :   Mike Laming                                                  *
.* Mods      :                                                                *
.*                                                                            *
.******************************************************************************
.* V11.04.03 04/11/2024 Thanh T          TSK 0950887                          *
.*           Removed Date of Payment and Payment Amount parameters for        *
.*           option 5 and 6	  
.* V11.04.02 24/10/2024 Thanh T           TSK 0946085                         *
.*           Changed VFIL0000 to validate the statement reference field       *
.* V11.04.01 26/02/2024 J.Tan             TSK 0943491                         *
.*           Added right justified to Hospital Approval Number                *
.******************************************************************************
.*        V11.03.02 23/01/2023 J.Tan             TSK 0920195                  *
.*                  Mod to excute us1 for Unix and validating Payment name    *
.*        V11.03.01 23/12/2022 J.Tan             TSK 0920195                  *
.*                  Added NZ Private Bulk receipt upload                      *
.*        V10.14.04 19/07/2019  Peter Vela       TSK 0857392                  *
.*                  Added ABF error report                                    *
.*        V10.14.03 03/07/2019  Peter Vela       TSK 0857392                  *
.*                  Added All Claim Types functionality                       * 
.*        V10.14.02 14/06/2019  J.Tan            TSK 0876543                  *
.*                  Fixed RCP Transaction count of tempfile                   *
.*        V10.14.01 20/03/2019  J.Tan            TSK 0857392                  *
.*                  Changed RCP Transaction count from DIM3 to DIM5           *
.*        V10.13.01 05/12/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (PRTFILE1) on program exit              *
.*        V10.11.03 21/08/2017  J.Tan             TSK 0843472                 *
.*                  Removed error message matching PRFA name                  *
.*        V10.11.02 09/08/2017  J.Tan             TSK 0843472                 *
.*                  Fixed error message matching PRFA name                    *
.*        V10.11.01 03/08/2017  J.Tan             TSK 0842930                 *
.*                  Mod payment amount to F12.2                               *
.*        V10.10.03 07/06/2017  J.Tan             TSK 0838953                 *
.*                  Fixed checking Error flag - ERRIN                         *
.*        V10.10.02 01/05/2017  J.Tan             TSK 0838953                 *
.*                  Fixed checking Error flag - ERRIN                         *
.*        V10.10.01 28/03/2017  J.Tan             TSK 0822482                 *
.*                  Mod to check for inactive register                        *
.*        V10.08.02 19/10/2016  J.Tan             TSK 0822482                 *
.*                  Mods to check for Fully Credited invoices                 *
.*        V10.08.01 30/08/2016  J.Tan         TSK 0819992                     *
.*                  Mods RCDTALLO to 2 instead of 0 for Suspense register     *
.*        V10.07.07 24/11/2016  J.Tan          CAR 321534                     *
.*                  Fixed for single hospital                                 *
.*        V10.07.06 29/01/2016  J.Tan         CAR 303942                      *
.*                  Mods for Payment types                                    *
.*        V10.07.05 16/12/2015  J.Tan    CAR 303942                           *
.*                  Mods for new payment types                                *
.*        V10.07.04 24/11/2016  J.Tan          CAR 321534                     *
.*                  Added Browser option to Non Health Fund receipts upload   *
.*        V10.07.03 24/11/2016  J.Tan          CAR 321534                     *
.*                  Added Browser option to Non Health Fund receipts upload   *
.*        V10.07.02 12/11/2016  J.Tan          CAR 321534                     *
.*                  Added option Non Health Fund receipts upload              *
.*        V10.07.01 12/08/2016  J.Tan          CAR 314900                     *
.*                  Added HSYS1=5 for Extended Integra interface              *
.*        V10.06.01 04/09/2015  J.Tan          CAR 320687                     *
.*                  Fixed up record length for TMPDEF02 (TMPBDTA1)            *
.*        V10.04.02 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.04.01 07/02/2014  Steve Armstrong  CAR 288084                   *
.*                  Recompiled for changes to TMPDTEFD and extended INPAYNAM  *
.*                  to DIM 50.                                                *
.******************************************************************************
.*        V10.01.01 13/01/2011  Mike Laming    CAR 233084                     *
.*                  Changed to use new include CLPATINV (Added PTINCNID)      *
.******************************************************************************
.*        V10.00.01 11/05/2010  J.Tan             CAR 216328                  *
.*                  New parameter HSYS1=3 for G/L Integra cash interface      *
.******************************************************************************
.*        V9.12.02  21/07/2009  Mike Laming  CAR 187485                       *
.*                  Change size of RCBDPAYD, RCBDDRAW, RCBDSTRF               *
.*        V9.12.01  20/07/2009  Mike Laming  CAR 187485                       *
.*                  Change size of RCBDPAYD & RCBDDRAW - Remove RCBDMONM      *
.*        V9.09.00  15/01/2008 Mike Laming   CAR 150425                       *
.*                  New Program plus alll the initial mods                    *
.*        V9.09.00  15/01/2008 Mike Laming   CAR 150425                       *
.*                  New Program plus alll the initial mods                    *
.******************************************************************************
+
          INC       STD001FD
          INC       STDHLPDF
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       FMSCHQFD/INC            * FMS Cheque Acct Master File
          INC       FMSCSAFD/INC            * FMS Selected Control Accounts
          INC       FMSLMAFD/INC            * FMS Ledger Master Details
          INC       PATCONFD/INC            * Patient Control file      
          INC       PATCODFD/INC            * Cat.Codes file
          INC       PATFN1FD/INC            * Health Fund Table
          INC       PATMA1FD/INC            * PMI Master
          INC       PATINVFD/INC            * Patient Invoice file
          INC       PATHOSFD/INC            * Hospital Details 
          INC       PATHSPFD/INC            * Hospital Details 
          INC       PATIN1FD/INC            * Insurance file
          INC       PATVISFD/INC            * Patient Visit Details
          INC       PMSVX1FD/INC            * Patient Visit Extension
          INC       PATMI1FD/INC            * Patient Admission Details
          INC       PATRE1FD/INC            * Patient Responsible Person
          INC       RCPCIDFD/INC            * Cashier ID file
          INC       RCPBNKFD/INC            * Receipting Bank Header file
          INC       RCPBDTFD/INC            * Receipting Bank Detail file
          INC       RCPDTEFD/INC            * Receipting Receipt Details file
          INC       RCPREGFD/INC            * Receipting Register Details     
          INC       TMPBNKFD/INC            * RCP Temp Bank Header file
          INC       TMPBDTFD/INC            * RCP Temp Bank Detail file
          INC       TMPDTEFD/INC            * RCP Temp Receipt Details file
          INC       WEBSECFD/INC            * Web Security / Web Users
.
          INC       AAEDE1FD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTSITFD/INC
.
. ----- Tempory Print file definition -----
.
PRTFILE1  FILE      ASCII, FIXED=256
.
. ----- Input Text File Definitions -----
.
INTEST01  FILE      ASCII, FIXED=2000       * used to test if file exists
INHFRTXT  FILE      HL7, FIXED=2000         * input file for Health Fund Remitt.
.
BROWSE    INIT      "Browse"
UNIX      INIT      "Unix"
HFRRECID  DIM       2
FLD10001  DIM       10
FLD10002  DIM       10
FLD10003  DIM       10
FLD10004  DIM       10
FLD10005  DIM       10
FLD60002  DIM       60
FLD60003  DIM       60
FLD60004  DIM       60
FLD60005  DIM       60
FLD60006  DIM       60
FLD60007  DIM       60
FLD60008  DIM       60
FLD60009  DIM       60
FLD60010  DIM       60
FLD60011  DIM       60
FLD60012  DIM       60
FLD60013  DIM       60
FLD60014  DIM       60
FLD60015  DIM       60
FLD60016  DIM       60
FLD60017  DIM       60
FLD60018  DIM       60
FLD60019  DIM       60
FLD60020  DIM       60
FLD60021  DIM       60
.
. ----- Program Variables -----
.
ALLINDIC  DIM       1
KIAMOUNT  FORM      12.2
KIPAYDAT  DIM       8
KIPAYTYP  DIM       2
KIPAYTPE  FORM      2
KISTTREF  DIM       20
KIHOSPNO  DIM       10
KICASHID  DIM       6
KICHQACC  DIM       15
KIHOSPID  DIM       3
KIHFUND   DIM       6
KCLMCODE  DIM       3
INSRCODE  DIM       6
INSCOMP   DIM       6
INSNAME   DIM       30
KIREGCOD  DIM       3
KICHQNUM  DIM       12
KIBNKNAM  DIM       30
KIBNKBCH  DIM       30
KIWEBUID  DIM       10
SUSPEND   FORM      1
SAVERECN  DIM       15
TMRCPTNO  FORM      12
TRANNUMB  FORM      5
INVPAID   FORM      12.2
TRANTAMT  FORM      12.2
IBALANCE  FORM      12.2
.
CDATEIS   DIM       8
CMDLINE   DIM       100
CODE      DIM       2
COLTXT    DIM       3
ECOL1     FORM      2
ERRDATA   DIM       40
ERRMESS   DIM       75
MAXRCPNO  FORM      12
FORM12    FORM      12
F12P2     FORM      12.2
HRECPT    FORM      12
HSYS1     FORM      1
IBCNMHOS  FORM      1
INVCOUNT  FORM      5
RCCNURCP  DIM       3
RECIN     FORM      5
HEDIN     FORM      5
REC00     FORM      5
REC03     FORM      5
PMIOK     FORM      1
RESPOK    FORM      1
ERRIN     FORM      5
PRTLINE   DIM       200
PRTLIN1   DIM       200
PATHNAME  DIM       500
PAYMCODE  DIM       3
.
.
DIM1A     DIM       1
DIM1B     DIM       1
DIM2A     DIM       2
DIM2B     DIM       2
DIM3A     DIM       3
DIM3B     DIM       3
DIM5A     DIM       5
DIM5B     DIM       5
DIM6A     DIM       6
DIM6B     DIM       6
DIM7A     DIM       7
DIM8A     DIM       8
DIM10A    DIM       10
DIM10B    DIM       10
DIM11A    DIM       11
DIM12A    DIM       12
DIM12B    DIM       12
DIM15A    DIM       15
DIM20A    DIM       20
DIM20B    DIM       20
DIM30A    DIM       30
DIM30B    DIM       30
DIM70     DIM       70
DIM120    DIM       120
DONE      FORM      1
HFRIDNO   FORM      2
.INHOSPNO  DIM       7                       * record type "01"
INHOSPNO  DIM       10                      * record type "01"
INHOSNAM  DIM       40
INHOSAD1  DIM       40
INHOSAD2  DIM       40
INHOSAD3  DIM       40
INHOSAD4  DIM       40
INHFDATE  DIM       9 
INHFCODE  DIM       4
INHFAMNT  FORM      12.2
.INHOSNO2  DIM       7                       * record type "02"
INHOSNO2  DIM       10                      * record type "02"
INHOSREF  DIM       9
INPAYNAM  DIM       50
ININVNUM  DIM       8
INAMT07   FORM      12.2
INFLD05   DIM       10
INFLD09   DIM       10
INFLD10   DIM       10
INFLD11   DIM       10
INFLD13   DIM       10
INFLD14   DIM       10
INFLD15   DIM       10
INFLD16   DIM       10
.
TTHFAMNT  FORM      12.2
FILETYPE  FORM      1
FLDNO     DIM       2   
FNAMESHT  DIM       20
FNAMEIN   DIM       150
FNAMEHFI  DIM       60
FLD60IN   DIM       60
FLD60OUT  DIM       60
FNO       FORM      1
F12       FORM      12
HEADDES1  DIM       35
HEADDES2  DIM       60
LENGTH    FORM      3 
LENIN     FORM      3 
LENOUT    FORM      3 
HEADNO    FORM      1 
SAVSDESC  DIM       35
SAVSINCA  DIM       14
SAVSBNKA  DIM       14
SAVTINCA  DIM       14
SAVTBNKA  DIM       14
SKEY30    DIM       30
TMPFIL01  DIM       8                            * "t77bnk"+PORT
TMPFIL02  DIM       8                            * "t77bdt"+PORT
TMPFIL03  DIM       8                            * "t77dte"+PORT
TMPPRTFL  DIM       8
.
. ----- Program Constants -----
.
RET       INIT      015                          * carriage return
ZERO4     INIT      "0000"
TMPDEF01  INIT      " 196 U1-12"                 * TMPFIL01
TMPDEF02  INIT      " 336 U9-20,21-23"           * TMPFIL02
TMPDEF03  INIT      " 467 U9-20,21-25"           * TMPFIL03
.
PRGNAM    INIT      "Upload Remittance File"
PRGID     INIT      "IBARCP77"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
          BRANCH    EXIT,ML9000
.
ML1000    CALL      OPTN0000                * Select Option          
          BRANCH    EXIT,ML9000
.
          CALL      CTMP0000                * Create Temp files for Receipting
          BRANCH    EXIT,ML1000
.
ML3000    CALL      OPEN0000                * Open files
          BRANCH    EXIT,ML1000
.
ML5000    CALL      GREG0000                * Retrieve & edit Register info
.
          BRANCH    EXIT,ML7200
.
          BRANCH    OPTION,ML7000,ML7000,ML7000,ML7000
.
. Option 5 and 6
.
          CALL      VFIL0000                * Validate Upload file
          COMPARE   ZERO,ERRIN
          GOTO      ML7200 IF NOT EQUAL
.
          CALL      SINP0000                * Process the input file NZ Private
          IF        OPTION = 6
            CALL      SPRC0000              * Process Temp files to RCP files
            CALL      EXCUA000              * Execute us2 scripts
          ENDIF
.
          GOTO      ML7200
.
. Option 1 to 4
.
ML7000    CALL      PINP0000                * Process the input file
          COMPARE   ZERO,ERRIN
          GOTO      ML7200 IF NOT EQUAL
.
          IF        OPTION = 2 | OPTION=4
            CALL      PROC0000              * Process Temp files to RCP files
          ENDIF
.
          CALL      ABFREP00
.
ML7200    IF        OPTION = 1 | OPTION=3 | OPTION=5
            PRINT     *N,*1,"Edit only processing complete"
          ENDIF
          PRINT     *N,*1,"*** End of Report ***"
          GOTO      ML1000
.
ML9000    MOVE      ZERO,EXIT
          CALL      KILL0000                * delete temp files
          MATCH     SP8,PGM
          GOTO      ML9999 IF EQUAL
.
          CHAIN     PGM                     * Chain back to appropriate menu
ML9999    SHUTDOWN  ""
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          MOVE      "99",CLNO               * force Report Header if errors
          MOVE      SP3,FLDNO 
          MOVE      SP3,COLTXT
          PACK      FNAMEIN,SP70,SP70,SP70
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"     
.
          DISPLAY   *P64:24,"rcpcidaf";
          OPEN      RCPCIDA1,"rcpcidaf"     
.
          DISPLAY   *P64:24,"rcpregaf";
          OPEN      RCPREGA1,"rcpregaf"     
          OPEN      RCPREGA3,"rcpregaf"     
.
          DISPLAY   *P64:24,"fmschqaf";
          OPEN      FMSCHQA1,"fmschqaf"     
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"     
.
          DISPLAY   *P64:24,"pathospf";
          OPEN      PATHOSP1,"pathospf"     
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"     
.
          DISPLAY   *P64:24,"patin1af";
          OPEN      PATIN1A1,"patin1af"     
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"     
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*79,CAPPRVNO
          READ      CONTROLF,TEN;*244,CHOSPNUM
          READ      CONTROLF,THIRTY6;*86,HSYS1
          READ      CONTROLF,HUND08;*109,RCCNURCP
          READ      CONTROLF,HUND28;*105,PTCNUABF
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TMPFIL01
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TMPFIL02
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TMPFIL03
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TMPPRTFL
.
          CALL      IBACLOCK
          PACK      CDATEIS,CCC,CYY,CMM,CDD
          REP       " 0",CDATEIS
.
INIT9000  MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*                         Select option to run upload                        *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,FNO         
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,FILETYPE
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Edit Health Fund Remittance":
                          " only":
                    *P1:6,*V2LON," 2",*HOFF," = Upload Health Fund Remittance":
                  *P1:7,*V2LON," 3",*HOFF," = Validate Non Health Fund Receipts":
                  *P1:8,*V2LON," 4",*HOFF," = Upload Non Health Fund Receipts":
                  *P1:9,*V2LON," 5",*HOFF," = Validate NZ Private Bulk Receipts":
                  *P1:10,*V2LON," 6",*HOFF," = Upload NZ Private Bulk Receipts":
                    *P1:12,"Select Option : ";
.
OPTN1000  KEYIN     *P17:12,*V2LON,*DV,UNDLN2,*P17:12,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN1500,OPTN1500,OPTN1500,OPTN1500:
                           OPTN1500,OPTN1500
          GOTO      OPTN1000
.
OPTN1500  MOVE      "99",CLNO
          CALL      HEAD0000
.
          BRANCH    OPTION,OPTN1700,OPTN1700,OPTN1700,OPTN1700
.
. Option 5 and 6 (NZ Private) 
. Display Hospital and Path/File name
.
          UNPACK    SP30,KIHOSPID,KIHOSPNO
          IF        IBCNMHOS = 1
            DISPLAY   *P4:15,"Hospital ID.              :";
            MOVE      "15",CVERT
            MOVE      "32",CCOL
            CALL      KYHOSPID                * keyin Hospital Id.
            BRANCH    EXIT,OPTN0000
            PACK      KIHOSPID,PTHSHOSP,SP10  * Hospital Id.
            SQUEEZE   PTHSAPPR
            PACK      KIHOSPNO,PTHSAPPR,SP10  * Hospital Number
.
            MOVE      KIHOSPNO,FLD60IN        * Hospital No.
            MOVE      "10",LENGTH             * retrieve No. and right justify
            CALL      RJUS0000                * right justify
            MOVE      FLD60OUT,KIHOSPNO
          ENDIF
.
          DISPLAY   *P1:3,*EF;
.
.         FILETYPE:  1=Browse, 2=Unix
.
          DISPLAY   *P4:3,"Upload File Type          :";
.
          DISPLAY   *P4:6,"Cashier Drawer            :":
                    *P4:7,"Cheque Account            :";
.
.          DISPLAY   *P4:8,"Date of Payment           :":
.                    *P4:9,"Payment Type              :":
.                    *P4:10,"Payment Amount            :":
.                    *P4:11,"Statement Reference       :":     * Pay type = 4
.                    *P55:11,"(for Direct Deposit)":
          DISPLAY   *P4:9,"Payment Type              :": 
                    *P4:12,"Cheque No.                :":     * Pay type = 2
                    *P55:12,"(for Cheque payment)":
                    *P4:13,"Bank                      :":
                    *P4:14,"Branch                    :":
                    *P4:15,"Register                  :";
.
          DISPLAY   *P4:18,"User Id                   :";
.
          DISPLAY   *P4:19,"Hospital ID.              :":
                    *P32:19,*V2LON,KEY3,SP4,PTHSNAME
.
          IF        OPTION=5
             PRINT     *30,"Upload Bulk Receipts - Validate only",*N
          ELSE
             PRINT     *30,"Upload Bulk Receipts",*N
          ENDIF
.
          PRINT     *1,"Hospital ID.              : ",KIHOSPID,SP10:
                    SP3,PTHSNAME
.
          IF        OPTION=5 | OPTION=6
            DISPLAY   *P1:24,*EL," 1. Browse, 2. Unix ";
            KEYIN     *P32:3,FILETYPE;
            MOVE      SP70,KEY20
            LOAD      KEY20,FILETYPE,BROWSE,UNIX
            DISPLAY   *P32:3,KEY20;
.
            BRANCH    FILETYPE,OPTN1620,OPTN1620
            GOTO      OPTN1000
          ENDIF
.
OPTN1620  DISPLAY   *P1:24,*EL;
.
          IF        FILETYPE=1
            MOVE      "4",CVERT
            DISPLAY   *P4:CVERT," Upload File - ";
            CALL      KASC0000                * keyin file
            BRANCH    EXIT,OPTN0000
            MOVE      "5",CVERT
            CALL      KPTH0000                * Keyin pathname
          ELSE
            MOVE      "5",CVERT
            CALL      KASCI000                * keyin file name
            BRANCH    EXIT,OPTN0000
          ENDIF
.
          MOVE      "6",CVERT
          GOTO      OPTN1900
.
. Option 1 to 4
.
OPTN1700  DISPLAY   *P1:3,*EF;
.
          DISPLAY   *P4:5,"Cashier Drawer            :":
                    *P4:6,"Cheque Account            :";
.
          IF        OPTION=3 | OPTION=4
            DISPLAY   *P4:7,"Claim Type Received from  :";
          ELSE
            DISPLAY   *P4:7,"Health Fund Received from :";
          ENDIF
.
          DISPLAY   *P4:8,"Date of Payment           :":
                    *P4:9,"Payment Type              :":
                    *P4:10,"Payment Amount            :":
                    *P4:11,"Statement Reference       :":     * Pay type = 4
                    *P55:11,"(for Direct Deposit)":
                    *P4:12,"Cheque No.                :":     * Pay type = 2
                    *P55:12,"(for Cheque payment)":
                    *P4:13,"Bank                      :":
                    *P4:14,"Branch                    :":
                    *P4:15,"Register                  :";
.
          IF        OPTION=3 | OPTION=4
            DISPLAY   *P4:16,"Upload File                  ":
                      *P4:17,"Path name    - ",*V2LON,UNDLN30,UNDLN30,*HOFF;
          ELSE
            DISPLAY   *P4:16,"Enter Path and File Name for ":
                      *P4:17,"Remitt. File - ",*V2LON,UNDLN30,UNDLN30,*HOFF;
          ENDIF
.
          DISPLAY   *P4:18,"User Id                   :";
.
          IF        OPTION = 2
            PRINT     *30,"Upload Health Fund Remittance File",*N
          ELSE
            IF        OPTION = 1
              PRINT     *30,"Health Fund Remittance File - Edit only",*N
            ELSE
              IF        OPTION = 3
                PRINT     *30,"Non Health Fund Receipts - Validate only",*N
              ELSE
                PRINT     *30,"Upload Non Health Fund Receipts",*N
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      "5",CVERT
OPTN1900  MOVE      "32",CCOL
          CALL      KEYCSHID                * keyin Cashier Drawer
.         BRANCH    EXIT,OPTN0000
.
          MOVE      RCPCASH,KICASHID        * Cashier Id.
          PRINT     *1,"Cashier Drawer            : ",KICASHID,SP10,RCPSUR
.
          MOVE      "6",CVERT
          IF        OPTION=5 | OPTION=6
            MOVE      "7",CVERT
          ENDIF
          MOVE      "32",CCOL
          CALL      KCHQFM00                * keyin Cheque Account
          COMPARE   ZERO,EXIT
          GOTO      OPTN0000 IF NOT EQUAL
          DISPLAY   *P45:CVERT,*V2LON,FMCHDES,*HOFF;
          MOVE      FMCHCHQ,KICHQACC        * Cheque Account No.
          PRINT     *1,"Cheque Account            : ",KICHQACC,SP1,FMCHDES
.
          UNPACK    SP70,INSCOMP,INSNAME
          BRANCH    OPTION,OPTN3620,OPTN3620,OPTN1980,OPTN1980:
                           OPTN4000,OPTN4000
.
. Option 3 and 4
.
OPTN1980  MOVE      SP70,KCLMCODE
          MOVE      "7",EVERT
          MOVE      "32",ECOL
          MOVE      SP3,ACODE
          MOVE      "CL",CODE
          MOVE      SP70,ALLINDIC
.
          CALL      KEYINCOD
.         BRANCH    EXIT,OPTN0000:
.                        OPTN9500           * Exitchar entered
.
          IF        EXIT=ONE
            MOVE      ONE,ALLINDIC
            GOTO      OPTN2000
          ENDIF
.
          IF        EXIT=TWO
            GOTO      OPTN9500
          ENDIF
.
          MATCH     SP70,PTCDASC5
          GOTO      OPTN0000 IF EQUAL       * Invalid claim code
.
          MOVE      ACODE,KCLMCODE
          MOVE      PTCDASC5,INSCOMP        * Insurance company
          MOVE      INSCOMP,KEY6
          CALL      RDINSR1
          BRANCH    OVRCD,OPTN0000          * Invalid Insurance company
          MOVE      INAME,INSNAME
.
          PRINT     *1,"Claim Type Received from  : ",KCLMCODE,SP10,TDESC
          GOTO      OPTN3800
.
OPTN2000  PRINT     *1,"Claim Type Received from  : ALL"
          GOTO      OPTN3800
.
. Option 1 and 2 
.
OPTN3620  MOVE      "7",CVERT
          MOVE      "32",CCOL
          CALL      KYHFUN00                * keyin Health Fund    
          COMPARE   ZERO,EXIT
          GOTO      OPTN0000 IF NOT EQUAL
          MOVE      HCODE,KIHFUND           * Health Fund code
          PRINT     *1,"Health Fund Received from : ",KIHFUND,SP10,HFNAME
          GOTO      OPTN3800
.
. Option 1 to 4
.
OPTN3800  MOVE      "8",CVERT
          MOVE      "32",CCOL
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE                 * keyin Date of Payment
          PACK      KIPAYDAT,CCENT,CYEAR,CMON,CDAY
          PRINT     *1,"Date of Payment           : ",CDAY,"/",CMON,"/":
                    CCENT,CYEAR
.
. Option 1 to 6
.
OPTN4000  MOVE      "9",EVERT
          MOVE      "32",ECOL               * keyin position
          MOVE      "40",ECOL1              * Display description position
          MOVE      SP3,ACODE
          MOVE      "Py",CODE
          MOVE      SP70,PAYMCODE
          CALL      KEYINCOD                * keyin code
          BRANCH    EXIT,OPTN0000:
                         OPTN9500           * Exitchar entered
          MOVE      ACODE,PAYMCODE
.
          SQUEEZE   PTCDNHCP
          MOVE      PTCDNHCP,KIPAYTPE
          MOVE      KIPAYTPE,KIPAYTYP
.
          DISPLAY   *P32:EVERT,*V2LON,PAYMCODE,SP6,TDESC;

          PRINT     *1,"Payment Code              : ",PAYMCODE,SP15,TDESC
.
          IF        OPTION=5 | OPTION=6
            GOTO      OPTN4200
          ENDIF
.
. Option 1 to 4
.
          MOVE      "10",CVERT
          KEYIN     *P32:CVERT,F12P2;
          MOVE      F12P2,KIAMOUNT
          DISPLAY   *P32:CVERT,*V2LON,KIAMOUNT,*HOFF;
          PRINT     *1,"Payment Amount            : ",KIAMOUNT
.
OPTN4200  MOVE      SP20,KISTTREF
          MOVE      SP20,KICHQNUM
          MOVE      SP30,KIBNKNAM
          MOVE      SP30,KIBNKBCH
.
OPTN4400  MOVE      SP20,DIM20A             * keyin Statement Reference
.
          IF        OPTION=5 | OPTION=6
            GOTO      OPTN4500
          ENDIF
.
. Option 1 to 4
.
          MOVE      "11",CVERT
          KEYIN     *P32:CVERT,DIM20A;
          DISPLAY   *P32:CVERT,*V2LON,DIM20A,*HOFF;
          IF        KIPAYTPE = 4 | KIPAYTPE = 7 | KIPAYTPE = 8
            MOVE      DIM20A,KISTTREF
            PRINT     *1,"Statement Reference       : ",KISTTREF
          ENDIF
.
. Option 1 to 6
.
OPTN4500  MOVE      SP20,DIM12A             * keyin Cheque Number         
          MOVE      "12",CVERT
          KEYIN     *P32:CVERT,DIM12A;
          DISPLAY   *P32:CVERT,*V2LON,DIM12A,*HOFF;
          IF        KIPAYTPE = 2
            MOVE      DIM12A,KICHQNUM
            PRINT     *1,"Cheque Number             : ",KICHQNUM
          ENDIF
.
OPTN4510  MOVE      "13",CVERT              * keyin Bank Name
          MOVE      SP30,DIM30A
          KEYIN     *P32:CVERT,DIM30A;
          DISPLAY   *P32:CVERT,*V2LON,DIM30A,*HOFF;
          IF        KIPAYTPE = 2
            MOVE      DIM30A,KIBNKNAM
            PRINT     *1,"Bank                      : ",KIBNKNAM
          ENDIF
.
OPTN4520  MOVE      "14",CVERT              * keyin Bank Branch
          MOVE      SP30,DIM30A 
          KEYIN     *P32:CVERT,DIM30A;
          DISPLAY   *P32:CVERT,*V2LON,DIM30A,*HOFF;
          IF        KIPAYTPE = 2
            MOVE      DIM30A,KIBNKBCH
            PRINT     *1,"Bank Branch               : ",KIBNKBCH
          ENDIF
.
OPTN4600  MOVE      "15",CVERT              * keyin Register
          CALL      KEYCDEA
          BRANCH    EXIT,OPTN0000
          DISPLAY   *P32:15,*V2LON,REGCODE,SP4,REGDESC;
          MOVE      REGCODE,KIREGCOD
          PRINT     *1,"Register                  : ",KIREGCOD,SP10,SP3,REGDESC
.
          BRANCH    OPTION,OPTN4800,OPTN4800,OPTN4800,OPTN4800
.
. Option 5 and 6
.
          MOVE      "18",CVERT              * keyin web user Id.
          MOVE      SP10,DIM10A
          KEYIN     *P32:CVERT,DIM10A;
          MOVE      DIM10A,KIWEBUID
          DISPLAY   *P32:CVERT,*V2LON,KIWEBUID,*HOFF;
          PRINT     *1,"Web User Id.              : ",KIWEBUID
.
          GOTO      OPTN6000
.
. Option 1 to 4
.
OPTN4800  IF        OPTION=3 | OPTION=4
            MOVE      "16",CVERT
            DISPLAY   *P4:CVERT," Upload File - ";
            CALL      KASC0000                * keyin file
            BRANCH    EXIT,OPTN0000
            MOVE      "17",CVERT
            CALL      KPTH0000              * Keyin pathname
          ELSE
            MOVE      "17",CVERT
            DISPLAY   *P4:CVERT,"Remitt. File - ";
            CALL      KASCI000                * keyin file name
            BRANCH    EXIT,OPTN0000
          ENDIF
.
OPTN4900  MOVE      FNAMEIN,FNAMEHFI
          IF        OPTION=3 | OPTION=4
            PRINT     *1,"Upload  File              : ",FNAMEHFI
          ELSE
            PRINT     *1,"Remitt. File              : ",FNAMEHFI
          ENDIF
.
OPTN5000  MOVE      "18",CVERT              * keyin web user Id.
          MOVE      SP10,DIM10A
          KEYIN     *P32:CVERT,DIM10A;
          MOVE      DIM10A,KIWEBUID
          DISPLAY   *P32:CVERT,*V2LON,KIWEBUID,*HOFF;
          PRINT     *1,"Web User Id.              : ",KIWEBUID
.
          UNPACK    SP30,KIHOSPID,KIHOSPNO
          IF        IBCNMHOS = 1
            DISPLAY   *P4:19,"Hospital ID.              :";
            MOVE      "19",CVERT
            MOVE      "32",CCOL
            CALL      KYHOSPID                * keyin Hospital Id.
            BRANCH    EXIT,OPTN0000
            PACK      KIHOSPID,PTHSHOSP,SP10  * Hospital Id.
            SQUEEZE   PTHSAPPR
            PACK      KIHOSPNO,PTHSAPPR,SP10  * Hospital Number
            PRINT     *1,"Hospital ID.              : ",KIHOSPID,SP10:
                      SP3,PTHSNAME
          ENDIF
.
. Option 1 to 6
.
OPTN6000  CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,OPTN9000,OPTN0000,OPTN9500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:CVERT,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                 KYHOSPID                                   *
.*                            Key in Hospital Id.                             *
.******************************************************************************
KYHOSPID  MOVE      ZERO,EXIT
.         UNPACK    SP30,KIHOSPID,KIHOSPNO
KYHOS100  MOVE      SP6,DIM3A 
          KEYIN     *PCCOL:CVERT,*V2LON,DIM3A;
.
          PACK      KEY3,DIM3A,SP10
          MATCH     SP3,KEY3
          GOTO      KYHOS950 IF EQUAL
.
          MATCH     QUEST,KEY3
          IF        @EQUAL
            CALL      GETSCR00
            DISPLAY   *P1:4,*EF,*P35:4,"Hospital Details";
            MOVE      "6",EVERT
            PACK      KEY3,SP70
            CALL      RSPTHSP1
KYHOS210    CALL      RKPTHSP1
            BRANCH    OVRCD,KYHOS290
            DISPLAY   *P1:EVERT,PTHSHOSP,SP4,PTHSNAME;
            ADD       ONE,EVERT
            IF        ECOL < 22
              GOTO      KYHOS210
            ENDIF
.
KYHOS290    DISPLAY   *P1:24,*EL,"Hospital ID : ",UNDLN3;
            KEYIN     *P15:24,*V2LON,DIM3A;
            PACK      KEY3,DIM3A,SP10
            CALL      PUTSCR00
          ENDIF
.
          MATCH     SP3,KEY3
          GOTO      KYHOS950 IF EQUAL
.
KYHOS300  UNPACK    SP70,PTHSHOSP,PTHSNAME,PTHSAPPR
          MOVE      "Unknown",PTHSNAME
          CALL      RDPTHSP1
          MATCH     SP10,PTHSAPPR
          IF        !@EQUAL
            SQUEEZE   PTHSAPPR
            PACK      KIHOSPNO,PTHSAPPR,SP10
          ENDIF
          DISPLAY   *PCCOL:CVERT,*V2LON,KEY3,SP4,PTHSNAME
          GOTO      KYHOS999
.
KYHOS950  MOVE      ONE,EXIT
.
KYHOS999  RETURN
+
.******************************************************************************
.*                                 KEYCSHID                                   *
.*                            Key in Cashier Id.                              *
.******************************************************************************
KEYCSHID  MOVE      ZERO,EXIT   
          MOVE      SP6,DIM6A 
          MOVE      CCOL,ECOL
          MOVE      CVERT,EVERT
          KEYIN     *PECOL:EVERT,*V2LON,DIM6A;
.
          PACK      KEY6,DIM6A,SP10
          MATCH     SP6,KEY6     
          GOTO      KEYCS950 IF EQUAL
.
          MATCH     QUEST,KEY6
          IF        @EQUAL
            CALL      GETSCR00
            CALL      RCPCIDDS              * Display all cashiers
            DISPLAY   *P1:24,*EL,"Cashier ID : ",UNDLN6;
            KEYIN     *P14:24,*V2LON,DIM6A;
            PACK      KEY6,DIM6A,SP10
            CALL      PUTSCR00
          ENDIF
.
          MATCH     SP6,KEY6
          GOTO      KEYCS950 IF EQUAL
          MOVE      KEY6,DIM6A
.
          UNPACK    SP70,RCPCASH,RCPPASS,RCPSUR,RCPGIVEN,RCCIHOSP,RCCICHQA
          MOVE      ZERO,RCPSECUR
          PACK      KEY6,DIM6A,SP10
          CALL      RDCIDA1
          BRANCH    OVRCD,KEYCS950
          DISPLAY   *PECOL:EVERT,*V2LON,RCPCASH,SP1,RCPGIVEN;
          MOVE      ZERO,EXIT
          GOTO      KEYCS999
.
KEYCS950  MOVE      ONE,EXIT  
.
KEYCS999  RETURN
+
.****************************************************************************
.*                                  KYHFUN00                                *
.*                            Key in Health Fund                            *
.****************************************************************************
KYHFUN00  MOVE      ZERO,EXIT  
          MOVE      SP6,DIM6A
          KEYIN     *PCCOL:CVERT,*V2LON,DIM6A;
.                   
          PACK      KEY6,DIM6A,SP10
KYHFUN20  MATCH     SP6,KEY6
          GOTO      KYHFUN95 IF EQUAL
.         
          MATCH     QUEST,KEY6
          IF        @EQUAL
            CALL      GETSCR00
            CALL      LFUN0000              * Display all Health Funds
            CALL      PUTSCR00
            GOTO      KYHFUN00
          ENDIF
.
          MATCH     SP6,KEY6
          GOTO      KYHFUN95 IF EQUAL
.
KYHFUN40  UNPACK    SP70,HCODE,HFTABL,HFNAME
          PACK      KEY14,KEY6,ZERO4,SP20
          CALL      RDFUND1
          BRANCH    OVRCD,KYHFUN95
.
          DISPLAY   *PCCOL:CVERT,*V2LON,HCODE,SP1,HFNAME;
          MOVE      ZERO,EXIT
          GOTO      KYHFUN99
.
KYHFUN95  MOVE      ONE,EXIT
.
KYHFUN99  RETURN
+
.****************************************************************************
.*                                  LFUN0000                                *
.*                        List health funds to screen                       *
.****************************************************************************
LFUN0000  MOVE      SP20,KEY14
          MOVE      SP20,KEY10
          MOVE      FIVE,EVERT
          MOVE      ONE,ECOL
          CALL      RDSFUND1
          DISPLAY   *P1:4,*EF,*P28:4,*ULON,*V2LON,"HEALTH FUNDS";
.
LFUN1000  CALL      RDKFUND1
          BRANCH    OVRCD,LFUN6000
.
          MATCH     "0000    ",HFTABL
          GOTO      LFUN1000 IF NOT EQUAL
.
          DISPLAY   *PECOL:EVERT,*V2LON,HCODE,SP2,*HOFF,HFNAME;
          ADD       ONE,EVERT
          COMPARE   "23",EVERT
          GOTO      LFUN1000 IF LESS
.
          MOVE      FIVE,EVERT
          ADD       "40",ECOL
          COMPARE   "81",ECOL
          GOTO      LFUN1000 IF LESS
.
LFUN2000  KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          RESET     ANS
          GOTO      LFUN3000 IF EOS
          REP       UPPLOW,ANS
.
          MATCH     "E",ANS
          GOTO      LFUN9999 IF EQUAL
          MATCH     "N",ANS
          GOTO      LFUN3000 IF EQUAL
          BEEP
          GOTO      LFUN2000
.
LFUN3000  MOVE      FIVE,EVERT
          MOVE      ONE,ECOL
          DISPLAY   *P1:5,*EF;
          GOTO      LFUN1000
.
LFUN6000
LFUN9999  RETURN
+
.******************************************************************************
.*                                 KEYCDEA                                    *
.*                           KEY IN REGISTER CODE                             *
.******************************************************************************
KEYCDEA   MOVE      ZERO,EXIT
          MOVE      CVERT,EVERT
          DISPLAY   *P32:CVERT,*EL;
          MOVE      "32",ECOL
          MOVE      ZERO,CKYIMODE
          CALL      RCPREGKY
          BRANCH    EXIT,KEYCDEA8,KEYCDEA8
.
          MOVE      REGCODE,KEY3
          CALL      RDREGA1
          COMPARE   ONE,OVRCD
          GOTO      KEYCDEA5 IF EQUAL
          GOTO      KEYCDEA9
.
KEYCDEA5  DISPLAY   *P1:23,"Register Code Invalid - ";
          CALL      HITENTER
          GOTO      KEYCDEA
.
KEYCDEA8  MOVE      ONE,EXIT
.
KEYCDEA9  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEIN WITH SP70,SP70,SP70
.
          KEYIN     *P1:CVERT,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEIN
.
          PACK      FNAMEIN,FNAMEIN,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEIN
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEIN
          IF        !@EQUAL
            SQUEEZE   FNAMEIN
            ENDSET    FNAMEIN
            APPEND    ".txt",FNAMEIN      * add ".txt" if there is a path
            APPEND    SP70,FNAMEIN
            RESET     FNAMEIN
          ENDIF
          RESET     FNAMEIN
.
          DISPLAY   *P26:CVERT,*V2LON,*EL,FNAMEIN;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      INHFRTXT,FNAMEIN        * open file
.
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
.
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                 KASCI000                                   *
.*                            Keyin ASCI file                                 *
.* Returns: EXIT  0 = no file name entered                                    *
.*                1 = valid filename entered                                  *
.******************************************************************************
KASCI000  KEYIN     *P19:CVERT,*V2LON,*EL,*RV,FNAMEIN;
.
          PACK      FNAMEIN,FNAMEIN,SP20,SP20,SP20
.
          MATCH     SP70,FNAMEIN            * anything entered ?
          GOTO      KASCI950 IF EQUAL       * no
          CMATCH    "\",FNAMEIN             * "\" entered ?
          GOTO      KASCI950 IF EQUAL       * no
.
          SCAN      "/",FNAMEIN
          IF        @EQUAL
            SCAN      ".txt",FNAMEIN
            IF        !@EQUAL
              SQUEEZE   FNAMEIN
              ENDSET    FNAMEIN
              APPEND    ".txt",FNAMEIN      * add ".txt" if there is a path
              RESET     FNAMEIN
              DISPLAY   *P19:CVERT,*V2LON,*EL,FNAMEIN;
            ENDIF
          ELSE   
            RESET     FNAMEIN
            SCAN      ".txt",FNAMEIN
            GOTO      KASCI100 IF NOT EQUAL
            BUMP      FNAMEIN,-1            * remove ".txt" if just a file name
            APPEND    SP20,FNAMEIN
            RESET     FNAMEIN
            DISPLAY   *P19:CVERT,*V2LON,*EL,FNAMEIN;
          ENDIF
.
.                                           * get the 8 char file name
KASCI100  RESET     FNAMEIN
          MOVE      ZERO,DONE
          MOVE      FNAMEIN,FNAMESHT        * in case there is no path
          REPEAT
            SCAN      "/",FNAMEIN
            IF        @EQUAL
              BUMP      FNAMEIN
              PACK      FNAMESHT,FNAMEIN,SP20
            ELSE
              MOVE      ONE,DONE
            ENDIF
          UNTIL       DONE = 1
.
          RESET     FNAMEIN
.
          SQUEEZE   FNAMESHT
          SCAN      ".txt",FNAMESHT
          GOTO      KASCI200 IF NOT EQUAL
          BUMP      FNAMESHT,-1             * remove ".txt" if just a file name
          APPEND    SP20,FNAMESHT
.
KASCI200  RESET     FNAMESHT
          SQUEEZE   FNAMESHT
          MOVELPTR  FNAMESHT,LENGTH
.
          COMPARE   "9",LENGTH
          GOTO      KASCI500 IF LESS
          DISPLAY   *P1:23,*EL,"File name must not be more than 8 characters":
                    *P1:24,*EL," ";
          CALL      HITENTER
          DISPLAY   *P1:23,*EF
          GOTO      KASCI000
.
.                                           * see if file exists
KASCI500  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      INTEST01,FNAMEIN        * open file
          TRAPCLR   IO
          IF        OVRCD = 0
            MOVE      CVERT,F2
            DISPLAY   *P70:F2,*HOFF,"file found";
          ELSE
            DISPLAY   *P1:24,*EL,*B,"File not found.  ";
            CALL      HITENTER
            GOTO      KASCI000
          ENDIF
.
          IF        OPTION=5 | OPTION=6
            CALL      EXCUS000
          ENDIF
.
KASCI900  MOVE      ZERO,EXIT
          GOTO      KASCI999
.
KASCI950  MOVE      ONE,EXIT
.
KASCI999  RETURN
+
.******************************************************************************
.             EXCUS000 - execute us1 scripts
.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibarcp77.us1 ",CMDLINE
          APPEND    FNAMEIN,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
.             EXCUA000 - execute us2 scripts
.         Execute us2 command before processing
.******************************************************************************
EXCUA000  MOVE      ZERO,F4
          PACK      KEY120,FNAMEIN,SP70,SP70
          RESET     KEY120
          SQUEEZE   KEY120
          SCAN      ".txt",KEY120
          GOTO      EXCUA200 IF NOT EQUAL
          BUMP      KEY120,-1             * remove ".txt" if just a file name
.
EXCUA200  APPEND    SP70,KEY120
          RESET     KEY120
          STRIP     KEY120
.
          CLEAR     KEY110                * file name without pathname
          MOVE      KEY120,DIM120
          ENDSET    DIM120                * Set pointer to end of line
EXCUA400  CMATCH    "/",DIM120
          GOTO      EXCUA600 IF EQUAL
.
          BUMP      DIM120,-1             * Move pointer back 1 char
          GOTO      EXCUA400 IF NOT EOS   * Test for end of address
.
          GOTO      EXCUA999
.
EXCUA600  BUMP      DIM120
          APPEND    DIM120,KEY110         * file name without .txt
          APPEND    SP70,KEY110
          APPEND    SP70,KEY110
          RESET     KEY110
          STRIP     KEY110
.
          PACK      KEY120,FNAMEIN,SP70,SP70
          STRIP     KEY120
.
          CLEAR     CMDLINE
          APPEND    "ibarcp77.us2 ",CMDLINE
          APPEND    KEY120,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    KEY110,CMDLINE        * file name without pathname
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUA999  RETURN
+
.******************************************************************************
.*                                  CTMP0000              Called by: ML0000   *
.*                        Create Temp Receipting files                        *
.******************************************************************************
CTMP0000  MOVE      ZERO,EXIT
          CALL      KILL0000
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFIL01,CMDLINE        * "t77bnk" + PORT
          APPEND    TMPDEF01,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPBNKA1,TMPFIL01
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFIL02,CMDLINE
          APPEND    TMPDEF02,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPBDTA1,TMPFIL02
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFIL03,CMDLINE
          APPEND    TMPDEF03,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPDTEA1,TMPFIL03
.
          CALL      CLER0000                * empty all files if Oracle
.
.                                           * set up temporary print file
          PREP      PRTFILE1,TMPPRTFL
.
CTMP9999  RETURN
+
.******************************************************************************
.*                             KILL0000                  Called by:           *
.*                      erase temporary files                                 *
.******************************************************************************
KILL0000  CLOSE     TMPBNKA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFIL01,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TMPBDTA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFIL02,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TMPDTEA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFIL03,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PREP      PRTFILE1,TMPPRTFL
          CLOSE     PRTFILE1,DELETE
.
KILL9999  RETURN
+
.******************************************************************************
.*                             CLER0000                 Called by:            *
.*               close and erase the temporary file                           *
.******************************************************************************
.
CLER0000  PACK      KEY12,SP20
          CALL      RSTMBNK1
          CALL      RKTMBNK1
          BRANCH    OVRCD,CLER1000
.
          PACK      KEY12,TMBNRECN,TMBNTDAT
          CALL      DETMBNK1
.
          GOTO      CLER0000
.
CLER1000  PACK      KEY15,SP20
          CALL      RSTMBDT1
          CALL      RKTMBDT1
          BRANCH    OVRCD,CLER2000
.
          PACK      KEY15,TMBDRECN,TMBDUNIQ
          CALL      DETMBDT1
.
          GOTO      CLER1000
.
CLER2000  PACK      KEY17,SP20
          CALL      RSTMDTE1
          CALL      RKTMDTE1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY17,TMDTRECN,TMDTTCNT
          CALL      DETMDTE1
.
          GOTO      CLER2000
.
CLER9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000              Called by: ML0000   *
.*                                 Open Files                                 *
.******************************************************************************
OPEN0000  MOVE      ZERO,EXIT
.                                           * open Health Fund upload text file 
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      INHFRTXT,FNAMEIN        * open file
          TRAPCLR   IO
.
          IF        OVRCD = 1
            IF        OPTION=3 | OPTION=4
              DISPLAY   *P1:24,*EL,*B,"Non Health Fund upload file is missing ";
            ELSE
              IF        OPTION=5 | OPTION=6
                DISPLAY   *P1:24,*EL,*B,"Bulk Receipt Upload file is missing ";
              ELSE
                DISPLAY   *P1:24,*EL,*B,"Health Fund upload file is missing  ";
              ENDIF
            ENDIF
            CALL      HITENTER
            MOVE      ONE,EXIT
            GOTO      OPEN9999
          ENDIF
.
. ----------------------------------------- * open IBA files -------------
.
OPEN4000
          DISPLAY   *P64:24,"fmscsaaf";
          OPEN      FMSCSAA1,"fmscsaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"     
          OPEN      PATMX1A1,"patmx1af"     
.
          DISPLAY   *P64:24,"patre1af";
          OPEN      PATRE1A1,"patre1af"     
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"     
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"     
.
          OPEN      AAEDE1A1,"aaede1af"
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"     
.
          DISPLAY   *P64:24,"rcpbnkaf";
          OPEN      RCPBNKA1,"rcpbnkaf"     
.
          DISPLAY   *P64:24,"rcpbdtaf";
          OPEN      RCPBDTA1,"rcpbdtaf"     
.
          DISPLAY   *P64:24,"rcpdteaf";
          OPEN      RCPDTEA1,"rcpdteaf"     
.
          OPEN      OUTSITA1,"outsitaf"
.
OPEN9999  RETURN
.******************************************************************************
.*                                  GREG0000                                  *
.*                         Retrieve & edit Register info                      *
.******************************************************************************
GREG0000  MOVE      ZERO,EXIT
          MOVE      ZERO,ERRIN
.
          PRINT     *N
          CALL      GTACCT00                * read Register records
          BRANCH    EXIT,GREG9000
.
          IF        OPTION=3 | OPTION=4 | OPTION=5 | OPTION=6
            IF        REGIBACD=1 | REGIBACD=5 | REGIBACD=99
              GOTO      GREG9000
            ENDIF
            PRINT     *1,"Register 'System Code' is '",REGISTRG,"' - it must ":
                      "be a Register code with a System Code of 1, 5 or 99":
                      " - *** Error ***"
          ELSE
            IF        REGIBACD=1 | REGIBACD=2 | REGIBACD=5 | REGIBACD=99
              GOTO      GREG9000
            ENDIF
            PRINT     *1,"Register 'System Code' is '",REGISTRG,"' - it must ":
                      "be a Register code with a System Code of 1, 2, 5 or 99":
                      " - *** Error ***"
          ENDIF
.
          ADD       ONE,ERRIN
.
GREG9000  IF        ERRIN > 0
            PRINT     *1," Errors have been detected";
            MOVE      ONE,EXIT
            IF        OPTION = 2 | OPTION =4 | OPTION =6
              PRINT   *28,"- *** file will not be uploaded ***"
            ELSE
              PRINT   *28,"in Edit only run"
            ENDIF
            GOTO      GREG9999
          ENDIF
          MOVE        ZERO,EXIT
.
GREG9999  RETURN
+
.******************************************************************************
.*                                  PINP0000                                  *
.*                         Process The Input Text File                        *
.******************************************************************************
PINP0000  MOVE      ZERO,EXIT
          MOVE      ZERO,RECIN
          MOVE      ZERO,REC03
          MOVE      ZERO,HEADNO
          MOVE      ZERO,TMRCPTNO           * File Header count
.
          MOVE      "Fld",COLTXT
          MOVE      "99",CLNO               * force Report Header if errors
          MOVE      "Health Fund upload file",HEADDES1
          IF        OPTION=3 | OPTION=4
            MOVE      "Non Health Fund upload file",HEADDES1
          ENDIF
.
          PACK      HEADDES2,FNAMEHFI,SP30
          STRIP     HEADDES2
.
. ----- Read the input data file into temporary variables -----
.
PINP1000  READ      INHFRTXT,SEQ;HFRRECID;  * read Health Fund record id
          GOTO      PINP5000 IF OVER        * end of file
.
          PACK      HFRRECID,HFRRECID,SP70
          MATCH     SP70,HFRRECID
          GOTO      PINP5000 IF EQUAL
.
          MOVE      "  ",FLDNO
          ADD       ONE,RECIN
          MOVE      ZERO,HFRIDNO            * identify the HF record id.
          MOVE      HFRRECID,HFRIDNO
          IF        HFRIDNO < 1 | HFRIDNO > 3 
            GOTO      PINP4000              * unknown record id. 
          ENDIF
.
.                                           * process the specific record id.
          PERFORM   HFRIDNO,P01R0000,P02R0000
          BRANCH    EXIT,PINP5200           * halt processing
.
          GOTO      PINP1000
.
.
PINP4000  UNPACK    SP70,FLD10001,FLD10002,FLD10003,FLD10004   * clear record
          READ      INHFRTXT,SEQ;FLD10001,FLD10002,FLD10003,FLD10004 
.
          PACK      ERRDATA,HFRRECID,SP1,FLD10001,SP1,FLD10002,SP1:
                            FLD10003,SP1,FLD10004,SP70
.
          CLEAR     ERRMESS
          APPEND    "Unknown Record Id. '",ERRMESS
          SQUEEZE   HFRRECID
          APPEND    HFRRECID,ERRMESS
          APPEND    "' - record bypassed *** Error ***",ERRMESS
          CALL      PRNT0000
          GOTO      PINP1000 
.
. ----- Finished -----
.
PINP5000  MOVE      ZERO,EXIT
PINP5200  CLOSE     PRTFILE1
.
          MATCH     "1",ALLINDIC
          IF        @EQUAL
            MATCH     SP70,INSCOMP 
            IF        @EQUAL | @EOS
              CLEAR     ERRMESS
              APPEND    "No Insurance Code found among invoices.",ERRMESS
              CALL      PRNT0000
            ENDIF
          ENDIF
.
          PRINT     *N,*1,"Total Records read   : ",RECIN,*N:
                    *1,"Records processed    : ",REC00,*N;
.
          PRINT     *1,"Errors               : ",ERRIN;    
          IF        ERRIN > 0 & (OPTION = 2 | OPTION=4)
            PRINT     *30," *** file will not be uploaded ***"
          ENDIF
          PRINT     *N,*N
.
.         MOVE      ZERO,CPAGENO
          MOVE      ZERO,INVCOUNT
          CALL      HED20000
.
          OPEN      PRTFILE1,TMPPRTFL
PINP6200  READ      PRTFILE1,SEQ;PRTLINE
          GOTO      PINP7000 IF OVER
.
          ADD       ONE,INVCOUNT
          PRINT     *1,PRTLINE
          ADD       ONE,CLNO
          COMPARE   "56",CLNO
          CALL      HED20000 IF NOT LESS    * Print the report header
          GOTO      PINP6200
.
PINP7000  CLOSE     PRTFILE1
          PRINT     *N,*1,"End of File"
          COMPARE   "45",CLNO
          CALL      HED20000 IF NOT LESS    * Print the report header
.
          MOVE      INVCOUNT,DIM5A
          SQUEEZE   DIM5A
          PRINT     *N,*20,"Invoices processed        : ",DIM5A
          MOVE      TRANTAMT,DIM15A
          SQUEEZE   DIM15A
          PRINT     *N,*20,"Payments by Invoice Total : $ ",DIM15A
.
          COMPARE   TRANTAMT,INHFAMNT
          IF        !@EQUAL
            MOVE      INHFAMNT,DIM15A
            SQUEEZE   DIM15A
            IF        OPTION=3 | OPTION=4
              PRINT     *20,"Header Total  : $ ",DIM15A,SP2:
                        "does not match 'Payments by Invoice' Total - ":
                        "*** Error ***"
            ELSE
              PRINT     *20,"Health Fund Header Total  : $ ",DIM15A,SP2:
                        "does not match 'Payments by Invoice' Total - ":
                        "*** Error ***"
            ENDIF
            ADD     ONE,ERRIN
          ENDIF
.
          IF        ERRIN > 0
            PRINT     *N,*1,"Errors have been detected ";
            IF        OPTION = 2 | OPTION = 4
              PRINT   *27,"- *** file will not be uploaded ***"
            ELSE
              PRINT   *27,"in Edit only run"
            ENDIF
            GOTO    PINP9500
          ENDIF
.
          GOTO      PINP9999
.
PINP9500  MOVE      ONE,EXIT
.
PINP9999  RETURN
+
.******************************************************************************
.*                                  P01R0000                                  *
.*                            Process "01" Record                             *
.******************************************************************************
P01R0000  MOVE      ZERO,EXIT
          MOVE      ZERO,TRANNUMB           * Transaction count for Invoices
          MOVE      ZERO,TRANTAMT           * Total Amount for all Tranactions
.
          READ      INHFRTXT,SEQ;FLD60002,FLD60003,FLD60004,FLD60005,FLD60006:
                                 FLD60007,FLD60008,FLD60009,FLD60010,FLD60011
          ADD       ONE,REC00
.
          PACK      ERRDATA,HFRRECID,SP1,FLD60002,SP1,FLD60003,SP1:
                            FLD60004,SP1,FLD60005,SP1,FLD60006,SP70
.
          ADD       ONE,HEADNO
          IF        HEADNO > 1
            CLEAR     ERRMESS
            IF        OPTION=1 | OPTION=2
              APPEND    "A second Health Fund header record ",ERRMESS
            ELSE
              APPEND    "A second Non Health Fund header record ",ERRMESS
            ENDIF
            APPEND    "has been detected -",ERRMESS
            CALL      PRNT0000
            CLEAR     ERRMESS
            APPEND    "Processing has been halted - *** Error ***",ERRMESS
            CALL      PRLN0000
            MOVE      ONE,EXIT
            GOTO      P01R9999
          ENDIF
.
P01R1000  MOVE      FLD60002,FLD60IN        * Fld 2 - Hospital No.
          MOVE      " 2",FLDNO
          MOVE      "7",LENGTH              * retrieve No. and right justify
          CALL      RJUS0000                * right justify
          MOVE      FLD60OUT,INHOSPNO
.
          IF        IBCNMHOS = 1       
            MATCH     INHOSPNO,KIHOSPNO
            IF        !@EQUAL
              CLEAR     ERRMESS
              APPEND    "Upload File Hospital No. '",ERRMESS
              APPEND    INHOSPNO,ERRMESS
              APPEND    "' does not match",ERRMESS
              CALL      PRNT0000
              CLEAR     ERRMESS
              APPEND    "Hospital Id. '",ERRMESS
              APPEND    KIHOSPID,ERRMESS
              APPEND    "' Hospital No. '",ERRMESS
              APPEND    KIHOSPNO,ERRMESS
              APPEND    "' - *** Error ***",ERRMESS
              CALL      PRLN0000
            ENDIF
          ENDIF
.
          PACK      INHOSNAM,FLD60003,SP30
          PACK      INHOSAD1,FLD60004,SP30
          PACK      INHOSAD2,FLD60005,SP30
          PACK      INHOSAD3,FLD60006,SP30
          PACK      INHOSAD4,FLD60007,SP30
          PACK      INHFDATE,FLD60008,SP30  * Health Fund Date of payment
          PACK      INHFCODE,FLD60009,SP30  * Health Fund Code
.
P01R2000  MOVE      "10",FLDNO
          REP       "#" $ , ",FLD60010
          MOVE      ZERO,F12P2
          MOVE      FLD60010,INHFAMNT
.
          COMPARE   KIAMOUNT,INHFAMNT
          IF        !@EQUAL
            CLEAR     ERRMESS
            APPEND    "Upload File Amount ' $ ",ERRMESS
            MOVE      INHFAMNT,DIM15A
            SQUEEZE   DIM15A
            APPEND    DIM15A,ERRMESS
            APPEND    "' does not match expected amount ",ERRMESS
            CALL      PRNT0000
.
            MOVE      KIAMOUNT,DIM15A
            SQUEEZE   DIM15A
            CLEAR     ERRMESS
            APPEND    "of ' $ ",ERRMESS
            APPEND    DIM15A,ERRMESS
            APPEND    "' - *** Error ***",ERRMESS
            CALL      PRLN0000
          ENDIF
.
.         set up Receipting Bank Header
.
P01R5000  MOVE      ZERO,EXIT
          CALL      CLTMPBNK
          CALL      CLTMPBDT
.
          ADD       ONE,TMRCPTNO
          MOVE      TMRCPTNO,TMBNRECN
          MOVE      KIPAYDAT,TMBNTDAT
          MOVE      INHFAMNT,TMBNTOTP
          MOVE      KICASHID,TMBNCDRW
          IF        IBCNMHOS = 1
            MOVE      KIHOSPID,TMBNMHOS
            MOVE      CHOSPNUM,TMBNMDHS
          ENDIF
          MOVE      "1",TMBNTTYP            * received from "Health Fund"
          MOVE      KIHFUND,TMBNRCOD
          IF        OPTION=3 | OPTION=4
            MOVE      "2",TMBNTTYP          * received from "Insurance"
            MOVE      INSCOMP,TMBNRCOD      * Insurance company
          ENDIF
          MOVE      "0",TMBNSTAT
          MOVE      KICHQACC,TMBNCHQA
.
          PACK      TMBNCDAT,CCC,CYY,CMM,CDD,SP10
          REP       " 0",TMBNCDAT
          CLOCK     TIME,TMBNCTIM
          MOVE      KIWEBUID,TMBNCUID
.
.         write the record
.
          PACK      KEY12,TMBNRECN
          CALL      RATMBNK1
          IF        OVRCD = 1
            CALL      WRTMBNK1
          ENDIF
.
.         set up Receipting Bank Detail Recd
.
P01R6000  MOVE      ZERO,EXIT
          MOVE      TMBNTDAT,TMBDTDAT
          MOVE      TMBNRECN,TMBDRECN
          MOVE      "  1",TMBDUNIQ
          MOVE      TMBNMHOS,TMBDMHOS
          MOVE      TMBNMDHS,TMBDMDHS
          SQUEEZE   KIPAYTYP
          MOVE      KIPAYTYP,TMBDPTYP
          MOVE      PAYMCODE,TMBDPCOD
          MOVE      TMBNTOTP,TMBDPAMT
          IF        KIPAYTPE = 2
            MOVE      KICHQNUM,TMBDCHQN
            MOVE      KIHFUND,TMBDDRAW
            IF        OPTION=3 | OPTION=4
              MOVE      INSCOMP,TMBDDRAW      * Insurance company
            ENDIF
            MOVE      KIBNKNAM,TMBDBANK
            MOVE      KIBNKBCH,TMBDBRCH
          ENDIF
          IF        KIPAYTPE = 4
            MOVE      KISTTREF,TMBDSTRF
            MOVE      KIHFUND,TMBDPAYD
            IF        OPTION=3 | OPTION=4
              MOVE      INSCOMP,TMBDPAYD      * Insurance company
            ENDIF
          ENDIF
          MOVE      TMBNCDAT,TMBDCDAT
          MOVE      TMBNCTIM,TMBDCTIM
          MOVE      TMBNCUID,TMBDCUID
.
          PACK      KEY15,TMBDRECN,TMBDUNIQ,SP70
          CALL      WRTMBDT1
.
P01R9999  RETURN
+
.******************************************************************************
.*                                  P02R0000                                  *
.*                            Process "02" Record                             *
.******************************************************************************
P02R0000  MOVE      ZERO,EXIT
          MOVE      ZERO,SUSPEND
.         
          READ      INHFRTXT,SEQ;FLD60002,FLD60003,FLD60004,FLD60005,FLD60006:
                                 FLD60007,FLD60008,FLD60009,FLD60010,FLD60011:
                                 FLD60012,FLD60013,FLD60014,FLD60015,FLD60016:
                                 FLD60017
          ADD       ONE,REC00
.           
          PACK      ERRDATA,HFRRECID,SP1,FLD60002,SP1,FLD60003,SP1:
                            FLD60004,SP1,FLD60005,SP1,FLD60006,SP70
.
          CALL      CLPATINV
          CALL      CLPATMAS
          CALL      CLPATVIS
          CALL      CLPMSVX1
          MOVE      ZERO,IBALANCE
.
          MOVE      SP70,FLD60IN            * clear FLD60IN
          LENSET    FLD60IN
          MOVE      FLD60002,FLD60IN        * Fld 2 - Hospital No.
          MOVE      " 2",FLDNO
          MOVE      "7",LENGTH              * retrieve No. and right justify
          CALL      RJUS0000                * right justify
          MOVE      FLD60OUT,INHOSNO2
          MATCH     INHOSPNO,INHOSNO2
          IF        !@EQUAL
            CLEAR     ERRMESS
            APPEND    "Hospital Id. '",ERRMESS
            APPEND    INHOSNO2,ERRMESS
            APPEND    "' is not the same as Header record ",ERRMESS
            APPEND    "- *** Error ***",ERRMESS
            CALL      PRNT0000
          ENDIF
.
          MOVE      FLD60003,INHOSREF
          MOVE      FLD60004,INPAYNAM       * Fld 4 - HFund Reference Name
.
          MOVE      SP70,FLD60IN            * clear FLD60IN       
          MOVE      FLD60005,FLD60IN        * Fld 5 - Invoice No.
          MOVE      " 5",FLDNO
          MOVE      "8",LENGTH
          CALL      RJUS0000                * right justify
          CALL      EDNUM000
          MOVE      FLD60OUT,ININVNUM
.
          MOVE      ZERO,RESPOK
          MOVE      ZERO,PMIOK
.
          MOVE      SP8,DPINVADM
          MOVE      SP8,PINVUR
          PACK      KEY8,ININVNUM,SP8
          CALL      RDPTINV1
          IF        OVRCD = 1
            CLEAR     ERRMESS
            APPEND    "Invoice No. '",ERRMESS
            APPEND    ININVNUM,ERRMESS
            APPEND    "' not found - *** Error ***      ",ERRMESS
            CALL      PRNT0000
            GOTO      P02R4000              * bypass Master & Admission
          ELSE
            MATCH     "1",PTINCRST
            IF        @EQUAL
              MOVE      ONE,SUSPEND
            ENDIF
          ENDIF
          MOVE      PINVAMT,IBALANCE        * Invoice Amount    
          ADD       PINVPATA,IBALANCE       * minus Amount paid by Patient
          ADD       PINVHFDA,IBALANCE       * minus Amount paid by Health Fund
          ADD       PINVOTHA,IBALANCE       * minus Other Amounts paid
          ADD       PINVJOUR,IBALANCE       * plus Journals
          ADD       PTINGSTJ,IBALANCE       * plus GST Adjustments
          ADD       PTINCRTT,IBALANCE
.
          MOVE      " 4",FLDNO              * Fld 4 - Patient Name
          PACK      KEY8,PINVUR
          CALL      RDMAST1                 * get PMI Master
          IF        OVRCD = 1
            CLEAR     ERRMESS
            APPEND    "PMI Master for UR '",ERRMESS
            APPEND    PINVUR,ERRMESS
            APPEND    "' not found - *** Error ***      ",ERRMESS
            CALL      PRNT0000
            GOTO      P02R4000
          ENDIF
          MOVE      ONE,PMIOK
.
.         get Responsible Person recd
.
          PACK      DIM30B,PSNAME,SP70
          MOVE      ZERO,RESPOK
          PACK      KEY8,DPINVADM,SP8
          CALL      RDRESP1
          IF        OVRCD = 0
            MOVE      ONE,RESPOK
            PACK      DIM30B,PKNAME,SP70
          ENDIF
.
          PACK      DIM30A,INPAYNAM,SP70
          SQUEEZE   DIM30A
.
.         SCAN      DIM30A,DIM30B
.         IF        !@EQUAL
.           RESET     DIM30B
.           MOVE      " 4",FLDNO
.           CLEAR     ERRMESS
.           IF        RESPOK =1
.             APPEND    "Surname of PRFA '",ERRMESS
.           ELSE
.             APPEND    "Surname on PMI '",ERRMESS
.           ENDIF
.           STRIP     DIM30B
.           APPEND    DIM30B,ERRMESS
.           APPEND    "' does not match '",ERRMESS
.           SQUEEZE   DIM30A
.           APPEND    DIM30A,ERRMESS
.           APPEND    "'",ERRMESS
.           CALL      PRNT0000
.         ENDIF
.
          CALL      PCLM0000             * Get patient's details
.
P02R4000  MOVE      " 8",FLDNO
          REP       "#" $ , ",FLD60008
          MOVE      FLD60008,INVPAID        * payment
.
.         set up Receipt Details
.
          MOVE      ZERO,EXIT
          CALL      CLTMPDTE
.
          MOVE      TMBNTDAT,TMDTTDAT
          MOVE      TMBNRECN,TMDTRECN
.
          COMPARE   "99999",TRANNUMB
          IF        @EQUAL
            PRINT     *1," ************** More than 99999 Transactions - ":
                      " Central Receipting cannot handle this ***************":
                      *N,*40,"Processing suspended *****************"
            ADD       ONE,ERRIN
            MOVE      ONE,EXIT
            GOTO      P02R9999
          ENDIF
.
          ADD       ONE,TRANNUMB
          MOVE      TRANNUMB,TMDTTCNT
.
          PACK      TMDTINVN,SP4,ININVNUM   * change Inv.No. from 8 to 12
          MOVE      DPINVADM,TMDTVIST
          MOVE      PINVUR,TMDTURNO
.
          MOVE      INPAYNAM,TMDTNAME
          MOVE      SP70,TMDTSFLG
.
          PACK      KEY8,DPINVADM,SP8
          CALL      RDRESP1
          IF        RESPOK = 1
            MOVE      INPAYNAM,TMDTNAME     * INPAYNAM (HF input Name)
            MOVE      INPAYNAM,TMDTREFR
            MOVE      PKADD1,TMDTADD1
            MOVE      PKADD2,TMDTADD2
            MOVE      PKSUBR,TMDTADD3
            MOVE      PKADD4,TMDTADD4
            MOVE      PKPOST,TMDTPOST
          ELSE
            IF        PMIOK = 1
              MOVE      SP1,DIM1A
              MOVE      PGNAME,DIM1A
              PACK      DIM30A,PTITL,SP1,DIM1A,DOT,PSNAME
              SQUEEZE   DIM30A
              PACK      TMDTNAME,DIM30A,SP70
              MOVE      PADD1,TMDTADD1
              MOVE      PADD2,TMDTADD2
              MOVE      PSUBRB,TMDTADD3
              MOVE      PADD4,TMDTADD4
              MOVE      PPOST,TMDTPOST
            ENDIF
          ENDIF
.                                           * get Account info. for INCA & BNKA
.                                           * selected in GTACCT00
          MOVE      KIREGCOD,TMDTREGI
          MOVE      SAVTINCA,TMDTINCA
          MOVE      SAVTBNKA,TMDTBNKA
.
          IF        SUSPEND = 1
            MATCH     SP70,RCCNURCP
            IF        !@EQUAL
              MOVE      RCCNURCP,TMDTREGI     * Suspend register
              MOVE      SAVSINCA,TMDTINCA
              MOVE      SAVSBNKA,TMDTBNKA
            ENDIF
          ENDIF
.
          ADD       INVPAID,TRANTAMT        * add Invoice amount to Total
          MOVE      INVPAID,TMDTAMNT
.                                           * Calc Outstanding Balance
          MOVE      ZERO,TMDTDISC
          MOVE      TMBNMHOS,TMDTMHOS

          MOVE      TMBNMDHS,TMDTMDHS
          MOVE      "0",TMDTALLO
          PACK      KEY3,TMDTREGI,SP3
          CALL      RDREGA1
          IF        OVRCD=0
            IF        REGIBACD=99
              MOVE      "2",TMDTALLO        * Suspense register
            ENDIF
          ENDIF
.
          MOVE      TMBNCDAT,TMDTCDAT
          MOVE      TMBNCTIM,TMDTCTIM
          MOVE      TMBNCUID,TMDTCUID
.
.         * write the records
.
          MOVE      REGDESC,DIM30A
          MOVE      SP70,KEY22
          IF        SUSPEND = 1
            MOVE    SAVSDESC,DIM30A
            MOVE    "Invoice Fully Credited",KEY22
          ENDIF
          PACK      DIM30B,INPAYNAM,SP30
          PACK      PRTLINE,DIM30A,SP2,TMDTINVN,SP4,TMDTURNO,SP4,DIM30B:
                    SP4,IBALANCE,SP4,INVPAID,SP2,KEY22,SP70
          WRITE     PRTFILE1,SEQ;PRTLINE
.
          PACK      KEY17,TMDTRECN,TMDTTCNT,SP70
          CALL      WRTMDTE1
          GOTO      P02R9999
.
P02R9999  RETURN
+
.******************************************************************************
.*                                  P03R0000                                  *
.*                            Process "03" Record                             *
.******************************************************************************
P03R0000  MOVE      ZERO,EXIT
.
          READ      INHFRTXT,SEQ;FLD60002,FLD60003,FLD60004,FLD60005,FLD60006:
                                 FLD60007,FLD60008,FLD60009,FLD60010,FLD60011:
                                 FLD60012,FLD60013
          ADD       ONE,REC03
.                                   
          PACK      ERRDATA,HFRRECID,SP1,FLD60002,SP1,FLD60003,SP1:           
                            FLD60004,SP1,FLD60005,SP1,FLD60006,SP70
.
.         ***************** Record type "03" not required ***************
.
P03R9999  RETURN
+
.******************************************************************************
.         Routine to validate and get the claim code
.******************************************************************************
PCLM0000  PACK      KEY8,DPINVADM,SP8
          CALL      RDPTVIS1                * get Visit record
          COMPARE   ZERO,OVRCD
          GOTO      PCLM0200 IF EQUAL
.
          CLEAR     ERRMESS
          APPEND    "Visit No. '",ERRMESS
          APPEND    DPINVADM,ERRMESS
          APPEND    "' not found - *** Error ***      ",ERRMESS
          GOTO      PCLM9000
.
PCLM0200  BRANCH    PINVSYS,PCLM1000,PCLM2000,PCLM3000
          GOTO      PCLM8000
.
PCLM1000  CALL      RDDETA1
          BRANCH    OVRCD,PCLM8000
          MOVE      ADACOMP,ACLAIM        * claim code
          GOTO      PCLM4000
.
PCLM2000  MOVE      "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
          PACK      CFNAME WITH OSTFILE,FILBB1A1
          CALL      OPOTBB11
.
          MOVE      PVIBILL,KEY8
          CALL      RDBOKB1
          CLOSE     OUTBB1A1
          BRANCH    OVRCD,PCLM8000
          MOVE      OBACOMP,ACLAIM        * claim code
          GOTO      PCLM4000
.
PCLM3000  CALL      RDMISS1
          BRANCH    OVRCD,PCLM8000
.
PCLM4000  IF        OPTION=1 | OPTION=2 | OPTION=5 |OPTION=6
            GOTO      PCLM9999
          ENDIF
.
.         Check the value of indicator 1 of the claim code
.
          MOVE      "CL",CODE
          PACK      KEY5,CODE,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,PCLM7000
.
          MOVE      TCINDC1,KEY1            * Save indicator 1
.
          MATCH     "1",ALLINDIC
          IF        @EQUAL
            MATCH     SP70,INSCOMP
            IF        @EQUAL
              MATCH     SP70,PTCDASC5
              IF        !@EQUAL
                PACK      KEY6,PTCDASC5
                CALL      RDINSR1
                IF        OVRCD=0
                  MOVE      PTCDASC5,INSCOMP
                  MOVE      INAME,INSNAME
.
.                 PACK      KEY12,TMBNRECN,SP70
.                 CALL      RDTMBNK1
.                 IF        OVRCD = 0
.                   MOVE      INSCOMP,TMBNRCOD
.                   CALL      UPTMBNK1
.                 ENDIF
.
.                 PACK      KEY15,TMBDRECN,TMBDUNIQ,SP70
.                 CALL      RDTMBDT1
.                 IF        OVRCD = 0
.                   MOVE      INSCOMP,TMBDDRAW
.                   CALL      UPTMBDT1
.                 ENDIF
.
                ENDIF
              ENDIF 
            ENDIF 
            GOTO      PCLM9999
          ENDIF
.
          PACK      KEY5,CODE,KCLMCODE
          CALL      RDCODE1
          BRANCH    OVRCD,PCLM7000
.
          MATCH     KEY1,TCINDC1
          GOTO      PCLM9999 IF EQUAL
.
PCLM7000  CLEAR     ERRMESS
          APPEND    "Pat.Clm Typ indic.1 for Vis.'",ERRMESS
          APPEND    DPINVADM,ERRMESS
          APPEND   "' NOT match with Clm Typ Receivd From.",ERRMESS
          GOTO      PCLM9000
.
PCLM8000  CLEAR     ERRMESS
          APPEND    "Patient Details for Visit No. '",ERRMESS
          APPEND    DPINVADM,ERRMESS
          APPEND    "' not found - *** Error ***      ",ERRMESS
          GOTO      PCLM9000
.
PCLM9000  APPEND    SP70,ERRMESS
          CALL      PRNT0000
PCLM9999  RETURN
+
.******************************************************************************
.*                                  GTACCT00                                  *
.*         Get Account and Banking details (based on GTACCT00 in RCPWEB03)    *
.******************************************************************************
GTACCT00  MOVE      ZERO,EXIT
          UNPACK    SP70,SAVSINCA,SAVSBNKA,SAVTINCA,SAVTBNKA
.
          MATCH     SP70,RCCNURCP
          IF        !@EQUAL
            MOVE      RCCNURCP,KEY3
            CALL      RDREGA1
            BRANCH    OVRCD,GTACCT91
.
            MATCH     "1",REGACTIV
            GOTO      GTACCT95 IF EQUAL       * Inactive
.
            IF        IBCNMHOS=1
              MATCH     KIHOSPID,REGHOSP
              GOTO      GTACCT92 IF NOT EQUAL * different hospital id
            ENDIF
            GOTO      GTACCT30
          ENDIF
.
.         Find the first Suspense register
.
          MOVE      "DEFAULT SUSPENSE ACCT",SAVSDESC
          MOVE      "99",DIM2A
          PACK      KEY5,DIM2A,SP3
          CALL      RDSREGA3
GTACCT10  CALL      RDKREGA3
          BRANCH    OVRCD,GTACCT40
.
          COMPARE   "99",REGIBACD           * Suspense register
          GOTO      GTACCT40 IF NOT EQUAL
          MATCH     "1",REGACTIV
          GOTO      GTACCT10 IF EQUAL       * Inactive
.
          IF        IBCNMHOS=1
            MATCH     KIHOSPID,REGHOSP
            GOTO      GTACCT10 IF NOT EQUAL
          ENDIF
.
          MOVE      REGCODE,RCCNURCP
GTACCT30  MOVE      REGDESC,SAVSDESC
          MOVE      REGGENLD,SAVSINCA       * Credit account
          MOVE      REGGENBK,SAVSBNKA       * Debit account
.
.         Check the Keyin Register
.
GTACCT40  PACK      KEY3,KIREGCOD,SP3
          CALL      RDREGA1
          BRANCH    OVRCD,GTACCT99
          IF        IBCNMHOS=1
            MATCH     KIHOSPID,REGHOSP
            GOTO      GTACCT94 IF NOT EQUAL * different hospital id
          ENDIF
.                                           ***********************
          COMPARE   TWO,HSYS1
          GOTO      GTACCT90 IF EQUAL       * Using Other FMS- get Acc.from Reg.
          COMPARE   THREE,HSYS1
          GOTO      GTACCT90 IF EQUAL       * Using Other FMS- for Integra
          COMPARE   FOUR,HSYS1
          GOTO      GTACCT90 IF EQUAL       * Using Comma delimited
          COMPARE   FIVE,HSYS1
          GOTO      GTACCT90 IF EQUAL       * Using Integra Extended
.
          MATCH     REGGENLD,SP20
          GOTO      GTACCT99 IF EQUAL
.
          MOVE      REGGENLD,SAVTINCA       
          CALL      GETC0000                * Get Control Account Details
          PACK      SAVTBNKA,FMLALEDG,FMCSBNK 
          GOTO      GTACCT99 
.
.         Get Credit and Bank Account from Register record
.
GTACCT90  COMPARE   "99",REGIBACD           * Suspense
          IF        @EQUAL
            MOVE      REGGENLD,SAVTINCA     * Credit account
            MOVE      REGGENBK,SAVTBNKA     * Debit account
          ELSE                              * Patient billing/Priv.Prac./etc
            MOVE      REGGENDB,SAVTINCA     * Debtors Control account as Crd.Acc
            MOVE      REGGENBK,SAVTBNKA     * Debit account
          ENDIF
          GOTO      GTACCT99
.
GTACCT91  PRINT     *1,"Suspense Register ",KEY3," is invalid"
          MOVE      ONE,EXIT
          GOTO      GTACCT98
.
GTACCT92  PRINT     *1,"Suspense Register ",KEY3," is for Hospital ID: ",REGHOSP
          MOVE      ONE,EXIT
          GOTO      GTACCT98
.
GTACCT94  PRINT     *1,"Keyin Register ",KEY3," is for Hospital ID: ",REGHOSP
          MOVE      ZERO,EXIT
          GOTO      GTACCT98
.
GTACCT95  PRINT     *1,"Suspense Register ",KEY3," is inactive"
          MOVE      ONE,EXIT
          GOTO      GTACCT98
.
GTACCT98  MOVE      ONE,ERRIN
GTACCT99  RETURN
+
.******************************************************************************
.*                                  GETC0000                                  *
.*                   Get Control Account Details (ex RCPWEB03)                *
.******************************************************************************
GETC0000  UNPACK    REGGENLD,KEY2,DIM12A
          CALL      RDFMLA1              * Ledger Master Details
          MOVE      FMLABNK,FMCSBNK
          MOVE      FMLADEB,FMCSDEB
          MOVE      FMLAAGST,FMCSAGST
.
          PACK      KEY14,REGGENLD,SP70
          CALL      RDFMCS1              * Selected Control Accounts
          BRANCH    OVRCD,GETC9000
.
          MATCH     SP70,FMCSBNK
          GOTO      GETC0010 IF NOT EQUAL
.
          MOVE      FMLABNK,FMCSBNK
.
GETC0010  MATCH     SP70,FMCSDEB
          GOTO      GETC0020 IF NOT EQUAL
.
          MOVE      FMLADEB,FMCSDEB
.
GETC0020  MATCH     SP70,FMCSAGST
          GOTO      GETC9999 IF NOT EQUAL
.
          MOVE      FMLAAGST,FMCSAGST
.
          GOTO      GETC9999
.
GETC9000  MOVE      FMLABNK,FMCSBNK
          MOVE      FMLADEB,FMCSDEB
          MOVE      FMLAAGST,FMCSAGST
.
GETC9999  RETURN
+
.******************************************************************************
.*                                  PROC0000                                  *
.*                          Process Temp file to RCP files                    *
.******************************************************************************
PROC0000  MOVE      ZERO,EXIT
          MOVE      SP15,SAVERECN
.                                           * read temp Bank Header
          MOVE      "           1",KEY12
          CALL      RDTMBNK1
          BRANCH    OVRCD,PROC9000
.                                           * read temp Bank Details record
          MOVE      "  1",KEY3 
          PACK      KEY15,KEY12,KEY3
          CALL      RDTMBDT1
          BRANCH    OVRCD,PROC9100
.                                           * get next Receipt Number
PROC1000  READ      CONTROLF,THIRTY6;*193,HRECPT
          ADD       ONE,HRECPT
          WRITAB    CONTROLF,THIRTY6;*193,HRECPT
          SUB       ONE,HRECPT
.
          MOVE      HRECPT,KEY12
          CALL      RDRCBNK1
          COMPARE   ZERO,OVRCD
          GOTO      PROC1000 IF EQUAL
.
.         write the RCPBNK & RCPBDT records
.
          MOVE      TMBNRECN,SAVERECN
          MOVE      HRECPT,TMBNRECN
          MOVE      HRECPT,TMBDRECN
.
          MATCH     "1",ALLINDIC
          IF        @EQUAL
            MOVE      INSCOMP,TMBNRCOD
          ENDIF
.
          CALL      MVTMBK00                * move temp BNK fields to real flds
          PACK      KEY12,TMBNRECN
          CALL      WRRCBNK1                * write to real file
.
          MATCH     "1",ALLINDIC
          IF        @EQUAL
            MOVE      INSCOMP,TMBDDRAW
            MOVE      INSCOMP,TMBDPAYD
          ENDIF
.
          CALL      MVTMBT00                * move temp BDT fields to real flds
          PACK      KEY15,TMBDRECN,TMBDUNIQ
          CALL      WRRCBDT1
.
.                                           * now process each Invoice record
          PACK      KEY17,SAVERECN,SP20
          CALL      RSTMDTE1
PROC3000  CALL      RKTMDTE1
          BRANCH    OVRCD,PROC5000
.
          MATCH     SAVERECN,TMDTRECN
          GOTO      PROC5000 IF NOT EQUAL
          MOVE      HRECPT,TMDTRECN
          CALL      MVTMDE00
.
.         If this is for Suspense register, blank out Inv.no, Visit and U/R no
.
          PACK      KEY3,TMDTREGI,SP3
          CALL      RDREGA1
          IF        OVRCD=0
            IF        REGIBACD=99
              MOVE      SP70,RCDTINVN
              MOVE      SP70,RCDTVIST
              MOVE      SP70,RCDTURNO
              IF        OPTION=1 | OPTION=2
                PACK      RCDTNAME,HFNAME,SP70    * health fund name
              ELSE
                PACK      RCDTNAME,INSNAME,SP70   * insurance name
              ENDIF
              PACK      RCDTADD1,TMDTINVN,SP1,TMDTVIST,SP1,TMDTURNO,SP70
              MOVE      SP70,RCDTADD2
              MOVE      SP70,RCDTADD3
              MOVE      SP70,RCDTADD4
              MOVE      SP70,RCDTPOST
            ENDIF
          ENDIF
.
          UNPACK    SP70,RCDTGLEX,RCDTRELU,RCDTSDOC,RCDTSPDT,RCDTSPTM,RCDTSPUI
          PACK      KEY17,TMDTRECN,TMDTTCNT
          CALL      WRRCDTE1
          GOTO      PROC3000
.
.
PROC5000  MOVE      ZERO,EXIT
          PRINT     *N,*20,"Receipt Number allocated  : ",TMBNRECN,*N,*N:
                    *1,"File sucessfully uploaded to Central Receipting"
          GOTO      PROC9999
.
.
PROC9000  PRINT     *N,*1,"Bank Header record not found - file has not been ":
                    "uploaded ****"
          GOTO      PROC9999
.
PROC9100  PRINT     *N,*1,"Bank Details record not found - file has not been ":
                    "uploaded ****"
          GOTO      PROC9999
.
PROC9999  RETURN
+
.******************************************************************************
.*                                  VFIL0000                                  *
.*     01 = Header record, 02 = Data record                                   *
.*  The Keyin amount must match with Total amount from all Header records     *
.*  The amount of Header record must match with Total amount of Detail records*
.******************************************************************************
VFIL0000  MOVE      ZERO,RECIN
          MOVE      ZERO,TRANNUMB           * Transaction count for Invoices
          MOVE      ZERO,TRANTAMT           * Total Amount for all Tranactions
.
          MOVE      "Fld",COLTXT
          MOVE      "99",CLNO               * force Report Header if errors
.
          MOVE      ZERO,TRANTAMT
          MOVE      ZERO,INHFAMNT
          MOVE      SP70,INSRCODE
          MOVE      ZERO,TTHFAMNT
          MOVE      ZERO,RECIN
          MOVE      ZERO,HEDIN
.
VFIL1000  READ      INHFRTXT,SEQ;HFRRECID:
                                 FLD60002,FLD60003,FLD60004,FLD60005,FLD60006:
                                 FLD60007,FLD60008,FLD60009,FLD60010,FLD60011
          GOTO      VFIL7000 IF OVER        * end of file
.
          PACK      HFRRECID,HFRRECID,SP70
          MATCH     SP70,HFRRECID
          GOTO      VFIL7000 IF EQUAL
.
          MOVE      ZERO,HFRIDNO            * identify the HF record id.
          MOVE      HFRRECID,HFRIDNO
          ADD       ONE,RECIN
.
          COMPARE   ONE,HFRIDNO
          GOTO      VFIL6000 IF NOT EQUAL
.
          MOVE      SP70,FLD60IN            * clear FLD60IN
          LENSET    FLD60IN
          MOVE      FLD60002,FLD60IN        * Fld 2 - Hospital No.
          MOVE      " 2",FLDNO
          MOVE      "10",LENGTH             * retrieve No. and right justify
          CALL      RJUS0000                * right justify
          MOVE      FLD60OUT,INHOSPNO
.
          IF        IBCNMHOS = 1
            MATCH     INHOSPNO,KIHOSPNO
            IF        !@EQUAL
              COMPARE   "56",CLNO
              CALL      HED40000 IF NOT LESS    * Print the report header
.
              PRINT     *N,*1,RECIN,*9,FLDNO:
                        *20,"Upload File Hospital No.",INHOSPNO:
                       " does not match Hospital Id. ",KIHOSPID," Hospital No.":
                        KIHOSPNO,"  *** Error ***"
              ADD       ONE,ERRIN
            ENDIF
          ENDIF
.
.         Check if the total of Details/Transaction records matches to 
.         Header record
.
          COMPARE   TRANTAMT,INHFAMNT
          IF        !@EQUAL
            COMPARE   "56",CLNO
            CALL      HED40000 IF NOT LESS    * Print the report header
.
            MOVE      "10",FLDNO
            PRINT     *N,*1,HEDIN,*9,FLDNO:
                      *20,INSRCODE,*40,"Header Total does not ":
                      "match 'Payments by Invoice' Total - ":
                      "*** Error ***"
.
            MOVE      INHFAMNT,DIM15A
            SQUEEZE   DIM15A
            PRINT     *40,"Header Total : $ ",DIM15A
.
            MOVE      TRANTAMT,DIM15A
            SQUEEZE   DIM15A
            PRINT     *40,"Payments Total : $ ",DIM15A
.
            ADD       ONE,ERRIN
          ENDIF
.
. Statement Reference - Type 01
.
VFIL5000  MOVE      "Py",CODE
          PACK      KEY5,CODE,PAYMCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VFIL5500
.
          MATCH     "4",PTCDNHCP  
          GOTO      VFIL5500 IF NOT EQUAL
.
          MOVE      FLD60009,DIM70 
          STRIP     DIM70
          MATCH     SP70,DIM70
          IF        @EQUAL | @EOS
            COMPARE   "56",CLNO
            CALL      HED40000 IF NOT LESS    * Print the report header
            MOVE      " 9",FLDNO
            PRINT     *N,*1,RECIN,*9,FLDNO:
                      *20,"Statement Reference is Mandatory ":
                      "for Record Type 01 - ":
                      "*** Error ***"
            ADD       ONE,ERRIN
          ELSE
            MOVELPTR  DIM70,F3
            IF        F3 > 40
              COMPARE   "56",CLNO
              CALL      HED40000 IF NOT LESS    * Print the report header
              MOVE      " 9",FLDNO
              PRINT     *N,*1,RECIN,*9,FLDNO:
                        *20,"Statement Reference length > 40 ":
                        "for Record Type 01 - ":
                        "*** Error ***"
              ADD       ONE,ERRIN
            ENDIF
          ENDIF
.
VFIL5500  MOVE      ZERO,INHFAMNT
          MOVE      ZERO,TRANTAMT
          MOVE      ZERO,TRANNUMB
.
.         Header record
.
          REP       "#" $ , ",FLD60010
          MOVE      ZERO,F12P2
          MOVE      FLD60010,F12P2
.
          ADD       F12P2,TTHFAMNT          * total amount of 01 records
          MOVE      F12P2,INHFAMNT          * amount for 01 records
.
          PACK      INSRCODE,FLD60011,SP30   * Insurance Code
          MOVE      RECIN,HEDIN
          GOTO      VFIL1000
.
VFIL6000  COMPARE   TWO,HFRIDNO
          GOTO      VFIL6300 IF NOT EQUAL
.
          MOVE      SP70,FLD60IN            * clear FLD60IN
          LENSET    FLD60IN
          MOVE      FLD60002,FLD60IN        * Fld 2 - Hospital No.
          MOVE      " 2",FLDNO
          MOVE      "10",LENGTH             * retrieve No. and right justify
          CALL      RJUS0000                * right justify
          MOVE      FLD60OUT,INHOSNO2
.
          IF        IBCNMHOS = 1
            MATCH     INHOSPNO,INHOSNO2
            IF        !@EQUAL
              COMPARE   "56",CLNO
              CALL      HED40000 IF NOT LESS    * Print the report header
              PRINT     *N,*1,RECIN,*9,FLDNO:
                        *20,"Hospital Id.",INHOSNO2:
                        " is not the same as Header record ":
                        "  *** Error ***"
              ADD       ONE,ERRIN
            ENDIF
          ENDIF
.
.         Validate invoice number
.
          MOVE      SP70,FLD60IN            * clear FLD60IN
          MOVE      FLD60005,FLD60IN        * Fld 5 - Invoice No.
          MOVE      " 5",FLDNO
          MOVE      "8",LENGTH
          CALL      RJUS0000                * right justify
          CALL      EDNUM000
          MOVE      FLD60OUT,ININVNUM
.
          MOVE      SP8,DPINVADM
          MOVE      SP8,PINVUR
          PACK      KEY8,ININVNUM,SP8
          CALL      RDPTINV1
          BRANCH    OVRCD,VFIL6100
.
          MOVE      " 4",FLDNO              * Fld 4 - Patient Name
          PACK      KEY8,PINVUR
          CALL      RDMAST1                 * get PMI Master
          IF        OVRCD = 1
            COMPARE   "56",CLNO
            CALL      HED40000 IF NOT LESS    * Print the report header
            PRINT     *N,*1,RECIN,*9,FLDNO:
                      *20,"PMI Master for UR ",PINVUR:
                      " not found - ":
                      "  *** Error ***"
            ADD      ONE,ERRIN
            GOTO     VFIL6100
          ENDIF
.
.         Validate surname
.
          PACK      DIM30B,PSNAME,SP70
          MOVE      ZERO,RESPOK
          PACK      KEY8,DPINVADM,SP8
          CALL      RDRESP1
          IF        OVRCD = 0
            MATCH     SP70,PKNAME
            IF        !@EQUAL
              MOVE      ONE,RESPOK
              PACK      DIM30B,PKNAME,SP70
            ENDIF
          ENDIF
          PACK      SKEY30,DIM30B,SP70
          STRIP     SKEY30
.
          PACK      DIM30A,FLD60004,SP70
          MATCH     SP70,DIM30A
          GOTO      VFIL6400 IF EQUAL   * Blank PRFA name
.
          STRIP     DIM30A
          PACK      KEY30,DIM30A,SP70   * save the original payment name
.
          REP       UPPLOW,DIM30A       * convert all chars to uppercase
          REP       UPPLOW,DIM30B
          SCAN      DIM30A,DIM30B
          IF        !@EQUAL
            REP       LOWUPP,DIM30A     * convert all chars to lowercase
            REP       LOWUPP,DIM30B
            SCAN      DIM30A,DIM30B
            IF        !@EQUAL
              RESET     DIM30B
              MOVE      " 4",FLDNO
              STRIP     DIM30B
              STRIP     KEY30
              COMPARE   "56",CLNO
              CALL      HED40000 IF NOT LESS    * Print the report header
              IF        RESPOK =1
                PRINT     *N,*1,RECIN,*9,FLDNO:
                          *20,"Surname of PRFA ",SKEY30:
                          "does not match ",KEY30:
                          "  *** Error ***"
              ELSE
                PRINT     *N,*1,RECIN,*9,FLDNO:
                          *20,"Surname of PMI ",SKEY30:
                          "does not match ",KEY30:
                          "  *** Error ***"
              ENDIF
            ENDIF
            ADD      ONE,ERRIN
          ENDIF
          GOTO      VFIL6200
.
VFIL6100  COMPARE   "56",CLNO
          CALL      HED40000 IF NOT LESS    * Print the report header
          PRINT     *N,*1,RECIN,*9,FLDNO:
                    *20,"Invoice No. ",ININVNUM:
                    " not found - ":
                    "  *** Error ***"
          ADD      ONE,ERRIN
.
VFIL6200  REP       "#" $ , ",FLD60008
          MOVE      ZERO,F12P2
          MOVE      FLD60008,F12P2
.
          ADD       F12P2,TRANTAMT          * total amount for 02 records
          ADD       ONE,TRANNUMB
.
          COMPARE   "99999",TRANNUMB
          IF        @EQUAL
            COMPARE   "56",CLNO
            CALL      HED40000 IF NOT LESS    * Print the report header
.
            PRINT     *1,HEDIN,*20,INSRCODE:
                      " *** More than 99999 Transactions - ":
                      " Central Receipting cannot handle this ***"
            ADD       ONE,ERRIN
          ENDIF
.
. Statement Reference - Type 02
.
VFIL6250  MOVE      "Py",CODE
          PACK      KEY5,CODE,PAYMCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VFIL6280
.
          MATCH     "4",PTCDNHCP
          GOTO      VFIL6280 IF NOT EQUAL
.
          MOVE      FLD60009,DIM70
          STRIP     DIM70
          MATCH     SP70,DIM70
          IF        !@EQUAL & !@EOS
            COMPARE   "56",CLNO
            CALL      HED40000 IF NOT LESS    * Print the report header
            MOVE      " 9",FLDNO
            PRINT     *N,*1,RECIN,*9,FLDNO:
                      *20,"Statement Reference must be Blank ":
                      "for Record Type 02 - ":
                      "*** Error ***"
            ADD       ONE,ERRIN
          ENDIF
.
VFIL6280  GOTO      VFIL1000
.
VFIL6300  COMPARE   "56",CLNO
          CALL      HED40000 IF NOT LESS    * Print the report header
.
          MOVE      "01",FLDNO
          PRINT     *N,*1,RECIN,*9,FLDNO:
                    *20,"Unknown Record Id. ",HFRRECID:
                    " - RECORD BYPASSED ":
                    "*** Error ***"
          ADD       ONE,ERRIN
          GOTO      VFIL1000
.
VFIL6400  COMPARE   "56",CLNO
          CALL      HED40000 IF NOT LESS    * Print the report header
.
          MOVE      " 4",FLDNO
          PRINT     *N,*1,RECIN,*9,FLDNO:
                    *20,"Payment Name is Blank ":
                    "*** Error ***"
          ADD       ONE,ERRIN
          GOTO      VFIL1000
.
VFIL7000  COMPARE   ZERO,INHFAMNT
          GOTO      VFIL8000 IF EQUAL
.
.         Check the last previous records
.
          COMPARE   TRANTAMT,INHFAMNT
          IF        !@EQUAL
            COMPARE   "56",CLNO
            CALL      HED40000 IF NOT LESS    * Print the report header
.
            MOVE      "10",FLDNO
            PRINT     *N,*1,HEDIN,*9,FLDNO:
                      *20,INSRCODE,*40,"Header Total does not ":
                      "match 'Payments by Invoice' Total - ":
                      "*** Error ***"
.
            MOVE      INHFAMNT,DIM15A
            SQUEEZE   DIM15A
            PRINT     *40,"Header Total : $ ",DIM15A
.
            MOVE      TRANTAMT,DIM15A
            SQUEEZE   DIM15A
            PRINT     *40,"Payments Total : $ ",DIM15A
.
            ADD       ONE,ERRIN
            ADD       ONE,CLNO
          ENDIF
.
.VFIL8000  COMPARE   KIAMOUNT,TTHFAMNT
.          IF        !@EQUAL
.
.            COMPARE   "56",CLNO
.            CALL      HED40000 IF NOT LESS    * Print the report header
.
.            MOVE      TTHFAMNT,DIM15A
.            SQUEEZE   DIM15A
.            PRINT     *N,*9,"Total Upload File Amount : $ ",DIM15A,SP2;
.
.            MOVE      KIAMOUNT,DIM15A
.            SQUEEZE   DIM15A
.            PRINT     "does not match expected amount of '$ : ",DIM15A:
.                      "*** Error ***",*N
.            ADD       ONE,ERRIN
.            ADD       TWO,CLNO
.          ENDIF
.
VFIL8000  IF        ERRIN > 0
            COMPARE   "56",CLNO
            CALL      HED40000 IF NOT LESS    * Print the report header
.
            PRINT     *N,*1,"Total Records read   : ",RECIN:
                      *N,*1,"Total number of erros: ",ERRIN,*N
.
            PRINT     *N,*1,"Errors have been detected ";
            IF        OPTION=6
              PRINT   *27,"- *** file will not be uploaded ***"
            ELSE
              PRINT   *27,"in Edit only run"
            ENDIF
          ENDIF
.
VFIL9999  RETURN
+
.******************************************************************************
.*                                  SINP0000                                  *
.*                         Process The Input Text File                        *
.******************************************************************************
SINP0000  MOVE      ZERO,EXIT
          MOVE      ZERO,RECIN
          MOVE      ZERO,TMRCPTNO           * File Header count
          MOVE      ZERO,MAXRCPNO
.
          OPEN      INHFRTXT,FNAMEIN        * open file
          MOVE      "Bulk Receipt Upload file",HEADDES1
          PACK      HEADDES2,FNAMEHFI,SP30
          STRIP     HEADDES2
.
          MOVE      "Fld",COLTXT
          MOVE      "99",CLNO               * force Report Header if errors
          MOVE      ZERO,TRANNUMB           * Transaction count for Invoices
          MOVE      ZERO,TRANTAMT           * Total Amount for all Tranactions
.
. ----- Read the input data file into temporary variables -----
.
SINP1000  READ      INHFRTXT,SEQ;HFRRECID;  * read Health Fund record id
          GOTO      SINP5000 IF OVER        * end of file
.
          PACK      HFRRECID,HFRRECID,SP70
          MATCH     SP70,HFRRECID
          GOTO      SINP5000 IF EQUAL
.
          MOVE      "  ",FLDNO
          ADD       ONE,RECIN
          MOVE      ZERO,HFRIDNO            * identify the HF record id.
          MOVE      HFRRECID,HFRIDNO
.
          PERFORM   HFRIDNO,S01R0000,S02R0000
          BRANCH    EXIT,SINP5200           * halt processing
.
          GOTO      SINP1000
.
.
. ----- Finished -----
.
SINP5000  MOVE      ZERO,EXIT
SINP5200  CLOSE     PRTFILE1
.
          PRINT     *N,*1,"Total Records read   : ",RECIN,*N:
                       *1,"Records processed    : ",REC00,*N
.
.         MOVE      ZERO,CPAGENO
          MOVE      ZERO,INVCOUNT
          CALL      HED20000
.
          MOVE      SP70,INSRCODE
          OPEN      PRTFILE1,TMPPRTFL
SINP6200  READ      PRTFILE1,SEQ;PRTLINE,PRTLIN1
          GOTO      SINP7000 IF OVER
.
          UNPACK    PRTLIN1,KEY22,INSCOMP
.
          MATCH     INSRCODE,INSCOMP
          IF        !@EQUAL
            PRINT     *N,*1,"Insurance Code : ",INSCOMP,*N;
            MOVE      INSCOMP,INSRCODE        * Save insurance company
          ENDIF
.
          ADD       ONE,INVCOUNT
          PRINT     *1,PRTLINE
          ADD       ONE,CLNO
          COMPARE   "56",CLNO
          CALL      HED20000 IF NOT LESS    * Print the report header
          GOTO      SINP6200
.
SINP7000  CLOSE     PRTFILE1
          PRINT     *N,*1,"End of File"
          COMPARE   "45",CLNO
          CALL      HED20000 IF NOT LESS    * Print the report header
.
          MOVE      INVCOUNT,DIM5A
          SQUEEZE   DIM5A
          PRINT     *N,*20,"Invoices processed        : ",DIM5A
          MOVE      TRANTAMT,DIM15A
          SQUEEZE   DIM15A
          PRINT     *N,*20,"Payments by Invoice Total : $ ",DIM15A
.
SINP9999  RETURN
+
.******************************************************************************
.*                                  S01R0000                                  *
.*                            Process "01" Record (NZ private)                *
.******************************************************************************
S01R0000  MOVE      ZERO,EXIT
          MOVE      ZERO,TRANNUMB           * Transaction count
.
          READ      INHFRTXT,SEQ;FLD60002,FLD60003,FLD60004,FLD60005,FLD60006:
                                 FLD60007,FLD60008,FLD60009,FLD60010,FLD60011
          ADD       ONE,REC00
.
          PACK      ERRDATA,HFRRECID,SP1,FLD60002,SP1,FLD60003,SP1:
                            FLD60004,SP1,FLD60005,SP1,FLD60006,SP70
.
.
          PACK      INHOSNAM,FLD60003,SP30
          PACK      INHOSAD1,FLD60004,SP30
          PACK      INHOSAD2,FLD60005,SP30
          PACK      INHOSAD3,FLD60006,SP30
          PACK      INHOSAD4,FLD60007,SP30
          PACK      INHFDATE,FLD60008,SP30  * Health Fund Date of payment
          PACK      INSCOMP,FLD60011,SP30   * Insurance Code

          PACK      KEY6,INSCOMP
          CALL      RDINSR1
          IF        OVRCD=0
            MOVE      INAME,INSNAME
          ENDIF
.
S01R2000  MOVE      "10",FLDNO
          REP       "#" $ , ",FLD60010
          MOVE      ZERO,F12P2
          MOVE      FLD60010,INHFAMNT
.
.         set up Receipting Bank Header
.
S01R5000  MOVE      ZERO,EXIT
          CALL      CLTMPBNK
          CALL      CLTMPBDT
.
          ADD       ONE,TMRCPTNO
          MOVE      TMRCPTNO,TMBNRECN
...          MOVE      KIPAYDAT,TMBNTDAT
          MOVE      CDATEIS,TMBNTDAT
          MOVE      INHFAMNT,TMBNTOTP
          MOVE      KICASHID,TMBNCDRW
          IF        IBCNMHOS = 1
            MOVE      KIHOSPID,TMBNMHOS
            MOVE      CHOSPNUM,TMBNMDHS
.
          ENDIF
.
          MOVE      "2",TMBNTTYP            * received from "Insurance"
          PACK      TMBNRCOD,INSCOMP,SP70   * Insurance company
.
          MOVE      "0",TMBNSTAT
          MOVE      KICHQACC,TMBNCHQA
.
          PACK      TMBNCDAT,CCC,CYY,CMM,CDD,SP10
          REP       " 0",TMBNCDAT
          CLOCK     TIME,TMBNCTIM
          MOVE      KIWEBUID,TMBNCUID
.
.         write the record
.
          PACK      KEY12,TMBNRECN
          MOVE      TMBNRECN,MAXRCPNO        * Maximum temporary receipt number
          CALL      RATMBNK1
          IF        OVRCD = 1
            CALL      WRTMBNK1
          ENDIF
.
.         set up Receipting Bank Detail Recd
.
S01R6000  MOVE      ZERO,EXIT
          MOVE      TMBNTDAT,TMBDTDAT
          MOVE      TMBNRECN,TMBDRECN
          MOVE      "  1",TMBDUNIQ
          MOVE      TMBNMHOS,TMBDMHOS
          MOVE      TMBNMDHS,TMBDMDHS
          SQUEEZE   KIPAYTYP
          MOVE      KIPAYTYP,TMBDPTYP
          MOVE      PAYMCODE,TMBDPCOD
          MOVE      TMBNTOTP,TMBDPAMT
          IF        KIPAYTPE = 2
            MOVE      KICHQNUM,TMBDCHQN
            MOVE      INSCOMP,TMBDDRAW
            MOVE      KIBNKNAM,TMBDBANK
            MOVE      KIBNKBCH,TMBDBRCH
          ENDIF
          IF        KIPAYTPE = 4
            MOVE      KISTTREF,TMBDSTRF
            MOVE      INSCOMP,TMBDPAYD      * Insurance company
            IF        OPTION=5 | OPTION=6
              MOVE      FLD60009,TMBDSTRF 
            ENDIF 
          ENDIF
          MOVE      TMBNCDAT,TMBDCDAT
          MOVE      TMBNCTIM,TMBDCTIM
          MOVE      TMBNCUID,TMBDCUID
.
          PACK      KEY15,TMBDRECN,TMBDUNIQ,SP70
          CALL      WRTMBDT1
.
S01R9999  RETURN
+
.******************************************************************************
.*                                  S02R0000                                  *
.*                            Process "02" Record (NZ private)                *
.******************************************************************************
S02R0000  MOVE      ZERO,EXIT
          MOVE      ZERO,SUSPEND
.
          READ      INHFRTXT,SEQ;FLD60002,FLD60003,FLD60004,FLD60005,FLD60006:
                                 FLD60007,FLD60008,FLD60009,FLD60010,FLD60011:
                                 FLD60012,FLD60013,FLD60014,FLD60015,FLD60016:
                                 FLD60017
          ADD       ONE,REC00
.
          PACK      ERRDATA,HFRRECID,SP1,FLD60002,SP1,FLD60003,SP1:
                            FLD60004,SP1,FLD60005,SP1,FLD60006,SP70
.
          CALL      CLPATINV
          CALL      CLPATMAS
          CALL      CLPATVIS
          CALL      CLPMSVX1
          MOVE      ZERO,IBALANCE
.
          MOVE      FLD60003,INHOSREF
          MOVE      FLD60004,INPAYNAM       * Fld 4 - HFund Reference Name
.
          MOVE      SP70,FLD60IN            * clear FLD60IN
          MOVE      FLD60005,FLD60IN        * Fld 5 - Invoice No.
          MOVE      " 5",FLDNO
          MOVE      "8",LENGTH
          CALL      RJUS0000                * right justify
          CALL      EDNUM000
          MOVE      FLD60OUT,ININVNUM
.
          MOVE      ZERO,RESPOK
.
          MOVE      SP8,DPINVADM
          MOVE      SP8,PINVUR
          PACK      KEY8,ININVNUM,SP8
          CALL      RDPTINV1
          BRANCH    OVRCD,S02R9999
.
          MATCH     "1",PTINCRST
          IF        @EQUAL
            MOVE      ONE,SUSPEND
          ENDIF
.
          MOVE      PINVAMT,IBALANCE        * Invoice Amount
          ADD       PINVPATA,IBALANCE       * minus Amount paid by Patient
          ADD       PINVHFDA,IBALANCE       * minus Amount paid by Health Fund
          ADD       PINVOTHA,IBALANCE       * minus Other Amounts paid
          ADD       PINVJOUR,IBALANCE       * plus Journals
          ADD       PTINGSTJ,IBALANCE       * plus GST Adjustments
          ADD       PTINCRTT,IBALANCE
.
.         get Responsible Person recd
.
          PACK      DIM30B,PSNAME,SP70
          MOVE      ZERO,RESPOK
          PACK      KEY8,DPINVADM,SP8
          CALL      RDRESP1
          IF        OVRCD = 0
            MOVE      ONE,RESPOK
            PACK      DIM30B,PKNAME,SP70
          ENDIF
.
          PACK      DIM30A,INPAYNAM,SP70
          SQUEEZE   DIM30A

          CALL      PCLM0000             * Get patient's details
.
S02R4000  MOVE      " 8",FLDNO
          REP       "#" $ , ",FLD60008
          MOVE      FLD60008,INVPAID        * payment
.
.         set up Receipt Details
          MOVE      ZERO,EXIT
          CALL      CLTMPDTE
.
          MOVE      TMBNTDAT,TMDTTDAT
          MOVE      TMBNRECN,TMDTRECN
.
          ADD       ONE,TRANNUMB
          MOVE      TRANNUMB,TMDTTCNT
.
          PACK      TMDTINVN,SP4,ININVNUM   * change Inv.No. from 8 to 12
          MOVE      DPINVADM,TMDTVIST
          MOVE      PINVUR,TMDTURNO
.
          MOVE      INPAYNAM,TMDTNAME
          MOVE      SP70,TMDTSFLG
.
          PACK      KEY8,DPINVADM,SP8
          CALL      RDRESP1
          IF        RESPOK = 1
            MOVE      INPAYNAM,TMDTNAME     * INPAYNAM (HF input Name)
            MOVE      INPAYNAM,TMDTREFR
            MOVE      PKADD1,TMDTADD1
            MOVE      PKADD2,TMDTADD2
            MOVE      PKSUBR,TMDTADD3
            MOVE      PKADD4,TMDTADD4
            MOVE      PKPOST,TMDTPOST
          ELSE
            MOVE      SP1,DIM1A
            MOVE      PGNAME,DIM1A
            PACK      DIM30A,PTITL,SP1,DIM1A,DOT,PSNAME
            SQUEEZE   DIM30A
            PACK      TMDTNAME,DIM30A,SP70
            MOVE      PADD1,TMDTADD1
            MOVE      PADD2,TMDTADD2
            MOVE      PSUBRB,TMDTADD3
            MOVE      PADD4,TMDTADD4
            MOVE      PPOST,TMDTPOST
          ENDIF
.                                           * get Account info. for INCA & BNKA
.                                           * selected in GTACCT00
          MOVE      KIREGCOD,TMDTREGI
          MOVE      SAVTINCA,TMDTINCA
          MOVE      SAVTBNKA,TMDTBNKA
.
          IF        SUSPEND = 1
            MATCH     SP70,RCCNURCP
            IF        !@EQUAL
              MOVE      RCCNURCP,TMDTREGI     * Suspend register
              MOVE      SAVSINCA,TMDTINCA
              MOVE      SAVSBNKA,TMDTBNKA
            ENDIF
          ENDIF
.
          ADD       INVPAID,TRANTAMT        * add Invoice amount to Total
          MOVE      INVPAID,TMDTAMNT
.                                           * Calc Outstanding Balance
          MOVE      ZERO,TMDTDISC
          MOVE      TMBNMHOS,TMDTMHOS

          MOVE      TMBNMDHS,TMDTMDHS
          MOVE      "0",TMDTALLO
          PACK      KEY3,TMDTREGI,SP3
          CALL      RDREGA1
          IF        OVRCD=0
            IF        REGIBACD=99
              MOVE      "2",TMDTALLO        * Suspense register
            ENDIF
          ENDIF
.
          MOVE      TMBNCDAT,TMDTCDAT
          MOVE      TMBNCTIM,TMDTCTIM
          MOVE      TMBNCUID,TMDTCUID
.
.         * write the records
.
          MOVE      REGDESC,DIM30A
          MOVE      SP70,KEY22
          IF        SUSPEND = 1
            MOVE    SAVSDESC,DIM30A
            MOVE    "Invoice Fully Credited",KEY22
          ENDIF
          PACK      DIM30B,INPAYNAM,SP30
          PACK      PRTLINE,DIM30A,SP2,TMDTINVN,SP4,TMDTURNO,SP4,DIM30B:
                    SP4,IBALANCE,SP4,INVPAID,SP2,SP70
          PACK      PRTLIN1,KEY22,INSCOMP,SP70
.
          WRITE     PRTFILE1,SEQ;PRTLINE,PRTLIN1
.
          PACK      KEY17,TMDTRECN,TMDTTCNT,SP70
          CALL      WRTMDTE1
          GOTO      S02R9999
S02R9999  RETURN
+
.******************************************************************************
.*                                  SPRC0000 (NZ private)                     *
.*                          Process Temp file to RCP files                    *
.******************************************************************************
SPRC0000  MOVE      ZERO,EXIT
          MOVE      SP15,SAVERECN
.                                           * read temp Bank Header
          MOVE      ZERO,FORM12
.
SPRC0200  ADD       ONE,FORM12
          COMPARE   FORM12,MAXRCPNO
          GOTO      SPRC9000 IF LESS
.
          MOVE      FORM12,KEY12
          CALL      RDTMBNK1
          BRANCH    OVRCD,SPRC9100
.                                           * read temp Bank Details record
          MOVE      "  1",KEY3
          PACK      KEY15,KEY12,KEY3
          CALL      RDTMBDT1
          BRANCH    OVRCD,SPRC9100
.                                           * get next Receipt Number
SPRC1000  READ      CONTROLF,THIRTY6;*193,HRECPT
          ADD       ONE,HRECPT
          WRITAB    CONTROLF,THIRTY6;*193,HRECPT
          SUB       ONE,HRECPT
.
          MOVE      HRECPT,KEY12
          CALL      RDRCBNK1
          COMPARE   ZERO,OVRCD
          GOTO      SPRC1000 IF EQUAL
.
.         write the RCPBNK & RCPBDT records
.
          MOVE      TMBNRECN,SAVERECN
          MOVE      HRECPT,TMBNRECN
          MOVE      HRECPT,TMBDRECN
.
          CALL      MVTMBK00                * move temp BNK fields to real flds
          PACK      KEY12,TMBNRECN
          CALL      WRRCBNK1                * write to real file
.
          CALL      MVTMBT00                * move temp BDT fields to real flds
          PACK      KEY15,TMBDRECN,TMBDUNIQ
          CALL      WRRCBDT1
.                                           * now SPRCess each Invoice record
          PACK      KEY17,SAVERECN,SP20
          CALL      RSTMDTE1
SPRC3000  CALL      RKTMDTE1
          BRANCH    OVRCD,SPRC0200
.
          MATCH     SAVERECN,TMDTRECN
          GOTO      SPRC0200 IF NOT EQUAL
          MOVE      HRECPT,TMDTRECN
          CALL      MVTMDE00
.
.         If this is for Suspense register, blank out Inv.no, Visit and U/R no
.
          PACK      KEY3,TMDTREGI,SP3
          CALL      RDREGA1
          IF        OVRCD=0
            IF        REGIBACD=99
              MOVE      SP70,RCDTINVN
              MOVE      SP70,RCDTVIST
              MOVE      SP70,RCDTURNO
              IF        OPTION=1 | OPTION=2
                PACK      RCDTNAME,HFNAME,SP70    * health fund name
              ELSE
                PACK      RCDTNAME,INSNAME,SP70   * insurance name
              ENDIF
              PACK      RCDTADD1,TMDTINVN,SP1,TMDTVIST,SP1,TMDTURNO,SP70
              MOVE      SP70,RCDTADD2
              MOVE      SP70,RCDTADD3
              MOVE      SP70,RCDTADD4
              MOVE      SP70,RCDTPOST
            ENDIF
          ENDIF
.
          UNPACK    SP70,RCDTGLEX,RCDTRELU,RCDTSDOC,RCDTSPDT,RCDTSPTM,RCDTSPUI
          PACK      KEY17,TMDTRECN,TMDTTCNT
          CALL      WRRCDTE1
          GOTO      SPRC3000
.
SPRC9000  MOVE      ZERO,EXIT
          PRINT     *N,*1,"File sucessfully uploaded to Central Receipting"
          GOTO      SPRC9999
.
SPRC9100  PRINT     *N,*1,"Temporary file not found - file has not been ":
                    "uploaded ****"
          GOTO      SPRC9999
.
SPRC9999  RETURN
+
.******************************************************************************
.*                                  RJUS0000                                  *
.*                         Right Justify a field                              *
.******************************************************************************
RJUS0000  MOVE      SP70,FLD60OUT
          SQUEEZE   FLD60IN
          MOVELPTR  FLD60IN,LENIN
          MOVE      LENGTH,LENOUT
          SUB       LENIN,LENOUT
          IF        LENOUT > 0
            SUB       ONE,LENOUT
            BUMP      FLD60OUT,LENOUT
            APPEND    FLD60IN,FLD60OUT
          ELSE
            MOVE      FLD60IN,FLD60OUT
          ENDIF
          RESET     FLD60OUT
.
RJUS9999  RETURN
.
.******************************************************************************
.*                                  EDNUM000                                  *
.*                              Edit Numeric field                            *
.******************************************************************************
EDNUM000  TYPE      FLD60OUT 
          GOTO      EDNUM999 IF EQUAL
.
EDNUM900  CLEAR     ERRMESS
          APPEND    "Numeric field '",ERRMESS
          APPEND    FLD60OUT,ERRMESS
          APPEND    "' is invalid",ERRMESS
          CALL      PRNT0000
          GOTO      EDNUM999
.
EDNUM999  RETURN
+
.******************************************************************************
.*                                  HEAD0000                                  *
.*                          Print error Report Header                         *
.******************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          MOVE      "- Keyin data",CPHDROPT
          CALL      HEAD132                 * Print the report header
          PRINT     *N;
          MOVE      ZERO,CLNO
HEAD9999  RETURN
.******************************************************************************
.*                                  HED10000                                  *
.*                          Print error Report Header                         *
.******************************************************************************
HED10000  MOVE      ONE,CNOUNDLN
          MOVE      "- Error report",CPHDROPT
          CALL      HEAD132                 * Print the report header
          PRINT     *9,HEADDES1,*40,HEADDES2,*N,*N:
                    *1,"Input data string",*42,"Rec.No.",*50,COLTXT:
                    *55,"Error",*N:
.                       ....*....1....*....2....*....3....*....4....*....5
                    *1,"------------------------------------     ------- -":
                       "--  ----------------------------------------------":
                       "------------------------"
          MOVE      ZERO,CLNO
HED19999  RETURN
+
.******************************************************************************
.*                                  HED20000                                  *
.*                          Print detail Report Header                        *
.******************************************************************************
HED20000  MOVE      ONE,CNOUNDLN
          MOVE      "- Detail report",CPHDROPT
          SQUEEZE   INHOSNAM
          SQUEEZE   INHOSAD1
          CALL      HEAD132                 * Print the report header
.
          BRANCH    OPTION,HED22000,HED22000,HED22000,HED22000
          GOTO      HED24000
.
HED22000  IF        OPTION=1 | OPTION=2
            PRINT     *1,"Remittance File from : ",HFNAME,*N;
          ELSE
            PRINT     *1,"Remittance File from : ",INSNAME,*N;
          ENDIF
.
          PRINT     *1,"                To   : ",INHOSNAM,SP1,INHOSAD1,*N
.
HED24000  PRINT     *N,*1,"Register",*34,"Invoice No.",*54,"U/R",*60,"Reference":
                    *103,"Balance",*117,"Payment Amt."
.
          PRINT     *1,"========",*34,"===========":
                    *54,"===",*60,"=========":
                    *103,"=======",*117,"============"
          ADD       SIX,CLNO
HED29999  RETURN
+
.******************************************************************************
.*                                  PRNT0000                                  *
 *                            Print An Error Report                           *
.******************************************************************************
PRNT0000  COMPARE   "56",CLNO
          CALL      HED10000 IF NOT LESS    * Print the report header
.
          RESET     ERRDATA
          RESET     ERRMESS
          PRINT     *1,ERRDATA,*43,RECIN,*51,FLDNO,*55,ERRMESS
.
          ADD       ONE,CLNO
          ADD       ONE,ERRIN
          PACK      ERRDATA,SP70
          PACK      ERRMESS,SP70
.
PRNT9999  RETURN
+
.******************************************************************************
.*                                  PRLN0000                                  *
 *                            Print a message line                            *
.******************************************************************************
PRLN0000  COMPARE   "56",CLNO
          CALL      HED10000 IF NOT LESS    * Print the report header
.
          RESET     ERRMESS
          PRINT     *55,ERRMESS
.
          ADD       ONE,CLNO
          PACK      ERRMESS,SP70
.
PRLN9999  RETURN
+
.******************************************************************************
.*                                  CLHBR000                                  *
.*                           Clear Input variables                            *
.******************************************************************************
CLHBR000  MOVE      SP70,FLD60002
          MOVE      SP70,FLD60003
          MOVE      SP70,FLD60004
          MOVE      SP70,FLD60005
          MOVE      SP70,FLD60006
          MOVE      SP70,FLD60007
          MOVE      SP70,FLD60008
          MOVE      SP70,FLD60009
          MOVE      SP70,FLD60010
          MOVE      SP70,FLD60011
          MOVE      SP70,FLD60012
          MOVE      SP70,FLD60013
          MOVE      SP70,FLD60014
          MOVE      SP70,FLD60015
          MOVE      SP70,FLD60016
          MOVE      SP70,FLD60017
          MOVE      SP70,FLD60018
          MOVE      SP70,FLD60019
          MOVE      SP70,FLD60020
          MOVE      SP70,FLD60021
.
CLHBR999  RETURN
+
CLTMPBNK  MOVE      SP70,TMBNRECN
          MOVE      SP70,TMBNTDAT
          MOVE      SP70,TMBNCDRW
          MOVE      SP70,TMBNMHOS
          MOVE      SP70,TMBNMDHS
          MOVE      SP70,TMBNTTYP
          MOVE      SP70,TMBNRCOD
          MOVE      SP70,TMBNSTAT
          MOVE      SP70,TMBNBDAT
          MOVE      SP70,TMBNBTIM
          MOVE      SP70,TMBNRDAT
          MOVE      SP70,TMBNRTIM
          MOVE      SP70,TMBNCHQA
          MOVE      SP70,TMBNCDAT
          MOVE      SP70,TMBNCTIM
          MOVE      SP70,TMBNCUID
          MOVE      SP70,TMBNUDAT
          MOVE      SP70,TMBNUTIM
          MOVE      SP70,TMBNUUID
          MOVE      SP70,TMBNPRIN
          MOVE      SP70,TMBNPRNT
          MOVE      SP70,TMBNSPAR
          MOVE      ZERO,TMBNTOTP
.
CLTMBN99  RETURN
.
.
CLTMPBDT  MOVE      SP70,TMBDTDAT
          MOVE      SP70,TMBDRECN
          MOVE      SP70,TMBDUNIQ
          MOVE      SP70,TMBDMHOS
          MOVE      SP70,TMBDMDHS
          MOVE      SP70,TMBDPTYP
          MOVE      SP70,TMBDPCOD
          MOVE      SP70,TMBDPAYD
          MOVE      SP70,TMBDCHQN
          MOVE      SP70,TMBDDRAW
          MOVE      SP70,TMBDBANK
          MOVE      SP70,TMBDBRCH
          MOVE      SP70,TMBDCRDT
          MOVE      SP70,TMBDSTRF
          MOVE      SP70,TMBDEFTT
          MOVE      SP70,TMBDCDAT
          MOVE      SP70,TMBDCTIM
          MOVE      SP70,TMBDCUID
          MOVE      SP70,TMBDUDAT
          MOVE      SP70,TMBDUTIM
          MOVE      SP70,TMBDUUID
          MOVE      SP70,TMBDSPAR
          MOVE      ZERO,TMBDPAMT
.
CLTMBD99  RETURN
.
.
CLTMPDTE  MOVE      SP70,TMDTTDAT
          MOVE      SP70,TMDTRECN
          MOVE      SP70,TMDTTCNT
          MOVE      SP70,TMDTREGI
          MOVE      SP70,TMDTINVN
          MOVE      SP70,TMDTVIST
          MOVE      SP70,TMDTURNO
          MOVE      SP70,TMDTNAME
          MOVE      SP70,TMDTADD1
          MOVE      SP70,TMDTADD2
          MOVE      SP70,TMDTADD3
          MOVE      SP70,TMDTADD4
          MOVE      SP70,TMDTPOST
          MOVE      SP70,TMDTSFLG
          MOVE      SP70,TMDTREFR
          MOVE      SP70,TMDTINCA
          MOVE      SP70,TMDTBNKA
          MOVE      SP70,TMDTMHOS
          MOVE      SP70,TMDTMDHS
          MOVE      SP70,TMDTGSTC
          MOVE      SP70,TMDTGSTA
          MOVE      SP70,TMDTALLO
          MOVE      SP70,TMDTRELD
          MOVE      SP70,TMDTRELT
          MOVE      SP70,TMDTPRAC
          MOVE      SP70,TMDTCDAT
          MOVE      SP70,TMDTCTIM
          MOVE      SP70,TMDTCUID
          MOVE      SP70,TMDTUDAT
          MOVE      SP70,TMDTUTIM
          MOVE      SP70,TMDTUUID
          MOVE      SP70,TMDTDPTY
          MOVE      SP70,TMDTPRIN
          MOVE      SP70,TMDTPRNT
          MOVE      SP70,TMDTSPAR
          MOVE      ZERO,TMDTAMNT
          MOVE      ZERO,TMDTDISC
          MOVE      ZERO,TMDTTAMT
.
CLTMDT99  RETURN
.------------------------------------------------------------
.  Move Temporary bank header fields to bank header fields
.------------------------------------------------------------
MVTMBK00  MOVE      TMBNRECN,RCBNRECN
          MOVE      TMBNTDAT,RCBNTDAT
          MOVE      TMBNTOTP,RCBNTOTP
          MOVE      TMBNCDRW,RCBNCDRW
          MOVE      TMBNMHOS,RCBNMHOS
          MOVE      TMBNMDHS,RCBNMDHS
          MOVE      TMBNTTYP,RCBNTTYP
          MOVE      TMBNRCOD,RCBNRCOD
          MOVE      TMBNSTAT,RCBNSTAT
          MOVE      TMBNBDAT,RCBNBDAT
          MOVE      TMBNBTIM,RCBNBTIM
          MOVE      TMBNRDAT,RCBNRDAT
          MOVE      TMBNRTIM,RCBNRTIM
          MOVE      TMBNCHQA,RCBNCHQA
          MOVE      TMBNCDAT,RCBNCDAT
          MOVE      TMBNCTIM,RCBNCTIM
          MOVE      TMBNCUID,RCBNCUID
          MOVE      TMBNUDAT,RCBNUDAT
          MOVE      TMBNUTIM,RCBNUTIM
          MOVE      TMBNUUID,RCBNUUID
          MOVE      SP70,RCBNSPAR
.
MVTMBK99  RETURN
.
.------------------------------------------------------------
.  Move Temporary bank detail fields to bank details fields
.------------------------------------------------------------
MVTMBT00  MOVE      TMBDTDAT,RCBDTDAT
          MOVE      TMBDRECN,RCBDRECN
          MOVE      TMBDUNIQ,RCBDUNIQ
          MOVE      TMBDMHOS,RCBDMHOS
          MOVE      TMBDMDHS,RCBDMDHS
          MOVE      TMBDPTYP,RCBDPTYP
          MOVE      TMBDPCOD,RCBDPCOD
          MOVE      TMBDPAMT,RCBDPAMT
          MOVE      TMBDPAYD,RCBDPAYD
          MOVE      TMBDCHQN,RCBDCHQN
          MOVE      TMBDDRAW,RCBDDRAW
          MOVE      TMBDBANK,RCBDBANK
          MOVE      TMBDBRCH,RCBDBRCH
          MOVE      TMBDCRDT,RCBDCRDT
          MOVE      TMBDSTRF,RCBDSTRF
          MOVE      TMBDEFTT,RCBDEFTT
          MOVE      TMBDCDAT,RCBDCDAT
          MOVE      TMBDCTIM,RCBDCTIM
          MOVE      TMBDCUID,RCBDCUID
          MOVE      TMBDUDAT,RCBDUDAT
          MOVE      TMBDUTIM,RCBDUTIM
          MOVE      TMBDUUID,RCBDUUID
          MOVE      SP70,RCBDSPAR
.
MVTMBT99  RETURN
.
.--------------------------------------------------------------------
.  Move Temporary receipt detail fields to Real receipt detail fields
.--------------------------------------------------------------------
MVTMDE00  MOVE      TMDTINVN,RCDTINVN
          MOVE      TMDTVIST,RCDTVIST
          MOVE      TMDTURNO,RCDTURNO
.
          MOVE      TMDTTDAT,RCDTTDAT
          MOVE      TMDTRECN,RCDTRECN
          MOVE      TMDTTCNT,RCDTTCNT
          MOVE      TMDTREGI,RCDTREGI
.
.
          MOVE      TMDTNAME,RCDTNAME
          MOVE      TMDTADD1,RCDTADD1
          MOVE      TMDTADD2,RCDTADD2
          MOVE      TMDTADD3,RCDTADD3
          MOVE      TMDTADD4,RCDTADD4
          MOVE      TMDTPOST,RCDTPOST
          MOVE      TMDTSFLG,RCDTSFLG
          MOVE      TMDTREFR,RCDTREFR
          MOVE      TMDTINCA,RCDTINCA
          MOVE      TMDTBNKA,RCDTBNKA
          MOVE      TMDTAMNT,RCDTAMNT
          MOVE      TMDTDISC,RCDTDISC
          MOVE      TMDTMHOS,RCDTMHOS
          MOVE      TMDTMDHS,RCDTMDHS
          MOVE      TMDTGSTC,RCDTGSTC
          MOVE      TMDTGSTA,RCDTGSTA
          MOVE      TMDTALLO,RCDTALLO
          MOVE      TMDTRELD,RCDTRELD
          MOVE      TMDTRELT,RCDTRELT
          MOVE      TMDTCDAT,RCDTCDAT
          MOVE      TMDTPRAC,RCDTPRAC
          MOVE      TMDTCTIM,RCDTCTIM
          MOVE      TMDTCUID,RCDTCUID
          MOVE      TMDTUDAT,RCDTUDAT
          MOVE      TMDTUTIM,RCDTUTIM
          MOVE      TMDTUUID,RCDTUUID
          MOVE      TMDTDPTY,RCDTDPTY
          MOVE      SP70,RCDTSPAR
.
MVTMDE99  RETURN
.
.--------------------------------------------------------------------
.  Print ABF Error Report
.--------------------------------------------------------------------
ABFREP00  MATCH     "1",PTCNUABF
          GOTO      ABFREP99 IF NOT EQUAL
.
          MOVE      ZERO,INVCOUNT
          CALL      HED30000
.
          PACK      KEY17,HRECPT,SP20
          CALL      RSRCDTE1
ABFREP10  CALL      RKRCDTE1
          BRANCH    OVRCD,ABFREP90
.
          MOVE      HRECPT,DIM12B
.
          MATCH     DIM12B,RCDTRECN
          GOTO      ABFREP90 IF NOT EQUAL
.
          MOVE      SP70,ACLAIM
          PACK      KEY8,RCDTVIST,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,ABFREP50
.
          MOVE      SP70,TDESC
          PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     "A",TCINDC2
            GOTO      ABFREP10 IF EQUAL
          ENDIF
.
ABFREP50  PRINT     *2,RCDTRECN,*16,RCDTINVN,*35,RCDTVIST:
                    *45,ACLAIM," ",TDESC
.
          GOTO      ABFREP10
.
ABFREP90  PRINT     *N,*1,"End of File"
          COMPARE   "45",CLNO
          CALL      HED30000 IF NOT LESS    * Print the report header
.
ABFREP99  RETURN
.
.******************************************************************************
.*                                  HED30000                                  *
.*                          Print detail Report Header                        *
.******************************************************************************
HED30000  MOVE      ONE,CNOUNDLN
          MOVE      "- ABF Exception Rep",CPHDROPT
.
          PRINT     *N,*N
.
          CALL      HEAD132                 * Print the report header
.
          PRINT     *1,"Receipt No.",*15,"Invoice No.",*30,"Adm No.":
                    *45,"Claim Type Code"
.
          PRINT     *N,*1,"=============",*15,"=============":
                    *30,"=============",*45,"========================"
          ADD       FOUR,CLNO
HED39999  RETURN
+
.******************************************************************************
.*                                  HED40000                                  *
.*                          Print error Report Header                         *
.******************************************************************************
HED40000  MOVE      ONE,CNOUNDLN
          MOVE      "- Error report",CPHDROPT
          CALL      HEAD132                 * Print the report header
.
          PRINT     *9,HEADDES1,*40,HEADDES2,*N,*N:
                    *1,"Rec.No.",*9,COLTXT:
                    *15,"Insurance Code",*40,"Error",*N:
                    *1,"-------",*9,"---":
                    *15,"--------------":
                    *40,"---------------------------------------"
          MOVE      ZERO,CLNO
HED49999  RETURN
+

.
.==============================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       KEYINCOD
          INC       PATDSCOD
          INC       STDHLPCD
          INC       RCPREGKY
          INC       RCPCIDDS
          INC       FMSCHQKY
...       INC       RCPBNKTM
...       INC       RCPBDTTM
...       INC       RCPDTETM
.
...       INC       CLPATDSC
...       INC       CLPATMIS
          INC       CLPATINV
          INC       CLPATMAS
          INC       CLPATVIS
          INC       CLPMSVX1
.
          INC       FMSCHQIO/INC            * FMS Cheque Acct Master File
          INC       FMSCSAIO/INC            * FMS Selected Control Accounts
          INC       FMSLMAIO/INC            * FMS Ledger Master Details
          INC       PATCODIO/INC
          INC       PATFN1IO/INC            * Health Fund Table
          INC       PATMA1IO/INC            * PMI Master
          INC       PATHOSIO/INC            * Hospital Details 
          INC       PATHSPIO/INC            * Hospital Details 
          INC       PATIN1IO/INC            * Insurance file
          INC       PATINVIO/INC            * Patient Invoice file
          INC       PATVISIO/INC            * Patient Visit Details
          INC       PMSVX1IO/INC            * Patient Visit Extension
          INC       PATMI1IO/INC            * Patient Admission Details
...       INC       PATDSCIO/INC            * Patient Discharge details
          INC       PATRE1IO/INC            * Patient Responsible Person
          INC       RCPCIDIO/INC            * Cashier ID file
          INC       RCPBNKIO/INC            * Receipting Bank Header file
          INC       RCPBDTIO/INC            * Receipting Bank Detail file
          INC       RCPDTEIO/INC            * Receipting Receipt Details file
          INC       RCPREGIO/INC            * Receipting Register Details     
          INC       TMPBNKIO/INC            * RCP Temp Bank Header file
          INC       TMPBDTIO/INC            * RCP Temp Bank Detail file
          INC       TMPDTEIO/INC            * RCP Temp Receipt Details file
...       INC       WEBSECIO/INC            * 
.
          INC       AAEDE1IO/INC
          INC       OUTBB1IO/INC
          INC       OUTSITIO/INC
+

. ********************************************************** 
. *                                                        *
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     Program Name  :   IBAHOS43                         *
. *                                                        *
. *     Function      :   Invoice Range Register           *
. *                                                        *
. *     Modifications : V5.01.01 05/07/89 K.Peak           * 
. *                        Changed Adm & U/R to 8 character* 
. *                   : V5.01.02 21/05/91 Steve O'Gorman   * 
. *                        Fixed Errors from New Compiler  * 
. *                                                        *
. **********************************************************
+
          INC       STD001FD
          INC       PATIRNFD/INC
          INC       PATCODFD/INC
.
ACCNO    DIM       12
.
CHG      FORM      1
CODE     DIM       1
.
DSH100   INIT      "--------------------------------------------------":
                   "--------------------------------------------------"
DSH30    INIT      "------------------------------"
.
FLAG     DIM       1
FIELD    FORM      1
.
IBADSC   DIM       26
IBACD    FORM      2
.
LINCNT   FORM      "60"
LEDNO    DIM       2
.
MODE     DIM       1
MODE1    DIM       1
.
PAGENO   FORM      3
.
SRDIM20  DIM      20
SRDIM20A DIM      20
SUNLED   DIM      3
.
TIME     DIM      8
.
.
PRGID    INIT    "IBAHOS43"
PRGNAM    INIT      "Invoice Range Register"
VERSION   INIT      "V12.01.00"
+
START    MOVE          ZERO,PAGENO
         CALL          DISPHEAD
.
         DISPLAY       *P56:24,"Opening patirnge";
         OPEN          PATIRNG1,"patirnge"
.
         DISPLAY       *P64:24,"controlf";
         OPEN          CONTROLF,"controlf"
.
START1   DISPLAY   *P1:24,*EL;
         KEYIN     *P1:10,"This program will list all Invoice Ranges":
                   *P1:11,"Do you want to continue (",*V2LON,"Y",*HOFF:
                   "/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS
.
         CMATCH    ANSN,ANS
         GOTO      ENDIT IF EQUAL
.
         CMATCH    ANSY,ANS
         GOTO      START2
.
START2   MOVE      SP15,KEY15
         CALL      RDSIRNG1
         MOVE      SIXTY,LINCNT
.
         DISPLAY   *P1:4,*EL:
                   *P10:24,"Printing Invoice Range : "
.
MAINLOOP CALL      RDKIRNG1
         BRANCH    OVRCD TO END
.
         DISPLAY   *P40:24,*V2LON,IRANGE
.
PRNTDT1  COMPARE   FIFTY5,LINCNT
         CALL      HEAD IF NOT LESS
.
         PRINT     "! ",IRANGE,*11,"! ",IRDESC,*55,"! ",IRNEXT:
                   *66,"! ",IRMAXI,*77,"!"
.
         ADD       ONE,LINCNT
.
         GOTO      MAINLOOP
.
HEAD     CLOCK     TIME,TIME
         ADD       ONE,PAGENO
.
         CALL      HEAD132
         CALL      LINE
.
         PRINT     "! INVOICE ",*11,"! ",*55,"!   NEXT  ":
                   *66,"! MAXIMUM",*77,"!":
                   *N,"!  RANGE  ",*11,"! DESCRIPTION",*55,"! INVOICE ":
                   *66,"! INVOICE",*77,"!"
.
         CALL      LINE
.
         MOVE      EIGHT,LINCNT
         RETURN
.
LINE     PRINT     "*-------------------------------------------------":
                   "--------------------------*"
         ADD       ONE,LINCNT
.
         RETURN
.
NOINV    DISPLAY   *P1:24,*EL,*V2LON,"*** No Invoice Ranges on File ***",*W2;
         GOTO      START1
.
END      COMPARE   ZERO,PAGENO
         GOTO      NOINV IF EQUAL
         CALL      LINE
         PRINT     *N,"   *** END OF REPORT ***";
         DISPLAY   *P1:24,*EL,*V2LON,"*** Report Generation Completed ***",*W2;
         GOTO      START1
.
ENDIT    CHAIN     PGM
         STOP
+
         INC       PATCODKY
         INC        STD001IO
         INC       PATIRNIO/INC
         INC       PATCODIO/INC
+

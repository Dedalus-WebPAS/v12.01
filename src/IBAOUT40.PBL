.******************************************************************************
.*                                                                            *
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:     IBAOUT40                                             *
.*                                                                            *
.*     FUNCTION:         LAST SESSIONS REPORT                                 *
.*                                                                            *
.*     AUTHOR:           N. SPIJKMAN                                          *
.*                                                                            *
.*     DATE WRITTEN:     28/07/88                                             *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.******************************************************************************
.*           V11.02.01   09/02/2022  Thanh T          TSK 0905641             *
.*                       Recompiled as OUTHEDFD/OUTMA1FD changes              *
.*           V10.05.01   15/07/2014  Ebon Clements  CAR 261630                *
.*                       Removed port number from program                     *
.*           V9.12.02    12/01/2010  Saroeun Soeur  CAR 209938                *
.*                       dont display inactive clinic session                 *
.*           V9.12.01    11/01/2010  Saroeun Soeur  CAR 209938                *
.*                       Added active clinics only prompt                     *
.*           V9.08.01    03/04/2007  Janet George   CAR 125059                *
.*                       Mods to output last session date correctly           *
.*                       by reading the session file (outsesaf)               *
.*           V5.08.B01   13/03/2000  Steve Armstrong  5.08 Mods               *
.*                       Mods for changes to OUTCLIFD (effective date)        *
.******************************************************************************
.*           V5.07.B01   18/01/1999  N Harrington 507 rebound error 095       *
.*                        Mods to use HEAD132                                 *
.******************************************************************************
. *               V4.00   09.09.88  E.Phan  (I.B.A.)       *
. *                       Reorganised screen & report      *
. *                       Removed unused file decs & opens *
. *               V4.01   29/09/88  M.HAYSE (I.B.A.)       *
. *                       MAKE REPORT WORK PROPERLY        *
. *               V4.02   16/10/88  M.HAYSE (I.B.A.)       *
. *                       USE OUTMASA & ADD DATE TO FILE   *
. *               V5.01   19.05.89 S.Barcham(I.B.A.)       *
. *                       Fix Report                       *
. *                       SRF100595                        *
. *               V5.02   03.06.89 S.Barcham(I.B.A.)       *
. *                       Split OUTBOK & OUTMAS includes   *
. *                       SRF 100751                       *
. *            V5.01.01   16/07/89 K.Peak   (I.B.A.)       *
. *                       Changed Adm & U/R to 8 characters*
. *            V5.01.02   30/07/90 D.Di.Paola  SRF 105864  *
. *                       Put in check thru outbokaf for   *
. *                       last session date if cancellation*
. *                       made at some time. Therefore last*
. *                       session date in outma1af is not  *
. *                       but date of last clinic is.....  *
. *            V5.01.03   04/10/91 Steve O'Gorman          *
. *                       Recompiled for Change OUTMA1FD   *
. *            V5.01.04   02/03/93 ROD                     *
. *                       Recompiled for Change OUTMA1FD   *
. **********************************************************
.
          INC       STD001FD
.
          INC       OUTCONFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCTYFD/INC
          INC       OUTSESFD/INC      * Session File 
          INC       OUTMA1FD/INC
          INC       OUTMABFD/INC
.
          INC       PATDO1FD/INC      * doctor file
.
.          Variables
.
COL       FORM      2
.
DDESC     DIM       9             /* Day description         */
DIM40     DIM       40        * pack patientname / pack doctorname
ENDSLOT   INIT      "999"
SP40      INIT      "                                        "
.
DESCID    DIM       30        * clinic ID description
DESCTYPE  DIM       30        * clinic type description
.
.
SITE      DIM       6        * store IDDD
DATE      DIM       10        * date to be printed
.
SAVID     DIM       30        * save clinic ID description
STRDATE   DIM       8
ENDDATE   DIM       8
OPTION2   DIM       1
.
PRGID     INIT      "IBAOUT40"
PRGNAM    INIT      "Last Sessions Report"
VERSION   INIT      "V12.01.00"
.
.         Start of program
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
.
          PACK      CFNAME WITH UID,FILMA1A1
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTMA11
.
          PACK      CFNAME WITH UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME WITH UID,FILSESA3
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTSESA3,CFNAME
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          MOVE      IDDD,SITE
.
START     DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print last session for all":
                    " Clinics"
.
          KEYIN     *P1:7,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF NEXT0
.
          DISPLAY   *P20:7,*B,*V2LON,"*** Invalid selection ***",*W2;
          GOTO      START
.
.
NEXT0     DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = All":
                    *P1:5,*V2LON,ONE,*HOFF," = Active Only":
                    " Clinics"
.
          KEYIN     *P1:7,*EL,"Please Select : ",*V2LON,OPTION2;
.
          MOVE      OPTION2,FORM1
.
          COMPARE   FORM1,ZERO
          GOTO      NEXT02 IF EQUAL
.
          COMPARE   FORM1,ONE
          GOTO      NEXT02 IF EQUAL
.
NEXT01    DISPLAY   *P20:7,*B,*V2LON,"*** Invalid selection ***",*W2;
          GOTO      NEXT0
.
NEXT02    MOVE      ZERO,CPAGENO                       * initialise
          MOVE      "60",CLNO
.
          DISPLAY   *P1:24,*EL,"ID   :":
                    *P31:24,"Day :":
                    *P60:24,"Time :";
.
          PACK      KEY18 WITH SITE,SP20
          CALL      RDSMASA1
.
NEXT      CALL      RDKMASA1
          BRANCH    OVRCD OF ENDP
.
          MATCH     OMASITE,SITE
          GOTO      ENDP IF NOT EQUAL
.
          LOAD      DDESC BY OMADAY FROM DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
.
          DISPLAY   *P10:24,*V2LON,OMACLIN:
                    *P40:24,DDESC:
                    *P67:24,OMASTART;
.
          CALL      PRNTDA
          GOTO      NEXT
.
.------------------------------------------------------------------------------
.        Print records
.------------------------------------------------------------------------------
.
.       Start of printing last sessions dates for all clinic ID's
.
PRNTDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      "Invalid Doctor Name            ",DESCID
.
          PACK      KEY20,SITE,OMACLIN,OMADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OMACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
.         last clinic active?
.
          MOVE      OPTION2,FORM1
          COMPARE   FORM1,ONE
          IF        @EQUAL
            MATCH   "0",OTCLIACT
            GOTO      ID99 IF NOT EQUAL
          ENDIF

          BRANCH    OVRCD,ID1
.
          MATCH     SP6,OCADOCT
          GOTO      ID0 IF EQUAL
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF ID0
.
          MOVE      DGNAME,ANS
          CALL      CLINAM
          MOVE      DIM40,DESCID                     * ID descrip in 30 char
          GOTO      ID1
.
ID0       MOVE      OCADESC,DESCID
.
ID1       MOVE      "Invalid Doctor Name            ",DESCTYPE
.
          PACK      KEY12 WITH SITE,OMATYP
          CALL      RDCTYA1
.
.         active clinic session ?
.
          MOVE      OPTION2,FORM1
          COMPARE   FORM1,ONE
          IF        @EQUAL
            COMPARE   OMASTAT,ZERO
            GOTO      ID99 IF NOT EQUAL
          ENDIF

          BRANCH    OVRCD TO ID2
.
          MOVE      OCTDESC,DESCTYPE
.
.-------- position on last record for that session and get osedate ----
.-------- this date will be last session date.... N.B. - last session date ---
.-------- in outma1af is inaccurate as there may have been deletions... ------
.
ID2       MOVE      DESCID,SAVID
          MOVE      "**** Session Missing ****",DESCID
.
.         Read session File
.
          MOVE      SP70,CPCDATE
          MOVE      SP70,DDESC
          PACK      KEY26,OMASITE,OMACLIN,OMADAY,OMASTART,Z70
          CALL      RDSSESA3
          CALL      RDPSESA3
          BRANCH    OVRCD,ID3
.
          MATCH     OMASITE,OSESITE
          GOTO      ID99 IF NOT EQUAL
.
          MATCH     OMACLIN,OSECLIN
          GOTO      ID99 IF NOT EQUAL
.
          COMPARE   OMADAY,SOSEDAY
          GOTO      ID99 IF NOT EQUAL
.
          MATCH     OMASTART,OSESTRT 
          GOTO      ID99 IF NOT EQUAL
.
          MOVE      OPTION2,FORM1
          COMPARE   FORM1,ONE
          IF        @EQUAL
            COMPARE     OMASTAT,ONE
            GOTO      ID99 IF EQUAL
          ENDIF
. 
          MOVE      SAVID,DESCID
          UNPACK    OSEDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          LOAD      DDESC BY OMADAY FROM DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
.
ID3       PRINT     *1,"|",*4,OMACLIN,*12,DESCID,*44,"|";
          MOVE      "48",CCOL
          PRINT     *CCOL,CPCDATE;
          PRINT     *61,"|",*64,DDESC,*75,"|":
                    *79,OMASTART,*88,"|",*91,OMATYP:
                    *99,DESCTYPE,*132,"|"
          ADD       ONE,CLNO
.
          DISPLAY   *P10:24,*V2LON,OMACLIN,*P30:24,OMATYP;
          MOVE      "50",CCOL
          MOVE      TWENTY4,CVERT
          MOVE      ZERO,CHIGHLT
          CALL      DSPDATE
          RETURN
.
PAGEND    PRINT     *1,"*------------------------------------------":
                    "-------------------------------------------":
                    "---------------------------------------------*"
ID99      RETURN
.
.
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NODATA IF EQUAL
.
          CALL      PAGEND
          PRINT     *N,*N,*5,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*V2LON,*P20:24:
                    "** Report Generation Completed **",*W2
          GOTO      START
.
NODATA    DISPLAY   *P1:24,*EL,*V2LON,*B,*P20:24:
                    "** No Last Session Date Found **",*W2
          GOTO      START
.
.
.------------------------------------------------------------------------------
.        Print report header
.------------------------------------------------------------------------------
.
HEAD  
          MOVE      ZERO,CLNO
          COMPARE   ZERO,CPAGENO
          GOTO      HEAD1 IF EQUAL
          CALL      PAGEND
.
HEAD1     MOVE      ONE,CNOUNDLN 
          CALL      IBACLOCK
          CALL      HEAD132 
          PRINT     *N,*N,"*------------------------------------------":
                    "-------------------------------------------":
                    "---------------------------------------------*":
                    *N,"|",*4,"Clinic",*44,"|",*47,"Last",*61,"|",*75,"|":
                    *77,"Session",*88,"|",*91,"Clinic",*132,"|":
                    *N,"|",*4,"ID",*12,"Description",*44,"|":
                    *47,"Session Date",*61,"|",*64,"Day",*75,"|":
                    *77,"Start Time",*88,"|",*91,"Type":
                    *99,"Description",*132,"|":
                    *N,"*------------------------------------------":
                    "-------------------------------------------":
                    "---------------------------------------------*"
.
          ADD       EIGHT,CLNO
          RETURN
.
...............................................................................
.        Pack doctor's title/initial/surname into a dim40
.
CLINAM    PACK      DIM40 WITH SP20,SP20
          MOVE      DTITL,DIM40
          ENDSET    DIM40
          CALL      BUMPCL
          LENSET    DIM40
          APPEND    SP1,DIM40
          APPEND    ANS,DIM40
          CALL      BUMPCL
          APPEND    ". ",DIM40
          APPEND    DSNAME,DIM40
          RESET     DIM40
          CMATCH    DIM40," "
          RETURN    IF NOT EQUAL
          MOVE      SP30,DIM40
          RETURN
.
BUMPCL    CMATCH    SP1,DIM40
          RETURN    IF NOT EQUAL
          BUMP      DIM40 BY -1
          RETURN    IF EOS
          GOTO      BUMPCL
.
          INC       OUTMA1IO/INC           * clinic MASTER IO
          INC       OUTCLIIO/INC           * clinic ID IO
          INC       OUTCTYIO/INC           * clinic type IO
          INC       OUTSESIO/INC      * Session File 
          INC       PATDO1IO/INC           * doctor IO
.
          INC       STD001IO
.
ENDIT     CHAIN     PGM
          STOP

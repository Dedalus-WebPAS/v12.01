.***************************************************************************
.* System    :   Waiting List                                              *
.* Program   :   FXWATCOM                                                  *
.* Desc      :   Copy waiting list and waiting list pre admission comments *
.*           :   from watmesaf & watpacaf to watmhdaf & watmtxaf           *
.***************************************************************************
.* Date      :   08/09/2010                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will copy all waiting list and waiting list  *
.*           :   pre admission comments to the header and line based files *
.*           :                                                             *
.* Mods      :                                                             *
.*               V10.03.01 21/10/2013 Ania P          CAR 263655           *
.*                         Split execution into 2 parts: copy data from    *
.*                         watcomaf AND copy data from watpacaf            *
.*               V10.00.00 08/09/2010 Ebon Clements   CAR 218982           *
.*                         Created program                                 *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       WATMESFD/INC
          INC       WATPACFD/INC
          INC       WATMHDFD/INC
          INC       WATMTXFD/INC
          INC       WATTR1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
FORM3     FORM      3
LIN1BLNK  FORM      1
NOTETYPE  DIM       3
NOTENUMB  FORM      6
RECNUM    FORM      8
.
PRGID     INIT      "FXWATCOM"
PRGNAM    INIT      "Waiting List Comments Conversion"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    BRANCH    COPTION,ML2100,ML2200
.
ML2100    CALL      WCOM0000               * process WL comments
          GOTO      ML9999
.
ML2200    CALL      PCOM0000               * process WL Pre Adm comments
          GOTO      ML9999
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      WATMESA1,"watmesaf"
          OPEN      WATPACA1,"watpacaf"
          OPEN      WATMHDA1,"watmhdaf"
          OPEN      WATMTXA1,"watmtxaf"
          OPEN      WATTR1A1,"wattr1af"
.
          MOVE      ZERO,RECNUM
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Move W/L Comments":
                    *P1:6,*V2LON,TWO,*HOFF,". Move W/L Preadmission Comments"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               WCOM0000              Called by: ML2000  *
.* WL Comments                                                            *
.**************************************************************************
WCOM0000  MOVE      ONE,NOTENUMB                  * Note number
          MOVE      "001",NOTETYPE                * WL comments
.
          PACK      KEY19,SP70
          CALL      RDSTREA1
WCOM1000  CALL      RDKTREA1                      * Check all WL records
          BRANCH    OVRCD,WCOM9999
.
          ADD       ONE,RECNUM                    * update record counter
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      ZERO,LIN1BLNK                 * Set line 1 blank to no
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          CALL      RDSMESA1
WCOM1100  CALL      RDKMESA1                      * Check for WL comments
          BRANCH    OVRCD,WCOM1000
.
          MATCH     WMURNO,WAURNO                 * UR
          GOTO      WCOM1000 IF NOT EQUAL
.
          MATCH     WMCODE,WAPROC                 * Procedure
          GOTO      WCOM1000 IF NOT EQUAL
.
          COMPARE   WMCNT,WACNT                   * Count
          GOTO      WCOM1000 IF NOT EQUAL
.
          IF        WACNT2=1
            MATCH    SP70,WATEXT
            IF       @EQUAL | @EOS
              MOVE     ONE,LIN1BLNK        * Line 1 is blank so skip 
              GOTO     WCOM1100
            ENDIF
          ENDIF
.
          IF        WACNT2=2
            MATCH    SP70,WATEXT
            IF       @EQUAL | @EOS
              BRANCH   LIN1BLNK,WCOM1100   * Line 1 & 2 are blank so skip 
            ENDIF
          ENDIF
.
          PACK      KEY28,WAURNO,WAPROC,WACNT,NOTETYPE,NOTENUMB,SP70 
          CALL      RDWTMHD1               * Create WL comments header record
          IF        OVRCD=1
            MOVE      WAURNO,WTMHURNO
            MOVE      WAPROC,WTMHPROC
            MOVE      WACNT,WTMHPCNT
            MOVE      NOTETYPE,WTMHTYPE
            MOVE      NOTENUMB,WTMHNOTE
.
            MOVE      SP70,WTMHDATE 
            MOVE      SP70,WTMHTIME 
            MOVE      SP70,WTMHUSER 
            MOVE      SP70,WTMHOCCG 
            MOVE      ZERO,WTMHDELT 
            MOVE      SP70,WTMHDDAT 
            MOVE      SP70,WTMHDTIM 
            MOVE      SP70,WTMHDUSE 
            MOVE      SP70,WTMHDOCC 
            MOVE      SP70,WTMHSPAR 
.
            PACK      KEY28,WTMHURNO,WTMHPROC,WTMHPCNT,WTMHTYPE,WTMHNOTE,SP70
            CALL      WRWTMHD1
          ENDIF
.
          MOVE      ZERO,FORM3
          MOVE      WACNT2,FORM3
.
          PACK      KEY31,WAURNO,WAPROC,WACNT,NOTETYPE,NOTENUMB,FORM3,SP70
          CALL      RDWTMTX1                * Create WL comments record
          IF        OVRCD=1
            MOVE      WAURNO,WTMTURNO
            MOVE      WAPROC,WTMTPROC
            MOVE      WACNT,WTMTPCNT
            MOVE      NOTETYPE,WTMTTYPE

            MOVE      NOTENUMB,WTMTNOTE
            MOVE      FORM3,WTMTUNIQ
            MOVE      WATEXT,WTMTCMNT
            MOVE      SP70,WTMTSPAR
            PACK      KEY31,WTMTURNO,WTMTPROC,WTMTPCNT,WTMTTYPE,WTMTNOTE:
                            WTMTUNIQ,SP70
            CALL     WRWTMTX1
          ENDIF
.
          GOTO      WCOM1100
.
WCOM9999  RETURN
+
.**************************************************************************
.*                               PCOM0000              Called by: ML2000  *
.* Pre Admission Comments                                                 *
.**************************************************************************
PCOM0000  MOVE      ONE,NOTENUMB                  * Note number
          MOVE      "002",NOTETYPE                * Pre Adm comments
.
          PACK      KEY19,SP70
          CALL      RDSTREA1
PCOM1000  CALL      RDKTREA1                      * Check all WL records
          BRANCH    OVRCD,PCOM9999
.
          ADD       ONE,RECNUM                    * update record counter
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      ZERO,LIN1BLNK                 * Set line 1 blank to no
.
          PACK      KEY21,WMURNO,WMCODE,DWMCNT,SP70
          CALL      RSWTPAC1
PCOM1100  CALL      RKWTPAC1                      * Check for Pre Adm comments
          BRANCH    OVRCD,PCOM1000
.
          MATCH     WMURNO,WTPCURNO               * UR
          GOTO      PCOM1000 IF NOT EQUAL
.
          MATCH     WMCODE,WTPCPCOD               * Procedure
          GOTO      PCOM1000 IF NOT EQUAL
.
          MATCH     DWMCNT,WTPCPCNT               * Count
          GOTO      PCOM1000 IF NOT EQUAL
.
          MATCH     " 1",WTPCLINE
          IF        @EQUAL
            MATCH    SP70,WTPCCOMM
            IF       @EQUAL | @EOS
              MOVE     ONE,LIN1BLNK        * Line 1 is blank so skip
              GOTO     PCOM1100
            ENDIF
          ENDIF
.
          MATCH     " 2",WTPCLINE
          IF        @EQUAL
            IF       @EQUAL | @EOS
              BRANCH   LIN1BLNK,PCOM1100   * Line 1 & 2 are blank so skip
            ENDIF
          ENDIF
.
          PACK      KEY28,WTPCURNO,WTPCPCOD,WTPCPCNT,NOTETYPE,NOTENUMB,SP70
          CALL      RDWTMHD1               * Create WL comments header record
          IF        OVRCD=1
            MOVE      WTPCURNO,WTMHURNO
            MOVE      WTPCPCOD,WTMHPROC
            MOVE      WTPCPCNT,WTMHPCNT
            MOVE      NOTETYPE,WTMHTYPE
            MOVE      NOTENUMB,WTMHNOTE
.
            MOVE      SP70,WTMHDATE
            MOVE      SP70,WTMHTIME
            MOVE      SP70,WTMHUSER
            MOVE      SP70,WTMHOCCG
            MOVE      ZERO,WTMHDELT
            MOVE      SP70,WTMHDDAT
            MOVE      SP70,WTMHDTIM
            MOVE      SP70,WTMHDUSE
            MOVE      SP70,WTMHDOCC
            MOVE      SP70,WTMHSPAR
.
            PACK      KEY28,WTMHURNO,WTMHPROC,WTMHPCNT,WTMHTYPE,WTMHNOTE,SP70
            CALL      WRWTMHD1
          ENDIF
.
          MOVE      ZERO,FORM3
          SQUEEZE   WTPCLINE
          MOVE      WTPCLINE,FORM3
.
          PACK      KEY31,WTPCURNO,WTPCPCOD,WTPCPCNT,NOTETYPE,NOTENUMB,FORM3:
                          SP70
          CALL      RDWTMTX1                * Create WL comments record
          IF        OVRCD=1
            MOVE      WTPCURNO,WTMTURNO
            MOVE      WTPCPCOD,WTMTPROC
            MOVE      WTPCPCNT,WTMTPCNT
            MOVE      NOTETYPE,WTMTTYPE

            MOVE      NOTENUMB,WTMTNOTE
            MOVE      FORM3,WTMTUNIQ
            MOVE      WTPCCOMM,WTMTCMNT
            MOVE      SP70,WTMTSPAR
            PACK      KEY31,WTMTURNO,WTMTPROC,WTMTPCNT,WTMTTYPE,WTMTNOTE:
                            WTMTUNIQ,SP70
            CALL     WRWTMTX1
          ENDIF
.
          GOTO      PCOM1100
.
PCOM9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       WATMESIO/INC
          INC       WATPACIO/INC
          INC       WATMHDIO/INC
          INC       WATMTXIO/INC
          INC       WATTR1IO/INC

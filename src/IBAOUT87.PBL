.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT87.PBL                                              *
.* Name           : Arrival Time Analysis                                     *
.******************************************************************************
.* Date           : 10/09/1993                                                *
.* Author         : Justin 'The Pom' Coates                                   *
.* Function       : To report on the number of patients who arrive to clinics *
.*                  early and break down into 3 time ranges, and total late   *
.* Modifications  :                                                           *
.*        V12.00.01 23/05/2025  Bella Turco   TSK 0955096                     *
.*                  Alphanumeric changes                                      *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.05.01 19/12/2014  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V5.08.01  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.B01 15/03/2000  Steve Armstrong   5.08 Mods                   *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.*        V5.01.02  24/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCLIFD/INC
          INC       OUTCONFD/INC            * system parameters
          INC       OUTOSFFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC            * National HCP file
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
TEMPA     IFILE     SQL, FIXED=58, KEY="U1-6,7-12,13-13,14-18,19-21,22-27"
KEYA      INIT      " 58 U1-6,7-12,13-13,14-18,19-21,22-27"
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  ---------------------
XSESNID   DIM       6           6      1  Session Id
XCLINID   DIM       6           6      7  Clinic Id
XCLDAY    DIM       1           1     13  Day
XTIME     DIM       5           5     14  Clinic Start Time
XHOSP     DIM       3           3     19  Hospital
XCTYPE    DIM       6           6     22  Clinic Type
DXATTN    DIM       6           6     28  Number of Attendances
DXEARLA   DIM       6           6     34  Early arrivals 00-30
DXEARLB   DIM       6           6     40  Early arrivals 31-60
DXEARLC   DIM       6           6     46  Early arrivals 61+
DXLATE    DIM       6           6     52  late arrivals
.End of record                        58
XATTN     FORM      6
XEARLA    FORM      6
XEARLB    FORM      6
XEARLC    FORM      6
XLATE     FORM      6
.
.         program variables
.
CDFRDATE  DIM       8
CDFRSNID  DIM       6
CDTODATE  DIM       8
CDTOSNID  DIM       6
CFNAMEA   DIM       8
CMDLINE   DIM       80
COUNT     FORM      1
CURRCLIN  DIM       6         * current print clinic id
CURRSNID  DIM       6         * current print session id
DIM3      DIM       3
DIM40     DIM       40
DOCCODE   DIM       6
.
DURATION  FORM      4         * calculated duration of operation
FORM4B    FORM      4         * time work variable
PRFRDATE  DIM       10
PRFRSNID  DIM       6
PRTODATE  DIM       10
PRTOSNID  DIM       6
PTCNDCQS  FORM      1
TIMESTOP  DIM       5         * ending time
TIMESTRT  DIM       5         * starting time
TOVRCD    FORM      1
.
.         program constants
.
COLUMN    FORM      "19"
FINISH    INIT      "Finish"
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
START     INIT      "Start "
Z6        INIT      "zzzzzz"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT87"
PRGNAM    INIT      "Arrival Time Analysis"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * run the option ??
          BRANCH    EXIT,ML9999             * exit
.
ML2000    CALL      SNID0000                * enter session id range
          BRANCH    EXIT,ML1000             * exitchar entered
.
          CALL      KDAT0000                * enter date range
          BRANCH    EXIT,ML2000             * nothing entered
.
          CALL      GDAT0000                * get the data
          CALL      REPT0000                * produce the report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening"
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTBOKA1,CFNAME
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P58:24,CFNAME
          CALL      OPOTBB11
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
          DISPLAY   *P58:24,"outosfaf"
          OPEN      OUTOSFA3,"outosfaf"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P58:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAMEA
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.*********************************************************************
.         run the option ??
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run report":
                    *P1:7,"Select option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,MOPTION
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN9999
          BEEP
          GOTO      OPTN1000
OPTN9999  RETURN
+
.*********************************************************************
.         keyin session id ranges
.*********************************************************************
SNID0000  DISPLAY   *P1:8,*EF:
                    *P1:09,"From Session Id : ":
                    *P1:10,"To   Session Id : "
.
.         enter the from code
.
          MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      SP6,CKYIDEF6            * default value
          MOVE      "09",EVERT
          MOVE      COLUMN,ECOL
          CALL      KYOUTCLI
          COMPARE   TWO,EXIT
          GOTO      SNID9100 IF EQUAL       * exitchar entered
.
          PACK      OCACLIN,OCACLIN,SP6
          MOVE      OCACLIN,CDFRSNID        * from code
          MOVE      OCACLIN,PRFRSNID        * from print code
          MATCH     SP6,OCACLIN
          IF        @EQUAL
            DISPLAY   *PCOLUMN:EVERT,*V2LON,START
            MOVE      START,PRFRSNID
          ELSE
            DISPLAY   *PCOLUMN:EVERT,*V2LON,OCACLIN,*HOFF,SP3,OCADESC
          ENDIF
.
.         enter the to code
.
SNID2000  MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      CDFRSNID,CKYIDEF6       * default value
          MOVE      "10",EVERT
          MOVE      COLUMN,ECOL
          CALL      KYOUTCLI
          COMPARE   TWO,EXIT
          GOTO      SNID9100 IF EQUAL       * exitchar entered
.
          PACK      OCACLIN,OCACLIN,SP6
          MOVE      OCACLIN,CDTOSNID        * set the value
          MOVE      OCACLIN,PRTOSNID        * to print code
          MATCH     SP6,OCACLIN
          IF        @EQUAL
            DISPLAY   *PCOLUMN:EVERT,*V2LON,FINISH
            MOVE      Z6,CDTOSNID
            MOVE      FINISH,PRTOSNID
          ELSE
            DISPLAY   *PCOLUMN:EVERT,*V2LON,OCACLIN,*HOFF,SP3,OCADESC
          ENDIF
.
          MOVE      ZERO,EXIT
          MATCH     CDFRSNID,CDTOSNID
          GOTO      SNID9999 IF NOT LESS    * end >= start so OK
.
          DISPLAY   *P1:24,*B,"Invalid range.  ",*EL;
          CALL      HITENTER
          GOTO      SNID0000
.
SNID9100  MOVE      ONE,EXIT
SNID9999  RETURN
+
.*********************************************************************
.*                  KDAT0000                    Called by : ML2000   *
.*        Enter the date ranges                                      *
.*        Returns : EXIT = 1      exit chosen                        * 
.*                  CDFRDATE      starting date  ccyymmdd            *
.*                  CDTODATE      ending date    ccyymmdd            *
.*                  PRFRDATE      starting date  dd/mm/ccyy          *
.*                  PRTODATE      ending date    dd/mm/ccyy          *
.*********************************************************************
KDAT0000  DISPLAY   *P1:12,"From Date       : ":
                    *P1:13,"To   Date       : ",*EL;
          MOVE      ONE,CCANLDTE            * blank cancel default
          MOVE      ONE,CDEFDTE             * enter once for default
          MOVE      ZERO,CHIGHLT            * use highlight
.
.         enter starting date
.
KDAT1000  MOVE      "12",CVERT              * row
          MOVE      COLUMN,CCOL             * column
          UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9000          * nothing entered
          BRANCH    CQUEST,KDAT1000
.
          PACK      CDFRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDFRDATE           * set up start date
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE         * formatted start date
.
.         enter ending date (default to starting date)
.
KDAT2000  MOVE      "13",CVERT              * row
          MOVE      COLUMN,CCOL             * column
          UNPACK    CDFRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT1000          * nothing entered
          BRANCH    CQUEST,KDAT2000
.
          PACK      CDTODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDTODATE           * set up end date
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted end date
.
          MATCH     CDFRDATE,CDTODATE
          GOTO      KDAT3000 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Invalid date range.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT2000
.
KDAT3000  CALL      CONTQST
          BRANCH    CEXIT,KDAT8000,KDAT0000,KDAT9000
.                         Yes      No       Cancel
.
KDAT8000  MOVE      ZERO,EXIT               * valid dates
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT                * exit selected
.
KDAT9999  RETURN
+
.*********************************************************************
.         get the details
.*********************************************************************
GDAT0000  DISPLAY   *P1:24,*EL,"Obtaining the information..."
          CLOCK     TIME,CTIMEIS
          CALL      CTPA0000                * create temp file
          MOVE      ZERO,COUNT
          PACK      KEY34,CDFRDATE,SP30
          CALL      RSOTOSF3
.
GDAT1000  CALL      RKOTOSF3
          BRANCH    OVRCD,GDAT9999          * finished
          MATCH     OTOSDATE,CDTODATE
          GOTO      GDAT9999 IF LESS        * after TO date
.
          MATCH     IDDD,OTOSSITE
          GOTO      GDAT1000 IF NOT EQUAL   * different site
.
          MATCH     CDFRSNID,OTOSSESS
          GOTO      GDAT1000 IF LESS        * before FROM session id
          MATCH     OTOSSESS,CDTOSNID
          GOTO      GDAT1000 IF LESS        * after  TO   session id
.
.         loop over bookings file for session found
.
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP3
          IF        COUNT = ZERO
            DISPLAY   *P41:24,*EL,KEY28
          ENDIF
          ADD       ONE,COUNT
          CALL      RDSBOKA1
.
GDAT3000  CALL      RDKBOKA1
          BRANCH    OVRCD,GDAT1000
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,KEY28
          GOTO      GDAT1000 IF NOT EQUAL   * different session
.
          MATCH     ZEROVISN,OBAOUTNO
          GOTO      GDAT3000 IF EQUAL       * no booking number
.
          COMPARE   FOUR,OBASTAT
          GOTO      GDAT3000 IF NOT EQUAL   * not an attendee
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,GDAT3000          * invalid booking number
.
          CALL      UPDT0000                * update temp file
          GOTO      GDAT3000
.
GDAT9999  RETURN
+
.*********************************************************************
.         update the temp file
.*********************************************************************
UPDT0000  MOVE      ZERO,XATTN
          MOVE      ZERO,XEARLA
          MOVE      ZERO,XEARLB
          MOVE      ZERO,XEARLC
          MOVE      ZERO,XLATE
.
          MOVE      OTOSSESS,XSESNID         * session id
          MOVE      OTOSCLID,XCLINID         * clinic  id
          MOVE      OBADAY,XCLDAY            * day
          MOVE      OTOSTIME,XTIME           * session time
          MOVE      OTOSHOSP,XHOSP           * hospital
          MOVE      OTOSCTYP,XCTYPE          * clinic type
.
          PACK      KEY27,XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE
          CALL      RDTEMPA
          MOVE      OVRCD,TOVRCD
.
          ADD       ONE,XATTN                * add to number of attendances
          MATCH     SP5,OTBBCITM
          GOTO      UPDT1000 IF EQUAL        * no check in time
.
.         calculate the duration and add to appropriate column
.
          MATCH     OTBBCITM,OBATIME
          IF        @LESS
            ADD       ONE,XLATE                * late arrival
            GOTO      UPDT1000
          ENDIF
.
          MOVE      OTBBCITM,TIMESTRT        * check in time
          MOVE      OBATIME,TIMESTOP         * slot time
          CALL      DURN0000
.
          IF        DURATION > 60
            ADD       ONE,XEARLC              * 61+ minutes early
            GOTO      UPDT1000
          ENDIF
          IF        DURATION > 30
            ADD       ONE,XEARLB              * 31-60 minutes early
            GOTO      UPDT1000
          ENDIF
          IF        DURATION > 0
            ADD       ONE,XEARLA              * 00-30 minutes early
            GOTO      UPDT1000
          ENDIF
.
UPDT1000  IF       TOVRCD = ONE
            CALL     WRTEMPA
          ELSE
            CALL     UPTEMPA
          ENDIF
.
UPDT9999  RETURN
+
.*********************************************************************
.         produce the report
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*EL,"Printing report..."
          MOVE      ZERO,CPAGENO
          MOVE      "******",CURRSNID
          MOVE      "******",CURRCLIN
          MOVE      "70",CLNO
.
          MOVE      SP30,KEY27
          CALL      RSTEMPA
.
REPT1000  CALL      RKTEMPA
          BRANCH    OVRCD,REPT9000
.
          CALL      PLIN0000
          GOTO      REPT1000
.
REPT9000  IF        CPAGENO = ZERO
            CALL      HEAD80
          ELSE
            CALL      UND80
          ENDIF
          PRINT     *N,"*** End of Report ***",*N
          CALL      DTPA0000
.
REPT9999  RETURN
+
.*********************************************************************
.         print a new page heading
.*********************************************************************
HEAD0000  CALL     HEAD80
          PRINT    *20,"Clinic Session : ",PRFRSNID,"     to ",PRTOSNID
          PRINT    *20,"Booking Dates  : ",PRFRDATE," to ",PRTODATE
          ADD      TWO,CLNO
          CALL     FNAM0000                 * format name
          PRINT    *N,"Clinic Session : ",XSESNID,SP2,OCADESC
          CALL     UND80
          PRINT    *1," Clinic                       Clinic        ":
                      "        Early Arrivals       Late"
          PRINT    *1," Id       Day     Time  Hosp  Type   Attendees ":
                      "  00-30   31-60    61+  Arrivals"
          CALL     UND80
          MOVE     TEN,CLNO
          MOVE     "******",CURRCLIN
HEAD9999  RETURN
+
.*********************************************************************
.*        Format Clinic/Surgeon name                                 *
.*********************************************************************
.
FNAM0000  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,IDDD,XSESNID,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     XSESNID,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          BRANCH    OVRCD,FNAM9999          * invalid code
.
          MATCH     SP6,OCADOCT
          GOTO      FNAM9999 IF EQUAL       * no docotor code to use
          MOVE      OCADOCT,KEY6            * use the doctor code
.
.         validate using doctors file
.
FNAM5000  CALL      RDDOCT1                 * read docs file
          BRANCH    OVRCD,FNAM9999          * invalid code
.
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DTITL,PACTITLE          * title
          MOVE      TWO,PACFRMT             * SURNAME, TITLE GIVEN
          CALL      PACNAME                 * format the name
          MOVE      PACFNAME,OCADESC        * set name
.
FNAM9999  RETURN
+
.*********************************************************************
.         print a line of the report
.*********************************************************************
PLIN0000  MATCH     CURRSNID,XSESNID
          IF        !@EQUAL
            MOVE      "70",CLNO
          ENDIF
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * print new page
.
          MATCH     CURRCLIN,XCLINID
          GOTO      PLIN2000 IF EQUAL       * same clinic id
.
          PRINT     *2,XCLINID;
.
PLIN2000  MOVE      XCLDAY,FORM1
          LOAD      KEY9,FORM1,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          PRINT     *9,KEY9,SP1,XTIME,SP1,XHOSP,SP3,XCTYPE,SP3:
                        XATTN,SP2,XEARLA,SP2,XEARLB,SP2,XEARLC,SP2,XLATE
          ADD       ONE,CLNO
          MOVE      XSESNID,CURRSNID
          MOVE      XCLINID,CURRCLIN
PLIN9999  RETURN
+
.*********************************************************************
.*                  DURN0000                    Called by : xxxx0000 *
.*        Calculate a duration given two times                       *
.*        Para's  : TIMESTOP      the end time   (HH:MM)             *
.*                  TIMESTRT      the start time (HH:MM)             *
.*        Returns : DURATION      the duration (minutes)             *
.*********************************************************************
DURN0000  MOVE      ZERO,DURATION
          MATCH     TIMESTOP,TIMESTRT
          GOTO      DURN9999 IF EQUAL       * no duration
          GOTO      DURN1000 IF LESS        * times not over midnight
.
.         operation went over midnight so make end time larger
.         eg 22:00 to 01:00 becomes 22:00 to 25:00 which gives 3 hours duration
.
          UNPACK    TIMESTOP,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2
          ADD       "24",FORM2
          MOVE      FORM2,CHOUR
          PACK      TIMESTOP,CHOUR,ANS,CMIN
.
.         get end time as minutes
.
DURN1000  UNPACK    TIMESTOP,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM4A            * set up work variable
          MULT      "60",FORM4A             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ADD       FORM2,FORM4A            * the total minutes of end time
.
.         get start time as minutes
.
          UNPACK    TIMESTRT,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM4B            * set up work variable
          MULT      "60",FORM4B             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ADD       FORM2,FORM4B            * the total minutes of start time
.
.         duration = end minutes - start minutes
.
          SUB       FORM4B,FORM4A           * the duration
          MOVE      FORM4A,DURATION         * the duration
.
DURN9999  RETURN
+
.*********************************************************************
.         Create a temp file
.*********************************************************************
CTPA0000  CALL      DTPA0000                * delete temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEA,KEYA,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPA,CFNAMEA           * open the temp file
CTPA9999  RETURN
+
.*********************************************************************
.         Delete temp file A
.*********************************************************************
DTPA0000  CLOSE     TEMPA                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEA * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
DTPA9999  RETURN
+
.*********************************************************************
.         temp file I/O
.*********************************************************************
RSTEMPA   READ      TEMPA,KEY27;;
          RETURN
.
RDTEMPA   MOVE      ZERO,OVRCD
          READ      TEMPA,KEY27;XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE:
                                DXATTN,DXEARLA,DXEARLB,DXEARLC,DXLATE
          GOTO      OVERCOND IF OVER
          MOVE      DXATTN,XATTN
          MOVE      DXEARLA,XEARLA
          MOVE      DXEARLB,XEARLB
          MOVE      DXEARLC,XEARLC
          MOVE      DXLATE,XLATE
          RETURN
.
RKTEMPA   MOVE      ZERO,OVRCD
          READKS    TEMPA;XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE:
                          DXATTN,DXEARLA,DXEARLB,DXEARLC,DXLATE
          GOTO      OVERCOND IF OVER
          MOVE      DXATTN,XATTN
          MOVE      DXEARLA,XEARLA
          MOVE      DXEARLB,XEARLB
          MOVE      DXEARLC,XEARLC
          MOVE      DXLATE,XLATE
          RETURN
.
WRTEMPA   MOVE      XATTN,DXATTN
          MOVE      XEARLA,DXEARLA
          MOVE      XEARLB,DXEARLB
          MOVE      XEARLC,DXEARLC
          MOVE      XLATE,DXLATE
          WRITE     TEMPA,KEY27;XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE:
                                DXATTN,DXEARLA,DXEARLB,DXEARLC,DXLATE
          RETURN
.
UPTEMPA   MOVE      XATTN,DXATTN
          MOVE      XEARLA,DXEARLA
          MOVE      XEARLB,DXEARLB
          MOVE      XEARLC,DXEARLC
          MOVE      XLATE,DXLATE
          UPDATE    TEMPA;XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE:
                          DXATTN,DXEARLA,DXEARLB,DXEARLC,DXLATE
          RETURN
+
          INC       STD001IO
          INC       KYOUTCLI
          INC       IBASEQIO/INC
          INC       OUTDSCLN
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTOSFIO/INC
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       TFILENAM
          INC       WEBERRIO/INC
..............................................................................

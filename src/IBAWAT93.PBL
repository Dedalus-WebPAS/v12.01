.******************************************************************************
.* System   : WAITING LIST                                                    *
.* Program  : IBAWAT93                                                        *
.* Desc     : Automatic Review Update.                                        *
.******************************************************************************
.* Author   : Bill Dew                                                        *
.* Date     : 06/08/1990                                                      *
.* Comments :                                                                 *
.* Mods     :                                                                 *
.* V9.03.01  13/08/2003  Sandra Barcham    42274                              *
.*           Defined WATHI002 with tilda's to put current date into           *
.*           WTHIEDAT to stop I * D                                           *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.* V9.02.01  27/02/2003  Jill Habasque SRF 34660                              *
.*           Recompiled for WLHISSUB                                          *
.******************************************************************************
.*        V5.08.03  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.02  27/07/2000  Jill Habasque                                 *
.*                  Recompiled for WLHISSUB                                   *
.*        V5.08.01  11/07/2000  Jill Habasque                                 *
.*                  Recompiled for WLHISSUB                                   *
.*            V5.04.01  15/04/1997  howellsy   RHA                            *
.*                      Write to the W/L History File                         *
.******************************************************************************
.*            V5.01.01  07/08/90 Bill Dew                                     *
.*                      Make WAT93 print a report showing the waiting list    *
.*                      treatment records that were updated in process.       *
.*                      This is done by modifying AUTO0000 to print           *
.*                      25/09/90 Bill Dew                                     *
.*                      Add the master file extension.                        *
.*            V5.01.02  19/10/90 DIG / i chung (15/11/90)                     *
.*                      Recompile for WATCATIO.INC - incorrect Keys size      *
.*            V5.01.03  27/11/90 Glen Hobby                                    *
.*                      Recompiled for changes to                              *
.*                      PATMX1FD                                               *
.*            V5.01.04  29/04/93 Robert Sumsion                                *
.*                      Start date entry problem  - SRF No. 121394             *
.******************************************************************************
.  $$$$$$
.
.    Program Description
.    -------------------
.    This program updates the priority code of a waiting list patient when
.    the current date has reached or passed the re-assessment date.
.    When this happens the priority code is decrease a step down.  For
.    example If a patient is urgent they will be set to semi-urgent.  Thus
.    Semi-urgent to Routine and Routine to, Who Knows may be they just kick'em 
.    out to emergency with a band aid and some mucurochrome.
.
.
.    06/08/1990 : Currently this program only applies to r5
.
.  $$$$$$
.*******************************************************************************
.
          INC       STD001FD
          INC       BOKRC1FD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       WATCATFD/INC
          INC       WATCONFD/INC
          INC       WATTR1FD/INC
          INC       WATHISFD/INC
          INC       WATNBRFD/INC
          INC       WATPROFD/INC
          INC       WEBSECFD/INC
+
.******************************************************************************
.                            VARIABLE DECLARATIONS
.******************************************************************************
.
ADDWLFLG  FORM      1
CODE      DIM       2
PTCNURHA  FORM      1
COUNTER   FORM      3
.
DATE1     DIM       10
DATE2     DIM       10
.
FDATE1    DIM       8        * FROMdate ccyymmdd
FDATE2    DIM       8        * TOdate ccyymmdd
FILEDATE  DIM       8
FORM8     FORM      8
.
WATHI002  INIT      "~~~~~~~~"
.
NORMDATE  DIM       10
.
PATNAME   DIM       27
PROCDESC  DIM       23
TODAY     DIM       8        * current date ccyymmdd
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
.--------------------------------< CONSTANTS >---------------------------------
.
NUM37     FORM      "37"
NUM55     FORM      "55"
.
PRGID     INIT      "IBAWAT93"
PRGNAM    INIT      "Automatic Review Update"
VERSION   INIT      "V12.01.00"
.**************************************************************************
.*                              MAINLINE                                  *
.                   This is MAIN LINE Rock & Rollllllllllllllll
.**************************************************************************
ML0000
.MLQQ      SPLOPEN   "data"
          CALL      INIT0000           * open files     
ML1000    
          CALL      MENU0000
          BRANCH    OPTION,ML1111
ML9999
.MLQQQ     SPLCLOSE  "data"
          CHAIN     PGM
ML1111
          CALL      DRNG0000           * keyin from/to date
          CALL      CONTQST
          BRANCH    CEXIT,ML2222,ML1111,ML9999
ML2222
          CALL      AUTO0000           * automatic update of reassessment date
          GOTO      ML1000
.***************************************************************************
.*        Initialisation , opening files                                   *
.***************************************************************************
INIT0000  
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA1,"watcataf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NUM37;*201,WTCNSEMI,WTCNROUT
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
INIT9999  RETURN
.**************************************************************************
.                                 MENU0000
.**************************************************************************
MENU0000  HLINE     *G37,2,52,80
          DISPLAY   *P39:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Automatic Review Update":
                    *P1:7,"Select Option : "
MENU1000
          KEYIN     *P17:7,*EL,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      MENU9999 IF EQUAL
.
          BRANCH    OPTION,MENU1111
          BEEP
          GOTO      MENU1000
MENU1111  DISPLAY    *P52:2,*V2LON,"- Automatic Review Update "
.          GOTO      MENU9999
MENU9999  RETURN
.*****************************************************************************
.                                 DRNG0000
.    Get date range Store dates in DATE1 DATE2 and FDATE1 FDATE2
.    This is a Bill Dew routine replacing the Old date range function.
.*****************************************************************************
DRNG0000
          DISPLAY    *P1:10,*EF:
                     *P1:10,"From Automatic Review Date :":
                     *P1:11,"To   Automatic Review Date :"
.
          MOVE      "30",CCOL       * setup screen position 1st keyin
          MOVE      "10",CVERT
          PACK      FILEDATE,SP10
          CALL      GDAT0000           * get month and year calender
.
          UNPACK    FILEDATE,CCENT,KEY6
          MATCH     "000000",KEY6      * test for return hit
          GOTO      DRNG4444 IF NOT EQUAL
          MOVE      "Start",DATE1      * set from the starting record
          MOVE      "00000000",FDATE1  * start of file is spaces
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,"Start"
          GOTO      DRNG5555
DRNG4444
          MOVE      NORMDATE,DATE1
          MOVE      FILEDATE,FDATE1
DRNG5555
          MOVE      TODAY,FILEDATE     * default end range to today
          MOVE      "30",CCOL
          MOVE      "11",CVERT
          CALL      GDAT0000           * get end date range
          MOVE      NORMDATE,DATE2
          MOVE      FILEDATE,FDATE2
.
          MATCH     FDATE1,FDATE2      * check for stupid operator input
          GOTO      DRNG8000 IF LESS
          GOTO      DRNG9999
DRNG8000
          DISPLAY   *P1:24,*B,*EL:
                    "The End date must occur after start date.  ";
          CALL      HITENTER
          GOTO      DRNG5555
.
DRNG9999  RETURN
.*****************************************************************************
.                                 GDAT0000
.    Get date : FILEDATE cc yy mm dd   NORMDATE dd / mm / ccyy
.    No Date can be > todays date.
.*****************************************************************************
GDAT0000
          UNPACK    FILEDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE            * keyin the period
          PACK      FILEDATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",FILEDATE      * replace blanks with zeros
.
          MATCH     FILEDATE,TODAY     * check date is < today
          GOTO      GDAT8888 IF LESS
.
          CALL      PACDATE
          PACK      NORMDATE,CPCDATE,SP10
          REPLACE   " 0",NORMDATE      * replace blanks with zeros
          GOTO      GDAT9999
GDAT8888
          DISPLAY   *P1:24,*EL,*B,"Date must be past or present.  ";
          CALL      HITENTER
          GOTO      GDAT0000
GDAT9999  RETURN
.*****************************************************************************
.                                 AUTO0000
.    Read treatment file 2nd index on waiting status.  Accept all unshceduled
.    patients (WMSTAT=1) then update their priority codes if re-assessment 
.    date is between the designated start and end date ranges.
.    Once a patient is found to suit the selection criteria decrease 
.    patients priority down next level.  Urgent to semi-urgent, semi-urgent
.    to routine, and routine to what I've stated previously.
.
.    Modify AUTO to print a report of patients affected by the update.
.*****************************************************************************
AUTO0000
.AUTOQQ1   DISPLAY   *P1:3,"AUTO> fdate1 ",FDATE1," fdate2 ",FDATE2:
.                    " semi ",WTCNSEMI," rout ",WTCNROUT;
.AUTOQQK1  KEYIN     ANS
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          CALL      HEAD0000
          DISPLAY   *P1:24,*EL,"U/R number : "
          MOVE      ZERO,FORM8         * zero patient update counter
          PACK      KEY20,ONE,SP20
          CALL      RDSTREA2           * read 2nd index on patient status
AUTO2222
          CALL      RDKTREA2
          BRANCH    OVRCD,AUTO8888
.
          COMPARE   ONE,WMSTAT         * test unscheduled patients
          GOTO      AUTO8888 IF NOT EQUAL
.
          MATCH     FDATE1,WTWMREAD    * test assessment date > start date
          GOTO      AUTO2222 IF LESS
          MATCH     WTWMREAD,FDATE2    * test assessment date < end date
          GOTO      AUTO2222 IF LESS
.
.BD        MATCH     WTWMREAD,TODAY     * test re-assessment date < todays date
.BD        GOTO      AUTO2222 IF LESS
.
          PACK      KEY5,ANST,ANSP,WMPTY
          CALL      RDCODE1            * get 1st indicator
          BRANCH    OVRCD,AUTO2222
          MOVE      WMPTY,WTCACODF     * set the old code
.
          CALL      SAVHIS00           * save history vars
.
.AUTOQQ2   DISPLAY   *P1:20,"AUTO>",WMURNO,"|",WMCODE,"|",WMCNT,"|":
.                    WTWMREAD,"|",WMPTY,"|",TCINDC1,"|";
.
          MATCH     ANSU,TCINDC1       * test for urgent priority
          GOTO      AUTO4444 IF NOT EQUAL
          MOVE      WTCNSEMI,WTCACODT  * set next priority code down from urgent
          GOTO      AUTO6666
AUTO4444
          MATCH     ANSS,TCINDC1
          GOTO      AUTO5555 IF NOT EQUAL
          MOVE      WTCNROUT,WTCACODT  * set next priority code down from semi
          GOTO      AUTO6666
AUTO5555
.    Thus if first indicator is blank or routine exclude it.  There is no 
.    priority code down from routine.
          GOTO      AUTO2222
AUTO6666
.AUTOQQ3   DISPLAY   *P1:21,"AUTO> old ",WTCACODF," new ",WTCACODT;
.AUTOQQK2  KEYIN     ANS
          DISPLAY   *P14:24,*V2LON,WMURNO
          CALL      PRNT0000
          ADD       ONE,FORM8          * increment the record counter
          CALL      WCAT0000           * write change to the category change 
          MOVE      SP8,WTWMREAD       * clear the re-assessment date
          MOVE      WTCACODT,WMPTY
          PACK      KEY19,WMURNO,WMCODE,WMCNT
          CALL      RDATREA1           * position fptr on 1st index
          CALL      UPTREA1            * update priority on treatment record
.
          CALL      WRTHIS00           * write W/L History
          GOTO      AUTO2222
AUTO8888
          COMPARE   ZERO,FORM8
          GOTO      AUTO9000 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*B,"No Patients updated.  ";
          CALL      HITENTER
AUTO9000
          CALL      PRNTLINE
          PRINT     *N,"Patients updated : ",FORM8:
                    *N,*N,"*** End of Report ***"
AUTO9999  RETURN 
.***************************************************************************** 
.                                 WCAT0000
.    Write a category change record for the change of priority code
WCAT0000
          PACK      KEY29,WTWMREAD,ANST,ANSP,WMURNO,WMCODE,WMCNT
          UNPACK    KEY29,WTCAEDAT,WTCACATF,WTCAURNO,WTCAPROC,DIM2
          MOVE      DIM2,WTCAPCNT
          CALL      RAWTCAT1           * check record already exist
          BRANCH    OVRCD,WCAT6666
          CALL      UPWTCAT1           * update existing record with new data
          GOTO      WCAT9999
WCAT6666
          CALL      WRWTCAT1           * write a record to category change file
WCAT9999  RETURN
.***************************************************************************** 
.                                 PRNT0000
.    Print a single line
.***************************************************************************** 
PRNT0000
          COMPARE   CLNO,NUM55         * test page number
          CALL      HEAD0000 IF LESS
.
          CALL      PATN0000           * get patients name
.
          MOVE      WMDESC,PROCDESC    * truncate treatment description
.
          UNPACK    WTWMREAD,CCENT,CYEAR,CMON,CDAY
          PACK      NORMDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          PRINT     "| ",WMURNO,"  | ",PATNAME," | ",PROCDESC,"  ":
                    "/",WMCNT," |   ",NORMDATE,"  | ";
.
          PACK      KEY5,ANST,ANSP,WTCACODF
          CALL      RDCODE1
          MOVE      TDESC,KEY17
          PRINT     KEY17," | ";
.
          PACK      KEY5,ANST,ANSP,WTCACODT
          CALL      RDCODE1
          MOVE      TDESC,KEY17
          PRINT     KEY17,*132,"|"
.
          ADD       ONE,CLNO           * increment printed line counter
PRNT9999  RETURN
.***************************************************************************** 
.                                 PATN0000
.    setup patients name
.***************************************************************************** 
PATN0000
          MOVE      "Unknown                 ",PATNAME
          PACK      KEY8,WMURNO,SP8
          CALL      RDMAST1
          BRANCH    OVRCD,PATN9999
.
          MOVE      TWO,PACFRMT        * title name surname
          MOVE      PSNAME,PACSNAME    * set patients name in PATNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
.
PATN9999  RETURN
.***************************************************************************** 
.                                 HEAD000   
.    Do main page heading for both summary and details
.*****************************************************************************
HEAD0000  
          CLOCK     TIME,CTIMEIS
          CALL      HEAD132
          PRINT     *40,"Review Update Date Range : ",DATE1," To ",DATE2,*N
.
          CALL      PRNTLINE
          PRINT     "| UR Number | Patient Name ",*43,"| Procedure":
                    *68,"Count | Re-assessment | Old Priority":
                    *110,"| New Priority",*132,"|"
          CALL      PRNTLINE
          ADD       FOUR,CLNO
HEAD9999  RETURN
.****************************************************************************
PRNTLINE  PRINT       *1,"*---------------------------------------------":
                         "-------------------------------------------":
                         "------------------------------------------*"
          ADD         ONE,CLNO
LINE9999  RETURN
.***************************************************************************** 
.
          INC       STD001IO
          INC       BOKRC1IO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       WATCATIO/INC
          INC       WATTR1IO/INC 
          INC       WATHISIO/INC 
          INC       WATNBRIO/INC 
          INC       WATPROIO/INC 
          INC       WEBSECIO/INC 
          INC       WLHISSUB     

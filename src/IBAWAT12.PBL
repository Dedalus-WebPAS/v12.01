.******************************************************************************
.* System   : WAITING LISTS                                                   *
.* Program  : IBAWAT12                                                        *
.* Desc     : Ad-Hoc Enquiry                                                  *
.******************************************************************************
.* Date     : 09/10/1988                                                      *
.* Author   : N.S.                                                            *
.* Comments :                                                                 *
.* Mods.    :                                                                 *
.******************************************************************************
.*        V12.00.01 30.05.2025  DA Sarkies       TSK 0955096                  *
.*                  Recompiled for Alpha-Numeric Changes                      *
.******************************************************************************
.*        V11.02.01 10/02/2022  Thanh T          TSK 0889899                  *
.*                  Recompiled as WATTR1FD changes                            *
.*        V11.02.01 10/02/2022  Thanh T          TSK 0889899                  *
.*                  Recompiled as WATTR1FD changes                            *
.******************************************************************************
.*        V9.04.01  01/03/2005  Peter Vela    CAR 57547                       *
.*                  Recompiled for PATVADFD and PATVADIO                      *
.*        V9.03.01  29/08/2003 J.Tan   CAR 42933                              *
.*                  Recompiled for PATSRCH                                    *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.04  03/11/1999 J Habasque                                     *
.*                  Mods to allow inactive codes                              *
.*        V5.07.03  26/10/1999 J Habasque                                     *
.*                  Recompiled for WATDSPRO                                   *
.*        V5.07.02  22/09/1999 J Habasque                                     *
.*                  Changed transfer description to use Cat WR not Cat BC     *
.*        V5.07.01  16/08/1999 J.Tan                                          *
.*                  Added transfer destination description                    *
.                   22/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
.*            V5.01.06  09/04/90  D.Di.Paola       SRF 104570                 *
.*                      Changed processing so that when an individual proc.   *
.*                      is chosen then exit will return back to the summary   *
.*                      screen of procedures, instead of having to re-enter   *
.*                      parameters.....                                       *
.*            V5.01.10  20/06/90 Bill Dew                                     *
.*                      Update IBAWAT12 to New Standards and add extra search *
.*                      fields due to the new waiting list FD's and IO's.     *
.*                      Again I have re-written most of the code.  Finished on*
.*                      on the 25/06/90 3 days.  My work with WAT12 is done.  *
.*            *****************************************************************
.*            * Programmer Notes by Bill Dew  20/06/90                        *
.*            * created a DRNG0000 which is used to obtain start and end      *
.*            * dates.  Also GCOD0000 which gets a code from codes file given *
.*            * the category type, it also handles ? redisplay correctly.     *
.*            *                                                               *
.*            * The search time can be accelerated by inputing a Date on list *
.*            * field because the program loops on the 3rd index of the       *
.*            * treatment file which is indexed on the date on list field.    *
.*            *****************************************************************
.*                      25/07/90 Paul Duncan                                  *
.*                      Implemented second surname file                       *
.*                      03/08/90 Justin Coates                                *
.*                      Added the use of WTCNTPUC                             *
.*                      Added (F)irst screen, (N)ext screen choices to line   *
.*                      24 prompt                                             *
.*                      29/08/90 Bill Dew                                     *
.*                      Problem with total menu option 22                     *
.*                      10/09/90 Bill Dew                                     *
.*                      Show patient cancellation code if patient was booked  *
.*                      then cancelled and place back on the waiting list.    *
.*                      Thus instead of testing WMSTAT=6 check if a           *
.*                      cancellation code exist.                              *
.*            V5.01.121 28/09/90 Bill Dew                                     *
.*                      Note the patient master extension has been added by   *
.*                      "root".  Fixed it so that after displaying patient    *
.*                      procedure details program does not begin the search   *
.*                      again but display what is stored in memory.           *
.*            V5.01.122 08/11/90 D.Di.Paola        SRF 106928, 106998         *
.*                      Fix user defines displays....                         *
.*                      Increase record counter for totals only option when   *
.*                      printing                                              *
.*            V5.01.123 25/01/91 Justin Coates                                *
.*                      Added BOKRC1FD/IO                                     *
.*            V5.01.124 06/02/91 i chung           SRF 107719                 *
.*                      Fixed wrong dates for Date on List, Last Review Date  *
.*                      and Scheduled Adm Date when (P)rint option is chosen  *
.*            V5.01.125 27/02/91 Justin Coates                                *
.*                      Had to increment version number                       *
.*            V5.01.126 14/05/91 i chung           SRF 108299                 *
.*                      Only display User Defined fields if switched on       *
.*            V5.01.127 23/05/91  Steve O'Gorman                              *
.*                      Fixed Errors found by the New Compiler                *
.*        V5.01.128 10/07/1991    Justin Coates  (Vic Health Dept Changes)    *
.*                  Add keyin of WTWMCLSS if using it                         *
.*                  Change display of Patient Category to Patient Class. as   *
.*                  the new field WTWMCLSS is Cat P = Patient Category        *
.*                  Also changed the way user defined fields were handled.    *
.*        V5.01.129 18/11/91      J.Tan     SRF 111797                        *
.*                  Initialised parameter for patient category                *
.*        V5.01.130 29/01/1992  Graeme Williams                               *
.*                  Use category description for category "A "                *
.*        V5.01.131 11/02/93 Justin Coates (DUDLEY- Do Right of the Mounties) *
.*                  added new open commands                                   *
.*                  dont display suspended procedures                         *
.*        V5.01.132 22/04/93 DIG                                              *
.*                  Re-Compiled for changes to PATALERT.                      *
.*        V5.01.133 26/08/93 ROD                                              *
.*                  Re-Compiled for NZHIS                                     *
.*        V5.01.134 05/01/1994  Glenn Berry      Taranaki Mods (3.4.1)        *
.*                  Display Telephone Number on patient heading.              *
.*        V5.01.135 18/04/1994  Glenn Berry                                   *
.*                  Allow for 20 char phone numbers                           *
.*        V5.01.136 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.02.00  12/10/1994  Whirlpool                                     *
.*                  Fixed display of removed procedures                       * 
.******************************************************************************
.*        V5.03.01  12/12/1994  Whirlpool   srf 640488                        *
.*                  Fixed display of "To when keying in UR'S                  * 
.******************************************************************************
. *       V5.04.01  15/04/1997  howellsy   RHA                                *
. *                 Recompiled for WATDSPRO                                   *
.*        V5.04.01  25/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.
          INC       STD001FD
          INC       BOKRC1FD/INC                 * booking master file
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       OUTPREFD/INC
          INC       PATALRFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCOMM/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       PATPR1FD/INC
          INC       PATGSRFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       WATCONFD/INC
          INC       WATPROFD/INC
          INC       WATRTDFD/INC
          INC       WATSUSFD/INC
          INC       WATTR1FD/INC
.
.         Variables Declaration
.
BJDAY     FORM      3
CJDAY     FORM      3
CATADES   DIM       20            * category A description
CODE      DIM       2
CODEA     INIT      "A "          *          A  (patient classification)
CODERS    INIT      "T "          *          RS (resident status)
CODET     INIT      "T "          * catagory T  (patient type)
CODETP    INIT      "TP"          * catagory TP (priority)
CODEW1    INIT      "W1"          * catagory W1
CODEW2    INIT      "W2"          * catagory W2
CODEW3    INIT      "W3"          * catagory W3
CODEW4    INIT      "W4"          * catagory W4
CODEWS    INIT      "WS"          *          WS (short notice)
CODEWU    INIT      "WU"          * catagory WU (clinic/unit)
CODEDESC  DIM       20
COL       FORM      2             * for WATDS*
COUNT     FORM      2
.
DATE1     DIM       10           * start date normal format
DATE2     DIM       10           * end date normal format
DATE10    DIM       10           * wmdate1  (dd/mm/ccyy)
DATE20    DIM       10           * wmdate2  (dd/mm/ccyy)
DATE30    DIM       10           * wmdate3  (dd/mm/ccyy)
DDESCWU   DIM       20           * display variable for referral/unit
DESCDEF1  DIM       20
DESCDEF2  DIM       20
DESCDEF3  DIM       20
DESCDEF4  DIM       20
DESCNOT   DIM       20           * description for short notice (cata WS)
DESCPCAT  DIM       20           * description for patient catag (cata P)
DESCPCLS  DIM       20           * description for patient classif (cata A)
DESCPTYP  DIM       20           * description for Waiting type (cata WT)
DESCSTAT  DIM       12           * description for status
DESCTP    DIM       20           * description for priority (cata TP)
DESCWU    DIM       20           * description for clinic/unit (cata WU)
DESCBC    DIM       20           * removal decription
DIM10     DIM       10
DIM3      DIM       3            * patdsdoc
DIM8      DIM       8            * store wmurno
DIMC01    DIM       19
DIMC02    DIM       19
DIMC03    DIM       19
DIMC04    DIM       19
DIMC05    DIM       19
DIMC06    DIM       19
DIMC07    DIM       19
DIMC08    DIM       19
DIMC09    DIM       19
DIMC10    DIM       19
DIMC11    DIM       19
DIMC12    DIM       19
DIMC13    DIM       19
DIMC14    DIM       19
DIMC15    DIM       19
DIMC16    DIM       19
DIMC17    DIM       19
DISPACT   FORM      1
DOCNAME   DIM       32           * doctor title initial.surname for display
DOCCODE   DIM       6
DSACNUM   FORM      3
DSEX      DIM       7            * descr. sex
.
ENDCODE   DIM       10
EXTRFLAG  FORM      1            * signal to search or display screen array
.                       ----- flags for parameters keyed in or not : 0=no  1=yes
FDATE1    DIM       8            * filedate start ccyymmdd
FDATE2    DIM       8            * filedate end ccyymmdd
FILECODE  DIM       3
FINISH    INIT      "Finish    "
FLGCO     FORM      1            * proccode
FLGD1     FORM      1            * date on list
FLGD2     FORM      1            * last review date
FLGD3     FORM      1            * scheduled admission
FLGDO     FORM      1            * doctor
FLGES     FORM      1            * estimated stay
FLGPC     FORM      1            * patient category
FLGPL     FORM      1            * patient classification
FLGPT     FORM      1            * priority
FLGSN     FORM      1            * short notice
FLGST     FORM      1            * procedure status
FLGUN     FORM      1            * unit
FLGUR     FORM      1            * UR
FLGRS     FORM      1            * Resident status 
FLGRR     FORM      1            * Removal Reason
FLGRD     FORM      1            * Removal Date
FLGRAD    FORM      1            * Re-Assessment Date
FLGDOS    FORM      1            * Date On System
FLGUDF1   FORM      1            * user define 1
FLGUDF2   FORM      1            * user define 2
FLGUDF3   FORM      1            * user define 3
FLGUDF4   FORM      1            * user define 4
FORM8     FORM      8            * counts records found in tally only option
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
.
KSTCODE   DIM       9
KSTDATE1  DIM       8
KSTDATE2  DIM       8
KSTDATE3  DIM       8
KSTDOC    DIM       6
KSTNOT    DIM       3
KSTPCAT   DIM       3
KSTPCLS   DIM       3
KSTPTY    DIM       3
KSTSTAY   FORM      5
KSTUNIT   DIM       3
KSTUR     DIM       8
KSTWTYP   DIM       3
KTOCODE   DIM       9
KTODATE1  DIM       8
KTODATE2  DIM       8
KTODATE3  DIM       8
KTODOC    DIM       6
KTOSTAY   FORM      5
KTOUNIT   DIM       3
KTOUR     DIM       8
KREMREAS  DIM       3            * removal reason
KREMDAT1  DIM       8            * start removal date
KREMDAT2  DIM       8            * end removal date
KREADAT1  DIM       8            * start re-assessment date
KREADAT2  DIM       8            * end re-assessment date
KDOSDAT1  DIM       8            * start date on system
KDOSDAT2  DIM       8            * end date on system
KSTATUS1  FORM      1            * start procedure status
KSTATUS2  FORM      1            * end procedure status
.
LINE27    FORM      "27"
LINE41    FORM      "41"
.
MASK      DIM       20
MOPT01    FORM      2             * variable number of optional field 1
MOPT02    FORM      2             * variable number of optional field 2
MOPT03    FORM      2             * variable number of optional field 3
MOPT04    FORM      2             * variable number of optional field 4
MOPT05    FORM      2             * variable number of optional field 5
NMPNUMB   DIM       20
NUM37     FORM      "37"
.
PATDOB    DIM       10           * DOB patient
PATNAME   DIM       30           * name patient
PROPTION  FORM      1
.
HUSRDES1  DIM       20
HUSRDES2  DIM       20
HUSRDES3  DIM       20
HUSRDES4  DIM       20
USERDEF1  DIM       3            * user define code 1
USERDEF2  DIM       3            * user define code 2
USERDEF3  DIM       3            * user define code 3
USERDEF4  DIM       3            * user define code 4
.
REPLY     DIM       2
.
SCNDGNAM  DIM       40
SCRNFLAG  FORM      1
START     INIT      "Start     "
STAT1     INIT      "Unscheduled "
STAT2     INIT      "Scheduled   "
STAT3     INIT      "Pre-admitted"
STAT4     INIT      "Admitted    "
STAT5     INIT      "Discharged  "
STAT6     INIT      "Removed     "
STAT7     INIT      "Performed   "
STAT8     INIT      "Not Performd"
STATDES1  DIM       10
STATDES2  DIM       10
STRTCODE  DIM       10
.
TOTLFLAG  FORM      1            * total only flag
VERT      FORM      2
.
XPOS      FORM      2            * same as CCOL
XPOS1     FORM      2
XPOS2     FORM      2
XPOS3     FORM      2
XPOS4     FORM      2
.
YPOS      FORM      2            * same as CVERT
YPOS1     FORM      2
YPOS2     FORM      2
YPOS3     FORM      2
YPOS4     FORM      2
Z30       INIT      "ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ"
.
PRGID     INIT      "IBAWAT12"
PRGNAM    INIT      "Ad-hoc Enquiry"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
ML0000    CALL      INIT0000           * initialization and open files
.
ML3000    CALL      CLRP0000           * clear parameter vars
          CALL      SCRP0000           * parameter screen + keyin params
          CALL      SELT0000           * select to update
          BRANCH    EXIT,ML9999        * (E)xit selected
          BRANCH    TOTLFLAG,ML4444
.
ML3500    CALL      EXTR0000           * extract details and select a patient
          BRANCH    EXIT,ML3000,ML9999
          CALL      DETT0000           * get Procedure descriptions
          CALL      SCRT0000           * detailed Procedure screen
          CALL      DIST0000           * display details Procedure
          CALL      USRD0000           * display user defines
          CALL      PRTT0000           * print Procedure ? EXIT=1 for NO
          BRANCH    EXIT,ML3500,ML9999
          CALL      PRND0000           * print Procedure details
          GOTO      ML3500
.
ML4444    CALL      TALY0000           * tally records according to search info
          GOTO      ML3000
.
ML9999    CHAIN     PGM
          STOP
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P64:24,"patpraxf";
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
.
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA3,"watproaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
INIT9000  READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,FIFTY9;*132,CALRDESC,*220,PTCNUADT
          READ      CONTROLF,NUM37;*52,HUSR1,HUSRDS1,HUSMAN1:
                                       HUSR2,HUSRDS2,HUSMAN2:
                                       HUSR3,HUSRDS3,HUSMAN3:
                                       HUSR4,HUSRDS4,HUSMAN4:
                    *122,WTCNTPUC,*185,WTCNTPDS,*200,WTCNREAS:
                    *207,WTCNUCLS
.
          MOVE      "Unknown",TDESC
          PACK      KEY5,CODEWU,SP5
          CALL      RDCODE1
          MOVE      TDESC,DDESCWU
.
          MOVE      "Unknown",TDESC
          PACK      KEY5,CODEA,SP5
          CALL      RDCODE1
          MOVE      TDESC,CATADES
.
          CALL      UDES0000
          MOVE      ONE,CNEWDS         * set for new ? display routine
.
          MOVE      ZERO,MOPT01
          MOVE      ZERO,MOPT02
          MOVE      ZERO,MOPT03
          MOVE      ZERO,MOPT04
          MOVE      ZERO,MOPT05
.
INIT9999  RETURN
+
.****************************************************************************
.*                                UDES0000                                  *
.*     Read user define descriptions from codes file and substitute those   *
.*     read in from controlf.                                               *
.****************************************************************************
UDES0000  PACK      KEY5,CODEW1,SP3
          CALL      RDCODE1
          MOVE      TDESC,HUSRDES1
.
          PACK      KEY5,CODEW2,SP3
          CALL      RDCODE1
          MOVE      TDESC,HUSRDES2
.
          PACK      KEY5,CODEW3,SP3
          CALL      RDCODE1
          MOVE      TDESC,HUSRDES3
.
          PACK      KEY5,CODEW4,SP3
          CALL      RDCODE1
          MOVE      TDESC,HUSRDES4
.
UDES9999  RETURN
+
.**************************************************************************
.*                              CLRP0000                                  *
.*                   clear keyin parameter variables                      *
.**************************************************************************
CLRP0000  MOVE      SP8,KSTUR
          MOVE      SP8,KTOUR
          PACK      KSTCODE,SP10
          PACK      KTOCODE,SP10
          PACK      KSTDATE1,SP10
          PACK      KTODATE1,SP10
          PACK      KSTDATE2,SP10
          PACK      KTODATE2,SP10
          PACK      KSTDATE3,SP10
          PACK      KTODATE3,SP10
          PACK      KSTUNIT,SP10
          PACK      KTOUNIT,SP10
          PACK      KSTDOC,SP10
          PACK      KTODOC,SP10
          MOVE      ZERO,KSTSTAY
          MOVE      "99999",KTOSTAY
          PACK      KSTPTY,SP10
          PACK      KSTNOT,SP10
          PACK      KSTPCLS,SP10
          PACK      KSTWTYP,SP10
          UNPACK    SP20,STATDES1,STATDES2
          UNPACK    SP20,KREMDAT1,KREMDAT2
          UNPACK    SP20,KREADAT1,KREADAT2
          UNPACK    SP20,KDOSDAT1,KDOSDAT2
          UNPACK    SP30,KREMREAS,DESCBC
          UNPACK    SP30,USERDEF1,USERDEF2,USERDEF3,USERDEF4
.
          MOVE      ONE,FLGUR
          MOVE      ONE,FLGCO
          MOVE      ONE,FLGST
          MOVE      ONE,FLGD1
          MOVE      ONE,FLGD2
          MOVE      ONE,FLGD3
          MOVE      ONE,FLGUN
          MOVE      ONE,FLGDO
          MOVE      ONE,FLGES
          MOVE      ONE,FLGPT
          MOVE      ONE,FLGSN
          MOVE      ONE,FLGPL
          MOVE      ONE,FLGRS
          MOVE      ONE,FLGRR 
          MOVE      ONE,FLGRD
          MOVE      ONE,FLGPC
          MOVE      ONE,FLGRAD
          MOVE      ONE,FLGDOS
          MOVE      ONE,FLGUDF1
          MOVE      ONE,FLGUDF2
          MOVE      ONE,FLGUDF3
          MOVE      ONE,FLGUDF4
          MOVE      ZERO,TOTLFLAG
          RETURN
+
.**************************************************************************
.*                                TALY0000                                *
.*        Tally the procedures that suit the search criteria.             *
.**************************************************************************
TALY0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Searching...";
.
          MOVE      ZERO,FORM8         * zero the number of records counter
          PACK      KEY27,KSTDATE1,SP20     * start date on list
          CALL      RDSTREA3
TALY2222
          CALL      RDKTREA3           * read by date on list index
          BRANCH    OVRCD,TALY6666
.
          CALL      MTCH0000           * match record against search criteria
          BRANCH    EXIT,TALY2222      * exit=1 if match failed
          ADD       ONE,FORM8          * increment the found record counter
          GOTO      TALY2222
TALY6666
          DISPLAY   *P1:24,*EL,"Total Records : ",*V2LON,FORM8,*HOFF,".  ";
          CALL      HITENTER
.
TALY9999  RETURN
+
.**************************************************************************
.*                              SCRP0000                                  *
.*                      parameter screen layout                           *
.**************************************************************************
SCRP0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 1",*HOFF,". U/R Number          : ":
                    "________   To ________  ":
                    *P1:5,*V2LON," 2",*HOFF,". ",WTCNTPDS," Code      : ":
                    "_________  To _________ ":
                    *P1:6,*V2LON," 3",*HOFF,". ",WTCNTPDS," Status    : ":
                    "__________ To __________":
                    *P1:7,*V2LON," 4",*HOFF,". Date On List        : ":
                    "__/__/____ To __/__/____ ":
                    *P1:8,*V2LON," 5",*HOFF,". Last Review Date    : ":
                    "__/__/____ To __/__/____ ":
                    *P1:9,*V2LON," 6",*HOFF,". Scheduled Admission : ":
                    "__/__/____ To __/__/____ ":
                    *P1:10,*V2LON," 7",*HOFF,". ",DDESCWU,*P25:10,": ":
                    "___        To ___ ":
                    *P1:11,*V2LON," 8",*HOFF,". Attending Doctor    : ":
                    "______     To ______ ":
                    *P1:12,*V2LON," 9",*HOFF,". Estimated Stay      : ":
                    "_____      To _____ ":
                    *P1:13,*V2LON,"10",*HOFF,". Priority Code       : ":
                    "___ ":
                    *P1:14,*V2LON,"11",*HOFF,". Short Notice        : ":
                    "___ ":
                    *P1:15,*V2LON,"12",*HOFF,". ",CATADES,*P25:15,": ":
                    "___ ":
                    *P1:16,*V2LON,"13",*HOFF,". Resident Status     : ":
                    "___ ":
                    *P1:17,*V2LON,"14",*HOFF,". Removal Reason      : ":
                    "___ ":
                    *P1:18,*V2LON,"15",*HOFF,". Removal Date        : ":
                    "__/__/____ To __/__/____ ":
                    *P1:19,*V2LON,"16",*HOFF,". Re-Assessment Date  : ":
                    "__/__/____ To __/__/____ ":
                    *P1:20,*V2LON,"17",*HOFF,". Date on System      : ":
                    "__/__/____ To __/__/____ ":
                    *P1:21,*V2LON,"18",*HOFF,". Total Only          :"
.
          BRANCH    TOTLFLAG,SCRP0100
          DISPLAY   *P27:21,*V2LON,"No ";
          GOTO      SCRP0150
.
SCRP0100  DISPLAY   *P27:21,*V2LON,"Yes";
.
.         set up for the optional fields
.
SCRP0150  MOVE      "18",FORM2              * set item counter
          MOVE      ZERO,IMON               * clear optional counter
.
.         see if displaying Patient Category
.
          COMPARE   ONE,WTCNUCLS
          GOTO      SCRP0200 IF NOT EQUAL   * not using it
.
          PACK      KEY5,ANSP,SP4
          CALL      RDCODE1
          ADD       ONE,FORM2               * add to item counter
          ADD       ONE,IMON                * add to optional field counter
          STORE     ONE,IMON,MOPT01,MOPT02,MOPT03,MOPT04,MOPT05
          DISPLAY   *P41:21,*V2LON,FORM2,*HOFF,". ",TDESC,": ___";
.
.         see if using UD field 1
.
SCRP0200  COMPARE   ONE,HUSR1
          GOTO      SCRP0210 IF NOT EQUAL
.
          ADD       ONE,FORM2               * add to item counter
          ADD       ONE,IMON                * add to optional field counter
          STORE     TWO,IMON,MOPT01,MOPT02,MOPT03,MOPT04,MOPT05
          LOAD      XPOS1,IMON,SIXTY7,TWENTY7,TWENTY7,SIXTY7,SIXTY7
          LOAD      CCOL,IMON,FORTY1,ONE,ONE,FORTY1,FORTY1
          LOAD      YPOS1,IMON,TWENTY1,TWENTY2,TWENTY3,TWENTY2,TWENTY3
          DISPLAY   *PCCOL:YPOS1,*V2LON,FORM2,*HOFF,". ",HUSRDES1,": ___";
.
.         see if using UD field 2
.
SCRP0210  COMPARE   ONE,HUSR2
          GOTO      SCRP0220 IF NOT EQUAL
.
          ADD       ONE,FORM2
          ADD       ONE,IMON                * add to optional field counter
          STORE     THREE,IMON,MOPT01,MOPT02,MOPT03,MOPT04,MOPT05
          LOAD      XPOS2,IMON,SIXTY7,TWENTY7,TWENTY7,SIXTY7,SIXTY7
          LOAD      CCOL,IMON,FORTY1,ONE,ONE,FORTY1,FORTY1
          LOAD      YPOS2,IMON,TWENTY1,TWENTY2,TWENTY3,TWENTY2,TWENTY3
          DISPLAY   *PCCOL:YPOS2,*V2LON,FORM2,*HOFF,". ",HUSRDES2,": ___";
.
.         see if using UD field 3
.
SCRP0220  COMPARE   ONE,HUSR3
          GOTO      SCRP0230 IF NOT EQUAL
.
          ADD       ONE,FORM2
          ADD       ONE,IMON                * add to optional field counter
          STORE     FOUR,IMON,MOPT01,MOPT02,MOPT03,MOPT04,MOPT05
          LOAD      XPOS3,IMON,SIXTY7,TWENTY7,TWENTY7,SIXTY7,SIXTY7
          LOAD      CCOL,IMON,FORTY1,ONE,ONE,FORTY1,FORTY1
          LOAD      YPOS3,IMON,TWENTY1,TWENTY2,TWENTY3,TWENTY2,TWENTY3
          DISPLAY   *PCCOL:YPOS3,*V2LON,FORM2,*HOFF,". ",HUSRDES3,": ___";
.
.         see if using UD field 4
.
SCRP0230  COMPARE   ONE,HUSR4
          GOTO      SCRP0999 IF NOT EQUAL
.
          ADD       ONE,FORM2
          ADD       ONE,IMON                * add to optional field counter
          STORE     FIVE,IMON,MOPT01,MOPT02,MOPT03,MOPT04,MOPT05
          LOAD      XPOS4,IMON,SIXTY7,TWENTY7,TWENTY7,SIXTY7,SIXTY7
          LOAD      CCOL,IMON,FORTY1,ONE,ONE,FORTY1,FORTY1
          LOAD      YPOS4,IMON,TWENTY1,TWENTY2,TWENTY3,TWENTY2,TWENTY3
          DISPLAY   *PCCOL:YPOS4,*V2LON,FORM2,*HOFF,". ",HUSRDES4,": ___";
.
SCRP0999  RETURN
+
.**************************************************************************
.*                              SCRP1000                                  *
.*                     parameter screen details                           *
.**************************************************************************
SCRP1000  BRANCH    FLGUR,SCRP1010
          DISPLAY   *P27:4,*V2LON,KSTUR,*P41:4,KTOUR;
.
SCRP1010  BRANCH    FLGCO,SCRP1020
          PACK      STRTCODE,KSTCODE,SP10
          PACK      ENDCODE,KTOCODE,SP10
          MOVE      "5",CVERT
          CALL      SRNG0000           * display pair of code range
.
SCRP1020  BRANCH    FLGST,SCRP1030
          DISPLAY   *P27:6,*V2LON,STATDES1,*P41:6,STATDES2;
.
SCRP1030  BRANCH    FLGD1,SCRP1040
          PACK      KEY16,KSTDATE1,KTODATE1
          MOVE      "7",CVERT
          CALL      SDAT0000
.
SCRP1040  BRANCH    FLGD2,SCRP1050
          PACK      KEY16,KSTDATE2,KTODATE2
          MOVE      "8",CVERT
          CALL      SDAT0000
.
SCRP1050  BRANCH    FLGD3,SCRP1060
          PACK      KEY16,KSTDATE3,KTODATE3
          MOVE      "9",CVERT
          CALL      SDAT0000
.
SCRP1060  BRANCH    FLGUN,SCRP1070
          PACK      STRTCODE,KSTUNIT,SP10
          PACK      ENDCODE,KTOUNIT,SP10
          MOVE      "10",CVERT
          CALL      SRNG0000           * display pair of code range
.
SCRP1070  BRANCH    FLGDO,SCRP1080
          PACK      STRTCODE,KSTDOC,SP10
          PACK      ENDCODE,KTODOC,SP10
          MOVE      "11",CVERT
          CALL      SRNG0000           * display pair of code range
.
SCRP1080  BRANCH    FLGES,SCRP1090
          DISPLAY   *P27:12,*V2LON,KSTSTAY,*P41:12,KTOSTAY;
.
SCRP1090  BRANCH    FLGPT,SCRP1100
          DISPLAY   *P27:13,*V2LON,KSTPTY,*HOFF,*P38:13,DESCTP;
.
SCRP1100  BRANCH    FLGSN,SCRP1110
          DISPLAY   *P27:14,*V2LON,KSTNOT,*HOFF,*P38:14,DESCNOT;
.
SCRP1110  BRANCH    FLGPL,SCRP1120
          DISPLAY   *P27:15,*V2LON,KSTPCLS,*HOFF,*P38:15,DESCPCLS;
.
SCRP1120  BRANCH    FLGRS,SCRP1130
          DISPLAY   *P27:16,*V2LON,KSTWTYP,*HOFF,*P38:16,DESCPTYP;
SCRP1130
          BRANCH    FLGRR,SCRP1140
          DISPLAY   *P27:17,*V2LON,KREMREAS,*HOFF,*P38:17,DESCBC;
SCRP1140
          BRANCH    FLGRD,SCRP1150
          PACK      KEY16,KREMDAT1,KREMDAT2
          MOVE      "18",CVERT
          CALL      SDAT0000
SCRP1150
          BRANCH    FLGRAD,SCRP1160
          PACK      KEY16,KREADAT1,KREADAT2
          MOVE      "19",CVERT
          CALL      SDAT0000
SCRP1160
          BRANCH    FLGDOS,SCRP1170
          PACK      KEY16,KDOSDAT1,KDOSDAT2
          MOVE      "20",CVERT
          CALL      SDAT0000
.
.         display of optional fields data values
.
SCRP1170  MOVE      ZERO,IMON               * clear counter
.
SCRP1180  ADD       ONE,IMON
          COMPARE   "5",IMON
          GOTO      SCRP9999 IF EQUAL       * finished
.
          LOAD      CCOL,IMON,SIXTY7,TWENTY7,TWENTY7,SIXTY7,SIXTY7
          LOAD      CVERT,IMON,TWENTY1,TWENTY2,TWENTY3,TWENTY2,TWENTY3
.
          LOAD      ICENT,IMON,MOPT01,MOPT02,MOPT03,MOPT04,MOPT05
          LOAD      ACODE,ICENT,KSTPCAT,USERDEF1,USERDEF2,USERDEF3,USERDEF4
.
          MATCH     SP3,ACODE
          GOTO      SCRP1180 IF EQUAL       * no code to display
.
          DISPLAY   *PCCOL:CVERT,*V2LON,ACODE;
          GOTO      SCRP1180
.
SCRP9999  RETURN
+
.*****************************************************************************
.*        Show range of codes selected                                       *
.*****************************************************************************
SRNG0000  MATCH     SP10,STRTCODE
          GOTO      SRNG2222 IF NOT EQUAL
          DISPLAY   *P27:CVERT,*V2LON,START;
          GOTO      SRNG4444
SRNG2222
          DISPLAY   *P27:CVERT,*V2LON,STRTCODE;
SRNG4444
          MATCH     "ZZZ",ENDCODE
          GOTO      SRNG6666 IF NOT EQUAL
          DISPLAY   *P41:CVERT,*V2LON,FINISH;
          GOTO      SRNG9999
SRNG6666
          DISPLAY   *P41:CVERT,*V2LON,ENDCODE;
SRNG9999  RETURN
+
.*****************************************************************************
.*        Display date to screen given date in KEY16 and CVERT               *
.*****************************************************************************
SDAT0000  UNPACK    KEY16,FDATE1,FDATE2
          MATCH     SP8,FDATE1
          GOTO      SDAT2222 IF NOT EQUAL
          DISPLAY   *P27:CVERT,*V2LON,START;
          GOTO      SDAT4444
SDAT2222
          UNPACK    FDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P27:CVERT,*V2LON,CPCDATE;
SDAT4444
          MATCH     "99999999",FDATE2
          GOTO      SDAT6666 IF NOT EQUAL
          DISPLAY   *P41:CVERT,*V2LON,FINISH;
          GOTO      SDAT9999
SDAT6666
          UNPACK    FDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P41:CVERT,*V2LON,CPCDATE;
.
SDAT9999  RETURN
+
.******************************************************************************
.*                                GOPT0000                              BD    *
.*        GOPT is gets the item selected or a command option                  *
.*        MASK  set the replace codes                                         *
.*        CCOL  x position for keyin                                          *
.******************************************************************************
GOPT0000  DISPLAY   *PCCOL:24,*EL,UNDLN2;
          KEYIN     *PCCOL:24,*JR,*ZF,*V2LON,REPLY;
          ENDSET    REPLY
          APPEND    SP2,REPLY
          RESET     REPLY
          REPLACE   UPPLOW,REPLY
.
          TYPE      REPLY              * a number was entered
          GOTO      GOPT5555 IF EQUAL
.
          REPLACE   MASK,REPLY 
          TYPE      REPLY
          GOTO      GOPT9000 IF EQUAL
GOPT4444
          BEEP
          GOTO      GOPT0000
GOPT5555
          MOVE      REPLY,CCITEM
          COMPARE   CCITEM,ZERO
          GOTO      GOPT4444 IF NOT LESS
          MOVE      ZERO,EXIT          * valid number exit=0
          GOTO      GOPT9999
GOPT9000
          MOVE      REPLY,EXIT
.
GOPT9999  RETURN
+
.******************************************************************************
.*                                SELT0000                                    *
.*                     select parameter(s) to fill in                         *
.******************************************************************************
SELT0000  DISPLAY   *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF,")ost, ":
                    "(",*V2LON,"C",*HOFF,")ancel, ":
                    "(",*V2LON,"E",*HOFF,")xit to system menu : __";
.
          MOVE      "P1C2E3",MASK      * set the replace command type
          MOVE      "56",CCOL          * set the keyin x pos for GOPT
.
          CALL      GOPT0000           * line selected in CCITEM
.
          BRANCH    EXIT,SELT0900,SELT0888,SELT0990
          BRANCH    CCITEM,SELT4000,SELT5000,SELT6000,SELT7000,SELT8000:
                           SELT9000,SELU1000,SELU2000,SELU3000,SELU4000:
                           SELU5000,SELU6000,SELU7000,GREM0000,GRED0000:
                           GREA0000,GDOS0000,TOTL0000,SELT0222,SELT0222:
                           SELT0222,SELT0222,SELT0222
.
SELT0111  BEEP
          GOTO      SELT0000
.
.         one of the optional fields was selected so find out which one
.
SELT0222  MOVE      ZERO,FORM2              * clear for invalid option
          SUB       "18",CCITEM             * get the optional field item #
          LOAD      FORM2,CCITEM,MOPT01,MOPT02,MOPT03,MOPT04,MOPT05
.
          BRANCH    FORM2,SELU8000,GUSR1000,GUSR2000,GUSR3000,GUSR4000
          GOTO      SELT0111
.
SELT0888  CALL      CLRP0000           * change mode
          CALL      SCRP0000
          GOTO      SELT0000
SELT0900
          MOVE      ZERO,EXIT          * post, begin search
          MOVE      ZERO,EXTRFLAG
          GOTO      SELT0999
SELT0990
          MOVE      ONE,EXIT           * exit program
.
SELT0999  RETURN
+
.------------------------------------------------------------------------------
. ----- Keyin UR range -----
.------------------------------------------------------------------------------
SELT4000  MOVE      "       0",KSTUR
          MOVE      ONE,FLGUR
          DISPLAY   *P27:4,*EL,"________   To ________ ";
.
          MOVE      FOUR,CVERT
          MOVE      LINE27,CCOL
          MOVE      ONE,SRCHOPT
          MOVE      ONE,SRCHSYS
          CALL      KURN
.
          COMPARE   ONE,EXIT
          GOTO      SELT4300 IF EQUAL
          MOVE      CPPURNO,KSTUR
.
SELT4300  MOVE      "99999999",KTOUR
          DISPLAY   *P27:4,*V2LON,KSTUR,*HOFF,*P38:4,"To",*P41:4,*EL,UNDLN8;
.          
          MOVE      FOUR,CVERT
          MOVE      LINE41,CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ONE,SRCHSYS
          CALL      KURN
.
          COMPARE   ONE,EXIT
          GOTO      SELT4400 IF EQUAL
          MOVE      CPPURNO,KTOUR
.
SELT4400  DISPLAY   *P27:4,*V2LON,KSTUR,*HOFF,*P38:4,"To":
                    *P41:4,*V2LON,KTOUR;
          MATCH     KSTUR,KTOUR
          GOTO      SELT4500 IF LESS
          GOTO      SELT4600
.
SELT4500  DISPLAY   *P52:4,*V2LON,"End U/R must be > start U/R",*W2;
          GOTO      SELT4000
.
SELT4600  MOVE      ZERO,FLGUR                   * got a value
          GOTO      SELT4999
.
SELT4700  DISPLAY   *P40:24,*EL,*V2LON,"U/R doesn't exist",*W2;
          GOTO      SELT4300
.
SELT4999  GOTO      SELT0000
.
.------------------------------------------------------------------------------
. ----- Keyin procedure code range -----
.------------------------------------------------------------------------------
SELT5000  MOVE      ONE,FLGCO
          DISPLAY   *P27:5,*EL,"_________  To _________ ";
SELT5100
          KEYIN     *P27:5,*V2LON,KSTCODE,*P27:5,*DV,KSTCODE;
SELT5110
          ENDSET    KSTCODE
          APPEND    SP9,KSTCODE
          RESET     KSTCODE
.
          MATCH     SP9,KSTCODE
          GOTO      SELT5111 IF NOT EQUAL
          DISPLAY   *P27:5,*V2LON,START
          GOTO      SELT5200
SELT5111
          CMATCH    QUEST,KSTCODE
          GOTO      SELT5200 IF NOT EQUAL
          MOVE      ONE,DISPACT
          CALL      WATDSPRO
          DISPLAY   *P1:24,*EL,WTCNTPDS," Code : __________";
          KEYIN     *P18:24,*V2LON,KSTCODE;
          CALL      SCRP0000           * redisplay the screen
          CALL      SCRP1000
          DISPLAY   *P27:5,*V2LON,KSTCODE;
          GOTO      SELT5110
SELT5200
          KEYIN     *P41:5,*V2LON,*EL,KTOCODE,*P41:5,*DV,KTOCODE;
SELT5211
          ENDSET    KTOCODE
          APPEND    SP9,KTOCODE
          RESET     KTOCODE
.
          MATCH     SP9,KTOCODE        * test for return hit
          GOTO      SELT5222 IF NOT EQUAL
          DISPLAY   *P41:5,*V2LON,FINISH;
          MOVE      Z30,KTOCODE
          GOTO      SELT5300
SELT5222
          CMATCH    QUEST,KTOCODE
          GOTO      SELT5300 IF NOT EQUAL
          MOVE      ONE,DISPACT
          CALL      WATDSPRO
          DISPLAY   *P1:24,*EL,WTCNTPDS," Code : __________";
          KEYIN     *P18:24,*V2LON,KTOCODE;
          MOVE      ZERO,FLGCO
          CALL      SCRP0000
          CALL      SCRP1000
          MOVE      ONE,FLGCO
          GOTO      SELT5211
.
SELT5300  MATCH     KSTCODE,KTOCODE
          GOTO      SELT5500 IF LESS
          MOVE      ZERO,FLGCO         * got a value
          GOTO      SELT0000           * return equivalent
SELT5500
          DISPLAY   *P1:24,*EL,*B,"End Code must appear after Start Code.  ";
          CALL      HITENTER
          GOTO      SELT5000
.
.------------------------------------------------------------------------------
. ----- Keyin Procedure status -----
.------------------------------------------------------------------------------
SELT6000  MOVE      ONE,FLGST
          DISPLAY   *P27:6,*EL,"__________ To __________";
          DISPLAY   *P1:24,*V2LON,"1",*HOFF,"=Unscheduled, ":
                    *V2LON,"2",*HOFF,"=Scheduled, ":
                    *V2LON,"3",*HOFF,"=Pre-Admitted, ":
                    *V2LON,"4",*HOFF,"=Admitted, ":
                    *V2LON,"5",*HOFF,"=Discharged, ":
                    *V2LON,"6",*HOFF,"=Removed";
.
          KEYIN     *P27:6,*V2LON,KSTATUS1;
          COMPARE   ZERO,KSTATUS1
          GOTO      SELT6111 IF NOT EQUAL
          MOVE      START,STATDES1
          GOTO      SELT6222
SELT6111
          MOVE      KSTATUS1,WMSTAT
          CALL      VSTA0000           * validate procedure status
          BRANCH    EXIT,SELT6000
          MOVE      DIM10,STATDES1
SELT6222
          DISPLAY   *P27:6,*V2LON,STATDES1;
SELT6333
          DISPLAY   *P1:24,*V2LON,"1",*HOFF,"=Unscheduled, ":
                    *V2LON,"2",*HOFF,"=Scheduled, ":
                    *V2LON,"3",*HOFF,"=Pre-admitted, ":
                    *V2LON,"4",*HOFF,"=Admitted, ":
                    *V2LON,"5",*HOFF,"=Discharged, ":
                    *V2LON,"6",*HOFF,"=Removed";
          KEYIN     *P41:6,*V2LON,KSTATUS2;
          COMPARE   ZERO,KSTATUS2
          GOTO      SELT6444 IF NOT EQUAL
          MOVE      FINISH,STATDES2
          MOVE      NINE,KSTATUS2      * set end range to highest form
          GOTO      SELT6555
SELT6444
          MOVE      KSTATUS2,WMSTAT
          CALL      VSTA0000           * validate procedure status
          BRANCH    EXIT,SELT6333
          MOVE      DIM10,STATDES2
SELT6555
          DISPLAY   *P41:6,*V2LON,STATDES2;
.
          COMPARE   KSTATUS1,KSTATUS2
          GOTO      SELT6888 IF LESS
          MOVE      ZERO,FLGST                   * got a value
          GOTO      SELT0000
SELT6888
          DISPLAY   *P1:24,*B,*EL,"End Code must be greater than Start Code.  ";
          CALL      HITENTER
          GOTO      SELT6000
.
.*****************************************************************************
.*        Validate procedure status                                          *
.*        load procedure description into DIM10                              *
.*****************************************************************************
VSTA0000  MOVE      SP20,TDESC
          COMPARE   WMSTAT,SIX         * test number is within range
          GOTO      VSTA8888 IF LESS 
          LOAD      TDESC,WMSTAT,STAT1,STAT2,STAT3,STAT4,STAT5,STAT6,STAT7,STAT8
          MOVE      TDESC,DIM10
          MOVE      ZERO,EXIT
          GOTO      VSTA9999
VSTA8888
          DISPLAY   *P1:24,*B,*EL,"Invalid Procedure Status.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
VSTA9999  RETURN
+
.*****************************************************************************
.*                                DRNG0000                                   *
.*        CVERT : screen line position must be set bfore call                *
.*        DATE1 : Normal start date display dd/mm/cc/yy                      *
.*        DATE2 : Normal end   date display dd/mm/cc/yy                      *
.*        FDATE1 : filedate start date ccyymmdd                              *
.*        FDATE2 : filedate end date ccyymmdd                                *
.*****************************************************************************
DRNG0000  DISPLAY   *P27:CVERT,*EL,"__/__/____ To __/__/____ ";
          MOVE      SP10,DATE1         * clear parameter variables
          MOVE      SP10,DATE2
          MOVE      SP10,FDATE1
          MOVE      SP10,FDATE2
          UNPACK    SP10,CDAY,CMON,CCENT,CYEAR
.
          MOVE      CCC,CCENT
          MOVE      ZERO,CHIGHLT
          MOVE      "27",CCOL
          CALL      KEYDATE            * get start date
          COMPARE   ZERO,OVRCD         * test for return hit
          GOTO      DRNG1111 IF EQUAL
          MOVE      START,DATE1        * set start if return hit on first date
          DISPLAY   *PCCOL:CVERT,*V2LON,START;
          GOTO      DRNG2222
DRNG1111
          PACK      FDATE1,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",FDATE1
          CALL      PACDATE
          MOVE      CPCDATE,DATE1
          REPLACE   " 0",DATE1
DRNG2222
          UNPACK    SP10,CDAY,CMON,CCENT,CYEAR
          MOVE      CCC,CCENT
          MOVE      "41",CCOL
          CALL      KEYDATE            * get second date range
          COMPARE   ZERO,OVRCD         * test for return hit
          GOTO      DRNG3333 IF EQUAL
          MOVE      "99999999",FDATE2  * set filedate to highest value
          MOVE      FINISH,DATE2
          DISPLAY   *PCCOL:CVERT,*V2LON,FINISH;
          GOTO      DRNG4444
DRNG3333
          PACK      FDATE2,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",FDATE2
          CALL      PACDATE
          MOVE      CPCDATE,DATE2
          REPLACE   " 0",DATE2
DRNG4444
          MATCH     FDATE1,FDATE2      * test dates are correct sequence
          GOTO      DRNG5555 IF LESS
          PACK      KEY40,FDATE1,FDATE2,DATE1,DATE2
          GOTO      DRNG9999
DRNG5555
          DISPLAY   *P1:24,*EL,*B,"To Date greater than From Date.  ";
          CALL      HITENTER
          GOTO      DRNG0000
.
DRNG9999  RETURN
+
.*****************************************************************************
.------------------------------------------------------------------------------
. ----- Keyin date on list range -----
.------------------------------------------------------------------------------
SELT7000  MOVE      SEVEN,CVERT
          CALL      DRNG0000           * get date range
          UNPACK    KEY40,KSTDATE1,KTODATE1
          MOVE      ZERO,FLGD1
          GOTO      SELT0000
.
.------------------------------------------------------------------------------
. ----- Keyin last review date range -----
.------------------------------------------------------------------------------
SELT8000  MOVE      EIGHT,CVERT
          CALL      DRNG0000                     * get date range
          UNPACK    KEY40,KSTDATE2,KTODATE2
          MOVE      ZERO,FLGD2
          GOTO      SELT0000
.
.------------------------------------------------------------------------------
. ----- Keyin scheduled admission date range -----
.------------------------------------------------------------------------------
SELT9000  MOVE      NINE,CVERT
          CALL      DRNG0000                     * get date range
          UNPACK    KEY40,KSTDATE3,KTODATE3
          MOVE      ZERO,FLGD3
          GOTO      SELT0000
+
.------------------------------------------------------------------------------
. ----- Keyin unit range -----
.------------------------------------------------------------------------------
SELU1000  MOVE      ONE,FLGUN
          DISPLAY   *P27:10,*EL,"___        To ___        ";
          KEYIN     *P27:10,*V2LON,KSTUNIT,*P27:10,*DV,KSTUNIT;
SELU1100
          ENDSET    KSTUNIT
          APPEND    SP3,KSTUNIT
          RESET     KSTUNIT
.
          MATCH     SP3,KSTUNIT
          GOTO      SELU1111 IF NOT EQUAL
          DISPLAY   *P27:10,*V2LON,START;
          GOTO      SELU1200
SELU1111
          CMATCH    QUEST,KSTUNIT
          GOTO      SELU1200 IF NOT EQUAL
          MOVE      CODEWU,CODE
          CALL      PATCODDS
          DISPLAY   *P1:24,*EL,"Unit Code : ___";
          KEYIN     *P13:24,*V2LON,KSTUNIT;
          CALL      SCRP0000
          CALL      SCRP1000
          ENDSET    KSTUNIT
          APPEND    SP3,KSTUNIT
          RESET     KSTUNIT
          MATCH     KSTUNIT,SP3
          GOTO      SELU1000 IF EQUAL
          DISPLAY   *P27:10,*V2LON,KSTUNIT;
          GOTO      SELU1100
SELU1200  
          KEYIN     *P41:10,*V2LON,KTOUNIT,*P41:10,*DV,KTOUNIT;
SELU1220
          ENDSET    KTOUNIT
          APPEND    SP3,KTOUNIT
          RESET     KTOUNIT
.
          MATCH     SP3,KTOUNIT
          GOTO      SELU1222 IF NOT EQUAL
          DISPLAY   *P41:10,*V2LON,FINISH;
          MOVE      Z30,KTOUNIT
SELU1222
          CMATCH    QUEST,KTOUNIT
          GOTO      SELU1300 IF NOT EQUAL
          MOVE      CODEWU,CODE
          CALL      PATCODDS
          DISPLAY   *P1:24,*EL,"Unit Code : ___";
          KEYIN     *P13:24,*V2LON,KTOUNIT;
          MOVE      ZERO,FLGUN
          CALL      SCRP0000
          CALL      SCRP1000
          ENDSET    KTOUNIT
          APPEND    SP3,KTOUNIT
          RESET     KTOUNIT
          MATCH     KTOUNIT,SP3
          GOTO      SELU1200 IF EQUAL
          MOVE      ONE,FLGUN
          GOTO      SELU1220
SELU1300
          MATCH     KSTUNIT,KTOUNIT
          GOTO      SELU1500 IF LESS
          MOVE      ZERO,FLGUN
          GOTO      SELT0000
SELU1500
          DISPLAY   *P1:24,*EL,*B,"End value greater than Start value.  ";
          CALL      HITENTER
          GOTO      SELU1000
.
.------------------------------------------------------------------------------
. ----- Keyin doctor range -----
.------------------------------------------------------------------------------
SELU2000  MOVE      ONE,FLGDO
          DISPLAY   *P27:11,*EL,"______     To ______     ";
.
          MOVE      "27",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,SELU2100,SELU2000
.
          MOVE      DCODE,KSTDOC
          GOTO      SELU2111
.
SELU2100  DISPLAY   *P27:11,*V2LON,START;
          MOVE      SP6,KSTDOC
.
SELU2111  MOVE      "41",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,SELU2222,SELU2111
.
          MOVE      DCODE,KTODOC
          GOTO      SELU2244
.
SELU2222  DISPLAY   *P41:11,*V2LON,FINISH;
          MOVE      Z30,KTODOC
.
SELU2244  MATCH     KSTDOC,KTODOC
          GOTO      SELU2500 IF LESS
          MOVE      ZERO,FLGDO
          GOTO      SELT0000
SELU2500
          DISPLAY   *P1:24,*EL,*B,"End value greater than Start value.  ";
          CALL      HITENTER
          GOTO      SELU2000
.
.------------------------------------------------------------------------------
. ----- Keyin estimated stay range -----
.------------------------------------------------------------------------------
SELU3000  MOVE      ONE,FLGES
          DISPLAY   *P27:12,*EL,"_____      To _____ ";
.
          MOVE      ZERO,KSTSTAY
          KEYIN     *P27:12,*V2LON,*RV,KSTSTAY,*P27:12,*DV,KSTSTAY;
.
          MOVE      "99999",KTOSTAY
          KEYIN     *P41:12,*V2LON,*RV,KTOSTAY,*P41:12,*DV,KTOSTAY;
.
          COMPARE   KSTSTAY,KTOSTAY
          GOTO      SELU3500 IF LESS
          MOVE      ZERO,FLGES
          GOTO      SELT0000
SELU3500
          DISPLAY   *P1:24,*EL,*B,"End value greater than Start value.  ";
          CALL      HITENTER
          GOTO      SELU3000
.
.******************************************************************************
.*                                GCOD0000                                    *
.*               get code and description from codes file.                    *
.*        YPOS and XPOS : set to the a line position on screen                *
.*        FILECODE : is the return code                                       *
.*        CODEDESC : is the return description                                *
.******************************************************************************
GCOD0000  MOVE      ONE,SCRNFLAG      * set the screen flag
.
GCOD1000  DISPLAY   *PXPOS:YPOS,*EL,UNDLN3,*PXPOS:YPOS;
.
GCOD1111  KEYIN     *V2LON,FILECODE
          ENDSET    FILECODE
          APPEND    SP3,FILECODE
          RESET     FILECODE
.
          MATCH     SP3,FILECODE       * test for return hit
          GOTO      GCOD7777 IF EQUAL
.
          CMATCH    "?",FILECODE
          GOTO      GCOD6666 IF NOT EQUAL
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG      * set flag to redisplay
GCOD4444
          DISPLAY   *P1:24,*EL,"Code : ___",*P8:24;
          GOTO      GCOD1111
GCOD6666
          PACK      KEY5,CODE,FILECODE,SP5
          CALL      RDCODE1            * read code file for entered code
          BRANCH    OVRCD,GCOD8000     * tell user his code not on file
          MOVE      TDESC,CODEDESC     * copy the description
          CALL      REDP0000           * redisplay screen if needed
          DISPLAY   *PXPOS:YPOS,*V2LON,FILECODE;
          COMPARE   YPOS,TEN7          * test if user define keyin
          GOTO      GCOD9999 IF LESS 
          DISPLAY   *P38:YPOS,CODEDESC;
          GOTO      GCOD9999
GCOD7777  
          CALL      REDP0000           * redisplay screen if needed
          DISPLAY   *PXPOS:YPOS,UNDLN3;
.
          BRANCH    SCRNFLAG,GCOD7900
          GOTO      GCOD0000
.
GCOD7900  PACK      CODEDESC,SP20      * clear description
          GOTO      GCOD9999
GCOD8000 
          DISPLAY   *P1:24,*B,*EL,"Code not on file.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,GCOD1000
          GOTO      GCOD4444
.
GCOD9999  RETURN
+
.******************************************************************************
REDP0000  BRANCH    SCRNFLAG,REDP9999
          CALL      SCRP0000
          CALL      SCRP1000
REDP9999  RETURN
+
.------------------------------------------------------------------------------
. ----- Keyin priority -----
.------------------------------------------------------------------------------
SELU4000  MOVE      ONE,FLGPT
          MOVE      CODETP,CODE        * set category
          MOVE      "13",YPOS
          MOVE      "27",XPOS
          CALL      GCOD0000           * get code routine
          MOVE      FILECODE,KSTPTY
          MOVE      CODEDESC,DESCTP
          MATCH     SP3,KSTPTY         * test for return hit
          GOTO      SELU4999 IF EQUAL
          MOVE      ZERO,FLGPT
SELU4999
          GOTO      SELT0000
.
.------------------------------------------------------------------------------
. ----- Keyin short notice -----
.------------------------------------------------------------------------------
SELU5000  MOVE      ONE,FLGSN
          MOVE      CODEWS,CODE
          MOVE      "14",YPOS
          MOVE      "27",XPOS
          CALL      GCOD0000           * get code routine
          MOVE      FILECODE,KSTNOT
          MOVE      CODEDESC,DESCNOT
          MATCH     SP3,KSTNOT         * test for return hit
          GOTO      SELU5999 IF EQUAL
          MOVE      ZERO,FLGSN
SELU5999
          GOTO      SELT0000
.
.------------------------------------------------------------------------------
. ----- Keyin patient classification -----
.------------------------------------------------------------------------------
SELU6000  MOVE      ONE,FLGPL
          MOVE      CODEA,CODE
          MOVE      "15",YPOS
          MOVE      "27",XPOS
          CALL      GCOD0000           * get code routine
          MOVE      FILECODE,KSTPCLS
          MOVE      CODEDESC,DESCPCLS
          MATCH     SP3,KSTPCLS
          GOTO      SELU6999 IF EQUAL
          MOVE      ZERO,FLGPL
SELU6999
          GOTO      SELT0000
.
.------------------------------------------------------------------------------
. ----- Keyin waiting type : resident status -----
.------------------------------------------------------------------------------
SELU7000  MOVE      ONE,FLGRS
          MOVE      CODERS,CODE
          MOVE      "16",YPOS
          MOVE      "27",XPOS
          CALL      GCOD0000           * get code routine
          MOVE      FILECODE,KSTWTYP
          MOVE      CODEDESC,DESCPTYP
          MATCH     SP3,KSTWTYP
          GOTO      SELU7999 IF EQUAL
          MOVE      ZERO,FLGRS
SELU7999
          GOTO      SELT0000
.
.------------------------------------------------------------------------------
. ----- Keyin Patient Category -----
.------------------------------------------------------------------------------
SELU8000  COMPARE   ONE,WTCNUCLS
          GOTO      SELU8999 IF NOT EQUAL   * not using Pat Cat
.
          MOVE      ONE,FLGPC               * signal as no code entered
          PACK      CODE,ANSP,SP1
          MOVE      "21",YPOS
          MOVE      "67",XPOS
          CALL      GCOD0000           * get code routine
          MOVE      FILECODE,KSTPCAT
          MOVE      CODEDESC,DESCPCAT
          MATCH     SP3,KSTPCAT        * test for return hit
          GOTO      SELU8999 IF EQUAL
          MOVE      ZERO,FLGPC              * a code entered
SELU8999
          GOTO      SELT0000
.
.******************************************************************************
.*        Get Removal reason                                                  *
.******************************************************************************
GREM0000  MOVE      ONE,FLGRR
          PACK      CODE,ANSB,ANSC
          MOVE      "17",YPOS
          MOVE      "27",XPOS
          CALL      GCOD0000           * get code routine
          MOVE      FILECODE,KREMREAS
          MOVE      CODEDESC,DESCBC
          MATCH     SP3,KREMREAS
          GOTO      GREM9999 IF EQUAL
          MOVE      ZERO,FLGRR
GREM9999
          GOTO      SELT0000
+
.******************************************************************************
.*        Get removal date                                                    *
.******************************************************************************
GRED0000  MOVE      "18",CVERT
          CALL      DRNG0000           * get date range
          UNPACK    KEY40,KREMDAT1,KREMDAT2
          MOVE      ZERO,FLGRD
          GOTO      SELT0000
+
.******************************************************************************
.*        Get Re-Assessment date                                              *
.******************************************************************************
GREA0000  MOVE      "19",CVERT
          CALL      DRNG0000           * get date range
          UNPACK    KEY40,KREADAT1,KREADAT2
          MOVE      ZERO,FLGRAD
          GOTO      SELT0000
+
.******************************************************************************
.*        Get Date on System                                                  *
.******************************************************************************
GDOS0000  MOVE      "20",CVERT
          CALL      DRNG0000           * get date range
          UNPACK    KEY40,KDOSDAT1,KDOSDAT2
          MOVE      ZERO,FLGDOS
          GOTO      SELT0000
+
.******************************************************************************
.         User Defined selected
.******************************************************************************
GUSR1000  MOVE      ONE,FLGUDF1             * UD field 1
          MOVE      YPOS1,YPOS
          MOVE      XPOS1,XPOS
          MOVE      CODEW1,CODE
          CALL      UDEF0000           * get code routine
          MOVE      FILECODE,USERDEF1
          MATCH     SP3,USERDEF1
          GOTO      GUSR1999 IF EQUAL
          MOVE      ZERO,FLGUDF1
GUSR1999
          GOTO      SELT0000
.
.******************************************************************************
GUSR2000  MOVE      ONE,FLGUDF2             * UD field 2
          MOVE      CODEW2,CODE
          MOVE      YPOS2,YPOS
          MOVE      XPOS2,XPOS
          CALL      UDEF0000           * get code routine
          MOVE      FILECODE,USERDEF2
          MATCH     SP3,USERDEF2
          GOTO      GUSR2999 IF EQUAL
          MOVE      ZERO,FLGUDF2
GUSR2999
          GOTO      SELT0000
.
.******************************************************************************
GUSR3000  MOVE      ONE,FLGUDF3             * UD field 3
          MOVE      CODEW3,CODE
          MOVE      YPOS3,YPOS
          MOVE      XPOS3,XPOS
          CALL      UDEF0000           * get code routine
          MOVE      FILECODE,USERDEF3
          MATCH     SP3,USERDEF3
          GOTO      GUSR3999 IF EQUAL
          MOVE      ZERO,FLGUDF3
GUSR3999
          GOTO      SELT0000
.
.******************************************************************************
GUSR4000  MOVE      ONE,FLGUDF4             * UD field 4
          MOVE      CODEW4,CODE
          MOVE      YPOS4,YPOS
          MOVE      XPOS4,XPOS
          CALL      UDEF0000           * get code routine
          MOVE      FILECODE,USERDEF4
          MATCH     SP3,USERDEF4
          GOTO      GUSR4999 IF EQUAL
          MOVE      ZERO,FLGUDF4
GUSR4999
          GOTO      SELT0000
+
.******************************************************************************
.*                                UDEF0000                                    *
.*               get code and description from codes file.                    *
.*        YPOS and XPOS : set to the a line position on screen                *
.*        FILECODE : is the return code                                       *
.******************************************************************************
UDEF0000  MOVE      ONE,SCRNFLAG      * set the screen flag
.
UDEF1000  DISPLAY   *PXPOS:YPOS,UNDLN3,*PXPOS:YPOS;
.
UDEF1111  KEYIN     *V2LON,FILECODE;
          ENDSET    FILECODE
          APPEND    SP3,FILECODE
          RESET     FILECODE
.
          MATCH     SP3,FILECODE       * test for return hit
          GOTO      UDEF7777 IF EQUAL
.
          CMATCH    "?",FILECODE
          GOTO      UDEF6666 IF NOT EQUAL
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG      * set flag to redisplay
.
UDEF4444  DISPLAY   *P1:24,*EL,"Code : ___",*P8:24;
          GOTO      UDEF1111
.
.         code entered
.
UDEF6666  PACK      KEY5,CODE,FILECODE,SP5
          CALL      RDCODE1            * read code file for entered code
          BRANCH    OVRCD,UDEF8000     * tell user his code not on file
          CALL      REDP0000           * redisplay screen if needed
          DISPLAY   *PXPOS:YPOS,*V2LON,FILECODE;
          GOTO      UDEF9999
.
.         nothing entered
.
UDEF7777  CALL      REDP0000           * redisplay screen if needed
          DISPLAY   *PXPOS:YPOS,UNDLN3;
          GOTO      UDEF9999
.
UDEF8000  DISPLAY   *P1:24,*B,*EL,"Code not on file.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,UDEF1000
          GOTO      UDEF4444
.
UDEF9999  RETURN
+
.******************************************************************************
.*        This toggles the switch if they like to total the search records.   *
.******************************************************************************
TOTL0000  ADD       ONE,TOTLFLAG       * if 0 then 1 else if 1 then 0
          LOAD      TOTLFLAG,TOTLFLAG,ONE,ZERO
TOTL1111
          BRANCH    TOTLFLAG,TOTL4444
          DISPLAY   *P27:21,*V2LON,"No ";
          GOTO      TOTL9999
TOTL4444
          DISPLAY   *P27:21,*V2LON,"Yes";
TOTL9999
          GOTO      SELT0000
+
.******************************************************************************
.*                                MTCH0000                                    *
.*           MATCH records against the specified search fields                *
.******************************************************************************
MTCH0000  BRANCH    FLGUR TO MTCH0100             * UR number
          MATCH     KSTUR,WMURNO
          GOTO      MTCH9990 IF LESS
          MATCH     WMURNO,KTOUR
          GOTO      MTCH9990 IF LESS
.
MTCH0100  BRANCH    FLGCO TO MTCH0200             * procedure code
          MATCH     KSTCODE,WMCODE
          GOTO      MTCH9990 IF LESS
          MATCH     WMCODE,KTOCODE
          GOTO      MTCH9990 IF LESS
.
MTCH0200  BRANCH    FLGST TO MTCH0300             * procedure status
          COMPARE   KSTATUS1,WMSTAT
          GOTO      MTCH9990 IF LESS
          COMPARE   WMSTAT,KSTATUS2
          GOTO      MTCH9990 IF LESS
.
MTCH0300  BRANCH    FLGD1,MTCH0400                * date on list
          MATCH     KSTDATE1,WMDATE1
          GOTO      MTCH9990 IF LESS
          MATCH     WMDATE1,KTODATE1
          GOTO      MTCH9990 IF LESS
.
MTCH0400  BRANCH    FLGD2 TO MTCH0500             * date last review
          MATCH     KSTDATE2,WMDATE2
          GOTO      MTCH9990 IF LESS
          MATCH     WMDATE2,KTODATE2
          GOTO      MTCH9990 IF LESS
.
MTCH0500  BRANCH    FLGD3 TO MTCH0600             * scheduled admission
          MATCH     KSTDATE3,WMDATE3
          GOTO      MTCH9990 IF LESS
          MATCH     WMDATE3,KTODATE3
          GOTO      MTCH9990 IF LESS
.
MTCH0600  BRANCH    FLGUN TO MTCH0700             * unit
          MATCH     KSTUNIT,WMUNIT
          GOTO      MTCH9990 IF LESS
          MATCH     WMUNIT,KTOUNIT
          GOTO      MTCH9990 IF LESS
.
MTCH0700  BRANCH    FLGDO TO MTCH0800             * doctor
          MATCH     KSTDOC,WMDOCTOR
          GOTO      MTCH9990 IF LESS
          MATCH     WMDOCTOR,KTODOC
          GOTO      MTCH9990 IF LESS
.
MTCH0800  BRANCH    FLGES TO MTCH0900             * estimated stay
          COMPARE   KSTSTAY,WMSTAY
          GOTO      MTCH9990 IF LESS
          COMPARE   WMSTAY,KTOSTAY
          GOTO      MTCH9990 IF LESS
.
MTCH0900  BRANCH    FLGPT TO MTCH1000             * priority
          MATCH     KSTPTY,WMPTY
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH1000  BRANCH    FLGSN TO MTCH1100             * short notice
          MATCH     KSTNOT,WMNOTICE
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH1100  BRANCH    FLGPL TO MTCH1200             * patient classification
          MATCH     KSTPCLS,WMPCAT
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH1200  BRANCH    FLGRS TO MTCH1300             * waiting type
          MATCH     KSTWTYP,WMPTYPE
          GOTO      MTCH9990 IF NOT EQUAL
MTCH1300
          BRANCH    FLGRR,MTCH1400                * removal code
          MATCH     KREMREAS,WMREMOVE
          GOTO      MTCH9990 IF NOT EQUAL
MTCH1400
          BRANCH    FLGRD,MTCH1500                * removal date
          MATCH     KREMDAT1,WMDATE4
          GOTO      MTCH9990 IF LESS
          MATCH     WMDATE4,KREMDAT2
          GOTO      MTCH9990 IF LESS
MTCH1500
          BRANCH    FLGRAD,MTCH1600               * re-assessment date
          MATCH     KREADAT1,WTWMREAD
          GOTO      MTCH9990 IF LESS
          MATCH     WTWMREAD,KREADAT2
          GOTO      MTCH9990 IF LESS
MTCH1600
          BRANCH    FLGDOS,MTCH1700               * date on system
          MATCH     KDOSDAT1,WMKEYDT
          GOTO      MTCH9990 IF LESS
          MATCH     WMKEYDT,KDOSDAT2
          GOTO      MTCH9990 IF LESS
MTCH1700
          BRANCH    FLGUDF1,MTCH1800
          MATCH     WMUDEF1,USERDEF1
          GOTO      MTCH9990 IF NOT EQUAL
MTCH1800
          BRANCH    FLGUDF2,MTCH1900
          MATCH     WMUDEF2,USERDEF2
          GOTO      MTCH9990 IF NOT EQUAL
MTCH1900
          BRANCH    FLGUDF3,MTCH2000
          MATCH     WMUDEF3,USERDEF3
          GOTO      MTCH9990 IF NOT EQUAL
MTCH2000
          BRANCH    FLGUDF4,MTCH2100
          MATCH     WMUDEF4,USERDEF4
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH2100  BRANCH    FLGPC,MTCH2200
          MATCH     WTWMCLSS,KSTPCAT
          GOTO      MTCH9990 IF NOT EQUAL   * not the same patient category
MTCH2200
          MOVE      ZERO,EXIT
          GOTO      MTCH9999
MTCH9990
          MOVE      ONE,EXIT
.
MTCH9999  RETURN
+
.******************************************************************************
.*                                SREC0000                                    *
.*                           Show valid record                                *
.******************************************************************************
SREC0000  MOVE      SP10,DATE10
          MOVE      SP10,DATE20
          MOVE      SP10,DATE30
.
          MATCH     SP8,WMDATE1                  * date on list
          GOTO      SREC3333 IF EQUAL
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DATE10,CPCDATE,SP10
          REP       " 0",DATE10
SREC3333
          MATCH     SP8,WMDATE2                  * last review date
          GOTO      SREC4444 IF EQUAL
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DATE20,CPCDATE,SP10
          REP       " 0",DATE20
SREC4444
          MATCH     SP8,WMDATE3
          GOTO      SREC5555 IF EQUAL
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DATE30,CPCDATE,SP10
          REP       " 0",DATE30
SREC5555
          BRANCH    PROPTION,SREC9999       * test if printing record
.
          MOVE      WMDESC,TDESC
          LOAD      DESCSTAT,WMSTAT,STAT1,STAT2,STAT3,STAT4:
                                    STAT5,STAT6,STAT7,STAT8
.
          DISPLAY   *P1:YPOS,*V2LON,COUNT,*HOFF,*P5:YPOS,WMURNO:
                    *P14:YPOS,WMCODE,*P24:YPOS,TDESC,*P45:YPOS,DATE10:
                    *P56:YPOS,DATE20,*P67:YPOS,DESCSTAT;
SREC9999  RETURN
+
.******************************************************************************
.*                                EXTR0000                                    *
.*                 extract records from Procedure file                        *
.*                 check if valid ( check parameters)                         *
.*   Modified 21/06/1990 Bill Dew : This is the second biggest load of shit   *
.*   I've found in WAT12.  Didnt the ancient programmers consider planning    *
.*   their routines.  I suppose good programmers are born not taught.         *
.*                                                                            *
.*   04/02/1993 : - Bill 'I need to spew' Dew.                                *
.*                                                                            *
.*   This routine attempts to extract the records which satisfy the search    *
.*   criteria and stores each record key into a array.                        *
.******************************************************************************
EXTR0000  MOVE      ZERO,PROPTION      * signal SREC to display data to screen
.
EXTR0200  DISPLAY   *P1:3,*EF,*V2LON:
                    *ULON,*P1:4,"Cnt",*P5:4,"  U/R No",*P14:4,"Code     ":
                    *P24:4,"Description         ",*P45:4,"On List   ":
                    *P56:4,"Review    ",*P67:4,"Status      ";
.
          BRANCH    EXTRFLAG,EXTR8888
EXTR0300
          MOVE      ZERO,EXTRFLAG      * set search thru wattr1af
          DISPLAY   *P1:5,*EF,*P1:24,*BLINKON,"Searching...";
          MOVE      ZERO,COUNT         * zero the number of records counter
          MOVE      FIVE,YPOS
          PACK      KEY27,KSTDATE1,SP20     * start date on list
          CALL      RDSTREA3
.
EXTR2222  CALL      RDKTREA3           * read by date on list index
          BRANCH    OVRCD TO EXTR8700
.
          CALL      MTCH0000           * match record against search criteria
          BRANCH    EXIT,EXTR2222      * exit=1 if match failed
.
          CALL      SISP0000                * see if on suspension
          BRANCH    EXIT,EXTR2222           * code is suspeneded
.
          ADD       ONE,COUNT          * increment the found record counter
          CALL      SREC0000           * show screen a single record 
.
          PACK      DIM19,WMURNO,WMCODE,WMCNT
.
. ---- Prepare the keys for the "select" later on
.
          STORE     DIM19,COUNT,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05,DIMC06:
                                DIMC07,DIMC08,DIMC09,DIMC10,DIMC11,DIMC12:
                                DIMC13,DIMC14,DIMC15,DIMC16,DIMC17
          ADD       ONE,YPOS
          COMPARE   TEN7,COUNT         * test for a full page of records
          GOTO      EXTR2222 IF NOT EQUAL
.
. ---- The screen is full, select on of the records for detailed display,
. ---- or search further on new screen
.
EXTR5555  MOVE      COUNT,DSACNUM      * save number of records on screen
          DISPLAY   *P1:24,*EL,"Select Line, ":
                    "(",*V2LON,"F",*HOFF,")irst screen, ":
                    "(",*V2LON,"N",*HOFF,")ext screen, ":
                    "(",*V2LON,"P",*HOFF,")rint, ":
                    "(",*V2LON,"E",*HOFF,")xit to system menu :";
.
          MOVE      "N1E2P3F4 5",MASK
.bugging  MOVE      "N1E2P3F4 5*6",MASK
          MOVE      "78",CCOL
          CALL      GOPT0000
          BRANCH    EXIT,EXTR6666,EXTR9222,EXTR9500,EXTR0300,EXTR9000,EXTR6688
          GOTO      EXTR7000
.
. ---- Further search, so clean the screen and read next record
.
EXTR6666  COMPARE   TEN7,COUNT
          GOTO      EXTR5555 IF NOT EQUAL
          MOVE      ZERO,COUNT
          MOVE      FIVE,YPOS
          DISPLAY   *P1:YPOS,*EF,*P1:24,*BLINKON,"Searching...";
          GOTO      EXTR2222
EXTR6688
          CALL      LLIB0000
          GOTO      EXTR5555
.
. ---- Select one of the records on the screen
. ---- load the key19 and read Procedure file
.
EXTR7000  COMPARE   ZERO,CCITEM        * test for zero selection 
          GOTO      EXTR7999 IF EQUAL
          COMPARE   CCITEM,COUNT       * test within range displayed on screen
          GOTO      EXTR7999 IF LESS
          LOAD      KEY19,CCITEM,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05,DIMC06:
                                 DIMC07,DIMC08,DIMC09,DIMC10,DIMC11,DIMC12:
                                 DIMC13,DIMC14,DIMC15,DIMC16,DIMC17
          CALL      RDTREA1
          BRANCH    OVRCD,EXTR8000
          MOVE      ZERO,EXIT
          GOTO      EXTR9999
EXTR7999
          BEEP
          GOTO      EXTR5555
EXTR8000
          DISPLAY   *P1:24,*EL,*B,"Record not on Waiting Lists file.  ";
          CALL      HITENTER
          GOTO      EXTR5555
EXTR8700
          COMPARE   ZERO,COUNT
          GOTO      EXTR5555 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*B,"No records found.  ";
          CALL      HITENTER
          GOTO      EXTR9000
EXTR8888
          CALL      DSAC0000           * display screen array contents
          GOTO      EXTR5555
EXTR9000
          MOVE      ONE,EXIT           * go back to previous screen
          GOTO      EXTR9999
EXTR9222
          MOVE      TWO,EXIT           * exit program
          GOTO      EXTR9999
EXTR9500
          CALL      PRNT0000           * print number of records
          GOTO      EXTR5555
.
EXTR9999  RETURN
+
.******************************************************************************
.*                                DSAC0000                                    *
.******************************************************************************
DSAC0000  MOVE      ZERO,COUNT        * zero the number of records counter
          MOVE      FIVE,YPOS
DSAC2222
          ADD       ONE,COUNT         * increment record counter
          LOAD      KEY19,COUNT,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05,DIMC06:
                                DIMC07,DIMC08,DIMC09,DIMC10,DIMC11,DIMC12:
                                DIMC13,DIMC14,DIMC15,DIMC16,DIMC17
          CALL      RDTREA1
          CALL      SREC0000           * show screen a single record 
          ADD       ONE,YPOS           * increment line counter
          COMPARE   COUNT,DSACNUM      * test for a full page of records
          GOTO      DSAC2222 IF NOT EQUAL
          RETURN
+
.*******************************************************************************
LLIB0000  KEYIN     *P56:24,CCITEM
          LOAD      KEY19,CCITEM,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05,DIMC06:
                                 DIMC07,DIMC08,DIMC09,DIMC10,DIMC11,DIMC12:
                                 DIMC13,DIMC14,DIMC15,DIMC16,DIMC17
.
          DISPLAY   *P1:24,*EL,"Confirm Delete ",KEY19," 0=yes 1=No : ";
          KEYIN     EXIT
          BRANCH    EXIT,LLIB9999
          CALL      DETREA1
LLIB9999
          RETURN
+
.*******************************************************************************
.*                                PRNT0000                                     *
.*                     ----   Print of variables   ----                        *
.*******************************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,"UR Number :":
                    *P50:24,*V2LON,*BLINKON,"*** Printing ***";
          MOVE      ONE,PROPTION       * set print mode, affects few routines
          MOVE      ZERO,FORM8
          MOVE      ZERO,CPAGENO
          CALL      PRNC0000           * print front cover heading
          CALL      PRNH0000           * print top page heading
.
          PACK      KEY27,KSTDATE1,SP20
          CALL      RDSTREA3
PRNT2222
          CALL      RDKTREA3           * read by date on list index
          BRANCH    OVRCD,PRNT8888
.
          CALL      MTCH0000           * match record against search criteria
          BRANCH    EXIT,PRNT2222      * exit=1 if match failed
.
          CALL      SISP0000                * see if on suspension
          BRANCH    EXIT,PRNT2222           * code is suspeneded
.
          CALL      SREC0000           * show screen a single record 
          DISPLAY   *P13:24,WMURNO;    * show operator something is printing.
          ADD       ONE,FORM8          * increment number of records found
          ADD       TWO,CLNO           * increment the printed line counter
.
          COMPARE   "60",CLNO          * test for a full page
          CALL      PRNH0000 IF NOT LESS
.
          MOVE      WMDESC,KEY12       * truncate treatment description
          CALL      VSTA0000           * get status var
          MOVE      TDESC,DIM8         * truncate the status description
          CALL      PAT0000            * Set patient name
          MOVE      PATNAME,KEY24      * truncate the patient name to 24 char
.
          PRINT     *1,"| ",WMURNO,*11,"| ",KEY24,*38,"| ",*40,WMCODE:
                    *50,"| ",*52,KEY12,*64,"| ",DATE10,*77,"| ",DATE20:
                    *89,"| ",DATE30,*101,"| ",WMUNIT,*107,"| ":
                    WMDOCTOR,*116,"| ",WMSTAY,*123,"|",DIM8,*132,"|"
.
          CALL      HEAD010            * Drawline
          GOTO      PRNT2222
PRNT8888
          PRINT     *N,*N,"Total Number of ",WTCNTPUC," = ",FORM8
          PRINT     *N,*N,"*** End of Report ***"
          RETURN
+
.******************************************************************************
.
HEAD010   PRINT     *1,"*----------------------------------------":
                    "----------------------------------------":
                    "--------------------------------------------------*"
          RETURN
+
.******************************************************************************
.*                                PAT0000                                     *
.*                     Get patient name in a dim20                            *
.******************************************************************************
PAT0000   MOVE      SP30,PATNAME
          MOVE      WMURNO,KEY8
          RESET     KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF PAT9999
          MOVE      ONE,PACFRMT
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
.
PAT9999   RETURN
+
.****************************************************************************
.*                              DETT0000                                    *
.*          Get descriptions for procedure screen variables                 *
.*          Get patient DOB for display and sex-description                 *
.****************************************************************************
DETT0000  UNPACK    KEY19,WMURNO,WMCODE
          MOVE      SP10,PATDOB
          MOVE      SP20,DSEX
          PACK      PATNAME,SP30,SP20
.
          MOVE      WMURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,DETT2000
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PATDOB,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          MOVE      PSEX,ANS
          REPLACE   "M1F2I3U4",ANS
          MOVE      ANS,FORM1
          LOAD      DSEX,FORM1,DMALE,DFEMALE,DINDETER,DUNK
.
          MOVE      ONE,PACFRMT        * setup patient name
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
DETT2000
          PACK      DESCWU,SP20
          MATCH     SP3,WMUNIT
          GOTO      DETT2222 IF EQUAL
          MOVE      "Unknown",TDESC
          PACK      KEY5,CODEWU,WMUNIT
          CALL      RDCODE1
          MOVE      TDESC,DESCWU
.
. ---- Attending doctor ----
.
DETT2222  MOVE      SP5,KEY5
          PACK      DOCNAME,SP30,SP20
          PACK      TDESC,SP20
          MOVE      WMDOCTOR,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD TO DETT3000
          CALL      DOCT0000
.
. ---- Priority ----
.
DETT3000  PACK      DESCTP,SP20
          MATCH     SP3,WMPTY
          GOTO      DETT4000 IF EQUAL
          MOVE      "Unknown",TDESC
          PACK      KEY5,CODETP,WMPTY
          CALL      RDCODE1
          MOVE      TDESC,DESCTP
.
. ---- Short notice ----
.
DETT4000  PACK      DESCNOT,SP20
          MATCH     SP3,WMNOTICE
          GOTO      DETT5000 IF EQUAL
          MOVE      "Unknown",TDESC
          PACK      KEY5,CODEWS,WMNOTICE
          CALL      RDCODE1
          MOVE      TDESC,DESCNOT
.
. ---- Patient classification ----
.
DETT5000  PACK      DESCPCLS,SP20
          MATCH     SP3,WMPCAT
          GOTO      DETT6000 IF EQUAL
          MOVE      "Unknown",TDESC
          PACK      KEY5,CODEA,WMPCAT
          CALL      RDCODE1
          MOVE      TDESC,DESCPCLS
.
. ---- Waiting type ----
.
DETT6000  PACK      DESCPTYP,SP20
          MATCH     SP3,WMPTYPE
          GOTO      DETT7777 IF EQUAL
          MOVE      "Unknown",TDESC
          PACK      KEY5,CODERS,WMPTYPE
          CALL      RDCODE1
          MOVE      TDESC,DESCPTYP
DETT7777
          PACK      DESCBC,SP20
          MATCH     SP3,WMREMOVE
          GOTO      DETT9999 IF EQUAL
          MOVE      "Unknown",TDESC
          PACK      KEY5,ANSW,ANSR,WMREMOVE
          CALL      RDCODE1
          MOVE      TDESC,DESCBC
.
          PACK      KEY19,WMURNO,WMCODE,DWMCNT
          PROC      DTRNDEST               * to get the transfer destination
          MOVE      TDESC,DESCBC
.
DETT9999  RETURN
+
.******************************************************************************
.*                                DOCT0000                                    *
.*                        Concatenate doctor name                             *
.******************************************************************************
DOCT0000  MOVE      DGNAME,ANS
          PACK      DOCNAME,DTITL,SP1,ANS,DOT,DSNAME
          RETURN
+
.******************************************************************************
.*                                SCRT0000                                    *
.*                       Detailed procedure screen                            *
.*     SCRT was copied from WAT10 patient procedure details enquire screen    *
.******************************************************************************
SCRT0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"U/R No.   : ":
                    *P23:4,"Name   : ":
                    *P3:5,"D.O.B. : ":
                    *P24:5,"Phone (P):":     
                    *P55:5,"(B):":
                    *P1:6,WTCNTPDS," Code        :":
                    *P1:7,WTCNTPDS," Description :":
                    *P1:8,WTCNTPDS," Time (mins) :         mins":
                    *P1:9,"Estimated stay (days) :         days":
                    *P1:10,"Reason / Diagnosis    :":
                    *P1:11,WTCNTPDS," Status      :":
                    *P1:12,DDESCWU,"  :":
                    *P1:13,"Doctor Code           :":
                    *P1:14,"Priority Code         :":
                    *P1:15,CATADES,"  :":
                    *P1:16,"Resident Status       :":
                    *P1:17,"Short Notice Code     :":
                    *P1:18,"Date on List          :":
                    *P1:19,"Last Review Date      :":
                    *P1:20,"Scheduled Admis. Date :";
.
          COMPARE   ONE,WTCNREAS
          GOTO      SCRT9999 IF NOT EQUAL
          DISPLAY   *P1:21,"Re-Assessment Date    :";
.
SCRT9999  RETURN
+
.******************************************************************************
.*                                DIST0000                                    *
.*                  Display variables of procedure screen                     *
.*      DIST was copied from WAT10 patient procedure details enquire screen   *
.******************************************************************************
DIST0000  MOVE      "11111111",ALRTMASK
          CALL      PATALERT
.
          DISPLAY   *P13:4,*V2LON,WMURNO,*HOFF:
                    *P32:4,*EL,*V2LON,PATNAME:
                    *P12:5,*V2LON,PATDOB,*P34:5,PTELEP,*P59:5,PTELEB:
                    *P25:6,*V2LON,WMCODE:
                    *P25:7,WMDESC:
                    *P25:8,WMTIME:
                    *P25:9,WMSTAY:
                    *P25:10,WMREASON;
          CALL      VSTA0000           * get status decription store in TDESC
          DISPLAY   *P25:11,*V2LON,TDESC;
          BRANCH    WMSTAT,DIST1111,DIST1000,DIST1000,DIST1000:
                           DIST1000,DIST1111,DIST1000,DIST1000
DIST1000
          DISPLAY   "( Booking Number : ",*V2LON,WMBOOK,*HOFF," )";
DIST1111
          DISPLAY   *P25:12,*V2LON,WMUNIT,*P38:12,*HOFF,DESCWU:
                    *P25:13,*V2LON,WMDOCTOR,*P38:13,*HOFF,DOCNAME:
                    *P25:14,*V2LON,WMPTY,*P38:14,*HOFF,DESCTP:
                    *P25:15,*V2LON,WMPCAT,*HOFF,SP10,DESCPCLS:
                    *P25:16,*V2LON,WMPTYPE,SP10,*HOFF,DESCPTYP:
                    *P25:17,*V2LON,WMNOTICE,SP10,*HOFF,DESCNOT;
.
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P25:18,*V2LON,CPCDATE;
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P25:19,*V2LON,CPCDATE;
          MATCH     SP8,WMDATE3
          GOTO      DIST2000 IF EQUAL
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P25:20,*V2LON,CPCDATE;
DIST2000
          COMPARE   ONE,WTCNREAS       * test if using re-assessment date
          GOTO      DIST2222 IF NOT EQUAL
          MATCH     SP8,WTWMREAD
          GOTO      DIST2222 IF EQUAL
          UNPACK    WTWMREAD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P25:21,*V2LON,CPCDATE;
DIST2222
          MATCH     SP3,WMREMOVE
          GOTO      DIST9999 IF EQUAL
          DISPLAY   *P1:22,"Removal Date          :":
                    *P1:23,"Removal Reason        :";
          UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P25:22,*V2LON,CPCDATE;
          DISPLAY   *P25:23,*V2LON,WMREMOVE,*HOFF,SP5,DESCBC,*HOFF;
.
DIST9999  RETURN
+
.******************************************************************************
.*                                USRD0000                                    *
.*                 Display user define heading and codes                      *
.******************************************************************************
USRD0000  MOVE      "18",YPOS          * start pos in USRD0000
          MOVE      ZERO,FORM1         * zero the user define counter
USRD1111
          ADD       ONE,FORM1          * increment the user define counter
.USRDQQ1   DISPLAY   *P1:3,"USRD>",HUSR1,HUSR2,HUSR3,HUSR4;
          LOAD      SCRNFLAG,FORM1,HUSR1,HUSR2,HUSR3,HUSR4
          COMPARE   ONE,SCRNFLAG       * test if using this user define
          GOTO      USRD5555 IF NOT EQUAL
.
          MOVE      SP20,TDESC
          LOAD      ACODE,FORM1,WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4
          MATCH     SP3,ACODE
          GOTO      USRD4444 IF EQUAL
.
          MOVE      "Unknown",TDESC
          LOAD      CODE,FORM1,CODEW1,CODEW2,CODEW3,CODEW4
          PACK      KEY5,CODE,ACODE
          CALL      RDCODE1
USRD4444
          STORE     TDESC,FORM1,DESCDEF1,DESCDEF2,DESCDEF3,DESCDEF4
          LOAD      KEY15,FORM1,HUSRDES1,HUSRDES2,HUSRDES3,HUSRDES4
          ADD       ONE,YPOS           * increment screen print position
          DISPLAY   *P38:YPOS,KEY15," :":
                    *P57:YPOS,*V2LON,ACODE,*HOFF,SP1,TDESC;
USRD5555
          COMPARE   FOUR,FORM1
          GOTO      USRD1111 IF NOT EQUAL
.
USRD9999  RETURN
+
.******************************************************************************
.*                                PRTT0000                                    *
.*                 Print these procedure details or exit ?                    *
.******************************************************************************
PRTT0000  MOVE      SP1,ANS
          MOVE      FALSE,EXIT
          DISPLAY   *P1:24,*EL,"(",*V2LON,ANSP,*HOFF,")rint, (",*V2LON:
                    ANSE,*HOFF,")xit to system menu : ";
          KEYIN     *P34:24,*EL,*V2LON,ANS;
          RESET     ANS
          MATCH     SP1,ANS
          GOTO      PRTT9000 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      PRTT9222 IF EQUAL
          MATCH     ANSP,ANS
          GOTO      PRTT9999 IF EQUAL
          BEEP
          GOTO      PRTT0000
PRTT9000
          MOVE      ONE,EXIT           * go back to last screen
          MOVE      ONE,EXTRFLAG       * signal to display screen array
          GOTO      PRTT9999
PRTT9222
          MOVE      TWO,EXIT           * exit program
.
PRTT9999  RETURN
+
.******************************************************************************
.*                                PRND0000                                    *
.*                        print Procedure details                             *
.******************************************************************************
PRND0000  DISPLAY   *P50:24,*BLINKON,*V2LON,*EL,"*** Printing ***";
          CALL      PRNH1111
          PRINT     *N,WTCNTPDS," Code           : ",WMCODE:
                    *N,WTCNTPDS," Description    : ",WMDESC:
                    *N,WTCNTPDS," Time           : ",WMTIME," mins.":
                    *N,"Estimate stay            : ",WMSTAY," days":
                    *N,"Reason/Diagnosis         : ",WMREASON;
          CALL      VSTA0000           * get status description
          PRINT     *N,WTCNTPDS," Status         : ",TDESC;
          BRANCH    WMSTAT,PRND2000,PRND1000,PRND1000,PRND1000:
                           PRND1000,PRND2000,PRND1000,PRND1000
.
PRND1000  PRINT     *50,"( Booking Number : ",WMBOOK," )";
.
PRND2000  PRINT     *N,DDESCWU,*26,": ",WMUNIT,*40,DESCWU:
                    *N,"Doctor Code              : ",WMDOCTOR,*40,DOCNAME:
                    *N,"Priority Code            : ",WMPTY,*40,DESCTP:
                    *N,CATADES,*26,": ",WMPCAT,*40,DESCPCLS:
                    *N,"Resident Status          : ",WMPTYPE,*40,DESCPTYP:
                    *N,"Short Notice Code        : ",WMNOTICE,*40,DESCNOT;
          MOVE      ONE,PROPTION
          CALL      SREC0000
          PRINT     *N,"Date on List             : ",DATE10: 
                    *N,"Last Review Date         : ",DATE20: 
                    *N,"Scheduled Admission Date : ",DATE30
.
          COMPARE   ONE,WTCNREAS
          GOTO      PRND3000 IF NOT EQUAL
          PRINT     "Re-Assessment Date       : ";
          MATCH     SP8,WTWMREAD
          GOTO      PRND3000 IF EQUAL
          UNPACK    WTWMREAD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE;
PRND3000
          PRINT     SP1                * finish the semi colon print from above
          MATCH     SP3,WMREMOVE
          GOTO      PRND4000 IF EQUAL
          UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "Removal Date             : ";
          PRINT     CPCDATE:
                    *N,"Removal Reason           : ",WMREMOVE,*40,DESCBC
PRND4000
          COMPARE   ONE,HUSR1
          GOTO      PRND5000 IF NOT EQUAL
          PRINT     *N,HUSRDES1,*26,": ",WMUDEF1,*40,DESCDEF1
.
PRND5000  COMPARE   ONE,HUSR2
          GOTO      PRND6000 IF NOT EQUAL
          PRINT     HUSRDES2,*26,": ",WMUDEF2,*40,DESCDEF2
.
PRND6000  COMPARE   ONE,HUSR3
          GOTO      PRND7000 IF NOT EQUAL
          PRINT     HUSRDES3,*26,": ",WMUDEF3,*40,DESCDEF3
.
PRND7000  COMPARE   ONE,HUSR4
          GOTO      PRND9999 IF NOT EQUAL
          PRINT     HUSRDES4,*26,": ",WMUDEF4,*40,DESCDEF4
.
PRND9999  RETURN
+
.******************************************************************************
.*                                PRNH0000                                    *
.*                              Print heading                                 *
.******************************************************************************
PRNH0000  MOVE      ZERO,CLNO
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
          PRINT     *N,*N,*1,"*----------------------------------------":
                          "----------------------------------------":
                          "----------------------------------------":
                          "----------*",*N:
                    *1,"| U/R No",*11,"| Patient Name",*38,"| ",WTCNTPDS:
                    *50,"| Description",*64,"| On List",*77,"| Review":
                    *89,"| Scheduled",*101,"| Unit",*107,"| Doctor":
                    *116,"| Stay",*123,"|Status",*132,"|":
                    *N,*1,"*----------------------------------------":
                     "----------------------------------------------":
                     "--------------------------------------------*"
          ADD       SIX,CLNO 
          GOTO      PRNH9999
.
.==============================================================================
PRNH1111
          MOVE      ZERO,CLNO
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
          PRINT     *N,*N,*5,"U/R Number  : ",WMURNO,*30,PATNAME:
                    *65,"D.O.B : ",PATDOB,*N:
                    *5,"Phone (P):",PTELEP," (B):",PTELEB:
                    *N,*N
          ADD       "7",CLNO 
.
PRNH9999  RETURN
+
.******************************************************************************
.*                                PRNC0000                                    *
.*                    Print first page with criteria                          *
.******************************************************************************
PRNC0000  CLOCK     TIME,CTIMEIS
.
          MOVE      ZERO,CPAGENO
          CALL      HEAD132
          PRINT     *N
.
          PRINT     *25,"*-------------------------------":
                    "-------------------------------*"
          PRINT     *25,"|",*39,"S e l e c t i o n     ":
                    "C r i t e r i a ",*88,"|"
          PRINT     *25,"*-------------------------------":
                    "-------------------------------*"
          MOVE      ONE,CLNO
          BRANCH    FLGUR TO PRNC1000
          ADD       ONE,CLNO
          PRINT     *25,"|  U/R Number          : ",KSTUR:
                    *61,"To",*64,KTOUR,*88,"|"
PRNC1000
          BRANCH    FLGCO TO PRNC2000
          ADD       ONE,CLNO
          PRINT     *25,"|  ",WTCNTPDS," Code      : ";
          PACK      STRTCODE,KSTCODE,SP10
          PACK      ENDCODE,KTOCODE,SP10
          CALL      PRNG0000
          PRINT     *88,"|"
PRNC2000
          BRANCH    FLGST TO PRNC3000
          ADD       ONE,CLNO
.
PRNC3000
          BRANCH    FLGD1 TO PRNC4000
          ADD       ONE,CLNO
          PRINT     *25,"|  Date On List        : ";
          PACK      KEY16,KSTDATE1,KTODATE1
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
PRNC4000
          BRANCH    FLGD2 TO PRNC5000
          ADD       ONE,CLNO
          PRINT     *25,"|  Date Last Review    : ";
          PACK      KEY16,KSTDATE2,KTODATE2
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
PRNC5000
          BRANCH    FLGD3 TO PRNC6000
          ADD       ONE,CLNO
          PRINT     *25,"|  Scheduled Admission : ";
          PACK      KEY16,KSTDATE3,KTODATE3
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
PRNC6000
          BRANCH    FLGUN TO PRNC7000
          ADD       ONE,CLNO
          PRINT     *25,"|  ",DDESCWU,": ";
          PACK      STRTCODE,KSTUNIT,SP10
          PACK      ENDCODE,KTOUNIT,SP10
          CALL      PRNG0000
          PRINT     *88,"|"
PRNC7000 
          BRANCH    FLGDO TO PRNC8000
          ADD       ONE,CLNO
          PRINT     *25,"|  Doctor              : ";
          PACK      STRTCODE,KSTDOC,SP10
          PACK      ENDCODE,KTODOC,SP10
          CALL      PRNG0000
          PRINT     *88,"|"
PRNC8000
          BRANCH    FLGES TO PRNC9000
          ADD       ONE,CLNO
          PRINT     *25,"|  Estimated Stay      : ",KSTSTAY:
                    *61,"To",*64,KTOSTAY,*88,"|"
PRNC9000
          BRANCH    FLGPT TO PRNE1000
          ADD       ONE,CLNO
          PRINT     *25,"|  Priority            : ",KSTPTY
.
.++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
.
PRNE1000  BRANCH    FLGSN TO PRNE2000
          ADD       ONE,CLNO
          PRINT     *25,"|  Short Notice        : ",KSTNOT
PRNE2000
          BRANCH    FLGPL TO PRNE3000
          ADD       ONE,CLNO
          PRINT     *25,"|  ",CATADES,": ",KSTPCLS;
PRNE3000
          BRANCH    FLGRS TO PRNE3100
          ADD       ONE,CLNO
          PRINT     *25,"|  Resident Status     : ",KSTWTYP
PRNE3100
          BRANCH    FLGRR,PRNE3200
          ADD       ONE,CLNO
          PRINT     *25,"|  Removal Reason      : ",KREMREAS
PRNE3200
          BRANCH    FLGRD,PRNE3300
          ADD       ONE,CLNO
          PRINT     *25,"|  Removal Date        : ";
          PACK      KEY16,KREMDAT1,KREMDAT2
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
PRNE3300
          BRANCH    FLGRAD,PRNE3400
          ADD       ONE,CLNO
          PRINT     *25,"|  Re-Assessment Date  : ";
          PACK      KEY16,KREADAT1,KREADAT2
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
PRNE3400
          BRANCH    FLGRAD,PRNE3500
          ADD       ONE,CLNO
          PRINT     *25,"|  Date on System      : ";
          PACK      KEY16,KDOSDAT1,KDOSDAT2
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
PRNE3500
          BRANCH    FLGUDF1,PRNE3600
          ADD       ONE,CLNO
          PRINT     *25,"|  ",HUSRDES1,": ",USERDEF1,*88,"|"
PRNE3600
          BRANCH    FLGUDF2,PRNE3700
          ADD       ONE,CLNO
          PRINT     *25,"|  ",HUSRDES2,": ",USERDEF2,*88,"|"
PRNE3700
          BRANCH    FLGUDF3,PRNE3800
          ADD       ONE,CLNO
          PRINT     *25,"|  ",HUSRDES3,": ",USERDEF3,*88,"|"
PRNE3800
          BRANCH    FLGUDF4,PRNE3900
          ADD       ONE,CLNO
          PRINT     *25,"|  ",HUSRDES4,": ",USERDEF4,*88,"|"
PRNE3900
          BRANCH    FLGPC,PRNE4000
          ADD       ONE,CLNO
          PACK      KEY5,ANSP,SP4
          CALL      RDCODE1
          PRINT     *25,"|  ",TDESC,*48,": ",KSTPCAT,*88,"|"
PRNE4000
          COMPARE   ONE,CLNO
          GOTO      PRNE5000 IF NOT EQUAL
          PRINT     *25,"|   No Criteria ",*88,"|"
PRNE5000  PRINT     *25,"*-------------------------------":
                    "-------------------------------*"
          RETURN
+
.******************************************************************************
.*        Print Date given date in KEY16                                      *
.******************************************************************************
PDAT0000  UNPACK    KEY16,FDATE1,FDATE2
          MATCH     SP8,FDATE1
          GOTO      PDAT2222 IF NOT EQUAL
          PRINT     START," To ";
          GOTO      PDAT4444
PDAT2222
          UNPACK    FDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE," To ";
PDAT4444
          MATCH     "99999999",FDATE2
          GOTO      PDAT6666 IF NOT EQUAL
          PRINT     FINISH;
          GOTO      PDAT9999
PDAT6666
          UNPACK    FDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE;
.
PDAT9999  RETURN
+
.******************************************************************************
.*        Print range of codes selected                                       *
.******************************************************************************
PRNG0000  MATCH     SP10,STRTCODE
          GOTO      PRNG2222 IF NOT EQUAL
          PRINT     START," To ";
          GOTO      PRNG4444
PRNG2222
          PRINT     STRTCODE," To ";
PRNG4444
          MATCH     "ZZZ",ENDCODE
          GOTO      PRNG6666 IF NOT EQUAL
          PRINT     FINISH;
          GOTO      PRNG9999
PRNG6666
          PRINT     ENDCODE;
PRNG9999  RETURN
+
.*********************************************************************
.         see if this procedure is suspended
.         Returns : EXIT = 1      procedure is suspended
.*********************************************************************
SISP0000  MOVE      ZERO,EXIT
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP2
          CALL      RSWTSUS1
          CALL      RKWTSUS1
          BRANCH    OVRCD,SISP9999          * not suspened
          PACK      KEY18,WTSUURNO,WTSUCODE,WTSUCNT
          MATCH     KEY18,KEY21
          GOTO      SISP9999 IF NOT EQUAL   * not suspended
          MOVE      ONE,EXIT
.
SISP9999  RETURN
+
.****************************************************************************
.
GETSVAR   RETURN
.
REDISPS   CALL      SCRP0000
          DISPLAY   *P27:4,*V2LON,KSTUR;
          RETURN
.
.==============================================================================
.
          INC       STD001IO
          INC       BOKRC1IO/INC            * booking master file
          INC       CALCAGE
          INC       TRANDEST           * transfer destination
          INC       NZCOMPIO/INC
          INC       OUTPREIO/INC
          INC       PATALERT
          INC       PATALRIO/INC
          INC       PATCOMN1
          INC       PATCODKY
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATGSRIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATSNDX
          INC       PATSNX2
          INC       WATDSPRO
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATRTDIO/INC
          INC       WATSUSIO/INC
AGECHK
CLPATMAS  RETURN
................................................................................

.-------------------------------------------------------------------------------
. Program       : WATWEB02 
.
. Function      : Waiting List Procedure Code Maintenance
.
. Modifications : 
.
.-------------------------------------------------------------------------------
. V11.03.01 22/11/2022  Bella Turco   TSK 0921957
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V11.00.02 20/03/2020  Peter Vela     TSK 0889143
.           Fixed WATLET IO calls
. V11.00.01 19/03/2020  Peter Vela     TSK 0889143
.           Recompiled for WATLETFD
. V10.15.01 19/11/2019  Thanh T        TSK 0882708
.           Changed PRORW000 to call CNVQTE00 to convert the single quote in
.           name/description
.-------------------------------------------------------------------------------
. V10.13.01 23/01/2019  Nicole Torrisi TSK 0868382
.           Fixed spelling errors in HDPHD00
. V10.12.01 30/01/2018  Nicole Torrisi TSK 0851284
.           Recompiled for WATPKIPR - add Procedure Code as keyword to watpkiaf
. V10.11.01 10/10/2017  Ania P         TSK 0841602
.           Added output of WPCHET to PRORW000
. V10.10.01 18/04/2017  Nicole Torrisi TSK 0837018
.           Changed VWLRW000 to use DIM40 for description
. V10.08.02 18/05/2016  Jill Parkinson TSK 0813956
.           Fixed tag for wathdp007          
. V10.08.01 18/04/2016  Jill Parkinson TSK 0813956
.           Added HDP History Maintenance
. V10.07.01 16/12/2015  Ebon Clements  CAR 294154
.           Added SMS message notifications to letter maintenance
. V10.04.02 31.03.2014  B.G.Ackland    CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
. V10.04.01 31/12/2013  Don Nguyen     CAR 294389
.           Recompiled for changes to WATLETFD & WATLETIO
.-------------------------------------------------------------------------------
.                 V10.03.05 16/07/2013  Jill Parkinson CAR 287923
.                           Added include of PATCONFD
.                 V10.03.04 01/03/2012  Ebon Clements  CAR 261063
.                           Added Active/Inactive to WL letters
.                 V10.03.03 25/02/2012 Davin          CAR 256376  
.                           Only display active docs from pmstmdaf (vpmtb100)
.                 V10.03.02 16/02/2012 J.Tan          CAR 256376
.                           Added Intended Hosp.for searching using pmstmdaf
.                 V10.03.01 12/02/2012 Jill Parkinson CAR 256376  
.                           Added search using pmstmdaf
.                 V10.02.02 12/08/2011 Davin          CAR 247912  
.                           Stop letters going over 99 lines (getlet00)
.                 V10.02.01 29/07/2011 Jill Parkinson CAR 243296  
.                           Added table type=4
.                 V10.01.01 18/01/2011 Jill Parkinson CAR 234014  
.                           Increased WTPRO002 to 80 characters
.                 V9.12.01  09/12/2009  Peter McMullen CAR 210130 
.                           convert to newer dr name format routine DOCNM000
.                 V9.11.01  02/04/2009  Ebon Clements CAR 193102
.                           Added procedure time to PRORW000 updateParent()
.                 V9.05.01  09/03/2006  Ebon Clements CAR 78533
.                           Mods for PATCODTM - Hospital specific category codes
.                 V9.05.B02 30/01/2006  Ebon Clements CAR 93186
.                           Mods for REDIRECT length change.
.                           Recompiled for IBAWEBDF
.                 V9.04.08  03.10.2005 Ebon Clements     78184 
.                           Added JAVDOC00 to VWLTB000 
.                 V9.04.07  27.09.2005 Ebon Clements     57956 
.                           Added keyword search to waiting list procedures
.                 V9.04.06  15.08.2005 Sandra Barcham    71804
.                           Ensure 10 active codes are displayed on each screen
.                 V9.04.05  10.08.2005 Jill Habasque CAR 71188
.                           Fixed waiting list letters, added sp70 to WLTEXT  
.                 V9.04.04  09.08.2005 Jill Habasque CAR 68092
.                           Added check if tabltype=2 only output active codes
.                 V9.04.03  31.03.2005 Ebon Clements CAR 57910
.                           Added procedure search without a unit
.                 V9.04.02  03.03.2005 Peter Vela    CAR 57507
.                           Added Waiting List Procedure Maintenance Search
.                           Code
.                 V9.04.01  24.08.2004 Jill Habasque CAR 52919
.                           Changed wtpro008 to 9 characters            
.                 V9.02.02  12.10.2002 Ebon Clements SRF 23197
.                           Added cgi variable for new field WTVWTELE   
.                 V9.02.01  21.08.2001 HPS & on 24.08.01 Ebon Clements
.                           Modified VWLWAT40 for clearing the variables   
.                           & Changed LETDET00 to update variable WLVAR
.                 V9.00.B04 27.07.2001 HPS
.                           Defect Fixing
.                 V9.00.B03 25.07.2001 HPS
.                           Defect Fixing
.                 V9.00.B02 14.05.2001 Bronko Sosic 
.                           Added changes as per Spec for Server            
.--------------------------------------------------------------------------
.                 V5.09.B02 02.01.2001 Katie Nelson 
.                           Fixed and next and previous links               
.                 V5.08.00 17.05.2000 Bronko Sosic
.                          Created Program                                      
.                    
.---------------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
          INC       PATCONFD/INC
          INC       PMSTMDFD/INC
          INC       WATESTFD/INC
          INC       WATVWLFD/INC
          INC       WATPROFD/INC
          INC       WATPHIFD/INC
          INC       WATLETFD/INC
          INC       PATDO1FD/INC   *Doctor File
          INC       WATPKIFD/INC
          INC       CNVQTETD/INC
.---------------------------------------------------------------------------
.  CGI Variables
.
CONFDESC  DIM       3
COUNTX    FORM      3
DIM40     DIM       40
ENDNAME   DIM       70
FRMTDATE  DIM       11
LETTNUMB  FORM      3
TABLTYPE  FORM      1       * Nextpage parameter CGI parameter
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
STARTKEY  DIM       10      * Starting Key
PTCOD002  DIM       3       * Code key 

DISPACTV  DIM       3
DOCTCODE  DIM       6
JAVFNAME  DIM       70
SMSSDESC  DIM       3
STRTNAME  DIM       70
INTDHOSP  DIM       3       * Intended Hospital
UPDTHIST  DIM       1
.
WTPRO001  DIM       9       * Procedure Code      
WTPRO002  DIM       80      * Description      
WTPRO003  FORM      5       * Procedure Time      
WTPRO004  FORM      3       * Estimated Length of stay in Hospital
WTPRO005  DIM       3       * Procedure Group (Category WG)
WTPRO006  DIM       6       * RHA Threshold Level
WTPRO007  DIM       6       * CHE Threshold Level
WTPRO008  DIM       9       * Procedure Code HDP Equivalent
WTPRO009  DIM       6       * DRG Code      
WTPRO010  FORM      1       * Status 0=Active 1=Inactive
.
WTHDP001  DIM       9       * Procedure Code
WTHDP002  DIM       8       * Effective to date
WTHDP003  DIM       9       * HDP Value
WTHDP004  DIM       6       * DRG Code
WTHDP005  DIM       6       * RHA Threshold Level
WTHDP006  DIM       6       * CHE Threshold Level
WTHDP007  DIM       6       * Default HDP from parent page
.
WTVWL001  DIM       6       * Consultant
WTVWL002  DIM       3       * List type
WTVWL003  DIM       40      * Description
WTVWL004  FORM      3       * Maximum Waiting Time - Elective
WTVWL005  FORM      3       * Maximum Waiting Time - Day Case
WTVWL006  FORM      1       * Status 0=Active 1=Inactive
WTVWL007  DIM       20      * Telephone Number              
.
WTLEEND   FORM      3       * Last Line of Loaded Letter Content
WTLETEXT  DIM       70[999]  * Line of Text
.
WTLET001  DIM       3       * Letter Number
WTLET002  DIM       70      * Description
WTLET003  DIM       2       * Letter Bottom Margin Size
WTLET004  DIM       2       * Letter Top Margin Size
WTLET005  DIM       3       * Physical Paper Length
WTLET006  DIM       2       * Left Margin
WTLET007  DIM       8       * Left Margin
WTLET008  DIM       1       * Audit Letter
WTLET009  DIM       1       * Active/Inactive/
WTLET010  DIM       1       * Communication Method
WTLET011  DIM       1       * Process Confirmation
.
WATES001  DIM       8       * ESIS Seal Date
TEXT      DIM       50      * Description
TXTLEN    FORM      3       * Text Length
SAVKEY5   DIM       5       * Saves key for Letters
SAVKEY59  DIM       59      * Saves key for Letters
SAVKEY9   DIM       9       * Saves key for Letters
WG        INIT      "WG"
WU        INIT      "WU"
SPECCHAR  INIT      "%"
.---------------------------------------------------------------------------
PRGID     INIT      "WATWEB02"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Waiting Maintenance"
.---------------------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          MOVE      ZERO,COUNTX
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      PROWAT00        * Waiting list Maintenance                
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
. 
MAIN1200  CALL      VWLWAT00        * Waiting list Consultant Code Maintenance 
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
. 
MAIN1300  DISPLAY   *P1:24,"LETWAT",COUNTX,"LETWAT"
          ADD       ONE,COUNTX
          CALL      LETWAT00        * Waiting list Letter Maintenance 
          GOTO      MAIN1000
. 
MAIN1400  CALL      ESIWAT00        * ESIS Seal Table Maintenance 
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
. 
MAIN1500  CALL      HDPWAT00        * Procedure HDP Table Maintenance 
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
. 
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages 
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);": 
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.-------------------------------------------------------------------------------
. Open Files and Initialize Variables
.-------------------------------------------------------------------------------
INIT0000  OPEN      PMSTMDA2,"pmstmdaf"    
          OPEN      WATPHIA1,"watphiaf"    
          OPEN      WATPROA2,"watproaf"    
          OPEN      WATPROA1,"watproaf"
          OPEN      WATESTA1,"watestaf"
          OPEN      WATPROA4,"watproaf"
          OPEN      WATPROA3,"watproaf"
          OPEN      WATAUDWP,"wataudwp"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      WATVWLA2,"watvwlaf"
          OPEN      WATLETR1,"watletrf"
          OPEN      WATLETR2,"watletrf"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      WATPKIA1,"watpkiaf"
          OPEN      WATPKIA2,"watpkiaf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
.-------------------------------------------------------------------------------
. Clear Parameters
.-------------------------------------------------------------------------------
CLRPAR00  MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,ADMISSNO
          MOVE      SP70,URNUMBER 
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
          MOVE      SP70,PTCOD002
          MOVE      SP70,KEYWORDS
          MOVE      SP70,INTDHOSP
.
          MOVE      SP70,WTPRO001
          MOVE      SP70,WTPRO002
          MOVE      ZERO,WTPRO003
          MOVE      ZERO,WTPRO004
          MOVE      SP70,WTPRO005
          MOVE      SP70,WTPRO006
          MOVE      SP70,WTPRO007
          MOVE      SP70,WTPRO008
          MOVE      SP70,WTPRO009
          MOVE      ZERO,WTPRO010
          MOVE      SP70,UPDTHIST
.
          MOVE      SP70,WTHDP001
          MOVE      SP70,WTHDP002
          MOVE      SP70,WTHDP003
          MOVE      SP70,WTHDP004
          MOVE      SP70,WTHDP005
          MOVE      SP70,WTHDP006
          MOVE      SP70,WTHDP007
.
          MOVE      SP70,WTVWL001
          MOVE      SP70,WTVWL002
          MOVE      SP70,WTVWL003
          MOVE      ZERO,WTVWL004
          MOVE      ZERO,WTVWL005
          MOVE      ZERO,WTVWL006
          MOVE      SP70,WTVWL007
.
          MOVE      ZERO,WTLEEND
          MOVE      Z70,WTLET001
          MOVE      Z70,WTLET002
          MOVE      Z70,WTLET003
          MOVE      Z70,WTLET004
          MOVE      Z70,WTLET005
          MOVE      Z70,WTLET006
          MOVE      Z70,WTLET007
          MOVE      Z70,WTLET008
          MOVE      Z70,WTLET009
          MOVE      Z70,WTLET010
          MOVE      Z70,WTLET011
.
          MOVE      Z70,WATES001
.
CLRPAR99  RETURN
.-------------------------------------------------------------------------------
. Set Parameters
.-------------------------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.   
          MATCH     "updthist=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTHIST
            GOTO      SETPAR99
          ENDIF
.   
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF  
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
. 
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ptcod002=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PTCOD002
            PACK      PTCOD002,PTCOD002,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "intdhosp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,INTDHOSP
            PACK      INTDHOSP,INTDHOSP,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wates001=",QRYNAME
          IF        @EQUAL
            PACK      QRYDATA,QRYDATA,SP30
            UNPACK    QRYDATA,KEY8,KEY3 * Check for formatted or unformatted 
            MATCH     SP70,KEY3
            IF        @EQUAL
              MOVE      QRYDATA,WATES001 * Do not change format = (CCYYMMDD)
            ELSE
              MOVE      QRYDATA,KEY11   * Change Date as format is (DD MON CCYY)
              PACK      KEY11,KEY11,SP70
              MATCH     SP70,KEY11
              IF        !@EQUAL
                UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
                CALL      SETMTH00
                PACK      WATES001,CCENT,CYEAR,CMON,CDAY
                REP       " 0",WATES001
              ENDIF
            ENDIF
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wtpro",QRYNAME
          IF        @EQUAL
            CALL      STWAT000
            GOTO      SETPAR99 
          ENDIF
.
          MATCH     "wthdp",QRYNAME
          IF        @EQUAL
            CALL      STHDP000
            GOTO      SETPAR99 
          ENDIF
.
          MATCH     "wtvwl",QRYNAME
          IF        @EQUAL
            CALL      STVWL000
            GOTO      SETPAR99 
          ENDIF
.
          MATCH     "wtletext",QRYNAME
          IF        @EQUAL
            CALL      GETLET00
            GOTO      SETPAR99 
          ENDIF
.
          MATCH     "wtlet",QRYNAME
          IF        @EQUAL
            CALL      STLET000
            GOTO      SETPAR99 
          ENDIF
.
          MATCH     "keywords=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEYWORDS
            REP       "~'",KEYWORDS
            GOTO      SETPAR99
          ENDIF
.
.
SETPAR99  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 1 - Waiting List Procedure Code Maintenance
.-------------------------------------------------------------------------------
STWAT000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STWAT001,STWAT002,STWAT003,STWAT004,STWAT005,STWAT006:
                       STWAT007,STWAT008,STWAT009,STWAT010
.
          GOTO      STWAT999
.
STWAT001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,WTPRO001
          PACK      WTPRO001,WTPRO001,SP70
          GOTO      STWAT999
.
STWAT002  MOVE      QRYDATA,WTPRO002
          GOTO      STWAT999
.
STWAT003  MOVE      QRYDATA,WTPRO003
          GOTO      STWAT999
.
STWAT004  MOVE      QRYDATA,WTPRO004
          GOTO      STWAT999
.
STWAT005  SQUEEZE   QRYDATA
          MOVE      QRYDATA,WTPRO005
          PACK      WTPRO005,WTPRO005,SP70
          GOTO      STWAT999
.
STWAT006  MOVE      QRYDATA,WTPRO006
          GOTO      STWAT999
.
STWAT007  MOVE      QRYDATA,WTPRO007
          GOTO      STWAT999
.
STWAT008  MOVE      QRYDATA,WTPRO008
          GOTO      STWAT999
.
STWAT009  MOVE      QRYDATA,WTPRO009
          GOTO      STWAT999
.
STWAT010  MOVE      QRYDATA,WTPRO010
          GOTO      STWAT999
.
STWAT999  RETURN
+
.-------------------------------------------------------------------------------
.   Set Parameters for Waiting List Procedure Code HDP Maintenance
.-------------------------------------------------------------------------------
STHDP000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STHDP001,STHDP002,STHDP003,STHDP004,STHDP005,STHDP006:
                       STHDP007
.
          GOTO      STHDP999
.
STHDP001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,WTHDP001
          PACK      WTHDP001,WTHDP001,SP70
          GOTO      STHDP999
.
STHDP002  PACK      QRYDATA,QRYDATA,SP30
          UNPACK    QRYDATA,KEY8,KEY3 * Check for formatted or unformatted
          MATCH     SP70,KEY3
          IF        @EQUAL
            MOVE      QRYDATA,WTHDP002 * Do not change format = (CCYYMMDD)
          ELSE
            MOVE      QRYDATA,KEY11   * Change Date as format is (DD MON CCYY)
            PACK      KEY11,KEY11,SP70
            MATCH     SP70,KEY11
            IF        !@EQUAL
              UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
              CALL      SETMTH00
              PACK      WTHDP002,CCENT,CYEAR,CMON,CDAY
              REP       " 0",WTHDP002
            ENDIF
          ENDIF
          GOTO      STHDP999
.
STHDP003  MOVE      QRYDATA,WTHDP003
          GOTO      STHDP999
.
STHDP004  MOVE      QRYDATA,WTHDP004
          GOTO      STHDP999
.
STHDP005  MOVE      QRYDATA,WTHDP005
          GOTO      STHDP999
.
STHDP006  MOVE      QRYDATA,WTHDP006
          GOTO      STHDP999
.
STHDP007  MOVE      QRYDATA,WTHDP007
          GOTO      STHDP999
.
STHDP999  RETURN
+
.-------------------------------------------------------------------------------
.   Set Parameters for Option 2 - Waiting List Consultant Code Maintenance
.-------------------------------------------------------------------------------
STVWL000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STVWL001,STVWL002,STVWL003,STVWL004,STVWL005,STVWL006:
                       STVWL007
.
          GOTO      STWAT999
.
STVWL001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,WTVWL001
          PACK      WTVWL001,WTVWL001,SP70
          GOTO      STVWL999
.
STVWL002  SQUEEZE   QRYDATA
          MOVE      QRYDATA,WTVWL002
          PACK      WTVWL002,WTVWL002,SP70
          GOTO      STVWL999
.
STVWL003  MOVE      QRYDATA,WTVWL003
          GOTO      STVWL999
.
STVWL004  MOVE      QRYDATA,WTVWL004
          GOTO      STVWL999
.
STVWL005  MOVE      QRYDATA,WTVWL005
          GOTO      STVWL999
.
STVWL006  MOVE      QRYDATA,WTVWL006
          GOTO      STVWL999
.
STVWL007  MOVE      QRYDATA,WTVWL007
          GOTO      STVWL999
.
STVWL999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 3 - Waiting List Letter File Maintenance
.-------------------------------------------------------------------------------
STLET000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          STORE     QRYDATA,F3,WTLET001,WTLET002,WTLET003,WTLET004,WTLET005:
                               WTLET006,WTLET007,WTLET008,WTLET009,WTLET010:
                               WTLET011
STLET999  RETURN
.------------------------------------------------------------
. Get Text from New Note
.------------------------------------------------------------
GETLET00  ADD       ONE,WTLEEND
          MOVE      QRYDATA,WTLETEXT[WTLEEND]
          RESET     WTLETEXT[WTLEEND]
.
GETLET10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA * Read CONFFILE sequentially
          GOTO      GETLET99 IF OVER             * If end of file
          MATCH     SP70,QRYNAME                 * If next CGI parameter
          GOTO      GETLET80 IF NOT EQUAL
          ADD       ONE,WTLEEND                   * Increment Array Counter
          PACK      WTLETEXT[WTLEEND],QRYDATA,SP70  * Input Note to Array
          IF        WTLEEND>=999
            GOTO      GETLET99
          ENDIF
          GOTO      GETLET10
.
GETLET80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
GETLET99  RETURN
.-------------------------------------------------------------------------------
. Waiting List Maintenance: Display,Add,Update,Delete
.-------------------------------------------------------------------------------
PROWAT00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      PROWAT40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      PROWAT10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      PROWAT20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      PROWAT30 IF EQUAL
.
          GOTO      PROWAT93
.
.         ************ ADD FORMACTION ***********
.
PROWAT10  CALL      CHKPRO00      * Checks mandatory fields
          BRANCH    EXIT,PROWAT99
.
          PACK      KEY9,WTPRO001,SP70
          CALL      RDPROA1
          COMPARE   ZERO,OVRCD
          GOTO      PROWAT92 IF EQUAL
          CALL      PROSAV00      * Moves input variables to file variables
          PACK      KEY9,WTPRO001,SP70
          CALL      WRPROA1       * Writes away the data
.
          PROC      WATPKIUP      * Update keyword index
.
          MOVE      "A",KEY1      * Set Add Parameter for PRAAUD00
          CALL      PROAUD00      * Write Audit File if Needed
.
          MOVE      ZERO,EXIT
          GOTO      PROWAT99
.
.         ************ UPDATE FORMACTION **********
.
PROWAT20  PACK      KEY9,WTPRO001,SP70
          CALL      RDPROA1
          BRANCH    OVRCD,PROWAT91
          CALL      CHKPRO00      * Checks mandatory fields
          BRANCH    EXIT,PROWAT99
.
          MOVE      "B",KEY1      * Set Before Changes Parameter for PRAAUD00
          CALL      PROAUD00      * Write Audit File if Needed
.
          CALL      PROSAV00      * Moves input variables to file variables
          PACK      KEY9,WTPRO001,SP70
          CALL      UPPROA1       * Writes away the data
.
          PROC      WATPKIUP      * Update keyword index
.
          MOVE      "C",KEY1      * Set After Changes Parameter for PRAAUD00
          CALL      PROAUD00      * Write Audit File if Needed
.
          MOVE      ZERO,EXIT
          MATCH     ANSY,UPDTHIST
          IF        @EQUAL
            MOVE      "1",NEXTPAGE
          ENDIF
          GOTO      PROWAT99
.
.         ************ DELETE FORMACTION **********
.
PROWAT30  PACK      KEY9,WTPRO001,SP70
          CALL      RDPROA1
          BRANCH    OVRCD,PROWAT91
          CALL      DEPROA1
.
          PROC      WATPKIUP      * Update keyword index
.
          MOVE      "D",KEY1      * Set Delete Parameter for PRAAUD00
          CALL      PROAUD00      * Write Audit File if Needed
.
          MOVE      ZERO,EXIT
          GOTO      PROWAT99
.
.         ************ DISPLAY FORMACTION **********
.
PROWAT40  PACK      KEY5,WG,PTCOD002,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,PTCOD002
          ENDIF
.
          PACK      KEY12,PTCOD002,WTPRO001,SP70
          CALL      RDPROA2
          IF        OVRCD=1
            CALL      CLRPRO00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Waiting List Procedure Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PROWAT99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      PROWAT99
.
PROWAT90  MOVE      "record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PROWAT99
.
PROWAT91  MOVE      "Procedure Code record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PROWAT99
.
PROWAT92  MOVE      "Procedure Code record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PROWAT99
.
PROWAT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PROWAT99
.
PROWAT99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRPRO00  MOVE      SP70,WPCODE
          MOVE      SP70,WPDESC 
          MOVE      ZERO,WPTIME  
          MOVE      ZERO,WPESTLS  
          MOVE      SP70,WPGRP  
          MOVE      SP70,WPRHAT 
          MOVE      SP70,WPCHET 
          MOVE      SP70,WTWPHDPE
          MOVE      SP70,WTWPDRGC 
          MOVE      SP70,WTWPACTV 
.
CLRPRO99  RETURN
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
PROSAV00  MOVE      WTPRO001,WPCODE
          MOVE      WTPRO002,WPDESC 
          MOVE      WTPRO003,WPTIME  
          MOVE      WTPRO004,WPESTLS  
          MOVE      WTPRO005,WPGRP  
          MOVE      WTPRO006,WPRHAT 
          MOVE      WTPRO007,WPCHET 
          MOVE      WTPRO008,WTWPHDPE
          MOVE      WTPRO009,WTWPDRGC 

          COMPARE   TWO,WTPRO010     * Status 0 = Active 1 = Non-Active
          IF        @EQUAL
            MOVE      ZERO,WTWPACTV   
          ELSE 
            MOVE      ONE,WTWPACTV
          ENDIF
.
PROSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKPRO00  RESET     WTPRO001
          MATCH     SP70,WTPRO001
          GOTO      CHKPRO90 IF EQUAL
.
          GOTO      CHKPRO98
.
CHKPRO90  MOVE      "Procedure Code cannot be blank!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKPRO99
.
CHKPRO98  MOVE      ZERO,EXIT
CHKPRO99  RETURN
.---------------------------------------------------------------------
. Writes Waiting List Patient Procedures Audit
.    Parameters :   KEY1 - Set Record Type
.---------------------------------------------------------------------
PROAUD00  CALL      IBACLOCK
          PACK      WTWPAUDD,CCC,CYY,CMM,CDD     * Date
          REP       " 0",WTWPAUDD
          MOVE      CTIMEIS,WTWPAUDT
.
          MOVE      PORT,WTWPAUDP                * Port
          MOVE      KEY1,WTWPAUDR                * Record Type
          MOVE      ONE,WTWPAUDS                 * Not Printed
          MOVE      PASSCODE,WTWPAUDO            * Operator
.
          PACK      KEY19,WTWPAUDD,WTWPAUDT,WTWPAUDP,WTWPAUDR
          CALL      AWPROA1                      * Write Audit File
.
PROAUD99  RETURN
.-------------------------------------------------------------------------------
. Waiting List Consultant Code Maintenance: Display,Add,Update,Delete
.-------------------------------------------------------------------------------
VWLWAT00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      VWLWAT40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      VWLWAT10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      VWLWAT20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      VWLWAT30 IF EQUAL
.
          GOTO      VWLWAT93
.
.         ************ ADD FORMACTION ***********
.
VWLWAT10  CALL      CHKVWL00      * Checks mandatory fields
          BRANCH    EXIT,VWLWAT99
.
          PACK      KEY9,WTVWL002,WTVWL001,SP70
          CALL      RDWTVWL2
          COMPARE   ZERO,OVRCD
          GOTO      VWLWAT92 IF EQUAL
.
          CALL      VWLSAV00      * Moves input variables to file variables
          PACK      KEY9,WTVWL002,WTVWL001,SP70
          CALL      WRWTVWL2      * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      VWLWAT99
.
.         ************ UPDATE FORMACTION **********
.
VWLWAT20  PACK      KEY9,WTVWL002,WTVWL001,SP70
          CALL      RDWTVWL2
          BRANCH    OVRCD,VWLWAT91
          CALL      CHKVWL00      * Checks mandatory fields
          BRANCH    EXIT,VWLWAT99
.
          CALL      VWLSAV00      * Moves input variables to file variables
          PACK      KEY9,WTVWL002,WTVWL001,SP70
          CALL      UPWTVWL2      * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      VWLWAT99
.
.         ************ DELETE FORMACTION **********
.
VWLWAT30  PACK      KEY9,WTVWL002,WTVWL001,SP70
          CALL      RDWTVWL2
          BRANCH    OVRCD,VWLWAT91
          CALL      DEWTVWL2
.
          MOVE      ZERO,EXIT
          GOTO      VWLWAT99
.
.         ************ DISPLAY FORMACTION **********
.
VWLWAT40  MOVE      SP70,DTITL
          MOVE      SP70,DGNAME
          MOVE      SP70,DSNAME
.
          PACK      KEY5,WU,PTCOD002,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,PTCOD002
          ENDIF
.
          PACK      KEY9,PTCOD002,WTVWL001,SP70
          CALL      RDWTVWL2
          IF        OVRCD=1
            CALL      CLRVWL00    * Clear all the file variables
          ENDIF
.
          PACK      KEY6,WTVWL001
          CALL      RDDOCT1
.
          CLEAR     WEBTITLE
          MOVE      "Wating List Consultant Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,VWLWAT99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      VWLWAT99
.

VWLWAT90  MOVE      "record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      VWLWAT99
.
VWLWAT91  MOVE      "Consultant Code record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      VWLWAT99
.
VWLWAT92  MOVE      "Consultant Code record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      VWLWAT99
.
VWLWAT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      VWLWAT99
.
.VWLWAT94  MOVE      "Doctor Record does not Exist.",WEBTITLE
.          CALL      WEBERR00
.          MOVE      ONE,EXIT
.          GOTO      VWLWAT99
.

VWLWAT99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRVWL00  MOVE      SP70,WTVWCONS
          MOVE      SP70,WTVWLTYP 
          MOVE      SP70,WTVWDESC  
          MOVE      ZERO,WTVWWTEL  
          MOVE      ZERO,WTVWWTDC  
          MOVE      ZERO,WTVWSTAT
.
CLRVWL99  RETURN
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
VWLSAV00  MOVE      WTVWL001,WTVWCONS
          MOVE      WTVWL002,WTVWLTYP 
          MOVE      WTVWL003,WTVWDESC  
          MOVE      WTVWL004,WTVWWTEL  
          MOVE      WTVWL005,WTVWWTDC  
.
          COMPARE   TWO,WTVWL006     * Status 0 = Active 1 = Non-Active
          IF        @EQUAL
            MOVE      ZERO,WTVWSTAT   
          ELSE 
            MOVE      ONE,WTVWSTAT
          ENDIF
          MOVE      WTVWL007,WTVWTELE  
.
VWLSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKVWL00  RESET     WTVWL001
          MATCH     SP70,WTVWL001
          GOTO      CHKVWL90 IF EQUAL
.
          GOTO      CHKVWL98
.
CHKVWL90  MOVE      "Consultant Code cannot be blank!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKVWL99
.
CHKVWL98  MOVE      ZERO,EXIT
CHKVWL99  RETURN
.-------------------------------------------------------------------------------
. ESIS Seal Table Maintenance: Display,Add,Update,Delete
.-------------------------------------------------------------------------------
ESIWAT00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ESIWAT40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ESIWAT10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ESIWAT20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ESIWAT30 IF EQUAL
.
          GOTO      ESIWAT93
.
.         ************ ADD FORMACTION ***********
.
ESIWAT10  CALL      CHKESI00      * Checks mandatory fields
          BRANCH    EXIT,ESIWAT99
.
          PACK      KEY8,WATES001,SP70
          CALL      RDWTEST1
          COMPARE   ZERO,OVRCD
          GOTO      ESIWAT92 IF EQUAL
.
          CALL      ESISAV00      * Moves input variables to file variables
          PACK      KEY8,WATES001,SP70
          CALL      WRWTEST1      * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      ESIWAT99
.
.         ************ UPDATE FORMACTION **********
.
ESIWAT20  PACK      KEY8,WATES001,SP70
          CALL      RDWTEST1
          BRANCH    OVRCD,ESIWAT91
          CALL      CHKESI00      * Checks mandatory fields
          BRANCH    EXIT,ESIWAT99
.
          CALL      ESISAV00      * Moves input variables to file variables
          PACK      KEY8,WATES001,SP70
          CALL      UUWTEST1      * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      ESIWAT99
.
.         ************ DELETE FORMACTION **********
.
ESIWAT30  PACK      KEY8,WATES001,SP70
          CALL      RDWTEST1
          BRANCH    OVRCD,ESIWAT91
          CALL      DEWTEST1
.
          MOVE      ZERO,EXIT
          GOTO      ESIWAT99
.
.         ************ DISPLAY FORMACTION **********
.
ESIWAT40  PACK      KEY8,WATES001,SP70
          CALL      RDWTEST1
          IF        OVRCD=1
            CALL      CLRESI00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ESIS Seal Date Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ESIWAT99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ESIWAT99
.
ESIWAT91  MOVE      "ESIS Seal Date record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ESIWAT99
.
ESIWAT92  MOVE      "ESIS Seal Date record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ESIWAT99
.
ESIWAT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ESIWAT99
.
ESIWAT99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRESI00  MOVE      SP70,WTESSDAT
          MOVE      SP70,WTESUSID     
          MOVE      SP70,WTESDATE
          MOVE      SP70,WTESTIME
.
CLRESI99  RETURN
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
ESISAV00  MOVE      WATES001,WTESSDAT
          MOVE      USERID,WTESUSID     
          CALL      IBACLOCK
          PACK      WTESDATE,CCC,CYY,CMM,CDD
          REP       " 0",WTESDATE
          CLOCK     TIME,WTESTIME
.
ESISAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKESI00  RESET     WATES001
          MATCH     SP70,WATES001
          GOTO      CHKESI90 IF EQUAL
.
          GOTO      CHKESI98
.
CHKESI90  MOVE      "Seal Date Code cannot be blank!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKESI99
.
CHKESI98  MOVE      ZERO,EXIT
CHKESI99  RETURN
.-------------------------------------------------------------------------------
. Waiting List Letter Maintenance: Display,Add,Update,Delete
.-------------------------------------------------------------------------------
LETWAT00  CALL      CLWATLET
          MATCH     SP1,UPDTTYPE
          GOTO      LETWAT40 IF EQUAL
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      LETWAT10 IF EQUAL
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      LETWAT20 IF EQUAL
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      LETWAT30 IF EQUAL
          GOTO      LETWAT93
.
LETWAT10  CALL      ADDLET00
          GOTO      LETWAT99
.
LETWAT20  CALL      UPDLET00
          GOTO      LETWAT99
.
LETWAT30  CALL      DELLET00
          GOTO      LETWAT99
.
.         ************ DISPLAY FORMACTION **********
.
LETWAT40  PACK      KEY6,WTLET001,SP1,SP1,ZERO
          CALL      RDLET1  
          IF        OVRCD=1
            CALL      CLWATLET    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Waiting List Letter Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LETWAT99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      LETWAT99
.
LETWAT91  MOVE      "Letter record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LETWAT99
.
LETWAT92  MOVE      "Letter record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LETWAT99
.
LETWAT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LETWAT99
.
LETWAT99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLWATLET  MOVE      ZERO,WLETNO
          MOVE      ZERO,WLINNO 
          MOVE      SP70,WLTEXT
          MOVE      ZERO,WLVAR  
          MOVE      ZERO,WLETPBOT  
          MOVE      ZERO,WLETPTOP  
          MOVE      ZERO,WLETPPAG
          MOVE      ZERO,WLETPTAB
          MOVE      SP70,WLETMMFN 
          MOVE      SP70,WLETVALD 
          MOVE      SP70,WLETAUDL
          MOVE      SP70,WLETACTV
          MOVE      SP70,WLETCOMM
          MOVE      SP70,WLETCONF
          MOVE      SP70,WLETSPAR
          RETURN
.------------------------------------------------------------
. Add Letter
.------------------------------------------------------------
ADDLET00  CALL      CLWATLET
          SQUEEZE   WTLET001
          MOVE      WTLET001,WLETNO
          MOVE      ZERO,WLINNO
          PACK      KEY6,WLETNO,WLINNO,SP70
          CALL      RDLET1
          IF        OVRCD=1
            CALL      MVLET000
            PACK      WLTEXT,WLTEXT,SP70
            CALL      WRLET1
            CALL      LETDET00  * Update Letter Details
            CALL      WINCLS00
          ELSE
            MOVE      "Letter Already on File",WEBTITLE
            CALL      WEBERR00
          ENDIF
          RETURN
.------------------------------------------------------------
. Update Extract
.------------------------------------------------------------
UPDLET00  CALL      CLWATLET
          SQUEEZE   WTLET001
          MOVE      WTLET001,WLETNO
          MOVE      ZERO,WLINNO
          PACK      KEY6,WLETNO,WLINNO,SP70
          CALL      RDLET1
          IF        OVRCD=0
            CALL      MVLET000
            PACK      WLTEXT,WLTEXT,SP70
            CALL      UPLET1
            CALL      LETDET00  * Update Letter Details
            CALL      WINCLS00
          ELSE
            MOVE      "Extract Not On File",WEBTITLE
            CALL      WEBERR00
          ENDIF
          RETURN
.------------------------------------------------------------
. Delete Extract
.------------------------------------------------------------
DELLET00  SQUEEZE   WTLET001
          MOVE      WTLET001,WLETNO
          MOVE      ZERO,WLINNO
          PACK      KEY6,WLETNO,WLINNO,SP70
          CALL      RDLET1
          IF        OVRCD=0
            CALL      DELLET1
            CALL      LETDET00
            CALL      WINCLS00
          ELSE
            MOVE      "Extract Not On File",WEBTITLE
            CALL      WEBERR00
          ENDIF
          RETURN
.
LETDET00  SQUEEZE   WTLET001
          MOVE      WTLET001,WLETNO
          MOVE      WTLET001,LETTNUMB
          MOVE      ZERO,WLINNO
          PACK      KEY6,WLETNO,WLINNO,SP70
          CALL      RDLET1
LETDET10  CALL      RDKLET1
          BRANCH    OVRCD,LETDET20
          COMPARE   WLETNO,LETTNUMB
          GOTO      LETDET20 IF NOT EQUAL
          PACK      KEY6,WLETNO,WLINNO,SP70
          CALL      DELLET1
          CALL      RDSLET1
          GOTO      LETDET10
.
LETDET20  MATCH     "D",UPDTTYPE
          GOTO      LETDET99 IF EQUAL
          COMPARE   ZERO,WTLEEND
          GOTO      LETDET99 IF EQUAL
          CALL      CLWATLET
          MOVE      ZERO,F3
.
LETDET30  ADD       ONE,F3
          MOVE      LETTNUMB,WLETNO
          MOVE      F3,WLINNO
          PACK      KEY6,WLETNO,WLINNO,SP70
          CALL      RDLET1
          IF        OVRCD=0
            MOVE      ZERO,WLVAR
            SCAN      SPECCHAR,WTLETEXT[F3]
            IF        @EQUAL
              MOVE      ONE,WLVAR
            ENDIF
            RESET     WTLETEXT[F3]
            MOVE      WTLETEXT[F3],WLTEXT
            PACK      WLTEXT,WLTEXT,SP70
            CALL      UPLET1
          ELSE
            MOVE      ZERO,WLVAR
            SCAN      SPECCHAR,WTLETEXT[F3]
            IF        @EQUAL
              MOVE      ONE,WLVAR
            ENDIF
            RESET     WTLETEXT[F3]
            MOVE      WTLETEXT[F3],WLTEXT
            PACK      WLTEXT,WLTEXT,SP70
            CALL      WRLET1
          ENDIF
          COMPARE   F3,WTLEEND
          GOTO      LETDET30 IF NOT EQUAL
.
LETDET99  RETURN
.
.-------------------------------------------------------------------------------
. Process Fields called from WEBBOD00 
.-------------------------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40,PROFLD50
          GOTO      PROFLD99
.
.       Waiting List Pocedures Maintenance
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
.
PROFLD11  CALL      CODD0000       * Patent Codes File
          GOTO      PROFLD99
.
PROFLD12  CALL      PROW0000       * Waiting List Procedure Code Maintanance
          GOTO      PROFLD99
.
.       Waiting List Consultant Maintenance
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22,PROFLD23,PROFLD24
.
PROFLD21  CALL      CODD0000       * Patent Codes File
          GOTO      PROFLD99
.
PROFLD22  CALL      VWLW0000       * Waiting List Consultant Code Maintanance
          GOTO      PROFLD99
.
PROFLD23  CALL      DOCF0000       * Doctor Search Details
          GOTO      PROFLD99
.
PROFLD24  CALL      PMWL0000       * Waiting List HCP Unit Search
          GOTO      PROFLD99
.
.
.       Waiting List Letter File Maintenance
.
PROFLD30  CALL      LETW0000       * Letter File Maintanance
          GOTO      PROFLD99
.
.       ESIS Seal Table Maintenance
.
PROFLD40  CALL      ESIW0000       
          GOTO      PROFLD99
.
.       Waiting List Pocedures HDP Maintenance
.
PROFLD50  BRANCH    FLDSETNO,PROFLD51
.
PROFLD51  CALL      WATH0000       * WL procedure HDP Maintenance
          GOTO      PROFLD99
.
PROFLD99  RETURN
.-----------------------------------------------------------
. Option 1 - Waiting List Mantenance
.-----------------------------------------------------------
PROW0000  BRANCH    FLDITMNO,PROW0100,PROW0200,PROW0300,PROW0400,PROW0500:
                             PROW0600,PROW0700,PROW0800,PROW0900,PROW1000:
                             PROW1100,PROW1200,PROW1300,PROW1400,PROW1500:
                             PROW1600,PROW1700
          GOTO      PROW9999
.
PROW0100  WRITE     HTMLFILE;WPCODE;    * Procedure Code
          GOTO      PROW9999
.
PROW0200  WRITE     HTMLFILE;WPDESC;    * Description
          GOTO      PROW9999
.
PROW0300  WRITE     HTMLFILE;WPTIME;    * Procedure Time
          GOTO      PROW9999
.
PROW0400  WRITE     HTMLFILE;WPESTLS;   * Estimated Length of Stay
          GOTO      PROW9999
.
PROW0500  WRITE     HTMLFILE;WPGRP;
          GOTO      PROW9999
.
PROW0600  WRITE     HTMLFILE;WPRHAT;    * RHA Threshold Level
          GOTO      PROW9999
.
PROW0700  WRITE     HTMLFILE;WPCHET;    * CHE Threshold Level
          GOTO      PROW9999
.
PROW0800  WRITE     HTMLFILE;WTWPHDPE;  * Procedure code HDP Equivalent
          GOTO      PROW9999
.
PROW0900  WRITE     HTMLFILE;WTWPDRGC;  * DRG Code
          GOTO      PROW9999
.
PROW1000  WRITE     HTMLFILE;WTWPACTV;  * Status
          GOTO      PROW9999
.
PROW1100  MOVE      ZERO,TABLTYPE * Maintenance Type
          CALL      PROTB000      * Table of Waiting List Maintanance
          GOTO      PROW9999
.
PROW1200  CALL      NEXTPR00     * Link to Next Set of Codes
          GOTO      PROW9999
.
PROW1300  CALL      PREVPR00     * Link to Prev Set of Codes
          GOTO      PROW9999
.
PROW1400  MOVE      ONE,TABLTYPE * Search Type
          CALL      PROTB000     * Table of Waiting List Maintanance
          GOTO      PROW9999
.
PROW1500  MOVE      TWO,TABLTYPE * Search Type
          CALL      PROTB000     * Table of Waiting List Maintanance - No Unit
          GOTO      PROW9999
.
PROW1600  MOVE      THREE,TABLTYPE * Search Type
          CALL      PROSR000     * Waiting List Procedure Search - Keyword
          GOTO      PROW9999
.
PROW1700  MOVE      FOUR,TABLTYPE * Search Type
          CALL      PROSR000     * Waiting List Procedure Search - Keyword
          GOTO      PROW9999
.
PROW9999  RETURN
+
.-----------------------------------------------------------
. Option 5 - Waiting List HDP Mantenance
.-----------------------------------------------------------
WATH0000  BRANCH    FLDITMNO,WATH0100,WATH0200,WATH0300,WATH0400,WATH0500:
                             WATH0600,WATH0700,WATH0800,WATH0900,WATH1000
          GOTO      WATH9999
.
WATH0100  WRITE     HTMLFILE;WTPHPROC;    * Pocedure Code
          GOTO      WATH9999
.
WATH0200  WRITE     HTMLFILE;WTPHEFDT;    * Effective Date
          GOTO      WATH9999
.
WATH0300  WRITE     HTMLFILE;WTPHHDPV;    * HDP Value
          GOTO      WATH9999
.
WATH0400  WRITE     HTMLFILE;WTPHDRGC;    * DRG Code
          GOTO      WATH9999
.
WATH0500  WRITE     HTMLFILE;WTPHRHAT;    * RHA Threshold level
          GOTO      WATH9999
.
WATH0600  WRITE     HTMLFILE;WTPHCHET;    * CHE Threshold level
          GOTO      WATH9999
.
WATH0700  CALL      HDPTAB00              * List of records
          GOTO      WATH9999
.
WATH0800  WRITE     HTMLFILE;WTHDP001;    * Procedure code
          GOTO      WATH9999
.
WATH0900  PACK      KEY9,WTHDP001,SP70
          CALL      RDPROA1
          IF        OVRCD=1
            MOVE      "Invalid Procedure Code",WPDESC
          ENDIF
          WRITE     HTMLFILE;WPDESC;      * Procedure description
          GOTO      WATH9999
.
WATH1000  WRITE     HTMLFILE;WTHDP007;    * Default HDP from parent page
          GOTO      WATH9999
.
WATH9999  RETURN
+
.-----------------------------------------------------------
. Show Procedure HDP List
.-----------------------------------------------------------
HDPTAB00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      HDPHD000      * Output Table Heading
.
          PACK      KEY17,WTHDP001,Z70
          CALL      RSWTPHI1
HDPTAB10  CALL      RPWTPHI1
          BRANCH    OVRCD,HDPTAB99
.
          MATCH     WTHDP001,WTPHPROC
          GOTO      HDPTAB99 IF NOT EQUAL
.
          UNPACK    WTPHEFDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      FRMTDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:DeleteRecord":
                             "(#"",WTPHPROC,"#",#"",WTPHEFDT,"#")'>":
                             "<img src=../images/DeleteIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>";
.
          WRITE     HTMLFILE;WTPHPROC,"&nbsp</td>":
                             "<td>",WTPHHDPV,"&nbsp</td>":
                             "<td>",FRMTDATE,"&nbsp":
                             "</td>":
                             "</tr>"
.
          GOTO      HDPTAB10
.
HDPTAB99  RETURN
+
.------------------------------------------------------------
. Procedure HDP List Heading (TABLE HEADING)
.------------------------------------------------------------
HDPHD000  WRITE     HTMLFILE;"<tr>":
                       "<td class=HeadingCell width=33%>Procedure Code</td>":
                       "<td class=HeadingCell width=33%>HDP Value</td>":
                       "<td class=HeadingCell width=33%>Effective to Date</td>":
                       "</tr>"
.
HDPHD999  RETURN
.
.-----------------------------------------------------------
. Code Record
.-----------------------------------------------------------
CODD0000  BRANCH    FLDITMNO,CODD0100,CODD0200,CODD0300,CODD0400,CODD0500:
                             CODD0600,CODD0700,CODD0800,CODD0900,CODD1000:
                             CODD1100,CODD1200,CODD1300,CODD1400,CODD1500:
                             CODD1600
          GOTO      CODD9999
.
CODD0100  WRITE     HTMLFILE;TCODE;
          GOTO      CODD9999
.
CODD0200  WRITE     HTMLFILE;ACODE;
          GOTO      CODD9999
.
CODD0300  MATCH     SP70,TDESC
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TDESC;
          GOTO      CODD9999
.
CODD0400  MATCH     SP70,THCSCOD
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;THCSCOD;
          GOTO      CODD9999
.
CODD0500  COMPARE   ZERO,TCFORM6
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCFORM6;
          GOTO      CODD9999
.
CODD0600  MATCH     SP70,PTCOACTV
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;PTCOACTV;
          GOTO      CODD9999
.
CODD0700  MATCH     SP70,TCINDC1
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC1;
          GOTO      CODD9999
.
CODD0800  MATCH     SP70,TCINDC2
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC2;
          GOTO      CODD9999
.
.
CODD0900  MATCH     SP70,TCINDC3
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC3;
          GOTO      CODD9999
.
CODD1000  MATCH     SP70,TCINDC4
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC4;
          GOTO      CODD9999
.
CODD1100  MATCH     SP70,TCINDC5
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC5;
          GOTO      CODD9999
.
CODD1200  MATCH     SP70,TCINDC6
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC6;
          GOTO      CODD9999
.
CODD1300  MATCH     SP70,TCINDC7
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC7; 
          GOTO      CODD9999
.
CODD1400  MATCH     SP70,TCINDC8
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC8;
          GOTO      CODD9999
.
CODD1500  MATCH     SP70,TCINDC9
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC9;
          GOTO      CODD9999
.
CODD1600  MATCH     SP70,TCINDC10
          GOTO      CODD9999 IF EQUAL
          WRITE     HTMLFILE;TCINDC10;
          GOTO      CODD9999
.
CODD9999  RETURN
.-----------------------------------------------------------
. Doctor Unit using pmstmdaf
.-----------------------------------------------------------
PMWL0000  BRANCH    FLDITMNO,PMWL0100,PMWL0200,PMWL0300
          GOTO      PMWL9999
.
PMWL0100  CALL      NEXTPR00     * Link to Next Set of Codes
          GOTO      PMWL9999
.
PMWL0200  CALL      PREVPR00     * Link to Prev Set of Codes
          GOTO      PMWL9999
.
PMWL0300  MOVE      ONE,TABLTYPE
          CALL      VPMTB000     * Table of Waiting List Consultant Maintanance
          GOTO      PMWL9999
.
PMWL0400  PACK      KEY5,ANSC,ANSU,PTCOD002,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PMWL9999
          WRITE     HTMLFILE;TDESC;
          GOTO      PMWL9999
.
PMWL9999  RETURN
+
.------------------------------------------------------------
. Table of Waiting List Consultant Codes - using pmstmdaf
.------------------------------------------------------------
VPMTB000  IF        TABLTYPE=0
            UNPACK    DIM127,D1,LINKPRG,DIM127
            UNPACK    DIM127,D1,LINKREP,DIM127
            UNPACK    DIM127,D1,LINKTEM,DIM127
          ENDIF
.
          CALL      VPHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      "10",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          MATCH     SP70,INTDHOSP
          IF        @EQUAL
            PACK      KEY21,WBSEHOSP,PTCOD002,STARTKEY,SP70
          ELSE
            PACK      KEY21,INTDHOSP,PTCOD002,STARTKEY,SP70
          ENDIF
          CALL      RDPMTMD2
          IF        OVRCD=0
            CALL      RPPMTMD2
          ENDIF
VPMTB100  CALL      RKPMTMD2
          BRANCH    OVRCD,VPMTB999
.
          MATCH     SP70,INTDHOSP
          IF        !@EQUAL
            MATCH     INTDHOSP,PMTDHOSP
            GOTO      VPMTB999 IF NOT EQUAL
          ENDIF
          MATCH     PTCOD002,PMTDUNIT
          GOTO      VPMTB999 IF NOT EQUAL
.
          PACK      KEY6,PMTDDOCT,SP70
          CALL      RDDOCT1
          BRANCH    OVRCD,VPMTB100
          IF        TABLTYPE=1
            COMPARE   ONE,DRSTAT
            GOTO      VPMTB100 IF EQUAL
          ENDIF
.
          MATCH     SP70,PMTDTEAM
          GOTO      VPMTB100 IF NOT EQUAL   * only return header records
.
          MATCH     "1",PMTDACTV
          GOTO      VPMTB100 IF EQUAL       * only return active docs
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      VPMTB100
          ENDIF
.
          CALL      VPMRW000    * Display Table Details Row
          ADD       ONE,DISNUMB
          PACK      KEY21,PMTDHOSP,PMTDUNIT,PMTDDOCT,Z70
          CALL      RSPMTMD2
          CALL      RPPMTMD2
          
          COMPARE   NORECORD,DISNUMB
          GOTO      VPMTB100 IF NOT EQUAL
.
VPMTB999  RETURN
.------------------------------------------------------------
. Waiting List Consultant Table Details (TABLE HEADING)
.------------------------------------------------------------
VPHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell>":
                               "Consultant Code</td>":
                               "<td class=HeadingCell>":
                               "Consultant Name</td>":
                               "<td class=HeadingCell>":
                               "Active</td>":
                             "</tr>"
.
VPHED999  RETURN
.------------------------------------------------------------
. Waiting List Consultant - pmstmdaf (TABLE ROW)
.------------------------------------------------------------
VPMRW000  MATCH     "1",PMTDACTV
          IF        @EQUAL
            MOVE      "Not Active",KEY10
          ELSE
            MOVE      "Active",KEY10
          ENDIF
.
          CALL      JAVDOC00                * Format javascript doctor name
.
          IF        TABLTYPE=0
            WRITE     HTMLFILE;"<tr><td nowrap>":
                               "<A HREF='javascript:ShowDetail02":
                               "(#"",LINKPRG,"#.pbl":
                               "?reportno=",LINKREP:
                               "&template=",LINKTEM:
                               "&wtvwl002=",PTCOD002:
                               "&ptcod002=",PTCOD002:
                               "&wtvwl001=",PMTDDOCT,"#")'>":
                               "<img src=../images/MaintenanceIcon.gif ":
                               "hspace=4 border=0 align=absmiddle></a>":
                               PMTDDOCT,"</td>";
          ELSE
            WRITE     HTMLFILE;"<tr><td nowrap>":
                               "<A HREF='javascript:updateParent":
                               "(#"",PMTDDOCT,"#",#"":
                                     JAVFNAME,"#",#"":
                                     PMTDUNIT,"#")'>":
                               PMTDDOCT,"</a></td>";
          ENDIF
          WRITE     HTMLFILE;"<td>",DOCFNAME,"</td>":
                             "<td>",KEY10,"</td>":
                             "</tr>";
.
VPMRW999 RETURN
+
.-----------------------------------------------------------
. Option 2 - Waiting List Consultant Code Mantenance
.-----------------------------------------------------------
VWLW0000  BRANCH    FLDITMNO,VWLW0100,VWLW0200,VWLW0300,VWLW0400,VWLW0500:
                             VWLW0600,VWLW0700,VWLW0800,VWLW0900,VWLW1000:
                             VWLW1100
          GOTO      VWLW9999
.
VWLW0100  WRITE     HTMLFILE;WTVWCONS;    * Consultant
          GOTO      VWLW9999
.
VWLW0200  WRITE     HTMLFILE;WTVWLTYP;    * List Type
          GOTO      VWLW9999
.
VWLW0300  WRITE     HTMLFILE;WTVWDESC;    * Description
          GOTO      VWLW9999
.
VWLW0400  WRITE     HTMLFILE;WTVWWTEL;    * Maximum Waiting Time - Elective
          GOTO      VWLW9999
.
VWLW0500  WRITE     HTMLFILE;WTVWWTDC;    * Maximum Waiting Time - Day Case
          GOTO      VWLW9999
.
VWLW0600  WRITE     HTMLFILE;WTVWSTAT;    * Status 0=Active 1=Inactive
          GOTO      VWLW9999
.
VWLW0700  MOVE      ZERO,TABLTYPE
          CALL      VWLTB000     * Table of Waiting List Consultant Maintanance
          GOTO      VWLW9999
.
VWLW0800  CALL      NEXTPR00     * Link to Next Set of Codes
          GOTO      VWLW9999
.
VWLW0900  CALL      PREVPR00     * Link to Prev Set of Codes
          GOTO      VWLW9999
.
VWLW1000  MOVE      ONE,TABLTYPE
          CALL      VWLTB000     * Table of Waiting List Consultant Maintanance
          GOTO      VWLW9999
.
VWLW1100  WRITE     HTMLFILE;WTVWTELE;    * Telephone Number             
          GOTO      VWLW9999
.
VWLW9999  RETURN
.-----------------------------------------------------------
. Option 3 - Waiting List Letter File Mantenance
.-----------------------------------------------------------
LETW0000  BRANCH    FLDITMNO,LETW0100,LETW0200,LETW0300,LETW0400,LETW0500:
                             LETW0600,LETW0700,LETW0800,LETW0900,LETW1000:
                             LETW1100,LETW1200,LETW1300
          GOTO      LETW9999
.
LETW0100  COMPARE   ZERO,WLETNO
          GOTO      LETW0110 IF EQUAL
          WRITE     HTMLFILE;WLETNO;
          GOTO      LETW9999
.
LETW0110  MOVE      ZERO,F3
          PACK      KEY6,F3,Z70
          CALL      RDSLET2
          CALL      RDPLET2
          IF        OVRCD=1
            WRITE     HTMLFILE;"  1";
          ELSE
            ADD       ONE,WLETNO
            WRITE     HTMLFILE;WLETNO;
            CALL      CLWATLET
          ENDIF
          GOTO      LETW9999
.
LETW0200  MATCH     SP70,WLTEXT
          GOTO      LETW9999 IF EQUAL
          WRITE     HTMLFILE;WLTEXT;    * Letter Bottom Margin Size
          GOTO      LETW9999
.
LETW0300  WRITE     HTMLFILE;WLETPBOT;    * Letter Bottom Margin Size
          GOTO      LETW9999
.
LETW0400  WRITE     HTMLFILE;WLETPTOP;    * Leter Top Margin Size
          GOTO      LETW9999
.
LETW0500  WRITE     HTMLFILE;WLETPPAG;    * Physical Paper Length
          GOTO      LETW9999
.
LETW0600  WRITE     HTMLFILE;WLETPTAB;    * Left Margin
          GOTO      LETW9999
.
LETW0700  WRITE     HTMLFILE;WLETMMFN;    * Mail Merge File ?
          GOTO      LETW9999
.
LETW0800  CALL      LETTAB00
          GOTO      LETW9999
.
LETW0900  SQUEEZE   WTLET001
          MOVE      ZERO,LETTNUMB
          MOVE      WTLET001,LETTNUMB
          COMPARE   ZERO,LETTNUMB
          GOTO      LETW9999 IF EQUAL
          PACK      KEY6,LETTNUMB,SP1,SP1,ZERO
          CALL      RDLET1
LETW0910  CALL      RDKLET1
          BRANCH    OVRCD,LETW0990
          COMPARE   LETTNUMB,WLETNO
          GOTO      LETW0990 IF NOT EQUAL
.
          STRIP     WLTEXT
          MOVELPTR  WLTEXT,F3
          IF        F3>0
            SFORMAT   VAR,F3
          ELSE
            SFORMAT   VAR,1
          ENDIF
          MOVE      WLTEXT,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
          GOTO      LETW0910
.
LETW0990  PACK      KEY6,LETTNUMB,SP1,SP1,ZERO
          CALL      RDLET1
          GOTO      LETW9999
.
LETW1000  WRITE     HTMLFILE;WLETAUDL;    
          GOTO      LETW9999
.
LETW1100  WRITE     HTMLFILE;WLETACTV;
          GOTO      LETW9999
.
LETW1200  WRITE     HTMLFILE;WLETCOMM;
          GOTO      LETW9999
.
LETW1300  WRITE     HTMLFILE;WLETCONF;
          GOTO      LETW9999
.
LETW9999  RETURN
.-----------------------------------------------------------
. Option 4 - ESIS Seal Table Mantenance
.-----------------------------------------------------------
ESIW0000  BRANCH    FLDITMNO,ESIW0100,ESIW0200,ESIW0300,ESIW0400,ESIW0500:
                             ESIW0600,ESIW0700
          GOTO      ESIW9999
.
ESIW0100  UNPACK    WTESSDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY12,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR          
          WRITE     HTMLFILE;KEY12
          GOTO      ESIW9999
.
ESIW0200  WRITE     HTMLFILE;WTESUSID;  
          GOTO      ESIW9999
.
ESIW0300  UNPACK    WTESDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY12,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR            
          WRITE     HTMLFILE;KEY12
          GOTO      ESIW9999
.
ESIW0400  WRITE     HTMLFILE;WTESTIME;   
          GOTO      ESIW9999
.
ESIW0500  CALL      ESITB000     * Table of ESIS Seal
          GOTO      ESIW9999
.
ESIW0600  CALL      NEXTPR00     * Link to Next Set of Codes
          GOTO      ESIW9999
.
ESIW0700  CALL      PREVPR00     * Link to Prev Set of Codes
          GOTO      ESIW9999
.
ESIW9999  RETURN
.-----------------------------------------------------------
. Link to Next Group Codes           
.-----------------------------------------------------------
NEXTPR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       "'~",KEYWORDS
.
          WRITE     HTMLFILE;"watweb02.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&ptcod002=",PTCOD002:
                                 "&startkey=",STARTKEY:
                                 "&keywords=",KEYWORDS;
.
          REP       "~'",KEYWORDS
.
NEXTPR99  RETURN
.-----------------------------------------------------------
. Link to Prev Group Codes           
.-----------------------------------------------------------
PREVPR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       "'~",KEYWORDS
.
          WRITE     HTMLFILE;"watweb02.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&ptcod002=",PTCOD002:
                                 "&startkey=",STARTKEY:
                                 "&keywords=",KEYWORDS;
.
          REP       "~'",KEYWORDS
.
PREVPR99  RETURN
.-----------------------------------------------------------
. Link for Redirect            
.-----------------------------------------------------------
LNKPRI00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127

          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;LINKPRG,"#.pbl":
                                 "?reportno=",LINKREP:
                                 "&template=",LINKTEM;
.
LNKPRI99  RETURN
.------------------------------------------------------------
. Table of Waiting List Procedure Codes
.------------------------------------------------------------
PROTB000  IF        TABLTYPE=0
            UNPACK    DIM127,D1,LINKPRG,DIM127
            UNPACK    DIM127,D1,LINKREP,DIM127
            UNPACK    DIM127,D1,LINKTEM,DIM127
          ENDIF
.
          CALL      PRHED000      * Output Table Heading
.
          IF        TABLTYPE<>2
            MATCH     SP70,PTCOD002
            GOTO      PROTB999 IF EQUAL
          ENDIF
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD     * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          IF        TABLTYPE=0
            PACK      KEY12,PTCOD002,STARTKEY,SP70
            CALL      RDPROA2                                          *I-57507
          ELSE
            IF        TABLTYPE=1
              PACK      KEY62,PTCOD002,STARTKEY,SP70
              CALL      RDPROA4
            ELSE
              PACK      KEY59,STARTKEY,SP70
              CALL      RDPROA3
            ENDIF
          ENDIF
.
          IF        OVRCD=0
            IF        TABLTYPE=0
              CALL      RDPPROA2                                       *I-57507
            ELSE
              IF        TABLTYPE=1
                CALL      RDPPROA4
              ELSE
                CALL      RDPPROA3
              ENDIF
            ENDIF
          ENDIF
.
PROTB100  IF        TABLTYPE=0
            CALL      RDKPROA2                                         *I-57507
          ELSE
            IF        TABLTYPE=1
              CALL      RDKPROA4
            ELSE
              CALL      RDKPROA3
            ENDIF
          ENDIF
.
          BRANCH    OVRCD,PROTB900
.
          IF        TABLTYPE<>2
            MATCH     PTCOD002,WPGRP
            GOTO      PROTB900 IF NOT EQUAL
          ENDIF
.
          IF        TABLTYPE=1|TABLTYPE=2   
            COMPARE   ZERO,WTWPACTV
            GOTO      PROTB100 IF NOT EQUAL
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      PROTB100
          ENDIF
.
          CALL      PRORW000    * Display Table Details Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      PROTB100 IF NOT EQUAL
          GOTO      PROTB999
.
PROTB900  CALL      NOMORE00    * Display No More Records Message 
.
PROTB999  RETURN
.------------------------------------------------------------
. Waiting List Table Details (TABLE HEADING)
.------------------------------------------------------------
PRHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=15%>":
                               "Procedure Code</td>":
                               "<td class=HeadingCell width=70%>":
                               "Description</td>":
                               "<td class=HeadingCell width=15%>":
                               "Active</td>":
                             "</tr>"
.
PRHED999  RETURN
.------------------------------------------------------------
. Waiting List Maintenance (TABLE ROW)
.------------------------------------------------------------
PRORW000  MOVE      "WG",CATEGORY
          MOVE      WPGRP,CODE 
          CALL      GETCOD00
.
          COMPARE   ZERO,WTWPACTV
          IF        @EQUAL
            MOVE      "Active",KEY10
          ELSE
            MOVE      "Not Active",KEY10
          ENDIF
.
          MOVE      WPDESC,CNVQTDES     * String to Convert
          CALL      CNVQTE00            * Replace "'" with "%27" &
.                                       * Replace """ with SPACE
.
          IF        TABLTYPE=0
            WRITE     HTMLFILE;"<tr><td nowrap>":
                               "<A HREF='javascript:ShowDetail01":
                               "(#"",LINKPRG,"#.pbl":
                               "?reportno=",LINKREP:
                               "&template=",LINKTEM:
                               "&ptcod002=",PTCOD002:
                               "&wtpro001=",WPCODE,"#")'>":
                               "<img src=../images/MaintenanceIcon.gif ":
                               "hspace=4 border=0 align=absmiddle></a>":
                               WPCODE,"</td>";
          ELSE
            IF        TABLTYPE=4
              WRITE     HTMLFILE;"<tr><td nowrap>":
                                 "<A HREF='javascript:updateParent":
                                 "(#"",WPCODE,"#",#"":
                                  CNVQTDES,"#",#"":
                                  WPESTLS,"#",#"":
                                  WPGRP,"#",#"":
                                  WPTIME,"#",#"":
                                  WPRHAT,"#")'>":
                                  WPCODE,"</a></td>";

            ELSE
              WRITE     HTMLFILE;"<tr><td nowrap>":
                                 "<A HREF='javascript:updateParent":
                                 "(#"",WPCODE,"#",#"":
                                  CNVQTDES,"#",#"":
                                  WPESTLS,"#",#"":
                                  WPGRP,"#",#"":
                                  WPTIME,"#",#"":
                                  WPCHET,"#")'>":
                                  WPCODE,"</a></td>";
            ENDIF
          ENDIF
          WRITE     HTMLFILE;"<td>",WPDESC,"</td>":
                             "<td>",KEY10,"</td>":
                             "</tr>"
.
PRORW999 RETURN
.------------------------------------------------------------
. Table of Waiting List Consultant Codes
.------------------------------------------------------------
VWLTB000  IF        TABLTYPE=0
            UNPACK    DIM127,D1,LINKPRG,DIM127
            UNPACK    DIM127,D1,LINKREP,DIM127
            UNPACK    DIM127,D1,LINKTEM,DIM127
          ENDIF
.
          CALL      VWHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY9,PTCOD002,STARTKEY,SP70
          CALL      RDWTVWL2
          IF        OVRCD=0
            CALL      RPWTVWL2
          ENDIF
VWLTB100  CALL      RKWTVWL2
          BRANCH    OVRCD,VWLTB900
          MATCH     PTCOD002,WTVWLTYP
          GOTO      VWLTB900 IF NOT EQUAL
          IF        TABLTYPE=1
            COMPARE   ZERO,WTVWSTAT
            GOTO      VWLTB100 IF NOT EQUAL
          ENDIF
.
          PACK      KEY6,WTVWCONS
          CALL      RDDOCT1
          BRANCH    OVRCD,VWLTB100
          IF        TABLTYPE=1
            COMPARE   ONE,DRSTAT
            GOTO      VWLTB100 IF EQUAL
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      VWLTB100
          ENDIF
.
          CALL      VWLRW000    * Display Table Details Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      VWLTB100 IF NOT EQUAL
          GOTO      VWLTB999
.
VWLTB900  CALL      NOMORE00    * Display No More Records Message 
.
VWLTB999  RETURN
.------------------------------------------------------------
. Waiting List Consultant Table Details (TABLE HEADING)
.------------------------------------------------------------
VWHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=15%>":
                               "Consultant Code</td>":
                               "<td class=HeadingCell width=30%>":
                               "Consultant Name</td>":
                               "<td class=HeadingCell width=45%>":
                               "Description</td>":
                               "<td class=HeadingCell width=10%>":
                               "Active</td>":
                             "</tr>"
.
VWHED999  RETURN
.------------------------------------------------------------
. Waiting List Consultant Maintenance (TABLE ROW)
.------------------------------------------------------------
VWLRW000  MATCH     SP70,WTVWDESC
          IF        @EQUAL
            MOVE      "&nbsp",DIM40
          ELSE
            MOVE      WTVWDESC,DIM40
          ENDIF
.
          COMPARE   ZERO,WTVWSTAT
          IF        @EQUAL
            MOVE      "Active",KEY10
          ELSE
            MOVE      "Not Active",KEY10
          ENDIF
.
          CALL      JAVDOC00                * Format javascript doctor name
. 
          IF        TABLTYPE=0
            WRITE     HTMLFILE;"<tr><td nowrap>":
                               "<A HREF='javascript:ShowDetail02":
                               "(#"",LINKPRG,"#.pbl":
                               "?reportno=",LINKREP:
                               "&template=",LINKTEM:
                               "&wtvwl002=",PTCOD002:
                               "&ptcod002=",PTCOD002:
                               "&wtvwl001=",WTVWCONS,"#")'>":
                               "<img src=../images/MaintenanceIcon.gif ":
                               "hspace=4 border=0 align=absmiddle></a>":
                               WTVWCONS,"</td>";
          ELSE
            WRITE     HTMLFILE;"<tr><td nowrap>":
                               "<A HREF='javascript:updateParent":
                               "(#"",WTVWCONS,"#",#"":
                                     JAVFNAME,"#",#"":
                                     WTVWLTYP,"#")'>":
                               WTVWCONS,"</a></td>";
          ENDIF
          WRITE     HTMLFILE;"<td>",DOCFNAME,"</td>":
                             "<td>",DIM40,"</td>":
                             "<td>",KEY10,"</td>":
                             "</tr>";
.
VWLRW999 RETURN
.------------------------------------------------------------
. Output Table of Letter Headers Details
.------------------------------------------------------------
LETTAB00  MOVE      ZERO,F3
          PACK      KEY6,F3,SP70
          CALL      RDSLET2
LETTAB10  CALL      RDKLET2
          BRANCH    OVRCD,LETTAB99
          COMPARE   ZERO,WLINNO
          GOTO      LETTAB99 IF NOT EQUAL
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateLetter('",HTMLLINK
          APPEND    WLETNO,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      DYES,DISPACTV
          MATCH     "1",WLETACTV
          IF        @EQUAL
            MOVE     DNO,DISPACTV
          ENDIF
.
          MATCH     "1",WLETCOMM
          IF        @EQUAL
            MOVE    DYES,SMSSDESC
          ELSE
            MOVE    DNO,SMSSDESC
          ENDIF
.
          MATCH     "1",WLETCONF
          IF        @EQUAL
            MOVE    DYES,CONFDESC
          ELSE
            MOVE    DNO,CONFDESC
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",WLETNO,"#",":
                             "#"",WLTEXT,"#",":
                             "#"",WLETPBOT,"#",":
                             "#"",WLETPTOP,"#",":
                             "#"",WLETPPAG,"#",":
                             "#"",WLETPTAB,"#",":
                             "#"",WLETMMFN,"#",":
                             "#"",DISPACTV,"#",":
                             "#"",SMSSDESC,"#",":
                             "#"",CONFDESC,"#");"
.
          GOTO      LETTAB10
.
LETTAB99  RETURN
.------------------------------------------------------------
. Table of ESIS Seals
.------------------------------------------------------------
ESITB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      ESHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY8,STARTKEY,SP70
          CALL      RSWTEST1
          IF        OVRCD=0
            CALL      RPWTEST1
          ENDIF
ESITB100  CALL      RKWTEST1
          BRANCH    OVRCD,ESITB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      ESITB100
          ENDIF
.
          CALL      ESIRW000    * Display Table Details Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      ESITB100 IF NOT EQUAL
          GOTO      ESITB999
.
ESITB900  CALL      NOMORE00    * Display No More Records Message
.
ESITB999  RETURN
.------------------------------------------------------------
. ESIS Seal Table Details (TABLE HEADING)
.------------------------------------------------------------
ESHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=25%>":
                               "Seal Date</td>":
                               "<td class=HeadingCell width=25%>":
                               "Web User ID</td>":
                               "<td class=HeadingCell width=25%>":
                               "Date Seal Entered</td>":
                               "<td class=HeadingCell width=25%>":
                               "Time Seal Entered</td>":
                             "</tr>"
.
ESHED999  RETURN
.------------------------------------------------------------
. ESIS Seal Table Details  (TABLE ROW)
.------------------------------------------------------------
ESIRW000  UNPACK    WTESDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY13,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR

          UNPACK    WTESSDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY12,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR

          WRITE     HTMLFILE;"<tr><td nowrap>":
                             "<A HREF='javascript:ShowDetail04":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&wates001=",WTESSDAT,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "hspace=4 border=0 align=absmiddle></a>":
                             KEY12,"</td>":
                             "<td>",WTESUSID,"</td>":
                             "<td>",KEY13,"</td>":
                             "<td>",WTESTIME,"</td>":
                             "</tr>";
.
ESIRW999 RETURN
.------------------------------------------------------------
. Move in Letter Details
.------------------------------------------------------------
MVLET000  MATCH     Z70,WTLET001
          IF        !@EQUAL
            SQUEEZE    WTLET001
            MOVE       WTLET001,WLETNO
          ENDIF
          MATCH     Z70,WTLET002
          IF        !@EQUAL
            MOVE       WTLET002,WLTEXT
          ENDIF
          MATCH     Z70,WTLET003
          IF        !@EQUAL
            SQUEEZE    WTLET003
            MOVE       WTLET003,WLETPBOT
          ENDIF
          MATCH     Z70,WTLET004
          IF        !@EQUAL
            SQUEEZE    WTLET004
            MOVE       WTLET004,WLETPTOP
          ENDIF
          MATCH     Z70,WTLET005
          IF        !@EQUAL
            SQUEEZE    WTLET005
            MOVE       WTLET005,WLETPPAG
          ENDIF
          MATCH     Z70,WTLET006
          IF        !@EQUAL
            SQUEEZE    WTLET006
            MOVE       WTLET006,WLETPTAB
          ENDIF
          MATCH     Z70,WTLET007
          IF        !@EQUAL
            MOVE       WTLET007,WLETMMFN
          ENDIF
.
          MATCH     Z70,WTLET008
          IF        !@EQUAL
            MATCH      "1",WTLET008
            IF         @EQUAL
              MOVE       "A",WLETAUDL
            ENDIF
          ENDIF
.
          MATCH     Z70,WTLET009
          IF        !@EQUAL
            MOVE       WTLET009,WLETACTV
          ENDIF
.
          MATCH     Z70,WTLET010
          IF        !@EQUAL
            MOVE       WTLET010,WLETCOMM
          ENDIF
.
          MATCH     Z70,WTLET011
          IF        !@EQUAL
            MOVE       WTLET011,WLETCONF
          ENDIF
.
          RETURN
.
.------------------------------------------------------------
. Table of Waiting List Procedure Codes
.------------------------------------------------------------
PROSR000  CALL      PRHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      "10",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          CALL      GETWRD00
.
          STRIP     KEYWRD01
          PACK      KEY59,KEYWRD01,SP70
          CALL      RSWTPKI2
PROSR100  CALL      RKWTPKI2
          BRANCH    OVRCD,PROSR999
.
          PACK      KEY9,WTPKCODE,SP70
          CALL      RDPROA1
          BRANCH    OVRCD,PROSR100
.
          CALL      WATPKB00
          BRANCH    EXIT,PROSR100,PROSR999
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      PROSR100
          ENDIF
.
          CALL      PRORW000    * Display Table Details Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      PROSR100 IF NOT EQUAL
.
PROSR999  RETURN
.
+
.------------------------------------------------------------
. Check keywords
.------------------------------------------------------------
WATPKB00  MATCH     KEYWRD01,WTPKKKWD
          GOTO      WATPKB92 IF NOT EQUAL
.
          IF        KEYWRDNO>1
            PACK      SAVKEY59,WTPKKKWD,WTPKCODE
            CALL      WATPKC00
            BRANCH    EXIT,WATPKB91
.
            MOVE      SAVKEY59,KEY59
            CALL      RDWTPKI2
          ENDIF
.
          COMPARE   ZERO,WTWPACTV
          GOTO      WATPKB91 IF NOT EQUAL
.
WATPKB90  MOVE      ZERO,EXIT
          GOTO      WATPKB99
.
WATPKB91  MOVE      ONE,EXIT
          GOTO      WATPKB99
.
WATPKB92  MOVE      TWO,EXIT
.
WATPKB99  RETURN
.------------------------------------------------------------
. Check for additional Matching Words
.------------------------------------------------------------
WATPKC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,WTPKCODE,SP70
.
WATPKC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      WATPKC99 IF EQUAL     * Exit OK
.
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
.
          PACK      KEY59,WTPKCODE,KEYWRDXX
          CALL      RDWTPKI1              * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      WATPKC10 IF EQUAL
.
          CALL      RKWTPKI1              * Not Exact so Read Next Record
          BRANCH    OVRCD,WATPKC90        * No Match So Exit No Match
.
          PACK      KEY9,WTPKCODE,SP70
          MATCH     SAVKEY9,KEY9          * Match Key Fields
          GOTO      WATPKC90 IF NOT EQUAL * No Match So Exit
.
          STRIP     KEYWRDXX
          MATCH     WTPKKKWD,KEYWRDXX     * Check Key Word
          GOTO      WATPKC90 IF NOT EQUAL * No Match So Exit
.
          GOTO      WATPKC10              * Check Next Search Word
.
WATPKC90  MOVE      ONE,EXIT
.
WATPKC99  RETURN
.
+
.-------------------------------------------------------------------------------
. Waiting List Procedure HDP Maintenance: Display,Add,Update,Delete
.-------------------------------------------------------------------------------
HDPWAT00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      HDPWAT40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      HDPWAT10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      HDPWAT20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      HDPWAT30 IF EQUAL
.
          GOTO      HDPWAT93
.
.         ************ ADD FORMACTION ***********
.
HDPWAT10  PACK      KEY17,WTHDP001,WTHDP002,SP70
          CALL      RDWTPHI1
          COMPARE   ZERO,OVRCD
          GOTO      HDPWAT92 IF EQUAL
          CALL      HDPSAV00      * Moves input variables to file variables
          PACK      KEY17,WTHDP001,WTHDP002,SP70
          CALL      WRWTPHI1      * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      HDPWAT99
.
.         ************ UPDATE FORMACTION **********
.
HDPWAT20  PACK      KEY17,WTHDP001,WTHDP002,SP70
          CALL      RDWTPHI1
          BRANCH    OVRCD,HDPWAT91
.
          CALL      HDPSAV00      * Moves input variables to file variables
          PACK      KEY17,WTHDP001,WTHDP002,SP70
          CALL      UPWTPHI1      * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      HDPWAT99
.
.         ************ DELETE FORMACTION **********
.
HDPWAT30  PACK      KEY17,WTHDP001,WTHDP002,SP70
          CALL      RDWTPHI1
          BRANCH    OVRCD,HDPWAT91
          CALL      DEWTPHI1
.
          MOVE      ZERO,EXIT
          GOTO      HDPWAT99
.
.         ************ DISPLAY FORMACTION **********
.
HDPWAT40  PACK      KEY17,WTHDP001,WTHDP002,SP70
          CALL      RDWTPHI1
          IF        OVRCD=1
            CALL      HDPCLR00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Waiting List Procedure Code HDP Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,HDPWAT99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      HDPWAT99
.
HDPWAT90  MOVE      "Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HDPWAT99
.
HDPWAT91  MOVE      "Procedure Code HDP record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HDPWAT99
.
HDPWAT92  MOVE      "Procedure Code HDP record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HDPWAT99
.
HDPWAT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HDPWAT99
.
HDPWAT99  RETURN
+
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
HDPCLR00  MOVE      SP70,WTPHPROC
          MOVE      SP70,WTPHEFDT
          MOVE      ZERO,WTPHHDPV
          MOVE      ZERO,WTPHDRGC
          MOVE      SP70,WTPHRHAT
          MOVE      SP70,WTPHCHET
          MOVE      SP70,WTPHSPAR
.
HDPCLR99  RETURN
+
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
HDPSAV00  MOVE      WTHDP001,WTPHPROC
          MOVE      WTHDP002,WTPHEFDT
          MOVE      WTHDP003,WTPHHDPV
          MOVE      WTHDP004,WTPHDRGC
          MOVE      WTHDP005,WTPHRHAT
          MOVE      WTHDP006,WTPHCHET
.
HDPSAV99  RETURN
+
.------------------------------------------------------------
. Format Doctors Name (for Javascript)
.*** format the surname and given name by changing any ' to %60
.---------------------------------------------------------------
JAVDOC00  MOVE      DTITL,FMTTITLE
          CLEAR     DISPNAME
          MOVE      SP70,JAVFNAME
.
          MOVE      DSNAME,FMTSNAME        * surname
          SCAN      "'",FMTSNAME             * find any apostrophies
          GOTO      JAVDOC10 IF NOT EQUAL
.
          MOVE      FMTSNAME,ENDNAME
          REP       "' ",ENDNAME             * remove the apostrophy
.
          LENSET    FMTSNAME
          RESET     FMTSNAME                 * get all characters before '
          MOVE      FMTSNAME,STRTNAME
.
          REP       "'%",STRTNAME
          STRIP     STRTNAME
          PACK      FMTSNAME,STRTNAME,SIXTY,ENDNAME,SP70 * formatted surname
.
JAVDOC10  MOVE      SP70,STRTNAME
          MOVE      SP70,ENDNAME
.
          MOVE      DGNAME,FMTGNAME   * given name
          SCAN      "'",FMTGNAME        * find any apostrophies
          GOTO      JAVDOC20 IF NOT EQUAL
.
          MOVE      FMTGNAME,ENDNAME    * store all chars after apostrophy
          REP       "' ",ENDNAME        * remove the apostrophy
.
          LENSET    FMTGNAME
          RESET     FMTGNAME
          MOVE      FMTGNAME,STRTNAME   * store all chars before the '
.
          REP       "'%",STRTNAME
          STRIP     STRTNAME
          PACK      FMTGNAME,STRTNAME,SIXTY,ENDNAME,SP70  * formatted given name
.
JAVDOC20  CALL      DOCNM000            * Get java script format name
          MOVE      DOCFNAME,JAVFNAME
.
          MOVE      DGNAME,FMTGNAME     * Reset normal format name 
          MOVE      DSNAME,FMTSNAME
          CALL      DOCNM000
.
JAVDOC99  RETURN
.
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
+
.------------------------------------------------------------
          INC       IBAWEBCD
          INC       PMSTMDIO/INC
          INC       WATESTIO/INC
          INC       WATPHIIO/INC
          INC       WATPROIO/INC
          INC       WATVWLIO/INC
          INC       WATLETIO/INC
          INC       PATDO1IO/INC
          INC       WATPKIIO/INC
          INC       PATDOCTM
          INC       PATDOCCD
          INC       NAMSTRCD
          INC       WATPKIPR
          INC       CNVQTETM
.


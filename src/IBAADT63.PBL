.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   IBAADT63                                                  *
.* Desc      :   Contract ID Master Listing                                *
.***************************************************************************
.* Date      :   23/02/2011                                                *
.* Author    :   Davin Sloan                                               *
.* Mods.     :                                                             *
.*        V10.01.00 23/02/2011 Davin          CAR 233033                   *
.*                  Created program                                        *
.***************************************************************************
          INC       STD001FD
          INC       PMSCIDFD/INC  
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
ASATDATE  DIM       8
DIM3      DIM       3
DISPDAT1  DIM       10
DISPDAT2  DIM       10
DISPDAT3  DIM       10
ENDCONTR  DIM       6
FRMCONTR  DIM       6
RECCOUNT  FORM      10
.
PRGNAM    INIT      "Contract ID Master Listing"
PRGID     INIT      "IBAADT63"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * open files/display header
.
ML1000    CALL      OPTN0000            * select option
          BRANCH    EXIT,ML9999
.
          CALL      CNID0000            * Select Contract ID
          CALL      KDAT0000            * Keyin as at date
.
ML2400    CALL      CONTQST             * OK to continue ?
          BRANCH    CEXIT,ML3000,ML1000,ML9999
          BEEP
          GOTO      ML2400
.
ML3000    CALL      PRIN0000            * print all details
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening ";
.
          DISPLAY   *P60:24,"pmscidaf";
          OPEN      PMSCIDA1,"pmscidaf"
.
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          DISPLAY    *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF:
                     " = Contract ID Sequence";
.
OPTN1000  KEYIN      *P1:7,*EL,"Please Select : ",*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9000 IF EQUAL
.
          BRANCH     OPTION,OPTN9999
          BEEP
          GOTO       OPTN1000
.
OPTN9000  MOVE       TRUE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                 CNID0000              Called By : ML0000   *
.*                          Select Contract ID                                *
.******************************************************************************
CNID0000  MOVE      SP6,FRMCONTR
          KEYIN     *P1:9,*EL,"Starting Contract ID : ",*V2LON,FRMCONTR
          ENDSET    FRMCONTR
          APPEND    SP70,FRMCONTR
          RESET     FRMCONTR
.
          MATCH     SP70,FRMCONTR
          IF        @EQUAL
            DISPLAY   *P23:9,*EL,"Start";
          ENDIF
.
CNID2000  MOVE      SP6,ENDCONTR
          KEYIN     *P1:11,*EL,"Ending Contract ID : ",*V2LON,ENDCONTR
          ENDSET    ENDCONTR
          APPEND    SP70,ENDCONTR
          RESET     ENDCONTR
.
          MATCH     SP70,ENDCONTR
          IF        @EQUAL
            DISPLAY   *P23:11,*EL,"End";
            MOVE      "ZZZZZZ",ENDCONTR
          ENDIF
.
CNID9999  RETURN
+
.**********************************************************************
.*                             KDAT0000                               *
.*                    Keyin Effective As At Date                      *
.**********************************************************************
KDAT0000  DISPLAY   *P1:4,*EF,*P1:11,"Keyin As At Date : ";
.
          MOVE      SP70,ASATDATE
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          MOVE      TEN1,CVERT
          MOVE      TWENTY,CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9999
.
          PACK      ASATDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ASATDATE
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CPCDATE,DISPDAT1
.
          MOVE      ICENT,CC
          MOVE      IYEAR,YY
          MOVE      IMON,MM
          MOVE      IDAY,DD
.
          
.
KDAT9999  RETURN
+
.**********************************************************************
.*                             PRIN0000                               *
.*                Read file and print all details                     *
.**********************************************************************
PRIN0000  MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      "60",CLNO
          MOVE      ZERO,RECCOUNT
          CALL      HEAD0000
.
          DISPLAY   *P1:24,"Contract ID                 : ";
.
          PACK      KEY14,SP70
PRIN0600  CALL      RSPMCID1                      * position to start of file
PRIN1000  CALL      RKPMCID1
          BRANCH    OVRCD,PRIN8000
.
          DISPLAY   *P35:24,*EL,*V2LON,PMCICNTR;
.
          MATCH     SP70,FRMCONTR
          IF        !@EQUAL
            MATCH     FRMCONTR,PMCICNTR          * within selected CID range?
            GOTO      PRIN1000 IF LESS           * no
          ENDIF
.
          MATCH     SP70,ENDCONTR
          IF        !@EQUAL
            MATCH     PMCICNTR,ENDCONTR          * within selected CID range?
            GOTO      PRIN1000 IF LESS           * no
          ENDIF
.
          MATCH     SP70,ASATDATE                * so date filter
          GOTO      PRIN2000 IF EQUAL
.
          MATCH     SP70,PMCIFDAT
          IF        !@EQUAL
            MATCH     PMCIFDAT,ASATDATE          * within selected date range?
            GOTO      PRIN1000 IF LESS           * no.
          ENDIF
.
          MATCH     SP70,PMCITDAT
          IF        !@EQUAL
            MATCH     ASATDATE,PMCITDAT          * within selected date range?
            GOTO      PRIN1000 IF LESS           * no.
          ENDIF
.
PRIN2000  CALL      PREC0000                       * print a Record  
          GOTO      PRIN1000
.
PRIN8000  CALL      UND132
          PRINT     *N,"Total Records: ",RECCOUNT
          PRINT     *N,"*** END OF REPORT  ***";
          GOTO      PRIN9999
.
PRIN9999  RETURN
+
.**********************************************************************
.*                                HEAD0000                            *
.*                          Print page header                         *
.**********************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *40,"Contract from   : ";
.
          MATCH     SP6,FRMCONTR
          IF        !@EQUAL
            PRINT     *58,FRMCONTR;
          ELSE
            PRINT     *58,"START";
          ENDIF
.
          PRINT     *65," to : ";
.
          MATCH     "ZZZZZZ",ENDCONTR
          IF        !@EQUAL
            PRINT     *71,ENDCONTR,*N;
          ELSE
            PRINT     *71,"END",*N;
          ENDIF
.
          PRINT     *40,"Effective as at : ",DISPDAT1,*N;
.
          CALL      UND132
.
.             | Effective  | Effective  |                                                                                 |        |
. Contract ID | From Date  | To Date    | Description                                                                     | Active |
. 123456      | 1234567890 | 1234567890 | 12345678901234567890123456789012345678901234567890123456789012345678901234567890| 123    |
.23456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
.
          PRINT     *1,"|             ",*15,"| Effective  ",*28,"| Effective  ":
                    *41,"|             ",*123,"|        ",*132,"|"
          PRINT     *1,"| Contract ID ",*15,"| From Date  ",*28,"| To Date    ":
                    *41,"| Description ",*123,"| Active ",*132,"|"
.
          CALL      UND132
          ADD       FOUR,CLNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                                PREC0000                            *
.*                   Routine to print a PRECord                       *
.**********************************************************************
PREC0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.             | Effective  | Effective  |                                                                                 |        |
. Contract ID | From Date  | To Date    | Description                                                                     | Active |
. 123456      | 1234567890 | 1234567890 | 12345678901234567890123456789012345678901234567890123456789012345678901234567890| 123    |
.23456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
.
          UNPACK    PMCIFDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CPCDATE,DISPDAT2
.
          MOVE      SP70,DISPDAT3
          MATCH     SP70,PMCITDAT
          IF        !@EQUAL
            UNPACK    PMCITDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            REP       " 0",CPCDATE
            MOVE      CPCDATE,DISPDAT3
          ENDIF
.
          MOVE      "Yes",DIM3
          MATCH     "1",PMCIDELE
          IF        @EQUAL
            MOVE      "No ",DIM3
          ENDIF
.
          PRINT     *1,"| ",PMCICNTR," ",*15,"| ",DISPDAT2,*28,"| ",DISPDAT3:
                    *41,"| ",PMCIDESC,*123,"| ",DIM3,*132,"|"
.
          ADD       ONE,RECCOUNT
          ADD       ONE,CLNO
PREC9999  RETURN
+
.
          INC       STD001IO
          INC       PMSCIDIO/INC

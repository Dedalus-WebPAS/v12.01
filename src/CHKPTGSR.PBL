.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   CHKPTGSR                                                    *
.* Desc      :   Checks to make sure that every valid U/R has an equivalent  *
.*               patgsrnf record.                                            *
.*****************************************************************************
.* Date      :   09/07/2012                                                  *
.* Author    :   Steve Armstrong     CAR 268870                              *
.* Function  :   This program will loop through the patma1af file and for    *
.*               every valid record found, it will check that there is an    *
.*               equivalent patgsrnf record.                                 *
.* Mods      :                                                               *
.*        V12.00.01 30/05/2025  Jacob Jackson    TSK 0955096                 *
.*                  Alphanumeric changes
.*        V10.03.02 16/07/2012  Steve Armstrong  CAR 268870                  *
.*                  Mods to include a print and fix option                   *
.*        V10.03.01 12/07/2012  Steve Armstrong  CAR 268870                  *
.*                  Mods to do a direct read on patgsrnf instead of looping  *
.*                  the file for the U/R.                                    *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATGSRFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ERORDESC  DIM       44
RECCOUNT  FORM      8
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
PRGID     INIT      "CHKPTGSR"
PRGNAM    INIT      "Data Integrity Report for PATGSRFD"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000:      * proceed with report
                            MAIN1000       * proceed with report & fix
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run report                              *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Report Only":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Report & Fix"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run report
                            OPTN9000             * run report & fix
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through the patma1af file and check for an equivalent         *
.*        patgsrnf record for each valid U/R.                                *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
          MOVE      ZERO,RECCOUNT                * initialise record count
.
          CALL      HEAD0000
.
          MOVE      SP8,KEY8
          CALL      RDSMAST1                     * position at start of file
PROC0500  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished.
.
          IF        (RECCOUNT%1000) = 0 | RECCOUNT = 1
            DISPLAY   *P13:24,*EL,*V2LON,PURNO,SP1,PSNAME;
          ENDIF
.
          COMPARE   ZERO,PSTAT                   * active U/R ?
          GOTO      PROC0500 IF NOT EQUAL        * no - ignore record
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21                     * PMI extension record found ?
          IF        OVRCD = 1
            MOVE      "PMI Master Extension Record not on file",ERORDESC
            CALL      PERR0000
            GOTO      PROC0500                   * get next record
          ENDIF
.
          MOVE      PTMASNAM,GSRSNAM             * load shorter patgsrnf fields
          MOVE      PMPXFNAM,GSRGNAM
          MOVE      PMPXSNAM,PTGSGNM2
          PACK      KEY115,PURNO,SP7,ZERO,GSRSNAM,GSRGNAM,PTGSGNM2,PBDATE,PSEX
          CALL      RDPTGSR1
          IF        OVRCD = 1
            MOVE      "No matching patgsrnf record found",ERORDESC
            IF        COPTION = 2
              CALL      WGSR0000                 * create patgsrnf record
              ENDSET    ERORDESC
              APPEND    " - fixed",ERORDESC
              RESET     ERORDESC
            ENDIF
            CALL      PERR0000
          ENDIF
          GOTO      PROC0500                     * get next patma1af record
.
PROC9000  DISPLAY   *P1:24,*EL,"Data integrity check completed.  ";
          CALL      HITENTER
.
          CALL      LINE0000
          PRINT     *N,*1,"*** End of Report ***"
.
PROC9999  RETURN
+
.****************************************************************************
.*                            WGSR0000                 Called by: PROC0000  *
.*                  Create a patgsrnf record for the patient                *
.* Requires:  Valid read on patma1af & pmspx2af                             *
.****************************************************************************
.
WGSR0000  MOVE      PURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      PTMASNAM,GSRSNAM             * load shorter patgsrnf fields
          MOVE      PMPXFNAM,GSRGNAM
          MOVE      PMPXSNAM,PTGSGNM2
          CALL      SOUNDEX                      * set soundex key
          CALL      SOUNDX2
          MOVE      PBDATE,GSRDOB
          MOVE      PSEX,GSRSEX
          MOVE      SP70,GSRSPARE
          CALL      WRPTGSR1                     * write record
.
WGSR9999  RETURN
+
.****************************************************************************
.*                            PERR0000                 Called by: PROC0000  *
.*                  Print an error line to the report                       *
.* Requires:  Valid read on patlocaf record                                 *
.*            ERORDESC - error description                                  *
.****************************************************************************
.
PERR0000  COMPARE   CLNO,SIXTY                   * page full ?
          CALL      HEAD0000 IF LESS             * yes
.
          PRINT     *1,PIPE,*3,PURNO,*12,PIPE,*14,PSNAME,*55,PIPE:
                    *57,ERORDESC,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
PERR9999  RETURN
+
.****************************************************************************
.*                            HEAD0000                 Called by: PROC0000  *
.*                       Print page heading                                 *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"U/R No.",*12,PIPE,*14,"Surname",*55:
                    PIPE,*57,"Error Message",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SIX,CLNO                     * set page line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      Draw line across page                     PROC0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATSNDX
          INC       PATSNX2
.
          INC       PATGSRIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC

. *****************************************************************************
. * System    :   Patient/Accounting System Interface                         *
. * Program   :   IBAPIN02                                                    *
. * Function  :   Interface Listing                                           *
. *****************************************************************************
. * Date      :   02.01.92                                                    *
. * Mods      :                                                               *
. *****************************************************************************
. * V11.00.01 11/03/2020  J.Tan          TSK 0838262                          *
. *           Added Effective from and to date to MBS Item file               *
. * V10.12.01 15.01.2018  J.Tan   TSK 0848146                                 *
. *           Added CACCFEE=5 - Accumulation FI by Claim code                 *
. * V10.01.01 20/12/2010  Mike Laming   CAR 233046                            *
. *           Mods to Misc.Charges PATMCHFD - Key change, logic changes       *
. * V10.00.01 30/04/2010  J.Tan     CAR 220887                                *
. *           Mods checking for Active/Inactive of Misc.charge                *
. * V9.08.01  29/03/2007  J.Tan         CAR 136149                            *
. *           Mods to use default claim code from system parameter            *
. * V9.03.01  17/06/2003  Lina Vo       CAR 40497                             *
. *           Changed index and read of patmchgf to include effective         *
. *           date.                                                           *
. * V9.02.02  29/06/2002  Sandra Barcham                                      *
. *           Fix printing of BAS Code                                        *
. * V9.02.01  18/10/2001  J.Tan                                               *
. *           Mods to keyin free format accounts if not using fms             *
. * V9.02.00  13/10/2001  J.Tan                                               *
. *           Copied from 5.09.                                               *
. * V5.09.B02 09/01/2001  Charles Handaya                                     *
. *           BAS Statement Modification                                      *
. * V5.09.B01 15/12/2000  Sandra Barcham                                      *
. *           Changed file from fmspataf to patfmsaf                          *
. *****************************************************************************
. * V5.08.02  16/08/2000  Caleb Sharman                                *
. *           Changed Health Fund variables to be 8 chars              *
. * V5.08.01  01/05/2000 J.Tan                                         *
. *           Fixed using patient item definition                      *
. * V5.08.B03 20/03/2000 J.Tan                                         *
. *           GST Mods                                                 *
. * V5.08.B01 16/03/2000 J.Tan                                         *
. *           Mods for GST                                             *
. **********************************************************************
. * V5.01     02/12/1992  Steve Armstrong  Quote 7437  SRF 115599      *
. *           Mods for extra CACCFEE option                            *
. * V5.02     10.09.1993 Neeriem Dye                                   *
. *           Mods to allow compile in rel 6                           *
. *           srf 122999                                               *
. * V6.01.01  01/06/1994 J.Tan      SRF 128956 Quote 8005              *
. *           Mods for extra CACCFEE (3)                               *
. * V6.03.01  30/05/1996 J.Tan      SRF 615415                         *
. *           Mods FMPTCOD from fmspataf file from dim12 to dim13      *
. * V6.03.02  29/10/1996 J.Tan                                         *
. *           Fixed spelling error on accommodation                    *
. * V5.04.00  22/05/1997 J.Tan                                         *
. *           Converted from release 6.04                              *
. * V5.04.01  31/07/1997 J.Tan      SRF 620606                         *
. *           Fixed the fees invoiced breakdown                        *
. * V5.07.01  15/03/1999 J.Tan                                         *
. *           Mods for casemix funding                                 *
. * V5.07.02  10/05/1999 J.Tan                                         *
. *           Fixed item file                                          *
. * V5.07.03  16/12/1999 J.Tan                                         *
. *           Fixed printing journal code                              *
. **********************************************************************
. Standard Accounting Include
.----------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       IBABASFD/INC    * BAS Code
.
          INC       FMSAMAFD/INC    * Accounts
          INC       FMSDISFD/INC    * Dissection
          INC       FMSLMAFD/INC    * Ledger
          INC       FMSRESFD/INC    * Responsibility
.
          INC       PATFMSFD/INC    * Interface File
.
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PATMCHFD/INC
          INC       PATWR1FD/INC
.
          INC       PATITMFD/INC    
.
.LOCAL VARIABLE DEFINITION
.-------------------------
FILEOPEN  DIM       30
XLCNT     FORM      2
FMSAMA    INIT      "fmsamaaf"
FMSDIS    INIT      "fmsdisaf"
FMSLMA    INIT      "fmslmaaf"
FMSRES    INIT      "fmsresaf"
FMSPAT    INIT      "fmspataf"
ALAB      INIT      "            "                * labels for screen A
ALAB1     INIT      "Print Report"
.
OPTIONA   FORM      1        * option on screen A
.
IBCNUBAS  FORM      1
IBCNUGST  FORM      1
DBASDESC  DIM       30       * default BAS description
IBASDESC  DIM       30
IDIDESC   DIM       35
IREDESC   DIM       35
ILDDESC   DIM       35
ICADESC   DIM       35
IDADESC   DIM       35
IGSDESC   DIM       35
TDIDESC   DIM       35
TREDESC   DIM       35
TLDDESC   DIM       35
TCADESC   DIM       35
TDADESC   DIM       35
.
CODEA     INIT      "A "
CODEJ     INIT      "J "
CODECL    INIT      "CL"
CODEFI    INIT      "FI"
DAYCASE   INIT      "Daycase   "
SECTION   FORM      1
LASTSEC   FORM      1
SAVSECT   FORM      1
SP14      INIT      "              "
WARD      DIM       3
CURRDATE  DIM       8
CLAIM     DIM       3
ATYPE     DIM       3
ATDESC    DIM       20
PTYPE     DIM       1
KEY21A    DIM       21
KEY26A    DIM       26
.
PRGID     INIT      "IBAPIN02"
PRGNAM    INIT      "Patient Interface Master Listing"
VERSION   INIT      "V12.01.00"
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SELA0000      * display screen A and get "report by" option
          BRANCH    EXIT,ML9999   * user wants to exit screen
.
          CALL      PRNT0000      * print report
          GOTO      ML1000        * get another option
.
ML9999    CHAIN     PGM
.******************************************************************************
.* INIT - Open Files                                                          *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P30:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*85,IBCNUGST,*107,IBCNUBAS
          READ      CONTROLF,TEN;*192,CACCFEE
          READ      CONTROLF,TEN8;*162,CGLIND,CGLPATH
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,FIFTY9;*60,PTCNUFMS
          READ      CONTROLF,SEVENTY9;*68,PTCNMFEE
.
          MATCH     SP70,PTCNDCLM
          IF        @EQUAL
            MOVE      "0  ",PTCNDCLM          * Use zero claim code
          ENDIF
          DISPLAY   *P38:24,"ibabasaf";
          OPEN      IBABASA1,"ibabasaf"
.
          COMPARE   ONE,CGLIND
          GOTO      INIT1000 IF EQUAL
          DISPLAY   *P1:24,*B,*EL,"General Ledger Interface is Turned Off - "
          CALL      HITENTER
          GOTO      ML9999       * Abort Program
.
INIT1000  MOVE      TWENTY,XLCNT
INIT1100  RESET     CGLPATH,XLCNT
          CMATCH    SP1,CGLPATH
          GOTO      INIT1200 IF NOT EQUAL
          SUB       ONE,XLCNT
          COMPARE   ZERO,XLCNT
          GOTO      INIT1100 IF NOT EQUAL
          CLEAR     CGLPATH
          GOTO      INIT1300
.
INIT1200  LENSET    CGLPATH
          RESET     CGLPATH
.
INIT1300  DISPLAY   *P38:24,"patfmsaf";
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATFMSA1,"patfmsaf"
          TRAPCLR   IO
          BRANCH    OVRCD,INIT8000
.
          PACK      FILEOPEN,CGLPATH,FMSAMA
          DISPLAY   *P38:24,FILEOPEN;
          OPEN      FMSAMAA1,FILEOPEN
          OPEN      FMSAMAA2,FILEOPEN
          OPEN      FMSAMAA3,FILEOPEN
          OPEN      FMSAMAA4,FILEOPEN
.
          PACK      FILEOPEN,CGLPATH,FMSDIS
          DISPLAY   *P38:24,FILEOPEN;
          OPEN      FMSDISA1,FILEOPEN
.
          PACK      FILEOPEN,CGLPATH,FMSLMA
          DISPLAY   *P38:24,FILEOPEN;
          OPEN      FMSLMAA1,FILEOPEN
.
          PACK      FILEOPEN,CGLPATH,FMSRES
          DISPLAY   *P38:24,FILEOPEN;
          OPEN      FMSRESA1,FILEOPEN
.
          DISPLAY   *P38:24,*EL,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P38:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P38:24,"patmchgf";
          OPEN      PATMCHG1,"patmchgf"           * Misc Charges file
.
          DISPLAY   *P38:24,"patitemn";
          OPEN      PATITEM1,"patitemn"           * Misc Charges file
.
          DISPLAY   *P38:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          GOTO      INIT9999
.
INIT8000  DISPLAY   *P1:24,*EL,*B,"File Not Found, patfmsaf - ";
          CALL      HITENTER
          GOTO      ML9999       * Abort Program
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.**********************************************************************
. SELA -  display screen A and get "print by" option   Called By ML
.         Returns : EXIT       (0=continue, 1=quit)
.**********************************************************************
SELA0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = ",ALAB1:
                    *P6:7,"Select Option :";
.
SELA1000  KEYIN     *P22:7,"_",*EL,*P22:7,*V2LON,OPTIONA; * read option
          COMPARE   OPTIONA,ZERO
          GOTO      SELA9500 IF EQUAL            * user wants to exit ?
.
          PACK      ALAB,SP70                    * clear screen label
          LOAD      ALAB,OPTIONA,ALAB1           * get screen label
.
          MATCH     SP70,ALAB
          GOTO      SELA9000 IF NOT EQUAL        * legal option ?
.
          BEEP                                   * illegal option
          GOTO      SELA1000                     * get another option
.
SELA9000  MOVE      ZERO,EXIT                    * continue
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * quit
.
SELA9999  RETURN
.********************************************************************
. PRNT - print report                               Called by ML
.********************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :   ";
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      "999",CLNO                   * init last line number
.
          CALL      FILE0000                    * go to correct position in file
.
PRNT1000  CALL      NREC0000                     * get next record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          DISPLAY   *P61:24,*V2LON,PTFMSUB,PTFMCOD,*EL;
          COMPARE   "45",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS
.
          CALL      WRIT0000                     * print details
          GOTO      PRNT1000                     * get next record
.
PRNT9000  COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL          * print header if nothing printed
          CALL      UND80                       * print line
          PRINT     "****   End of Report   ****"
.
PRNT9999  RETURN
.**********************************************************************
.  FILE - Set file pointer                             Called By PRNT
.**********************************************************************
FILE0000  
.
.---- print by type 1 ----
.
FILE0100  PACK      KEY14,SP70
          CALL      RSPTFM1
          GOTO      FILE9999
.
FILE9999  RETURN
.**********************************************************************
.  NREC - Get next record                              Called By PRNT
.**********************************************************************
NREC0000  MOVE      SP10,WARD
          MOVE      SP10,CLAIM
          MOVE      SP10,ATYPE
          MOVE      SP10,PTYPE
.
.---- print by type 1 ----
.
NREC0100  CALL      RKPTFM1
          BRANCH    OVRCD,NREC9500
          MATCH     "1",PTFMSUB
          GOTO      NREC1000 IF EQUAL
          MATCH     "3",PTFMSUB
          GOTO      NREC2000 IF EQUAL
          MATCH     "5",PTFMSUB
          GOTO      NREC3000 IF EQUAL
          GOTO      NREC0100
.
NREC1000  MOVE      ONE,SECTION
          MATCH     SP70,PTFMCOD
          GOTO      NREC1100 IF NOT EQUAL
          MOVE      ONE,SAVSECT    * Default
.
          MOVE      SP70,DBASDESC
          PACK      KEY3,PTFMBASC,SP70
          CALL      RDIBBA1
          MOVE      IBBADESC,DBASDESC    * save default BAS Description
          MOVE      IBBADESC,IBASDESC
.
          GOTO      NREC5000
.
NREC1100  PACK      KEY3,PTFMBASC,SP70
          CALL      RDIBBA1
          IF        OVRCD=0
            MOVE      IBBADESC,IBASDESC
          ELSE
            MOVE      DBASDESC,IBASDESC       * display default BAS Description
          ENDIF
.
          CMATCH    ANSD,PTFMCOD            * check for daily casemix
          GOTO      NREC1190 IF EQUAL
.
          CMATCH    ANSC,PTFMCOD            * check for lumpsum casemix
          GOTO      NREC1180 IF EQUAL 
.
          CMATCH    ANSA,PTFMCOD
          GOTO      NREC1200 IF NOT EQUAL
          MOVE      TWO,SAVSECT    * Admission Type/Ward Claim
          BRANCH    CACCFEE,NREC1150:
                            NREC1151:
                            NREC1152:
                            NREC1152A:
                            NREC1140
.
          UNPACK    PTFMCOD,KEY1,KEY3
          PACK      KEY5,CODEA,KEY3
          CALL      RDCODE1
          GOTO      NREC5000
.
NREC1140  UNPACK    PTFMCOD,KEY1,KEY3
          PACK      KEY5,CODECL,KEY3
          CALL      RDCODE1
          GOTO      NREC5000
.
NREC1150  UNPACK    PTFMCOD,KEY1,WARD,CLAIM
          GOTO      NREC1153
.
NREC1151  UNPACK    PTFMCOD,KEY1,WARD,CLAIM,PTYPE
          GOTO      NREC1153
.
NREC1152  UNPACK    PTFMCOD,KEY1,CLAIM,ATYPE
          GOTO      NREC1153
.
NREC1152A UNPACK    PTFMCOD,KEY1,WARD,ATYPE,CLAIM
.
NREC1153  MOVE      SP70,WBDESC
          MATCH     SP70,WARD
          GOTO      NREC1155 IF NOT EQUAL
          MOVE      "All Wards",WBDESC
          GOTO      NREC1160
.
NREC1155  PACK      KEY6,WARD,SP3
          CALL      RDWARD1
.
NREC1160  MOVE      SP70,TDESC
          MATCH     SP70,CLAIM
          GOTO      NREC1165 IF NOT EQUAL
          MOVE      "All Claim Codes",TDESC
          GOTO      NREC1167
.
NREC1165  PACK      KEY5,CODECL,CLAIM
          CALL      RDCODE1
.
NREC1167  MOVE       SP20,ATDESC
          MATCH     SP70,ATYPE
          GOTO      NREC1169 IF NOT EQUAL
          MOVE      "All Admission Type",ATDESC
          GOTO      NREC5000
.
NREC1169  PACK      KEY5,CODEA,ATYPE
          CALL      RDCODE1
          MOVE      TDESC,ATDESC
          GOTO      NREC5000
.
NREC1180  UNPACK    PTFMCOD,KEY1,DIM1,ATYPE
          MOVE      "All Admission Codes",TDESC
          PACK      KEY5,CODEA,ATYPE
          CALL      RDCODE1
          MOVE      THREE,SAVSECT           * Lumpsum casemix
          GOTO      NREC5000
.
NREC1190  UNPACK    PTFMCOD,KEY1,WARD
          MOVE      "All Wards",WBDESC
          PACK      KEY6,WARD,SP3
          CALL      RDWARD1
          MOVE      FOUR,SAVSECT            * Daily casemix
          GOTO      NREC5000
.
NREC1200  CMATCH    ANSG,PTFMCOD
          GOTO      NREC5000 IF NOT EQUAL
          MOVE      FIVE,SAVSECT    * Govt Subsidy
          GOTO      NREC5000
.
NREC2000  MOVE      TWO,SECTION
          MATCH     SP70,PTFMCOD
          GOTO      NREC2100 IF NOT EQUAL
          MOVE      ONE,SAVSECT    * Default
.
          MOVE      SP70,DBASDESC
          PACK      KEY3,PTFMBASC,SP70
          CALL      RDIBBA1
          IF        OVRCD=0
            MOVE      IBBADESC,DBASDESC       * save default BAS Description
            MOVE      IBBADESC,IBASDESC
          ELSE
            MOVE      DBASDESC,IBASDESC       * display default BAS Description
          ENDIF
.
          GOTO      NREC5000
.
NREC2100  PACK      KEY3,PTFMBASC,SP70
          CALL      RDIBBA1
          IF        OVRCD=0
            MOVE      IBBADESC,IBASDESC
          ELSE
            MOVE      DBASDESC,IBASDESC       * display default BAS Description
          ENDIF
.
          MOVE      SP1,DIM1
          BRANCH    PTCNMFEE,NREC2300
.
          CMATCH    ANST,PTFMCOD            * Check for casemix
          IF        @EQUAL
            UNPACK    PTFMCOD,KEY1,DIM1,KEY3,KEY9   * ANSM,Episode,Grp,Itm cod
          ELSE
            UNPACK    PTFMCOD,KEY1,KEY3,KEY9        * ANSM,Grp,Itm code
          ENDIF
.
          MATCH     SP70,KEY3
          GOTO      NREC2200 IF EQUAL
          MOVE      TWO,SAVSECT
          PACK      KEY5,CODEFI,KEY3,SP70
          CALL      RDCODE1
          GOTO      NREC5000
NREC2200  MOVE      THREE,SAVSECT
          PACK      KEY17,KEY9,CURRDATE,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,NREC2250
          MOVE      IDESC,MDESC
          GOTO      NREC5000
.
NREC2250  PACK      KEY29,PTCNDCLM,SP6,SP3,KEY9,CURRDATE,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      NREC5000 IF EQUAL
.
NREC2260  MOVE      "Invalid Code",MDESC
          GOTO      NREC5000
.         
.         MISC.ITEM WITH CLAIM CODE
.
NREC2300  CMATCH    ANST,PTFMCOD            * Check for casemix
          IF        @EQUAL
            UNPACK    PTFMCOD,KEY1,DIM1,CLAIM,KEY3  * ANSM,Episode,Claim,group
          ELSE
            UNPACK    PTFMCOD,KEY1,CLAIM,KEY3       * ANSM,Claim,group
          ENDIF
          MATCH     SP70,CLAIM
          GOTO      NREC2400 IF EQUAL
          MOVE      TWO,SAVSECT
          PACK      KEY5,CODECL,CLAIM,SP70
          CALL      RDCODE1
          GOTO      NREC5000
.
NREC2400  MOVE      THREE,SAVSECT
          MATCH     SP70,KEY3
          GOTO      NREC2500 IF EQUAL
          PACK      KEY5,CODEFI,KEY3,SP70
          CALL      RDCODE1
          GOTO      NREC5000
.
NREC2500  MOVE      "Invalid item group",TDESC
          GOTO      NREC5000
.
NREC3000  MOVE      THREE,SECTION
          MATCH     SP70,PTFMCOD
          GOTO      NREC3100 IF NOT EQUAL
          MOVE      ONE,SAVSECT    * Default
          GOTO      NREC5000
.
NREC3100  MOVE      TWO,SAVSECT    * Default
          MOVE      PTFMCOD,KEY2
          MOVE      SP20,ATDESC
          PACK      KEY5,CODEJ,KEY2,SP70
          CALL      RDCODE1
          MOVE      TDESC,ATDESC
.
          COMPARE   THREE,CACCFEE
          GOTO      NREC5000 IF NOT EQUAL
.
          UNPACK    PTFMCOD,KEY2,DIM3,KEY3
          PACK      KEY5,CODECL,KEY3
          CALL      RDCODE1
          GOTO      NREC5000
.
NREC5000  MOVE      SP70,FMDSDESC
          MOVE      SP70,FMLADESC
          MOVE      SP70,FMRSDESC
          MOVE      SP70,FMAMDESC
          MOVE      PTFMIDI,KEY5
          CALL      RDFMDS1
          MOVE      FMDSDESC,IDIDESC
.
          MOVE      PTFMTDI,KEY5
          CALL      RDFMDS1
          MOVE      FMDSDESC,TDIDESC
.
          MOVE      PTFMIRE,KEY4
          CALL      RDFMRS1
          MOVE      FMRSDESC,IREDESC
.
          MOVE      PTFMTRE,KEY4
          CALL      RDFMRS1
          MOVE      FMRSDESC,TREDESC
.
          MOVE      PTFMILD,KEY2
          CALL      RDFMLA1
          MOVE      FMLADESC,ILDDESC
.
          MOVE      PTFMTLD,KEY2
          CALL      RDFMLA1
          MOVE      FMLADESC,TLDDESC
.
          PACK      KEY14,PTFMILD,PTFMIDA
          CALL      RDFMAM1
          MOVE      FMAMDESC,IDADESC
.
          PACK      KEY14,PTFMILD,PTFMICA
          CALL      RDFMAM1
          MOVE      FMAMDESC,ICADESC
.
          PACK      KEY14,PTFMILD,PTFMIGS
          CALL      RDFMAM1
          MOVE      FMAMDESC,IGSDESC
.
          PACK      KEY14,PTFMTLD,PTFMTDA
          CALL      RDFMAM1
          MOVE      FMAMDESC,TDADESC
.
          PACK      KEY14,PTFMTLD,PTFMTCA
          CALL      RDFMAM1
          MOVE      FMAMDESC,TCADESC
.
          COMPARE   SECTION,LASTSEC
          GOTO      NREC9000 IF EQUAL
          MOVE      SECTION,LASTSEC
          MOVE      "99",CLNO
          GOTO      NREC9000
.
NREC9000  MOVE      ZERO,OVRCD         * continue
          GOTO      NREC9999
.
NREC9500  MOVE      ONE,OVRCD          * quit
.
NREC9999  RETURN
.********************************************************************
. HEAD - print report                               Called by PRNT
.********************************************************************
HEAD0000  CALL      HEAD80                      * print heading
.
          CALL      UND80                       * print line
          BRANCH    SECTION,HEAD1000,HEAD2000,HEAD3000
HEAD1000  PRINT     "| Accommodation",*80,"|"
          GOTO      HEAD4000
HEAD2000  PRINT     "| Miscellaneous Charges",*80,"|"
          GOTO      HEAD4000
HEAD3000  PRINT     "| Journals",*80,"|"
HEAD4000
          ADD       ONE,CLNO
.
HEAD9999  RETURN
.********************************************************************
. WRIT - print data line                            Called by PRNT
.********************************************************************
WRIT0000  CALL      UND80                       * print line
          BRANCH    SECTION,WRIT1000,WRIT2000,WRIT3000
.
WRIT1000  BRANCH    SAVSECT,WRIT1100,WRIT1200,WRIT1300,WRIT1400:
                            WRIT1500
.
WRIT1100  PRINT     "| Default Accommodation Interface Details",*80,"|"
          ADD       ONE,CLNO
.
          GOTO      WRIT5000
.
WRIT1200  BRANCH    CACCFEE,WRIT1250:      
                            WRIT1250:
                            WRIT1250:
                            WRIT1250:
                            WRIT1210
          PRINT     "| Admission Type             : ",KEY3,SP1,TDESC,*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT1210  PRINT     "| Claim Type                 : ",KEY3,SP1,TDESC,*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT1250  COMPARE   THREE,CACCFEE
          GOTO      WRIT1260 IF EQUAL
          MATCH     SP70,WARD
          GOTO      WRIT1255 IF NOT EQUAL
          PRINT     "| Ward                       : All Wards",*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT1260
.
WRIT1255  PRINT     "| Ward                       : ",WARD,SP1,WBDESC,*80,"|"
          ADD       ONE,CLNO
          COMPARE   FOUR,CACCFEE
          GOTO      WRIT1271 IF EQUAL             * goto admtype
.
WRIT1260  MATCH     SP70,CLAIM
          GOTO      WRIT1265 IF NOT EQUAL
          PRINT     "| Claim Code                 : All Claim Codes",*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT1270
.
WRIT1265  PRINT     "| Claim Code                 : ",CLAIM,SP1,TDESC,*80,"|"
          ADD       ONE,CLNO
.
WRIT1270  COMPARE   TWO,CACCFEE
          IF        @EQUAL
            MOVE      PTYPE,FORM1
            MOVE      "Inpatient ",DIM10
            LOAD      DIM10,FORM1,DAYCASE
            PRINT     "| Patient Type               : ",PTYPE,SP1,DIM10,*80,"|"
            GOTO      WRIT5000
          ENDIF
          COMPARE   THREE,CACCFEE
          GOTO      WRIT5000 IF NOT EQUAL
.
WRIT1271  MATCH     SP70,ATYPE
          GOTO      WRIT1272 IF NOT EQUAL
          PRINT     "| Admission Type             : All Admission Type",*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT1280
.
WRIT1272  PRINT     "| Admission Type            : ",ATYPE,SP1,ATDESC,*80,"|"
          ADD       ONE,CLNO
.
WRIT1280  COMPARE   FOUR,CACCFEE
          GOTO      WRIT5000 IF NOT EQUAL
          GOTO      WRIT1260
.
WRIT1300  PRINT     "| Casemix Lumpsum Payment ",*80,"|"
          MATCH     "1",DIM1
          IF        @EQUAL
            PRINT     "| Episode Type               : ",1," - Sameday":
                      *80,"|"
          ELSE
            PRINT     "| Episode Type               : ",0," - MSO":
                      *80,"|"
          ENDIF
          ADD       TWO,CLNO
.
          MATCH     SP70,ATYPE
          IF        @EQUAL
            PRINT     "| Admission Type             : All Admission Codes":
                      *80,"|"
          ELSE
            PRINT     "| Admission Type             : ",ATYPE,SP1,TDESC,*80,"|"
          ENDIF
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT1400  MATCH     SP70,WARD
          IF        @EQUAL
            PRINT     "| Casemix Daily Charge                  ",*80,"|":
                      *N,"| Ward                       : All Wards",*80,"|"
          ELSE
            PRINT     "| Casemix Daily Charge                  ",*80,"|":
                      *N,"| Ward                       : ",WARD,SP1,WBDESC:
                      *80,"|"
          ENDIF
          ADD       TWO,CLNO
          GOTO      WRIT5000
.
WRIT1500  PRINT     "| Government Subsidy Interface Details",*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT2000  BRANCH    SAVSECT,WRIT2100,WRIT2200,WRIT2300
WRIT2100  PRINT     "| Default Misc. Items Interface Details",*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT2200  MATCH     "3",DIM1
          IF        @EQUAL
            PRINT     "| Episode Type               : ",THREE," - Sameday":
                      *80,"|"
            ADD       ONE,CLNO
          ELSE
            MATCH     "5",DIM1
            IF        @EQUAL
              PRINT     "| Episode Type               : ",FIVE," - MSO":
                        *80,"|"
              ADD       ONE,CLNO
            ENDIF
          ENDIF
.
          COMPARE   THREE,SAVSECT
          GOTO      WRIT2300 IF EQUAL
.
          IF        PTCNMFEE = 0
            PRINT     "| Group Code                 : ",KEY3,SP1,TDESC,*80,"|"
          ELSE
            PRINT     "| Claim Code                 : ",KEY3,SP1,TDESC,*80,"|"
          ENDIF
.
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT2300  IF        PTCNMFEE = 0
            PRINT     "| Item Code                  : ",KEY9,SP1,MDESC,*80,"|"
          ELSE
            PRINT     "| Group Code                 : ",KEY9,SP1,TDESC,*80,"|"
          ENDIF
.
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT3000  BRANCH    SAVSECT,WRIT3100,WRIT3200
WRIT3100  PRINT     "| Default Journal Interface Details",*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT3200  UNPACK    PTFMCOD,KEY2,DIM3,KEY3
          COMPARE   THREE,CACCFEE
          GOTO      WRIT3210 IF NOT EQUAL
          PRINT     "| Journal Code               : ",KEY2,SP1,ATDESC,*80,"|":
                    "! Claim Code                 : ",KEY3,SP1,TDESC,*80,"!"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT3210  PRINT     "| Journal Code               : ",KEY2,SP1,ATDESC,*80,"|"
          ADD       ONE,CLNO
          GOTO      WRIT5000
.
WRIT5000  PRINT     "|",*80,"|":
                    *N,"| Invoiced       Dissection  : ":
                    *32,PTFMIDI,*45,IDIDESC,*80,"|":
                    *N,"|                Resp.       : ":
                    *32,PTFMIRE,*45,IREDESC,*80,"|":
                    *N,"|                Ledger      : ":
                    *32,PTFMILD,*45,ILDDESC,*80,"|";
.
          IF        IBCNUGST=2 & SECTION<>3
            PRINT     *N,"|                Income Acct : ":
                      *32,PTFMICA,*45,ICADESC,*80,"|";
.
            IF      PTCNUFMS=1
              PRINT   *N,"|      Debtors Control Acct  : ":
                      *32,PTFMIDA,*45,IDADESC,*80,"|":
                      *N,"|          GST Control Acct  : ":
                      *32,PTFMIGS,*45,IGSDESC,*80,"|";
            ENDIF
          ELSE
            PRINT     *N,"|                Credit Acct : ":
                      *32,PTFMICA,*45,ICADESC,*80,"|":
                      *N,"|                Debit Acct  : ":
                      *32,PTFMIDA,*45,IDADESC,*80,"|";
          ENDIF
          IF        IBCNUBAS=1 & SECTION <>3
            PRINT     *N,"|                BAS Code    : ":
                      *32,PTFMBASC,*45,IBASDESC,*80,"|";
            ADD       ONE,CLNO
          ENDIF
.
          COMPARE   THREE,SECTION
          GOTO      WRIT6000 IF NOT EQUAL      * Journal has no To be invoiced
.
          PRINT     " "
          GOTO      WRIT9000
.
WRIT6000  PRINT     *N,*1,"| To be Invoiced Dissection  : ":
                    *32,PTFMTDI,*45,TDIDESC,*80,"|":
                    *N,"|                Resp.       : ":
                    *32,PTFMTRE,*45,TREDESC,*80,"|":
                    *N,"|                Ledger      : ":
                    *32,PTFMTLD,*45,TLDDESC,*80,"|":
                    *N,"|                Credit Acct : ":
                    *32,PTFMTCA,*45,TCADESC,*80,"|":
                    *N,"|                Debit Acct  : ":
                    *32,PTFMTDA,*45,TDADESC,*80,"|"
.
WRIT9000  ADD       TEN,CLNO
WRIT9999  RETURN
+

.******************************************************************************
.  INCLUDES FOR I/O'S
.******************************************************************************
.---------------------------------
. Standard Accounting Code include
.---------------------------------
          INC       IBABASIO/INC    * BAS Code
.
          INC       FMSAMAIO/INC    * Accounts
          INC       FMSDISIO/INC    * Dissection
          INC       FMSLMAIO/INC    * Ledger
          INC       FMSRESIO/INC    * Responsibility
.
          INC       PATFMSIO/INC    * Interface File
.
          INC       PATCODIO/INC
          INC       PATMCHIO/INC
          INC       PATWR1IO/INC
          INC       PATITMIO/INC   
.
          INC       PATITMRD
          INC       FMSSTDCD           * FMS Acc. standard routines
          INC       PATMCHRD           * Miscellaneous Charges read routine

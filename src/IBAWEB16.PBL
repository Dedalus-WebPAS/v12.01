.******************************************************************************
.* System   : Patient Management System                                       *
.* Program  : IBAWEB16                                                        *
.* Desc     : Generating DB4 Form for WebServices Bulk Billing (Task 0909175) *
.******************************************************************************
.* Date     :  22/07/2021                                                     *
.* Author   :  Peter Vela                                                     *
.* Mods     :                                                                 *
.******************************************************************************
.* V11.04.01 11/04/2024  J.Tan         TSK 0941662                            *
.*           Mod PRDTGSTM - GST of Total amount(Item amount x Quantity)       *
.* V11.02.01 09/02/2022  Thanh T          TSK 0905641                         *
.*           Recompiled as OUTHEDFD changes                                   *
.******************************************************************************
.*             V11.01.04 12/01/2022 Peter Vela Task 0906538                   *
.*             Added Hosp Ind validation for Facility Id                      *
.*             V11.01.03 06/01/2022 Peter Vela Task 0906538                   *
.*             Added Date of service and facility ID                          *
.*             V11.01.02 05/01/2022 Peter Vela Task 0906538                   *
.*             NOI formatting changes                                         *
.*             V11.01.01 22/07/2021 Peter Vela Task 0909175                   *
.*             Created program                                                *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCONFD/INC
.
.         INC       PATLCNFD/INC           * Eclipse Location file
          INC       WEBB2BFD/INC           * WebServices Device file
          INC       PATHSPFD/INC           * Hospital file
          INC       PRIHDBFD/INC           * Private practice Header file
          INC       PRIHREFD/INC           * Private practice referral file
          INC       PRIDTRFD/INC           * Private practice transaction file
          INC       PRIPRAFD/INC           * Medical practice file
          INC       PRIINVFD/INC           * PP Invoice file
          INC       OUTDTRFD/INC           * O/P Debtor transactioc file
          INC       PATINVFD/INC           * Outpatient Invoice file
          INC       PMSHCPFD/INC           * HCP file 
          INC       PATDO1FD/INC           * Doctor file
          INC       RCPDTEFD/INC           * Receipt details file
          INC       OUTSITFD/INC           * Outpatient site file
          INC       OUTBOAFD/INC           * Booking file
          INC       OUTBB1FD/INC           * Booking file
          INC       OUTSESFD/INC           * Session file
          INC       OUTHEDFD/INC           * O/P template header file
.
          INC       PATCODFD/INC           * patient codes file
          INC       PATMA1FD/INC           * patient master file
          INC       PATMI1FD/INC           * admissions file
          INC       PATVISFD/INC           * visit file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC           * visit extention file
.
          INC       IBACONFD/INC
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
DIM3      DIM       3
DISPOPTN  DIM       20
DOPT1     INIT      "Outpatient         "
DOPT2     INIT      "Private Practice   "
DIM2A     DIM       2
DIM10     DIM       10
DIM35     DIM       35
UNIQNUMB  FORM      8
FORM12    FORM      12
FORM12P2  FORM      12.2
.
INVOICEN  FORM      8
PTFRMTNM  DIM       40
TOTFEE    FORM      12.2
CFILEPRE  DIM       3                       * site prefix
PRITFLAG  FORM      1
.
PRGID     INIT      "IBAADT16"
PRGNAM    INIT      "Generating DB4 Form"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    COPTION,ML2000,ML2000
          GOTO      ML9999                 * Exit selected
.
ML2000    CALL      KINV0000               * input Invoice number
          BRANCH    EXIT,ML9999 
.
          CALL      RPAT0000               * Read patient data
          BRANCH    EXIT,ML9999
.
          BRANCH    COPTION,ML2100,ML2100
.
ML2100    CALL      PDBF0000               * Print DB4 Form
          GOTO      ML9999
.
ML9999    CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
.         DISPLAY   *P64:24,"patlcnaf";
.         OPEN      PATLCNA1,"patlcnaf"
.
          DISPLAY   *P64:24,"webb2baf";
          OPEN      WEBB2BA2,"webb2baf"
.
          DISPLAY   *P64:24,"prihreff";
          OPEN      PRIHREF1,"prihreff"
.
          DISPLAY   *P64:24,"pridtraf";
          OPEN      PRIDTRA4,"pridtraf"
.
          DISPLAY   *P64:24,"pripracf";
          OPEN      PRIPRAC1,"pripracf"
.
          DISPLAY   *P64:24,"prihdbtf";
          OPEN      PRIHDBT2,"prihdbtf"
.
          DISPLAY   *P64:24,"outdtrof";
          OPEN      OUTDTRO4,"outdtrof"
.
          DISPLAY   *P64:24,"priinvof";
          OPEN      PRIINVO1,"priinvof"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"rcpdteaf";
          OPEN      RCPDTEA3,"rcpdteaf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*79,CAPPRVNO
          READ      CONTROLF,TEN3;*1,CHADD1,CHADD2,CHPOST
          READ      CONTROLF,HUND14;*145,PTCNCLID
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.****************************************************************************
OPTN0000  HLINE     *G37,2,57,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Outpatient":
                    *P1:6,*V2LON,TWO,*HOFF," = Private Practice"
.
OPTN1000  KEYIN     *P1:8,"Select Option : ",*EL:
                    *P17:8,*DV,UNDLN2:
                    *P17:8,*V2LON,COPTION:
                    *P17:8,*DV,COPTION
.
.         Validate selection
.
          BRANCH    COPTION,OPTN8000,OPTN8000
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9999 IF EQUAL              * exit      
.
          BEEP
          GOTO      OPTN1000
.
OPTN8000  LOAD      DISPOPTN,COPTION,DOPT1,DOPT2
          DISPLAY   *P56:2,*V2LON,"- ",DISPOPTN
.
OPTN9999  RETURN
+
.**********************************************************************
.*                           KINV0000                                 *
.*                      Input Invoice number                          *
.**********************************************************************
KINV0000  
          DISPLAY   *P1:14,*EF,"Invoice Number: ";
.
          KEYIN     *P18:14,*EL,*DV,UNDLN8:
                    *P18:14,*V2LON,INVOICEN
.
          COMPARE   ZERO,INVOICEN              * was anything input ?
          GOTO      KINV9000 IF EQUAL        * no.
.
          DISPLAY   *P18:14,*V2LON,INVOICEN
.
          MOVE      INVOICEN,KEY8
          BRANCH    COPTION,KINV1000,KINV2000
          GOTO      KINV9999
.
KINV1000  CALL      RDINV1
          BRANCH    OVRCD,KINV9100
.
          COMPARE   TWO,PINVSYS
          GOTO      KINV9100 IF NOT EQUAL     * Not an Outpatient invoice
.
          GOTO      KINV8000
.
KINV2000  CALL      RDPRIN1
          BRANCH    OVRCD,KINV9100
.
KINV8000  MOVE      ZERO,EXIT
          GOTO      KINV9999
.
KINV9000  MOVE      ONE,EXIT
          GOTO      KINV9999
.
KINV9100  DISPLAY   *P1:24,*EL,*B,"Invalid Invoice number. ";
          CALL      HITENTER
          GOTO      KINV0000
.
KINV9999  RETURN
+
.**********************************************************************
.         Read patient data
.**********************************************************************
RPAT0000  
          BRANCH    COPTION,RPAT4000         * Outpatient
.
.         Private Practice
.
          PACK      KEY6,PRINPRAC
          CALL      RDPRPR1
          BRANCH    OVRCD,RPAT9000           * Invalid Medical practice
.
          MOVE      PRINUNIQ,KEY8
          CALL      RDPRHD2
          BRANCH    OVRCD,RPAT9000
.
.         Get Patient indicator from pridtraf file, so we can read prihreaf file
.
          PACK      KEY22,PRINNUMB,SP70
          CALL      RSPRDT4
          CALL      RKPRDT4
          BRANCH    OVRCD,RPAT9000
.
          MATCH     PRINNUMB,PRDTINVN          * Same invoice number ?
          GOTO      RPAT9000 IF NOT EQUAL
.
          PACK      KEY27,PRDTUNIQ,PRDTPRAC,PRDTDOCT,PRDTCODE,SP70
          CALL      RDPRHR1
          BRANCH    OVRCD,RPAT9000
.
          MOVE      PRHDDEBT,KEY8            * U/R number
          GOTO      RPAT6000
.
.         Outpatient
.
RPAT4000  MOVE      PINVADM,KEY8
          CALL      RDVISA1                  * valid visit record ?
          BRANCH    OVRCD,RPAT9000           * no.
.
          OPEN      OUTSITA1,"outsitaf"
          MOVE      "out",CFILEPRE
          MOVE      PVISITE,KEY6             * For this site ...
          CALL      RDSITA1                  * Get file prefix
          BRANCH    OVRCD,RPAT9000
.
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE
          ENDIF
.
          PACK      CFNAME,CFILEPRE,FILBB1A2  * Set up physical filename
          CALL      OPOTBB12
          PACK      KEY16,PVIDATE,PVIBILL    * Key is date & outpatient no.
          CALL      RDBOKB2                  * Read record
          BRANCH    OVRCD,RPAT9000
.
          PACK      CFNAME,CFILEPRE,FILSESA1  * Get the name of the SESA2 file
          CLOSE     OUTSESA1
          OPEN      OUTSESA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILHEDA1  * Get the name of the HEDA1 file
          CLOSE     OUTHEDA1
          OPEN      OUTHEDA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILBOKA1 * Open booking file
          CLOSE     OUTBOKA6
          OPEN      OUTBOKA6,CFNAME
.
          PACK      KEY8,DOBAOUTN,SP70
          PACK      KEY36,KEY8,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,RPAT9000
.
          MATCH     KEY8,DOBAOUTN
          GOTO      RPAT9000 IF NOT EQUAL
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,RPAT9000
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,RPAT9000
          MOVE      PVIURNO,KEY8               * key to master file
.
RPAT6000  CALL      RDMAST1                  * get the PMI details
          BRANCH    OVRCD,RPAT9000
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,RPAT9000
.
RPAT8000  MOVE      ZERO,EXIT                * we have a valid patient
          GOTO      RPAT9999
.
RPAT9000  MOVE      ONE,EXIT                 * nothing input
RPAT9999  RETURN
+
.**********************************************************************
.         Print DB4 Form
.**********************************************************************
PHED0000  CALL      TOPH0000
.
          PRINT     *1,"Location ID:":
                    *26,PTCNCLID;
.
          IF        COPTION=1
            UNPACK    PTINCTIM,KEY2,ANS,DIM2,KEY1,DIM2A
            PACK      KEY22,PTCNCLID,PTINCDAT,KEY2,DIM2,DIM2A,SP70
          ELSE
            UNPACK    PRINCTIM,KEY2,ANS,DIM2,KEY1,DIM2A
            PACK      KEY22,PTCNCLID,PRINCDAT,KEY2,DIM2,DIM2A,SP70
          ENDIF
.
          PRINT     *68,"Reference:":
                    *90,KEY22
          ADD       ONE,CLNO
.
          CALL      GLOC0000              * Get Servicing location details
.
          PRINT     *1,"Servicing Location:":
                    *68,PTHSNAME
          ADD       ONE,CLNO
.
          PRINT     *68,PTHSADD1
          ADD       ONE,CLNO
.
          PRINT     *68,PTHSADD2
          ADD       ONE,CLNO
.
          MATCH     SP70,PTHSADD3
          IF        !@EQUAL
            PRINT     *68,PTHSADD3
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *68,KEY35            * address line 4 and postcode
          ADD       ONE,CLNO
.
          PRINT     *1,*V2LON,"Patient Details",*HOFF
          ADD       ONE,CLNO
.
          PRINT     *1,"Medicare No:":
                    *26,PMEDI:
                    *N,*1,"Ref No: ":
                    *26,PTMXMCCD
          ADD       TWO,CLNO
.
          MOVE      PTITL,PACTITLE              * format pt. name
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PTFRMTNM
.
          PRINT     *1,"First Name,Initial, ":
                    *26,PTFRMTNM:
                    *N,*1,"Telephone: ":
                    *26,PTELEP:
                    *N,*1,"Surname:"
          ADD       THREE,CLNO
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY      * DOB
          CALL      PACDATE
          PRINT     *1,"Date of Birth:":
                    *26,CPCDATE;
.
          PRINT     *1,"Residential Address: ":
                    *13,PADD1
          ADD       ONE,CLNO
.
          PRINT     *13,PADD2
          ADD       ONE,CLNO
.
          MATCH     SP70,PADD4
          IF        @EQUAL
            PACK      KEY35,PSUBRB,SP1,PPOST
          ELSE
            PRINT     *13,PSUBRB
            ADD       ONE,CLNO
            STRIP     PADD4
            PACK      KEY35,PADD4,SP1,PPOST
          ENDIF
          PRINT     *13,KEY35
          ADD       ONE,CLNO
.
          PRINT     *N,*N,*1,*V2LON,"This Bulk Bill claim has been: ":
                    "STORED FOR LATER TRANSMISSION",*HOFF
          PRINT     *N
          ADD       FOUR,CLNO
.
PHED9999  RETURN
+
.**********************************************************************
.         Print Lodgement Advice for Private practice/Ed
.**********************************************************************
PDBF0000  MOVE      ZERO,PRITFLAG
          MOVE      ZERO,CLNO
          CALL      PHED0000               * print heading
.
          IF        COPTION=1
            PACK      KEY10,OTBBADCS,SP70
            CALL      GHCP0000              * Service doctor
            PACK      DIM35,KEY35,SP70
            PACK      DIM10,PMHCPRV1,SP70  * provider no for service doctor
.
.           If Payee provider and Service doctor are same, payee provider blank
.
            MATCH     SP70,OTHEDEFD
            IF        !@EQUAL
              MATCH     OTBBADCS,OTHEDEFD
              IF        !@EQUAL
                PACK      KEY10,OTHEDEFD,SP70
                CALL      GHCP0000              * Payee provider
                GOTO      PDBF1000
              ENDIF
            ENDIF
            UNPACK    SP70,KEY35,PMHCPRV1
          ELSE
            PACK      KEY10,PRHRDOCT,SP70
            CALL      GHCP0000              * Service doctor
            PACK      DIM35,KEY35,SP70
            PACK      DIM10,PMHCPRV1,SP70   * provider no for service doctor
.
.           If primary doctor and service doctor are same, payee provider blank
.
            MATCH     PRHRDOCT,PRPRPDOC
            IF        !@EQUAL
              PACK      KEY10,PRPRPDOC,SP70
              CALL      GHCP0000              * Medical practice Primary doctor
            ELSE
              UNPACK    SP70,KEY35,PMHCPRV1
            ENDIF
          ENDIF
.
PDBF1000  
          PRINT     *1,"Name of practitioner who rendered":
                    *38,DIM35:
                    *73,"Payee Provider:":
                    *92,KEY35:
                    *N,*1,"the services:"
          PRINT     *N
          ADD       THREE,CLNO
.
          PRINT     *1,"Provider No:":
                    *38,DIM10:
                    *73,"Payee Provider No:":
                    *92,PMHCPRV1
          PRINT     *N
          ADD       TWO,CLNO
.
          IF        COPTION=1
            PACK      KEY10,OTBBRFGP,SP70
            CALL      GHCP0000                           * Referring doctor
            UNPACK    OTBBRDAT,CCENT,CYEAR,CMON,CDAY      * Referring date
            CALL      PACDATE
          ELSE
            PACK      KEY10,PRHRREFD,SP70
            CALL      GHCP0000                           * Referring doctor
            UNPACK    PRHRRDAT,CCENT,CYEAR,CMON,CDAY      * Referring date
            CALL      PACDATE
          ENDIF
.
.         IF        IBCNMHOS = 1
.           MOVE      PTHSAPPR,KEY8
.         ELSE
.           MOVE      CAPPRVNO,KEY8
.         ENDIF
.
          MOVE      INVOICEN,KEY8
          PACK      KEY22,KEY8,SP70
          CALL      RSPRDT4
PDBF1050  CALL      RKPRDT4
          BRANCH    OVRCD,PDBF1100
.
          MATCH     KEY8,PRDTINVN          * Same invoice number ?
          GOTO      PDBF1100 IF NOT EQUAL
.
          COMPARE   ONE,PRDTRTYP
          GOTO      PDBF1050 IF NOT EQUAL
.
          MATCH     "1",PRDTHOSI
          GOTO      PDBF1050 IF NOT EQUAL
.
          IF        IBCNMHOS = 1
            MOVE      PTHSAPPR,KEY8
          ELSE
            MOVE      CAPPRVNO,KEY8
          ENDIF
.
          PRINT     *1,"Requesting/Referring practitioner's":
                    *38,KEY35:
                    *73,"ACRF:":
                    *92,INVOICEN:
                    *N,*1,"name:":
                    *73,"Facility Id: ":
                    *92,KEY8
.
          PRINT     *N
          ADD       THREE,CLNO
.
          GOTO      PDBF1500
.
PDBF1100  PRINT     *1,"Requesting/Referring practitioner's":
                    *38,KEY35:
                    *73,"ACRF:":
                    *92,INVOICEN:
                    *N,*1,"name:"
.
          PRINT     *N
          ADD       THREE,CLNO
.
PDBF1500  PRINT     *1,"Provider No:":
                    *38,PMHCPRV1
          ADD       ONE,CLNO
.
          PRINT     *1,"Date of Referral:":
                    *38,CPCDATE
          ADD       ONE,CLNO
.
          MOVE      SP70,KEY6
          MOVE      SP70,KEY15
          MOVE      "RF",KEY2
          IF        COPTION=1
            PACK      KEY5,KEY2,OTBBBKPR         * period of referral
          ELSE
            PACK      KEY5,KEY2,PRHRRFPD         * period of referral
          ENDIF
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     "N",TCINDC1
            IF        @EQUAL
              IF        COPTION=1
                PACK      KEY6,OTBBBKPM,SP70      * Referral period Months
              ELSE
                PACK      KEY6,PRHRRPER,SP70
              ENDIF
            ELSE
              MATCH     "I",TCINDC1
              IF        @EQUAL
                MOVE      "Indefinite     ",KEY15
                GOTO      PDBF2000
              ELSE
                PACK      KEY6,PTCDASC1,SP70
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,KEY6
          IF        !@EQUAL
            SQUEEZE   KEY6
            CLEAR     KEY15
            APPEND    KEY6,KEY15
            APPEND    " months",KEY15
            APPEND    SP70,KEY15
            RESET     KEY15
          ENDIF
.
PDBF2000  
          PRINT     *1,"Period of Referral:":
                    *38,KEY15
          ADD       ONE,CLNO
.
          CALL      ROVR0000             * Get Referral Override
.
          PRINT     *N,*N
          ADD       TWO,CLNO
.
          CALL      PRIT0000             * Print items
          CALL      TAIL0000             * Print tail
.
          GOTO      PDBF9999
.
PDBF9999  RETURN
+
.**********************************************************************
.         Print heading for list of Items
.**********************************************************************
ITMH0000  PRINT     *1,"Date of Service",*25,"Item No":
                       *40,"Description of Service":
                       *92,"Benefit Assigned"
          ADD       ONE,CLNO
ITMH9999  RETURN
+
.**********************************************************************
.         Get Referral Override
.**********************************************************************
ROVR0000  MOVE      SP70,TDESC
          IF        COPTION=1
            MOVE      OTBBBKOV,KEY3          * Outpatient Ref.Override
            MOVE      OTBBRFGP,KEY10         * Ref.Doctor
          ELSE
            MOVE      PRHRROVR,KEY3          * PP Ref.Override
            MOVE      PRHRREFD,KEY10         * Ref.Doctor
          ENDIF
.
          MATCH     SP70,KEY3
          GOTO      ROVR9999 IF EQUAL        * blank Referral override
.
          MOVE      "ro",KEY2
          PACK      KEY5,KEY2,KEY3           * Referral override
          CALL      RDCODE1
          BRANCH    OVRCD,ROVR9999
.
          MATCH     "N",THCSCOD
          GOTO      ROVR8000 IF EQUAL        * Not required
.
          MATCH     SP70,KEY10
          GOTO      ROVR9999 IF NOT EQUAL    * if Ref.doctor exists,No Overwrite
.
          MATCH     "L",THCSCOD
          GOTO      ROVR8100 IF EQUAL        * Lost Referral
          MATCH     "E",THCSCOD
          GOTO      ROVR8200 IF EQUAL        * Emergency Referral
          MATCH     "H",THCSCOD
          GOTO      ROVR8300 IF EQUAL        * In-Hospital Referral
.
          GOTO      ROVR9999
.
ROVR8000  MOVE      "Not Required",TDESC
          GOTO      ROVR9000
.
ROVR8100  MOVE      "Lost Referral",TDESC
          GOTO      ROVR9000
.
ROVR8200  MOVE      "Emergency Referral",TDESC
          GOTO      ROVR9000
.
ROVR8300  MOVE      "In-Hospital Referral",TDESC
          GOTO      ROVR9000
.
ROVR9000  PRINT     *1,"                 Referral Override:":
                    *38,TDESC
          ADD       ONE,CLNO
ROVR9999  RETURN
+
.**********************************************************************
.         Print items
.**********************************************************************
PRLN0000  PRINT     "________________________________________":
                    "________________________________________":
                    "________________________________________":
                    "____________"
          ADD       ONE,CLNO
PRLN9999  RETURN
+
.**********************************************************************
.         Print items
.**********************************************************************
PRIT0000  MOVE      ONE,PRITFLAG
          MOVE      ZERO,TOTFEE                * total fee
.
.         IF        CLNO<45
            CALL      ITMH0000             * print heading for list of items
.         ELSE
.           CALL      TOPH0000
.         ENDIF
.
          BRANCH    COPTION,PRIT2000           * Outpatient
.
.         Private practice items
.
          MOVE      INVOICEN,KEY8
          PACK      KEY22,KEY8,SP70
          CALL      RSPRDT4
PRIT1000  CALL      RKPRDT4
          BRANCH    OVRCD,PRIT9000
.
          MATCH     KEY8,PRDTINVN          * Same invoice number ?
          GOTO      PRIT9000 IF NOT EQUAL
.
          COMPARE   ONE,PRDTRTYP
          GOTO      PRIT1000 IF NOT EQUAL      * Not an Item record type
.
          UNPACK    PRDTSDAT,CCENT,CYEAR,CMON,CDAY      * DOB
          CALL      PACDATE
.
          MOVE      SP70,KEY1
          MATCH     "1",PRDTHOSI
          IF        @EQUAL
            MOVE      "*",KEY1                  * Hospital indicator
          ENDIF
.
          MOVE      SP70,KEY15
          MOVE      PRDTITMN,KEY15
          STRIP     KEY15
          ENDSET    KEY15
          APPEND    KEY1,KEY15
          APPEND    SP70,KEY15
          RESET     KEY15
.
.         COMPARE   "55",CLNO
.         CALL      TOPH0000 IF NOT LESS
.
          PRINT     *1,CPCDATE,*25,KEY15:
                    *40,PRDTDESC;
.
          MOVE      PRDTAMNT,FORM12P2
          ADD       PRDTGSTM,FORM12P2
.
          PRINT     *92,FORM12P2
          PRINT     *1,PRDTSTIM
          ADD       TWO,CLNO
          ADD       FORM12P2,TOTFEE
.
          CALL      EXTD0000                    * Print extra details
          GOTO      PRIT1000
.
.         Outpatient items
.
PRIT2000  MOVE      INVOICEN,KEY8
          PACK      KEY23,KEY8,SP70
          CALL      RDSDTRO4
PRIT2100  CALL      RDKDTRO4
          BRANCH    OVRCD,PRIT9000
.
          MATCH     KEY8,OTINVNO          * Same invoice number ?
          GOTO      PRIT9000 IF NOT EQUAL
.
          COMPARE   ONE,OTRECTYP          * Misc.item
          GOTO      PRIT2200 IF EQUAL
          COMPARE   SIX,OTRECTYP          * MBS
          GOTO      PRIT2100 IF NOT EQUAL * Not an Item record type
.
PRIT2200  UNPACK    OTDDATE,CCENT,CYEAR,CMON,CDAY      * DOB
          CALL      PACDATE
.
.         COMPARE   "55",CLNO
.         CALL      TOPH0000 IF NOT LESS
.
          PRINT     *1,CPCDATE,*25,OTITEMNO:
                    *40,OTDDESC;
.
          MOVE      OTPATAMT,FORM12P2
          ADD       OTDTGSTM,FORM12P2
.
          PRINT     *92,FORM12P2
          PRINT     *1,OTCHGTIM
          ADD       TWO,CLNO
.
          ADD       FORM12P2,TOTFEE
.
          CALL      EXTD0000                    * Print extra details
          GOTO      PRIT2100
.
.         Print totals
.
PRIT9000  
          PRINT     *N,*60,"Total:",*92,TOTFEE
          ADD       TWO,CLNO
.
PRIT9999  RETURN
+
.**********************************************************************
.         Print Extra details
.**********************************************************************
EXTD0000  BRANCH    COPTION,EXTD2000           * Outpatient
.
.         Private practice
.
          MOVE      SP70,TDESC
          MOVE      "Ro",KEY2
          MATCH     SP70,PRDTROVC              * Restrictive Override
          IF        !@EQUAL
            PACK       KEY5,KEY2,PRDTROVC
            CALL       RDCODE1
          ENDIF
          MATCH     SP70,TDESC
          IF        !@EQUAL
            PRINT     *40,"Restrictive Override: ",TDESC
            ADD       ONE,CLNO
          ENDIF
.
          UNPACK    PRDTSTXT,KEY80             * Service text
          MATCH     SP70,KEY80
          IF        !@EQUAL
            PRINT     *40,"Service Text: ",KEY80
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     SP70,PRDTNMPT
          IF        !@EQUAL
            PRINT     *40,"Number Of Patient Seen: ",PRDTNMPT
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     SP70,PRDTTDUR
          IF        !@EQUAL
            PRINT     *40,"Time Duration: ",PRDTTDUR
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     SP70,PRDTLSPN
          IF        !@EQUAL
            PRINT     *40,"LSPN: ",PRDTLSPN
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     "1",PRDTACOI               * After Care Override Indicator
          IF        @EQUAL
            PRINT     *40,"NNAC"
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     "1",PRDTDSOV               * Duplicate Service override
          IF        @EQUAL
            PRINT     *40,"NDS"
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     "1",PRDTMPOV               * Multiple Procedure Override
          IF        @EQUAL
            PRINT     *40,"MPO"
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     SP70,PRDTSDCD
          IF        !@EQUAL
            PRINT     *40,"Self Deemed: ",PRDTSDCD
            ADD       ONE,CLNO
          ENDIF
.
          GOTO      EXTD9999
.
.         Emergency
.
EXTD2000  MOVE      SP70,TDESC
          MOVE      "Ro",KEY2
          MATCH     SP70,OTDTNCAT              * Restrictive Override
          IF        !@EQUAL
            PACK       KEY5,KEY2,OTDTNCAT
            CALL       RDCODE1
          ENDIF
          MATCH     SP70,TDESC
          IF        !@EQUAL
            PRINT     *40,"Restrictive Override: ",TDESC
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     SP70,OTDTSRTX
          IF        !@EQUAL
            PRINT     *40,"Service Text: ",OTDTSRTX
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     SP70,OTDTNPAT
          IF        !@EQUAL
            PRINT     *40,"Number Of Patient Seen: ",OTDTNPAT
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     SP70,OTDTTDUR
          IF        !@EQUAL
            PRINT     *40,"Time Duration: ",OTDTTDUR
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     SP70,OTDTLSPN
          IF        !@EQUAL
            PRINT     *40,"LSPN: ",OTDTLSPN
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     "1",OTDTACAO               * After Care Override Indicator
          IF        @EQUAL
            PRINT     *40,"NNAC"
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     "1",OTDTDSRO               * Duplicate Service override
          IF        @EQUAL
            PRINT     *40,"NDS"
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     "1",OTDTMPRO               * Multiple Procedure Override
          IF        @EQUAL
            PRINT     *40,"MPO"
            ADD       ONE,CLNO
          ENDIF
.
          MOVE      SP70,THCSCOD
          MOVE      "Sd",KEY2
          MATCH     SP70,OTDTSFDM              * Self Deem Code
          IF        !@EQUAL
            PACK       KEY5,KEY2,OTDTSFDM
            CALL       RDCODE1
          ENDIF
          MATCH     SP70,THCSCOD
          IF        !@EQUAL
            PRINT     *40,"Self Deemed: ",THCSCOD
            ADD       ONE,CLNO
          ENDIF
.
EXTD9999  RETURN
+
.**********************************************************************
.         Print Top Heading
.**********************************************************************
TOPH0000  PRINT     *F,*120,*V2LON,"DB4",*HOFF:
                    *N,*50,*V2LON,"MEDICARE - ONLINE CLAIMING",*HOFF:
                    *N,*45,*V2LON,"BULK BILL ASSIGNMENT OF BENEFIT FORM",*HOFF:
                    *N,*1,"This form is the approved form as prescribed ":
                      "under section 20A of the ":
                      *ITALIC,"Health Insurance Act 1973",*HOFF:
                    *N,*N,*1,"Bulk Bill Claim for assessment by Services Australia ":
                    *N,*1,"Please retain for your records."
          MOVE      SIX,CLNO
.
          PRINT     *N
          ADD       TWO,CLNO
.
          IF        PRITFLAG=1
            CALL      ITMH0000             * print heading for list of items
          ENDIF
.
TOPH9999  RETURN
+
.**********************************************************************
.         Print tail
.**********************************************************************
TAIL0000  MOVE      ZERO,PRITFLAG
.         COMPARE   "40",CLNO
.         CALL      TOPH0000 IF NOT LESS
.
          PRINT     *N,*1,*V2LON,"Privacy Notice: ",*HOFF,"Your personal ":
                    "information is protected by law, including the ":
                    *ITALIC,"Privacy Act 1988, ",*HOFF:
                    "and is collected by ":
                    *N,"Services Australia ":
                    "for the assessment and administration of ":
                    "payments and services. ":
                    *N,*N,"This Information is required ":
                    "to process your application or claim."
.
          PRINT     *N,"Your information may be used by the agency or ":
                    "given to other parties for the purposes of research, ":
                    "investigation or where you ":
                    *N,"have agreed or it is ":
                    "required or authorised by law."
.
          PRINT     *N,"You can get more information about the way in which ":
                    "Services Australia will manage your ":
                    "personal information, ":
                    *N,"including our privacy policy at ":
                    *V2LON,"servicesaustralia.gov.au/privacy ",*HOFF:
                    " or by requesting a copy from the agency."
.
          PRINT     *N,"Information about medical/dental expenses for people ":
                     "under the age of 18 may also be disclosed to adults on ":
                     "the same Medicare card, through taxation statements."
.
          PRINT     *N,*1,*V2LON,"To be completed by the person assigning ":
                    "benefits for the services stated on this form.",*HOFF
.
          PRINT     *N,*1,"I assign my right to benefits to the Practitioner ":
                    "who rendered the services"
.
          PRINT     *N,*N,*N,*N,*N
.
          PRINT     *1,"...................................................":
                    *62,"..................................................":
                    *N,*1,"Patient Signature":
                    *62,"Date"
.
TAIL9999  RETURN
+
.**********************************************************************
.         Get HCP details
.**********************************************************************
GHCP0000  MOVE      SP70,KEY35
          MOVE      SP70,PMHCPRV1
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,KEY35          * Service doctor
          ENDIF
GHCP9999  RETURN
+
.**********************************************************************
.         Get Doctor details
.**********************************************************************
GDOC0000  MOVE      SP70,KEY35
          MOVE      SP70,PMHCPRV1
          MATCH     SP70,KEY6
          GOTO      GDOC9999 IF EQUAL         * Blank doctor
.
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,KEY35          * Service doctor
            MOVE      DPROV,PMHCPRV1
          ENDIF
GDOC9999  RETURN
+
.**********************************************************************
.         Get Servicing Location details
.**********************************************************************
GLOC0000  PACK      KEY71,PTCNCLID,SP70
          CALL      RSWBB2B2
          CALL      RKWBB2B2
          IF        OVRCD=1
            MOVE      SP70,WBB2HOSP
          ENDIF
.
          MATCH     WBB2LOCN,PTCNCLID
          IF        !@EQUAL
            MOVE      SP70,WBB2HOSP
          ENDIF
.
          MATCH     SP70,WBB2HOSP
          GOTO      GLOC2000 IF EQUAL
.
.         PACK      KEY8,PTCNCLID
.         CALL      RDPTLCN1
.         IF        OVRCD=1
.           MOVE      SP70,PTLNHOSP
.         ENDIF
.
.         MATCH     SP70,PTLNHOSP
.         GOTO      GLOC2000 IF EQUAL
.
          MOVE      WBB2HOSP,KEY3
          CALL      RDPTHSP1
          COMPARE   ZERO,OVRCD
          GOTO      GLOC8000 IF EQUAL
.
GLOC2000  UNPACK    CNAME,PTHSNAME
          UNPACK    CHADD1,PTHSADD1
          UNPACK    CHADD2,PTHSADD2
          UNPACK    SP70,PTHSADD3
          UNPACK    SP70,PTHSADD4
          UNPACK    CHPOST,PTHSPCOD
.
          PACK      KEY35,PTHSPCOD,SP70
.
GLOC8000  STRIP     PTHSADD4
          PACK      KEY35,PTHSADD4,SP1,PTHSPCOD
.
GLOC9999  RETURN
+
.**********************************************************************
         INC       STD001IO
.
.       INC       PATLCNIO/INC           * Eclipse Location file
        INC       PATHSPIO/INC           * Hospital file
        INC       WEBB2BIO/INC           * WebServices Devices file
        INC       PRIHDBIO/INC           * Private practice Header file
        INC       PRIHREIO/INC           * Private practice referral file
        INC       PRIDTRIO/INC           * Private practice transaction file
        INC       PRIPRAIO/INC           * Medical practice file
        INC       PRIINVIO/INC           * PP Invoice file
        INC       OUTDTRIO/INC           * ED Debtor transactioc file
        INC       PATINVIO/INC           * Emergency Invoice file
        INC       PMSHCPIO/INC           * HCP file 
        INC       PATDO1IO/INC           * Doctor file
        INC       RCPDTEIO/INC           * Receipt details file
        INC       OUTSITIO/INC           * Outpatient site file
        INC       OUTBOAIO/INC           * Booking file
        INC       OUTBB1IO/INC           * Booking file
        INC       OUTSESIO/INC           * Session file
        INC       OUTHEDIO/INC           * O/P template header file
.
        INC       PATCODIO/INC           * patient codes file
        INC       PATMA1IO/INC           * patient master file
        INC       PATMI1IO/INC           * admissions file
        INC       PATVISIO/INC           * visit file
        INC       PMSPX2IO/INC
        INC       PMSVX1IO/INC           * visit extention file
.

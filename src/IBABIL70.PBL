. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBABIL70                                            *
. *                                                                           *
. *     FUNCTION:         FUTURE ACTION REPORT                                *
. *                                                                           *
. *     DATE WRITTEN:     02/12/87                                            *
. *                                                                           *
. *     AUTHOR:           J.TAN                                               *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *****************************************************************************
. * V12.00.01 19/05/2025 Thanh T         TSK 0955096                          *
. *           Changed for alphanumeric visit number                           *
. *****************************************************************************
. *       V11.00.03 13/07/2020  Sunny         TSK 0892915                     *
. *                 Fixed opening balance in PELN0000 for Export File         *
. *       V11.00.02 09/07/2020  Sunny         TSK 0892915                     *
. *                 Fixed routine PELN0000 for Export File                    *
. *       V11.00.01 26/06/2020  Sunny         TSK 0892915                     *
. *                 Added routines for Create Export File - EHED0000,         *
. *                 PELN0000,CRTX0000                                         *
. *       V10.14.01 06/06/2019  Ebon Clements TSK 08760768                    *
. *                 Deleted temp file (PSTROL) after it has been processed    *
. *       V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
. *                 Deleted temp file (PSTROL) after processing               *
. *       V10.08.01 18/11/2016  Ken Bell      TSK 0822849                     *
. *                 Added 'Date Action Completed'                             *
. *       V10.05.01 02/02/2015  Ania P        CAR 261630                      *
. *                 Removed use of PORT for temp file naming                  *
. *       V10.00.02 18/07/2010  J.Tan         CAR 218429                      *
. *                 Fixed printing the heading                                *
. *       V10.00.01 01/07/2010  J.Tan         CAR 218429                      *
. *                 Added Future action filter                                *
. *       V9.09.01  25/10/2007  Ebon Clements CAR 151941                      *
. *                 Added health fund and occupational                        *
. *                 group filters                                             *
. *       V9.07.01  31/08/2006  Peter Vela    CAR 114268                      *
. *                 Converted lower case to upper case                        *
. *                 ANS when posting data                                     *
. *       V9.05.01  10/03/2006  Mike Laming   CAR 79281                       *
. *                 Increase PATFACT2 to KEY19 & add FACODE to key string     *
. *       V9.04.03  12/09/2004  J.Tan   CAR 75032                             *
. *                 Mods to check for credit notes                            *
. *       V9.04.02  14/12/2004  J.Tan   CAR 55649                             *
. *                 Mods for multi hospital (hospital id)                     *
. *       V9.04.01  27/09/2003  J.Tan                                         *
. *                 Moved around codes for P*M errors                         *
. *       V9.03.01  13/08/2003  Lina Vo   CAR41196                            *
. *                 Recompiled for big changes in PATSELFN.                   *
. *       V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *                   V5.07.B02 27/01/1999  N Harrington                      *
. *                             507 rebounds                                  *
. *                   V5.07.B01 12/11/98 Jim Stathopoulos                     *
. *                             507 Changes                                   *
. *****************************************************************************
. *                    V5.01.01 06/07/89 K.Peak                               *
. *                             Changed Adm & U/R to 8 chrs                   *
. *                    V5.01.02 14/07/89 J.Tan                                *
. *                             Remove system options                         *
. *                             SRF 101451                                    *
. *                    V5.01.03 29/09/89 Graeme Williams                      *
. *                             Put system options back in                    *
. *                             SRF 101621                                    *
. *                    V5.01.04 20/11/89 J.Tan                                *
. *                             Added other finacial option                   *
. *                             in PATSELFN.   SRF 102252                     *
. *                    V5.01.05 11/07/90 Paul Duncan                          *
. *                             recomp for OUTBOA & OUTBOB                    *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *       V5.03.B1  06/06/95  Paul Howells                  SRF 136735        *
. *                 Recompiled for PATSELFN                                   *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
          INC       IBASECFD/INC
          INC       IBAPASFD/INC
          INC       AAEDE1FD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTSITFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATINVFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATFACFD/INC
          INC       PATHSPFD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATSELDT/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.        Temporary file layout
.
SORTSTR  DIM      20
SORT1    INIT     "u1-47"
SORT2    INIT     "u1-3,12-47"
.
PATACT1  IFILE    SQL, FIXED=155, KEY=SORTSTR
.
.NAME    TYPE    SIZE   PHYSICAL  START    DESCRIPTION
.----    ----    ----   --------  -----    -----------
FCLM     DIM      3        3         1     CLAIM TYPE
FDATE    DIM      8        8         4     FUTURE ACTION DATE (CCYYMMDD)
FSNAME   DIM      20       20       12     PATIENT SURNAME
FADM     DIM      8        8        32     PATIENT ADMISSION NUMBER
UNIQUEX  DIM      8        8        40     UNIQUE NUMBER
FGNAME   DIM      1        1        48     INITIAL GIVEN NAME
FDDAT    DIM      10       10       49     PATIENT DISCHARGED DATE (DD/MM/CCYY)
FOPEN    FORM     9.2      7        59     OPENING BALANCE
FCUR     FORM     9.2      7        66     CURRENT BALANCE OUTSTANDING
FPAY     FORM     9.2      7        73     PAYMENT TO DATE
FCODE    DIM      3        3        80     ACTION CODE
FCOMM    DIM      60       60       83     COMMENTS
FSYS     FORM     1        2       143     SYSTEM (1=A&E, 2=O/P, 3=I/P, 4=Othr)
FADTCOMP DIM      10       10      145     FUTURE ACTION DATE COMPLETED (DD/MM/CCYY)
.                          EOR     155
.
.        Extra variables
.
ACTDESC  DIM      18
.
CMDLINE  DIM      50
CODECL   INIT     "CL"
CODEFA   INIT     "FA"
CUDATE   DIM      8
CODE     DIM      2
COUNTER  FORM     1         * Counter for Excel heading
.
DIM10    DIM      10
DIM30    DIM      30
.
EXPORTFL FORM     1         * Create Export File
.
FILTHFND DIM      6
FILTOCGR DIM      3
FILTFUTR DIM      3
FNAMEI   DIM      8
FNAMER   DIM      8
FORM8    FORM     8
FROMDAT  DIM      8        * FROM DATE IN (CCYYMMDD) FORMAT
FUNDDESC DIM      40
FUTRDESC DIM      20
FIVE9    FORM     "59"
HOSPID   DIM      3
HOSPNAME DIM      50
.
OCGRDESC DIM      20
OPCND    FORM     1
.
PSTROL   FILE      ASCII, FIXED=256
PSTSRT   FILE      ASCII, FIXED=256
.
SAVCLM   DIM      3
STATUS   FORM     1
.
TODAT    DIM      8        * TO DATE IN (CCYYMMDD) FORMAT
TOTJOR   FORM     9.2
TOTPAY   FORM     9.2
.
UNIQUE   FORM     8
.
VSYSTEM  DIM      8
VAAE     INIT     "   A & E"
VOUT     INIT     "Outpatnt"
VINP     INIT     " Inpatnt"
VOTH     INIT     "   Other"
.
PRGID     INIT     "IBABIL70"
PRGNAM    INIT      "Future Action Report"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patfactf";
          OPEN      PATFACT2,"patfactf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN9;*212,CFEETYP
.
          PACK      CUDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
+
.
.        START OF PROGRAM
.
START   DISPLAY     *P1:4,*EF,"Keyin From Date :":
                        *P1:6,"Keyin To   Date :"
+
.
.       Key in the from date for the report
.
KADAT1  MOVE        FOUR,CVERT
        MOVE        TWENTY,CCOL
        UNPACK      SP8 WITH CCENT,CYEAR,CMON,CDAY
        CALL        KEYDATE
        BRANCH      OVRCD,ENDIT
.
        PACK        FROMDAT WITH CCENT,CYEAR,CMON,CDAY
        REP         " 0",FROMDAT
.
        MATCH       CUDATE,FROMDAT
        GOTO        INVDTE1 IF LESS
.
.       Key in to date
.
KADAT2  MOVE        SIX,CVERT
        CALL        KEYDATE
.
        PACK        TODAT WITH CCENT,CYEAR,CMON,CDAY
        REP         " 0",TODAT
.
        MATCH       FROMDAT,TODAT
        GOTO        INVDATG IF LESS
.
        MATCH       CUDATE,TODAT
        GOTO        INVDTE2 IF LESS
        GOTO        KEYSEQ
.
INVDATG DISPLAY    *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                   "To Date **",*W3,*P1:24,*EL;
        GOTO       KADAT1
.
INVDTE1 DISPLAY    *P1:24,*B,*EL,*V2LON:
                   "** Warning: From date less than the current date **":
                   *W3,*P1:24,*EL;
        GOTO       KADAT2
.
INVDTE2 DISPLAY    *P1:24,*B,*EL,*V2LON,"** To Date must be >= ":
                   "current Date **",*W3,*P1:24,*EL;
.
.        Input Sequence
.
KEYSEQ  MOVE        ZERO,OPTION
        DISPLAY     *P1:8,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:9,*V2LON,ONE,*HOFF," = Date Sequence":
                    *P1:10,*V2LON,TWO,*HOFF," = Surname Sequence"
.
KEYOPT  KEYIN       *P1:12,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE     ZERO,OPTION
        GOTO        START IF EQUAL
.
        BRANCH      OPTION OF REKEY,REKEY
        DISPLAY     *P15:12,*B,*EL,*V2LON,"** Invalid Option ",*W2;
        GOTO        KEYOPT
.
.        Check if dates are OK or not
.
REKEY   CALL        PATSELFN
        BRANCH      EXIT,START
.
        IF          IBCNMHOS=1
          DISPLAY     *P1:12,"    Hospital Id        :"
          CALL        KHOS0000                    
        ENDIF
.
        CALL        KFND0000                * Keyin Health Fund
        BRANCH      EXIT,START
.
        CALL        KOCG0000                * Keyin Occupational Group
        BRANCH      EXIT,START
.
        CALL        KFUT0000                * Keyin Future action code
        BRANCH      EXIT,START
.
        CALL        CRTX0000                * Create Export File 
        BRANCH      EXIT,START 
.
        KEYIN       *P1:24,*EF," Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,"/":
                    *V2LON,"C",*HOFF,") ? ",*V2LON,ANS;
.
        REP         UPPLOW,ANS
.
        MATCH       "C" TO ANS
        GOTO        ENDIT IF EQUAL
.
        MATCH       "N" TO ANS
        GOTO        START IF EQUAL
.
        MATCH       "Y" TO ANS
        GOTO        CNTCHK IF EQUAL
.
        BEEP
        GOTO       REKEY
.
.       Start processing of the data
.
CNTCHK  DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                   "** Initialising Future Action File **",*W2,*P1:10;
        CALL       INDZD
        BRANCH     OPCND OF ENDIT
.
        MOVE        ZERO,COUNTER         * Counter for Excel heading
        MOVE        ZERO,CPAGENO
        CLOCK       TIME,CTIMEIS         * Time report was started
        MOVE        ZERO,UNIQUE
.
        DISPLAY     *P50:24,"Admission No: ";
.
.       Loop over the future action file for the supplied date range
.
        PACK        KEY19 WITH FROMDAT,SP20                            *C-79281
        CALL        RDSFACT2
NXTFAC  CALL        RDKFACT2
        BRANCH      OVRCD OF REPORT
.
        MATCH       FADATE,TODAT
        GOTO        REPORT IF LESS
.
        MATCH       SP70,FILTHFND
        IF          !@EQUAL
          MATCH       FILTHFND,PTFCHFND
          GOTO        NXTFAC IF NOT EQUAL
        ENDIF
.
        MATCH       SP70,FILTOCGR
        IF          !@EQUAL
          MATCH       FILTOCGR,PTFCOCGR
          GOTO        NXTFAC IF NOT EQUAL
        ENDIF
.
        MATCH       SP70,FILTFUTR
        IF          !@EQUAL
          MATCH       FILTFUTR,FACODE
          GOTO        NXTFAC IF NOT EQUAL
        ENDIF
.
        DISPLAY     *P65:24,*V2LON,FADMNO;
.
        MOVE        FADMNO,PVIBILL
        MOVE        PVIBILL,KEY8
        CALL        RDVISA1
        BRANCH      OVRCD,NXTFAC
.
        IF          IBCNMHOS =1
          MATCH       SP10,HOSPID
          IF          !@EQUAL
            PACK        KEY8,PVIBILL,SP8
            CALL        RDPMVX11
            IF          OVRCD=0
              MATCH       PMVXMHOS,HOSPID
              GOTO        NXTFAC IF NOT EQUAL
            ENDIF
          ENDIF
        ENDIF
.
        COMPARE     FSTSYS,PVITYPE
        GOTO        NXTFAC IF LESS
.
        COMPARE     PVITYPE,FEDSYS
        GOTO        NXTFAC IF LESS
.
        MATCH       FSTSITE,PVISITE
        GOTO        NXTFAC IF LESS
.
        MATCH       PVISITE,FEDSITE
        GOTO        NXTFAC IF LESS
.
        MATCH       SP3,FSTDEPT
        GOTO        CNTI IF EQUAL
.
        MOVE        PVISITE,KEY6
        CALL        RDSITA1
        BRANCH      OVRCD,NXTFAC
.
        MATCH       FSTDEPT,OSTSYST
        GOTO        NXTFAC IF LESS
.
        MATCH       OSTSYST,FEDDEPT
        GOTO        NXTFAC IF LESS
.
.       Initialize the variables
.
CNTI    MOVE        ZERO,FOPEN
        MOVE        ZERO,FCUR
        MOVE        ZERO,FPAY
        MOVE        ZERO,TOTPAY
        MOVE        ZERO,TOTJOR
.
        MOVE        FADMNO,DIM8VISN
        MOVE        DIM8VISN,FADM
        MOVE        FADATE,FDATE
        MOVE        FACODE,FCODE
        MOVE        FACOMM,FCOMM
        MOVE        PVITYPE,FSYS
.
.       Get Action Completed Date (DD/MM/CCYY)
        MOVE        SP10,FADTCOMP
        UNPACK      PTFCDTCP,CCENT,CYEAR,CMON,CDAY
        CALL        PACDATE
        MOVE        CPCDATE,FADTCOMP
.
.       Get the patient details (set up FCLM and FDDAT)
.
        CALL        GETD0000
        BRANCH      EXIT,NXTFAC
.
.       Read in the master file record
.
        MOVE        PVIURNO,PURNO
        MOVE        PURNO,KEY8
        CALL        RDMAST1
        BRANCH      OVRCD OF NXTFAC
.
        MOVE        PSNAME,FSNAME
        MOVE        PGNAME,FGNAME
.
.       Loop over the invoice file to get the dollar amounts
.
        PACK        KEY16 WITH FADMNO,SP8
        CALL        RDSINV3
NXTINV  CALL        RDKINV3
        BRANCH      OVRCD OF CALBAL
.
        MATCH       FADMNO,PINVADM
        GOTO        CALBAL IF NOT EQUAL
.
        ADD         PINVAMT,FOPEN
        ADD         PINVPATA,TOTPAY
        ADD         PINVHFDA,TOTPAY
        ADD         PINVOTHA,TOTPAY
        ADD         PINVJOUR,TOTJOR
        ADD         PTINCRTT,TOTJOR      * credit notes
        GOTO        NXTINV
.
.       Calculate the current amount outstanding
.
CALBAL  MOVE        FOPEN,FCUR
        ADD         TOTPAY,FCUR
        ADD         TOTJOR,FCUR
.
.       Make the payment figure positive
.
        MOVE        TOTPAY,FPAY
        MULT        SEQ,FPAY
.
.       Pack up the key depending on the option selected
.
        ADD         ONE,UNIQUE
        BRANCH      OPTION OF OPT10,OPT20
.
.       Date order
.
OPT10   MOVE        UNIQUE,UNIQUEX
        PACK        KEY45 WITH FCLM,FDATE,FSNAME,FADM,UNIQUEX
        WRITE       PATACT1,KEY45;FCLM,FDATE,FSNAME,FADM,UNIQUEX,FGNAME:
                                FDDAT,FOPEN,FCUR,FPAY,FCODE,FCOMM,FSYS,FADTCOMP
.
        GOTO        NXTFAC
.
.       Surname order
.
OPT20   MOVE        UNIQUE,UNIQUEX
        PACK        KEY39 WITH FCLM,FSNAME,FADM,UNIQUEX
        WRITE       PATACT1,KEY39;FCLM,FDATE,FSNAME,FADM,UNIQUEX,FGNAME:
                                FDDAT,FOPEN,FCUR,FPAY,FCODE,FCOMM,FSYS,FADTCOMP
        GOTO        NXTFAC
.
.       Create an indexed file based on port
.
INDZD
        MOVE        ZERO,STATUS
.
        CALL        TFILENAM
        MOVE        TFILNAME,FNAMEI
.
        CALL        TFILENAM
        MOVE        TFILNAME,FNAMER
.
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT
.
        CALL          KILLIDZ
.
        PREP        PSTROL,FNAMER
        LOAD        SORTSTR USING OPTION OF SORT1,SORT2
.
        WRITE       PSTROL,SEQ;"isbuild ",FNAMEI," 155 ",SORTSTR
        WEOF        PSTROL,SEQ
.
        MOVE        ONE,STATUS
.
        CLEAR       CMDLINE
        APPEND      "sh ",CMDLINE
        APPEND      FNAMER,CMDLINE
        APPEND      ".txt",CMDLINE
        RESET       CMDLINE
.
        DISPLAY     *P1:12,*EF;
        EXECUTE     CMDLINE,TASKID
        CLOSE       PSTROL,DELETE
        DISPLAY     *P1:12,*EF;
.
        MOVE        ZERO,OPCND
.        TRAP        NOINDZ IF IO
        OPEN        PATACT1,FNAMEI
.        TRAPCLR     IO
ENDC    RETURN
.
NOINDZ  MOVE        ONE,OPCND
        DISPLAY     *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMEI," Not Found **",*W4;
        TRAPCLR     IO
        RETURN
.
.       Deleting an indexed file based on port
.
KILLIDZ CLOSE      PATACT1
.
        MOVE       ZERO,STATUS
.
        DISPLAY    *P1:24,*EL,*P20:24,*V2LON:
                   "** Deleting Indexed File **",*W;
.
        CLEAR      CMDLINE
        APPEND     "iserase ",CMDLINE
        APPEND     FNAMEI,CMDLINE
        RESET      CMDLINE
        EXECUTE    CMDLINE,TASKID
        RETURN
+
.
.         Get the visit details
.
GETD0000  BRANCH    PVITYPE,GETD1000,GETD2000,GETD3000,GETD4000,GETD9000
          GOTO      GETD9000
.
.         A+E visit
.
GETD1000  MOVE      SP10,FDDAT
          MOVE      PVIBILL,KEY8
          CALL      RDDETA1
          BRANCH    OVRCD,GETD9000
.
          MOVE      ADACOMP,FCLM
          GOTO      GETD8000
.
.         Outpatient visit
.
GETD2000  MOVE      SP10,FDDAT
          MOVE      "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
.
          PACK      CFNAME,OSTFILE,FILBB1A1
          CALL       OPOTBB11
          MOVE      PVIBILL,KEY8
          CALL      RDBOKB1
          CLOSE     OUTBB1A1
          BRANCH    OVRCD,GETD9000
.
          MOVE      OBACOMP,FCLM
          GOTO      GETD8000
.
.         Inpatient visit
.
GETD3000  MOVE      SP10,FDDAT
          MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,GETD9000
.
          MOVE      ACLAIM,FCLM
.
          MOVE      PVIBILL,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,GETD8000
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FDDAT
          GOTO      GETD8000
.
.         Other financial visit
.
GETD4000  MOVE      SP10,FDDAT
          MOVE      SP3,FCLM
          PACK      KEY16,PVIBILL,SP8
          CALL      RDSINV3
          CALL      RDKINV3
          BRANCH    OVRCD,GETD9000
.
          MATCH     PVIBILL,PINVADM
          GOTO      GETD9000 IF NOT EQUAL
.
          MOVE      PINVCLM,FCLM
.
.         Details found
.
GETD8000  MOVE      ZERO,EXIT
          GOTO      GETD9999
.
.         No details found
.
GETD9000  MOVE      ONE,EXIT
.
GETD9999  RETURN
+
.
.         Loop over the temporary file and generate the report
.
REPORT    COMPARE    ZERO,UNIQUE
          GOTO       NODATA IF EQUAL
.
          DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                     "** Generating Report **",*W2:
                     *P1:24,*EL,*P20:24,"Printing Future Action :";
.
          MOVE      "60",CLNO
          MOVE      SP3,SAVCLM
.
          READ      PATACT1,SP30;;
NXTACT    READKS    PATACT1;FCLM,FDATE,FSNAME,FADM,UNIQUEX,FGNAME:
                                FDDAT,FOPEN,FCUR,FPAY,FCODE,FCOMM,FSYS,FADTCOMP
          GOTO      ENDLN IF OVER
          MOVE      UNIQUEX,UNIQUE
.
          DISPLAY   *P50:24,*V2LON,FADM;
.
.         Check if we have changed claim code
.
.
          MATCH     SAVCLM,FCLM
          CALL      HEAD IF NOT EQUAL
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
.         Get the system
.
          MOVE      SP10,VSYSTEM
          LOAD      VSYSTEM,FSYS,VAAE,VOUT,VINP,VOTH
.
.         Get the description of the action code
.
          MOVE      SP20,ACTDESC
.
          MATCH     SP3,FCODE
          GOTO      PRTDTA IF EQUAL
.
          PACK      KEY5 WITH CODEFA,FCODE
          CALL      RDCODE1
          BRANCH    OVRCD OF PRTDTA
.
          MOVE      TDESC,ACTDESC
.
.         Print the data
.
PRTDTA    IF        EXPORTFL=1
            CALL      PELN0000                * Print export file line
          ELSE
            UNPACK    FDATE WITH XCENT,XYEAR,XMON,XDAY
            PRINT     *1,"|",*2,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                      *12,"|",*14,FGNAME,DOT,FSNAME,*36,"|",FADM:
                      *46,"|",*48,FCODE,*52,ACTDESC,*70,"|",*72,FDDAT:
                      *84,FOPEN,*96,FCUR,*108,FPAY,*121,FADTCOMP,*132,"|":
                      *N,*1,"|",*12,"|",*36,"|",VSYSTEM,*46,"|";
.
            IF        IBCNMHOS=1
              PACK      KEY8,FADM,SP8
              CALL      RDPMVX11
              PRINT     *48,PMVXMHOS;
            ENDIF
.
            PRINT     *70,"|",*72,FCOMM,*132,"|"
            CALL      PAGEND
          ENDIF 
.
          ADD       TWO,CLNO
          MOVE      FCLM,SAVCLM
          GOTO      NXTACT
.
.
.                 HEADINGS FOR OUTPUT
.
HEAD      BRANCH    EXPORTFL,HEAD50
.
          CALL      IBACLOCK
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          BRANCH    OPTION OF HEAD10,HEAD20
.
HEAD10    PRINT     *N,*40,"DATE - SEQUENCE";
          GOTO      HEAD30
.
HEAD20    PRINT     *N,*40,"SURNAME - SEQUENCE";
.
HEAD30    MOVE      SP30,TDESC
          PACK      KEY5 WITH CODECL,FCLM
          CALL      RDCODE1
          UNPACK    FROMDAT WITH XCENT,XYEAR,XMON,XDAY
          UNPACK    TODAT WITH CCENT,CYEAR,CMON,CDAY
.
          IF        IBCNMHOS=1
            PRINT     *N,*40,"Hospital ID : ",HOSPID," - ",HOSPNAME;
          ENDIF
.
          IF        CFEETYP=5
            PRINT     *N,*40,"Contract : ",FUNDDESC;
            PRINT     *N,*40,"Occupational Group : ",OCGRDESC;
          ELSE
            PRINT     *N,*40,"Health Fund : ",FUNDDESC;
            PRINT     *N,*40,"Occupational Group : ",OCGRDESC;
          ENDIF
          PRINT     *N,*40,"Future Action Code : ",FUTRDESC;
.
          PRINT     *N,*N,*1,"Claim Type : ",FCLM," ",TDESC:
                    *40,"From Date : ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *65,"To Date : ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,*N,*N;
.
          CALL      PRTSEL
          CALL      PAGEND
.
          PRINT     *1,"|  Action",*12,"| Patient Name",*36,"|  Adm No":
                    *46,"| Action Code",*70,"| Dsch Date":
                    *89,"Opening",*101,"Current":
                    *112,"Payments",*121,"Date Action",*132,"|":
                    *N,*1,"|   Date",*12,"|",*36,"|";
.
          IF        IBCNMHOS=1
            PRINT     *46,"| Hospital Id";
          ELSE
            PRINT     *46,"|";
          ENDIF
.
          PRINT     *70,"| Comments",*89,"Balance",*101,"Balance",*121,"Completed",*132,"|"
          CALL      PAGEND
.
          MOVE      TEN3,CLNO
          IF        IBCNMHOS=1
            ADD       ONE,CLNO
          ENDIF
          MOVE      FCLM,SAVCLM
          CLOCK     TIME,CTIMEIS
          GOTO      HEAD9999
.
.         Create Export File
.
HEAD50    IF        COUNTER=0
            CALL      EHED0000                * Print the heading
          ENDIF 
.
HEAD9999  RETURN
+
.------------------------------------------------------------------------------
.
NODATA    
.         DISPLAY   *P1:24,*EL,*P10:24,*B,*V2LON,"No Patient Selected",*W2;
          CALL      KILLIDZ
          GOTO      START
.
.         End of report
.
ENDLN     COMPARE   ONE,EXPORTFL
          GOTO      ENDLN10 IF EQUAL
          PRINT     *N,*N,"  ***  END OF REPORT  ***"
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Report Generation Completed **",*W2;
ENDLN10   CALL      KILLIDZ
          GOTO      START
.
+
PRTSEL    COMPARE   ZERO,FSTSYS
          GOTO      AEHEAD IF NOT EQUAL
.
          PRINT     *40,"CONSOLIDATED REPORT",*N,*N;
          COMPARE   ZERO,FSTSYS
          RETURN    IF EQUAL
.
AEHEAD    COMPARE   ONE,FSTSYS
          GOTO      OPHEAD IF NOT EQUAL
.
          PRINT     *40,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          COMPARE   ONE,FSTSYS
          RETURN    IF EQUAL
.
OPHEAD    COMPARE   TWO,FSTSYS
          GOTO      IPHEAD IF NOT EQUAL
.
          PRINT     *40,"OUTPATIENT REPORT",*N;
          MATCH     SP6,FSTSITE
          GOTO      ALSITE IF NOT EQUAL
.
          PRINT     *40,"ALL SITES";
          GOTO      OPDEPT
.
ALSITE    PRINT     *40,"SITE",FSTSITE;
.
OPDEPT    MATCH     SP3,FSTDEPT
          GOTO      ALDEPT IF NOT EQUAL
.
          PRINT     *54,"ALL DEPARTMENTS",*N,*N;
          GOTO      PRTSEL9
.
ALDEPT    PRINT     *54,"DEPT",FSTDEPT,*N,*N;
          GOTO      PRTSEL9
.
IPHEAD    COMPARE   THREE,FSTSYS
          GOTO      OTHEAD IF NOT EQUAL
.
          PRINT     *40,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
OTHEAD    PRINT     *40,"OTHER FINANCIAL REPORT",*N,*N;
.
PRTSEL9   RETURN 
.
+
.         Subroutine to print the page end's
.
PAGEND    PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
.         Keyin hospital id 
.
KHOS0000  MOVE       "26",ECOL
          MOVE       "12",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS5000,KHOS9000
          MOVE       PTHSHOSP,HOSPID
          MOVE       PTHSNAME,HOSPNAME
          GOTO       KHOS6000
.
KHOS5000  MOVE       "All Hospitals",HOSPNAME
          MOVE       SP70,HOSPID  
.
KHOS6000  MOVE      ZERO,EXIT
          GOTO      KHOS9000
.
KHOS9000  MOVE      ONE,EXIT
KHOS9999  RETURN
.
.****************************************************************************
.*                                KOCG0000                                  *
.* Keyin occupational group                                                 *
.****************************************************************************
KOCG0000  DISPLAY   *P1:14,"Occupational Group : ",SP30
          MOVE      SP70,FILTOCGR
          MOVE      SP70,OCGRDESC
          MOVE      "22",ECOL
          MOVE      "14",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          MOVE      "w0",CODE
          DISPLAY   *PECOL:EVERT,SP6
.         
          CALL      PATCODKY
          BRANCH    EXIT,KOCG9000,KOCG9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P26:EVERT,TDESC
          MOVE      ACODE,FILTOCGR
          MOVE      TDESC,OCGRDESC
.
KOCG9000  MOVE      ZERO,EXIT
          GOTO      KOCG9999
.
KOCG9100  MOVE      ONE,EXIT
          GOTO      KOCG9999
.
KOCG9999  RETURN
.
.****************************************************************************
.*                                KFUT0000                                  *
.* Keyin Future action code                                                 *
.****************************************************************************
KFUT0000  DISPLAY   *P1:15,"Future Action Code : ",SP30
          MOVE      SP70,FILTFUTR
          MOVE      SP70,FUTRDESC
          MOVE      "22",ECOL
          MOVE      "15",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          MOVE      "FA",CODE
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,KFUT9000,KFUT9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P26:EVERT,TDESC
          MOVE      ACODE,FILTFUTR
          MOVE      TDESC,FUTRDESC
.
KFUT9000  MOVE      ZERO,EXIT
          GOTO      KFUT9999
.
KFUT9100  MOVE      ONE,EXIT
          GOTO      KFUT9999
.
KFUT9999  RETURN
.
.****************************************************************************
.*                                CRTX0000                                  *
.* Create export file for excel                                             *
.****************************************************************************
CRTX0000  MOVE      ZERO,EXIT
          MOVE      ZERO,EXPORTFL                  * No to create export file
          DISPLAY   *P1:23,"Create Export file for Excel : "
          KEYIN     *P32:23,*V2LON,*RV,DIM1:
                    *P32:23,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:23,*V2LON,DIM1
          MOVE      ONE,EXPORTFL
          MATCH     "Y",DIM1
          GOTO      CRTX9999 IF EQUAL
          MOVE      ZERO,EXPORTFL
.
CRTX9999  RETURN
.
.*******************************************************************************
.         Routine to print heading for Export File
.*******************************************************************************
EHED0000  PRINT     "<table border=1><tr><td width=10%>Action Date</td>":
                    "<td>Patient Name</td>":
                    "<td>Admission No.</td>":
                    "<td>Visit Type</td>":
                    "<td>Health Fund</td>":
                    "<td>Occupational Group</td>":
                    "<td>Action Code</td>":
                    "<td>Comments</td>"
.
          IF        IBCNMHOS=1
            PRINT    "<td>Hospital Id</td>"
          ENDIF
.
          PRINT     "<td>Discharge Date</td>":
                    "<td>Opening Balance</td>":
                    "<td>Current Balance</td>":
                    "<td>Payments</td>":
                    "<td>Date Action Completed</td>"
.
.
          PRINT    "</tr>"
          MOVE     ONE,COUNTER
.
EHED9999  RETURN
.
.****************************************************************************
.         Print line for Export file
.****************************************************************************
PELN0000    PACK      KEY19,FDATE,FADM,FCODE      * Occ group and health fund
            CALL      RDFACT2                     
.
            UNPACK    FDATE WITH XCENT,XYEAR,XMON,XDAY
            MOVE      SP70,DIM10
            PACK      DIM10,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR  * Action date
.
            MOVE      SP70,DIM30
            PACK      DIM30,FGNAME,DOT,FSNAME                  * Patient Name
.
            PRINT     "<tr><td>",DIM10,"</td>":  
                      "<td>",DIM30,"</td>":
                      "<td>",FADM,"</td>":
                      "<td>",PVITYPE,"</td>":
                      "<td>",PTFCHFND,"</td>"   
.
            PRINT     "<td>",PTFCOCGR,"</td>":   
                      "<td>",FCODE,"</td>":
                      "<td>",FCOMM,"</td>"
.
            IF        IBCNMHOS=1
              PACK      KEY8,FADM,SP8
              CALL      RDPMVX11
              PRINT     "<td>",PMVXMHOS,"</td>"
            ENDIF
.
            PRINT     "<td>",FDDAT,"</td>":       
                      "<td>",FOPEN,"</td>":     
                      "<td>",FCUR,"</td>":
                      "<td>",FPAY,"</td>":
                      "<td>",FADTCOMP,"</td>"
.
            PRINT       "</tr>"
            MOVE        SP70,DIM30
            MOVE        SP70,DIM10
PELN9999    RETURN
.
.****************************************************************************
.*                                KFND0000                                  *
.* Keyin occupational group                                                 *
.****************************************************************************
KFND0000  IF        CFEETYP=5
            DISPLAY   *P1:13,"Contract    : ",SP30
          ELSE
            DISPLAY   *P1:13,"Health Fund : ",SP30
          ENDIF
.
          MOVE      SP70,FILTHFND
          MOVE      SP70,FUNDDESC
          MOVE      "15",ECOL
          MOVE      "13",EVERT
          MOVE      SP70,CKYIDEF6
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATFNDKY
          BRANCH    EXIT,KFND9000,KFND9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,HCODE,*HOFF,*P25:EVERT,HFNAME
          MOVE      HCODE,FILTHFND
          MOVE      HFNAME,FUNDDESC
.
KFND9000  MOVE      ZERO,EXIT
          GOTO      KFND9999
.
KFND9100  MOVE      ONE,EXIT
          GOTO      KFND9999
.
KFND9999  RETURN
.
          INC       PATCODKY
          INC       PATFNDKY
          INC       STD001IO
          INCLUDE   PATSELFN
          INC       PATHSPKY
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
          INC       PATFACIO/INC
          INC       PATHSPIO/INC
          INC       PATMI1IO/INC
          INC       PATDSCIO/INC
          INC       PATFN1IO/INC
          INC       PATMA1IO/INC
          INC       PATINVIO/INC
          INC       PATCODIO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       OUTBB1IO/INC
          INC       OUTSITIO/INC
          INC       AAEDE1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
ENDIT     CHAIN     PGM
          STOP

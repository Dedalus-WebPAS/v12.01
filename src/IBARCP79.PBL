.******************************************************************************
.* System    :   Central Receipting                                           *
.* Program   :   IBARCP79                                                     *
.* Desc      :   BPay Remittances (Direct Deposit) upload to Central Receiptng*
.******************************************************************************
.* Date      :   03/08/2009       (Cloned from IBARCP77)                      *
.* Author    :   Mike Laming                                                  *
.* Mods      :                                                                *
.*                                                                            *
.*        V10.14.02 14/06/2019  J.Tan            TSK 0876543                  *
.*                  Fixed RCP Transaction count of tempfile                   *
.*        V10.14.01 20/03/2019  J.Tan            TSK 0857392                  *
.*                  Changed RCP Transaction count from DIM3 to DIM5           *
.*        V10.13.01 05/12/2018  Don Nguyen     TSK 0838558                    *
.*                  Deleted temp files (PRTFILE1 & CSVFILE2) on program exit  *
.*        V10.09.02 12/12/2016  J.Tan          TSK 0828047                    *
.*                  Fixed Non Multi Hospital                                  *
.*        V10.09.01 12/12/2016  J.Tan          TSK 0828047                    *
.*                  Fixed Non Multi Hospital                                  *
.*        V10.08.04 24/11/2016  J.Tan          TSK 0828047                    *
.*                  Fixed printing error messages                             *
.*                  21/11/2016  J.Tan          TSK 0822482                    *
.*                  Mods to check for Fully Credited invoices                 *
.*        V10.08.03 30/08/2016  J.Tan         TSK 0819992                     *
.*                  Mods RCDTALLO to 2 instead of 0 for Suspense register     *
.*        V10.08.02 21.07.2016  J.Tan          TSK 0822384                    *
.*                  Fixed Checking BPAY Header amount                         *
.*        V10.08.01 12.07.2016  J.Tan          TSK 0822384                    *
.*                  Fixed record length for TMPBDTA - TMPDEF02                *
.*        V10.07.03 29.01.2016  J.Tan          CAR 303942                     *
.*                  Mods to get payment code for BPAY                         *
.*        V10.07.02 12.01.2016  J.Tan          CAR 303942                     *
.*                  Mods to get payment code for BPAY                         *
.*        V10.07.01 16.11.2016  J.Tan          CAR 303942                     *
.*                  Mods to get payment code for direct deposit               *
.*        V10.04.02 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.04.01 07/02/2014  Steve Armstrong  CAR 288084                   *
.*                  Recompiled for changes to TMPDTEFD                        *
.******************************************************************************
.*        V10.01.02 13/01/2011  Mike Laming    CAR 233084                     *
.*                  Changed to use new include CLPATINV (Added PTINCNID)      *
.*        V10.01.01 26/11/2010 Mike Laming   CAR ......                       *
.*                  Correct test of ECOL (to EVERT) at KYHOS210               *
.******************************************************************************
.*        V9.12.02  23/02/2010 Mike Laming   CAR 215885                       *
.*                  Mods to remove "Episode" column & mods to Warnings & Errs *
.*        V9.12.01  26/08/2009 Mike Laming   CAR 175448                       *
.*                  Add PTINCRTT to IBALANCE                                  *
.*        V9.12.00  03/08/2009 Mike Laming   CAR 1R75448                      *
.*                  New Program to read BPay ".csv" load file                 *
.******************************************************************************
+
          INC       STD001FD
          INC       STDHLPDF
          INC       RSHWEBDF
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       FMSCHQFD/INC            * FMS Cheque Acct Master File
          INC       FMSCSAFD/INC            * FMS Selected Control Accounts
          INC       FMSLMAFD/INC            * FMS Ledger Master Details
          INC       PATCONFD/INC            * Patient Control file      
          INC       PATCODFD/INC            * Cat.Codes file
          INC       PATFN1FD/INC            * Health Fund Table
          INC       PATMA1FD/INC            * PMI Master
          INC       PATINVFD/INC            * Patient Invoice file
          INC       PATHOSFD/INC            * Hospital Details 
          INC       PATHSPFD/INC            * Hospital Details 
          INC       PATVISFD/INC            * Patient Visit Details
          INC       PMSVX1FD/INC            * Patient Visit Extension
          INC       PATRE1FD/INC            * Patient Responsible Person
          INC       RCPCIDFD/INC            * Cashier ID file
          INC       RCPBNKFD/INC            * Receipting Bank Header file
          INC       RCPBDTFD/INC            * Receipting Bank Detail file
          INC       RCPDTEFD/INC            * Receipting Receipt Details file
          INC       RCPREGFD/INC            * Receipting Register Details     
          INC       TMPBNKFD/INC            * RCP Temp Bank Header file
          INC       TMPBDTFD/INC            * RCP Temp Bank Detail file
          INC       TMPDTEFD/INC            * RCP Temp Receipt Details file
          INC       WEBSECFD/INC            * Web Security / Web Users
.
          INC       WEBCONFD/INC
          INC       WEBSCHFD/INC
          INC       IBAPDFFD/INC
.
. ----- Tempory Print file definition -----
.
PRTFILE1  FILE      ASCII, FIXED=256
.
. ----- Input Text File Definitions -----
.
CSVFILE1  FILE      ASCII, FIXED=200        * re-named ".csv" file (rcp79csv.txt
CSVFILE2  FILE      ASCII, FIXED=200        * output for piped ".txt" file
CSVHL7FL  FILE      HL7, FIXED=200          * input file for BPAY Remittance
.
DIM100A   DIM       100
DIM100B   DIM       100
FLD20001  DIM       20
FLD20002  DIM       20
FLD20003  DIM       20
FLD20004  DIM       20
FLD20005  DIM       20
FLD20006  DIM       20
FLD20007  DIM       20
.
TMPINVA1   IFILE    SQL, FIXED=40, KEY="U1-8"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
TMPPRINV  DIM       8      8        1     Invoice No (to be printed) 
TMPPSPAR  DIM       31     31       9
.                   EOR             40
. ----- Program Variables -----
.
KIAMOUNT  FORM      8.2
KIPAYDAT  DIM       8
KIPAYTYP  DIM       1
KIPAYTPE  FORM      1
KISTTREF  DIM       20
KIHOSPNO  DIM       8
KICASHID  DIM       6
KICHQACC  DIM       15
KIHOSPID  DIM       3
KIREGCOD  DIM       3
KIWEBUID  DIM       10
KIFLNAME  DIM       80
KIFLALTD  DIM       80
KIFLDISP  DIM       80
KIWEBPRT  DIM       3
SUSPEND   FORM      1
SAVERECN  DIM       15
TMRCPTNO  FORM      12
TRANNUMB  FORM      5
IBALANCE  FORM      12.2
TRANTAMT  FORM      8.2
.
AMTERR    FORM      1
AMTERTXT  DIM       30
COMMTTXT  DIM       30
CDATEIS   DIM       8
CMDLINE   DIM       100
COLTXT    DIM       3
ERRDATA   DIM       40
ERRMESS   DIM       75
FORM8     FORM      8
FILESTAT  FORM      1
WARNTEXT  DIM       130[10]
WARNNO    FORM      5
F8P2      FORM      8.2
HRECPT    FORM      12
HSYS1     FORM      1
IBCNMHOS  FORM      1
INVCOUNT  FORM      5
OPAYNO    FORM      5
OPAYTOTL  FORM      8.2
RCCNURCP  DIM       3
RECDCNT   FORM      5
RECIN     FORM      5
REC00     FORM      5
REC03     FORM      5
PMIOK     FORM      1
RESPOK    FORM      1
ERRIN     FORM      5
PRTLINE   DIM       132
.
.
DIM1A     DIM       1
DIM1B     DIM       1
DIM2A     DIM       2
DIM2B     DIM       2
DIM3A     DIM       3
DIM3B     DIM       3
DIM5A     DIM       5
DIM5B     DIM       5
DIM6A     DIM       6
DIM6B     DIM       6
DIM7A     DIM       7
DIM8A     DIM       8
DIM10A    DIM       10
DIM10B    DIM       10
DIM11A    DIM       11
DIM12A    DIM       12
DIM15A    DIM       15
DIM15B    DIM       15
DIM15C    DIM       15
DIM20A    DIM       20
DIM20B    DIM       20
DIM30A    DIM       20
DIM80A    DIM       80
DONE      FORM      1
FILEDATE  DIM       12
INFLTOTL  FORM      12.2
INFLDATE  DIM       8
INFLSUFX  DIM       4
INFLHNAM  DIM       15
INFLDTOT  DIM       15
INVISTNO  DIM       8
INCHKDIG  DIM       2
INAMNTPD  FORM      12.2
INBANKID  DIM       5
INBNKRCP  DIM       20
INHOSNAM  DIM       40
ININVNUM  DIM       8
INOVERPD  FORM      8.2
.
FLDNO     DIM       2   
FNAMESHT  DIM       20
FNO       FORM      1
F12       FORM      12
F12P2     FORM      12.2
HEADDES1  DIM       25
HEADDES2  DIM       60
HEADDES3  DIM       90
LENGTH    FORM      3 
LENIN     FORM      3 
LENOUT    FORM      3 
HEADNO    FORM      1 
PAYTYPE   DIM       15
SAVSCODE  DIM       3
SAVSDESC  DIM       35
SAVSINCA  DIM       14
SAVSBNKA  DIM       14
SAVTINCA  DIM       14
SAVTBNKA  DIM       14
TMPFIL01  DIM       8
TMPFIL02  DIM       8
TMPFIL03  DIM       8
TMPFIL04  DIM       8
TXTFILE1  DIM       8
TXTFILE2  DIM       8
TMPPRTFL  DIM       8
REPRINT   FORM      5
.
. ----- Extra bits for Web Scheduler
.
DIM8      DIM       8
DRAWERT   DIM       6
USERID    DIM       10
VAR       DIM       127
.
. ----- Program Constants -----
.
HDRLIT    INIT      "HDR|"
RET       INIT      015                          * carriage return
ZERO4     INIT      "0000"
TMPDEF01  INIT      " 196 U1-12"                 * TMPFIL01
TMPDEF02  INIT      " 336 U9-20,21-23"           * TMPFIL02
TMPDEF03  INIT      " 467 U9-20,21-25"           * TMPFIL03
TMPDEF04  INIT      " 40 U1-8"                   * TMPFIL04
.
PRGNAM    INIT      "Upload BPay Remittance"
PRGID     INIT      "IBARCP79"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
          BRANCH    EXIT,ML9000
.
ML1000    CALL      OPTN0000                * Select Option          
          BRANCH    EXIT,ML9000
.
          CALL      GREG0000                * Retrieve & edit Register info
          BRANCH    EXIT,ML9000
.
          CALL      CCSV0000                * convert ".csv" to Piped txt file
          CALL      PRER0000                * print any Errors
          BRANCH    EXIT,ML9000
.
          CALL      OPEN0000                * Open files
          CALL      CTMP0000                * Create Temp files for Receipting
.
          CALL      PINP0000                * Process the input file
          BRANCH    EXIT,ML1000
.
          IF        OPTION = 2
            IF        ERRIN = 0
              CALL      PROC0000            * Process Temp files to RCP files
              CALL      PRTINV00            * print unbalanced Invoices
            ELSE
              PRINT     *N,*1,"Errors detected - file will not be uploaded ":
                        "to Receipting Files",*N
            ENDIF
            CALL      SETPRT00              * save printer info (ex RSHWEB02)
          ELSE
            PRINT     *N,*1,"Edit only processing complete"
          ENDIF
.
          PRINT     *N,*1,"*** End of Report ***"
          GOTO      ML1000
.
ML9000    MOVE      ZERO,EXIT
          CALL      KILL0000                * delete temp files
          MATCH     SP8,PGM
          GOTO      ML9999 IF EQUAL
.
          CHAIN     PGM                     * Chain back to appropriate menu
ML9999    SHUTDOWN  ""
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          MOVE      SP3,FLDNO 
          MOVE      SP3,COLTXT
          PACK      KIFLNAME,SP30,SP30
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"     
.
          DISPLAY   *P64:24,"rcpcidaf";
          OPEN      RCPCIDA1,"rcpcidaf"     
.
          DISPLAY   *P64:24,"rcpregaf";
          OPEN      RCPREGA1,"rcpregaf"     
          OPEN      RCPREGA3,"rcpregaf"     
.
          DISPLAY   *P64:24,"fmschqaf";
          OPEN      FMSCHQA1,"fmschqaf"     
.
          DISPLAY   *P64:24,"fmscsaaf";
          OPEN      FMSCSAA1,"fmscsaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"     
.
          DISPLAY   *P64:24,"pathospf";
          OPEN      PATHOSP1,"pathospf"     
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"     
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"     
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*79,CAPPRVNO
          READ      CONTROLF,TEN;*244,CHOSPNUM
          READ      CONTROLF,THIRTY6;*86,HSYS1
          READ      CONTROLF,HUND08;*109,RCCNURCP
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TMPFIL01
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TMPFIL02
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TMPFIL03
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TMPFIL04
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TMPPRTFL
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TXTFILE1
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TXTFILE2
.
          CALL      IBACLOCK
          PACK      CDATEIS,CCC,CYY,CMM,CDD
          REP       " 0",CDATEIS
.
INIT9000  MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*                         Select option to run upload                        *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
....      CALL      DISPHEAD                * Display screen header
          MOVE      ZERO,FNO         
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Edit BPay Remittance":
                          " only":
                    *P1:6,*V2LON," 2",*HOFF," = Upload BPay Remittance":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN1500,OPTN1500
          GOTO      OPTN1000 IF NOT EQUAL
.
OPTN1500  MOVE      ZERO,EXIT
          MOVE      "99",CLNO
          CALL      HEAD0000
.
OPTN2000  DISPLAY   *P1:3,*EF;
.
          DISPLAY   *P4:5,"Cashier Drawer            :":
                    *P4:6,"Cheque Account            :":
                    *P4:7,"Register                  :":
                    *P4:8,"Date of Payment           :":
                    *P4:9,"Expected Payment Amount   :":
                    *P4:10,"Statement Reference       :":     * Pay type = 4
                    *P55:10,"(for Direct Deposit)":
                    *P4:12,"Enter Path and File Name for ":
                    *P4:13,"Remittance File    : ",*V2LON,UNDLN30,UNDLN20,*HOFF:
                    *P4:14,"Frontend File name : ",*V2LON,UNDLN30,UNDLN20,*HOFF:
                    *P4:15,"User Id                   :":
                    *P4:16,"Frontend Web Printer      :";
.
OPTN3000  IF        OPTION = 2
            PRINT     *30,"Upload BPay Remittance File",*N
          ELSE
            PRINT     *30,"BPay Remittance File - Edit only",*N
          ENDIF
.
          MOVE      "5",CVERT
          MOVE      "32",CCOL
          CALL      KEYCSHID                * keyin Cashier Drawer
          BRANCH    EXIT,OPTN0000
          MOVE      RCPCASH,KICASHID        * Cashier Id.
          PRINT     *1,"Cashier Drawer            : ",KICASHID,SP10,RCPSUR
.
OPTN3400  MOVE      "6",CVERT
          MOVE      "32",CCOL
          CALL      KCHQFM00                * keyin Cheque Account
          BRANCH    EXIT,OPTN0000
          DISPLAY   *P45:CVERT,*V2LON,FMCHDES,*HOFF;
          MOVE      FMCHCHQ,KICHQACC        * Cheque Account No.
          PRINT     *1,"Cheque Account            : ",KICHQACC,SP1,FMCHDES
.
OPTN3600  MOVE      "7",CVERT              * keyin Register
          CALL      KEYCDEA
          BRANCH    EXIT,OPTN0000
          DISPLAY   *P32:7,*V2LON,REGCODE,SP4,REGDESC;
          MOVE      REGCODE,KIREGCOD
          PRINT     *1,"Register                  : ",KIREGCOD,SP10,SP3,REGDESC
.
.                                           * Payment Type 
...                                         * "1 = Cash          ":
...                                         * "2 = Cheque        ":
...                                         * "3 = Credit Card   ":
...                                         * "4 = Direct Deposit":
...                                         * "5 = Money Order   ":
...                                         * "6 = EFTPOS        ";
          MOVE      "4",KIPAYTYP
.
OPTN3800  MOVE      "8",CVERT
          MOVE      "32",CCOL
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE                 * keyin Date of Payment
          PACK      KIPAYDAT,CCENT,CYEAR,CMON,CDAY
          PRINT     *1,"Date of Payment           : ",CDAY,"/",CMON,"/":
                    CCENT,CYEAR
.
.
OPTN4200  MOVE      ZERO,F8P2               * keyin Payment Amount
          MOVE      "9",CVERT
          KEYIN     *P32:CVERT,F12P2;
          MOVE      F12P2,KIAMOUNT
          DISPLAY   *P36:CVERT,*V2LON,KIAMOUNT,*HOFF;
          PRINT     *1,"Payment Amount            : ",KIAMOUNT
.
          MOVE      SP20,KISTTREF
.
OPTN4400  MOVE      SP20,DIM20A             * keyin Statement Reference
          MOVE      "10",CVERT
          KEYIN     *P32:CVERT,DIM20A;
          DISPLAY   *P32:CVERT,*V2LON,DIM20A,*HOFF;
          MOVE      DIM20A,KISTTREF
          PRINT     *1,"Statement Reference       : ",KISTTREF
.
OPTN4600  MOVE      "13",CVERT
          DISPLAY   *P4:CVERT,"Remittance File    : ";
          CALL      KASCI000                * keyin file name
          BRANCH    EXIT,OPTN0000
          MOVE      KIFLNAME,KIFLDISP
.
.
OPTN4800  MOVE      "14",CVERT              * file name from Front End
          DISPLAY   *P4:CVERT,"Frontend File name : ";
          CALL      KASCY000                * keyin Front-end file name
          PRINT     *1,"Remittance File           : ",KIFLALTD
.
OPTN5000  MOVE      "15",CVERT              * keyin web user Id.
          MOVE      SP10,DIM10A
          KEYIN     *P32:CVERT,DIM10A;
          MOVE      DIM10A,KIWEBUID
          DISPLAY   *P32:CVERT,*V2LON,KIWEBUID,*HOFF;
          PRINT     *1,"Web User Id.              : ",KIWEBUID
          MOVE      KIWEBUID,USERID
.
OPTN5200  MOVE      "16",CVERT              * keyin web user Id.
          MOVE      SP10,KIWEBPRT
          KEYIN     *P32:CVERT,KIWEBPRT;
          DISPLAY   *P32:CVERT,*V2LON,KIWEBPRT,*HOFF;
          PRINT     *1,"Web Printer Code          : ",KIWEBPRT
.
          UNPACK    SP30,KIHOSPID,KIHOSPNO
          IF        IBCNMHOS = 1
            DISPLAY   *P4:17,"Hospital ID.              :";
            MOVE      "17",CVERT
            MOVE      "32",CCOL
            CALL      KYHOSPID                * keyin Hospital Id.   
            BRANCH    EXIT,OPTN0000
.
            PACK      KIHOSPID,PTHSHOSP,SP10  * Hospital Id.
            SQUEEZE   PTHSAPPR
            PACK      KIHOSPNO,PTHSAPPR,SP10  * Hospital Number
            PRINT     *1,"Hospital ID.              : ",KIHOSPID,SP10:
                      SP3,PTHSNAME
          ENDIF
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,OPTN9000,OPTN0000,OPTN9500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 KYHOSPID                                   *
.*                            Key in Hospital Id.                             *
.******************************************************************************
KYHOSPID  MOVE      ZERO,EXIT
          MOVE      SP6,DIM3A 
          KEYIN     *PCCOL:CVERT,*V2LON,DIM3A;
.
          COMPARE   ONE,IBCNMHOS
          GOTO      KYHOS999 IF NOT EQUAL     * Not using multi hospital
.
          PACK      KEY3,DIM3A,SP10
KYHOS200  MATCH     SP3,KEY3
          GOTO      KYHOS950 IF EQUAL
.
          MATCH     QUEST,KEY3
          IF        @EQUAL
            CALL      GETSCR00
            DISPLAY   *P1:4,*EF,*P35:4,"Hospital Details";
            MOVE      "6",EVERT
            PACK      KEY3,SP70
            CALL      RSPTHSP1
KYHOS210    CALL      RKPTHSP1
            BRANCH    OVRCD,KYHOS290
            DISPLAY   *P1:EVERT,PTHSHOSP,SP4,PTHSNAME;
            ADD       ONE,EVERT
            IF        EVERT < 22
              GOTO      KYHOS210
            ENDIF
.
KYHOS290    DISPLAY   *P1:24,*EL,"Hospital ID : ",UNDLN3;
            KEYIN     *P15:24,*V2LON,DIM3A;
            PACK      KEY3,DIM3A,SP10
            CALL      PUTSCR00
          ENDIF
.
          MATCH     SP3,KEY3
          GOTO      KYHOS950 IF EQUAL
.
KYHOS300  UNPACK    SP70,PTHSHOSP,PTHSNAME,PTHSAPPR
          MOVE      "Unknown",PTHSNAME
          CALL      RDPTHSP1
          MATCH     SP10,PTHSAPPR
          IF        !@EQUAL
            SQUEEZE   PTHSAPPR
            PACK      KIHOSPNO,PTHSAPPR,SP10
          ENDIF
          DISPLAY   *PCCOL:CVERT,*V2LON,KEY3,SP4,PTHSNAME
          GOTO      KYHOS999
.
KYHOS950  MOVE      ONE,EXIT
.
KYHOS999  RETURN
+
.******************************************************************************
.*                                 KEYCSHID                                   *
.*                            Key in Cashier Id.                              *
.******************************************************************************
KEYCSHID  MOVE      ZERO,EXIT   
          MOVE      SP6,DIM6A 
          MOVE      CCOL,ECOL
          MOVE      CVERT,EVERT
          KEYIN     *PECOL:EVERT,*V2LON,DIM6A;
.
          PACK      KEY6,DIM6A,SP10
KEYCS200  MATCH     SP6,KEY6     
          GOTO      KEYCS950 IF EQUAL
.
          MATCH     QUEST,KEY6
          IF        @EQUAL
            CALL      GETSCR00
            CALL      RCPCIDDS              * Display all cashiers
            DISPLAY   *P1:24,*EL,"Cashier ID : ",UNDLN6;
            KEYIN     *P14:24,*V2LON,DIM6A;
            PACK      KEY6,DIM6A,SP10
            CALL      PUTSCR00
          ENDIF
.
          MATCH     SP6,KEY6
          GOTO      KEYCS950 IF EQUAL
          MOVE      KEY6,DIM6A
.
KEYCS300  UNPACK    SP70,RCPCASH,RCPPASS,RCPSUR,RCPGIVEN,RCCIHOSP,RCCICHQA
          MOVE      ZERO,RCPSECUR
          PACK      KEY6,DIM6A,SP10
          CALL      RDCIDA1
          BRANCH    OVRCD,KEYCS950
          DISPLAY   *PECOL:EVERT,*V2LON,RCPCASH,SP1,RCPGIVEN;
          GOTO      KEYCS999
.
KEYCS950  MOVE      ONE,EXIT  
.
KEYCS999  RETURN
+
.******************************************************************************
.*                                 KEYCDEA                                    *
.*                           KEY IN REGISTER CODE                             *
.******************************************************************************
KEYCDEA   MOVE      ZERO,EXIT
          MOVE      CVERT,EVERT
          DISPLAY   *P32:CVERT,*EL;
          MOVE      "32",ECOL
          MOVE      ZERO,CKYIMODE
          CALL      RCPREGKY
          BRANCH    EXIT,KEYCDEA8,KEYCDEA8
.
          MOVE      REGCODE,KEY3
          CALL      RDREGA1
          COMPARE   ONE,OVRCD
          GOTO      KEYCDEA5 IF EQUAL
          GOTO      KEYCDEA9
.
KEYCDEA5  DISPLAY   *P1:23,"Register Code Invalid - ";
          CALL      HITENTER
          GOTO      KEYCDEA
.
KEYCDEA8  MOVE      ONE,EXIT
.
KEYCDEA9  RETURN
+
.******************************************************************************
.*                                 KASCI000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASCI000  MOVE      ZERO,EXIT
          MOVE      ZERO,FILESTAT
.
KASCI100  KEYIN     *P25:CVERT,*V2LON,*EL,*RV,KIFLNAME;
.
          PACK      KIFLNAME,KIFLNAME,SP20,SP20,SP20
.
          MATCH     SP70,KIFLNAME            * anything entered ?
          GOTO      KASCI950 IF EQUAL       * no
          CMATCH    "\",KIFLNAME             * "\" entered ?
          GOTO      KASCI950 IF EQUAL       * no
.
          SCAN      ".csv",KIFLNAME
          IF        !@EQUAL
            RESET     KIFLNAME
            SCAN      ".",KIFLNAME
            IF        @EQUAL
              MOVE      KIFLNAME,INFLSUFX
              MOVE      TWO,FILESTAT
            ELSE
              SQUEEZE   KIFLNAME
              ENDSET    KIFLNAME
              APPEND    ".csv",KIFLNAME      * add ".csv" if not there 
              RESET     KIFLNAME
              DISPLAY   *P25:CVERT,*V2LON,*EL,KIFLNAME;
            ENDIF
          ENDIF
          RESET     KIFLNAME
.
          GOTO      KASCI999
.
KASCI950  MOVE      ONE,EXIT
.
KASCI999  RETURN
+
.******************************************************************************
.*                                 KASCY000                                   *
.*                            Keyin Front-end file name                       *
.******************************************************************************
KASCY000  MOVE      ZERO,EXIT
          PACK      KIFLALTD,SP30,SP30
.
KASCY100  KEYIN     *P25:CVERT,*V2LON,*EL,*RV,KIFLALTD;
.
          MATCH     SP8,KIFLALTD
          IF        @EQUAL | @EOS
            PACK      KIFLALTD,KIFLDISP,SP20,SP20,SP20
          ELSE
            PACK      KIFLALTD,KIFLALTD,SP20,SP20,SP20
          ENDIF
.
          MOVE      ZERO,DONE               * remove directory data
          REPEAT
            SCAN      "/",KIFLALTD
            IF        @EQUAL
              BUMP      KIFLALTD
            ELSE
              MOVE      ONE,DONE
            ENDIF
          UNTIL     DONE = 1
          MOVE      KIFLALTD,DIM80A         * move what is left of KIFLALTD
          MOVE      DIM80A,KIFLALTD
.
          DISPLAY   *P25:CVERT,*V2LON,*EL,KIFLALTD
.
KASCY999  RETURN
.******************************************************************************
.*                                  CCSV0000              Called by: ML0000   *
.*                  Convert ".csv" file to a piped ".txt"                     *
.******************************************************************************
CCSV0000  MOVE      ZERO,EXIT
.                                           * change the file name to ".txt"
          RESET     KIFLNAME
          SQUEEZE   KIFLNAME
          CLEAR     CMDLINE
          APPEND    "cp ",CMDLINE
          APPEND    KIFLNAME,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    TXTFILE1,CMDLINE        * "t79in1.." (+ Port)
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MATCH     "0",TASKID
          GOTO      CCSV1000 IF EQUAL
.
          PRINT     *3,"Error occured renaming the '.csv' Upoad file"
          MOVE      ONE,EXIT
          GOTO      CCSV9999
.                                           * see if file exists
CCSV1000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CSVFILE1,TXTFILE1       * open file  "t79in1pp"
          TRAPCLR   IO
.
          IF        OVRCD = 0
            MOVE      CVERT,F2
            DISPLAY   *P70:F2,*HOFF,"file found";
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Upload File not found.  ";
            MOVE      ONE,FILESTAT
          ENDIF
.
.                                           * clean up "t79in1pp.txt" and output
.                                           * "t79in2pp" (change to Pipes "|"s)
CCSV2000  MOVE      ZERO,EXIT
.
          PREP      CSVFILE2,TXTFILE2        * change its name to "rcp79txt" ?
          MOVE      ZERO,RECDCNT
.
CCSV3000  READ      CSVFILE1,SEQ;DIM100A
          GOTO      CCSV9000 IF OVER        * end of file
.
          SCAN      RET,DIM100A             * find any "carriage return"
          IF        @EQUAL
            CMOVE     SP1,DIM100A
            RESET     DIM100A
          ENDIF
.
          SQUEEZE   DIM100A                 * remove any inbedded blanks
          PACK      DIM100B,DIM100A,SP70
          MATCH     SP10,DIM100B            * get rid of blank lines
          GOTO      CCSV3000 IF EQUAL
.
.                                           * remove unwanted headers
....      MATCH     "Site,Account",DIM100B                            *D-215885
          MATCH     "Site,Invoice",DIM100B                            *I-215885
          GOTO      CCSV3000 IF EQUAL
          CMATCH     "##",DIM100B
          GOTO      CCSV3000 IF EQUAL
          MATCH     ",,,,",DIM100B
          GOTO      CCSV3000 IF EQUAL
.
.                                           * select the wanted headers
CCSV3500  MATCH     "File Date",DIM100B
          GOTO      CCSV5000 IF EQUAL
          MATCH     "FileDate",DIM100B
          GOTO      CCSV5000 IF EQUAL
          MATCH     "Site",DIM100B
          GOTO      CCSV5000 IF EQUAL
          MATCH     "TOTAL",DIM100B
          GOTO      CCSV5000 IF EQUAL
.            
.                                           * now clean up data records
CCSV4000  RESET     DIM100A
          SCAN      "$",DIM100A
          GOTO      CCSV7000 IF NOT EQUAL
.
          CMOVE     SP1,DIM100A
          MOVE      ZERO,F1
          REPEAT
            BUMP      DIM100A
            CMATCH    ".",DIM100A
            GOTO      CCSV7000 IF EQUAL
            CMATCH    ",",DIM100A
            IF        @EQUAL
              CMOVE     SP1,DIM100A
            ENDIF
            ADD       ONE,F1
          UNTIL     F1 = 4
          GOTO      CCSV7000
.
.
CCSV5000  PACK      DIM100A,HDRLIT,DIM100B  * clean up header records
          REP       "#" ",DIM100A
          SCAN      "$",DIM100A             * fix Total amount on Total record
          IF        @EQUAL
            CMOVE     SP1,DIM100A
            MOVE      ZERO,F1
            REPEAT
              BUMP      DIM100A
              CMATCH    ".",DIM100A         * find end of dollar amount - finish
              GOTO      CCSV7000 IF EQUAL
              CMATCH    ",",DIM100A         * find commas in 1,200,099.00
              IF        @EQUAL
                CMOVE     SP1,DIM100A
              ENDIF
              ADD       ONE,F1
            UNTIL     F1 = 4
            RESET     DIM100A
          ENDIF
.                                           * changes made -  write piped file
CCSV7000  RESET     DIM100A
          SQUEEZE   DIM100A
          REP       ",|",DIM100A
          WRITE     CSVFILE2,SEQ;*+,DIM100A
          GOTO      CCSV3000
.
CCSV9000  MOVE      ZERO,EXIT
          CLOSE     CSVFILE1,DELETE
          CLOSE     CSVFILE2
.
CCSV9999  RETURN
+
.******************************************************************************
.*                                  PRER0000              Called by: ML0000   *
.*                        Print Errors from Key-in data                       *
.******************************************************************************
PRER0000  MOVE      ZERO,EXIT
.
          IF        ERRIN = 1
            PRINT     *3,"Register 'System Code' is '",REGISTRG,"' - it must ":
                      "be a Register code with a System Code of 1, 2, 5 or 99":
                      " - *** Error ***"
          ENDIF
.
          IF        FILESTAT = 1
            PRINT     *N,*3,"Upload file could not be found or is not ":
                      " a '.csv' file  - *** Error ***"
            ADD       ONE,ERRIN
            MOVE      ONE,EXIT
          ENDIF
.
          IF        FILESTAT = 2
            PRINT     *3,"File name has a sufix of '",INFLSUFX,"' - it must ":
                      "be '.csv' - *** Error ***"
            ADD       ONE,ERRIN
            MOVE      ONE,EXIT
          ENDIF
          IF        FILESTAT = 3
            PRINT     *N,*3,"No file name was provided - *** Error ***"
            ADD       ONE,ERRIN
            MOVE      ONE,EXIT
          ENDIF
          PRINT       *N
.
PRER9999  RETURN
+
.******************************************************************************
.*                                  CTMP0000              Called by: ML0000   *
.*                        Create Temp Receipting files                        *
.******************************************************************************
CTMP0000  MOVE      ZERO,EXIT
          CALL      KILL0000
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFIL01,CMDLINE        * "t79bnk" + PORT
          APPEND    TMPDEF01,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPBNKA1,TMPFIL01
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFIL02,CMDLINE
          APPEND    TMPDEF02,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPBDTA1,TMPFIL02
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFIL03,CMDLINE
          APPEND    TMPDEF03,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPDTEA1,TMPFIL03
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFIL04,CMDLINE
          APPEND    TMPDEF04,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPINVA1,TMPFIL04
.
          CALL      CLER0000                * empty all files if Oracle
.
          PREP      PRTFILE1,TMPPRTFL
.
CTMP9999  RETURN
+
.******************************************************************************
.*                             KILL0000                  Called by:           *
.*                      erase temporary files                                 *
.******************************************************************************
KILL0000  CLOSE     TMPBNKA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFIL01,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TMPBDTA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFIL02,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TMPDTEA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFIL03,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TMPINVA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFIL04,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PREP      PRTFILE1,TMPPRTFL
          CLOSE     PRTFILE1,DELETE
.
          PREP      CSVFILE2,TXTFILE2
          CLOSE     CSVFILE2,DELETE
.
KILL9999  RETURN
+
.******************************************************************************
.*                             CLER0000                 Called by:            *
.*               close and erase the temporary file                           *
.******************************************************************************
.
CLER0000  PACK      KEY12,SP20
          CALL      RSTMBNK1
          CALL      RKTMBNK1
          BRANCH    OVRCD,CLER1000
.
          PACK      KEY12,TMBNRECN,TMBNTDAT
          CALL      DETMBNK1
.
          GOTO      CLER0000
.
CLER1000  PACK      KEY15,SP20
          CALL      RSTMBDT1
          CALL      RKTMBDT1
          BRANCH    OVRCD,CLER2000
.
          PACK      KEY15,TMBDRECN,TMBDUNIQ
          CALL      DETMBDT1
.
          GOTO      CLER1000
.
CLER2000  PACK      KEY17,SP20
          CALL      RSTMDTE1
          CALL      RKTMDTE1
          BRANCH    OVRCD,CLER9000
.
          PACK      KEY17,TMDTRECN,TMDTTCNT
          CALL      DETMDTE1
.
          GOTO      CLER2000
.
CLER9000  CALL      CLTMP400
.
CLER9999  RETURN
+
.******************************************************************************
.*                                  CLTMP400              Called by: ML0000   *
.*                            Clear Temp file 4                              *
.******************************************************************************
CLTMP400  MOVE      ZERO,EXIT
CLTMP410  PACK      KEY8,SP20
          CALL      RSTMINV1
          CALL      RKTMINV1
          BRANCH    OVRCD,CLTMP499
.
          PACK      KEY8,TMPPRINV,TMPPSPAR
          CALL      DETMINV1
.
          GOTO      CLTMP410
CLTMP499  RETURN
+
.******************************************************************************
.*                                  OPEN0000              Called by: ML0000   *
.*                                 Open Files                                 *
.******************************************************************************
OPEN0000  MOVE      ZERO,EXIT
.                                           * open BPay upload text file 
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CSVHL7FL,TXTFILE2        * open file
          TRAPCLR   IO
.
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"BPay upload file not found ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
.
. ----------------------------------------- * open IBA files -------------
.
          DISPLAY   *P64:24,"fmscsaaf";
          OPEN      FMSCSAA1,"fmscsaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"     
          OPEN      PATMX1A1,"patmx1af"     
.
          DISPLAY   *P64:24,"patre1af";
          OPEN      PATRE1A1,"patre1af"     
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"     
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"     
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"     
.
          DISPLAY   *P64:24,"rcpbnkaf";
          OPEN      RCPBNKA1,"rcpbnkaf"     
.
          DISPLAY   *P64:24,"rcpbdtaf";
          OPEN      RCPBDTA1,"rcpbdtaf"     
.
          DISPLAY   *P64:24,"rcpdteaf";
          OPEN      RCPDTEA1,"rcpdteaf"     
.
.
OPEN9999  RETURN
.******************************************************************************
.*                                  GREG0000                                  *
.*                         Retrieve & edit Register info                      *
.******************************************************************************
GREG0000  MOVE      ZERO,EXIT
          MOVE      ZERO,ERRIN
          MOVE      ZERO,WARNNO  
.
          MOVE      RCCNURCP,KEY3           * validate Suspense register code
          CALL      RDREGA1
          IF        OVRCD=1
            MOVE      SP70,RCCNURCP
          ENDIF
.
          PRINT     *N
          CALL      GTACCT00                * read Register records
          BRANCH    EXIT,GREG9999
.
          IF        REGIBACD=1 | REGIBACD=2 | REGIBACD=5 | REGIBACD=99
            GOTO      GREG9999
          ENDIF
          ADD       ONE,ERRIN
.
GREG9999  RETURN
+
.******************************************************************************
.*                                  PINP0000                                  *
.*                         Process The Input Text File                        *
.******************************************************************************
PINP0000  MOVE      ZERO,EXIT
          MOVE      ZERO,RECIN
          MOVE      ZERO,REC03
          MOVE      ZERO,HEADNO
          MOVE      ZERO,REPRINT
          MOVE      ZERO,OPAYNO
          MOVE      ZERO,OPAYTOTL
          MOVE      ZERO,TMRCPTNO           * File Header count
.
          MOVE      "Fld",COLTXT
          MOVE      "Remittance upload file : ",HEADDES1
          PACK      HEADDES2,KIFLALTD,SP30
          RESET     HEADDES2
          STRIP     HEADDES2
.
          CALL      CLTMP400                * clear Temp file 4
.
. ----- Read the input data file into temporary variables -----
.
.                                                   *removed FLD20007 *C-215885
PINP1000  READ      CSVHL7FL,SEQ;FLD20001,FLD20002,FLD20003,FLD20004:
                                 FLD20005,FLD20006
          GOTO      PINP5000 IF OVER
.
          ADD       ONE,RECIN
          MATCH     "HDR",FLD20001
          IF        @EQUAL
            CALL      PIHDR000              * process Header data
            GOTO      PINP1000
          ENDIF
.
          PACK      DIM80A,FLD20001,FLD20002,FLD20003,FLD20004,SP70
          MATCH     SP20,DIM80A
          GOTO      PINP1000 IF EQUAL       * get rid of blank entries

          CALL      PIINV000                * process Invoice data
          GOTO      PINP1000 
.
. ----- Finished -----
.
PINP5000  MOVE      ZERO,EXIT
          CLOSE     PRTFILE1
.
          IF        OPAYNO > 5
            ADD       ONE,WARNNO
            CLEAR     WARNTEXT[WARNNO]
            ASSIGN    (REC00/3),F5          * 30%
            IF        OPAYNO > F5 | OPAYTOTL = 1000
              ADD     ONE,ERRIN
              APPEND    "*** Error ***   ",WARNTEXT[WARNNO]
            ELSE
              APPEND    "*** Warning *** ",WARNTEXT[WARNNO]
            ENDIF
            APPEND    OPAYNO,WARNTEXT[WARNNO]
            APPEND    " Over payments have been detected ",WARNTEXT[WARNNO]
            APPEND    "(with a value of $ ",WARNTEXT[WARNNO]
            MOVE      OPAYTOTL,DIM15A
            SQUEEZE   DIM15A
            APPEND    DIM15A,WARNTEXT[WARNNO]
            APPEND    ")  ",WARNTEXT[WARNNO]
            RESET     WARNTEXT[WARNNO]
          ENDIF
.
          IF        WARNNO > 0
            PRINT     *N,*N                 * space 2 lines
            MOVE      ONE,F2
            REPEAT
              PRINT     *1,WARNTEXT[F2]
              ADD       ONE,F2
            UNTIL     F2 > WARNNO
          ENDIF
.
          CALL      CHKT0000             * Check total amount
.
          PRINT     *N,*1,"BPAY Transactions processed   : ",REC00,*N:
                       *1,"Errors                        : ",ERRIN,*N:
                       *1,"Warnings                      : ",WARNNO
.
          IF        ERRIN > 0 & OPTION = 2
            PRINT     *N,*30," *** file will not be uploaded ***"
          ENDIF
          PRINT     *N,*N
.
PINP6000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,INVCOUNT
          CALL      HED20000
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CLOSE     PRTFILE1
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRTFILE1,TMPPRTFL
          TRAPCLR   IO
          BRANCH    OVRCD,PINP7000
.
PINP6200  READ      PRTFILE1,SEQ;PRTLINE
          GOTO      PINP7000 IF OVER
.
          ADD       ONE,INVCOUNT
          PRINT     *1,PRTLINE
          ADD       ONE,CLNO
          COMPARE   "56",CLNO
          CALL      HED20000 IF NOT LESS    * Print the report header
          GOTO      PINP6200
.
PINP7000  CLOSE     PRTFILE1
          PRINT     *1,"End of File"
          COMPARE   "45",CLNO
          CALL      HED20000 IF NOT LESS    * Print the report header
.
          MOVE      INVCOUNT,DIM5A
          SQUEEZE   DIM5A
          PRINT     *N,*20,"Invoices processed        : ",DIM5A
.
          IF        ERRIN > 0
            PRINT     *N,*3,"Errors have been detected ";
            IF        OPTION = 2
              PRINT   *29,"- *** file will not be uploaded ***"
            ELSE
              PRINT   *29,"in Edit only run"
            ENDIF
            GOTO    PINP9500
          ENDIF
.
          GOTO      PINP9999
.
PINP9500  MOVE      ONE,EXIT
.
PINP9999  RETURN
+
.******************************************************************************
.         Check total values
.******************************************************************************
CHKT0000  MOVE      TRANTAMT,DIM15A
          SQUEEZE   DIM15A
          MOVE      KIAMOUNT,DIM15B       * Keyin (expected) file total
          SQUEEZE   DIM15B
          MOVE      SP70,DIM15C
          MOVE      INFLTOTL,DIM15C
          SQUEEZE   DIM15C
          PRINT     *N,*1,"BPAY Remittance Total     : $ ",DIM15A:
                    *N,*1,"Expected File Total       : $ ",DIM15B,SP2;
.
          COMPARE   TRANTAMT,KIAMOUNT     * Remittance total vs Expected total
          IF        !@EQUAL
            PRINT    *56,"does not match 'BPAY Remittance' Total - ":
                      "*** Error ***"
            ADD     ONE,ERRIN
          ENDIF
.
          PRINT     *1,"BPAY Header Total         : $ ",DIM15C,SP2;
.
          COMPARE   TRANTAMT,INFLTOTL     * BPAY Header
          IF        !@EQUAL
            PRINT    *56,"does not match 'BPAY Remittance' Total - ":
                      "*** Error ***"
            ADD     ONE,ERRIN
          ENDIF
.
          COMPARE   KIAMOUNT,INFLTOTL
          IF        !@EQUAL
            PRINT     *56,"does not match the 'Expected' Total - ":
                      "*** Error ***"
            ADD     ONE,ERRIN
          ENDIF
          PRINT     *N;
CHKT9999  RETURN
.
.******************************************************************************
.*                                  PIHDR000                                  *
.*                            Process "01" Record                             *
.******************************************************************************
PIHDR000  MOVE      ZERO,EXIT
          MOVE      ZERO,TRANNUMB           * Transaction count for Invoices
          MOVE      ZERO,TRANTAMT           * Total Amount for all Remittances
.
          MATCH     "FileDate",FLD20002     * 1st Header record
          IF        @EQUAL
            MOVE      FLD20003,KEY10
            SQUEEZE   FLD20003
            MOVELPTR  FLD20003,F2
            IF        F2 < 10
              PACK      KEY10,SP1,FLD20003
            ENDIF
            MOVE      KEY10,FILEDATE
            REP       " 0",KEY10
            REP       "/ ",KEY10
            SQUEEZE   KEY10
            UNPACK    KEY10,CDAY,CMON,CYEAR,CCENT
            PACK      INFLDATE,CCENT,CYEAR,CMON,CDAY
            GOTO      PIHDR999
          ENDIF
.
          MATCH     "Site",FLD20002         * 2nd Header record
          IF        @EQUAL
            PACK      INFLHNAM,FLD20003,SP20
            GOTO      PIHDR999
          ENDIF
.
          MATCH     "TOTAL",FLD20002    
          GOTO      PIHDR999 IF NOT EQUAL   * bypass unknown header records
.
          MOVE      ZERO,INFLTOTL
          MOVE      FLD20006,INFLTOTL       * TOTAL Header record
.
.
PIHDR100  CLEAR     HEADDES3
          APPEND    "Date : ",HEADDES3
          APPEND    FILEDATE,HEADDES3
          APPEND    "      Site : ",HEADDES3
          APPEND    INFLHNAM,HEADDES3
          APPEND    "  Total : $",HEADDES3
          APPEND    INFLTOTL,HEADDES3
          RESET     HEADDES3
          MOVE      "99",CLNO
          CALL      HED10000
.                                           * Edit for other Header errors
          ADD       ONE,HEADNO
          IF        HEADNO > 1
            CLEAR     ERRMESS
            APPEND    "A second BPay Remittance header ",ERRMESS
            APPEND    "has been detected -",ERRMESS
            APPEND    "' - *** Error ***",ERRMESS
            ADD       ONE,ERRIN             * "process will be halted"
            CALL      PRNT0000
            MOVE      ONE,EXIT
          ENDIF
.
          MOVE      ZERO,EXIT
          CALL      CLTMPBNK
          CALL      CLTMPBDT
.
.         set up record for tmpbnkaf
.
          ADD       ONE,TMRCPTNO            * dummy Receipt No.
          MOVE      TMRCPTNO,TMBNRECN
          MOVE      KIPAYDAT,TMBNTDAT
          MOVE      INFLTOTL,TMBNTOTP
          MOVE      KICASHID,TMBNCDRW
          IF        IBCNMHOS = 1
            MOVE      KIHOSPID,TMBNMHOS
          ENDIF
          MOVE      "0",TMBNTTYP            * "0"=Patient "1"=H/F   
          MOVE      "BPay",TMBNRCOD
          MOVE      "0",TMBNSTAT
          MOVE      KICHQACC,TMBNCHQA       * ???? not needed ?????
.
          PACK      TMBNCDAT,CCC,CYY,CMM,CDD,SP10
          REP       " 0",TMBNCDAT
          CLOCK     TIME,TMBNCTIM
          MOVE      KIWEBUID,TMBNCUID
.
.                                           * write the record
          PACK      KEY12,TMBNRECN
          CALL      RATMBNK1
          IF        OVRCD = 1
            CALL      WRTMBNK1
          ENDIF
.
.         set up record for tmpbdtaf
.                                           * set up Receipting Bank Detail Recd
          MOVE      ZERO,EXIT
          MOVE      TMBNTDAT,TMBDTDAT
          MOVE      TMBNRECN,TMBDRECN
          MOVE      "  1",TMBDUNIQ
          MOVE      TMBNMHOS,TMBDMHOS
          MOVE      TMBNMDHS,TMBDMDHS
          MOVE      TMBNTOTP,TMBDPAMT
.
          MOVE      SP70,TMBDPCOD
          CALL      BPYM0000            * Get payment code
          MOVE      KISTTREF,TMBDSTRF
.
          MOVE      TMBNCDAT,TMBDCDAT
          MOVE      TMBNCTIM,TMBDCTIM
          MOVE      TMBNCUID,TMBDCUID
.
          PACK      KEY15,TMBDRECN,TMBDUNIQ,SP70
          CALL      WRTMBDT1
.
PIHDR999  RETURN
+
.*****************************************************************************
.         Get payment type for BPAY/Direct Deposit/Cash
.*****************************************************************************
BPYM0000  MOVE      "7",FORM2                * 7 for BPAY
          MOVE      ZERO,F1
.
BPYM1000  MOVE      "Py",KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
BPYM2000  CALL      RDKCODE1
          BRANCH    OVRCD,BPYM6000
.
          MATCH     "Py",TCODE
          GOTO      BPYM6000 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      BPYM2000 IF EQUAL        * Inactive
.
          MOVE      ZERO,F2
          SQUEEZE   PTCDNHCP
          MOVE      PTCDNHCP,F2
.
          COMPARE   FORM2,F2                 * Check payment type
          GOTO      BPYM2000 IF NOT EQUAL
.
          MOVE      ACODE,TMBDPCOD           * Payment code for BPAY
          MOVE      F2,F1
          MOVE      F1,KEY1
          MOVE      KEY1,TMBDPTYP            * Payment Type 7=BPAY 4=DirectDep
          GOTO      BPYM9999
.
BPYM6000  ADD       ONE,F1
          BRANCH    F1,BPYM6200,BPYM6400
          GOTO      BPYM8000
.
BPYM6200  MOVE      "4",FORM2                * try Direct Deposit
          GOTO      BPYM1000
.
BPYM6400  MOVE      "1",FORM2                * default to Cash
          GOTO      BPYM1000
.
BPYM8000  MOVE      SP70,TMBDPAYD
          MOVE      "1",TMBDPTYP             * default to cash payment
BPYM9999  RETURN
+
.******************************************************************************
.*                                  PIINV000                                  *
.*                        Process Payments by Invoice                         *
.******************************************************************************
PIINV000  MOVE      ZERO,EXIT
          MOVE      ZERO,SUSPEND
          MOVE      ZERO,AMTERR
.         
          ADD       ONE,REC00
.           
          PACK      ERRDATA,FLD20001,SP1,FLD20002,SP1,FLD20003,SP1:
                            FLD20004,SP1,FLD20005,SP1,FLD20006,SP70
.
          CALL      CLPATINV                * clear Invoice fields
          CALL      CLPATMAS
          CALL      CLPATVIS
          CALL      CLPMSVX1
          MOVE      ZERO,IBALANCE
.
          MOVE      " 2",FLDNO
          MOVE      SP70,ININVNUM
          MOVE      ZERO,FORM8
          SQUEEZE   FLD20002
          MOVE      FLD20002,FORM8
          IF        FORM8<>0
            MOVE      FORM8,ININVNUM
          ENDIF
.
          REP       "#" $ , ",FLD20004
          MOVE      ZERO,INAMNTPD
          MOVE      FLD20004,INAMNTPD
.
          MOVE      FLD20005,INBANKID       * Fld 5 - Bank ID         *C-215885
          MOVE      FLD20006,INBNKRCP       * Fld 6 - Bank Receipt No *C-215885
.
          MOVE      ZERO,RESPOK
          MOVE      ZERO,PMIOK
.
          MOVE      SP8,DPINVADM
          MOVE      SP8,PINVUR
          MOVE      ZERO,AMTERR
          PACK      AMTERTXT,SP30
          PACK      COMMTTXT,SP30
.
          PACK      KEY8,ININVNUM,SP8
          CALL      RDPTINV1
          IF        OVRCD = 1
            CLEAR     ERRMESS
            APPEND    "Invoice No. '",ERRMESS
            APPEND    ININVNUM,ERRMESS
            APPEND    "' not found - *** Error ***      ",ERRMESS
            ADD       ONE,ERRIN
            MOVE      "** Invoice No. error",COMMTTXT
.
            CALL      PRNT0000
            GOTO      PIINV500              * bypass Master & Admission
          ELSE
            MATCH     "1",PTINCRST
            IF        @EQUAL
              MOVE      ONE,SUSPEND
            ENDIF
          ENDIF
.                                           * calc. outstanding Inv. balance
          MOVE      PINVAMT,IBALANCE        * Invoice Amount    
          ADD       PINVPATA,IBALANCE       * minus Amount paid by Patient
          ADD       PINVHFDA,IBALANCE       * minus Amount paid by Health Fund
          ADD       PINVOTHA,IBALANCE       * minus Other Amounts paid
          ADD       PINVJOUR,IBALANCE       * plus Journals
          ADD       PTINGSTJ,IBALANCE       * plus GST Adjustments
          ADD       PTINCRTT,IBALANCE       * Credit Note
.
          PACK      KEY8,PINVUR
          CALL      RDMAST1                 * get PMI Master
          IF        OVRCD = 1
            CLEAR     ERRMESS
            APPEND    "PMI Master for UR '",ERRMESS
            APPEND    PINVUR,ERRMESS
            APPEND    "' not found - *** Error ***      ",ERRMESS
            ADD       ONE,ERRIN
            MOVE      "** Master file error",COMMTTXT
            CALL      PRNT0000
            GOTO      PIINV500
          ENDIF
          MOVE      ONE,PMIOK
.
.                                           * get Responsible Person recd
          MOVE      PSNAME,DIM20B
          MOVE      ZERO,RESPOK
          PACK      KEY8,DPINVADM,SP8
          CALL      RDRESP1
          IF        OVRCD = 0
            MOVE      ONE,RESPOK
            PACK      DIM20B,PKNAME
          ENDIF
.
PIINV300  PACK      KEY8,DPINVADM,SP8
          CALL      RDPTVIS1                * get Visit record
          IF        OVRCD = 1
            CLEAR     ERRMESS
            APPEND    "Visit No. '",ERRMESS
            APPEND    DPINVADM,ERRMESS
            APPEND    "' not found - *** Error ***      ",ERRMESS
            MOVE      "** Visit No. error",COMMTTXT
            ADD       ONE,ERRIN
            CALL      PRNT0000
          ENDIF
.                                           * check Amount Paid vs Invoice total
PIINV400  COMPARE   IBALANCE,INAMNTPD
          GOTO      PIINV600 IF EQUAL
.
          MOVE      ONE,AMTERR
          IF        IBALANCE < INAMNTPD
            ASSIGN    (INAMNTPD-IBALANCE),INOVERPD
            ADD       INOVERPD,OPAYTOTL
            MOVE      "Over payment",AMTERTXT
            ADD       ONE,OPAYNO            * count number of Over-payments
          ENDIF
.
.
PIINV500  MOVE      ININVNUM,TMPPRINV
          PACK      TMPPSPAR,SP30,SP30
          PACK      KEY8,TMPPRINV
          CALL      RATMINV1
          IF        OVRCD <> 0
            CALL      WRTMINV1
            ADD       ONE,REPRINT
          ENDIF
.                                           ***********************************
.                                           * set up Receipt Details
PIINV600  MOVE      ZERO,EXIT
          CALL      CLTMPDTE
.
          MOVE      TMBNTDAT,TMDTTDAT
          MOVE      TMBNRECN,TMDTRECN
.
          COMPARE   "99999",TRANNUMB
          IF        @EQUAL
            PRINT     *1," ************** More than 99999 Transactions - ":
                      " Central Receipting cannot handle this ***************":
                      *N,*40,"Processing suspended *****************"
            ADD       ONE,ERRIN
            MOVE      ONE,EXIT
            GOTO      PIINV999
          ENDIF
.
PIINV700  ADD       ONE,TRANNUMB
          MOVE      TRANNUMB,TMDTTCNT
.
          MOVE      "Bpay Remittance",TMDTNAME
          PACK      TMDTINVN,SP4,ININVNUM   * change Inv.No. from 8 to 12
          MOVE      DPINVADM,TMDTVIST
          MOVE      PINVUR,TMDTURNO
.
          PACK      KEY8,DPINVADM,SP8
          CALL      RDRESP1
          IF        RESPOK = 1
            PACK      TMDTNAME,PKNAME,SP70       * Person responsible Name
            MOVE      PKNAME,TMDTREFR
            MOVE      PKADD1,TMDTADD1
            MOVE      PKADD2,TMDTADD2
            MOVE      PKSUBR,TMDTADD3
            MOVE      PKADD4,TMDTADD4
            MOVE      PKPOST,TMDTPOST
            MOVE      "0",TMDTSFLG
          ELSE
            IF        PMIOK = 1
              MOVE      SP1,DIM1A
              MOVE      PGNAME,DIM1A
              PACK      DIM30A,PTITL,SP1,DIM1A,DOT,PSNAME
              SQUEEZE   DIM30A
              PACK      TMDTNAME,DIM30A,SP70  * ????? what name here ?
              MOVE      PADD1,TMDTADD1
              MOVE      PADD2,TMDTADD2
              MOVE      PSUBRB,TMDTADD3
              MOVE      PADD4,TMDTADD4
              MOVE      PPOST,TMDTPOST
              MOVE      "1",TMDTSFLG
            ENDIF
          ENDIF
.
.         get Account info. for INCA & BNKA selected in GTACCT00
.
          MOVE      KIREGCOD,TMDTREGI
          MOVE      SAVTINCA,TMDTINCA
          MOVE      SAVTBNKA,TMDTBNKA
.
          IF        SUSPEND = 1
            MATCH     SP70,RCCNURCP
            IF        !@EQUAL
              MOVE      RCCNURCP,TMDTREGI     * Suspend register
              MOVE      SAVSINCA,TMDTINCA     * fully credited invoices
              MOVE      SAVSBNKA,TMDTBNKA
            ENDIF
          ENDIF
.
.         Calc Outstanding Balance
.
          ADD       INAMNTPD,TRANTAMT       * add Received amount to Total
          MOVE      INAMNTPD,TMDTAMNT
.
          MOVE      ZERO,TMDTDISC
          MOVE      TMBNMHOS,TMDTMHOS

          MOVE      TMBNMDHS,TMDTMDHS
          MOVE      "0",TMDTALLO
          PACK      KEY3,TMDTREGI,SP3
          CALL      RDREGA1
          IF        OVRCD=0
            IF        REGIBACD=99
              MOVE      "2",TMDTALLO        * Suspense register
            ENDIF
          ENDIF
.
          MOVE      TMBNCDAT,TMDTCDAT
          MOVE      TMBNCTIM,TMDTCTIM
          MOVE      TMBNCUID,TMDTCUID
.
.         write the records
.
          MOVE      REGDESC,DIM20A
          IF        SUSPEND = 1
            MOVE    SAVSDESC,DIM20A
          ENDIF
          PACK      DIM20B,TMDTNAME,SP30
.
.                              20           12           8          20
          IF        AMTERR = 0
            PACK      PRTLINE,DIM20A,SP2,TMDTINVN,SP4,TMDTURNO,SP4,DIM20B:
                      SP4,IBALANCE,SP4,INAMNTPD,SP3,COMMTTXT,SP70
          ELSE
            PACK      PRTLINE,DIM20A,SP2,TMDTINVN,SP4,TMDTURNO,SP4,DIM20B:
                      SP4,IBALANCE,SP4,INAMNTPD,SP6,AMTERTXT,SP70
          ENDIF
          WRITE     PRTFILE1,SEQ;PRTLINE
.
          PACK      KEY17,TMDTRECN,TMDTTCNT,SP70
          CALL      WRTMDTE1
          GOTO      PIINV999
.
PIINV999  RETURN
+
.******************************************************************************
.*                                  GTACCT00                                  *
.*         Get Account and Banking details (based on GTACCT00 in RCPWEB03)    *
.******************************************************************************
GTACCT00  MOVE      ZERO,EXIT
          UNPACK    SP70,SAVSINCA,SAVSBNKA,SAVTINCA,SAVTBNKA
.
          MATCH     SP70,RCCNURCP
          IF        !@EQUAL
            MOVE      RCCNURCP,KEY3
            CALL      RDREGA1
            BRANCH    OVRCD,GTACCT91
            IF        IBCNMHOS=1
              MATCH     KIHOSPID,REGHOSP
              GOTO      GTACCT92 IF NOT EQUAL * different hospital id
            ENDIF
            GOTO      GTACCT30
          ENDIF
.
.         Find the first Suspense register
.
          MOVE      "DEFAULT SUSPENSE ACCT",SAVSDESC
          MOVE      "99",DIM2A
          PACK      KEY5,DIM2A,SP3
          CALL      RDSREGA3
GTACCT10  CALL      RDKREGA3
          BRANCH    OVRCD,GTACCT40
.
          COMPARE   "99",REGIBACD           * Suspense register
          GOTO      GTACCT40 IF NOT EQUAL
          MATCH     "1",REGACTIV
          GOTO      GTACCT10 IF EQUAL       * Inactive
.
          IF        IBCNMHOS=1
            MATCH     KIHOSPID,REGHOSP
            GOTO      GTACCT10 IF NOT EQUAL
          ENDIF
.
          MOVE      REGCODE,RCCNURCP
GTACCT30  MOVE      REGDESC,SAVSDESC
          MOVE      REGGENLD,SAVSINCA       * Credit account
          MOVE      REGGENBK,SAVSBNKA       * Debit account
.
.         Check the Keyin Register
.
GTACCT40  PACK      KEY3,KIREGCOD,SP3
          CALL      RDREGA1
          BRANCH    OVRCD,GTACCT99
          IF        IBCNMHOS=1
            MATCH     KIHOSPID,REGHOSP
            GOTO      GTACCT94 IF NOT EQUAL
          ENDIF
.
          COMPARE   TWO,HSYS1
          GOTO      GTACCT90 IF EQUAL       * Using Other FMS- get Acc.from Reg.
.
          MATCH     REGGENLD,SP20
          GOTO      GTACCT99 IF EQUAL
.
          MOVE      REGGENLD,SAVTINCA       
          CALL      GETC0000                * Get Control Account Details
          PACK      SAVTBNKA,FMLALEDG,FMCSBNK 
          GOTO      GTACCT99 
.
.         Get Credit and Bank Account from Register record
.
GTACCT90  COMPARE   "99",REGIBACD           * Suspense
          IF        @EQUAL
            MOVE      REGGENLD,SAVTINCA     * Credit account
            MOVE      REGGENBK,SAVTBNKA     * Debit account
          ELSE                              * Patient billing/Priv.Prac./etc
            MOVE      REGGENDB,SAVTINCA     * Debtors Control account as Crd.Acc
            MOVE      REGGENBK,SAVTBNKA     * Debit account
          ENDIF
          GOTO      GTACCT99
.
GTACCT91  PRINT     *1,"Suspense Register ",KEY3," is invalid"
          MOVE      ONE,EXIT
          GOTO      GTACCT98
.
GTACCT92  PRINT     *1,"Suspense Register ",KEY3," is for Hospital ID: ",REGHOSP
          MOVE      ONE,EXIT
          GOTO      GTACCT98
.
GTACCT94  PRINT     *1,"Keyin Register ",KEY3," is for Hospital ID: ",REGHOSP
          MOVE      ZERO,EXIT
.
GTACCT98  MOVE      ONE,ERRIN
GTACCT99  RETURN
+
.******************************************************************************
.*                                  GETC0000                                  *
.*                   Get Control Account Details (ex RCPWEB03)                *
.******************************************************************************
GETC0000  UNPACK    REGGENLD,KEY2,DIM12A
          CALL      RDFMLA1              * Ledger Master Details
          MOVE      FMLABNK,FMCSBNK
          MOVE      FMLADEB,FMCSDEB
          MOVE      FMLAAGST,FMCSAGST
.
          PACK      KEY14,REGGENLD,SP70
          CALL      RDFMCS1              * Selected Control Accounts
          BRANCH    OVRCD,GETC9000
.
          MATCH     SP70,FMCSBNK
          GOTO      GETC0010 IF NOT EQUAL
.
          MOVE      FMLABNK,FMCSBNK
.
GETC0010  MATCH     SP70,FMCSDEB
          GOTO      GETC0020 IF NOT EQUAL
.
          MOVE      FMLADEB,FMCSDEB
.
GETC0020  MATCH     SP70,FMCSAGST
          GOTO      GETC9999 IF NOT EQUAL
.
          MOVE      FMLAAGST,FMCSAGST
.
          GOTO      GETC9999
.
GETC9000  MOVE      FMLABNK,FMCSBNK
          MOVE      FMLADEB,FMCSDEB
          MOVE      FMLAAGST,FMCSAGST
.
GETC9999  RETURN
.******************************************************************************
.*                                  PROC0000                                  *
.*                          Process Temp file to RCP files                    *
.******************************************************************************
PROC0000  MOVE      ZERO,EXIT
          MOVE      SP15,SAVERECN
.                                           * read temp Bank Header
          MOVE      "           1",KEY12
          CALL      RDTMBNK1
          BRANCH    OVRCD,PROC9000
.                                           * read temp Bank Details record
          MOVE      "  1",KEY3 
          PACK      KEY15,KEY12,KEY3
          CALL      RDTMBDT1
          BRANCH    OVRCD,PROC9100
.                                           * get next Receipt Number
PROC1000  READ      CONTROLF,THIRTY6;*193,HRECPT
          ADD       ONE,HRECPT
          WRITAB    CONTROLF,THIRTY6;*193,HRECPT
          SUB       ONE,HRECPT
.
          MOVE      HRECPT,KEY12
          CALL      RDRCBNK1
          COMPARE   ZERO,OVRCD
          GOTO      PROC1000 IF EQUAL
.
.                                           * write the RCPBNK & RCPBDT records
          MOVE      TMBNRECN,SAVERECN
          MOVE      HRECPT,TMBNRECN
          MOVE      HRECPT,TMBDRECN
.
          CALL      MVTMBK00                * move temp BNK fields to real flds
          PACK      KEY12,TMBNRECN
          CALL      WRRCBNK1                * write to real file
          CALL      MVTMBT00                * move temp BDT fields to real flds
          PACK      KEY15,TMBDRECN,TMBDUNIQ
          CALL      WRRCBDT1
.
.                                           * now process each Invoice record
          PACK      KEY17,SAVERECN,SP20
          CALL      RSTMDTE1
PROC3000  CALL      RKTMDTE1
          BRANCH    OVRCD,PROC5000
.
          MATCH     SAVERECN,TMDTRECN
          GOTO      PROC5000 IF NOT EQUAL
          MOVE      HRECPT,TMDTRECN
          CALL      MVTMDE00
.
.         If this is for Suspense register, blank out Inv.no, Visit and U/R no
.
          PACK      KEY3,TMDTREGI,SP3
          CALL      RDREGA1
          IF        OVRCD=0
            IF        REGIBACD=99
              MOVE      SP70,RCDTINVN
              MOVE      SP70,RCDTVIST
              MOVE      SP70,RCDTURNO
              PACK      RCDTADD1,TMDTINVN,SP1,TMDTVIST,SP1,TMDTURNO,SP70
              MOVE      SP70,RCDTADD2
              MOVE      SP70,RCDTADD3
              MOVE      SP70,RCDTADD4
              MOVE      SP70,RCDTPOST
            ENDIF
          ENDIF
.
          PACK      KEY17,TMDTRECN,TMDTTCNT
          CALL      WRRCDTE1
          GOTO      PROC3000
.
.
PROC5000  MOVE      ZERO,EXIT
          PRINT     *N,*20,"Receipt Number allocated  : ",TMBNRECN,*N,*N:
                    *1,"File successfully uploaded to Central Receipting"
          GOTO      PROC9999
.
.
PROC9000  PRINT     *N,*1,"Bank Header record not found - file has not been ":
                    "uploaded ****"
          GOTO      PROC9999
.
PROC9100  PRINT     *N,*1,"Bank Details record not found - file has not been ":
                    "uploaded ****"
          GOTO      PROC9999
.
PROC9999  RETURN
+
.******************************************************************************
.*                                  SETPRT00                                  *
.*           Save Printer info for the Front-end (RSHWEB02 doesn't work)      *
.******************************************************************************
SETPRT00  MOVE      ZERO,EXIT
.
          UNPACK    SP70,IBPDCOP,IBPDDOC,IBPDNUM,IBPDSPA
          MOVE      "REPRTINV",IBPDPID
          MOVE      "01",IBPDREP
          MOVE      KIWEBUID,IBPDUID
          MOVE      KIWEBPRT,IBPDPRT
.
          OPEN      IBAPDFA1,"ibapdfaf"
          PACK      KEY20,IBPDPID,IBPDREP,IBPDUID
          CALL      RAIBPD1
          IF        OVRCD = 0
            CALL      UPIBPD1
          ELSE
            CALL      WRIBPD1
          ENDIF
.
SETPRT99  RETURN
+
.******************************************************************************
.*                                  PRTINV00                                  *
.*                       Print unbalanced Invoices                            *
.******************************************************************************
PRTINV00  MOVE      ZERO,EXIT
          COMPARE   ZERO,REPRINT
          GOTO      PRTINV99 IF EQUAL
          MATCH     SP8,USERID              * no Userid, no printing
          GOTO      PRTINV99 IF EQUAL
.
.              *  Schedule "Re-print Invoice" (IBABIL21) for unbalanced Invoices
          MOVE      "IBABIL21",SCHDPGID
          MOVE      "Reprint a BPay Invoice",SCHDDESC
          PACK      SCHDDATE,CCC,CYY,CMM,CDD
          REP       " 0",SCHDDATE
          CLOCK     TIME,SCHDTIME
          MOVE      KIWEBPRT,SCHDPRNT
.
.
          PACK      KEY8,SP8
          CALL      RSTMINV1
PRTINV10  CALL      RKTMINV1
          BRANCH    OVRCD,PRTINV99
.
.                                           * Write Keyin parameters
          MOVE      "2",KEYSTARR[1]         * "Option - Single Invoice
          MOVE      TMPPRINV,KEYSTARR[2]    * "Invoice Number"
          MOVE      "R",KEYSTARR[3]         * "(R)e-print"
          MOVE      SP70,KEYSTARR[4]
          MOVE      SP70,KEYSTARR[5]
.
          MOVE      FIVE,KEYSTCNT           * Number of Keyins
.
          CALL      SCHPRO00                * Schedule Extract
          GOTO      PRTINV10
.
PRTINV99  RETURN
+
.******************************************************************************
.*                                  HEAD0000                                  *
.*                          Print error Report Header                         *
.******************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          MOVE      "- Keyin data",CPHDROPT
          CALL      HEAD132                 * Print the report header
          PRINT     *N;
          MOVE      ZERO,CLNO
HEAD9999  RETURN
.******************************************************************************
.*                                  HED10000                                  *
.*                          Print error Report Header                         *
.******************************************************************************
HED10000  MOVE      ONE,CNOUNDLN
          MOVE      "- Error report",CPHDROPT
          CALL      HEAD132                 * Print the report header
          PRINT     *1,HEADDES1,*26,HEADDES2,*N,*1,HEADDES3,*N,*N:
                    *1,"Input data string",*42,"Rec.No.",*50,COLTXT:
                    *55,"Error",*N:
.                       ....*....1....*....2....*....3....*....4....*....5
                    *1,"------------------------------------     ------- -":
                       "--  ----------------------------------------------":
                       "------------------------"
          MOVE      ZERO,CLNO
HED19999  RETURN
+
.******************************************************************************
.*                                  HED20000                                  *
.*                          Print detail Report Header                        *
.******************************************************************************
HED20000  MOVE      ONE,CNOUNDLN
          MOVE      "- Detail report",CPHDROPT
          SQUEEZE   INHOSNAM
          CALL      HEAD132                 * Print the report header
          PRINT     *1,"Remittance File from : ","BPay",*N:
                    *1,"                To   : ",INFLHNAM,*N:
                    *79,"Balance"
          PRINT     *1,"Register",*25,"Invoice No.",*43,"U/R",*51,"Payer Name":
                    *79,"Outstanding",*97,"Payment Amt."
          MOVE      ZERO,CLNO
HED29999  RETURN
+
.******************************************************************************
.*                                  PRNT0000                                  *
 *                            Print An Error Report                           *
.******************************************************************************
PRNT0000  COMPARE   "56",CLNO
          CALL      HED10000 IF NOT LESS    * Print the report header
.
          RESET     ERRDATA
          RESET     ERRMESS
          PRINT     *1,ERRDATA,*43,RECIN,*51,FLDNO,*55,ERRMESS
.
          ADD       ONE,CLNO
          PACK      ERRDATA,SP70
          PACK      ERRMESS,SP70
.
PRNT9999  RETURN
+
.******************************************************************************
.*                                  PRLN0000                                  *
 *                            Print a message line                            *
.******************************************************************************
PRLN0000  COMPARE   "56",CLNO
          CALL      HED10000 IF NOT LESS    * Print the report header
.
          RESET     ERRMESS
          PRINT     *55,ERRMESS
.
          ADD       ONE,CLNO
          PACK      ERRMESS,SP70
.
PRLN9999  RETURN
+
.******************************************************************************
.*                                  CLHBR000                                  *
.*                           Clear Input variables                            *
.******************************************************************************
CLHBR000  MOVE      SP70,FLD20002
          MOVE      SP70,FLD20003
          MOVE      SP70,FLD20004
          MOVE      SP70,FLD20005
          MOVE      SP70,FLD20006
          MOVE      SP70,FLD20007
.
CLHBR999  RETURN
+
CLTMPBNK  MOVE      SP70,TMBNRECN
          MOVE      SP70,TMBNTDAT
          MOVE      SP70,TMBNCDRW
          MOVE      SP70,TMBNMHOS
          MOVE      SP70,TMBNMDHS
          MOVE      SP70,TMBNTTYP
          MOVE      SP70,TMBNRCOD
          MOVE      SP70,TMBNSTAT
          MOVE      SP70,TMBNBDAT
          MOVE      SP70,TMBNBTIM
          MOVE      SP70,TMBNRDAT
          MOVE      SP70,TMBNRTIM
          MOVE      SP70,TMBNCHQA
          MOVE      SP70,TMBNCDAT
          MOVE      SP70,TMBNCTIM
          MOVE      SP70,TMBNCUID
          MOVE      SP70,TMBNUDAT
          MOVE      SP70,TMBNUTIM
          MOVE      SP70,TMBNUUID
          MOVE      SP70,TMBNPRIN
          MOVE      SP70,TMBNPRNT
          MOVE      SP70,TMBNSPAR
          MOVE      ZERO,TMBNTOTP
.
CLTMBN99  RETURN
.
.
CLTMPBDT  MOVE      SP70,TMBDTDAT
          MOVE      SP70,TMBDRECN
          MOVE      SP70,TMBDUNIQ
          MOVE      SP70,TMBDMHOS
          MOVE      SP70,TMBDMDHS
          MOVE      SP70,TMBDPTYP
          MOVE      SP70,TMBDPAYD
          MOVE      SP70,TMBDCHQN
          MOVE      SP70,TMBDDRAW
          MOVE      SP70,TMBDBANK
          MOVE      SP70,TMBDBRCH
          MOVE      SP70,TMBDCRDT
          MOVE      SP70,TMBDSTRF
....      MOVE      SP70,TMBDMONM
          MOVE      SP70,TMBDEFTT
          MOVE      SP70,TMBDCDAT
          MOVE      SP70,TMBDCTIM
          MOVE      SP70,TMBDCUID
          MOVE      SP70,TMBDUDAT
          MOVE      SP70,TMBDUTIM
          MOVE      SP70,TMBDUUID
          MOVE      SP70,TMBDSPAR
          MOVE      ZERO,TMBDPAMT
          MOVE      SP70,TMBDPCOD
.
CLTMBD99  RETURN
.
.
CLTMPDTE  MOVE      SP70,TMDTTDAT
          MOVE      SP70,TMDTRECN
          MOVE      SP70,TMDTTCNT
          MOVE      SP70,TMDTREGI
          MOVE      SP70,TMDTINVN
          MOVE      SP70,TMDTVIST
          MOVE      SP70,TMDTURNO
          MOVE      SP70,TMDTNAME
          MOVE      SP70,TMDTADD1
          MOVE      SP70,TMDTADD2
          MOVE      SP70,TMDTADD3
          MOVE      SP70,TMDTADD4
          MOVE      SP70,TMDTPOST
          MOVE      SP70,TMDTSFLG
          MOVE      SP70,TMDTREFR
          MOVE      SP70,TMDTINCA
          MOVE      SP70,TMDTBNKA
          MOVE      SP70,TMDTMHOS
          MOVE      SP70,TMDTMDHS
          MOVE      SP70,TMDTGSTC
          MOVE      SP70,TMDTGSTA
          MOVE      SP70,TMDTALLO
          MOVE      SP70,TMDTRELD
          MOVE      SP70,TMDTRELT
          MOVE      SP70,TMDTPRAC
          MOVE      SP70,TMDTCDAT
          MOVE      SP70,TMDTCTIM
          MOVE      SP70,TMDTCUID
          MOVE      SP70,TMDTUDAT
          MOVE      SP70,TMDTUTIM
          MOVE      SP70,TMDTUUID
          MOVE      SP70,TMDTDPTY
          MOVE      SP70,TMDTPRIN
          MOVE      SP70,TMDTPRNT
          MOVE      SP70,TMDTSPAR
          MOVE      ZERO,TMDTAMNT
          MOVE      ZERO,TMDTDISC
          MOVE      ZERO,TMDTTAMT
.
CLTMDT99  RETURN
.------------------------------------------------------------
.  Move Temporary bank header fields to bank header fields
.------------------------------------------------------------
MVTMBK00  MOVE      TMBNRECN,RCBNRECN
          MOVE      TMBNTDAT,RCBNTDAT
          MOVE      TMBNTOTP,RCBNTOTP
          MOVE      TMBNCDRW,RCBNCDRW
          MOVE      TMBNMHOS,RCBNMHOS
          MOVE      TMBNMDHS,RCBNMDHS
          MOVE      TMBNTTYP,RCBNTTYP
          MOVE      TMBNRCOD,RCBNRCOD
          MOVE      TMBNSTAT,RCBNSTAT
          MOVE      TMBNBDAT,RCBNBDAT
          MOVE      TMBNBTIM,RCBNBTIM
          MOVE      TMBNRDAT,RCBNRDAT
          MOVE      TMBNRTIM,RCBNRTIM
          MOVE      TMBNCHQA,RCBNCHQA
          MOVE      TMBNCDAT,RCBNCDAT
          MOVE      TMBNCTIM,RCBNCTIM
          MOVE      TMBNCUID,RCBNCUID
          MOVE      TMBNUDAT,RCBNUDAT
          MOVE      TMBNUTIM,RCBNUTIM
          MOVE      TMBNUUID,RCBNUUID
          MOVE      SP70,RCBNSPAR
.
MVTMBK99  RETURN
.
.------------------------------------------------------------
.  Move Temporary bank detail fields to bank details fields
.------------------------------------------------------------
MVTMBT00  MOVE      TMBDTDAT,RCBDTDAT
          MOVE      TMBDRECN,RCBDRECN
          MOVE      TMBDUNIQ,RCBDUNIQ
          MOVE      TMBDMHOS,RCBDMHOS
          MOVE      TMBDMDHS,RCBDMDHS
          MOVE      TMBDPTYP,RCBDPTYP
          MOVE      TMBDPAMT,RCBDPAMT
          MOVE      TMBDPAYD,RCBDPAYD
          MOVE      TMBDCHQN,RCBDCHQN
          MOVE      TMBDDRAW,RCBDDRAW
          MOVE      TMBDBANK,RCBDBANK
          MOVE      TMBDBRCH,RCBDBRCH
          MOVE      TMBDCRDT,RCBDCRDT
          MOVE      TMBDSTRF,RCBDSTRF
....      MOVE      TMBDMONM,RCBDMONM
          MOVE      TMBDEFTT,RCBDEFTT
          MOVE      TMBDCDAT,RCBDCDAT
          MOVE      TMBDCTIM,RCBDCTIM
          MOVE      TMBDCUID,RCBDCUID
          MOVE      TMBDUDAT,RCBDUDAT
          MOVE      TMBDUTIM,RCBDUTIM
          MOVE      TMBDUUID,RCBDUUID
          MOVE      TMBDPCOD,RCBDPCOD
          MOVE      SP70,RCBDSPAR
.
MVTMBT99  RETURN
.
.--------------------------------------------------------------------
.  Move Temporary receipt detail fields to Real receipt detail fields
.--------------------------------------------------------------------
MVTMDE00  MOVE      TMDTTDAT,RCDTTDAT
          MOVE      TMDTRECN,RCDTRECN
          MOVE      TMDTTCNT,RCDTTCNT
          MOVE      TMDTREGI,RCDTREGI
          MOVE      TMDTINVN,RCDTINVN
          MOVE      TMDTVIST,RCDTVIST
          MOVE      TMDTURNO,RCDTURNO
          MOVE      TMDTNAME,RCDTNAME
          MOVE      TMDTADD1,RCDTADD1
          MOVE      TMDTADD2,RCDTADD2
          MOVE      TMDTADD3,RCDTADD3
          MOVE      TMDTADD4,RCDTADD4
          MOVE      TMDTPOST,RCDTPOST
          MOVE      TMDTSFLG,RCDTSFLG
          MOVE      TMDTREFR,RCDTREFR
          MOVE      TMDTINCA,RCDTINCA
          MOVE      TMDTBNKA,RCDTBNKA
          MOVE      TMDTAMNT,RCDTAMNT
          MOVE      TMDTDISC,RCDTDISC
          MOVE      TMDTMHOS,RCDTMHOS
          MOVE      TMDTMDHS,RCDTMDHS
          MOVE      TMDTGSTC,RCDTGSTC
          MOVE      TMDTGSTA,RCDTGSTA
          MOVE      TMDTALLO,RCDTALLO
          MOVE      TMDTRELD,RCDTRELD
          MOVE      TMDTRELT,RCDTRELT
          MOVE      TMDTCDAT,RCDTCDAT
          MOVE      TMDTPRAC,RCDTPRAC
          MOVE      TMDTCTIM,RCDTCTIM
          MOVE      TMDTCUID,RCDTCUID
          MOVE      TMDTUDAT,RCDTUDAT
          MOVE      TMDTUTIM,RCDTUTIM
          MOVE      TMDTUUID,RCDTUUID
          MOVE      TMDTDPTY,RCDTDPTY
          MOVE      SP70,RCDTSPAR
.
MVTMDE99  RETURN
.
.==============================================================================
.
RSTMINV1  RESET     KEY5
          READ      TMPINVA1,KEY8;;
          RETURN
.
RKTMINV1
          MOVE      ZERO,OVRCD
          READKS    TMPINVA1;TMPPRINV,TMPPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RATMINV1  RESET     KEY85
          MOVE      ZERO,OVRCD
          READ      TMPINVA1,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMINV1  RESET     KEY15
          WRITE     TMPINVA1,KEY8;TMPPRINV,TMPPSPAR
          RETURN
.         
DETMINV1  RESET     KEY8                                                 
          DELETE    TMPINVA1,KEY8                                            
          RETURN
.
.==============================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       STDHLPCD
          INC       RCPREGKY
          INC       RCPCIDDS
          INC       FMSCHQKY
.
          INC       RSHWEBCD
...       INC       RCPBNKTM
...       INC       RCPBDTTM
...       INC       RCPDTETM
.
...       INC       CLPATDSC
...       INC       CLPATMIS
          INC       CLPATINV
          INC       CLPATMAS
          INC       CLPATVIS
          INC       CLPMSVX1
.
          INC       FMSCHQIO/INC            * FMS Cheque Acct Master File
          INC       FMSCSAIO/INC            * FMS Selected Control Accounts
          INC       FMSLMAIO/INC            * FMS Ledger Master Details
          INC       PATCODIO/INC
          INC       PATFN1IO/INC            * Health Fund Table
          INC       PATMA1IO/INC            * PMI Master
          INC       PATHOSIO/INC            * Hospital Details 
          INC       PATHSPIO/INC            * Hospital Details 
          INC       PATINVIO/INC            * Patient Invoice file
          INC       PATVISIO/INC            * Patient Visit Details
          INC       PMSVX1IO/INC            * Patient Visit Extension
...       INC       PATMI1IO/INC            * Patient Admission Details
...       INC       PATDSCIO/INC            * Patient Discharge details
          INC       PATRE1IO/INC            * Patient Responsible Person
          INC       RCPCIDIO/INC            * Cashier ID file
          INC       RCPBNKIO/INC            * Receipting Bank Header file
          INC       RCPBDTIO/INC            * Receipting Bank Detail file
          INC       RCPDTEIO/INC            * Receipting Receipt Details file
          INC       RCPREGIO/INC            * Receipting Register Details     
          INC       TMPBNKIO/INC            * RCP Temp Bank Header file
          INC       TMPBDTIO/INC            * RCP Temp Bank Detail file
          INC       TMPDTEIO/INC            * RCP Temp Receipt Details file
          INC       WEBSECIO/INC            * 
          INC       WEBSCHIO/INC
          INC       IBAPDFIO/INC
+

.******************************************************************************
.  System         : Theatre System          Release 5.01
.  Program        : IBAOPR52.PBL
.  Description    : Re-print Operation Records
.******************************************************************************
.  Date           : 26/02/90   Start of a new decade                         
.  Author         : Bill Dew
.  Mod's    :                                                                
.******************************************************************************
.*        V12.00.01 22/05/2025  Bella Turco      TSK 0955096                  *
.*                  Alphanumeric changes                                      *
.*        V11.03.02 21/04/2023  Thanh T          TSK 0909393                  *
.*                  Changed TEAM0000 to use RSOPDEA6/RPOPDEA6 instead of      *
.*                  RDOPDEA1                                                  *
.*        V11.03.01 20/03/2023  PJ Le Febour   Task 0909393                   *
.*                  Amendments KEY19 to KEY22 for theatre index changes       *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.01.01 03/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added PMSVX1FD                                            *
.*        V9.04.01  04/03/2005  Lina Vo       CAR 56404                       *
.*                  Removed doctor preference FD - Not used.                  *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.02  31/08/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.00  28/10/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*****************************************************************************
.*        V5.03.01  08/05/1996 Howellsy        5.04                      *
.*           :           Added 2 more nurse codes & types.                    *
.*                  20/04/90  Bill Dew
.                   Convert program from r5.01 to r5
.         V5.01.01  19/10/1990    Justin Coates
.                   Modified according to new FD's
.         V5.00     19/12/90     Rod Knowles (all of it!)
.                   Conversion to Version 5
.         V5.01.01  21/01/91     ROD            
.                   Converted to Release 5.01      
.         V5.01.02  19/02/1991    Graeme Williams
.                   Fixed date to be <= current date (not >=)
.                   Fixed teams to allow for defaults.
.         V5.01.03  03/04/1991    Justin Coates
.                   Changed to use OPPRIREC for printing records
.         V5.01.04  25/07/1991    ROD 
.                   Recompiled for OPRSESFD
.         V5.01.05  13/09/1991    Graeme Williams    SRF 109632 (Quote 7075)
.                   Changes to OPPRIREC for southland format
.         V5.01.10  24/05/1995  Matt Surridge                                
.                   Fixed bugs for global recompile.                          
.*****************************************************************************
.
.$$$$$$
.
.   Reprint operation records IBAOPR52
.   ----------------------------------
.
.   Program description : This program enables operator to re-print operation
.   details.
.
.   Program flow :
.   Get clinic key details which are code date and time.
.   Get a valid admission number and display patient details.
.
.   END
.
.$$$$$$
.
          INC       STD001FD
          INC       OPRCLIFD/INC       * clinic
          INC       OPRCONFD/INC       * system parameters
          INC       OPRDEAFD/INC       * session booking detail file
          INC       OPRDEBFD/INC       * operating team file
          INC       OPRDECFD/INC       * operating room usage
..          INC       OPRDPHFD/INC       * doctor preference header
          INC       OPRNURFD/INC       * operating room nurses
          INC       OPRRECFD/INC       * operating record
          INC       OPRSESFD/INC       * session file
          INC       PATCODFD/INC       * codes file 
          INC       PATCONFD/INC       * system parameters 
          INC       PATDO1FD/INC       * doctors detail
          INC       PATMA1FD/INC       * patient master file
          INC       PATMI1FD/INC       * patient admission file
          INC       PMSVX1FD/INC       * patient admission file
          INC       PATNOBFD/INC       * no bed
          INC       PATTRNFD/INC       * patient tranfer 
          INC       PATWR1FD/INC       * ward bed file
.
. ------- variables needed for OPPRIREC.PBL -------
.
CMDLINE   DIM       50        * for storage
CURSESS   DIM       22        * current session
DOCCODE   DIM       6
DESC10    DIM       10        * description var
DESC15    DIM       15        * description var
DESC20    DIM       20        * description var
DESC27    DIM       27        * description var
DOCTCODE  DIM       6         * selected doctor code
HOMEPHON  DIM       12        * description var
FORM3     FORM      3         * count variable
NEWSCODE  DIM       6         * anesthetic code
NURSCODE  DIM       3         * selected nurse code
OPERSURG  DIM       6         * operating surgeon
PREF01    DIM       9         * doctor codes
PREF02    DIM       9         * doctor codes
PREF03    DIM       9         * doctor codes
PREF04    DIM       9         * doctor codes
SESS01    DIM       19        * shorten desc
SESS02    DIM       19        * shorten desc
.
. ------- program variables --------
.
ADMINNUM  DIM       8                  * ADMIssioN NUMber
ANESCODE  DIM       6
AMOUNT    FORM      3                  * number of items quantity
BIRTHDAY  DIM       10                 * birthday is date of birth
.
CASENUM   DIM       3                  * CASE NUMber
CLINIC    DIM       6                  * CLINIC code
CLINDESC  DIM       25                 * CLINic DESCription
COUNTER   FORM      2
DIM3      DIM       3                  * patdsdoc
DIM36     DIM       36
DIM42     DIM       42
ENDTIME   DIM       5                  * end time 
.
FORM6     FORM      6
INDEX     FORM      2
.
.
LASTCASE  DIM       3                  * last case number displayed
LASTHOSP  DIM       3                  * last hospital id displayed
LINE80    DIM       80                 * multi purpose 80 char line buffer
.
MASK      DIM       20
MAXRANGE  FORM      2
NEXTADMN  DIM       11
NORMDATE  DIM       10                 * session date display format for users
OPSTRT    DIM       5                  * operation start time
OPENDT    DIM       5                  * operation end time
PATAGE    DIM       3                  * PATient AGE
PATNAME   DIM       30                 * Patient's Name
REPLY     DIM       2
.
SCRNFLAG  FORM      1                  * signals redisplays vars
SESNDATE  DIM       8                  * use as key file date cc yy mm dd
SESNTIME  DIM       5                  * start time of operation
SEXDESC   DIM       8                  * male or female
SP36      INIT      "                                    "
SP42      INIT      "                                          "
STARTIME  DIM       5                  * expected startime of operation
STRTADMN  DIM       11
SURGCODE  DIM       6                  * SURGeon CODE
SURGDESC  DIM       30                 * SURGeon DESCription
SURGTYPE  DIM       3                  * surgeon type
.
TEAMNUM   DIM       1                  * team number
THEATRE   DIM       6                  * operating room code
TODAY     DIM       8                  * todays file date
URNUMBER  DIM       8                  * UR NUMBER patient code
WARDBED   DIM       7                  * WARD and BED number
.
. ------- program constants -------
.
NUM40     FORM      "40"
NUM62     FORM      "62"
ITEMHEAD  INIT      "Type Item    Description           Qty"
.
PRGID     INIT      "IBAOPR52"
PRGNAM    INIT      "Re-print Operation Records"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.                         MAIN LINE Rock and Roll
.*****************************************************************************
.
ML0000    CALL      INIT0000           * initialisation routine
.
ML1000    CALL      CLRV0000           * clear variables
          CALL      SCRN0000           * display screen
.
          CALL      CLIN0000           * get clinic code
          BRANCH    EXIT,ML9999        * nothing entered
.
          CALL      GDAT0000           * Get session DATe 
          CALL      GTIM0000           * Get session TIMe
.
          CALL      VSES0000           * validate session key information
          BRANCH    EXIT,ML1000        * invalid session
.
          CALL      CONT0000           * Confirm correct session
          BRANCH    EXIT,ML1000        * exit=0 yes and exit=1 no
.
ML1111    CALL      CLRV5555           * clear variables
.
          CALL      DADM0000           * display all admissions for session
          BRANCH    EXIT,ML1000        * none available
.
          CALL      SADM0000           * select admission
          BRANCH    EXIT,ML1000        * nothing entered
.
          CALL      SDET0000           * Show operation DETails
.
          CALL      TEAM0000           * get team num 
          BRANCH    EXIT,ML1111        * exit=1 if team number not on oprdetbf
.
          CALL      CONT1111
          BRANCH    EXIT,ML1111
.
          CALL      OPPRIREC           * print operation records
          GOTO      ML1111
.
ML9999    CHAIN     PGM                * chain back to calling menu
+
.*****************************************************************************
.                   INIT0000
.*****************************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,*EL,"opening oprcliaf"
          OPEN      OPRCLIA1,"oprcliaf"
          OPEN      OPRCLIA2,"oprcliaf"
          DISPLAY   *P58:24,"oprsesaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
          DISPLAY   *P58:24,"oprdetaf"
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA6,"oprdetaf"
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P58:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
          DISPLAY   *P58:24,"patnobef"
          OPEN      PATNOBE1,"patnobef"
          DISPLAY   *P58:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P58:24,"oprdetbf"
          OPEN      OPRDETB1,"oprdetbf"
          DISPLAY   *P58:24,"oprdetcf"
          OPEN      OPRDETC1,"oprdetcf"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P58:24,"oprnuraf"
          OPEN      OPRNURA1,"oprnuraf"
          DISPLAY   *P58:24,"oprrecaf"
          OPEN      OPRRECA1,"oprrecaf"
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,NUM40;*96,OPCNCDSC,OPCNCLSU
          READ      CONTROLF,FORTY2;*19,OPCNFOTM,OPCNTOTM,*68,OPCNPREC
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REPLACE   " 0",TODAY         * set todays date
          MOVE      ONE,CNEWDS         * set the new ? routines
.
INIT9999  RETURN
+
.*****************************************************************************
.                   CLRV0000
.         Clear all main variables with spaces or zeros
.*****************************************************************************
CLRV0000
          PACK      LINE80,SP30,SP30,SP20
          UNPACK    LINE80,CLINIC,CLINDESC,SESNDATE,SESNTIME,NORMDATE
.*****************************************************************************
CLRV5555
          PACK      LINE80,SP30,SP30,SP20
          UNPACK    LINE80,CASENUM,ENDTIME,STARTIME,PATNAME,TEAMNUM
          UNPACK    LINE80,WARDBED,PATAGE,SEXDESC,URNUMBER
CLRV9999  RETURN
.*****************************************************************************
SCRN0000  DISPLAY   *P1:4,*EF,*P5:4,OPCNCDSC,":":
                    *P5:5,"Session Date   :":
                    *P35:5,"Session Time :":
                    *V2LON,*P22:4,CLINIC,*P22:5,NORMDATE,*P50:5,SESNTIME:
                    *HOFF,*P35:4,CLINDESC
SCRN9999  RETURN
.*****************************************************************************
.                   CLIN0000
.         Get clinic code
.         Returns : CLINIC        the clinic code
.                   CLINDESC      the clinic description
.*****************************************************************************
.
CLIN0000  MOVE      ONE,SCRNFLAG       * set the redisplay flag
          DISPLAY   *P22:4,*EL,UNDLN6,*P22:4;
.
CLIN1111  KEYIN     *V2LON,CLINIC      * get doctor code
.
          ENDSET    CLINIC
          APPEND    SP6,CLINIC
          RESET     CLINIC
.
          MATCH     SP6,CLINIC         * test for return hit
          GOTO      CLIN7777 IF EQUAL
.
          CMATCH    "?",CLINIC         * test for ? routine
          GOTO      CLIN6666 IF NOT EQUAL
.
.         ? entered
.
          CALL      QRTN0000           * codes ? routine
          IF        OPCNCLSU = 1
            BRANCH    EXIT,CLIN7777
            GOTO      CLIN6666
          ENDIF
.
CLIN4444  DISPLAY   *P1:24,*EL,OPCNCDSC," : ",UNDLN6,*P19:24;
          GOTO      CLIN1111
.
.         validate what was entered
.
CLIN6666  CALL      VCLN0000           * validate clinic or surgeon
          BRANCH    EXIT,CLIN0000,CLIN4444 * invalid,question entered
.
.         valid clinic/doctor entered
.
          BRANCH    SCRNFLAG,CLIN7000  * dont redisplay
.
          CALL      SCRN0000           * redisplay
.
CLIN7000  DISPLAY   *P22:4,*V2LON,CLINIC,*HOFF,*P35:4,CLINDESC
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      CLIN9999
.
.         nothing entered
.
CLIN7777  MOVE      ONE,EXIT           * exit=1 if return hit
          DISPLAY   *P22:4,*EL         * clear this region
          BRANCH    SCRNFLAG,CLIN9999  * no redisplay needed
          CALL      SCRN0000           * redisplay
.
CLIN9999  RETURN
+
.*****************************************************************************
.                   QRTN0000
.    Question mark routine display codes from doctors or clinic depending
.    on sys params
.*****************************************************************************
QRTN0000  BRANCH    OPCNCLSU,QRTN5555  * test if using surgeons or clinics
          CALL      OPRCLIDS           * codes ? routine
          MOVE      ZERO,SCRNFLAG      * set flag requiring redisplay
          MOVE      ZERO,EXIT
          GOTO      QRTN9999
.
QRTN5555  CALL      GETSCR00
          MOVE      ONE,SCRNFLAG      * set flag requiring redisplay
          MOVE      SP6,CLINIC
          CALL      PATDOCDS
          CALL      PUTSCR00                      * restore screen
          BRANCH    EXIT,QRTN9999
          MOVE      FALSE,EXIT                    * valid input
          MOVE      DOCCODE,CLINIC
.
QRTN9999  RETURN
+
.*****************************************************************************
.                   VCLN0000
.         Validate clinic or surgeon code
.*****************************************************************************
VCLN0000  BRANCH    OPCNCLSU,VCLN5555  * test if using surgeons or clinics
          PACK      KEY6,CLINIC,SP6
          CALL      RDOPCLI1           * check for valid clinic
          BRANCH    OVRCD,VCLN8000     * tell user his code not on file
          MOVE      OPCLDESC,CLINDESC  * copy clinic description to work var
          GOTO      VCLN7777
VCLN5555
          PACK      KEY6,CLINIC,SP6
          CALL      RDDOCT1            * read doctors information
          BRANCH    OVRCD,VCLN8800     * 
.
          MOVE      ONE,PACFRMT        * title name surname of doctor
          MOVE      DSNAME,PACSNAME    * use pacdate to fix doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          CALL      PACNAME            * pack up doctors name
          MOVE      PACFNAME,CLINDESC
VCLN7777
          MOVE      ZERO,EXIT
          GOTO      VCLN9999
VCLN8000  
          DISPLAY   *P1:24,*B,*EL,"Clinic code not on file.  ";
          GOTO      VCLN8888
VCLN8800 
          DISPLAY   *P1:24,*B,*EL,"Doctor code not on file.  ";
VCLN8888
          CALL      HITENTER
          MOVE      TWO,EXIT           * scrnflag=1 exit=1 or scrnflag=0 exit=2
          LOAD      EXIT,SCRNFLAG,ONE
VCLN9999  RETURN
+
.*****************************************************************************
.                   REDP0000
.    Redisplay routine : this routine handles all redisplay formats by 
.    assigning a unique value to REDPFMAT 
.*****************************************************************************
REDP0000  BRANCH    SCRNFLAG,REDP9999
.
          MOVE      ONE,SCRNFLAG
          CALL      SCRN0000
          CALL      SDET0000
REDP9999  RETURN
+
.*****************************************************************************
.                   GDAT0000
.         Enter the session date
.         Returns : SESNDATE      the session date
.*****************************************************************************
.
GDAT0000  MOVE      "22",CCOL
          MOVE      "5",CVERT
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
.
GDAT3333  CALL      KEYDATE            * start date default todays date
          BRANCH    OVRCD,GDAT3333
.
          PACK      SESNDATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",SESNDATE
.
          MATCH     SESNDATE,TODAY     * test start date is >= today
          GOTO      GDAT8888 IF LESS
.
          PACK      NORMDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REPLACE   " 0",SESNDATE
          GOTO      GDAT9999
GDAT8888
          DISPLAY   *P1:24,*B,*EL:
                    "Session date must not be greater than todays date. ";
          CALL      HITENTER
          GOTO      GDAT0000
.
GDAT9999  RETURN
+
.*****************************************************************************
.                   GTIM0000
.         Get start time for booking the session
.         Returns : SESNTIME      the time
.*****************************************************************************
GTIM0000  MOVE      FIVE,CVERT         * set up time keyin screen position
          MOVE      "50",CCOL
.
GTIM4444  PACK      SESNTIME,SP5       * clear the startime 
          UNPACK    SP4,CHOUR,CMIN     * blank the hour and mins for KEYTIME
          CALL      KEYTIME            * get start time
          BRANCH    OVRCD,GTIM0000
          PACK      SESNTIME,CHOUR,COLON,CMIN
          MOVE      ZERO,EXIT          * exit=0 valid time
          GOTO      GTIM9999
.
GTIM9990  MOVE      ONE,EXIT           * exit=1 for return hit
.
GTIM9999  RETURN
+
.*****************************************************************************
.                   VSES0000
.         validate the Clinic date and time exist on the session file.
.         EXIT=0 session on file  EXIT=1 session not on file
.         Returns : CURSESS       the current session data
.******************************************************************************
VSES0000  PACK      KEY22,SESNDATE,SESNTIME,CLINIC,SP20
          MOVE      KEY22,CURSESS      * set current session value
.         CALL      RDOPSES7           * read session masters file
.         BRANCH    OVRCD,VSES8888
.
          CALL      RSOPSES7
          CALL      RKOPSES7
          BRANCH    OVRCD,VSES8888
.
          MATCH     SESNDATE,OPSEDATE    * same date
          GOTO      VSES8888 IF NOT EQUAL
.
          MATCH     SESNTIME,OPSETIME    * same time
          GOTO      VSES8888 IF NOT EQUAL
.
          MATCH     CLINIC,OPSECLIN      * same clinic
          GOTO      VSES8888 IF NOT EQUAL
.
          MOVE      OPSETHET,THEATRE   * get the operating room
          MOVE      ZERO,EXIT          * Exit equals 0 if code entered
          GOTO      VSES9999
.
VSES8888  DISPLAY   *P1:24,*EL,*B,"Session not on file.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
VSES9999  RETURN
+
.*****************************************************************************
.                   CONT0000
.         Continue with information entered
.         EXIT=1 No and EXIT=0 for yes this is known as negative logic
.*****************************************************************************
CONT0000
          DISPLAY   *P1:24,"Correct Session";
          MOVE      ONE,EXIT
          GOTO      CONT2222
.*****************************************************************************
CONT1111
          DISPLAY   *P1:24,"Re-Print Operation Detail Report";
          MOVE      TWO,EXIT
          GOTO      CONT2222
.*****************************************************************************
CONT2222
          DISPLAY   "  (",*V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") : ";
          KEYIN     *V2LON,ANS
          REPLACE   UPPLOW,ANS
          CMATCH    "Y",ANS
          GOTO      CONT3333 IF NOT EQUAL
          MOVE      ZERO,EXIT          * exit=0 if yes
          GOTO      CONT8888
CONT3333
          CMATCH    "N",ANS
          GOTO      CONT6666 IF NOT EQUAL
          MOVE      ONE,EXIT          * exit=1 if No 
          GOTO      CONT8888
CONT6666
          BRANCH    EXIT,CONT0000,CONT1111
CONT8888
          DISPLAY   *P1:24,*EL
CONT9999  RETURN
+
.*****************************************************************************
.                   SDET0000
.         Show operation DETails and Prompts
.*****************************************************************************
.
SDET0000  DISPLAY   *P1:6,*EF:
                    *P5:6,"Admission No   :",*P35:6,"Ward/Bed     :":
                    *P61:6,"Age :",*P5:7,"U/R Number     :":
                    *P5:8,"Case Number    :",*P35:8,"Team Number  :"
.
          DISPLAY   *V2LON,*P22:6,ADMINNUM,*P50:6,WARDBED,*P66:6,PATAGE:
                    *P71:6,SEXDESC,*P22:7,URNUMBER,*P22:8,CASENUM:
                    *P50:8,TEAMNUM,*HOFF,*P35:7,PATNAME
SDET9999  RETURN
+
.*****************************************************************************
.                   PATN0000
.         format patients name
.*****************************************************************************
PATN0000  PACK      PATNAME,PTITL,SP30,SP10
          SCAN      SP10,PATNAME       * this scans for the end of title
          MOVEFPTR  PATNAME,FORM2      * save the current form ptr
          APPEND    PGNAME,PATNAME     * append the given name
          RESET     PATNAME,FORM2      * reset formptr for scan
          SCAN      SP4,PATNAME        * set the next location to load givenname
          APPEND    PSNAME,PATNAME   
          RESET     PATNAME
PATN9999  RETURN
.
.*****************************************************************************
.                   RDPD0000
.         This routine gets patient's UR number, ward bed, age, sex, and name
.*****************************************************************************
.
RDPD0000  MOVE      ADMINNUM,KEY8
          CALL      RDMISS1
          MOVE      AURNO,URNUMBER
.
.---- set up patient name ----
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1            * get surname given name and title
.
RDPD2000  CALL      PATN0000           * set patient name patient name
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BIRTHDAY
          REP       " 0",BIRTHDAY
.
          MOVE      PSAGE,PATAGE       * get patient age
.
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,SEXDESC
.* ******************************************   End of Changes         *C-49016
.
RDPD4499  PACK      KEY30,ADMINNUM,Z70     * pack key to read at end of admin
          CALL      RDSTRAN2           * position the fptr
          CALL      RDPTRAN2           * and read back one
          BRANCH    OVRCD,RDPD9000
.
          MATCH     ADMINNUM,TADMN        * check whether the right adminssion
          GOTO      RDPD9000 IF NOT EQUAL
.
          PACK      WARDBED,TWARD,SLASH,TBED
          MATCH     SP3,TBED           * test there is a bed number
          GOTO      RDPD9999 IF NOT EQUAL
.
          MOVE      APLUR,NBPLUR
          PACK      KEY13,TWARD,ADMINNUM,NBPLUR
          CALL      RDNOBE1
          BRANCH    OVRCD,RDPD9999
.
          PACK      WARDBED,TWARD,SLASH,NBROOM
          GOTO      RDPD9999
.
RDPD9000  MOVE      SP10,WARDBED
.
RDPD9999  RETURN
+
.*****************************************************************************
.                   DADM0000
.         Display session admission numbers of patients in the oprdetaf also
.         calculate the number of records in session MAXRANGE
.*****************************************************************************
.
DADM0000  DISPLAY   *P1:6,*EF,*P1:7,*V2LON,*ULON:
                     "    Admin Num  Patient Names                ":
                     "            Case Num  Start time"
          MOVE      SEVEN,CVERT
          MOVE      ZERO,MAXRANGE
.         PACK      KEY26,SESNDATE,SESNTIME,CLINIC,ZERO,SP70
          PACK      KEY26,SESNDATE,SESNTIME,CLINIC,SP3,ZERO,SP70
          CALL      RSOPDEA6
.
DADM2222  CALL      RKOPDEA6                * next read on details file
          BRANCH    OVRCD,DADM8888
.
          MATCH     SESNDATE,OPDADATE
          GOTO      DADM8888 IF NOT EQUAL
          MATCH     SESNTIME,OPDATIME
          GOTO      DADM8888 IF NOT EQUAL
          MATCH     CLINIC,OPDACLIN
          GOTO      DADM8888 IF NOT EQUAL
          COMPARE   ZERO,OPDASTAT
.         GOTO      DADM8888 IF NOT EQUAL
          GOTO      DADM2222 IF NOT EQUAL
.
          ADD       ONE,CVERT          * increment the row counter
          COMPARE   TWENTY3,CVERT      * test if reached a page
          GOTO      DADM3000 IF LESS
          MOVE      ZERO,EXIT 
          GOTO      DADM9999
.
DADM3000  MOVE      OPDAADMN,KEY8
          CALL      RDMISS1            * get patients UR number from admission
          BRANCH    OVRCD,DADM4000
          MATCH     ZEROUR,AURNO       * ignore patient if zero UR 
          GOTO      DADM4000 IF EQUAL
.
DADM3333  
          MOVE      AURNO,KEY8
          CALL      RDMAST1            * get surname given name and title
          BRANCH    OVRCD,DADM4000
.
          CALL      PATN0000           * set patient name
          GOTO      DADM5555
.
DADM4000  SUB       ONE,CVERT          * update row counter
          GOTO      DADM2222           * get next patient
.
DADM4444  PACK      PATNAME,SP20,SP20
.
DADM5555  ADD       ONE,MAXRANGE      * display another patient case
          DISPLAY   *P1:CVERT,*V2LON,MAXRANGE,*HOFF,". ",OPDAADMN,SP3:
                    PATNAME,*P58:CVERT,OPDACASE,*P67:CVERT,OPDAEXPS
          PACK      NEXTADMN,OPDAADMN,OPDACASE
          MOVE      OPDACASE,LASTCASE       * save the last case number
          MOVE      OPDAHOSP,LASTHOSP       * save the last hospital id
          GOTO      DADM2222          * lets go and get another one
.
DADM8888  MOVE      ZERO,EXIT 
.
          COMPARE   ZERO,MAXRANGE     * test if any admission found
          GOTO      DADM9999 IF NOT EQUAL
.
          DISPLAY   *P1:24,*B,*EL,"No more Patients booked in session.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
DADM9999  RETURN
+
.*****************************************************************************
.                   SADM0000
.         Select a admission number in the session chosen
.         This routine is closely linked with DADM0000 because it displays
.         the patients in the selected session with a index counter.
.         and Passes MAXRANGE ( maximum number of selectable patients )
.         When a patient is selected the routine reads backwards to the
.         desired record.  This is more efficient than reading forward because
.         of multiple pages.  Furthermore this routine operates the paging
.         capabilities.
.*****************************************************************************
.
SADM0000  CALL      GOPT0111
          BRANCH    EXIT,SADM9999,SADM3333,SADM5555
.
          COMPARE   INDEX,MAXRANGE
          GOTO      SADM0000 IF LESS
          MOVE      MAXRANGE,COUNTER   * set counter to count to index
.
.         PACK      KEY26,SESNDATE,SESNTIME,CLINIC,ZERO,LASTCASE
          PACK      KEY26,SESNDATE,SESNTIME,CLINIC,LASTCASE,ZERO:
                          LASTHOSP,SP70
          CALL      RDOPDEA6
.
SADM2222  COMPARE   INDEX,COUNTER      * test if read backwards enough
          GOTO      SADM3000 IF EQUAL
.
          CALL      RPOPDEA6           * Read backwards to desired item
.
. check if patient has a UR no. If not don't update COUNTER
.
          MOVE      OPDAADMN,KEY8
          CALL      RDMISS1            * get patients UR number from admission
          BRANCH    OVRCD,SADM2222
          MATCH     ZEROUR,AURNO         * ignore patient if zero UR 
          GOTO      SADM2222 IF EQUAL
.
          SUB       ONE,COUNTER        * we have a non zero UR
          GOTO      SADM2222
.
.         record selected - get valid OPRDEAFD record and PMI details
.
SADM3000  MOVE      OPDACASE,CASENUM   * record found lets getsome
          MOVE      OPDAADMN,ADMINNUM
          CALL      RDPD0000           * read patient details
.
          MOVE      ZERO,EXIT          * valid number selected
          GOTO      SADM9999
.
.         redisplay the page
.
SADM3333  PACK      STRTADMN,SP20      * set to start page
          CALL      DADM0000           * redisplay all valid admissions
          GOTO      SADM0000
.
.         Next page selected
.
SADM5555  COMPARE   TEN5,MAXRANGE      * must be a screen full for next page
          GOTO      SADM0000 IF NOT EQUAL
.
          PACK      STRTADMN,NEXTADMN  * set the next page to begin reading 
          CALL      DADM0000
          GOTO      SADM0000
.
SADM9999  RETURN
+
.*****************************************************************************
.                   TEAM0000
.    Get team number from operating team file, if only one team then display
.    else get user to select which operating team
.*****************************************************************************
.
TEAM0000  DISPLAY   *P1:24,*EL;
.
.          PACK      KEY26,SESNDATE,SESNTIME,CLINIC,ZERO,CASENUM
.          CALL      RDOPDEA1                * read details file
.          BRANCH    OVRCD,TEAM3000          * invalid details
.
          PACK      KEY26,SESNDATE,SESNTIME,CLINIC,CASENUM,ZERO,SP70
          CALL      RSOPDEA6
          CALL      RKOPDEA6
          BRANCH    OVRCD,TEAM3000
.
          MATCH     SESNDATE,OPDADATE    * same date
          GOTO      TEAM3000 IF NOT EQUAL
.
          MATCH     SESNTIME,OPDATIME    * same time
          GOTO      TEAM3000 IF NOT EQUAL
.
          MATCH     CLINIC,OPDACLIN      * same clinic
          GOTO      TEAM3000 IF NOT EQUAL
.
          MATCH     "0",DOPDASTA         * same status
          GOTO      TEAM3000 IF NOT EQUAL
.
          MATCH     CASENUM,OPDACASE     * same case number
          GOTO      TEAM3000 IF NOT EQUAL
.
.         Check if we have multiple teams
.
          PACK      KEY11,OPDAUNIQ,TWO      * Just check for team 2
          CALL      RDOPDEB1
          BRANCH    OVRCD,TEAM1000          * No team two.
          GOTO      TEAM4000                * Yes. Ask for which team
.
.         We have at most one team. Now check if we have any teams
.
TEAM1000  PACK      KEY11,OPDAUNIQ,ONE      * Just check for team 1
          CALL      RDOPDEB1
          BRANCH    OVRCD,TEAM2000          * No team one. Check for a def team
          MOVE      OPDBTEAM,TEAMNUM        
          GOTO      TEAM6666
.
.         Check for a default team
.
TEAM2000  CALL      DEFT0000
          BRANCH    EXIT,TEAM3100
          GOTO      TEAM6666
.
.         Operating details have disappeared
.
TEAM3000  DISPLAY   *P1:24,*B,*EL,"Operating details have disappeared.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      TEAM9999
.
.         No operating team details available.
.
TEAM3100  DISPLAY   *P1:24,*B,*EL,"Operating Team Information not on file.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      TEAM9999
.
.         Ask the user to select the team to be used.
.
TEAM4000  CALL      DTEM0000                * display team details
.
TEAM5555  KEYIN     *P50:8,"_",*P50:8,*V2LON,FORM1    * select a team
.
          COMPARE   ZERO,FORM1
          GOTO      TEAM9000 IF EQUAL
.
          PACK      KEY11,OPDAUNIQ,FORM1
          CALL      RDOPDEB1
          BRANCH    OVRCD,TEAM5800          * invalid team number
          GOTO      TEAM6666
.
.         Check for a default team
.
TEAM5800  COMPARE   ONE,FORM1
          GOTO      TEAM5555 IF NOT EQUAL
.
          CALL      DEFT0000
          BRANCH    EXIT,TEAM5555
.
TEAM6666  MOVE      OPDBTEAM,TEAMNUM        * Save team number
          DISPLAY   *P50:8,*V2LON,TEAMNUM,*P1:13,*EF
          PACK      KEY11,OPDAUNIQ,TEAMNUM    * get team selected
          CALL      RDOPDEB1                * get current OPRDEBFD record
.
          MOVE      ZERO,EXIT               * display the team number
          GOTO      TEAM9999
.
TEAM9000  MOVE      ONE,EXIT
.
TEAM9999  RETURN
.*****************************************************************************
.                   DEFT0000
.         Set up default team              
.*****************************************************************************
.
DEFT0000  PACK      DIM42,OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4,OPSETYD5:
                          OPSETYD6,OPSETYD7,OPSETYD8,OPSETYN1,OPSETYN2:
                          OPSETYN3,OPSETYN4,OPSETYN5,OPSETYN6
.
          MATCH     SP42,DIM42                    * are details blank?
          GOTO      DEFT9000 IF EQUAL
.
.         We have a default team
.
          UNPACK    DIM42,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4,OPDBTYD5:
                          OPDBTYD6,OPDBTYD7,OPDBTYD8,OPDBTYN1,OPDBTYN2:
                          OPDBTYN3,OPDBTYN4,OPDBTYN5,OPDBTYN6
          PACK      DIM36,OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4,OPSEDCC5:
                          OPSEDCC6,OPSEDCC7,OPSEDCC8
          UNPACK    DIM36,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4,OPDBDCC5:
                          OPDBDCC6,OPDBDCC7,OPDBDCC8
          PACK      DIM36,OPSENUC1,OPSENUC2,OPSENUC3,OPSENUC4,OPSENUC5,OPSENUC6
          UNPACK    DIM36,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5,OPDBNUC6
          MOVE      ONE,OPDBTEAM
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
.         No default team
.
DEFT9000  MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.                   DTEM0000
.         Display possible Team Numbers
.*****************************************************************************
.
DTEM0000  DISPLAY   *P1:12,*EL,*V2LON,*ULON,*P1:13,*EF,"Team Number":
                    *P15:13,"Chief Surgeon",*P50:13,"Anaesthetist"
.
.         read operation details file to get OPDAUNIQ so can access team file
.
          MOVE      TEN4,CVERT
          MOVE      ZERO,FORM1
DTEM2222  ADD       ONE,FORM1
          COMPARE   ZERO,FORM1
          GOTO      DTEM9999 IF EQUAL
.
          PACK      KEY11,OPDAUNIQ,FORM1    * key of unique number
          CALL      RDOPDEB1                * Read team details
          BRANCH    OVRCD,DTEM2500
          GOTO      DTEM3000
.
.         We may need to check for a default team as the 1st team
.
DTEM2500  COMPARE   ONE,FORM1
          GOTO      DTEM9999 IF NOT EQUAL   * Not team 1 -> we have finished
.
          CALL      DEFT0000
          BRANCH    EXIT,DTEM2222           * no default team
.
.         Get the details of this team
.
DTEM3000  UNPACK    SP20,SURGCODE,ANESCODE  * clear surgeon and anaes. code
          MOVE      ZERO,INDEX              * zero doctor type counter
.
.         get the doctor type
.
DTEM3333  ADD       ONE,INDEX
          LOAD      ACODE,INDEX,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          MATCH     SP3,ACODE
          GOTO      DTEM6666 IF EQUAL       * no code on file
.
          PACK      KEY5,ANSO,ANSP,ACODE
          CALL      RDCODE1                 * get indicator
          BRANCH    OVRCD,DTEM6666
.
          CMATCH    ANSS,TCINDC1
          GOTO      DTEM4444 IF NOT EQUAL   * not a surgeon
.
.         get the doctors code
.
          LOAD      SURGCODE,INDEX,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          GOTO      DTEM6666                * exit loop and get doctors detail
.
.         test if code read is an anaesthetist
.
DTEM4444  CMATCH    ANSN,TCINDC1
          GOTO      DTEM6666 IF NOT EQUAL   * not an anaesthetist
.
          LOAD      ANESCODE,INDEX,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
.
DTEM6666  COMPARE   EIGHT,INDEX
          GOTO      DTEM3333 IF LESS        * more codes to check
.
          PACK      KEY6,SURGCODE,SP6       * get the chief surgeon's name
          CALL      DOCN0000
          DISPLAY   *P6:CVERT,*V2LON,OPDBTEAM,*HOFF,*P15:CVERT,SURGDESC
          PACK      KEY6,ANESCODE,SP6       * get the anesthetist name
          CALL      DOCN0000
          DISPLAY   *P50:CVERT,SURGDESC
.
          ADD       ONE,CVERT               * increment the display position
          GOTO      DTEM2222
.
DTEM9999  RETURN
+
.*****************************************************************************
.                   GOPT0000
.  GOPT is a wonderful routine that gets the item selected or a command option
.  GOPT0111 ->  Select item, (F)irstpage, (N)extpage, (E)xit :
.******************************************************************************
GOPT0000
          BRANCH    EXIT,GOPT0111,GOPT0222,GOPT0333
GOPT0111
          DISPLAY   *P1:24,*EL,"Select item, ":
                    "(",*V2LON,"F",*HOFF,")irstpage, ":
                    "(",*V2LON,"N",*HOFF,")extpage, ":
                    "(",*V2LON,"E",*HOFF,")xit : ";
          MOVE      "F2N3E1",MASK      * set the replace conversion for GOPT
          MOVE      "48",CCOL
          MOVE      ONE,EXIT
          GOTO      GOPT4444
GOPT0222
GOPT0333
GOPT4444
          KEYIN     *PCCOL:24,*JR,*ZF,*V2LON,REPLY
          ENDSET    REPLY
          APPEND    SP2,REPLY
          RESET     REPLY
          REPLACE   UPPLOW,REPLY
.
          TYPE      REPLY              * a number was entered
          GOTO      GOPT5555 IF EQUAL
.
          REPLACE   MASK,REPLY 
          TYPE      REPLY
          GOTO      GOPT9000 IF EQUAL
          BEEP
          GOTO      GOPT0000
GOPT5555
          MOVE      REPLY,INDEX
          COMPARE   INDEX,ZERO
          GOTO      GOPT0000 IF NOT LESS
          MOVE      ZERO,EXIT          * valid number exit=0
          GOTO      GOPT9999
GOPT9000
          MOVE      REPLY,EXIT
.
GOPT9999  RETURN
+
.*****************************************************************************
.                   DOCN0000
.         Setup doctors or nurses name 
.*****************************************************************************
DOCN0000
          MOVE      SP30,SURGDESC      * clear doctors name 
          CALL      RDDOCT1            * get doctor details
          BRANCH    OVRCD,DOCN8888
          GOTO      DOCN3333
.*****************************************************************************
DOCN1111
          MOVE      SP30,SURGDESC      * call for nurses
          CALL      RDOPNUR1           * get operating nurse details
          BRANCH    OVRCD,DOCN8888
          MOVE      OPNRSNAM,DSNAME
          MOVE      OPNRGNAM,DGNAME
          MOVE      SP10,DTITL
.*****************************************************************************
DOCN3333
          MOVE      ONE,PACFRMT        * title name surname of doctor
          MOVE      DSNAME,PACSNAME    * use pacdate to fix doctors name
          PACK      PACGNAME,DGNAME,SP30    
          MOVE      DTITL,PACTITLE
          CALL      PACNAME            * pack up doctors name
          MOVE      PACFNAME,SURGDESC
          GOTO      DOCN9999
DOCN8888
          MOVE      "Unknown",SURGDESC           
DOCN9999  RETURN
+
.
. ------- I/O includes -------
.
          INC       STD001IO           * standard routines
          INC       SEXDSCIO
          INC       OPPRIREC/PBL       * print operation records
          INC       OPRCLIDS/PBL       * clinic ? routine
          INC       OPRCLIIO/INC       * clinic
          INC       OPRDEAIO/INC       * session booking detail file
          INC       OPRDEBIO/INC       * operating team file
          INC       OPRDECIO/INC       * operating room usage
          INC       OPRNURIO/INC       * operating room nurses
          INC       OPRRECIO/INC       * operating record
          INC       OPRSESIO/INC       * session file
          INC       PATDOCDS/PBL       * doctors detail
          INC       PATDOCKY/PBL       * doctors detail
          INC       PATCODIO/INC       * codes file 
          INC       PATDO1IO/INC       * doctors detail
          INC       PATMA1IO/INC       * patient masters file
          INC       PATMI1IO/INC       * patient admission file
          INC       PMSVX1IO/INC       * patient admission file
          INC       PATNOBIO/INC       * no bed
          INC       PATTRNIO/INC       * patient tranfer 
          INC       PATWR1IO/INC       * ward bed file
.

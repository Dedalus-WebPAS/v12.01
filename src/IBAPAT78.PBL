. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAPAT78                                            *
. *                                                                           *
. *     FUNCTION:   CURRENT ADMISSIONS BY RELIGION                            *
. *                                                                           *
. *     DATE WRITTEN:     23/11/87                                            *
. *                                                                           *
. *     AUTHOR:           JENI TAN  (I.B.A)                                   *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
.******************************************************************************
.         V12.00.01 30.05.2025  DA Sarkies     TSK 0955096                    *
.                   Updated for Alpha-Numeric Visit Number                    *
. *****************************************************************************
. *       V11.03.01 19/07/2023 Peter Vela       TSK 0935334                   *
. *                 Added redefined variables for UNIQUE/DUNIQUE              *
. *       V10.04.01 18/06/2014 Steve Armstrong  CAR 301639                    *
. *                 Moved call to TFILENAM for FNAMET to start of program.    *
. *                 Added call to KILLIDX on exit of program.                 *
. *****************************************************************************
. *       V10.03.01 31/12/2012 Patrick Adair  CAR 261630                      *
. *                 Removed port number from temp file name                   *
. *****************************************************************************
. *       V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
. *                 Added pmsvx1af                                            *
. *****************************************************************************
. *       V9.04.01  20/08/2004  Lina Vo                                       *
. *                 Changed Report to allow All Hospitals for Multi-Hospital  *
. *****************************************************************************
. *       V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *****************************************************************************
. *           V5.07.02  15/09/1999 Andrew Peel  SRF 634539                    *
. *                     Fixed page breaks for report                          *
. *           V5.07.01  03/02/1999 Jim Stathopoulos                           *
. *                     Added IBACLOCK in HEAD routine                        *
. *           V5.07.B02 25/01/1999 Jim Stathopoulos                           *
. *                     Report Modifications                                  *
.                   09/11/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
. *           V5.01.121 27/11/90 Glen Hobby                                   *
. *                     Recompiled for changes to                             *
. *                     PATMX1FD                                              *
. *           V5.01.122 04/03/93 Steve O'Gorman                               *
. *                     Allow for Multi Hospital                              *
. *           V5.01.123 25/03/93 Steve O'Gorman                               *
. *                     Exit on Blank Hospital                                *
. *           V5.01.124 27/03/1993  Graeme Williams                           *
. *                     Recompiled for IBACONFD                               *
. *           V5.01.125 25/05/1994 Ian Rutt                                   *
. *                     Fixed Global Recompile Bugs                           *
. *           V5.01.126 06/07/1994 Rob Leonard  SRF 440978                    *
. *                     Fixed Hospital Keyin                                  *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
.
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATMA1FD/INC

          INC       PATCODFD/INC
          INC       PATWR1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
PATTMP    IFILE     SQL, FIXED=84, KEY="U1-11"
.
.               WORK AREA
.----------------------------------------------------------------
.Name     Type     Length     Physical     Start
.----     ----     ------     --------     -----
.PREG     DIM         3             3         1
DUNIQUE  DIM         8             8         4
.AWARD    DIM         3             3        12
.ABED     DIM         3             3        15
.AURNO    DIM         8             8        18
.AADMNO   DIM         8             5        26
.ADATE    DIM         8             8        31
.PSNAME   DIM        20            20        39
.PGNAME   DIM        25            25        59
.
.  END OF RECORD                             84
.
.  redefine form fields from key
.  -----------------------------
.Name     Type     Length     Start
.----     ----     ------     -----
UNIQUE    FORM        8          4
.----------------------------------------------------------------
SPREG     DIM       3
.
.
FNAMET    DIM       8
.
.      EXTRA VARIABLES
.
CMDLINE   DIM       50
CODER     INIT      "R "
.
SKIP      FORM      1
.
DALPHA    DIM       6
DNAME     DIM       30
.
.
PRGID     INIT      "IBAPAT78"
PRGNAM    INIT      "Current Admissions By Religion"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,*EL,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN       CONTROLF,"controlf"
          READ       CONTROLF,ZERO;*43,IBCNMHOS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMET
.
          MOVE       ZERO,CNOUNDLN
.
.             SELECT OPTION
.
          DISPLAY   *P1:4,*EF;
          CALL      HOSP0000                     * Keyin Hospital Code
          BRANCH    EXIT,START,ENDIT
.
START     KEYIN     *P1:4,*EF:
                    *P1:4,*V2LON,*DV,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,*DV,ONE,*HOFF," = Religion Sequence":
                    *P1:7,"Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF PSKIP
          BEEP
          GOTO      START
.
NOADMN    DISPLAY   *P20:24,*B,*EL,*V2LON:
                    "** No Current Admissions by Religion **",*W3;
          GOTO      START
.
PSKIP     KEYIN     *P1:9,"Do you want a new page for each Religion (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*EL,*V2LON,ANS;
.
          CMATCH    "Y",ANS
          GOTO      PSKIPY IF EQUAL
.
          CMATCH    "N",ANS
          GOTO      PSKIPN IF EQUAL
.
          BEEP
          GOTO      PSKIP
.
PSKIPY    MOVE      ZERO,SKIP
          GOTO      ADMNNO
.
PSKIPN    MOVE      ONE,SKIP
+
.
.          GET ONLY CURRENT ADMISSIONS..IE ASTAT=2
.
ADMNNO    CALL      CREAIDX
.
          OPEN      PATTMP,FNAMET
          MOVE      ONE TO UNIQUE
.
          PACK      KEY9 WITH TWO,SP8
          CALL      RDSMISS2
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Scanning ADMISSIONS **",*W2;
.
          DISPLAY   *P20:24,*EL,"Admission :";
.
NEXTA     CALL      RDKMISS2
          BRANCH    OVRCD OF SORTF
.
          COMPARE   TWO,ASTAT
          GOTO      SORTF IF NOT EQUAL
.
          DISPLAY   *P32:24,*V2LON,AADMNO;
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,NEXTA
.
          MATCH     SP3,AWARD
          GOTO      ENDWRD IF NOT EQUAL
.
.         CHECK FOR STANDBY PATIENTS
.
          MOVE       AADMNO,WSTBY
          PACK       KEY14 WITH WSTBY,SP6
          CALL       RDSWARD4
          CALL       RDKWARD4
          BRANCH     OVRCD OF ENDWRD
.
          MATCH     AADMNO,WSTBY
          GOTO      ENDWRD IF NOT EQUAL
.
          MOVE      WWARD,AWARD
          MOVE      WBED,ABED
.
ENDWRD    IF        IBCNMHOS=1
            MATCH     SP3,PTHSHOSP
            GOTO      ENDWRD1 IF EQUAL
            MATCH     SP3,AWARD
            IF        !@EQUAL
              PACK      KEY6,AWARD,SP3
              CALL      RDWARD1
              BRANCH    OVRCD,ENDWRD1
.
              MATCH     PTHSHOSP,PTWDHOSN        * Different Hospital
              GOTO      NEXTA IF NOT EQUAL
            ENDIF
          ENDIF
.
ENDWRD1   PACK      KEY11 WITH PREG,UNIQUE
          CALL      WRTEMPD
          ADD       ONE,UNIQUE
          GOTO      NEXTA
.
.
.
SORTF     DISPLAY   *P1:14,*EF
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Religion :";
.
          MOVE      SP20,KEY11
          CALL      RDSSORTD
NEXTS     CALL      RDKSORTD
          BRANCH    OVRCD OF ENDP
.
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
          CALL      DISPDA
.
          DISPLAY   *P32:24,*V2LON,PREG;
          GOTO      NEXTS
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOADMN IF EQUAL
.
          CALL      UND132
          PRINT     *N,"  ***  END OF REPORT  ***"
.
          CALL      KILLIDX
.
          DISPLAY   *P20:24,*EL,*B,*V2LON:
                    "** Report Generation Completed **",*W3;
          GOTO      START
+
.
.     PRINT ACTUAL DATA
.
DISPDA    MATCH     PREG,SPREG
          GOTO      CNTPRT2 IF EQUAL
.
          BRANCH    SKIP OF CHKHD
.
          MOVE      "60",CLNO
.
CHKHD     COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      "UNKNOWN RELIGION   ",TDESC
          PACK      KEY5 WITH CODER,PREG
          CALL      RDCODE1
.
          PRINT     *1,"! ",PREG,*10,"! ",TDESC:
                    *42,"!",AURNO,*51,"! ",PSNAME,*74,PGNAME:
                    *100,"! ",AWARD,"/",ABED:
                    *110,"! ",AADMNO:
                    *120,"! ",XDAY,"/",XMON,"/",XCENT,XYEAR,*132,"!":
                    *N,*1,"!",*10,"!",*42,"!",*51,"!":
                    *100,"!",*110,"!",*120,"!",*132,"!"
.
          MOVE      PREG,SPREG
          ADD       TWO,CLNO
          RETURN
.
CNTPRT2   COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"!",*10,"!":
                    *42,"!",AURNO,*51,"! ",PSNAME,*74,PGNAME:
                    *100,"! ",AWARD,"/",ABED:
                    *110,"! ",AADMNO:
                    *120,"! ",XDAY,"/",XMON,"/",XCENT,XYEAR,*132,"!":
                    *N,*1,"!",*10,"!",*42,"!",*51,"!":
                    *100,"!",*110,"!",*120,"!",*132,"!"
.
          ADD       TWO,CLNO
          COMPARE   "55",CLNO
          RETURN    IF LESS
          MOVE      SP3,SPREG
          RETURN
+
.*********************************************************************
.*        HOSP0000                               Called by ML0000    *
.*        Get hospital code                                          *
.*********************************************************************
HOSP0000  IF        IBCNMHOS=1
            DISPLAY   *P1:4,"    Hospital    : "
            MOVE      "4",EVERT
            MOVE      "19",ECOL
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
            OPEN      PATHSPA1,"pathspaf"
            CALL      PATHSPKY
            BRANCH    EXIT,HOSP2000,HOSP9999
            GOTO      HOSP3000
.
HOSP2000    MOVE      "All Hospitals",PTHSNAME
            MOVE      SP10,PTHSHOSP
.
HOSP3000    CLOSE     PATHSPA1
            DISPLAY   *P25:CVERT,PTHSNAME;
          ENDIF
.
HOSP9999  RETURN
+
.
.*********************************************************************
.*        HEAD                                                       *
.*        HEADINGS FOR OUTPUT                                        *
.*********************************************************************
.
HEAD      CALL      IBACLOCK
          CALL      HEAD132
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital  :  ",PTHSHOSP,SP3,PTHSNAME:
                      *N
          ENDIF
.
          CALL      UND132
          PRINT     *1,"!Religion",*10,"! Description ":
                    *42,"! U/R no",*51,"! Patients Name":
                    *100,"! Ward/Bed",*110,"! Admn No":
                    *120,"! Admn Date",*132,"!"
          CALL      UND132
.
.    *** CAUSES RELIGION TO BE PRINTED ON A NEW PAGE NO MATTER WHAT
.
          MOVE      SP3,SPREG
          IF        IBCNMHOS=0
            MOVE      EIGHT,CLNO
          ELSE
            MOVE      TEN,CLNO
          ENDIF
          RETURN
+
.       Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLOSE     PATTMP
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:9,*EF;
          RETURN
.
CREAIDX   CALL      KILLIDX
.
          DISPLAY   *P1:13,*EF,*P1:14;
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 84 U1-11",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          DISPLAY   *P1:13,*EF,*P1:14
          RETURN
.
+
WRTEMPD   MOVE      UNIQUE,DUNIQUE
          WRITE     PATTMP,KEY11;PREG,DUNIQUE,AWARD,ABED,AURNO,AADMNO,ADATE:
                                 PSNAME,PGNAME
          RETURN
.
RDSSORTD  READ      PATTMP,KEY11;;
          RETURN
.
RDKSORTD  MOVE      ZERO,OVRCD
          READKS    PATTMP;PREG,DUNIQUE,AWARD,ABED,AURNO,AADMNO,ADATE:
                                 PSNAME,PGNAME
          GOTO      OVERCOND IF OVER
          MOVE      DUNIQUE,UNIQUE 
          RETURN
.
          INC       PATHSPKY
          INC       PATHSPIO/INC
          INC       TFILENAM                     * Generate Temp File Name

          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATWR1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
          INCLUDE   STD001IO
.
ENDIT     CALL      KILLIDX                      * remove temp file
          CHAIN     PGM
          STOP

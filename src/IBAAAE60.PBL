.*******************************************************************************
.*               P R O G R A M  S U M M A R Y                                  *
.*               -------------  -------------                                  *
.* PROGRAM        : IBAAAE60                                                   *
.*                                                                             *
.* FUNCTION       : CHARGE MASTER LISTING                                      *
.*                                                                             *
.* DATE WRITTEN   : 02/09/88                                                   *
.*                                                                             *
.* AUTHOR         : M.HAYSE                                                    *
.*                                                                             *
.* MODIFICATIONS  :                                                            *
.*        V9.02.00  13/10/2001 J.Tan                                           *
.*                  Changed patnip file
.*        V5.01.02  15/05/1991    Justin Coates       Quote 6943 SRF 108605    *
.*                  Added the description of NBINDC if AECNCHRS = 1            *
.*                  and modularized the code                                   *
.*******************************************************************************
.
          INC       STD001FD                * standard variables
          INC       PATCODFD/INC            * Codes file
          INC       PATNIPFD/INC            * Billing charges file
.
AECNCHRS  FORM      1         * charging by discharge status 1 = Yes
.
.         Program variables
.
LN55      FORM      "55"
.
.
CATED     INIT      "ED"      * discharge status
CATT      INIT      "T "
CATCL     INIT      "CL"
CATCI     INIT      "CI"
.
DESCRESD  DIM       20        * description - resident status
DESCCOMP  DIM       20        * description - compensation code
DESCINDC  DIM       20        * description - discharge status 
.
PRGID     INIT      "IBAAAE60"
PRGNAM    INIT      "Charge Master Listing"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000                * initialization
.
ML1000    CALL      MENU0000                * see if to print report
          BRANCH    EXIT,ML9999             * exit selected
.
ML2000    CALL      REPT0000                * produce the report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
          STOP
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization                                             *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patnipbf";
          OPEN      PATNIPB1,"patnipbf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY;*196,AECNCHRS
.
INIT9999  RETURN
+
.*********************************************************************
.*                  MENU0000                    Called by : ML1000   *
.*        See if to print the report                                 *
.*********************************************************************
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print charges"
.
MENU1000  KEYIN     *P1:8,*EF,"Please Select : ",*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9100 IF EQUAL         * exit
.
          BRANCH    MOPTION,MENU9000
.
          BEEP
          GOTO      MENU1000
.
MENU9000  MOVE      ZERO,EXIT
          GOTO      MENU9999
.
MENU9100  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.*********************************************************************
.*                  REPT0000                    Called by : ML1000   *
.*        Produce the report                                         *
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "*** Generating ***"
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P10:24,"Printing Charges : ";
.
          PACK      KEY13 WITH ONE,SP10
          CALL      RDSNIPB1
.
. ------- loop over PATNIPFD -------
.
REPT1000  CALL      RDKNIPB1
          BRANCH    OVRCD OF REPT9000       * end of file
.
          COMPARE   ONE,NBSYST
          GOTO      REPT9000 IF NOT EQUAL       * finished A&E patients
.
. ------- validate which item to use -------
.
          BRANCH    AECNCHRS,REPT2000       * using discharge status
.
.         not using discharge status
.
          MATCH     SP3,NBINDV
          GOTO      REPT3000 IF EQUAL       * valid item to display
          GOTO      REPT1000                * invalid item
.
.         using discharge status
.
REPT2000  MATCH     SP3,NBINDV
          GOTO      REPT1000 IF EQUAL       * no code therefore dont display
.
.         get the descriptions
.
REPT3000  MOVE      SP30,DESCRESD           * resident status
          PACK      KEY5 WITH CATT,NBRESD
          CALL      RDCODE1
          BRANCH    OVRCD OF REPT4000
          MOVE      TDESC,DESCRESD
.
REPT4000  MOVE      SP30,DESCCOMP           * compensation code
          PACK      KEY5 WITH CATCL,NBCOMP
          CALL      RDCODE1
          BRANCH    OVRCD OF REPT4100
          MOVE      TDESC,DESCCOMP
.
REPT4100  MOVE      SP20,DESCINDC           * discharge status
          COMPARE   ONE,AECNCHRS
          GOTO      REPT5000 IF NOT EQUAL   * not using status
.
          MATCH     SP3,NBINDV  
          GOTO      REPT5000 IF EQUAL       * no code to display
.
          PACK      KEY5,CATED,NBINDV
          CALL      RDCODE1
          BRANCH    OVRCD,REPT5000
          MOVE      TDESC,DESCINDC
.
REPT5000  DISPLAY   *P45:24,*V2LON,NBRESD,*P50:24,NBCOMP,*P55:24,NBINDV
.
          CALL      PLIN0000
          GOTO      REPT1000
.
.         end of the report
.
REPT9000  COMPARE   ZERO,CPAGENO
          GOTO      REPT9100 IF EQUAL       * no data printed
.
          PRINT     *N,*N,"***  END OF REPORT  ***"
          DISPLAY   *P1:24,*EL,*P15:24,*V2LON:
                    "*** Report Generation Completed ***"
          GOTO      REPT9999
.
REPT9100  DISPLAY   *P1:24,*B,"No Charge Data Available.  ",*EL;
          CALL      HITENTER 
.
REPT9999  RETURN
+
.*********************************************************************
.*                  PLIN0000                    Called by : REPT5000 *
.*        Print the line of data                                     *
.*********************************************************************
PLIN0000  COMPARE   LN55,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,NBRESD,SP1,DESCRESD,*26,NBCOMP,SP1,DESCCOMP,*51;
.
          COMPARE   ONE,AECNCHRS
          GOTO      PLIN1000 IF NOT EQUAL   * dont print discharge status
.
          PRINT     NBINDV,SP1,DESCINDC,*76;
.
PLIN1000  PRINT     NBITEM,SP2,NBDESC,SP1,"$ ",NBAMNT,*N;
.
          ADD       ONE,CLNO
.
PLIN9999  RETURN
+
.*********************************************************************
.*                  HEAD0000                    Called by : xxxx0000 *
.*        Print a new page heading                                   *
.*********************************************************************
HEAD0000  CALL      HEAD132                 * print heading
          PRINT     *1,"Resident Status",*26,"Compensation Code",*51;
.
          COMPARE   ONE,AECNCHRS
          GOTO      HEAD1000 IF NOT EQUAL   * not printing discharge status
.
          PRINT     "Discharge Status",*76;
.
HEAD1000  PRINT     "Item",SP1,"Description",SP20,"Amount",*N:
                    *1,"------------------------":
                    *26,"------------------------",*51;
.
          COMPARE   ONE,AECNCHRS
          GOTO      HEAD2000 IF NOT EQUAL   * not printing discharge status
.
          PRINT     "-----------------------",*76;
.
HEAD2000  PRINT     "----",SP1,"------------------------------":
                    SP1,"-----------",*N,*N;
.
          MOVE      SEVEN,CLNO
.
HEAD9999  RETURN
+
.
.         I/O includes needed
.
          INC       STD001IO                * standard routines
          INC       PATNIPIO/INC            * Billing file
          INC       PATCODIO/INC            * codes file
................................................................................

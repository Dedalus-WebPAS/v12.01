.******************************************************************************
.* System   : PRIVATE PRACTICE SYSTEM                                         *
.* Program  : IBAPRI80                                                        *
.* Desc     : MONTHLY STATISTICAL UPDATE                                      *
.******************************************************************************
.* Date     : 26/11/91                                                        *
.* Author   : DIG                                                             *
.* Comment  : This program is a modified version of IBAPPP80.                 * 
.* Mods     :                                                                 *
.*----------------------------------------------------------------------------*
.* V11.04.01 11/04/2024  J.Tan         TSK 0941662                            *
.*           Mod PRDTGSTM - GST of Total amount(Item amount x Quantity)       *
.* V10.13.01 07/08/2018  Tracey Nguyen    TSK 0848506                         *
.*           1)Changed SAVKEY16 to SAVKEY20                                   *
.*           2)Changed KEY16 to KEY20 for RDPRST1, RSPRST1 (pristatf)         *
.*----------------------------------------------------------------------------*
.*        V9.10.01  21/05/07 J.Tan    CAR 147470                              *
.*                  Fixed generating cr.notes for invoices raised last year   *
.*        V9.09.01  23/10/07 J.Tan    CAR 147470                              *
.*                  Mods to include credit notes to Journals                  *
.*        V9.04.01  16/08/05 Davin    CAR 62750                               *
.*                  Mods for removing redefined amount variables              *
.*        V9.03.00  06/02/2004  Lina Vo    CAR 44131                          *
.*                  Ported to V9.03                                           *
.*        V5.08.01  09/05/2000  J.Tan                                         *
.*                  Mods to include GST amount                                *
.*        V5.07.02  22/06/1999  Jill Habasque                                 *
.*                  Recompiled for PRISTAIO                                   *
.*        V5.07.01  09/06/1999  Greg Horvat                                   *
.*                  Recompiled for PRIINVIO - Fixed problem with direct read  *
.*                                                                            *
.*            V6.00.01 31/08/92 Steve Armstrong                               *
.*                     Commented out PRCNSDAT as not being used               *
.*            V6.00.02 07/10/92  Steve Armstrong                              *
.*                     Modified for alphanumeric debtor number                *
.*            V6.00.03 08/01/93 J.Tan                                         *
.*                     Mods to use date range file                            *
.*            V6.00.04 03/03/92 J.Tan    SRF 120827                           *
.*                     Fixed receipt amnts mult.by negative in pristat        *
.*            V6.00.05 02/12/93 Greg Horvat                                   *
.*                     Changed keyed in date to date range                    *
.*                     07/12/1993  Graeme Williams                            *
.*                     Use 4th index of pridtraf, not 1st (PRI35 uses 4th)    *
.*            V5.02.01 08/12/1993  Greg Horvat                                *
.*                     Display CPCDATE when showing date range                *
.*            V5.02.02 08/05/1995   Gabrielle   SRF  135709                   *
.*                     When re-calc opening balance subtract cash receipts    *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PRICONFD/INC
          INC       PRIDTRFD/INC
          INC       PRIINVFD/INC
          INC       PRISTAFD/INC
          INC       PRICNOFD/INC
          INC       PATDRGFD/INC
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
COUNT     FORM      4 
CPTDATE   DIM       8
.
FINIDTE   DIM       8
FORM82    FORM      8.2
FORM12P2  FORM      12.2
FROM      FORM      4
.
IBCNUGST  FORM      1
MONTH     DIM       9
.
SAVAMNT   FORM      12.2
SAVKEY20  DIM       20
SELMTH    DIM       6
STRTDTE   DIM       8
STRTNUM   DIM       2
STRTYEAR  DIM       4
.
TEMPOPEN  FORM      12.2
TO        FORM      4
TODATE    DIM       6
TOTCASH   FORM      12.2
TOTINV    FORM      12.2
TOTJOUR   FORM      12.2
+
.**********************************************************************
.*                       GLOBAL CONSTANTS                             *
.**********************************************************************
.
PRGID     INIT      "IBAPRI80"
PRGNAM    INIT      "MONTHLY STATISTICAL UPDATE"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
.
ML0000    CALL      INIT0000                * initialization
.                                             open files
.                                             display header
.
.------ display the program description ------
.
ML1000    CALL      DISP0000                * display program description
.
          CALL      CONT0000                * Ok to proceed ?
          BRANCH    EXIT,ML9999
.
.------ keyin the from date ------
.
ML2000    CALL      DATF0000                * Keyin fromdate
.
          CALL      DATT0000                * Keyin todate
          BRANCH    EXIT,ML1000 
.
          CALL      CONT0000                * Ok to proceed ?
          BRANCH    EXIT,ML1000
.
          CALL      REST0000                * Reset relevant records in stats 
.                                             file
          CALL      GINV0000                * Generating invoices
.
          CALL      GCAJ0000                * Generating cash & journals
          CALL      CRDN0000                * Check for Credit notes if any
.
          CALL      AJOP0000                * Adjusting opening balances
.
          CALL      END0000                 * End of processing
          BRANCH    EXIT,ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD                * display screen header
.
          DISPLAY   *P50:24,"Opening ":
                    *P60:24,"pridtraf";
          OPEN      PRIDTRA2,"pridtraf"
          OPEN      PRIDTRA4,"pridtraf"
.
          DISPLAY   *P60:24,"priinvof";
          OPEN      PRIINVO1,"priinvof"
          OPEN      PRIINVO2,"priinvof"
.
          DISPLAY   *P60:24,"pristatf";
          OPEN      PRISTAT1,"pristatf"
          OPEN      PRISTAT2,"pristatf"
.
          DISPLAY   *P60:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          OPEN      PRICNOA4,"pricnoaf"
.
          DISPLAY   *P60:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*85,IBCNUGST
.
          RETURN
+
.**********************************************************************
.*                               DISP0000                             *
.*                  Display the program description                   *
.**********************************************************************
DISP0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"This program recreates ",*V2LON:
                          "Medical Practice/Doctor statistics ",*HOFF:
                          "from a given date":
                    *P1:5,"range from items in the transaction file, i.e. all ":
                          "invoices raised, cash":
                    *P1:6,"receipts and journals that have been put into the ":
                          "system. The financial figures":
                    *P1:7,"for a month are replaced and subsequent ":
                          "opening balances are recalculated.":
                    *P1:9,*V2LON,"Please ensure that no private ":
                          "practice transactions are put through the system":
                    *P1:10,"until the program has completed its run.";
.
DISP9999  RETURN
+
.**********************************************************************
.*                               CONT0000                             *
.*          See if O.K. to proceed with the stats update              *
.**********************************************************************
CONT0000  MOVE      ZERO,EXIT
.
.------ checking date range ------
. 
CONT0500  MATCH     STRTDTE,DRGTDTE
          IF        @LESS
            DISPLAY   *EL,*B,*V2LON,*P1:24,"From date greater than To date.  ";
            CALL      HITENTER
            GOTO      CONT9000
          ENDIF
.
.------ see if O.K. to proceed ------
.
CONT1000  MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"Ok to proceed (",*V2LON,"Y",*HOFF,"/":
                           *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
.
          REPLACE   "nNyY",ANS
.
          CMATCH    ANSY,ANS                * see if Yes entered
          GOTO      CONT9999 IF EQUAL
.
          CMATCH    ANSN,ANS                * see if No entered
          GOTO      CONT9000 IF EQUAL
.
          BEEP                              * invalid option
.
          GOTO      CONT1000
.
.------ we do not want to continue ------
.
CONT9000  MOVE      ONE,EXIT                * Cancel
.
CONT9999  RETURN
+
.**********************************************************************
.*                                DATF0000                            *
.*                          Keyin the from date                       *
.**********************************************************************
DATF0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:12,*EF,"From Date : ";
.
          MOVE      TEN2,CVERT
          MOVE      TEN3,CCOL
          UNPACK    SP4,CYEAR,CMON
          MOVE      CCC,CCENT
.
          MOVE      ZERO,CHIGHLT
          CALL      KEYPER
          BRANCH    OVRCD,DATF2000
.
          PACK      KEY6,ICENT,IYEAR,IMON
          MOVE      KEY6,TODATE
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DATF1000
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P38:CVERT,DRGLDSC,*P60:CVERT,CPCDATE;
          PACK      SELMTH,DRGYR,DRGNUM
          REP       " 0",SELMTH
          MOVE      DRGFDTE,STRTDTE
          MOVE      DRGNUM,STRTNUM
          MOVE      DRGYR,STRTYEAR
          GOTO      DATF9999
.
DATF1000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Date not found in Date Range file **    ";
          CALL      HITENTER
          GOTO      DATF0000
.
.------ checking for spaces ------
.
DATF2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Date range must not be blank.  ";
          CALL      HITENTER 
          GOTO      DATF0000
.
DATF9999  RETURN
+
.**********************************************************************
.*                                DATT0000                            *
.*                          Keyin the from date                       *
.**********************************************************************
DATT0000  DISPLAY   *P1:14,*EF,"To Date : ";
.
          MOVE      SP8,DRGFDTE
          MOVE      SP8,DRGTDTE
          MOVE      SP4,DRGYR
          MOVE      SP2,DRGNUM 
          MOVE      SP20,DRGLDSC
          MOVE      TEN4,CVERT
          MOVE      TEN3,CCOL
          UNPACK    TODATE,CCENT,CYEAR,CMON
.
          MOVE      ZERO,CHIGHLT
          CALL      KEYPER
          BRANCH    OVRCD,DATT9999
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DATT1000
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P38:CVERT,DRGLDSC,*P60:CVERT,CPCDATE;
          MOVE      DRGTDTE,FINIDTE
          GOTO      DATT9999
.
DATT1000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Date not found in Date Range file **    ";
          CALL      HITENTER
          GOTO      DATT0000
.
DATT9999  RETURN
+
.**********************************************************************
.*                                REST0000                            *
.*       Reset all the relevant records on the stats file to zero     *
.**********************************************************************
REST0000  DISPLAY    *P1:17,*EF,*V2LON,"*** Resetting Statistics ***",*HOFF:
                     *P1:19,"Practice : ":
                     *P1:20,"Doctor   : ":
                     *P1:21,"Year     : ";
.
          PACK       KEY20,STRTYEAR,SP20
          CALL       RSPRST1                * position on the financial 
.                                             statistics file
.------ read through the financial statistics file ------
.
REST1000  CALL       RKPRST1
          BRANCH     OVRCD,REST9999
.
          MATCH      PRSTYEAR,DRGYR
          GOTO       REST9999 IF LESS
.
          DISPLAY    *P12:19,*V2LON,PRSTPRAC:
                     *P12:20,PRSTDOCT:
                     *P12:21,PRSTYEAR;
.
          MOVE       ZERO,COUNT
.
REST1500  MATCH      PRSTYEAR,STRTYEAR
          IF         @EQUAL 
            MOVE       STRTNUM,FROM
          ELSE
            MOVE       ONE,FROM
          ENDIF
.
          MATCH      PRSTYEAR,DRGYR
          IF         @EQUAL
            MOVE       DRGNUM,TO
          ELSE
            MOVE       TEN3,TO
          ENDIF
.
          MOVE       FROM,COUNT
          SUB        ONE,COUNT
.
.------ reset all the relevant amounts ------
.
REST2000  ADD        ONE,COUNT
.
          COMPARE    COUNT,TO               * see if we have processed all of
          GOTO       REST3000 IF LESS         the periods
.
          STORE      ZERO,COUNT,PRSTIP01,PRSTIP02,PRSTIP03,PRSTIP04:
                                PRSTIP05,PRSTIP06,PRSTIP07,PRSTIP08:
                                PRSTIP09,PRSTIP10,PRSTIP11,PRSTIP12:
                                PRSTIP13
          STORE      ZERO,COUNT,PRSTCP01,PRSTCP02,PRSTCP03,PRSTCP04:
                                PRSTCP05,PRSTCP06,PRSTCP07,PRSTCP08:
                                PRSTCP09,PRSTCP10,PRSTCP11,PRSTCP12:
                                PRSTCP13
          STORE      ZERO,COUNT,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04:
                                PRSTJP05,PRSTJP06,PRSTJP07,PRSTJP08:
                                PRSTJP09,PRSTJP10,PRSTJP11,PRSTJP12:
                                PRSTJP13
.
          GOTO       REST2000
.
.------ update the financial statistics file ------
.
REST3000  CALL       UPPRST1
.
          GOTO       REST1000
.
REST9999  RETURN
+
.**********************************************************************
.*                              GINV0000                              *
.*     Routine to generate the invoiced amounts to update the stats   *
.*     file with                                                      *
.**********************************************************************
GINV0000  DISPLAY    *P1:17,*EF,*V2LON,"*** Generating Invoices ***",*HOFF:
                     *P1:19,"Practice : ":
                     *P1:20,"Doctor   : ":
                     *P1:21,"Date     : ";
.
          PACK       KEY16,STRTDTE,SP20
          CALL       RSPRIN2                * position on the invoice file
.
.------ read through the invoice file ------
.
GINV1000  CALL       RKPRIN2
          BRANCH     OVRCD,GINV9999
. 
          MATCH      PRINDATE,FINIDTE
          GOTO       GINV9999 IF LESS
.
          UNPACK     PRINDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL       PACDATE                * pack the date
.
.------ display the details ------
.
          DISPLAY    *P12:19,*V2LON,PRINPRAC:
                     *P12:20,PRINDOCT:
                     *P12:21,CPCDATE;
.
          MOVE       ZERO,TOTINV
.
          PACK       KEY22,PRINNUMB,SP30
          CALL       RSPRDT4                * position on the debtors 
.                                             transaction file
.------ read through the debtors transaction file ------
.
GINV1100  CALL       RKPRDT4
          BRANCH     OVRCD,GINV2000
.
          MATCH      PRINNUMB,PRDTINVN      * match invoice numbers
          GOTO       GINV2000 IF NOT EQUAL
.
          BRANCH     PRDTRTYP,GINV1200      * make sure we have the right
.                                             transaction type
          GOTO       GINV1100
.
GINV1200  MOVE       PRDTAMNT,FORM12P2
          IF         IBCNUGST=2
            ADD        PRDTGSTM,FORM12P2
          ENDIF
          ADD        FORM12P2,TOTINV
.
          GOTO       GINV1100
.
.------ Determine financial month and year of statistics ------
.
GINV2000  PACK       CPTDATE,PRINDATE
          REP        " 0",CPTDATE
          CALL       GPERD
          COMPARE    ONE,EXIT
          IF         @EQUAL
            DISPLAY    *P1:24,*EL,*B,"Date not found in date range period ":
                              "file - Hit <",*V2LON,"ENTER",*HOFF,"> to":
                              " continue";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       GINV2000
          ENDIF
.
          MOVE       DRGNUM,COUNT
          MOVE       DRGYR,PRSTYEAR
          MOVE       PRINPRAC,PRSTPRAC
          MOVE       PRINDOCT,PRSTDOCT
.
          PACK       KEY20,PRSTYEAR,PRSTPRAC,PRSTDOCT
          CALL       RDPRST1                * read the financial statistics 
          BRANCH     OVRCD,GINV3000           file
.
          CALL       ADDI0000               * Add figures to correct month
.
          CALL       UPPRST1                * update the financial statistics
.                                             file
          GOTO       GINV1000
.
.------ record is not on the financial statistics file ------
.
GINV3000  CALL       CLER0000               * Clear current stats variables
.
          CALL       ADDI0000               * Add figures to correct month
.
          CALL       WRPRST1                * write to the financial statistics
.                                             file
          GOTO       GINV1000
.
GINV9999  RETURN
+
.**********************************************************************
.*                                GCAJ0000                            *
.*       Routine to generate the cash and journals to update the      *
.*       stats file with                                              *
.**********************************************************************
GCAJ0000  DISPLAY    *P1:17,*EF,*V2LON,"*** Generating Cash/Journals ***":
                     *HOFF,*P1:19,"Practice : ":
                     *P1:20,"Doctor   : ":
                     *P1:21,"Date     : ";
.
          PACK       KEY30,STRTDTE,SP20,SP5
          CALL       RSPRDT2                * position on the debtors 
.                                             transaction file
.------ read through the debtors transaction file
.
GCAJ1000  CALL       RKPRDT2
          BRANCH     OVRCD,GCAJ9999
.
          MATCH      PRDTSDAT,FINIDTE
          GOTO       GCAJ9999 IF LESS
. 
          MOVE       ZERO,TOTCASH
          MOVE       ZERO,TOTJOUR
.
          BRANCH     PRDTRTYP,GCAJ1000,GCAJ2000,GCAJ3000   * branch on the
.                                                            transaction type
          GOTO       GCAJ1000
.
.------ transaction is a cash receipt ------
.
GCAJ2000  MOVE       PRDTAMNT,TOTCASH
          MULT       "-1",TOTCASH
.
          GOTO       GCAJ4000
.
.------ transaction is a journal ------
.
GCAJ3000  MOVE       PRDTAMNT,TOTJOUR
          IF         IBCNUGST=2
            ADD        PRDTGSTM,TOTJOUR
          ENDIF
.
.------ display the details ------
.
GCAJ4000  UNPACK     PRDTSDAT,CCENT,CYEAR,CMON,CDAY
.
          CALL       PACDATE                * pack the date
.
          DISPLAY    *P12:19,*V2LON,PRDTPRAC:
                     *P12:20,PRDTDOCT:
                     *P12:21,CPCDATE;
.
GCAJ4500  PACK       CPTDATE,PRDTSDAT
          REP        " 0",CPTDATE
          CALL       GPERD
          COMPARE    ONE,EXIT
          IF         @EQUAL
            DISPLAY    *P1:24,*EL,*B,"Date not found in date range period ":
                              "file - Hit <",*V2LON,"ENTER",*HOFF,"> to":
                              " continue";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       GCAJ4500
          ENDIF
.
          MOVE       DRGNUM,COUNT
          MOVE       DRGYR,PRSTYEAR
          MOVE       PRDTPRAC,PRSTPRAC
          MOVE       PRDTDOCT,PRSTDOCT
.
          PACK       KEY20,PRSTYEAR,PRSTPRAC,PRSTDOCT
          CALL       RDPRST1                * position on the financial
          BRANCH     OVRCD,GCAJ5000           statistics file
.
          CALL       ADDJ0000               * Add figures to correct month
.
          CALL       UPPRST1                * update the financial statistics 
.                                             file
          GOTO       GCAJ1000
.
.------ record not on the financial statistics file ------
.
GCAJ5000  CALL       CLER0000               * Clear variables
.
          CALL       ADDJ0000               * Add figures to correct month
.
          CALL       WRPRST1                * update the financial statistics
.                                             file
          GOTO       GCAJ1000
.
GCAJ9999  RETURN
+
.******************************************************************************
.*        CRDN0000 - Credit for credit note                                   *
.******************************************************************************
CRDN0000  DISPLAY    *P1:17,*EF,*V2LON,"*** Generating Credit Notes ***"
.
          PACK      KEY16,STRTDTE,SP70
          CALL      RSPRCNO4
CRDN1000  CALL      RKPRCNO4
          BRANCH    OVRCD,CRDN9999
          MATCH     PRCODATE,FINIDTE
          GOTO      CRDN9999 IF LESS
.
          MOVE       PRCOINVN,KEY8
          CALL       RDPRIN1
          BRANCH     OVRCD,CRDN1000
.
          MOVE      PRCOAMNT,FORM12P2
.
.------ Determine financial month and year of statistics ------
.
CRDN2000  PACK       CPTDATE,PRCODATE
          REP        " 0",CPTDATE
          CALL       GPERD
          COMPARE    ONE,EXIT
          IF         @EQUAL
            DISPLAY    *P1:24,*EL,*B,"Date not found in date range period ":
                              "file - Hit <",*V2LON,"ENTER",*HOFF,"> to":
                              " continue";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       CRDN2000
          ENDIF
.
          MOVE       DRGNUM,COUNT
          MOVE       DRGYR,PRSTYEAR
          MOVE       PRINPRAC,PRSTPRAC
          MOVE       PRINDOCT,PRSTDOCT
.
          PACK       KEY20,PRSTYEAR,PRSTPRAC,PRSTDOCT
          CALL       RDPRST1                * read the financial statistics
          BRANCH     OVRCD,CRDN5000           statistics file
.
          CALL       ADDC0000               * Add Credit notes to correct month
.
          CALL       UPPRST1                * update the financial statistics
.                                             file
          GOTO       CRDN1000
.
.------ record not on the financial statistics file ------
.
CRDN5000  CALL       CLER0000               * Clear variables
.
          CALL       ADDC0000               * Add Credit notes to correct month
.
          CALL       WRPRST1                * update the financial statistics
.                                             file
          GOTO      CRDN10000
.
CRDN9999  RETURN
+
.**********************************************************************
.*                              ADDI0000                              *
.*          Add the invoiced amounts to the stats file                *
.**********************************************************************
ADDI0000  LOAD      SAVAMNT,COUNT,PRSTIP01,PRSTIP02,PRSTIP03,PRSTIP04:
                                  PRSTIP05,PRSTIP06,PRSTIP07,PRSTIP08:
                                  PRSTIP09,PRSTIP10,PRSTIP11,PRSTIP12:
                                  PRSTIP13
.
          ADD       TOTINV,SAVAMNT
.
          STORE     SAVAMNT,COUNT,PRSTIP01,PRSTIP02,PRSTIP03,PRSTIP04:
                                  PRSTIP05,PRSTIP06,PRSTIP07,PRSTIP08:
                                  PRSTIP09,PRSTIP10,PRSTIP11,PRSTIP12:
                                  PRSTIP13
ADDI9999  RETURN
+
.**********************************************************************
.*                                ADDJ0000                            *
.*              Add the cash and journals to the stats file           *
.**********************************************************************
ADDJ0000  LOAD      SAVAMNT,COUNT,PRSTCP01,PRSTCP02,PRSTCP03,PRSTCP04:
                                  PRSTCP05,PRSTCP06,PRSTCP07,PRSTCP08:
                                  PRSTCP09,PRSTCP10,PRSTCP11,PRSTCP12:
                                  PRSTCP13
          ADD       TOTCASH,SAVAMNT
          STORE     SAVAMNT,COUNT,PRSTCP01,PRSTCP02,PRSTCP03,PRSTCP04:
                                  PRSTCP05,PRSTCP06,PRSTCP07,PRSTCP08:
                                  PRSTCP09,PRSTCP10,PRSTCP11,PRSTCP12:
                                  PRSTCP13
.
          LOAD      SAVAMNT,COUNT,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04:
                                  PRSTJP05,PRSTJP06,PRSTJP07,PRSTJP08:
                                  PRSTJP09,PRSTJP10,PRSTJP11,PRSTJP12:
                                  PRSTJP13
          ADD       TOTJOUR,SAVAMNT
          STORE     SAVAMNT,COUNT,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04:
                                  PRSTJP05,PRSTJP06,PRSTJP07,PRSTJP08:
                                  PRSTJP09,PRSTJP10,PRSTJP11,PRSTJP12:
                                  PRSTJP13
ADDJ9999  RETURN
+
.**********************************************************************
.         Add Credit notes amount to correct month
.**********************************************************************
ADDC0000  MOVE      ZERO,SAVAMNT
          LOAD      SAVAMNT,COUNT,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04:
                                  PRSTJP05,PRSTJP06,PRSTJP07,PRSTJP08:
                                  PRSTJP09,PRSTJP10,PRSTJP11,PRSTJP12:
                                  PRSTJP13
          ADD       FORM12P2,SAVAMNT
          STORE     SAVAMNT,COUNT,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04:
                                  PRSTJP05,PRSTJP06,PRSTJP07,PRSTJP08:
                                  PRSTJP09,PRSTJP10,PRSTJP11,PRSTJP12:
                                  PRSTJP13
ADDC9999  RETURN
+
.**********************************************************************
.*                                 CLER0000                           *
.*               Routine to clear the stats file variables            *
.**********************************************************************
CLER0000  MOVE       ZERO,PRSTOBAL
          MOVE       ZERO,FORM2
.
.------ increment the counter ------
.
CLER1000  ADD        ONE,FORM2
.
          COMPARE    FORM2,TEN3             * see if we have finished
          GOTO       CLER9999 IF LESS
.
          STORE      ZERO,FORM2,PRSTIP01,PRSTIP02,PRSTIP03,PRSTIP04:
                                PRSTIP05,PRSTIP06,PRSTIP07,PRSTIP08:
                                PRSTIP09,PRSTIP10,PRSTIP11,PRSTIP12:
                                PRSTIP13
          STORE      ZERO,FORM2,PRSTCP01,PRSTCP02,PRSTCP03,PRSTCP04:
                                PRSTCP05,PRSTCP06,PRSTCP07,PRSTCP08:
                                PRSTCP09,PRSTCP10,PRSTCP11,PRSTCP12:
                                PRSTCP13
          STORE      ZERO,FORM2,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04:
                                PRSTJP05,PRSTJP06,PRSTJP07,PRSTJP08:
                                PRSTJP09,PRSTJP10,PRSTJP11,PRSTJP12:
                                PRSTJP13
.
          GOTO       CLER1000
.
CLER9999  RETURN
+
.**********************************************************************
.*                                AJOP0000                            *
.*        Routine to adjust the opening balance figure on the         *
.*        stats file                                                  *
.**********************************************************************
AJOP0000  DISPLAY    *P1:17,*EF,*V2LON,"*** Adjusting Opening Balance ***":
                     *HOFF,*P1:19,"Practice : ":
                     *P1:20,"Doctor   : ":
                     *P1:21,"Year     : ";
.
.         UNPACK     PRCNSDAT,XDAY,XMON,XYEAR
          UNPACK     SELMTH,CCENT,CYEAR,CMON
          PACK       PRSTYEAR,CCENT,CYEAR
          PACK       KEY20,PRSTYEAR,SP20
          CALL       RSPRST1                * position on the financial
.                                             statistics file
.------ read through the financial statistics file ------
.
AJOP1000  CALL       RKPRST1
          BRANCH     OVRCD,AJOP9999
.
          DISPLAY    *P12:19,*V2LON,PRSTPRAC:
                     *P12:20,PRSTDOCT:
                     *P12:21,PRSTYEAR;
          MOVE       PRSTOBAL,TEMPOPEN   
          MOVE       ZERO,COUNT
.
.------ increment the counter ------
.
AJOP2000  ADD        ONE,COUNT
.
          COMPARE    COUNT,TEN4             * see if we have processed all of
          GOTO       AJOP3000 IF EQUAL        the periods
.
          CALL       CALC0000               * calculate this years balance
.
          GOTO       AJOP2000
.
.------ Check if next financial year's figures exist ------
.
AJOP3000  PACK       SAVKEY20,PRSTYEAR,PRSTPRAC,PRSTDOCT  
          MOVE       PRSTYEAR,FORM4
          ADD        ONE,FORM4
          MOVE       FORM4,PRSTYEAR
          REP        " 0",PRSTYEAR
          PACK       KEY20,PRSTYEAR,PRSTPRAC,PRSTDOCT 
          CALL       RDPRST1                * read the financial statistics 
          BRANCH     OVRCD,AJOP5000           file
.
          MOVE       TEMPOPEN,PRSTOBAL
.
          CALL       UPPRST1                * update the financial statistics
.                                             file
.------ reposition on the original record ------
.
AJOP5000  MOVE       SAVKEY20,KEY20     
          CALL       RDPRST1                * read the financial statistics 
.                                             file
          GOTO       AJOP1000          
.
AJOP9999  RETURN
+
.**********************************************************************
.*                                CALC0000                            *
.*          Calculate the opening balance for the current year        *
.**********************************************************************
CALC0000  LOAD      SAVAMNT,COUNT,PRSTIP01,PRSTIP02,PRSTIP03,PRSTIP04:
                                  PRSTIP05,PRSTIP06,PRSTIP07,PRSTIP08:
                                  PRSTIP09,PRSTIP10,PRSTIP11,PRSTIP12:
                                  PRSTIP13
          ADD       SAVAMNT,TEMPOPEN  
.
          LOAD      SAVAMNT,COUNT,PRSTCP01,PRSTCP02,PRSTCP03,PRSTCP04:
                                  PRSTCP05,PRSTCP06,PRSTCP07,PRSTCP08:
                                  PRSTCP09,PRSTCP10,PRSTCP11,PRSTCP12:
                                  PRSTCP13
          SUB       SAVAMNT,TEMPOPEN 
.
          LOAD      SAVAMNT,COUNT,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04:
                                  PRSTJP05,PRSTJP06,PRSTJP07,PRSTJP08:
                                  PRSTJP09,PRSTJP10,PRSTJP11,PRSTJP12:
                                  PRSTJP13
          ADD       SAVAMNT,TEMPOPEN
.
CALC9999  RETURN
+
.**********************************************************************
.*                               END0000                              *
.*                   We have finished processing                      *
.**********************************************************************
END0000   DISPLAY    *P1:24,*EL,*V2LON,"** Recalculation completed **  ",*HOFF;
          CALL       HITENTER
.
END1000  MOVE      ZERO,EXIT
.
.------ see if O.K. to start again ------
.
END2000   MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"Do you want to process another date range ? (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ",*V2LON,ANS
.
          REPLACE   "nNyY",ANS
.
          CMATCH    ANSY,ANS                * see if Yes entered
          GOTO      END9000 IF EQUAL
.
          CMATCH    ANSN,ANS                * see if No entered
          GOTO      END9999 IF EQUAL
.
          BEEP                              * invalid option
.
          GOTO      END2000
.
.------ we do not want to continue ------
.
END9000   MOVE      ONE,EXIT                * repeat 
.
END9999   RETURN
+
.
          INC       PATCOMN3
          INC       STD001IO
          INC       PATDRGIO/INC
          INC       PRIDTRIO/INC
          INC       PRIINVIO/INC
          INC       PRISTAIO/INC
          INC       PRICNOIO/INC

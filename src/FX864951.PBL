.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX864951                                                  *
.* Desc      :   Loop through existing Concession Card record deatils file *
.*               PMSCCDFD and copy all the records to New Concession Card  *
.*               History table - PATCCHFD                                  *
.***************************************************************************
.* Date      :   20/06/2019                                                *
.* Author    :   Richa Phogat                                              *
.* Function  :   This program will copy concession card records to new     *
.*               Audit File                                                *
.* Modifications  :                                                        *
.* V10.14.01  17/07/2019  Richa Phogat  TSK 0864951                        *
.*            Updated CPYCOUNT from 3 to 7                                 *
.* V10.14.00  20/06/2019  Richa Phogat  TSK 0864951                        *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSCCDFD/INC      * Concession Card Details
          INC       PATCCHFD/INC      * Concession Card History File
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CPYCOUNT  FORM      7
.
.
PRGID     INIT      "FX864951"
PRGNAM    INIT      "Concession Card History Table"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAIN0000                                  *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
. 
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with clean up
.
MAIN1000  CALL      PROC0000               * process PMSCCDFD
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsccdaf";
          OPEN      PMSCCDA1,"pmsccdaf"
.
          DISPLAY   *P64:24,"patcchaf";
          OPEN      PATCCHA1,"patcchaf"
          OPEN      PATCCHA2,"patcchaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: MAIN0000*
.*   Copy Records from Concession Card file to Concerssion Card History   *
.*   file                                                                 *
.**************************************************************************
PROC0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,*HOFF,"Hit Enter to start"
          CALL      HITENTER
.
          MOVE      ZERO,CPYCOUNT
          PACK      KEY19,SP70
          CALL      RSPMCCD1         * Position at start of the file
PROC0010  CALL      RKPMCCD1         * read next record
          BRANCH    OVRCD,PROC9900   * eof - finished
.
          CALL      CLPATCCH
          PACK      PTCCURNO,PMCDURNO
          PACK      PTCCCTYP,PMCDCTYP
          PACK      PTCCCNUM,PMCDCNUM
          PACK      PTCCEXDT,PMCDEXDT
          PACK      PTCCDVAC,PMCDDVAC
          MOVE      "A",PTCCUPDT
          PACK      PTCCDATE,PMCDCDAT
          PACK      PTCCTIME,PMCDCTIM
          MATCH     PMCDUUID,SP70
          IF        !@EQUAL
            PACK      PTCCURID,PMCDUUID
          ELSE
            PACK      PTCCURID,PMCDCUID
          ENDIF
.
          DISPLAY   *P1:19,*EL,"Record Read : ":
                    *V2LON,PMCDURNO,"|",PMCDEXDT,"|",PMCDCTYP,*HOFF,SP5;
.
          PACK      KEY35,PTCCURNO,PTCCEXDT,PTCCCTYP,PTCCDATE,PTCCTIME,SP70
          CALL      RAPTCCH1
          IF        OVRCD=0
            GOTO      PROC0010
          ENDIF 
.          
          CALL      WRPTCCH1          * write record to history file
          ADD       ONE,CPYCOUNT
.
          PRINT     *1,"|",PTCCURNO,"|",CPYCOUNT
          GOTO      PROC0010
.
PROC9900  PRINT     *N,*1,"Total number of records: ",CPYCOUNT
.
          DISPLAY   *P1:20,*EL,"Total Records Written : ":
                    *V2LON,CPYCOUNT,*HOFF,SP5;
          DISPLAY   *P1:23;
          CALL      HITENTER
.
          MOVE      ZERO,EXIT
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       CLPATCCH
          INC       STD001IO
          INC       PATCCHIO/INC   * Concession Card History File
          INC       PMSCCDIO/INC   * Concession Card Details
.

.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT50                                                 *
.* Description    :  Western Australia Public Health Department Transmission  *
.******************************************************************************
.* Date           :  06/12/1996                                               *
.* Author         :  Jeni Tan                                                 *
.* Function       :  This program will extract all patients who discharged in *
.*                   the selected date range                                  *
.* Modifications  :                                                           *
.*----------------------------------------------------------------------------*
.*        V12.00.05 12/09/2025  Nikitha B      TSK 0966564                    *
.*                  Added read RDVISA1 at CODC1100 and TRIG5000.              *
.*        V12.00.04 26/08/2025  Nikitha B      TSK 0964837                    *
.*                  Modified contrated days calculation at CNTL0000           *
.*        V12.00.03 07/08/2025  Nikitha B     TSK 0964837                     *
.*                  Reinstated DOYR2024 check around XSEPTP at SDSC5000       *
.*        V12.00.02 26/06/2025  Nikitha B     TSK 0963143                     *
.*                  Removed DOYR2025 check around XSEPTP at SETF5020,SDSC5000 *
.*                  Removed at SETF5100 to report discharge delay by default  *
.*        V12.00.01 27/05/2025  PJ Le Febour   TSK 0955096 AlphaNum VisitNum  *
.*                  change COMPARE AADMNO,PTEDADMN to MATCH AADMNO,PTEDADMN   *
.*                         COMPARE AADMNO,TADMN    to MATCH AADMNO,TADMN      *
.*                  CLOS0000  amended OVRCD=0 & AADMNO=TADMN
.*        V11.05.03 08/04/2025  PJ Le Febour   TSK 0958382                    *
.*                  change DOYR2024 to DOYR2025 change JULY2024 to JULY2025   *
.*        V11.05.02 24/03/2025  Davin          TSK 0956210                    *
.*                  Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*        V11.05.01 17.12.2024  DA Sarkies     TSK 0951086                    *
.*                  Increased size of LOS variables                           *
.*        V11.04.03 04/07/2024  Thannh T       TSK 0948804                    *
.*                  Changed SDSC0000 to extract mode of separation based on   *
.*                  JULY2024 and not DOYR2024                                 *
.*        V11.04.02 22/03/2024  Thannh T       TSK 0940239                    *
.*                  WA HMDS Statutory Changes 2024-25                         *
.*        V11.04.01 11/01/2024  Ebon Clements  TSK 0927979                    *
.*                  Include contracted leave days in qualified newborn days   *
.*                  if the patient was a qualified newborn when on contracted *
.*                  leave. PRSCLW00 QUALIFNB                                  *
.*        V11.03.06 16/11/2023  Thanh T        TSK 0936867                    *
.*                  Changed EPSD5000 for Additional statutory changes 2023/24 *
.*        V11.03.05 31/08/2023  Jacob Jackson  TSK 0927979                    *
.*                  Change calculation of Qualified Newborn Days to include   *
.*                  value of Contracted Leave Days.                           *
.*        V11.03.04 20/06/2023  Davin          TSK 0933992                    *
.*                  Mods to retain current format for pre-01/07/2023 extracts *
.*        V11.03.03 13/06/2023  Davin          TSK 0933580                    *
.*                  Added PTCNED90/PTCNE100/PTCNE110 for DRG 9.0/10.0/11.0    *
.*        V11.03.02 16/05/2023  Bella Turco    TSK 0929777                    *
.*                  Added PreJulyTest to DSCD0000                             *
.*        V11.03.01 01/05/2023  Bella Turco    TSK 0929777                    *
.*                  WA Health Public stat changes for 2023 - "Gi" Gender      *
.*        V11.02.01 31/03/2022  Davin          TSK 0917793                    *
.*                  Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*        V11.01.10 26/08/2021  Jill Parkinson Task 0910749                   *
.*                  Added reposition on last tran record in CNTL9999          *
.*        V11.01.09 20/08/2021  Jill Parkinson Task 0910030                   *
.*                  Changed ADHOC option to also set RERUNFL                  *
.*        V11.01.08 29/07/2021  Jill Parkinson Task 0909693                   *
.*                  Moved CNTL0000 to be called after DSCR0000                *
.*        V11.01.07 19/07/2021  Jill Parkinson Task 0909226                   *
.*                  Modified to allow HDP=C or R to calculate contracted leave*
.*        V11.01.06 14/07/2021  Jill Parkinson Task 0909014                   *
.*                  Corrected CQNB000 to match "1 " so "10" is not chosen as  *
.*                  unqualified                                               *
.*        V11.01.05 06/07/2021  Jill Parkinson Task 0908635                   *
.*                  Changed back to 1 July 2021                               *
.*        V11.01.04 16/06/2021  Jill Parkinson Task 0907716                   *
.*                  Changed back to 1 July 2021                               *
.*        V11.01.03 25/05/2021  Jill Parkinson Task 0901851                   *
.*                  Added check on THCSCOD=9 in CQNB0000                      *
.*                  Fixed XCLNST being reset to 1 when Qualified Newborn      *
.*        V11.01.02 20/05/2021  Jill Parkinson Task 0901851                   *
.*                  Fixed client status error message                         *
.*        V11.01.01 17/05/2021  Jill Parkinson Task 0901851 and 0904708       *
.*                  Added contract fields for coding and NDIS information     *
.*        V11.00.02 27/03/2020  Jill Parkinson Task 0879163                   *
.*                  Changed to use indicator 1 of Category G for sex          *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added include SEXDSCIO                                    *
.*        V10.14.03 18/06/2019  Ken Bell       TSK 0872503 (Prev ref 0870709) *
.*                  Submit 99 if Number Leave Periods for Patient  exceeds 99 *
.*        V10.14.02 13/06/2019  Don Nguyen     TSK 0876402                    *
.*                  Moved code to delete temp files (WORK & WORK3) to DELTMP00*
.*        V10.14.01 14/03/2019  Don Nguyen     TSK 0869412                    *
.*                  Added read of ICD10 Ed.11 Go-live Date parameter (PTCNGDX1)*
.*        V10.13.01 05/12/2018  Don Nguyen     TSK 0838558                    *
.*                  Called PREP on temp files (WORK & WORK3) before delete    *
.*        V10.12.01 01/02/2018  Peter Vela     TSK 0851127                    *
.*                  Added business rules to not add a psychday if admitted    *
.*                  into a psych bed but transferred out in the same day      *
.*                  Added business rules to assign 1 psychday if admitted to  *
.*                  psych bed but total psych days are 0 at discharge         *
.*        V10.11.06 29/11/2017  Peter Vela     TSK 0849011                    *
.*                  Added psych days validation for multiple change records   *
.*                  in one day                                                *
.*        V10.11.05 21/11/2017  Peter Vela     TSK 0836875                    *
.*                  Don't add one day for overnight psych days between        *
.*                  change and leave record                                   *
.*        V10.11.04 17/11/2017  Peter Vela     TSK 0836875                    *
.*                  Move one psych day if change bed type is psych bed in     *
.*                  daycase                                                   *
.*        V10.11.03 17/11/2017  Peter Vela     TSK 0836875                    *
.*                  Move one psych day if admit bed type is psych bed in      *
.*                  daycase                                                   *
.*        V10.11.02 10/11/2017  Peter Vela     TSK 0836875                    *
.*                  Add on psych day if a change record going from a psych bed*
.*                  type to a non psych bed type                              *
.*        V10.11.01 13/10/2017  Peter Vela     TSK 0836875                    *
.*                  Don't add one psych day on a change record                *
.*                  (workpsyc @ LEND0000)                                     *
.*        V10.10.01 30/05/2017  Davin          TSK 0836775                    *
.*                  Added PTCNED80 to extract drg version 8.0 if required     *
.*        V10.09.01 28/12/2016 Jill Parkinson TSK 0831312                     *
.*                  Changed payment classification to allow 34                *
.*        V10.08.03 15/08/2016  Davin          TSK 0306678                    *
.*                  Calculate at least one psych day when patient admitted    *
.*                  into psych bed (workpsyc @ LEND0000)                      *
.*        V10.08.02 10/08/2016  Jill Parkinson TSK 0822571                    *
.*                  Mods to replace space with zero for icu hours             *
.*        V10.08.01 31/05/2016  Jill Parkinson TSK 0807963                    *
.*                  Removed clearing of Admitted from (XCTHOS) for contracted *
.*                  patients
.*        V10.06.04 13/07/2015  Jill Parkinson CAR 312206                     *
.*                  Changes XINLOS to use THCSCOD instead of TCINDC1 and      *
.*                  removed mapping of values                                 *
.*        V10.06.03 09/07/2015  Don Nguyen      CAR 316404                    *
.*                  Modified LDAY0000 - commented out subtraction of leave    *
.*                  days count for case when leave start on admission date.   *
.*        V10.06.02 26/03/2015  Jill Parkinson  CAR 304357                    *
.*                  Mods to check patecmaf to determine if coding has been    *
.*                  sent previously                                           *
.*        V10.06.01 25/03/2015  Don Nguyen      CAR 312700                    *
.*                  Changes for 01 July 2015 (DOYR2015)                       *
.*        V10.05.03 21/10/2014 Sandra Barcham   CAR 307233                    *
.*                  Fix Admission Weight population                           *
.*        V10.05.02 22/07/2014  Davin           CAR 300780                    *
.*                  Fix for psych days calculation                            *
.*        V10.05.01 16/07/2014  Davin           CAR 303652                    *
.*                  Get drg from patpgraf based on discharge date grouper     *
.*                  version. Get mdc from patdgwaf. (drgv0000)                *
.*        V10.04.04 15/06/2014  Jill Parkinson CAR 30639                      *
.*                  Moved TFILENAM to INIT0000 and added extra KILL0000       *
.*        V10.04.03 20/05/2014  Don Nguyen      CAR 301012                    *
.*                  Restored date check at the start of LEND0000 so LOS       *
.*                  calculation not called multiple times for the same stay.  *
.*        V10.04.02 25/02/2014  Davin           CAR 291926                    *
.*                  Only subtract 1 leave day if onleave date = adm. date     *
.*                  Always calculate days between dates (lend0000)            *
.*        V10.04.01 12/02/2014  Davin           CAR 291926                    *
.*                  Added medicare + reference number (xpmedic)               *
.*                  If onleave date = adm. date, don't count as a leave day   *
.*        V10.03.51 16/09/2013 Sandra Barcham   CAR 291531                    *
.*                  Stop adding extra countd if more than one primary disease *
.*        V10.03.50 09/08/2013 Sandra Barcham   CAR 289410                    *
.*                  Change read of patecdaf from index 2 to index 1           *
.*        V10.03.49 30/07/2013 Sandra Barcham   CAR 289410                    *
.*                  Fix primary diagnosis and external cause                  *
.*        V10.03.48 03/07/2013  Don Nguyen      CAR 286505                    *
.*                  Modified SETF4000 to match PADD4 against Cat C codes and  *
.*                  use the long description for XSUBRB, with matching TDESC. *
.*        V10.03.47 03/05/2013  Don Nguyen      CAR 283642                    *
.*                  Changes for 01 July 2013 (DOYR2013)                       *
.*        V10.03.46 22/05/2013 Sandra Barcham   CAR 285045                    *
.*                  Fix dagger astersk                                        *
.*                  Fix Interpreter Required                                  *
.*        V10.03.45 09/05/2013 Sandra Barcham   CAR 285045                    *
.*                  Correct diagnosis count                                   *
.*        V10.03.44 29/04/2013 J.Tan            CAR 278522                    *
.*                  Mods to CO Diagnosis to have OC                           *
.*        V10.03.43 26/04/2013 J.Tan            CAR 284429                    *
.*                  Removed warning message Birth Weight over 9000 grams      *
.*        V10.03.42 18/04/2013 J.Tan            CAR 278522                    *
.*                  Removing checking for Daggers and Asterisk                *
.*                  Implement new rules for assigning code prefix             *
.*        V10.03.41 08/04/2013 J.Tan            CAR 278522                    *
.*                  Mods checking for Daggers and Asterisk                    *
.*        V10.03.40 22/03/2013 Davin            CAR 273655                    *
.*                  Added filter on triggers for adhoc extract (trig1000)     *
.*        V10.03.39 19/12/2012 Patrick Adair    CAR 261630                    *
.*                  Removed port number from temp file name                   *
.*        V10.03.38 20/11/2012 Mike Laming      CAR 271939                    *
.*                  Don't write pmswaeaf if ADHOCEXT=1 at WSED2000            *
.*        V10.03.37 05/11/2012 Davin            CAR 275490                    *
.*                  Ignore and delete trigger if coding date is not less than *
.*                  extract start date                                        *
.*        V10.03.36 05/11/2012 Davin            CAR 275584                    *
.*                  Send referral transport field (xsortra) as blanks if not  *
.*                  populated in webPAS                                       *
.*        V10.03.35 10/10/2012 Davin            CAR 274387                    *
.*                  Send Contracting Establishment when client type = 5 or 0  *
.*        V10.03.34 18/09/2012 Davin            CAR 272377                    *
.*                  Changed to not write 'DIS' records (wrec0000)             *
.*                  Comment out checks for start of financial year (sfinyear) *
.*        V10.03.33 30/07/2012 Jill Parkinson CAR 267503                      *
.*                  Change L to map to 4 (not 9) for XSEPT                    *
.*                  Added default to 2 for interpreter                        *
.*                  Put back in change commented out in V10.03.20             *
.*        V10.03.32 20/07/2012 Jill Parkinson CAR 267503                      *
.*                  Commented out matches of acoddte to extract start date    *
.*                  in ECDA0000 and ECOA0000                                  *
.*        V10.03.31 19/07/2012 Jill Parkinson CAR 269224                      *
.*                  Mods to only call WREC0000 once from TRIG0000             *
.*        V10.03.30 17/07/2012 Jill Parkinson CAR 269224                      *
.*                  Added checks for acoddte in ECOA0000 (already in ECDA0000)*
.*        V10.03.29 16/07/2012 Jill Parkinson CAR 269224                      *
.*                  Mods to use pthshosp in temp file name                    *
.*        V10.03.28 12/07/2012 Jill Parkinson CAR 267503                      *
.*                  Mods to set codingfl in TRIG0000                          *
.*        V10.03.27 03/07/2012 Sandra Barcham CAR 268467                      *
.*                  Change to Payment Classification to use catagory cx       *
.*        V10.03.26 27/06/2012 Jill Parkinson CAR 267503                      *
.*                  Mods for adhoc to not update trigger records              *
.*        V10.03.25 06/06/2012 Davin            CAR 265260                    *
.*                  Changed formatting of various fields                      *
.*        V10.03.24 22/05/2012 Davin            CAR 260777                    *
.*                  Changed to not write to pmswaeaf if adhoc extract         *
.*        V10.03.23 15/05/2012 Davin            CAR 260777                    *
.*                  Changed to always check for trigger records to be sent    *
.*                  If zero total records, create .ack file but not .txt file *
.*                  Added read of patvisaf for triggers (pmit0000)            *
.*        V10.03.22 02/05/2012 Davin            CAR 260777                    *
.*                  Write CHK record based on CHKFLAG not COUNTD              *
.*        V10.03.21 27/04/2012 Davin            CAR 260777                    *
.*                  If adm source = 0407 (care type change) set 'admitted     *
.*                  from' field to 0944 (admf0000/XCTHOS)                     *
.*        V10.03.20 23/04/2012 Davin            CAR 260777                    *
.*                  Process LOS details even when dates are equal (lend0000)  *
.*                  If post code = 8888 populate XSUBRB with padd4            *
.*        V10.03.19 20/04/2012 Davin            CAR 260777                    *
.*                  Remove default on unplanned visit to theatre (upln0000)   *
.*                  Changed Mode of Separation for disch transfers (sdsc0000) *
.*        V10.03.18 29/03/2012 Davin            CAR 260777                    *
.*                  Default source of referral transport to "06"              *
.*                  Only write CHK line if patient has coding                 *
.*                  Write 2 lines to 'ack' file 1)total records 2)SUM records *
.*                  Changed mvex0000 to move ack file correctly in us1 script *
.*                  Write a separate record for place of occ./activity codes  *
.*        V10.03.17 27/03/2012 Davin            CAR 262220                    *
.*                  Calculate psych days based on bed type (cat.BT,indc.3=A/D)*
.*        V10.03.16 23/03/2012 Davin            CAR 260777                    *
.*                  Validate hospital when sending coded patients (codc0000)  *
.*                  Validate hospital when sending PMI triggers (pmit0000)    *
.*        V10.03.15 22/03/2012 Sandra Barcham   CAR 260777                    *
.*                  Allow indicator D & P for psych days                      *
.*        V10.03.14 22/03/2012 Davin            CAR 260777                    *
.*                  Changed mothers identifier to be ur number                *
.*                  Changed location desc to be derived from ward only        *
.*                  Changed to ensure only one principal diagnosis is sent    *
.*        V10.03.13 21/03/2012 Jill Parkinson   CAR 260777                    *
.*                  Match hospital code when looping pmswtraf                 *
.*                  Mods to only call ECDA and ECOA if coding complete        *
.*        V10.03.12 15/03/2012 Jill Parkinson   CAR 261919                    *
.*                  Removed mental health indexes from pmswtraf               *
.*        V10.03.11 14/03/2012 Sandra Barcham   CAR 261785                    *
.*                  Fix DVA Card colour to translage from alpha to numeric.   *
.*        V10.03.10 13/03/2012 Jill Parkinson   CAR 261568                    *
.*                  Added check of SFINYEAR for trigger records               *
.*        V10.03.09 05/03/2012 Jill Parkinson   CAR 260777                    *
.*                  Added read of patma1af in SETF0000                        *
.*                  Calculate XSTATE properly                                 *
.*        V10.03.08 02/03/2012 Davin            CAR 260777                    *
.*                  Check index 2 before writing to pmswtraf                  *
.*                  Added episode number to diagnosis record (85-86)          *
.*                  Added episode number to procedure record (93-94)          *
.*                  Added episode number to check record (65-66)              *
.*                  Default Unplanned Return to Theatre = "2"                 *
.*                  Use short description of ward/bed for location XLOCTN     *
.*                  Changed Source of Referral (Locationn) as per Call Note 3 *
.*                  Extract blanks not zeros & print error line if no         *
.*                  procedure date exists.                                    *
.*                  Use epidsode count for diagnosis priority (xprior/ptedcnt)*
.*        V10.03.07 28/02/2012 Davin            CAR 260777                    *
.*                  Mods to not validate date range for adhoc extract         *
.*        V10.03.06 17/02/2012 Sandra Barcham   CAR 253987                    *
.*                  Fix Source of Referral Professional                       *
.*        V10.03.05 16/02/2012 Sandra Barcham   CAR 253987                    *
.*                  Use parameter for Procedure prefix not ANSO               *
.*                  Use parameter for Diagnosis prefix not ANSP,ANSC,ANSA     *
.*                  Fix DVA Card Colour                                       *
.*                  Change Resident status from ptype to pviresi              *
.*        V10.03.04 14/02/2012  Jill Parkinson  CAR 259568                    *
.*                  Added DIS lines                                           *
.*        V10.03.03 13/02/2012  Jill Parkinson  CAR 253987                    *
.*                  Fixed DVA details to come from pmsccdaf                   *
.*        V10.03.02 09/02/2012  Jill Parkinson  CAR 253987                    *
.*                  Changed to left justify XURNO                             *
.*        V10.03.01 16/01/2012  Jill Parkinson  CAR 253987                    *
.*                  Copied from IBAADT49 - Modified for WA Health Public      *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCALAG/INC
          INC       IBAPOSFD/INC
          INC       OPRDEAFD/INC 
          INC       OPRDEBFD/INC
          INC       PATASFFD/INC
          INC       PATCMPFD/INC            * Complex mapping file
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Control file
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC            * Discharge file
          INC       PATECDFD/INC            * Patient episode disease file
          INC       PATECOFD/INC            * Patient episode operation file
          INC       PATECMFD/INC
          INC       PATICDFD/INC
          INC       PATICOFD/INC
          INC       PATICUFD/INC
          INC       PATDGWFD/INC
          INC       PATFN1FD/INC
          INC       PATLINFD/INC
          INC       PATMA1FD/INC            * Patient master file
          INC       PRSPMIFD/INC            * PMI Triggers
          INC       PMSCCDFD/INC
          INC       PMSEXTFD/INC
          INC       PMSWAEFD/INC            * WA Extract ranges
          INC       PMSWTRFD/INC            * WA Trigger
          INC       PMSPX2FD/INC            * PMI extension
          INC       PATHSPFD/INC            * Hospital Details table
          INC       PATATRFD/INC            
          INC       PATMI1FD/INC            * Patient admission file
          INC       PATVISFD/INC            * Patient visit record
          INC       PMSVX1FD/INC            * Visit extension file
          INC       PATPGRFD/INC
          INC       PATTRNFD/INC            * Transfer file
          INC       PATWR1FD/INC
          INC       PATWVEFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
TEMP      IFILE     SQL, FIXED=84, KEY="D1-8"
.Name      Type      Length   Physical   Start   Description
.----      ----      ------   --------   -----   -----------------
.PURNO     DIM       8           8         1     U/R Number
.AADMNO    FORM      8           5         9     Admission Number
ERRDESC    DIM       70         70        14     Error Description
.
.End of Record                            84
.
WORK      FILE      ASCII, FIXED=267        * acknowledgement file
.
WORK3     FILE      ASCII, FIXED=600       * layout after July 1 2024
.
.Name     Type   Length    Start    Description
.----     ----   ------    -----    -----------
XEVNTYP   DIM       4         1     Event Type
XMORTYP   DIM       1         5     Morbidity Record Type
XEOCLNK   DIM      12         6     Episode of Care Link Field
XDAYQNB   DIM       3        18     Days of qual newborn care
XNUMLEV   DIM       2        21     Number of leave periods
XACCOCC   FORM      1        23     Accomodation Occupied
XLANG     DIM       4        24     Language
XUPDFLG   DIM       1        28     Update Flag
XSORLOC   DIM       2        29     Source of Referral - location
XSORCLI   DIM       2        31     Source of Referral - clinical/professional
XSORTRA   DIM       2        33     Source of Referral - transport
XHIHCD    DIM       3        35     Hospital in home care days
.SP7      DIM       7        38     Blank                            *C-114471
XHCODE    DIM       4        45     Establishment/Hospital Code
XADMNO    DIM      12        49     Admission/Account Number
XPURNO    DIM      10        61     Client Identifier/UR Number
XMUMSID   DIM      10        71     Mothers Identifier
XADATE    DIM       8        81     Admission Date
XATIME    DIM       4        89     Admission Time
XDDATE    DIM       8        93     Separation Date
XDTIME    DIM       4       101     Separation Time
XPSNAME   DIM      50       105     Surname
XPGNAME   DIM      30       155     First Forename
XSFNAME   DIM      30       185     Second Forename
XPADD1    DIM      50       215     Residential address
XSUBRB    DIM      30       265     Suburb
XPCODE    DIM       6       295     Postcode
XSTATE    DIM       1       301     State
.SP6      DIM       6       302     Blank
XDOBDT    DIM       8       308     Date of Birth
XPSEX     DIM       1       316     Sex at Birth
XABORN    DIM       1       317     Indigenous status
XCNTBR    DIM       4       318     Country of Birth
XMSTAT    DIM       1       322     Marital Status
XEMPST    DIM       2       323     Employment Status
XINTRP    DIM       1       325     Interpreter Service
XOCCPC    DIM       4       326     Occupation Code
XSREFR    DIM       2       330     Source of referral
XINLOS    DIM       1       332     Intended LOS
XCTHOS    DIM       4       333     Admitted From
XLOCTN    DIM      20       337     Location ATIO
...XCLINC    DIM       6       357     Clinician Responsible for Care *C-233977
XNMRNO    DIM      13       357     Practitioner Registration No.  (NMR No.)
XSPLTY    DIM       3       370     Speciality on Admit (Doct Type of Att.doc)
XSPCDS    DIM       3       373     Speciality on Disch.
XADTYP    DIM       1       376     Admission Status
XWEIGH    DIM       4       377     Admission Weight
XLEVDY    DIM       4       381     Total days of leave
XPSYCH    DIM       4       385     Total days of Psychiatric Care
XMHLGL    DIM       1       389     Mental Health Legal Status
XPAYCL    DIM       2       390     Payment Classification (CL)
XVETRN    DIM       1       392     Veteran Entitlement
XINSRN    DIM       1       393     Insurance Status
XICUDY    DIM       4       394     Days in I.C.U.
XVENHR    DIM       5       398     Ventilator hours
XREADM    DIM       1       403     Readmission Status
XURETN    DIM       1       404     Unplanned Return to Theatre
XEPSCR    DIM       2       405     Type of Episode of care
XCLNST    DIM       2       407     Client status
XCTRES    DIM       4       409     Contracting Establishmentn-0 FORD
XSEPTP    DIM       2       413     Separation Mode
XDSCTO    DIM       4       415     Transferred to/transfer source code
...XDSATT    DIM       6       412     Clinician on discharge         *C-233977
XDSATT    DIM      13       419     Clinician on discharge (NMR No.)
XGRPVR    DIM      10       432     Group Version
XDIACG    DIM       3       442     Major Diagnostic Category
XDIAGR    DIM       4       445     Diagnosis Related Group
XCODID    DIM      20       449     Code Identifier
XDVAFN    DIM      12       469     DVA File Number
XDVAAN    DIM      12       481     DVA Authorisation Number
XDVAAD    DIM       8       493     DVA Authorisation Date
XHOMEPH   DIM      18       501     Home Phone
XCONTPH   DIM      18       519     Contact Phone
XRESSTA   DIM       3       537     Resident Status
XICUHR    DIM       5       540     Hours in ICU
XPMEDIC   DIM      11       545     Medicare and Reference Number
.
XCONLDY   DIM       3       556     Contracted leave days * after 1 july 2021
XNDISFL   DIM       2       558     NDIS flag
XNDISCN   DIM      20       560     NDIS card number
XGENDER   DIM       1       581     Gender
XMDCLDTE  DIM       8       582     Medically Cleared for Discharge Date
XMDCLTME  DIM       6       590     Medically Cleared for Discharge Time
XMDCPRSN  DIM       2       596     Primary Reason for Discharge Delay
XMDCSRSN  DIM       2       598     Secondary Reason for Discharge Delay
.
.End of Record              600     * after 1 July 2024
.
.
.Diagnosis/External Cause/Place of occurence/Activity/Morphology codes
.
.Name     Type   Length    Start    Description
.----     ----   ------    -----    -----------
.XEVNTYP   DIM       4         1     Event Type
.XADMNO    DIM      12         5     Admission/Account Number
.XPURNO    DIM      10        17     Client Identifier/UR Number
SP18       DIM      18        27     Blank
.XHCODE    DIM       4        45     Establishment/Hospital Code
XPRIOR     DIM       3        49     Priority
XICDCOD    DIM      10        52     ICD Code
XPLCOCC    DIM      10        62     Place of occurence
XACTIV     DIM      10        72     Activity
XCOFICD    DIM       1        82     Condition onset flag for ICD
XCOFPOO    DIM       1        83     Condition onset flag for Place of Occurence
XCOFACT    DIM       1        84     Condition onset flag for Activity
XDCCRFL    DIM       2        85     Contracted care * after 1 july 2021
.End of Record                85
.End of Record                87     * after 1 July 2021
.
.Procedure Codes
.Name     Type   Length    Start    Description
.----     ----   ------    -----    -----------
.XEVNTYP   DIM       4         1     Event Type
.XADMNO    DIM      12         5     Admission/Account Number
.XPURNO    DIM      10        17     Client Identifier/UR Number
.SP18      DIM      18        27     Blank
.XHCODE    DIM       4        45     Establishment/Hospital Code
.XPRIOR    DIM       3        49     Priority
XPROCCOD   DIM      10        52     Procedure Code
...XCLINPR    DIM       6        62     Clinician for procedure       *C-233977
XCLINPR    DIM      13        62     Clinician for procedure  (NMR No.)
XDATPROC   DIM       8        75     Date of procedure
.SP10      DIM      10        83     Blank
XPCCRFL    DIM       2        93     Contracted care * after 1 july 2021
.End of Record                93 
.End of Record                95     * after 1 July 2021
.
.Check Line
.Name     Type   Length    Start    Description
.----     ----   ------    -----    -----------
.XEVNTYP   DIM       4         1     Event Type
.XADMNO    DIM      12         5     Admission/Account Number
.XPURNO    DIM      10        17     Client Identifier/UR Number
.SP18      DIM      18        27     Blank
.XHCODE    DIM       4        45     Establishment/Hospital Code
XDIAGCNT   DIM       4        49     Diagnosis Count
XEXCACNT   DIM       4        53     External Cause count
XMORPCNT   DIM       4        57     Morphology Codes count
XPROCCNT   DIM       4        61     Procedure count
.End of Record                65


. ----- Program Variables -----
.
ACTIVITY  DIM       2
ADHOCEXT  FORM      1
ADMTYPE   DIM       3
BEDTYPE   DIM       3
BJDAY     FORM      3                       * For CALCAGE
CJDAY     FORM      3                       * For CALCAGE
CHKFLAG   FORM      1                       * flag to write CHK record
CHDATE    DIM       8
CODEFLG   FORM      1
CODINGFL  FORM      1
COUNT     FORM      2
COUNTA    FORM      2
COUNTD    FORM      4
COUNTEX   FORM      4
COUNTE    FORM      2
COUNTP    FORM      2
COUNTO    FORM      4
COUNTM    FORM      4
CALDAYS   FORM      6
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CMDLINE   DIM       120
CMPCNT    FORM      1
DEPTYPE   DIM       3
DAAFLAG   FORM      1
DDAFLAG   FORM      1
MHCODE    DIM       2
CATA      INIT      "A "
CATBT     INIT      "BT"
CATCT     INIT      "ct"
CATRT     INIT      "RT"
CODECC    INIT      "CC"
CODECL    INIT      "CL"
CODECX    INIT      "cx"
CODEDD    INIT      "DD"
CODEGi    INIT      "Gi"
CODEP     INIT      "P "
CODEP1    INIT      "P1"
CODEP2    INIT      "P2"
CODEP3    INIT      "P3"
CODEP4    INIT      "P4"
CODEP5    INIT      "P5"
CODEP6    INIT      "P6"
CODEPO    INIT      "PO"
CODEU1    INIT      "U1"
CODEU2    INIT      "U2"
CODEU3    INIT      "U3"
CODEU4    INIT      "U4"
CODEU5    INIT      "U5"
CODEU6    INIT      "U6"
CODEU7    INIT      "U7"
CODEU8    INIT      "U8"
CODEU9    INIT      "U9"
CODEV1    INIT      "V1"
CODEV2    INIT      "V2"
CODEVA    INIT      "VA"
CODEVB    INIT      "VB"
CURDATE   DIM       8
CDYSYEAR  FORM      2
DISCHDAT  DIM       8
DIM1A     DIM       1
DIM1B     DIM       1
DIM1C     DIM       1
DIM2A     DIM       2
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8
DIM9      DIM       9
DIM9A     DIM       9
DIM10     DIM       10
DIM12     DIM       12
DIM15     DIM       15
DIM17     DIM       17
DIM17A    DIM       17
DOCNAME   DIM       31
DOYR2025  FORM      1       
DRGVERS   DIM       3
DRG041    INIT      "041"
DRG042    INIT      "042"
DRG050    INIT      "050"
DRG051    INIT      "051"
DRG052    INIT      "052"
DRG060    INIT      "060"
DRG062    INIT      "062"
DRG070    INIT      "070"
DRG080    INIT      "080"
DRG090    INIT      "090"
DRG100    INIT      "100"
DRG110    INIT      "110"
ENDDATE   DIM       8
FCCENT    FORM      2                        * Form 2 of CCENT
FCMON     FORM      2                        * Form 2 of CMON
FCYEAR    FORM      2                        * Form 2 of CYEAR
FILE1     INIT      "file1"
FILE3     INIT      "file3"
FNAME1    DIM       8
FNAME3    DIM       8
FNAMEA    DIM       20
FNAMEB    DIM       24
FNAMES    DIM       8
FNAMEUS1  DIM       24
FORM3     FORM      3
FORM6     FORM      6
FORM8     FORM      8
FORM9     FORM      9
FROMDATE  DIM       8
IBCNMHOS  FORM      1
JULY2024  INIT      "20240701"    
JULY2025  INIT      "20250701"    
.
NBRDAYS   FORM      4
NUMLEV    FORM      2
RECNUM    FORM      6
PATNAME   DIM       31
PSAGECON  DIM       3
PSAGETYP  DIM       1
QUALIFNB  FORM      1
ADMPFLAG  FORM      1
PSYCFLAG  FORM      1
POSTCODE  DIM       4
RERUNFL   FORM      1
RTDAYS    FORM      3
SCODFLG   FORM      1
SAVDATE   DIM       10
SEX       DIM       8
SAVKEY10  DIM       10
SENDFLAG  FORM      1             * send flag for activity codes
UPDATEFL  FORM      1
.                                       1 = No
.                                       0 = Yes
SAVKEY19  DIM       19
SFINYEAR  DIM       8                        * Start of Financial year
.
SKEY8     DIM       8
STRTDATE  DIM       8
SUMCOUNT  FORM      8             * number of SUM records
TODATE    DIM       8
EPIADATE  DIM       8
TOTCOUNT  FORM      8             * total number of lines in extract
SP12      INIT      "            "
SP50      INIT      "                                                     "
TIMEIS    DIM       8
WAHCS     INIT      "WAHCS"
WDATE1    DIM       8
WDATE2    DIM       8
WEBUSRID  DIM       10
WRDBED    DIM       6
WORKHITH  FORM      3
WORKICLS  FORM      4
WORKLDAY  FORM      4
WORKPSYC  FORM      4
WORKQUAL  FORM      3
FILETYPE  DIM       4
SVADDATE  DIM       8
ACK       INIT      ".ack"
TXT       INIT      ".txt"
XF2       FORM      2
.
.
. ----- Program Constants -----
.
LOWCT     INIT      "ct"
OYEAR     FORM      "365"
ZED30     INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAADT50"
PRGNAM    INIT      "WA Health Department Transmissions"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialize variables & open files
ML1000    CALL      KADH0000                * Keyin Adhoc extraction
          BRANCH    EXIT,ML9999
.
          CALL      KHOS0000                * Keyin hospital if required
          BRANCH    EXIT,ML9999
.
          CALL      DSCD0000                * Keyin discharge date
          BRANCH    EXIT,ML9999
.
          CALL      WSED0000                * Validate last extract date range
          BRANCH    EXIT,ML9999
.
          CALL      CDID0000                * sending coder id ?
          CALL      ACTV0000                * ask user if activity is sent
          CALL      KUSR0000                * Keyin user id
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML7500,ML1000,ML1000
.                         Yes    No     Cancel
.
ML7500    CALL      EXTC0000                * Extract data for selected period
          CALL      ERRS0000                * print error message if any
          CALL      ENDP0000                * end of processing, copy files.
.
          IF        TOTCOUNT>0
            MOVE      FNAMEA,FNAMEUS1
            MOVE      TXT,FILETYPE
            CALL      MVEX0000
          ENDIF
          MOVE      FNAMEB,FNAMEUS1
          MOVE      ACK,FILETYPE
          CALL      MVEX0000
          GOTO      ML9999
.
ML9999    CALL      KILL0000
          CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          SCAN      "PreJulyTest",REPTNAME
          IF        @EQUAL
            MOVE      "20250101",JULY2025     
          ENDIF
          RESET     REPTNAME
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.
          DISPLAY   *P64:24,"pmsccdaf";
          OPEN      PMSCCDA1,"pmsccdaf"
.
          DISPLAY   *P64:24,"pmsextaf";
          OPEN      PMSEXTA1,"pmsextaf"
.
          DISPLAY   *P64:24,"pmswaeaf";
          OPEN      PMSWAEA1,"pmswaeaf"
.
          DISPLAY   *P64:24,"prspmiaf";
          OPEN      PRSPMIA1,"prspmiaf"
.
          DISPLAY   *P64:24,"pmswtraf";
          OPEN      PMSWTRA1,"pmswtraf"
          OPEN      PMSWTRA2,"pmswtraf"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"oprdetbf";
          OPEN      OPRDETB1,"oprdetbf"
.
          DISPLAY   *P64:24,"patasfaf";
          OPEN      PATASFA1,"patasfaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P64:24,"patdadaf";
          OPEN      PATDADA1,"patdadaf"
.
          DISPLAY   *P64:24,"patdgwaf";
          OPEN      PATDGWA1,"patdgwaf"
.
          DISPLAY   *P64:24,"patvadaf";
          OPEN      PATVADA1,"patvadaf"
.
          DISPLAY   *P64:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH3,"patdschf"
          OPEN      PATDSCH2,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA1,"patecdaf"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"patecmaf";
          OPEN      PATECMA1,"patecmaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA1,"patecoaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA1,"patecoaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P64:24,"paticddf"
          CALL      OPICD1           
.
          DISPLAY   *P64:24,"paticdof"
          CALL      OPICO1
.
          DISPLAY   *P64:24,"paticuaf";
          OPEN      PATICUA1,"paticuaf"
.
          DISPLAY   *P64:24,"patlinkf";
          OPEN      PATLINK1,"patlinkf"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A4,"patmi1af"
          OPEN      PATATRA3,"patatraf"
.         
          DISPLAY   *P64:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patpgraf";
          OPEN      PATPGRA1,"patpgraf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"

.
          DISPLAY   *P64:24,"patwvetf";
          OPEN      PATWVET1,"patwvetf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMES
.
          READ      CONTROLF,TEN;*37,CMOPRT,*54,CFILENO,*235,CMDISP,CMDISS:
                                 *237,CMDISC,*238,CMDISA
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TWENTY1;*214,PTCNUREA                    * ?????
          READ      CONTROLF,TWENTY1;*220,PTNSWHDP
          READ      CONTROLF,FIFTY9;*220,PTCNUADT
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D
          READ      CONTROLF,HUND03;*245,PTCNWAUC
          READ      CONTROLF,HUND05;*158,PTCNDGPV,*185,PTCNELPS
          READ      CONTROLF,TWENTY1;*46,PTCNBAGE
          READ      CONTROLF,HUND10;*228,PTCNHCCC
          READ      CONTROLF,HUND19;*183,PTCNED41,*191,PTCNED42,*199,PTCNED50:
                                    *207,PTCNED51,*215,PTCNED52,*223,PTCNED60:
                                    *231,PTCNED62,*239,PTCNED70       * 303652
.
          READ      CONTROLF,NINTY8;*149,PTCNGDV2
          READ      CONTROLF,HUND09;*151,PTCNGDV3
          READ      CONTROLF,HUND05;*217,PTCNGDV4,*227,PTCNGDV5
          READ      CONTROLF,HUND12;*239,PTCNGDV6
          READ      CONTROLF,HUND10;*61,PTCNGDV7,*69,PTCNGDV8,*77,PTCNGDV9:
                                    *85,PTCNGDVX
          READ      CONTROLF,HUND17;*220,PTCNGDX1
          READ      CONTROLF,HUND20;*232,PTCNED80
          READ      CONTROLF,HUND23;*232,PTCNED90
          READ      CONTROLF,HUND24;*224,PTCNE100
          READ      CONTROLF,HUND28;*231,PTCNGDX2,*239,PTCNE110       *I-0917793
          READ      CONTROLF,HUND30;*86,PTCNGDX3,*94,PTCNE120         *I-0956210
.
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 DSCD0000               Called by: ML0000   *
.*                         Keyin The Discharge Date                           *
.******************************************************************************
DSCD0000  MOVE      ZERO,EXIT
          MOVE      ZERO,DOYR2025
          CALL      CRET0000
.
. ----- Enter Starting Date -----
.
DSCD1000  DISPLAY   *P1:8,*EF,"Starting Discharge Date : ":
                    *P1:9,*EL,"Ending   Discharge Date : ";
          MOVE      "8",CVERT
          MOVE      "27",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,DSCD9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE                 * Pack the starting date
.
          MATCH     JULY2025,STRTDATE   
          IF        !@LESS
            MOVE      ONE,DOYR2025
          ENDIF
.
.
. ----- Check If Starting Date Is Greater Than Today's Date -----
.
          MATCH     STRTDATE,CURDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                      "date. ";
            CALL      HITENTER
            GOTO      DSCD1000
          ENDIF
.
. ----- Enter Ending Date -----
.
DSCD2000  MOVE      "9",CVERT
          MOVE      "27",CCOL
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,DSCD1000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the starting date
.
. ----- Check If Ending Date Is Greater Tha Today's Date -----
.
          MATCH     ENDDATE,CURDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                      "date. ";
            CALL      HITENTER
            GOTO      DSCD2000
          ENDIF
.
. ----- Check That The Ending Date Is Greater Than The Starting Date -----
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending date must be greater than the ":
                      "starting date. ";
            CALL      HITENTER
            GOTO      DSCD2000
          ENDIF
.
. ----- Check If Discharge Date Range Exists -----
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2                * Position on discharge file
          CALL      RDKDSCH2                * Read the discharge file
          BRANCH    OVRCD,DSCD3000
.
          MATCH     DDATE,ENDDATE
          GOTO      DSCD9999 IF NOT LESS    * Patients found in date range, exit
.
DSCD3000  DISPLAY   *P1:24,*EL,*B,"No discharged patients found in the ":
                    "date range. ";
.>>>>     CALL      HITENTER
.>>>>     GOTO      DSCD1000                * removed on 15/05/2012 (DS)
          GOTO      DSCD9999
.
DSCD9000  MOVE      ONE,EXIT
DSCD9999  RETURN
+
.******************************************************************************
.*                                 CDID0000               Called by: ML0000   *
.*                        Question of Sending coder ID                        *
.******************************************************************************
CDID0000  MOVE      ZERO,SCODFLG              * default to not sending ID
.
          DISPLAY   *P1:10,*EF,"Do you want to send coder ID":
                               " (",*V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?";
CDID1000  KEYIN     *P40:10,*DV,UNDLN1,*P40:10,*V2LON,ANS;
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      CDID1100 IF EQUAL
          REP       UPPLOW,ANS
          DISPLAY   *P40:10,*V2LON,ANS;
.
          CMATCH    "Y",ANS
          GOTO      CDID2000 IF EQUAL
          CMATCH    "N",ANS
          GOTO      CDID9999 IF EQUAL
CDID1100  BEEP
          GOTO      CDID1000
.
CDID2000  MOVE      ONE,SCODFLG             * Send coder id
CDID9999  RETURN
+
.******************************************************************************
.*                                 ACTV0000               Called by: ML0000   *
.*                Ask if user is sending activity codes                       *
.* Returns: SENDFLAG  0 = Yes, send activity codes                            *
.*                    1 = No, don't send activity codes                       *
.******************************************************************************
.
ACTV0000  KEYIN     *P1:11,*EL,"Sending Activity codes (",*V2LON,*DV,ANSY,*HOFF:
                    "/",*V2LON,*DV,ANSN,*HOFF,") :":
                    *P32:11,*V2LON,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * yes - entered ?
          IF        @EQUAL
            MOVE      ZERO,SENDFLAG              * yes
            GOTO      ACTV9999
          ENDIF
.
          MATCH     ANSN,ANS                     * no - entered ?
          IF        @EQUAL
            MOVE      ONE,SENDFLAG               * yes
            GOTO      ACTV9999
          ENDIF
.
          BEEP                                   * invalid entry
          GOTO      ACTV0000
.
ACTV9999  RETURN
+
.******************************************************************************
.*                                 SADM0000               Called by: ML0000   *
.*                        Question of generating an admission                 *
.******************************************************************************
SADM0000  DISPLAY   *P1:12,*EF,"Do you want to generate Data for an admission":
                               " (",*V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?";
SADM1000  KEYIN     *P55:12,*DV,UNDLN1,*P55:12,*V2LON,ANS;
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      SADM1100 IF EQUAL
          REP       UPPLOW,ANS
          DISPLAY   *P55:12,*V2LON,ANS;
.
          CMATCH    "Y",ANS
          GOTO      SADM1150 IF EQUAL 
          CMATCH    "N",ANS
          GOTO      SADM9999 IF EQUAL
SADM1100  BEEP
          GOTO      SADM0000
.
SADM1150  DISPLAY   *P1:13,*EL,"Please enter the type of update :";
          DISPLAY   *P1:24,*V2LON,"Y",*HOFF,"=Updated, ",*V2LON,"D":
                    *HOFF,"=Deleted, ",*V2LON,"M",*HOFF,"=Merged, ":
                    *V2LON,"C",*HOFF,"=Confirmed ",*V2LON,"V":
                    *HOFF,"=Validation";
          KEYIN     *P35:13,ANS;
          REP       UPPLOW,ANS
          MOVE      ANS,DIM1
          REP       "Y1D1M1C1V1",DIM1
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
          MATCH     SP1,ANS
          GOTO      SADM1000 IF EQUAL
          MATCH     "1",DIM1
          IF        @EQUAL
            MOVE      ANS,XUPDFLG
          ELSE
            GOTO      SADM1150
          ENDIF
.
SADM2000  KEYIN     *P1:14,*EF,"Admission No. : ",*V2LON,AADMNO;
          MATCH     ZEROVISN,AADMNO
          GOTO      SADM2100 IF NOT EQUAL
          DISPLAY   *P1:14,*EL;
          GOTO      SADM1150
.
SADM2100  PACK      KEY8,AADMNO
          CALL      RDMISS1
          BRANCH    OVRCD,SADM2200
.
          PACK      KEY8,AADMNO
          CALL      RDVISA1                 * Read the visit file
          BRANCH    OVRCD,SADM2200
.
          PACK      KEY8,AADMNO
          CALL      RDPMVX11                * Read the visit extn file
          BRANCH    OVRCD,SADM2200
.
          COMPARE   FIVE,ASTAT          * Cancelled ?
          GOTO      SADM2150 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Patient is cancelled.**    ";
          CALL      HITENTER
          GOTO      SADM2000
.
SADM2150  COMPARE   THREE,ASTAT         * Discharged ?
          GOTO      SADM3000 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Patient is not discharged.**    ";
          CALL      HITENTER
          GOTO      SADM2000
.
SADM2200  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid admission number.**    ";
          CALL      HITENTER
          GOTO      SADM2000
.
.         check if master data exist for this Admission Number
.
SADM3000  MOVE      AURNO,KEY8
          CALL      RDMAST1
          COMPARE   ZERO,OVRCD
          IF        @EQUAL
            CALL      RDPMPX21
            COMPARE   ZERO,OVRCD
            GOTO      SADM4000 IF EQUAL
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Missing master record.**    ";
          CALL      HITENTER
          GOTO      SADM2000
.
.         display basic details for this patient and ask if patient is correct
.
SADM4000  DISPLAY   *P1:13,*EF,*P1:14,"Admission No. : ":
                    *P26:14,"Name :",*P65:14,"Sex :":
                    *P1:15,"U/R Number    :",*P26:15,"Born : ",*P44:15,"Age :":
                    *P54:15,"Admission Date : ":
                    *P1:16,"Ward : ",*P16:16,"Doct : ",*P54:16,"Discharge":
                           " Date :";
.
          CALL      GDET0000                * get patient's details
          BRANCH    EXIT,SADM2000
          CALL      CALCAGE
.
          DISPLAY   *P17:14,*V2LON,AADMNO,*P33:14,PATNAME,*P71:14,SEX:
                    *P17:15,AURNO,*P33:15,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *P50:15,PSAGEYRS,*P71:15,SAVDATE:
                    *P8:16,WRDBED,*P23:16,DOCNAME,*P71:16,CPCDATE;
.
.         patient who discharged within the date range must have been processed
.
          MATCH     STRTDATE,DDATE
          GOTO      SADM5000 IF LESS
.
          MATCH     DDATE,ENDDATE
          GOTO      SADM5000 IF LESS 
.
          DISPLAY   *P1:19,*EL,*B,*V2LON,"** Patient has been processed. **  ";
          CALL      HITENTER
          GOTO      SADM2000
.
SADM5000  DISPLAY   *P1:24,*EL,"Correct Patient (",*V2LON,"Y",*HOFF,"/",*V2LON:
                               "N",*HOFF,") ?";
.
SADM7000  KEYIN     *P25:24,*DV,UNDLN1,*P25:24,*V2LON,ANS;
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      SADM7200 IF EQUAL
          REP       UPPLOW,ANS
          DISPLAY   *P25:24,*V2LON,ANS;
.
          MATCH     ANSY,ANS
          GOTO      SADM8000 IF EQUAL
          MATCH     ANSN,ANS
          GOTO      SADM2000 IF EQUAL
.
SADM7200  BEEP
          GOTO      SADM7000
.
SADM8000  MOVE      ZERO,COUNTO
          MOVE      ZERO,COUNTM
          MOVE      ZERO,COUNTEX
          MOVE      ZERO,COUNTD
          CALL      SETF0000                * Set up fields
          CALL      WREC0000                * Write Record
          CALL      ECDA0000                * Get dis & ops after

          CALL      ECOA0000                * writing summary details.
          CALL      WRCHK
          GOTO      SADM2000                * Next admission
.
SADM9999  RETURN
+
.******************************************************************************
.*                                  GDET0000                                  *
.*        get patient's details                called by : GAEP0000, SADM0000 *
.******************************************************************************
GDET0000  MOVE      ZERO,EXIT
.
. ------  get patient's name  ------
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
.
. ------  get patient's sex  ------
.
          PACK      SEX,SP70
          PACK      KEY5,ANSG,SP1,PSEX,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      SEX,TCINDC1,SP70
          ENDIF
.
.
. ------  get patient's date of birth  ------
.
GDET1200  UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
.
          MATCH     SP3,AWARD
          GOTO      GDET2100 IF NOT EQUAL
.
          MOVE      "N/A",AWARD
          MOVE      SP3,ABED
.
GDET2100  PACK      WRDBED,AWARD,ABED            * for screen display
.
. ------  get attending doctor  ------
.
          PACK      PACFNAME,SP30,SP30
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GDET3000
.
          LENSET    DGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCNAME
.
. ------  get Admission Date  ------
.
GDET3000  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,SAVDATE
.
. ------  get Discharge Date  ------
.
          MOVE      SP8,CPCDATE
          PACK      KEY8,AADMNO
          CALL      RDDSCH1
          BRANCH    OVRCD,GDET8000
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.                                               * I CAR 37921
          GOTO      GDET9000
.
GDET8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Missing discharged record.**    ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      GDET9999
.
GDET9000  MOVE      ZERO,EXIT
GDET9999  RETURN
+
.******************************************************************************
.*                                 EXTC0000               Called by: ML0000   *
.*                        Load The Admission Details                          *
.******************************************************************************
EXTC0000  MOVE      ZERO,EXIT
          MOVE      SP1,XUPDFLG             * set update flag to "No"
          MOVE      ZERO,RECNUM
          MOVE      ZERO,CHKFLAG
          MOVE      ZERO,SUMCOUNT
          MOVE      ZERO,TOTCOUNT
          DISPLAY   *P1:24,*EL,"Scanning : ",*P25:24,"Loading : ";
          CALL      CREA0000                * Open the tempfile
          CALL      VHCD0000                * validating hospital code
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2                * Position on the discharge file
EXTC1000  CALL      RDKDSCH2                * Read the discharge file
          BRANCH    OVRCD,EXTC8000
.
          MATCH     DDATE,ENDDATE
          GOTO      EXTC8000 IF LESS        * Out of date range, exit
.
          COMPARE   ONE,IBCNMHOS
          GOTO      EXTC1100 IF NOT EQUAL
.                                           * Multi Hospital - identify current
.                                           * checking against Visit Extn.
          PACK      KEY8,DDADMNO
          CALL      RDPMVX11                * Read a visit extension record
          IF        OVRCD=1
            MOVE      "Missing visit ext file record",ERRDESC
            CALL      WRTEMP
            GOTO      EXTC1000              * Print an error message
          ENDIF
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      EXTC1000 IF NOT EQUAL
.
EXTC1100  DISPLAY   *P12:24,*V2LON,DADMNO;
          PACK      KEY8,DURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY8,DURNO
          CALL      RDPMPX21                * Read Extn File 2
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY8,DADMNO
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY8,DADMNO
          CALL      RDVISA1                 * Read the visit file
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY8,DADMNO
          CALL      RDPMVX11                * Read the visit extn file
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY5,CATA,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,EXTC7000
.
          MATCH     "X",TCINDC11            * srf 638331
          GOTO      EXTC1000 IF EQUAL
.
          CALL      CHCO0000                * check if patient is coded
.....     BRANCH    CODEFLG,EXTC1000
.
EXTC7000  MATCH     "5",DASTAT
          GOTO      EXTC1000 IF EQUAL       * no cancelled records
.
          MOVE      ZERO,COUNTO
          MOVE      ZERO,COUNTM
          MOVE      ZERO,COUNTEX
          MOVE      ZERO,COUNTD
          CALL      SETF0000                * Set up fields
          CALL      WREC0000                * Write Record
          MATCH     SP70,ACODDTE
          IF        !@EQUAL
            MATCH     ACODDTE,ENDDATE
            IF        !@LESS
              MOVE      ONE,CODINGFL
              CALL      WREC0000                * Write Record
              CALL      ECDA0000                * Get dis & ops after
              CALL      ECOA0000                * writing summary details.
              MOVE      ZERO,CODINGFL
            ENDIF
          ENDIF
          CALL      WRCHK    
          GOTO      EXTC1000
.
EXTC8000  CALL      CODC0000               * send records that have been coded
          CALL      PMIT0000               * write PMI trigger records
          CALL      TRIG0000               * send records that have been altered
.
....      CALL      SADM0000                * process an admission ?
.
          MOVE      SP10,DIM8
          MOVE      TOTCOUNT,DIM8           * total line count (line 1)
          REP       " 0",DIM8
          WRITE     WORK,SEQ;DIM8
.
          MOVE      SP10,KEY8
          MOVE      SUMCOUNT,KEY8           * SUM record count (line 2)
          REP       " 0",KEY8
          WRITE     WORK,SEQ;KEY8
.
          DISPLAY   *P1:24,*EL,*V2LON,*B,"Transmission completed. ";
          CALL      HITENTER
.
EXTC9999  RETURN
+
.******************************************************************************
.*                                  CODC0000              Called by: PROC0000 *
.*   Send records that were discharged previously but coded within the period *
.******************************************************************************
CODC0000  PACK      KEY16,STRTDATE,SP70
          CALL      RDSMISS4
CODC1000  CALL      RDKMISS4
          BRANCH    OVRCD,CODC9999
.
          MATCH     ACODDTE,ENDDATE
          GOTO      CODC9999 IF LESS
.
.
          MOVE      ZERO,UPDATEFL
.
. the following code is to check if this patients coding has been sent in a
. previous extract.  patecmaf stores acoddte changes.  If acoddte has been run
. in another extract, then send a U to show it is an updated record.
          PACK      KEY18,DAADMNO,SP1,ONE,STRTDATE,SP70
          CALL      RSPTECM1
          CALL      RPPTECM1
          BRANCH    OVRCD,CODC1100
.
          MATCH     DAADMNO,DPTEMADM
          GOTO      CODC1100 IF NOT EQUAL
.
          MOVE      ONE,UPDATEFL
.
CODC1100  COMPARE   ONE,IBCNMHOS
          GOTO      CODC5000 IF NOT EQUAL
.
          PACK      KEY8,DAADMNO
          CALL      RDVISA1                 * Read the visit file
          BRANCH    OVRCD,CODC1000
.
          PACK      KEY8,DAADMNO
          CALL      RDPMVX11                * Read a visit extension record
          BRANCH    OVRCD,CODC1000
.
          MATCH     PMVXMHOS,PTHSHOSP       * only want selected hospital
          GOTO      CODC1000 IF NOT EQUAL
.
CODC5000  PACK      KEY8,DAADMNO,SP70
          CALL      RDDSCH1
          BRANCH    OVRCD,CODC1000
.
          MATCH     STRTDATE,DDATE
          GOTO      CODC1000 IF NOT LESS
.
          MOVE      ZERO,COUNTO
          MOVE      ZERO,COUNTM
          MOVE      ZERO,COUNTEX
          MOVE      ZERO,COUNTD
          CALL      SETF0000                * Set up fields
          MOVE      ONE,CODINGFL
          CALL      WREC0000                * Write Record
          MOVE      ZERO,CODINGFL
          CALL      ECDA0000                * Get dis & ops after
          CALL      ECOA0000                * writing summary details.
          CALL      WRCHK
.
          GOTO      CODC1000
.
CODC9999  RETURN
+
.******************************************************************************
.*                                  TRIG0000              Called by: PROC0000 *
.*   Send records that have changed (pmswtraf records)                        * 
.****************************************************************************** 
TRIG0000  CALL      CSFY0000     * calculate start of previous financial year
          COMPARE   ONE,RERUNFL
          IF        @EQUAL
            PACK      KEY19,PTHSHOSP,STRTDATE,SP70
          ELSE
            PACK      KEY19,PTHSHOSP,SP70
          ENDIF
          CALL      RSPMWTR2
TRIG1000  CALL      RKPMWTR2
          BRANCH    OVRCD,TRIG9999
.
          MATCH     PTHSHOSP,PMWTHOSP
          GOTO      TRIG9999 IF NOT EQUAL
.
          COMPARE   ONE,RERUNFL
          IF        @EQUAL
            MATCH     STRTDATE,PMWTSDAT
            GOTO      TRIG2000 IF EQUAL
            MATCH     PMWTSDAT,ENDDATE
            GOTO      TRIG9999 IF LESS
          ELSE
            MATCH     SP70,PMWTSDAT
            GOTO      TRIG9999 IF NOT EQUAL
          ENDIF
.
TRIG2000  IF        ADHOCEXT = 1
.           MATCH     PMWAHOSP,PTHSHOSP
.           GOTO      TRIG5000 IF NOT EQUAL
.
.           MATCH     PMWASDAT,STRTDATE
.           GOTO      TRIG5000 IF NOT EQUAL
.
.           MATCH     PMWAEDAT,ENDDATE
.           GOTO      TRIG5000 IF NOT EQUAL
.
.           MATCH     PMWTSDAT,STRTDATE          * CAR 273655
.           GOTO      TRIG1000 IF NOT EQUAL
.
          ENDIF
.
TRIG5000  PACK      SAVKEY19,PMWTHOSP,PMWTSDAT,PMWTVISN,SP70
.
          PACK      KEY8,PMWTVISN,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,TRIG8000
.
          COMPARE   "5",ASTAT
          IF        @EQUAL
            PACK      KEY19,SAVKEY19,SP70
            CALL      DEPMWTR2
            GOTO      TRIG1000
          ENDIF
.
          PACK      KEY8,PMWTVISN,SP70
          CALL      RDDSCH1
          BRANCH    OVRCD,TRIG1000
.
          MATCH     STRTDATE,DDATE
          GOTO      TRIG1000 IF NOT LESS
.
.davvy    MATCH     SFINYEAR,DDATE          * removed for CAR 272377
.davvy    IF        @LESS
.davvy      PACK      KEY19,SAVKEY19,SP70
.davvy      CALL      DEPMWTR2
.davvy      GOTO      TRIG1000
.davvy    ENDIF
.
          PACK      KEY8,PMWTVISN
          CALL      RDVISA1                 * Read the visit file
          BRANCH    OVRCD,TRIG1000
.
          MOVE      ZERO,COUNTO
          MOVE      ZERO,COUNTM
          MOVE      ZERO,COUNTEX
          MOVE      ZERO,COUNTD
          CALL      SETF0000                * Set up fields
          MOVE      ONE,UPDATEFL
.
          MATCH     SP70,ACODDTE
          GOTO      TRIG7000 IF EQUAL
.
          MATCH     STRTDATE,ACODDTE
          IF        !@LESS
            PACK      KEY19,SAVKEY19,SP70
            CALL      DEPMWTR2              * CAR 275490
            GOTO      TRIG1000
          ENDIF
.
          MATCH     ACODDTE,ENDDATE
          IF        !@LESS
            MOVE      ONE,CODINGFL
          ENDIF
.
TRIG7000  CALL      WREC0000                * Write Record
          MOVE      ZERO,UPDATEFL
          CALL      ECDA0000                * Get dis & ops after
          CALL      ECOA0000                * writing summary details.
          CALL      WRCHK
          MOVE      ZERO,CODINGFL
.
          BRANCH    RERUNFL,TRIG1000
          GOTO      TRIG8000
.
TRIG8000  PACK      PMWTSDAT,STRTDATE,SP70
          PACK      KEY19,PMWTHOSP,PMWTSDAT,PMWTVISN,SP70
          CALL      RAPMWTR2
          IF        OVRCD=0
            COMPARE   ONE,ADHOCEXT
            GOTO      TRIG8500 IF EQUAL       * don't remove if adhoc=yes
.
            PACK      KEY19,PMWTHOSP,PMWTSDAT,PMWTVISN,SP70
            CALL      DEPMWTR2
            GOTO      TRIG8500
          ENDIF
.
          PACK      KEY19,SAVKEY19,SP70
          CALL      RDPMWTR2
          IF        OVRCD=0
            COMPARE   ONE,ADHOCEXT
            GOTO      TRIG8500 IF EQUAL       * don't remove if adhoc=yes
.
            PACK      PMWTSDAT,STRTDATE,SP70
            CALL      UPPMWTR2
          ENDIF
.
TRIG8500  PACK      KEY19,SAVKEY19,SP70
          CALL      RSPMWTR2
          GOTO      TRIG1000
.
TRIG9999  IF        RERUNFL=1
            MOVE      ZERO,RERUNFL
            GOTO      TRIG0000
          ENDIF
          RETURN
+
.*******************************************************************************
.*                              CSFY0000                  CALLED BY : ML1000   *
.*                                                                    ML2000   *
.* Calculate the Start of financial year from the end of current month/quarter *
.*******************************************************************************
CSFY0000  UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY  * End of period/current month
.
          MOVE      CMON,FCMON
          MOVE      CYEAR,FCYEAR
          COMPARE   FCMON,SIX                * Month > June
          IF        @LESS
            SUB       ONE,FCYEAR
          ELSE
            SUB       TWO,FCYEAR
          ENDIF
          MOVE      FCYEAR,CYEAR
.
CSFY1000  PACK      SFINYEAR,CCENT,CYEAR,ZERO,SEVEN,ZERO,ONE * Start of fin. Yr
.
CSFY9999  RETURN
+
.******************************************************************************
.*                                  PMIT0000              Called by: PROC0000 *
.*   Send records that have changed pmi details                               * 
.****************************************************************************** 
PMIT0000  CALL      CSFY0000     * calculate start of previous financial year
          PACK      KEY11,SP70
          CALL      RSP2PMI1
PMIT0100  CALL      RKP2PMI1
          BRANCH    OVRCD,PMIT9999
.
          PACK      KEY24,P2PMURNO,Z70
          CALL      RDSDSCH3
PMIT1000  CALL      RDPDSCH3
          BRANCH    OVRCD,PMIT5000
.
          MATCH     P2PMURNO,DURNO
          GOTO      PMIT5000 IF NOT EQUAL   * same u/r?
.
.davvy    MATCH     SFINYEAR,DDATE          * removed for CAR 272377
.davvy    GOTO      PMIT1000 IF LESS        * Ddate must be >= start HDP date
.
          PACK      KEY8,DADMNO,SP70
          CALL      RDMISS1                 * get admission date
          BRANCH    OVRCD,PMIT5000
.
          MATCH     STRTDATE,DDATE
          GOTO      PMIT1000 IF NOT LESS
.
          PACK      KEY8,DAADMNO
          CALL      RDVISA1                 * Read a visit record
          BRANCH    OVRCD,PMIT1000
.
          COMPARE   ONE,IBCNMHOS
          GOTO      PMIT3000 IF NOT EQUAL
.
          MATCH     PMVXMHOS,PTHSHOSP       * only want selected hospital
          GOTO      PMIT1000 IF NOT EQUAL
.
PMIT3000  PACK      KEY19,PMVXMHOS,AADMNO,SP70
          CALL      RDPMWTR1
          IF        OVRCD<>1
            GOTO      PMIT5000    * record is already on file to be resent
          ENDIF
.
          CALL      IBACLOCK                * Get the current date
          PACK      PMWTCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMWTCDAT
          MOVE      CTIMEIS,PMWTCTIM
.
          PACK      PMWTHOSP,PMVXMHOS,SP70
          PACK      PMWTVISN,AADMNO,SP70
          PACK      PMWTSDAT,SP70
          PACK      PMWTMDAT,SP70
          PACK      PMWTWEBC,WEBUSRID,SP70
.
          PACK      KEY19,PMWTHOSP,PMWTVISN,PMWTSDAT,SP70
          CALL      RAPMWTR1
          IF        OVRCD=1
            PACK      KEY19,PMWTHOSP,PMWTVISN,PMWTSDAT,SP70
            CALL      WRPMWTR1
          ENDIF
.
          GOTO      PMIT1000
.
PMIT5000  PACK      KEY11,P2PMURNO,P2PMUNIQ
.
          COMPARE   ONE,ADHOCEXT
          GOTO      PMIT0100 IF EQUAL       * don't remove if adhoc=yes
.
          CALL      DEP2PMI1
          GOTO      PMIT0100
.
PMIT9999  RETURN
+
.******************************************************************************
.*                                  CHCO0000              Called by: PROC0000 *
.*                           Check If Patients Coded                          *
.******************************************************************************
CHCO0000  MOVE      ZERO,CODEFLG            * Default to coded
          PACK      KEY13,AADMNO,SP20                                 *C-240107
          CALL      RSPTECD1
          CALL      RKPTECD1
          BRANCH    OVRCD,CHCO1000
.
          MATCH     AADMNO,PTEDADMN
          GOTO      CHCO9000 IF EQUAL
.
CHCO1000  PACK      KEY13,AADMNO,SP20                                 *C-240107
          CALL      RSPTECO1
CHCO2000  CALL      RKPTECO1
          BRANCH    OVRCD,CHCO3000
.
          MATCH     AADMNO,PTEOADMN
          GOTO      CHCO3000 IF NOT EQUAL
.
          MATCH     ANSX,PTEOTYPE
          GOTO      CHCO2000 IF EQUAL
          GOTO      CHCO9000
.
CHCO3000  MOVE      ONE,CODEFLG             * Not coded
.
CHCO9000  MOVE      ZERO,EXIT
CHCO9999  RETURN
+
.******************************************************************************
.*                                 IVAR0000               Called by: EXTC0000 *
.*                        Initialisation variables                            *
.******************************************************************************
IVAR0000  MOVE      ZERO,EXIT
.
          UNPACK    SP70,XPROCCOD,XCLINPR,XDATPROC
          UNPACK    SP70,XPRIOR,XICDCOD,XPLCOCC,XACTIV
          UNPACK    SP70,XCOFICD,XCOFPOO,XCOFACT
          UNPACK    SP70,XDCCRFL
          UNPACK    SP70,XPCCRFL
          UNPACK    SP70,XUPDFLG
.
          UNPACK    SP50,XDAYQNB                 * CAR 265260
          MOVE      ZERO,XACCOCC
          UNPACK    SP50,XHIHCD
          UNPACK    SP50,XEVNTYP,XMORTYP,XEOCLNK,XNUMLEV,XLANG
          UNPACK    SP50,XSORLOC,XSORCLI,XSORTRA,XADMNO               *C-114471
          UNPACK    SP50,XPURNO,XMUMSID,XADATE,XATIME,XDDATE,XDTIME
          UNPACK    SP50,XPSNAME
          UNPACK    SP50,XPGNAME
          UNPACK    SP50,XSFNAME
          UNPACK    SP50,XPADD1
          UNPACK    SP50,XSUBRB
          UNPACK    SP50,XPCODE,XSTATE,XDOBDT,XPSEX,XABORN
          UNPACK    SP50,XCNTBR,XMSTAT,XEMPST,XINTRP,XOCCPC,XSREFR
          UNPACK    SP70,XINLOS,XCTHOS,XLOCTN,XNMRNO,XSPLTY,XSPCDS
          UNPACK    SP50,XADTYP,XWEIGH,XLEVDY,XPSYCH,XMHLGL
          UNPACK    SP50,XPAYCL,XVETRN,XINSRN,XICUDY,XVENHR,XREADM
          UNPACK    SP50,XURETN,XEPSCR,XCLNST,XCTRES,XSEPTP,XDSCTO
          UNPACK    SP50,XDSATT,XGRPVR,XDIACG,XDIAGR,XCODID,XDVAFN
          UNPACK    SP50,XDVAAN,XDVAAD
          UNPACK    SP70,XHOMEPH,XCONTPH,XRESSTA,XICUHR,XPMEDIC
          PACK      XCONLDY,SP1,SP1,ZERO
          UNPACK    SP70,XNDISFL,XNDISCN,XGENDER,XMDCLDTE,XMDCLTME
          UNPACK    SP70,XMDCPRSN,XMDCSRSN
.
          MOVE      ZERO,NUMLEV
          MOVE      SP10,ACTIVITY
          MOVE      ONE,F2
.
IVAR9999  RETURN
+
.******************************************************************************
.*                                 SETF0000               Called by: EXTC0000 *
.*                        Set up fields                                       *
.******************************************************************************
SETF0000  CALL      IVAR0000                     * initialise variables
.
........  MOVE      AADMNO,FORM8
.......   MOVE      FORM8,DIM8
          MOVE      AADMNO,DIM8 

          PACK      XADMNO,SP4,DIM8              * Admission Number
          REP       " 0",XADMNO
          MOVE      AADMNO,SKEY8
.
....      IF        AURNO = 0
          MATCH     "       0",AURNO
          IF        @EQUAL
            MOVE      "Missing U/R number ",ERRDESC
            CALL      WRTEMP 
          ELSE
            MOVE      AURNO,DIM8
            PACK      XPURNO,SP2,DIM8              * U/R Number
            LJUSTIFY  XPURNO
          ENDIF
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          COMPARE   ZERO,OVRCD
          IF        @EQUAL
            CALL      RDPMPX21
          ENDIF
.
          PACK      KEY5,ANSC,ANSC,ACARE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            UNPACK    THCSCOD,DIM1,DIM1A
            MATCH     "6",DIM1A
            IF        @EQUAL
              MATCH     ZEROVISN,AMUMADM
              IF        !@EQUAL
                MOVE      AADMNO,DIM8             * save baby's admissno
                MOVE      AMUMADM,KEY8            * read mother's adm.record
                CALL      RDMISS1
                IF        OVRCD<>1
                  PACK      XMUMSID,AURNO,SP70    * Mother's U/R number
                  LJUSTIFY  XMUMSID
                ENDIF
                MOVE      DIM8,KEY8               * restore baby's adm.record
                CALL      RDMISS1
              ENDIF
            ENDIF
          ENDIF
.
          PACK      XADATE,SP20
          MATCH     PBDATE,ADATE
          IF        @LESS
            MOVE      "Adm.Date must be >= DOB",ERRDESC
            CALL      WRTEMP
          ELSE
            MATCH     ADATE,DDATE
            IF        @LESS
              MOVE    "Disc.Date must be >= Adm.Date",ERRDESC
              CALL      WRTEMP
            ELSE
              UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
              PACK      XADATE,XDAY,XMON,XCENT,XYEAR
              REP       " 0",XADATE                  * adm.date
            ENDIF
          ENDIF
.
          UNPACK    ATIME,DIM2,ANS,DIM2A
          PACK      XATIME,DIM2,DIM2A
          REP       " 0",XATIME                  * adm.time
          MOVE      ZERO,FORM4
          MOVE      XATIME,FORM4
          IF        FORM4 >= 2400 
            MOVE      "Adm.Time is >= 2400 Hrs",ERRDESC
            CALL      WRTEMP
          ENDIF
.
          SQUEEZE   PTMIOGNO
          MOVE      PTMIOGNO,XEOCLNK            * EOC link admission number
.
          UNPACK    SP30,XSORLOC,XSORCLI,XSORTRA
          PACK      KEY5,ANSS,SP1,ASRCE,SP70     * location SOR
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,XSORLOC
          ENDIF
.
          MATCH     "00",XSORLOC
          GOTO      SETF0500 IF NOT EQUAL     * use adm transfer source code?
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPTDAD1
          BRANCH    OVRCD,SETF0500
.
          MOVE      "07",XSORLOC              * default (CAR 260777)
          PACK      KEY5,PTDAADTS,SP70
          CALL      RDPTVAD1
          BRANCH    OVRCD,SETF0500
.
          MATCH     "N",PTVADTYP
          IF        @EQUAL
            PACK    XSORLOC,ZERO,TWO
          ENDIF
          MATCH     "L",PTVADTYP
          IF        @EQUAL
            PACK    XSORLOC,ZERO,THREE
          ENDIF
          MATCH     "H",PTVADTYP
          IF        @EQUAL
            PACK    XSORLOC,ZERO,FOUR
          ENDIF
          MATCH     "P",PTVADTYP
          IF        @EQUAL
            PACK    XSORLOC,ZERO,FIVE
          ENDIF
          MATCH     "R",PTVADTYP
          IF        @EQUAL
            PACK    XSORLOC,ZERO,SIX
          ENDIF
.
SETF0500  PACK      KEY5,ANSS,SP1,ASRCE       * clinical SOR
          CALL      RDCODE1
          IF        OVRCD<>1
            UNPACK      THCSCOD,DIM2,XSORCLI
          ENDIF
.
          PACK      KEY5,ANSS,ANSI,PMVXSTRA     * transport SOR
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,DIM1
            MATCH     SP1,DIM1
            IF        @EQUAL
.davvy        MOVE      SIX,DIM1                * default to 6 if blank
              PACK      XSORTRA,SP70            * CAR 275584
            ELSE
              PACK      XSORTRA,ZERO,DIM1
            ENDIF
          ENDIF
.
          CALL      SDSC0000                     * setup discharged details
.
          PACK      XPSNAME,PSNAME,SP20,SP20
          MATCH     SP20,PSNAME
          IF        @EQUAL
            MOVE      "Surname is UNKNOWN ",ERRDESC
            CALL      WRTEMP
          ENDIF
          CALL      GSNM0000                * Get the given/second name
.
          PACK      DIM12,PMEDI,PTMXMCCD
          SQUEEZE   DIM12
          PACK      XPMEDIC,DIM12,SP10      * medicare + ref number
.
          CALL      CNDIS000                * check for ndis
.
          PACK      XPADD1,PADD1,SP20,SP20
          PACK      XPCODE,PPOST,SP10
          PACK      XSUBRB,SP30,SP30
.
          PACK      POSTCODE,PPOST,SP70
          PACK      XRESSTA,PVIRESI,SP70
          PACK      XHOMEPH,PTELEP,SP70
          LJUSTIFY  XHOMEPH                 * CAR 265260
          MATCH     SP70,PTMXCELL
          IF        !@EQUAL
            PACK      XCONTPH,PTMXCELL,SP70
          ELSE
            PACK      XCONTPH,PTELEP,SP70
          ENDIF
.
          PACK      KEY56,PPOST,PADD2,SP10,PADD4,SP70                 *C-240184
          CALL      RDIBPOS1                * Read a postcode file record
          BRANCH    OVRCD,SETF1000
.
          MOVE      IBPOSUBR,XSUBRB         * Suburb
          GOTO      SETF4000
.
SETF1000  PACK      KEY56,PPOST,PSUBRB,SP10,PADD4,SP70                *C-240184
          CALL      RDIBPOS1                * Read a postcode file record
          BRANCH    OVRCD,SETF2000
.
          MOVE      IBPOSUBR,XSUBRB         * Suburb
          GOTO      SETF4000
.
SETF2000  PACK      KEY56,PPOST,SP70                                  *C-240184
          CALL      RSIBPOS1                * Position & read a postcode file
SETF2100  CALL      RKIBPOS1                  record
          BRANCH    OVRCD,SETF3000
.
          MATCH     PPOST,IBPOPCOD
          GOTO      SETF3000 IF NOT EQUAL   * Different postcode, error
.
          MATCH     "OS ",IBPOSTAT
          GOTO      SETF2100 IF EQUAL
.
          MOVE      IBPOSUBR,XSUBRB         * Suburb
          GOTO      SETF4000
.
SETF3000  MOVE      "Invalid Post code ",ERRDESC
          CALL      WRTEMP
.
SETF4000  PACK      XSTATE,SP10
          MATCH     "8888",XPCODE
          IF        @EQUAL
            MOVE      PADD4,XSUBRB          * Suburb (CAR 260777 23/4)
            MOVE      "0",XSTATE
.
            PACK      PTCDKEY2,ANSC,SP70    * Match Country codes
            CALL      RDSCODE2 
SETF4001    CALL      RDKCODE2
            BRANCH    OVRCD,SETF4100
.
            MATCH     "C ",TCODE
            GOTO      SETF4100 IF NOT EQUAL
.
            MATCH     PADD4,TDESC           * Match country name
            IF        @EQUAL
              MOVE      PTCDLDES,XSUBRB     * Use long description
              GOTO      SETF4100
            ENDIF
            GOTO      SETF4001
          ENDIF
.
          MATCH     "NSW",IBPOSTAT
          IF        @EQUAL
            MOVE      "1",XSTATE
            GOTO      SETF4100
          ENDIF
          MATCH     "VIC",IBPOSTAT
          IF        @EQUAL
            MOVE      "2",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "QLD",IBPOSTAT
          IF        @EQUAL
            MOVE      "3",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "SA ",IBPOSTAT
          IF        @EQUAL
            MOVE      "4",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "WA ",IBPOSTAT
          IF        @EQUAL
            MOVE      "5",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "TAS",IBPOSTAT
          IF        @EQUAL
            MOVE      "6",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "NT ",IBPOSTAT
          IF        @EQUAL
            MOVE      "7",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "ACT",IBPOSTAT
          IF        @EQUAL
            MOVE      "8",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MOVE      "0",XSTATE
.
SETF4100  UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          MATCH     SP8,PBDATE
          IF        @EQUAL
            MOVE      "Missing Date of Birth ",ERRDESC
            CALL      WRTEMP
          ELSE
            PACK      XDOBDT,XDAY,XMON,XCENT,XYEAR   * dob
          ENDIF
.
.                                           * original code for sex desc. has
.                                           * been deleted & replaced by :-
          PACK      XPSEX,SP70
          PACK      KEY5,ANSG,SP1,PSEX,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      XPSEX,TCINDC1,SP70
          ENDIF
.             
          CALL      INDG0000                     * Indigenous status
.
          PACK      XCNTBR,SP10                  * Country of birth
          PACK      KEY5,ANSC,SP1,PCONT
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,XCNTBR
          ENDIF
.
          MATCH     SP4,XCNTBR
          IF        @EQUAL
            MOVE      "Country of Birth code is blank ",ERRDESC
            CALL      WRTEMP
          ELSE
            TYPE      XCNTBR
            IF        !@EQUAL
              MOVE      "Country of Birth Code is in not in Numeric ",ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
.
          PACK      XMSTAT,SP10                  * Marital status
          PACK      KEY5,ANSM,SP1,PMSTAT
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      XMSTAT,THCSCOD,SP20        * Marital status
          ENDIF
          MATCH     SP1,XMSTAT   
          IF        @EQUAL
            MOVE      "Marital status is blank ",ERRDESC
            CALL      WRTEMP
          ELSE
            MOVE      ZERO,FORM1
            MOVE      XMSTAT,FORM1
            IF        FORM1 < 1 | FORM1 > 6
              MOVE     "Marital status must be between 1 to 6 ",ERRDESC
              CALL      WRTEMP
            ENDIF
            CALL      CALCAGE
            IF        PSAGEYRS < 14
              MATCH     "1",XMSTAT
              IF        !@EQUAL
                MOVE      "Age < 14, marital status shoud be 1 (Single)",ERRDESC
                CALL      WRTEMP
              ENDIF
            ENDIF
          ENDIF
.
          CALL      EMPL0000                * Get occupation/employment stat
.
          CALL      LANG0000                * routine from IBAPMS49
.
SETF4500  MOVE      SP10,XSREFR               * Source of referral
.
SETF5000  CALL      ILOS0000                  * intended LOS
.
          CALL      CLST0000                  * Client status
.
          CALL      ADMF0000                  * contracting/adm.from/trans.to
.
          CALL      ADST0000                  * admission status
.
          CALL      WGHT0000                  * birth weight
.
          CALL      PYCL0000                  * Payment classification
          CALL      VETR0000                  * Veteran
.
          CALL      INST0000                  * Insurance status
.
          CALL      GEND0000                  * Gender
.
SETF5100  MOVE      "028",DIM3
          CALL      GPTATR00
          IF        EXIT = 1
            CALL      CLPATATR
            GOTO      SETF5500 
          ENDIF
.
SETF5120  UNPACK    PTARTXT1,CCENT,CYEAR,CMON,CDAY
          PACK      XMDCLDTE,CDAY,CMON,CCENT,CYEAR
          UNPACK    PTARTXT2,CHOUR,DIM1,CMIN,DIM1,DIM2
          PACK      XMDCLTME,CHOUR,CMIN,DIM2
.
          MOVE      "z1",DIM2
          PACK      KEY5,DIM2,PTARCOD1
          CALL      RDCODE1
          IF        OVRCD = 1
            GOTO      SETF5180
          ENDIF
.
          MATCH     "I",PTCOACTV            * Inactive?
          GOTO      SETF5180 IF EQUAL
.  
SETF5140  UNPACK    THCSCOD,XMDCPRSN
          STRIP     XMDCPRSN 
          MOVE      ZERO,F2
          MOVE      XMDCPRSN,F2
          IF        F2 < 1 | F2 > 18
            GOTO      SETF5180
          ENDIF
.
          GOTO      SETF5200 
.
SETF5180  MOVE      "Primary Reason for Discharge Delay code must be between 1-18",ERRDESC
          CALL      WRTEMP
.
SETF5200  MOVE      "z1",DIM2
          PACK      KEY5,DIM2,PTARCOD2
          CALL      RDCODE1
          IF        OVRCD = 1
            GOTO      SETF5280
          ENDIF
.
          MATCH     "I",PTCOACTV            * Inactive?
          GOTO      SETF5280 IF EQUAL
.
          UNPACK    THCSCOD,XMDCSRSN
          STRIP     XMDCSRSN
          MOVE      ZERO,F2
          MOVE      XMDCSRSN,F2
          IF        F2 < 1 | F2 > 18
            GOTO      SETF5280
          ENDIF
.
          GOTO      SETF5500
.
SETF5280  MOVE      "Secondary Reason for Discharge Delay code must be between 1-18",ERRDESC
          CALL      WRTEMP
.
SETF5500  MOVE      "00000",XVENHR            * CAR 265260
          PACK      KEY8,AADMNO
          CALL      RDPTICU1
          IF        OVRCD = 0
            COMPARE   ZERO,PTICUMEC
            IF        !@EQUAL
              MOVE      PTICUMEC,XVENHR       * Ventilator in hours    *C-250267
              REP       " 0",XVENHR
            ENDIF
          ENDIF
.
.         Re-admission Status                    **********
          PACK      XREADM,SP20
          MOVE      SP10,DIM3
.
          PACK      KEY5,ANSK,ANSO,PMVXUREA
          CALL      RDCODE1                 * Cat."KO"
          IF        OVRCD<>1
            UNPACK    THCSCOD,XREADM           * use Indicator 1
          ENDIF
          REP       "Y1N2",XREADM
.
.
          CALL      UPLN0000                  * Unplanned return
.
          CALL      EPSD0000                  * Episode care
.
          PACK      ACTIVITY,SP10
          PACK      KEY5,CODEPO,APLOCCR
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      ACTIVITY,THCSCOD        * Episode of care
          ENDIF
.
          CALL      CLOS0000                  * loop through trans file
.
          MATCH     "70",XSEPTP
          IF        @EQUAL
            MATCH     "0000",XLEVDY
            IF        @EQUAL
              CLEAR     ERRDESC
              APPEND    "Separation mode discharge from leave & ",ERRDESC
              APPEND    "leave days = 0",ERRDESC
              RESET     ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
.
SETF9999  RETURN
+
.******************************************************************************
.*                                 CNDIS000               Called by: SETF0000 *
.*                        Check for NDIS flag                                 *
.******************************************************************************
CNDIS000  PACK       KEY19,AURNO,SP70
          CALL       RSPMCCD1
CNDIS100  CALL       RKPMCCD1
          BRANCH     OVRCD,CNDIS999
.
          MATCH      AURNO,PMCDURNO
          GOTO       CNDIS999 IF NOT EQUAL
.
          PACK       KEY5,LOWCT,PMCDCTYP,SP70
          CALL       RDCODE1
          BRANCH     OVRCD,CNDIS100
.
          MATCH      "7",TCINDC1
          IF         @EQUAL
            PACK       XNDISFL,ZERO,ONE
            PACK       XNDISCN,PMCDCNUM,SP70
          ENDIF
.
CNDIS999  RETURN
+
.******************************************************************************
.*                                 VHCD0000               Called by: SETF0000 *
.*                        Validating hospital file no / establishment         *
.******************************************************************************
VHCD0000  COMPARE   ONE,IBCNMHOS
          IF        @EQUAL
            UNPACK    PTHSHSID,DIM4
          ELSE
            UNPACK    CFILENO,DIM4
          ENDIF
.
          MOVE      DIM4,XHCODE
          RESET     DIM4
VHCD1000  CMATCH    SP1,DIM4
          GOTO      VHCD9000 IF EQUAL
          MOVE      SP1,DIM1
          MOVE      DIM4,DIM1
          TYPE      DIM1
          GOTO      VHCD9000 IF NOT EQUAL
          BUMP      DIM4,1
          GOTO      VHCD1000 IF NOT EOS
          GOTO      VHCD9999
.
VHCD9000  MOVE      "Hospital code must be a 4 character number ",ERRDESC
          MOVE      SP10,SKEY8
          CALL      WRTEMP
VHCD9999  RETURN
+
.******************************************************************************
.*                                 INDG0000               Called by: SETF0000 *
.*                        Get the indigenous status                           *
.******************************************************************************
INDG0000  PACK      XABORN,SP10                  * Aboriginal
.
          MATCH     SP3,PMPXABRG
          IF        !@EQUAL
            PACK      KEY5,CODEVA,PMPXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,XABORN           * Aboriginal
            ENDIF
          ENDIF
.
          MATCH     SP1,XABORN
          IF        @EQUAL
            MOVE      "Indigenous Status is blank ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
INDG9999  RETURN
+
.******************************************************************************
.*                                 EMPL0000               Called by: SETF0000 *
.*                        Get employment status/Occupation                    *
.******************************************************************************
EMPL0000  MOVE      SP10,XOCCPC                  * occupation not required
.
          READ      CONTROLF,EIGHTY8;*237,PTCNEMPL
          MOVE      SP10,ACODE
          MOVE      ZERO,FORM1
          UNPACK    SP2,DIM1,DIM1A
          UNPACK    PTCNEMPL,DIM1,DIM1A
          MOVE      DIM1A,FORM1
          MATCH     ANSU,DIM1
          IF        @EQUAL
            LOAD      ACODE,FORM1,AUSR1,AUSR2,AUSR3,AUSR4,AUSR5,AUSR6,AUSR7
          ENDIF
.
          MATCH     ANSP,DIM1
          IF        @EQUAL
            LOAD      ACODE,FORM1,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5,PUSR6
          ENDIF
.
          PACK      KEY5,PTCNEMPL,ACODE,SP70
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,DIM1,DIM2        * Employment status
            PACK      XEMPST,DIM1,SP2
          ENDIF
.
          MATCH     SP3,PMVXEMPL
          IF        !@EQUAL
            PACK      KEY5,ANSK,ANSD,PMVXEMPL,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,DIM1,DIM2        * Employment status
              PACK      XEMPST,DIM1,SP2
            ENDIF
          ENDIF
.
EMPL9999  RETURN
+
.******************************************************************************
.*                                 CLST0000               Called by: SETF0000 *
.*                        Client status                                       *
.******************************************************************************
CLST0000  PACK      XCLNST,SP10
          MOVE      SP1,DIM1
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,DIM2,DIM4
            PACK      XCLNST,DIM2,SP1         * client status
          ENDIF
.
          MATCH     SP2,XCLNST
          IF        !@EQUAL
            SQUEEZE   XCLNST
            MOVE      XCLNST,FORM2
            IF        FORM2 = 0
              GOTO      CLST9999
            ENDIF
            IF        FORM2 > 0 & FORM1 < 11
              GOTO      CLST9999
            ENDIF
          ENDIF
.
CLST9000  MOVE        SP1,ANS
          MOVE        "Patient Type must  be b/w 0 & 11 ",ERRDESC  
          CALL        WRTEMP
.
CLST9999  PACK        XCLNST,XCLNST,SP70
          RETURN
+
.******************************************************************************
.*                                 ILOS0000               Called by: SETF0000 *
.*                        Intended LOS                                        *
.******************************************************************************
ILOS0000  PACK      XINLOS,SP10                  * Intended LOS
          MOVE      SP1,TCINDC1
.
          MATCH     SP3,PMVXIDUS
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSI,PMVXIDUS,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,XINLOS           * Intended LOS
            ENDIF
          ENDIF
.
...       MATCH     "1",XINLOS
...       IF        @EQUAL
...         MOVE      "2",XINLOS
...         GOTO      ILOS9999
...       ENDIF
.
...       MATCH     "2",XINLOS
...       IF        @EQUAL
...         MOVE      "1",XINLOS
...         GOTO      ILOS9999
...       ENDIF
.
          MATCH     SP1,XINLOS
          IF        @EQUAL
            MOVE      "Intended LOS is blank ",ERRDESC
            CALL      WRTEMP
          ELSE
            MOVE      ZERO,FORM1
            MOVE      XINLOS,FORM1
            IF        FORM1 <> 1 & FORM1 <> 2
              MOVE      "Intended LOS must be 1 or 2 ",ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
ILOS9999  RETURN
+
.******************************************************************************
.*                                 ADMF0000               Called by: SETF0000 *
.*                        Admitting from/transfer to                          *
.******************************************************************************
ADMF0000  MOVE      SP10,XCTRES              * contracting
          MOVE      SP10,XCTHOS              * admitting from
          MOVE      SP10,XDSCTO              * transfer to
.
          IF        PTCNUADT = 1
            PACK      KEY8,AADMNO
            CALL      RDPTDAD1
            BRANCH    OVRCD,ADMF5000 
.
            PACK      KEY5,PTDAADTS,SP70
            CALL      RDPTVAD1
            IF        OVRCD=1
              MOVE      PTDAADTS,XCTHOS
            ELSE
              MATCH     SP70,PTVANHSC
              IF        !@EQUAL
                MOVE      PTVANHSC,XCTHOS      * Valid transfer source
              ELSE
                MOVE      PTVACODE,XCTHOS      * Valid transfer source
              ENDIF
            ENDIF
            MATCH     "1",PTCNWAUC
            IF        @EQUAL
              MATCH     "5 ",XCLNST            * check if client status = 5
              IF        @EQUAL
                PACK      KEY25,AADMNO,ANSA,SP30
                CALL      RDPTASF1
                IF        OVRCD = 0
                  PACK      XCTRES,PTAFCSID    * contracting
                ENDIF
              ENDIF
              MATCH     "0 ",XCLNST            * check if client status = 0
              IF        @EQUAL
                PACK      KEY25,AADMNO,ANSA,SP30
                CALL      RDPTASF1
                IF        OVRCD = 0
                  PACK      XCTRES,PTAFCSID    * contracting
                ENDIF
              ENDIF
              MATCH     "9 ",XCLNST            * check if client status = 9
              IF        @EQUAL
                PACK      KEY25,AADMNO,ANSA,SP30
                CALL      RDPTASF1
                IF        OVRCD = 0
                  PACK      XCTRES,PTAFCSID    * contracting
                ENDIF
              ENDIF
              MATCH     "10",XCLNST            * check if client status = 10
              IF        @EQUAL
                PACK      KEY25,AADMNO,ANSA,SP30
                CALL      RDPTASF1
                IF        OVRCD = 0
                  PACK      XCTRES,PTAFCSID    * contracting
                ENDIF
              ENDIF
            ENDIF
            PACK      KEY5,PTDADCTS,SP70
            CALL      RDPTVAD1
            IF        OVRCD=1
              MOVE      PTDADCTS,XDSCTO
            ELSE
              MATCH     SP70,PTVANHSC
              IF        !@EQUAL
                MOVE      PTVANHSC,XDSCTO      * Valid transfer source
              ELSE
                MOVE      PTVACODE,XDSCTO      * Valid transfer source
              ENDIF
            ENDIF
.
ADMF5000    MATCH     SP4,XCTHOS
            IF        @EQUAL
              MOVE      "0900",XCTHOS
            ENDIF
.
            PACK      KEY5,ANSS,SP1,ASRCE,SP70   * admission source
            CALL      RDCODE1
            BRANCH    OVRCD,ADMF6000
.
            MATCH     "0407",THCSCOD             * care type change ?
            IF        @EQUAL
              MOVE      "0944",XCTHOS            * Reclassified This Hospital
            ENDIF
.
ADMF6000    MATCH     SP4,XDSCTO
            IF        @EQUAL
              PACK      KEY8,AADMNO
              CALL      RDDSCH1
.                                               * I CAR 37921
              PACK      KEY5,SP10
....          IF        PTDTWHDP = 1         * this is not in V9 - use PTNSWHDP
              IF        PTNSWHDP = 0
                MATCH     SP3,DDEST
                IF        !@EQUAL
                  PACK      KEY5,ANSD,ANSD,DDEST
                ENDIF
              ELSE
                MATCH     SP3,DSTAT
                IF        !@EQUAL
                  PACK      KEY5,ANSD,SP1,DSTAT
                ENDIF
              ENDIF
              CALL      RDCODE1
              MATCH     SP4,THCSCOD
              IF        @EQUAL
                MOVE      "0900",XDSCTO
              ELSE
                MOVE      THCSCOD,XDSCTO
              ENDIF
            ENDIF
.
          ENDIF
.
          MATCH     "0944",XCTHOS           * Reclassified This Hospital
          IF        @EQUAL
            CALL      EOCL0000
          ENDIF
.
          GOTO      ADMF9999 
.
ADMF9000  MOVE      "Admitted from is blank ",ERRDESC
          CALL      WRTEMP
.
ADMF9999  RETURN
+
.******************************************************************************
.*                                 EOCL0000               Called by: ADMF0000 *
.*                        Get episode of care link if statistical admission   *
.******************************************************************************
EOCL0000  PACK      KEY24,AURNO,ADATE,AADMNO,SP70
          CALL      RDDSCH3
          CALL      RDPDSCH3
          BRANCH    OVRCD,EOCL9000
.
          MATCH     AURNO,DURNO
          GOTO      EOCL9000 IF NOT EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      EOCL9000 IF NOT EQUAL
.
          SQUEEZE   DDADMNO
          MOVE      DDADMNO,XEOCLNK
.
EOCL9000  PACK      KEY8,AADMNO,SP70        * Read original admission
          CALL      RDDSCH1
.
EOCL9999  RETURN
+
.******************************************************************************
.*                                 ADST0000               Called by: SETF0000 *
.*                        Admission status                                    *
.******************************************************************************
ADST0000  PACK      XADTYP,SP10
          MOVE      SP3,DIM3
          LOAD      KEY2,PTCNELPS,CODEU1,CODEU2,CODEU3,CODEU4,CODEU5:
                                  CODEU6,CODEU7,CODEU8,CODEU9,CODEV1:
                                  CODEV2,CODECC,CODEP,CODEVB
.
          LOAD      KEY3,PTCNELPS,AUSR1,AUSR2,AUSR3,AUSR4,AUSR5:
                                  AUSR6,AUSR7,DIM3,DIM3,DIM3:
                                  DIM3,ACARE,ACLSS,PMVXCADM
          MATCH     SP3,KEY3
          IF        !@EQUAL
            PACK      KEY5,KEY2,KEY3
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,XADTYP             * admission status
            ENDIF
          ENDIF
.
          MOVE      ZERO,FORM1
          MOVE      XADTYP,FORM1
          IF        FORM1 < 3 | FORM1 > 7
            MOVE      "Admission Status must be 3,4,6 or 7",ERRDESC
            CALL      WRTEMP
          ENDIF
.
ADST9999  RETURN
+
.******************************************************************************
.*                                 WGHT0000               Called by: SETF0000 *
.*                        birth weight                                        *
.******************************************************************************
WGHT0000  MOVE      SP10,XWEIGH
          MOVE      SP10,DIM4
          MOVE      ZERO,FORM4
          MOVE      ZERO,FORM6
          MOVE      ABWGHT,FORM6
.
WGHT1000  PACK      AGEDATE,ADATE
          CALL      CALCAGE                 * Calculate age
.
          COMPARE   PSAGEDAY,PTCNBAGE
          GOTO      WGHT9999 IF LESS
.
WGHT2000  COMPARE   ZERO,FORM6
          GOTO      WGHT3000 IF EQUAL
.
          IF        FORM6<10000
            MOVE      FORM6,FORM4
            MOVE      FORM4,DIM4
            REP       " 0",DIM4
            PACK      XWEIGH,DIM4,SP10
          ELSE
            MOVE      "9999",XWEIGH
          ENDIF
          GOTO      WGHT9999

WGHT3000  IF        PSAGEDAY < PTCNBAGE
            MOVE      "Baby must have weight ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
WGHT9999  RETURN
+
.******************************************************************************
.*                                 PYCL0000               Called by: SETF0000 *
.*                        get payment classification                          *
.******************************************************************************
PYCL0000  PACK      XPAYCL,SP10
          PACK      KEY5,CODECL,ACLAIM
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,XPAYCL             * payment class.
          ENDIF
.
          MATCH     "21",XPAYCL
          GOTO      PYCL1000 IF NOT EQUAL
.
          PACK      KEY5,CODECX,PMVXFSRC
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,XPAYCL
          ENDIF
.
PYCL1000  MATCH     SP2,XPAYCL                   * I CAR 37921
          IF        !@EQUAL
            STRIP     XPAYCL
            MOVE      XPAYCL,XF2 
            SETLPTR   XPAYCL
            IF        XF2=21|XF2=22|XF2=23|XF2=24|XF2=25|XF2=26|XF2=27|XF2=28|XF2=29|XF2=30|XF2=31|XF2=32|XF2=33|XF2=34
              GOTO      PYCL9999
            ENDIF
          ENDIF
.
          MOVE      "Payment classification must be b/w 21 to 34 ",ERRDESC
          CALL      WRTEMP
.                                                * end I CAR 37921
PYCL9999  RETURN
+
.******************************************************************************
.*                                 VETR0000               Called by: SETF0000 *
.*                        get Veteran card                                    *
.******************************************************************************
VETR0000  PACK      XVETRN,SP10                  * Veteran
          MOVE      SP10,ACODE
          MOVE      ZERO,FORM1
          PACK      XDVAFN,SP70        * claim number
          PACK      XDVAAN,SP70        * authorisation number
          PACK      XDVAAD,SP70        * authorisation date
.
          MATCH     "27",XPAYCL                * DVA
          GOTO      VETR9999 IF NOT EQUAL
.
          PACK      KEY19,AURNO,Z70
          CALL      RSPMCCD1                     * position on U/R
VETR0050  CALL      RPPMCCD1                     * read previous record
          BRANCH    OVRCD,VETR9000               * eof - nothing found
.
          MATCH     AURNO,PMCDURNO               * same U/R still ?
          GOTO      VETR9000 IF NOT EQUAL        * no - nothing found
.
.         Check if the card type is DVA or Pension (Cat ct, Indicator 1)
.
          PACK      KEY5,CATCT,PMCDCTYP
          CALL      RDCODE1                      * code on file ?
          BRANCH    OVRCD,VETR0050               * no - ignore record
.
          MATCH     "3",TCINDC1                  * indicator 1=3 ?
          GOTO      VETR0050 IF NOT EQUAL        * no  - ignore record
.
          MATCH     SP3,PMCDDVAC
          IF        !@EQUAL 
            PACK      KEY5,ANSD,ANSX,PMCDDVAC,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              PACK      XVETRN,THCSCOD
              REP       "G1",XVETRN
              REP       "W2",XVETRN
            ENDIF
          ENDIF
.
          PACK      XDVAFN,PMCDCNUM    * claim number
          PACK      KEY11,AADMNO,ACLAIM,SP70
          CALL      RDPMEXT1
          IF        OVRCD<>1
            PACK      XDVAAN,PMEXAUTH    * authorisation number
            UNPACK    PMEXAUDA,CCENT,CYEAR,CMON,CDAY    * authorisation date
            PACK      XDVAAD,CDAY,CMON,CCENT,CYEAR * authorisation date
            REP       " 0",XDVAAD
            MATCH     "00000000",XDVAAD
            IF        @EQUAL
              MOVE      SP8,XDVAAD
            ENDIF
          ENDIF
.
          MATCH     "27",XPAYCL
          IF        @EQUAL
            MATCH     SP1,XVETRN
            IF        @EQUAL
            MOVE      "Payment Class is 27,VA Colour must not be blank",ERRDESC
              CALL      WRTEMP
            ENDIF
          ELSE
            GOTO      VETR9999     * not DVA patient
          ENDIF
.         
          GOTO      VETR9999
.
VETR9000  MOVE      "No Veterans Affairs Details Found",ERRDESC
          CALL      WRTEMP
VETR9999  RETURN
+
.******************************************************************************
.*                                 INST0000               Called by: SETF0000 *
.*                        Insurance status                                    *
.******************************************************************************
INST0000  MOVE      "2",XINSRN                * not insured
          MATCH     SP6,AFUNDH
          IF        !@EQUAL
            MOVE      "1",XINSRN              * insured
            MATCH     "23",XPAYCL
            IF        @EQUAL
              MOVE      "2",XINSRN              * insured
            ENDIF
          ENDIF
.                                             * C CAR 37921
          MATCH     "22",XPAYCL
          IF        @EQUAL
            MATCH     "1",XINSRN
            IF        !@EQUAL
              MOVE    "Payment Class.=22, Insurance status must be 1 ",ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
.                                             * end C CAR 37921
INST9999  RETURN
+
.******************************************************************************
.*                                 GEND0000               Called by: SETF0000 *
.*                                  Gender                                    *
.******************************************************************************
GEND0000  PACK      XGENDER,SP70                 * Gender
.
          MATCH     SP3,PMPXUCC4
          IF        !@EQUAL
            PACK      KEY5,CODEGi,PMPXUCC4,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,XGENDER          * Gender    
            ENDIF
          ENDIF 
.
          MOVE      ZERO,FORM1
          MOVE      SP1,DIM1
          UNPACK    XGENDER,DIM1
          MATCH     SP1,DIM1
          IF        !@EQUAL
            MOVE      DIM1,FORM1
            IF        FORM1=0 | FORM1=6 | FORM1=7 | FORM1=8
              GOTO      GEND2000
            ENDIF
            GOTO      GEND9999
          ENDIF
.
GEND2000  MOVE      "Gender code must be b/w 1-5 or 9 ",ERRDESC
          CALL      WRTEMP
.         
GEND9999  RETURN
+
.******************************************************************************
.*                                 UPLN0000               Called by: SETF0000 *
.*                        Unplanned return to theatre                         *
.******************************************************************************
UPLN0000  MOVE      SP10,DIM3
.>>>>     MOVE      "2",XURETN              * default to 2 (CAR 260777)
.
          MATCH     SP3,PMVXUVTH
          GOTO      UPLN9999 IF EQUAL
.
          PACK      KEY5,ANSK,ANSB,PMVXUVTH
          CALL      RDCODE1                 * Cat."KB"
          IF        OVRCD<>1
            UNPACK    THCSCOD,XURETN        * unplanned readmission alternate
          ENDIF
.
          MATCH     SP1,XURETN
          GOTO      UPLN9999 IF EQUAL
.
          MOVE      ZERO,FORM1
          MOVE      XURETN,FORM1
          IF        FORM1 <> 1 & FORM1 <> 2
            MOVE      "Unplanned return to theatre must be 1 or 2 ",ERRDESC
            CALL      WRTEMP
          ENDIF
UPLN9999  RETURN
+
.******************************************************************************
.*                                 EPSD0000               Called by: SETF0000 *
.*                   type episode of care                                     *
.******************************************************************************
EPSD0000  PACK      XEPSCR,SP10
          MOVE      ZERO,FORM2
          PACK      KEY5,ANSC,ANSC,ACARE
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      XEPSCR,THCSCOD          * Episode of care
          ENDIF
.
          MATCH     SP2,XEPSCR
          IF        @EQUAL
            MOVE      "Episode of care is blank ",ERRDESC
            CALL      WRTEMP
            GOTO      EPSD9999
          ELSE
            UNPACK    XEPSCR,DIM1,DIM1A
            MATCH     SP1,DIM1A
            IF        @EQUAL
              MOVE      DIM1,FORM2
            ELSE
              MOVE      XEPSCR,FORM2
            ENDIF
.
EPSD1000    IF        FORM2 > 20 & FORM2 < 34
              GOTO      EPSD5000
            ENDIF
          ENDIF
.
          MOVE        "Episode of care must be b/w 21 to 33 ",ERRDESC
          CALL      WRTEMP
.
          GOTO      EPSD9999
.
.
EPSD5000  MOVE      SP1,ANS                 * check Client Status combinations
.
          MATCH     "0",XCLNST
          IF        @EQUAL
            IF  FORM2=21 | FORM2=22 | FORM2=23 | FORM2=24 | FORM2=25 | FORM2=29 | FORM2=32 | FORM2=33
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "1",XCLNST
          IF        @EQUAL
            IF        FORM2=26
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "2",XCLNST
          IF        @EQUAL
            IF        FORM2=26
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "3",XCLNST
          IF        @EQUAL
            IF        FORM2=28
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "4",XCLNST
          IF        @EQUAL
            IF        FORM2=25
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "5",XCLNST
          IF        @EQUAL
            IF  FORM2=21 | FORM2=22 | FORM2=23 | FORM2=24 | FORM2=25 | FORM2=29 | FORM2=32 | FORM2=33
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "6",XCLNST
          IF        @EQUAL
            IF  FORM2=21 | FORM2=22 | FORM2=23 | FORM2=24 | FORM2=25 | FORM2=29 | FORM2=32 | FORM2=33
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "7",XCLNST
          IF        @EQUAL
            IF        FORM2=27
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "8",XCLNST
          IF        @EQUAL
            IF        FORM2=30 | FORM2=31
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "9",XCLNST
          GOTO      EPSD9999 IF EQUAL
.
          MATCH     "10",XCLNST
          GOTO      EPSD9999 IF EQUAL
.
EPSD6000  MOVE      "Patient TYpe/Care Type combination invalid ",ERRDESC 
          CALL      WRTEMP
.
EPSD9999  RETURN
+
.******************************************************************************
.*                                 ERRS0000               Called by: SETF0000 *
.*                        print error messages                                *
.******************************************************************************
ERRS0000  COMPARE   ZERO,RECNUM
          GOTO      ERRS9999 IF EQUAL
.
          MOVE      "60",CLNO
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000
.
          PACK      KEY8,SP10
          CALL      RSTEMP
ERRS1000  CALL      RKTEMP
          BRANCH    OVRCD,ERRS9000
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"|",*3,PURNO,*12,"|",*14,AADMNO:
                    *23,"|",*25,ERRDESC,*132,"|"
          ADD       ONE,CLNO
          GOTO      ERRS1000
.
ERRS9000  CALL      UND132
ERRS9999  RETURN
+
.******************************************************************************
.*                                 HEAD0000               Called by: SETF0000 *
.*                        Setup Heading                                       *
.******************************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD132
.
          CALL      UND132 
          PRINT     *1,"|U/R Number",*12,"|Adm Number":
                    *23,"| Description",*132,"|"
          CALL      UND132
          ADD       TWO,CLNO
HEAD9999  RETURN
+
.******************************************************************************
.*                                 SDSC0000               Called by: SETF0000 *
.*                        Setup discharged details                            *
.******************************************************************************
SDSC0000  PACK      KEY8,AADMNO,SP70
          CALL      RDDSCH1
          BRANCH    OVRCD,SDSC9000
.
          MOVE      DDATE,DISCHDAT
.                                               * I CAR 37921
          MATCH     PBDATE,DDATE
          IF        @LESS
            MOVE      "Disc.Date must be >= DOB",ERRDESC
            CALL      WRTEMP
          ELSE
            MATCH     ADATE,DDATE
            IF        @LESS
              MOVE    "Disc.Date must be >= Adm.Date",ERRDESC
              CALL      WRTEMP
            ELSE
              UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY        * Discharge date
              PACK      XDDATE,XDAY,XMON,XCENT,XYEAR
              REP       " 0",XDDATE
            ENDIF
          ENDIF
.
          UNPACK    DTIME,DIM2,ANS,DIM2A
          PACK      XDTIME,DIM2,DIM2A
          REP       " 0",XDTIME
          MOVE      ZERO,FORM4
          MOVE      XDTIME,FORM4
          IF        FORM4 >= 2400 
            MOVE      "Disc.Time is >= 2400 Hrs",ERRDESC
            CALL      WRTEMP
          ENDIF
.
          CALL      DRGV0000
.
          MATCH     SP3,PMVXMHLS                * Mental Health Legal Status
          IF        !@EQUAL
            PACK      KEY5,ANSL,ANSS,PMVXMHLS,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      THCSCOD,XMHLGL          * Legal status        *I-101974
            ENDIF
          ENDIF
.
          PACK      KEY5,SP10
          IF        PTNSWHDP = 1
            MATCH     SP3,DSTAT
            IF        !@EQUAL
              PACK      KEY5,ANSD,SP1,DSTAT
            ENDIF
          ELSE
            MATCH     SP3,DDEST
            IF        !@EQUAL
              PACK      KEY5,ANSD,ANSD,DDEST
            ENDIF
          ENDIF
.
          MATCH     JULY2024,DDATE
          IF        !@LESS
            GOTO      SDSC5000
          ENDIF
.
SDSC0400  CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    TCINDC5,XSEPTP           * Discharge to
          ENDIF
.
          MATCH     "1",XSEPTP
          IF        !@EQUAL
            MATCH     "2",XSEPTP
            GOTO      SDSC0500 IF NOT EQUAL    * use adm transfer source code?
          ENDIF
.
SDSC0420  MOVE      "9",XSEPTP                 * default (change:CAR 260777)
          PACK      KEY8,AADMNO,SP70
          CALL      RDPTDAD1
          BRANCH    OVRCD,SDSC0500
.
          MATCH     SP70,PTDADCTS
          GOTO      SDSC0500 IF EQUAL
.
          PACK      KEY5,PTDADCTS,SP70
          CALL      RDPTVAD1
          BRANCH    OVRCD,SDSC0500
.
          MATCH     "N",PTVADTYP
          IF        @EQUAL
            MOVE      "2",XSEPTP
          ENDIF
          MATCH     "L",PTVADTYP
          IF        @EQUAL
            MOVE      "4",XSEPTP
          ENDIF
          MATCH     "H",PTVADTYP
          IF        @EQUAL
            MOVE      "1",XSEPTP
          ENDIF
          MATCH     "P",PTVADTYP
          IF        @EQUAL
            MOVE      "3",XSEPTP
          ENDIF
          MATCH     "R",PTVADTYP
          IF        @EQUAL
            MOVE      "9",XSEPTP
          ENDIF
.
SDSC0500  UNPACK    SP2,DIM1,DIM1A
          MOVE      ZERO,FORM1
          UNPACK    XSEPTP,DIM1
          MATCH     SP1,DIM1
          IF        !@EQUAL
            MOVE      DIM1,FORM1
            COMPARE   ZERO,FORM1
            GOTO      SDSC9999 IF NOT EQUAL
          ENDIF
          MOVE      "Mode of separation must be b/w 1 to 9 ",ERRDESC
          CALL      WRTEMP
          GOTO      SDSC9999
.
SDSC5000  CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    PTCDUDF3,XSEPTP
          ENDIF
.
          MATCH     "10",XSEPTP
          GOTO      SDSC6000 IF NOT EQUAL
.
SDSC5200  MOVE      "90",XSEPTP
          PACK      KEY8,AADMNO,SP70
          CALL      RDPTDAD1
          BRANCH    OVRCD,SDSC6000
.
          MATCH     SP70,PTDADCTS
          GOTO      SDSC6000 IF EQUAL
.
          PACK      KEY5,PTDADCTS,SP70
          CALL      RDPTVAD1
          BRANCH    OVRCD,SDSC6000
.
          MATCH     "L",PTVADTYP
          IF        @EQUAL
            MOVE      "40",XSEPTP
          ENDIF
          MATCH     "H",PTVADTYP
          IF        @EQUAL
            MOVE      "10",XSEPTP
          ENDIF
          MATCH     "P",PTVADTYP
          IF        @EQUAL
            MOVE      "30",XSEPTP
          ENDIF
          MATCH     "R",PTVADTYP
          IF        @EQUAL
            MOVE      "90",XSEPTP
          ENDIF
.
SDSC6000  MOVE      ZERO,F2
          MOVE      XSEPTP,F2
.
          IF        F2 = ZERO
            GOTO      SDSC7000
          ENDIF
.
          IF        F2 < 10 | F2 > 90
            GOTO      SDSC7000
          ENDIF
.
          GOTO      SDSC9999       
.
SDSC7000  MOVE      "Mode of Separation must be b/w 10 to 90",ERRDESC
          CALL      WRTEMP
          GOTO      SDSC9999  
.
SDSC9000  MOVE      "Missing Discharged record",ERRDESC
          CALL      WRTEMP
.
SDSC9999  RETURN
+
.******************************************************************************
.*                                DRGV0000                Called by: PROC0000 *
.*      Get the drg from patpgraf based on the discharge date grouper version *
.******************************************************************************
.
DRGV0000  MATCH     PTCNE110,DISCHDAT       * disc.date > DRG 11.0 effect.date?
          IF        !@LESS
            MOVE      DRG110,DRGVERS        * Use 11.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNE100,DISCHDAT       * disc.date > DRG 10.0 effect.date?
          IF        !@LESS
            MOVE      DRG100,DRGVERS        * Use 10.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED90,DISCHDAT       * disc.date > DRG 9.0 effect.date?
          IF        !@LESS
            MOVE      DRG090,DRGVERS        * Use 9.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED80,DISCHDAT       * disc.date > DRG 8.0 effect.date?
          IF        !@LESS
            MOVE      DRG080,DRGVERS        * Use 8.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED70,DISCHDAT       * disc.date > DRG 7.0 effect.date?
          IF        !@LESS
            MOVE      DRG070,DRGVERS        * Use 7.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED62,DISCHDAT       * disc.date > DRG 6.2 effect.date?
          IF        !@LESS
            MOVE      DRG062,DRGVERS        * Using 6.2 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED60,DISCHDAT       * disc.date > DRG 6.0 effect.date?
          IF        !@LESS
            MOVE      DRG060,DRGVERS        * Using 6.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED52,DISCHDAT       * disc.date > DRG 5.2 effect.date?
          IF        !@LESS
            MOVE      DRG052,DRGVERS        * Using 5.2 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED51,DISCHDAT       * disc.date > DRG 5.1 effect.date?
          IF        !@LESS
            MOVE      DRG051,DRGVERS        * Using 5.1 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED50,DISCHDAT       * disc.date > DRG 5.0 effect.date?
          IF        !@LESS
            MOVE      DRG050,DRGVERS        * Using 5.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED42,DISCHDAT       * disc.date > DRG 4.2 effect.date?
          IF        !@LESS
            MOVE      DRG042,DRGVERS        * Using 4.2 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED41,DISCHDAT       * disc.date > DRG 4.1 effect.date?
          IF        !@LESS
            MOVE      DRG041,DRGVERS        * Using 4.1 DRG version
            GOTO      DRGV1000
          ENDIF
.
          GOTO      DRGV9000
.
DRGV1000  PACK      KEY13,AADMNO,SP1,ONE,DRGVERS
          CALL      RDPTPGR1                * DRG found for grouper vers ?
          BRANCH    OVRCD,DRGV9000          * no - finished
.
          MATCH     SP4,PTPGNDRG            * blank DRG ?
          GOTO      DRGV9000 IF EQUAL       * yes - finished
.
          MOVE      PTPGNDRG,XDIAGR         * load DRG
.
          UNPACK    DRGVERS,DIM1,D1,ANS     * break up into 0,5,2
          MATCH     "1",DIM1
          IF        @EQUAL
            PACK      XGRPVR,DIM1,D1,DOT,ANS,SP10  * set DRG Version as "10.0"
          ELSE
            PACK      XGRPVR,D1,DOT,ANS,SP10       * set DRG Version as "5.2"
          ENDIF
.
          PACK      KEY7,PTPGNDRG,DRGVERS
          CALL      RDPTDGW1
          BRANCH    OVRCD,DRGV9000
.
          MOVE      PTDWMDCC,XDIACG         * load MDC
.
DRGV9000  MOVE      ZERO,EXIT
.
DRGV9999  RETURN
+
.******************************************************************************
.*                                 GSNM0000               Called by: EXTC0000 *
.*                        Get the second name                                 *
.******************************************************************************
GSNM0000  PACK      XPGNAME,PGNAME,SP20,SP20  * first forename 
          RESET     XPGNAME
GSNM1000  BUMP      XPGNAME
          GOTO      GSNM2000 IF EOS
          MATCH     SP1,XPGNAME
          GOTO      GSNM1000 IF NOT EQUAL
          APPEND    SP20,XPGNAME
          APPEND    SP20,XPGNAME
          RESET     XPGNAME
.
GSNM2000  PACK      XSFNAME,SP20,SP20       * second name
          RESET     PGNAME
GSNM3000  BUMP      PGNAME
          GOTO      GSNM9999 IF EOS
          MATCH     SP1,PGNAME
          GOTO      GSNM3000 IF NOT EQUAL
          BUMP      PGNAME
          PACK      XSFNAME,PGNAME,SP20,SP20
GSNM9999  RETURN
+
.****************************************************************************** .*                                 ECDA0000               Called by: EXTC0000 * .*                        Generate Diseases detail                            *
.******************************************************************************
ECDA0000  MOVE      ZERO,COUNT
          MOVE      ZERO,COUNTA
          MOVE      ZERO,COUNTE
          MOVE      ZERO,COUNTP
.
          MATCH     SP70,ACODDTE          * don't send if coding not complete
          GOTO      ECDA9999 IF EQUAL
.
....      MATCH     STRTDATE,ACODDTE
....      GOTO      ECDA9999 IF LESS
.
          MATCH     ACODDTE,ENDDATE
          GOTO      ECDA9999 IF LESS
.
          MOVE      ZERO,DAAFLAG
          MOVE      ZERO,DDAFLAG
          CALL      CDTP0000               * Check first three Diaseas code
.
          PACK      KEY13,AADMNO,SP20                                 (C-240107
          CALL      RSPTECD1                * Position on & read a patient
ECDA1000  CALL      RKPTECD1                  episode disease file record
          BRANCH    OVRCD,ECDA9999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      ECDA9999 IF NOT EQUAL
.
          MOVE      ZERO,CMPCNT             * Complex map counter - no
.
ECDA3000  MATCH     PTCNI10D,DDATE
          IF        @LESS
            PACK      KEY9,PTEDCODE
            CALL      RD10T9D1              * Read an ICD9 disease file record
            BRANCH    OVRCD,ECDA1000
.
            MOVE      DICD10CD,PTEDCODE     * Mapped ICD10 disease code
          ENDIF
.
          PACK      KEY9,PTEDCODE
          MOVE      DDATE,ICDRDDTE       * send Coding Date
          CALL      RDPTICD1                * read ICD10 Vn - selected by routin
          BRANCH    OVRCD,ECDA1000
.
ECDA4000  MOVE      PTEDCODE,XICDCOD
          MOVE      PTEDOSET,XCOFICD
          PACK      XDCCRFL,PTEDCCFL,SP70
          CALL      WRDIS000                * write a disease record
          GOTO      ECDA1000
.
ECDA5000  COMPARE   ZERO,CMPCNT
          GOTO      ECDA1000 IF EQUAL       * Not using complex mapping
.
          ADD       ONE,CMPCNT
          COMPARE   FOUR,CMPCNT
          GOTO      ECDA1000 IF NOT LESS    * Complex map counter >=4
.
          MOVE      SP9,PTEDCODE
          LOAD      PTEDCODE,CMPCNT,PTCP1MCD,PTCP2MCD,PTCP3MCD
          MATCH     SP9,PTEDCODE
          GOTO      ECDA1000 IF EQUAL       * Blank code
          GOTO      ECDA2000
.
ECDA9999  RETURN
+
.*******************************************************************************
.*         RMD000 - Remove Dot, place of occurrence and activity fields        *
.*******************************************************************************
.
RMD000    REP       "E ",KEY9
          RESET     KEY9
          CLEAR     KEY7
.
RMD100    CMATCH    SP1,KEY9
          GOTO      RMD200 IF EQUAL
.
          CMATCH    DOT,KEY9
          GOTO      RMD200 IF EQUAL
.
          CMATCH    "-",KEY9
          GOTO      RMD200 IF EQUAL
.
          CMATCH    SLASH,KEY9              * Ignore slashes
          GOTO      RMD200 IF EQUAL
          MOVE      KEY9,ANS
          APPEND    ANS,KEY7
.
RMD200    BUMP      KEY9
          GOTO      RMD100 IF NOT EOS
.
RMD300    RESET     KEY9
          APPEND    SP10,KEY7
          RESET     KEY7
.
RMD999  RETURN
+
.******************************************************************************
.*                                 ECOA0000               Called by: EXTC0000 *
.*                        Generate operations detail                          *
.******************************************************************************
ECOA0000  MOVE      ZERO,COUNT
          MATCH     SP70,ACODDTE          * don't send if coding not complete
          GOTO      ECOA9999 IF EQUAL
.
....      MATCH     STRTDATE,ACODDTE
....      GOTO      ECOA9999 IF LESS
.
          MATCH     ACODDTE,ENDDATE
          GOTO      ECOA9999 IF LESS
.
          PACK      KEY13,AADMNO,SP2                                  *C-240107
          CALL      RSPTECO1                * Position on & read a patient
ECOA1000  CALL      RKPTECO1                  episode operation file record
          BRANCH    OVRCD,ECOA9999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      ECOA9999 IF NOT EQUAL
.
          MATCH     CMOPRT,PTEOTYPE                                    *I-48888
          GOTO      ECOA1000 IF NOT EQUAL   * Ignore "X" "I" & "N"     *I-48888
.
          MATCH     PTCNEDRG,PTEOCODE
          GOTO      ECOA1000 IF EQUAL       * Exclude dummy code
.
          MOVE      ZERO,CMPCNT             * Complex map counter - no
          PACK      KEY9,PTEOCODE
          MATCH     "19990701",DDATE
          GOTO      ECOA3000 IF NOT LESS    * do coding in ICD10-M
.
.         Sending codes in ICD9
.
          MATCH     PTCNI10D,PTEODTCD
          IF        !@LESS
            MOVE      PTEOCODE,PTCPCOD
            CALL      PATM10T9              * Map ICD10 to ICD9 codes
            MOVE      PTCP1MCD,PTEOCODE
            MOVE      ONE,CMPCNT            * Complex map counter - yes
          ENDIF
.
ECOA2000  PACK      KEY9,PTEOCODE
          CALL      RD10T9O1                * Read an ICD9 operation file record
          BRANCH    OVRCD,ECOA5000
          GOTO      ECOA4000
.
.         Sending codes in ICD10
.
ECOA3000  MATCH     PTCNI10D,DDATE
          IF        @LESS
            PACK      KEY9,PTEOCODE
            CALL      RD10T9O1              * Read an ICD9 oper file record
            BRANCH    OVRCD,ECOA1000
.
            MOVE      OICD10CD,PTEOCODE     * Mapped ICD10 oper code
          ENDIF
.
          PACK      KEY9,PTEOCODE
....      CALL      RDPT10O1                * Read an ICD10 oper file record
          MOVE      DDATE,ICDRDDTE       * send Coding Date
          CALL      RDPTICO1                * read ICD10 Vn - selected by routin
          BRANCH    OVRCD,ECOA1000
.
ECOA4000  MOVE      PTEOCODE,XPROCCOD       
.
          MOVE      PTEOCODE,DIM9
          CALL      RMDT0000                 * Remove any dots
          PACK      DIM10,DIM9A,SP10
.
          ADD       ONE,COUNT
.
          MATCH     SP70,PTEODATE
          IF        @EQUAL
            MOVE      "Blank procedure date ",ERRDESC
            CALL      WRTEMP
          ELSE
            REP       " 0",PTEODATE
            UNPACK    PTEODATE,XCENT,XYEAR,XMON,XDAY
            PACK      PTEODATE,XDAY,XMON,XCENT,XYEAR
            MOVE      PTEODATE,XDATPROC
          ENDIF
.
          PACK      XPCCRFL,PTEOCCFL,SP70
.
          PACK      KEY6,TADOCT             * Attending Doc.
          CALL      RDDOCT1
          IF        OVRCD = 0
            MATCH     SP15,PTDOREGN                        * start    *C-233977
            IF        !@EQUAL
              MOVE      PTDOREGN,KEY15      * National Registration No.
            ELSE
              MOVE      PTDOWHDP,KEY15      * WA HDP Doctor code
            ENDIF
            SQUEEZE   KEY15
            PACK      XCLINPR,KEY15,SP15    * Aust Health Prac. Reg. No.
          ENDIF
.
          CALL      WRPROC                  * write an operation record
          GOTO      ECOA1000
.
ECOA5000  COMPARE   ZERO,CMPCNT
          GOTO      ECOA1000 IF EQUAL       * Not using complex mapping
.
          ADD       ONE,CMPCNT
          COMPARE   FOUR,CMPCNT
          GOTO      ECOA1000 IF NOT LESS    * Complex map counter >=4
.
          MOVE      SP9,PTEOCODE
          LOAD      PTEOCODE,CMPCNT,PTCP1MCD,PTCP2MCD,PTCP3MCD
          MATCH     SP9,PTEOCODE
          GOTO      ECOA1000 IF EQUAL       * Blank code
          GOTO      ECOA2000
.
ECOA9999  RETURN
+
.******************************************************************************
.*                                  RMDT0000              Called by: EXTC0000 *
.*          Move DIM9 to DIM9A (Left Justify), eliminating any dots           *
.******************************************************************************
RMDT0000  MOVE      DIM9,DIM9A
          RESET     DIM9A
          REP       ". - / ",DIM9A
          SQUEEZE   DIM9A
          PACK      DIM9A,DIM9A,SP9
.
RMDT9000  MOVE      ZERO,EXIT
RMDT9999  RETURN
+
.******************************************************************************
.*                                 WREC0000               Called by: EXTC0000 *
.*                        Write a record                                      *
.******************************************************************************
WREC0000  DISPLAY   *P40:24,*V2LON,DADMNO;
          ADD       ONE,RECNUM
          COMPARE   ONE,CODINGFL
          IF        @EQUAL
            MOVE      "SUM",XEVNTYP
            ADD       ONE,SUMCOUNT
            MOVE      ONE,CHKFLAG
          ELSE
            MOVE      "DIS",XEVNTYP            * removed for CAR 272377
            GOTO      WREC9999
          ENDIF
          COMPARE   ONE,UPDATEFL
          IF        @EQUAL
            MOVE      ANSU,XUPDFLG             * set update flag to "U"
          ENDIF
          MOVE      "I",XMORTYP
          MOVE      NUMLEV,XNUMLEV
          REP       " 0",XNUMLEV               * CAR 265260
          REP       "G1",XVETRN
          REP       "W2",XVETRN
.
          PACK      KEY13,AADMNO,SP20                                 *C-240107
          CALL      RSPTECD1                * Position on & read a patient
          CALL      RKPTECD1                  episode disease file record
          BRANCH    OVRCD,WREC1000
.
          MATCH     AADMNO,PTEDADMN
          GOTO      WREC1000 IF NOT EQUAL
.
          COMPARE   ONE,SCODFLG             * Check if to send coder id ?
          GOTO      WREC1000 IF NOT EQUAL   * no.
.
.         PACK      XCODID,PTEDOPER,SP20    * user name
          PACK      XCODID,PTEDUSID,SP20    * user name
.
WREC1000  MOVE      ZERO,EXIT 
.
          UNPACK    XSORLOC,D1,KEY1
          CMATCH    "0",D1
          IF        @EQUAL
            PACK      XSORLOC,KEY1,SP1      * CAR 265260
          ENDIF
          UNPACK    XSORCLI,D1,KEY1
          CMATCH    "0",D1
          IF        @EQUAL
            PACK      XSORCLI,KEY1,SP1      * CAR 265260
          ENDIF
.
          WRITE     WORK3,SEQ;XEVNTYP,XMORTYP,XEOCLNK,XDAYQNB,XNUMLEV:
                              XACCOCC,XLANG,XUPDFLG,XSORLOC,XSORCLI:
                              XSORTRA,XHIHCD,SP7,XHCODE,XADMNO:
                              XPURNO,XMUMSID,XADATE,XATIME,XDDATE:
                              XDTIME,XPSNAME,XPGNAME,XSFNAME,XPADD1,XSUBRB:
                              XPCODE,XSTATE,SP6,XDOBDT,XPSEX,XABORN,XCNTBR:
                              XMSTAT,XEMPST,XINTRP,XOCCPC,XSREFR,XINLOS,XCTHOS:
                              XLOCTN,XNMRNO,XSPLTY,XSPCDS,XADTYP,XWEIGH,XLEVDY:
                              XPSYCH,XMHLGL,XPAYCL,XVETRN,XINSRN,XICUDY,XVENHR:
                              XREADM,XURETN,XEPSCR,XCLNST,XCTRES,XSEPTP,XDSCTO:
                              XDSATT,XGRPVR,XDIACG,XDIAGR,XCODID,XDVAFN:
                              XDVAAN,XDVAAD,XHOMEPH,XCONTPH,XRESSTA,XICUHR:
                              XPMEDIC,XCONLDY,XNDISFL,XNDISCN,XGENDER:
                              XMDCLDTE,XMDCLTME,XMDCPRSN,XMDCSRSN 
.
          ADD       ONE,TOTCOUNT
.
WREC9999  RETURN
+
.******************************************************************************
.*                                 WRDIS000               Called by: EXTC0000 *
.*                        Write a disease code record                         *
.******************************************************************************
.
WRDIS000  MATCH     CMDISP,PTEDTYPE         * Primary Disease?
          IF        @EQUAL
            MOVE      "PD  ",XEVNTYP 
            ADD       ONE,COUNTD
            IF        COUNTD>1
              MOVE      "OC  ",XEVNTYP      * only one PD required (CAR 260777)
            ENDIF
            GOTO      WRDIS90
          ENDIF
.
WRDIS10   MATCH     "2",PT0DACRQ          * External cause code?
          IF        @EQUAL
            MOVE      "EC  ",XEVNTYP
            ADD       ONE,COUNTEX
            GOTO      WRDIS90
          ENDIF
.
          MATCH     "6",PT0DACRQ          * External cause code?
          IF        @EQUAL
            MOVE      "EC  ",XEVNTYP
            ADD       ONE,COUNTEX
            GOTO      WRDIS90
          ENDIF
.
          MATCH     "8",PT0DACRQ          * External cause code?
          IF        @EQUAL
            MOVE      "EC  ",XEVNTYP
            ADD       ONE,COUNTEX
            GOTO      WRDIS90
          ENDIF
.
          MATCH     "P",PT0DACRQ
          IF        @EQUAL
            MOVE      "PL  ",XEVNTYP
            ADD       ONE,COUNTEX
            GOTO      WRDIS90
          ENDIF
.
          MATCH     "A",PT0DACRQ
          IF        @EQUAL
            MOVE      "AC  ",XEVNTYP
            ADD       ONE,COUNTEX
            GOTO      WRDIS90
          ENDIF
.
          MATCH     "4",PT0DACRQ          * Morphology code?
          IF        @EQUAL
            MOVE      "MO  ",XEVNTYP
            ADD       ONE,COUNTM
            GOTO      WRDIS90
          ENDIF
.
          MATCH     CMDISC,PTEDTYPE         * Complication
          IF        @EQUAL
            MOVE      "CM  ",XEVNTYP
            ADD       ONE,COUNTD
            GOTO      WRDIS90
          ENDIF
.
          MATCH     CMDISA,PTEDTYPE         * Co diagnosis
          IF        @EQUAL
.           MATCH     SP70,PT0DADTP
.           IF        !@EQUAL
              MOVE      "OC  ",XEVNTYP         * associated
              ADD       ONE,COUNTD
              GOTO      WRDIS90
.           ENDIF
          ENDIF
.
.         MATCH     CMDISS,PTEDTYPE         * Sequela
.         IF        @EQUAL
.           MOVE      "OC  ",XEVNTYP 
.           ADD       ONE,COUNTD
.           GOTO      WRDIS90
.         ENDIF
.
          MOVE      "CA  ",XEVNTYP
          ADD       ONE,COUNTD
.
WRDIS90   PACK      XPRIOR,PTEDCNT        * priority  (CAR 260777)
.>>>>     PACK      XPRIOR,SP1,F2
          ADD       ONE,F2
.
WRDIS95   MOVE      ZERO,EXIT
          WRITE     WORK3,SEQ;XEVNTYP,XADMNO,XPURNO,SP18,XHCODE,XPRIOR:
                                XICDCOD,XPLCOCC,XACTIV,XCOFICD,XCOFPOO:
                                XCOFACT,XDCCRFL
          ADD       ONE,TOTCOUNT
          UNPACK    SP50,XICDCOD,XPLCOCC,XACTIV
          UNPACK    SP70,XCOFICD,XCOFPOO,XCOFACT
          UNPACK    SP70,XDCCRFL
.
WRDIS99   RETURN
+
.******************************************************************************
.         Check Asterisk/Dagger type
.******************************************************************************
CDTP0000  MOVE      ZERO,FORM6
.
.         Check if there is only three Episode disease codes
.
          PACK      KEY13,AADMNO,SP20                                 (C-240107
          CALL      RSPTECD1
CDTP1000  CALL      RKPTECD1                * read next Episode disease code
          BRANCH    OVRCD,CDTP2000
.
          MATCH     AADMNO,PTEDADMN
          GOTO      CDTP2000 IF NOT EQUAL
.
          CALL      VICD0000               * Validate ICD code
          BRANCH    OVRCD,CDTP1000
.
          ADD       ONE,FORM6
          GOTO      CDTP1000
.
CDTP2000  COMPARE   THREE,FORM6
          GOTO      CDTP9999 IF NOT EQUAL   * More than 3 episode diseases
.
          PACK      KEY13,AADMNO,SP20                                 (C-240107
          CALL      RSPTECD1
CDTP2100  CALL      RKPTECD1                * position read Episode disease code
          BRANCH    OVRCD,CDTP9999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      CDTP9999 IF NOT EQUAL
          CALL      VICD0000               * Validate ICD code
          BRANCH    OVRCD,CDTP2100
.
.         The first Disease code must be Dagger
.
          MATCH     "D",PT0DADTP
          GOTO      CDTP9999 IF NOT EQUAL
.
CDTP2200  CALL      RKPTECD1                * get second record
          BRANCH    OVRCD,CDTP9999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      CDTP9999 IF NOT EQUAL
          CALL      VICD0000               * Validate ICD code
          BRANCH    OVRCD,CDTP2200
.
          MATCH     "D",PT0DADTP
          GOTO      CDTP3000 IF NOT EQUAL   * third record is Not Dagger
.
.         Second record is Dagger, Check if third one is Asterisk
.
CDTP2300  CALL      RKPTECD1                * get third record
          BRANCH    OVRCD,CDTP9999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      CDTP9999 IF NOT EQUAL
          CALL      VICD0000               * Validate ICD code
          BRANCH    OVRCD,CDTP2300
.
          MATCH     "A",PT0DADTP
          GOTO      CDTP9999 IF NOT EQUAL   * Third is not Asterisk
.
          MOVE      ONE,DDAFLAG
          GOTO      CDTP9999
.
.         Check if Second is Asterisk
.
CDTP3000  MATCH     "A",PT0DADTP
          GOTO      CDTP9999 IF NOT EQUAL   * Second record is not an Asterisk
.
.         Second record is Asterisk, Check if third one is Asterisk
.
CDTP3200  CALL      RKPTECD1                * get third record
          BRANCH    OVRCD,CDTP9999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      CDTP9999 IF NOT EQUAL
          CALL      VICD0000               * Validate ICD code
          BRANCH    OVRCD,CDTP3200
.
          MATCH     "A",PT0DADTP
          GOTO      CDTP9999 IF NOT EQUAL   * Third is not Asterisk
          MOVE      ONE,DAAFLAG
.
CDTP9999  RETURN
+
.******************************************************************************
.         validate ICD record
.******************************************************************************
VICD0000  MATCH     PTCNI10D,DDATE
          IF        @LESS
            PACK      KEY9,PTEDCODE
            CALL      RD10T9D1              * Read an ICD9 disease file record
            BRANCH    OVRCD,VICD9999
.
            MOVE      DICD10CD,PTEDCODE     * Mapped ICD10 disease code
          ENDIF
.
          PACK      KEY9,PTEDCODE
          MOVE      DDATE,ICDRDDTE       * send Coding Date
          CALL      RDPTICD1                * read ICD10 Vn - selected by routin
.
VICD9999  RETURN
+
.******************************************************************************
.*                                 WRPROC                 Called by: EXTC0000 *
.*                        Write a procedure code record                       *
.******************************************************************************
.
WRPROC    IF        COUNTO=0
            MOVE      "PP",XEVNTYP        * principal procedure
          ELSE
            MOVE      "OP",XEVNTYP        * additional procedure
          ENDIF
. 
....      PACK      XPRIOR,SP1,PTEOCNT    * priority                  *D-240107
          PACK      XPRIOR,PTEOCNT        * priority                  *I-240107
          WRITE     WORK3,SEQ;XEVNTYP,XADMNO,XPURNO,SP18,XHCODE,XPRIOR:
                              XPROCCOD,XCLINPR,XDATPROC,SP10,XPCCRFL
          ADD       ONE,COUNTO
          ADD       ONE,TOTCOUNT
WRPROC99  RETURN
+
.******************************************************************************
.*                                 WRCHK                  Called by: EXTC0000 *
.*                        Write the check line (totals)                       *
.******************************************************************************
.
WRCHK     COMPARE   ONE,CHKFLAG
          GOTO      WRCHK99 IF NOT EQUAL    * only write for coded patients
.
          MOVE      "CHK",XEVNTYP
          MOVE      COUNTD,XDIAGCNT
          MOVE      COUNTEX,XEXCACNT
          MOVE      COUNTM,XMORPCNT
          MOVE      COUNTO,XPROCCNT
.
          WRITE     WORK3,SEQ;XEVNTYP,XADMNO,XPURNO,SP18,XHCODE,XDIAGCNT:
                              XEXCACNT,XMORPCNT,XPROCCNT
          ADD       ONE,TOTCOUNT
          MOVE      ZERO,CHKFLAG
WRCHK99   RETURN
+
.******************************************************************************
.*                                 CREA0000               Called by: EXTC0000 *
.*                        Create a text file                                  *
.******************************************************************************
CREA0000  CLOSE     WORK
          CLOSE     WORK3
.
          PACK      FNAME1,FILE1,PTHSHOSP,SP70
          PREP      WORK,FNAME1
          PACK      FNAME3,FILE3,PTHSHOSP,SP70
          PREP      WORK3,FNAME3
.
CREA9999  RETURN
.
+
.******************************************************************************
.*                                  CLOS0000              Called by: GDAT0000 *
.*                     Calculate The LOS For This Patient                     *
.******************************************************************************
CLOS0000  MOVE      ZERO,WORKHITH           * Initialise HITH care days
          MOVE      ZERO,WORKICLS           * Initialise ICU LOS
          MOVE      ZERO,WORKLDAY           * Initialise leave days
          MOVE      ZERO,WORKPSYC           * Initialise psychiatric days
          MOVE      ZERO,PSYCFLAG           * Initialise psych flag
          MOVE      ZERO,ADMPFLAG
          MOVE      ZERO,WORKQUAL           * Initialise qualified days
          MOVE      ACLSSFT,DEPTYPE
.
. ----- Daycase -----
.
          MATCH     ADATE,DDATE
          IF        @EQUAL
            PACK      KEY30,AADMNO,SP30
            CALL      RDSTRAN2              * Position on & read the transfer
            CALL      RDKTRAN2                file
.........   IF        OVRCD = 0 & AADMNO=TADMN
            BRANCH    OVRCD,CLOS0400          
            MATCH     AADMNO,TADMN
            GOTO      CLOS0400 IF NOT EQUAL
            CALL      GADM0000            * Get admitting details 
.
            MOVE      TRBTYP,BEDTYPE
            CALL      CPSY0000            * Check for psychiatric care
            IF        EXIT=1
              MOVE      ONE,WORKPSYC
              MOVE      ZERO,EXIT
            ENDIF
........    ENDIF
.
CLOS0400   PACK      KEY30,AADMNO,SP30
           CALL      RDSTRAN2              * Position on & read the transfer
CLOS0500   CALL      RDKTRAN2                file
.......    IF        OVRCD = 0 & AADMNO=TADMN
.
           BRANCH    OVRCD,CLOS0600
           MATCH     AADMNO,TADMN
           GOTO      CLOS0600 IF NOT EQUAL

           CMATCH    ANSR,TMOVE
           GOTO      CLOS0550 IF EQUAL
.
           CMATCH    ANSC,TMOVE
           GOTO      CLOS0550 IF EQUAL
.
           GOTO      CLOS0500
.
CLOS0550   MOVE      TRBTYP,BEDTYPE
           CALL      CPSY0000            * Check for psychiatric care
           IF        EXIT=1
             MOVE      ONE,WORKPSYC
             MOVE      ZERO,EXIT
           ENDIF
.
........   ENDIF
.
CLOS0600    PACK      KEY30,AADMNO,ZED30
            CALL      RDSTRAN2              * Position on & read the transfer
            CALL      RDPTRAN2                file
.......     IF        OVRCD=0 & AADMNO=TADMN
            BRANCH    OVRCD,CLOS9000
            MATCH     AADMNO,TADMN 
            GOTO      CLOS9000 IF NOT EQUAL
.
            MOVE      TATYPE,ADMTYPE
            MOVE      TDEPT,DEPTYPE
            MOVE      TRBTYP,BEDTYPE
            CALL      CHKC0000            * Check a stay in a ICU
            IF        EXIT=1
              MOVE      ONE,WORKICLS
              MOVE      ZERO,EXIT
            ENDIF
            CALL      CPSY0000            * Check for psychiatric care
            IF        EXIT=1
              MOVE      ONE,WORKPSYC
              MOVE      ZERO,EXIT
            ENDIF
            CALL      CQNB0000            * Check for qualified care
            IF        EXIT=1
              MOVE      ONE,WORKQUAL
.                PACK      XCLNST,ONE,SP70  * Removed for Task 0901851
              MOVE      ZERO,EXIT
            ENDIF
            CALL      CHIH0000            * Check for hospital in the home
            IF        EXIT=1
              MOVE      ONE,WORKHITH
              MOVE      ZERO,EXIT
            ENDIF
........    ENDIF
            GOTO      CLOS9000
          ENDIF
.
. ----- Non Daycase -----
.
          PACK      KEY30,AADMNO,SP20
          CALL      RDSTRAN2                * Position on & read the transfer
CLOS1000  CALL      RDKTRAN2                  file
.....     IF        OVRCD=1 | AADMNO<>TADMN
.....       GOTO      CLOS7000              * Error
.....     ENDIF
          BRANCH    OVRCD,CLOS7000
.
          MATCH     AADMNO,TADMN
          GOTO      CLOS7000 IF NOT EQUAL

          CMATCH    ANSA,TMOVE
          GOTO      CLOS2000 IF EQUAL       * Admission record
.
          CMATCH    ANSL,TMOVE
          GOTO      CLOS3000 IF EQUAL       * Leave record
.
          CMATCH    ANSR,TMOVE
          GOTO      CLOS4000 IF EQUAL       * Return from leave record
.
          CMATCH    ANSC,TMOVE
          GOTO      CLOS5000 IF EQUAL       * Change record
.
          CMATCH    ANSD,TMOVE
          GOTO      CLOS6000 IF EQUAL       * Discharge record
.
          GOTO      CLOS5000                * Assume change record
.
. ----- Admission Record -----
.
CLOS2000  CALL      LSAV0000                * Save details
          CALL      GADM0000                * Get admitting details
          GOTO      CLOS1000
.
. ----- Leave Record -----
.
CLOS3000  CALL      LEND0000                * Process end of a period
          GOTO      CLOS1000
.
. ----- Return Record -----
.
CLOS4000  CALL      LDAY0000                * Calculate the leave days
          CALL      LSAV0000                * Save details
          GOTO      CLOS1000
.
. ----- Change Record -----
.
CLOS5000  CALL      LEND0000                * Process end of a period
          CALL      LSAV0000                * Save new details
          GOTO      CLOS1000
.
. ----- Discharge Record -----
.
CLOS6000  CALL      LEND0000                * Process end of a period
          GOTO      CLOS9000
.
. ----- Error -----
.
CLOS7000  MOVE      DDATE,TDATE             * Assume discharge date
          CALL      LEND0000                * Process end of a period
          GOTO      CLOS9999
.
. ----- Finished -----
.
CLOS9000  MOVE      WORKICLS,XICUDY       * ICU LOS
          REPLACE   " 0",XICUDY           * zero fill
.
          MOVE      SP10,XICUDY           * ICU days; no longer sent
          MOVE      SP10,XICUHR           * ICU hours
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1
          IF        OVRCD=0
            COMPARE   ZERO,PTICUINT
            IF        !@EQUAL
              MOVE      PTICUINT,XICUHR   * ICU hours
              REP       " 0",XICUHR
            ENDIF
          ENDIF
          REP       " 0",XICUHR
.
CLOS9010  CALL      DSCR0000                * Get discharged details
          CALL      CNTL0000    * calculate contracted leave and sub from normal
          MOVE      WORKLDAY,XLEVDY         * Total leave days
          REPLACE   " 0",XLEVDY             * zero fill
          MOVE      WORKQUAL,XDAYQNB        * qualified days
          IF        WORKQUAL = 0
            MOVE      SP3,XDAYQNB           * CAR 265260
          ENDIF
          MOVE      WORKHITH,XHIHCD         * hospital in the home care days
          REPLACE   " 0",XHIHCD             * zero fill CAR 265260
          MOVE      WORKPSYC,XPSYCH         * Psychiatric days
          REPLACE   " 0",XPSYCH             * zero fill CAR 265260
          IF        WORKPSYC = 0
            MOVE      "0000",XPSYCH
            IF        PSYCFLAG=1
              MOVE      "0001",XPSYCH       * patient had some psych stay
            ENDIF
          ENDIF
CLOS9999  RETURN
+
.******************************************************************************
.*                                  CNTL0000              Called by: CNTL0000 *
.*         Calculate contract leave value and subtract from normal leave      * 
.******************************************************************************
CNTL0000  MOVE      ZERO,XCONLDY 
          MOVE      ZERO,XCONLDY 
          PACK      CHDATE,ADATE
          PACK      FROMDATE,ADATE
          PACK      TODATE,DDATE
          PACK      EPIADATE,ADATE
          CALL      PRSCLW00                * Calculate contract leave days
          IF        NBRDAYS > 999
            MOVE      "999",F3
          ELSE
            MOVE      NBRDAYS,F3
          ENDIF
          MOVE      F3,XCONLDY         * Total contract leave days
          REP       " 0",XCONLDY
.
          MOVE      XCONLDY,F3
          SUB       F3,WORKLDAY
.
CNTL9999  PACK      KEY30,AADMNO,ZED30 
          CALL      RDSTRAN2              * Position on & read the transfer
          CALL      RDPTRAN2                file
          RETURN
+
.******************************************************************************
.*                                  LSAV0000              Called by: CLOS0000 *
.*         Save Start Of Period Details For Length Of Stay Calculations       *
.******************************************************************************
LSAV0000  PACK      CDYSFDTE,TDATE
          MOVE      TATYPE,STATYPE
          MOVE      TRBTYP,STRBTYP
.
          MATCH     "A",TMOVE
          IF        @EQUAL
            PACK      SVADDATE,TDATE
.
            MATCH     SP3,TRBTYP
            GOTO      LSAV9999 IF EQUAL        * Dont allow blank codes
.
            PACK      KEY5,ANSB,ANST,TRBTYP    * first check for any psych days
            CALL      RDCODE1
            BRANCH    OVRCD,LSAV9999
.
            MATCH     ANSA,TCINDC3
            IF        @EQUAL
              MOVE      ONE,ADMPFLAG           * patient has psych care
            ENDIF
            MATCH     ANSD,TCINDC3
            IF        @EQUAL
              MOVE      ONE,ADMPFLAG           * patient has psych care
            ENDIF
          ENDIF
.
LSAV9999  RETURN
+
.******************************************************************************
.*                                  LEND0000              Called by: CLOS0000 *
.*              Process End Of Period Details For Length Of Stay              *
.******************************************************************************
LEND0000  PACK      CDYSTDTE,TDATE
.
LEND1000  CALL      CALCDAYS                * Calculate diff between dates
          SUB       ONE,CDYSDAYS            * We dont want inclusive dates
          MOVE      STATYPE,ADMTYPE
          MOVE      TDEPT,DEPTYPE
          MOVE      STRBTYP,BEDTYPE
          CALL      CHKC0000                * Check if stay in a ICU
          IF        EXIT=1
            ADD       CDYSDAYS,WORKICLS
            MOVE      ZERO,EXIT
          ENDIF
.
          CALL      CPSY0000                * Check for psychiatric care
          IF        EXIT=1
            ADD       CDYSDAYS,WORKPSYC
            IF        WORKPSYC = 0
              CMATCH    ANSC,TMOVE          * Check if it's not a change rec
              IF        !@EQUAL
                CMATCH    ANSL,TMOVE        * Check if it's not a leave rec
                IF        !@EQUAL
                  ADD       ONE,WORKPSYC    * at least 1 psych day (TSK 0306678)
                ENDIF
              ELSE
                IF        PSYCFLAG=0
                  MOVE      ZERO,WORKPSYC
                ENDIF
              ENDIF
            ENDIF
            MOVE      ZERO,EXIT
          ELSE
            CMATCH    ANSC,TMOVE
            IF        @EQUAL
              IF        PSYCFLAG=1
                MATCH     TDATE,SVADDATE
                IF        @EQUAL
                  MOVE      ZERO,WORKPSYC
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          CMATCH    ANSD,TMOVE
          IF        @EQUAL
            IF        ADMPFLAG = 1
              IF        WORKPSYC = 0
                MOVE      ONE,WORKPSYC
              ENDIF
            ENDIF
          ENDIF
.
          CALL      CQNB0000                * Check for qualified new born
          IF        EXIT=1
            ADD       CDYSDAYS,WORKQUAL
.            PACK      XCLNST,ONE,SP70      * Removed for Task 0901851
            MOVE      ZERO,EXIT
          ENDIF
.
          CALL      CHIH0000                * Check for hospital in the home
          IF        EXIT=1
            ADD       CDYSDAYS,WORKHITH
            MOVE      ZERO,EXIT
          ENDIF
.
LEND9000  MOVE      TDATE,STDATE            * Save transfer date for leave days
LEND9999  RETURN
+
.******************************************************************************
.*                                  LDAY0000              Called by: CLOS0000 *
.*                           Calculate The Leave Days                         *
.******************************************************************************
LDAY0000  PACK      CDYSFDTE,STDATE
          PACK      CDYSTDTE,TDATE
          MATCH     CDYSTDTE,CDYSFDTE
          GOTO      LDAY9999 IF NOT LESS
.
          CALL      CALCDAYS                * Calculate diff between dates
          SUB       ONE,CDYSDAYS            * We dont want inclusive dates
          ADD       CDYSDAYS,WORKLDAY       * Total leave days
.
          MATCH     STDATE,ADATE
          IF        @EQUAL
.           SUB       ONE,WORKLDAY          * don't count adm.date - CAR 291926
          ENDIF
.
          COMPARE   ZERO,WORKLDAY
          GOTO      LDAY9999 IF EQUAL       * not a leave period - CAR 291926
.
          COMPARE   NINTY9,NUMLEV
          GOTO      LDAY9999 IF EQUAL       * don't increase for more than 99
.
          ADD       ONE,NUMLEV
.
LDAY9999  RETURN
+
.******************************************************************************
.*                                  GADM0000                                  *
.*                        Get admitting details (doctor and ward)             *
.******************************************************************************
GADM0000  MOVE      ZERO,EXIT                                         *C-132806
.                                           * following code moved to DSCR0000
....      PACK      XLOCTN,TWARD,SP10       * location
....      MATCH     SP6,XLOCTN
....      IF        @EQUAL
....        MOVE      "Location is blank ",ERRDESC
....        CALL      WRTEMP
....      ENDIF
.
          PACK      KEY6,TADOCT             * attending doctor
          CALL      RDDOCT1
          IF        OVRCD = 0
            MATCH     SP15,PTDOREGN                        * start    *C-233977
            IF        !@EQUAL
              MOVE      PTDOREGN,KEY15      * National Registration No.
            ELSE
              MOVE      PTDOWHDP,KEY15      * WA HDP Doctor code
            ENDIF
            SQUEEZE   KEY15
            PACK      XNMRNO,KEY15,SP15     * Aust Health Prac. Reg. No.
            MATCH     SP6,XNMRNO
            IF        @EQUAL
              MOVE      "Invalid National Regist. No. on Admission",ERRDESC
              CALL      WRTEMP                             * end      *C-233977
            ENDIF
.
          ELSE
            MOVE      "Invalid admission attending doctor code ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
          PACK      KEY5,ANSA,ANSC,TDEPT,SP70
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      XSPLTY,THCSCOD,SP10      * Specialty on admission
          ENDIF
          RJUSTIFY  XSPLTY                     * CAR 265260
.
          MATCH     SP3,XSPLTY
          IF        @EQUAL
            MOVE      "Invalid speciality on admission ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
GADM9999  RETURN
+
.******************************************************************************
.*                           DSCR0000                                         *
.*                   Get Discharged details                                   *
.******************************************************************************
DSCR0000  MOVE      ZERO,EXIT
.                                                                     *I-132806
          PACK      XLOCTN,SP70
          PACK      KEY6,TWARD,SP70              * ward only (CAR 260777)
          CALL      RDWARD1
          IF        OVRCD=0
            PACK      XLOCTN,PTWDSDES,SP70       * location (CAR 260777)
          ENDIF
          UNPACK    XLOCTN,D5,DIM15
          MATCH     "Ward ",D5
          IF        @EQUAL
            PACK      XLOCTN,DIM15,SP70          * CAR 265260 (remove 'Ward ')
          ENDIF
.
          MATCH     SP70,XLOCTN
          IF        @EQUAL
            MOVE      "Location is blank ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
          MOVE      TWO,XACCOCC
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
          IF        OVRCD<>1
            PACK      KEY5,CATRT,WRTYPE
            CALL      RDCODE1           * accomodation occupied
            IF        OVRCD<>1
              UNPACK    THCSCOD,DIM1
              MOVE      DIM1,XACCOCC
            ENDIF
            COMPARE    "1",XACCOCC
            IF         !@EQUAL
              COMPARE    "2",XACCOCC
              IF         !@EQUAL
                MOVE       "Invalid accomodation occupied",ERRDESC
                CALL       WRTEMP
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY6,TADOCT             * Attending Doc.
          CALL      RDDOCT1
          IF        OVRCD = 0
            MATCH     SP15,PTDOREGN                        * start    *C-233977
            IF        !@EQUAL
              MOVE      PTDOREGN,KEY15      * National Registration No.
            ELSE
              MOVE      PTDOWHDP,KEY15      * WA HDP Doctor code
            ENDIF
            SQUEEZE   KEY15
            PACK      XDSATT,KEY15,SP15     * Aust Health Prac. Reg. No.
            MATCH     SP6,XDSATT
            IF        @EQUAL
              MOVE      "Invalid National Regist. No. on separation",ERRDESC
              CALL      WRTEMP                             * end      *C-233977
            ENDIF
.
          ELSE
            MOVE      "Invalid discharged attending doctor code ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
          PACK      KEY5,ANSA,ANSC,TDEPT,SP70
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      XSPCDS,THCSCOD,SP10
          ENDIF
          RJUSTIFY  XSPCDS                     * CAR 265260
.
          MATCH     SP3,XSPCDS
          IF        @EQUAL
            MOVE      "Invalid Speciality on separation ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
DSCR9999  RETURN
+
.******************************************************************************
.*                                  CHKC0000                                  *
.*                   Check For A Stay In A Critical Care Unit                 *
.******************************************************************************
CHKC0000  MOVE      ZERO,EXIT               * Default normal/HDU bed stay
          IF        PTCNHCCC=1
            MATCH     SP3,BEDTYPE
            GOTO      CHKC9999 IF EQUAL     * Dont allow blank codes
.
            PACK      KEY5,ANSB,ANST,BEDTYPE   * Using bed type for CCU/ICU
            CALL      RDCODE1
            BRANCH    OVRCD,CHKC9999
.
            MATCH     ANSI,TCINDC1
            GOTO      CHKC9999 IF NOT EQUAL * Not an CCU/ICU stay
          ELSE
            MATCH     SP3,ADMTYPE
            GOTO      CHKC9999 IF EQUAL     * Dont allow blank codes
.
            PACK      KEY5,ANSA,SP1,ADMTYPE    * Using admin type for CCU/ICU
            CALL      RDCODE1
            BRANCH    OVRCD,CHKC9999
.
            COMPARE   ONE,TCFORM6
            GOTO      CHKC9999 IF NOT EQUAL * Not an CCU/ICU stay
          ENDIF
          MOVE      ONE,EXIT                * CCU/ICU stay (not HDU)
CHKC9999  RETURN
+
.******************************************************************************
.*                                  CPSY0000                                  *
.*                   Check For Psychiatric care (use category BT)             *
.******************************************************************************
CPSY0000  MOVE      ZERO,EXIT                * Default normal/HDU bed stay
          MATCH     SP3,TRBTYP  
          GOTO      CPSY9999 IF EQUAL        * Dont allow blank codes
.
          PACK      KEY5,ANSB,ANST,TRBTYP    * first check for any psych days
          CALL      RDCODE1
          BRANCH    OVRCD,CPSY9999
.
          MATCH     ANSA,TCINDC3
          IF        @EQUAL
            MOVE      ONE,PSYCFLAG           * patient has psych care
          ENDIF
          MATCH     ANSD,TCINDC3
          IF        @EQUAL
            MOVE      ONE,PSYCFLAG           * patient has psych care
          ENDIF
.
CPSY8000  MATCH     SP3,BEDTYPE
          GOTO      CPSY9999 IF EQUAL        * Dont allow blank codes
.
          PACK      KEY5,ANSB,ANST,BEDTYPE   * now check for period psych days
          CALL      RDCODE1
          BRANCH    OVRCD,CPSY9999
.
          MATCH     ANSA,TCINDC3
          GOTO      CPSY9000 IF EQUAL        * psychiatric care
.
          MATCH     ANSD,TCINDC3
          GOTO      CPSY9999 IF NOT EQUAL    * Not psychiatric care
.
CPSY9000  MOVE      ONE,EXIT
CPSY9999  RETURN
+
.******************************************************************************
.*                                  CQNB0000                                  *
.*                   Check For Qualified new born                             *
.******************************************************************************
CQNB0000  MOVE      ZERO,EXIT
          MATCH     SP3,ADMTYPE
          GOTO      CQNB9999 IF EQUAL     * Dont allow blank codes
.
          PACK      KEY5,ANSA,SP1,ADMTYPE    * Using admin type for qualified
          CALL      RDCODE1
          BRANCH    OVRCD,CQNB9999
.
          MATCH     "1 ",THCSCOD
          GOTO      CQNB9000 IF EQUAL       * Qualified
.
          MATCH     "9",THCSCOD
          GOTO      CQNB9000 IF EQUAL       * Qualified - added for Task 0901851
.
          GOTO      CQNB9999                * Not qualified

......    MATCH     SP3,BEDTYPE
......    GOTO      CQNB9999 IF EQUAL     * Dont allow blank codes
.
......    PACK      KEY5,ANSB,ANST,BEDTYPE   * Using bed type for CCU/ICU
......    CALL      RDCODE1
......    BRANCH    OVRCD,CQNB9999
.
......    MATCH     ANSQ,TCINDC5
......    GOTO      CQNB9999 IF NOT EQUAL * Not a qualified stay
.
CQNB9000  MOVE      ONE,EXIT
CQNB9999  RETURN
+
.******************************************************************************
.*                                  CHIH0000                                  *
.*                       Check For Hospital In The Home                       *
.******************************************************************************
CHIH0000  MATCH     SP3,BEDTYPE
          GOTO      CHIH9999 IF EQUAL        * Dont allow blank codes
.
          PACK      KEY5,ANSB,ANST,BEDTYPE   * Using bed type
          CALL      RDCODE1
          BRANCH    OVRCD,CHIH9999
.
          MATCH     ANSH,TCINDC1
          GOTO      CHIH9999 IF NOT EQUAL    * Not hospital in the home
.
          MOVE      ONE,EXIT
CHIH9999  RETURN
+
.******************************************************************************
.*                                  LANG0000                                  *
.* Get Preferred Language and Interpreter Required                            *
.*                                           (Routine cut from IBAPMS49 V9.07)*
.******************************************************************************
.
LANG0000  MOVE      TWO,XINTRP                   * default to 2 CAR 267503
          MATCH     SP3,PMVXLNG1                 * pref. language blank ?
          GOTO      LANG9999 IF EQUAL            * yes - finished language
.
          PACK      KEY5,ANSL,ANSA,PMVXLNG1,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            TYPE      THCSCOD                    * numeric HDP Equiv.?
            IF        @EQUAL
              MOVE      THCSCOD,DIM4             * yes
              SQUEEZE   DIM4
              MOVELPTR  DIM4,FORM1               * 4 digits in length ?
              IF        FORM1 = 4
                MOVE      THCSCOD,XLANG          * yes - load preferred language
                GOTO      LANG1000
              ENDIF
            ENDIF
          ENDIF
.
.         Invalid preferred language
.
          MOVE      "Invalid preferred language ",ERRDESC
          CALL      WRTEMP
.
.         Set the Interpreter required field
.
LANG1000  MATCH     "0",PMVXINTR                 * interpreter req'd ?
          IF        @EQUAL
            MOVE      TWO,XINTRP                 * no
            GOTO      LANG9999
          ENDIF
.
          MATCH     "1",PMVXINTR                 * interpreter req'd ?
          IF        @EQUAL
            MOVE      ONE,XINTRP                 * yes
            GOTO      LANG9999
          ENDIF
.
          MOVE      "Invalid interpreter required ",ERRDESC
          CALL      WRTEMP
.
LANG9999  RETURN
+
.******************************************************************************
.*                                  RJUS0000                                  *
.*                   Move field to be right justified                         *
.******************************************************************************
RJUS0000  MOVE      ZERO,FORM4
          MOVE      SP10,DIM4
          RESET     THCSCOD,4
          GOTO      RJUS2500
.
RJUS2000  BUMP      THCSCOD,-1
          GOTO      RJUS9999 IF EOS
.
RJUS2500  CMATCH    SP1,THCSCOD
          GOTO      RJUS2000 IF EQUAL
.
          MOVEFPTR  THCSCOD,FORM2
.
          SETLPTR   THCSCOD,FORM2
.
RJUS3000  RESET     THCSCOD
          MOVE      THCSCOD,FORM4
          MOVE      FORM4,DIM4
          RESET     DIM4
.
RJUS9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMES,CMDLINE
          APPEND    " 84 D1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP,FNAMES
.
CRET9999  RETURN
+
.******************************************************************************
.*                                 ENDP0000                                   *
.*                        End of processing                                   *
.******************************************************************************
ENDP0000  CALL      IBACLOCK
          UNPACK    CFILENO,DIM4
          CLOCK     TIME,TIMEIS
          UNPACK    TIMEIS,XHOUR,ANS,XMIN
          MOVE      SP30,FNAMEA
          MOVE      SP30,FNAMEB
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          MOVE      "WAHCS.txt",FNAMEA
          IF        IBCNMHOS=1
            UNPACK    PTHSHSID,DIM4
            PACK      FNAMEA,WAHCS,DIM4,TXT
            PACK      FNAMEB,WAHCS,DIM4,ACK
          ENDIF
.
          PACK      CMDLINE,SP20,SP20,SP20
          CLEAR     CMDLINE
          PACK      DIM12,FNAME3,TXT
          SQUEEZE   DIM12
          APPEND    "mv `ldat ",CMDLINE
          APPEND    DIM12,CMDLINE
          APPEND    "` ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP20,SP20,SP20
          CLEAR     CMDLINE 
          PACK      DIM12,FNAME1,TXT
          SQUEEZE   DIM12
          APPEND    "mv `ldat ",CMDLINE
          APPEND    DIM12,CMDLINE
          APPEND    "` ",CMDLINE
          APPEND    FNAMEB,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CALL      KILL0000
          CALL      DELTMP00
.
ENDP9999  RETURN
+
.*************************************************************************
.*                             KILL0000             Called by:           *
.*                   erase temporary file                                *
.*************************************************************************
KILL0000  CLOSE     TEMP
          PACK      CMDLINE,SP20,SP20,SP20
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMES,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*************************************************************************
.*                             DELTMP00             Called by: ENDP0000  *
.*                  Delete any temp text files                           *
.*************************************************************************
DELTMP00  CLOSE     WORK,DELETE
          CLOSE     WORK3,DELETE
.
DELTMP99  RETURN
+
.******************************************************************************
. Move Extract to Web Directory
.******************************************************************************
MVEX0000  PACK      KEY3,PTHSHOSP,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            CALL      CLPATHSP
          ENDIF
.
          STRIP     FNAMEUS1
.
          CLEAR     CMDLINE
          APPEND    "ibaadt50.us1 '",CMDLINE
          APPEND    FNAMEUS1,CMDLINE          * Report .txt file
.
          APPEND    "' '",CMDLINE
          APPEND    PTHSHOSP,CMDLINE          * Hospital Id
.
          APPEND    "' '",CMDLINE
.
          MATCH     SP70,PTHSHSID
          IF        @EQUAL
            PACK      KEY4,PTHSHOSP,SP70
          ELSE
            MOVE      PTHSHSID,KEY4           * Health service identifier
          ENDIF
          APPEND    KEY4,CMDLINE
.
          APPEND    "' '",CMDLINE
          APPEND    STRTDATE,CMDLINE          * From Date
.
          APPEND    "' '",CMDLINE
          APPEND    ENDDATE,CMDLINE          * To Date
.
          APPEND    "' '",CMDLINE
          APPEND    WEBUSRID,CMDLINE          * User Id
.
          APPEND    "' '",CMDLINE
          APPEND    FILETYPE,CMDLINE          * File Type
.
          APPEND    "'",CMDLINE
.
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
MVEX9999  RETURN
.
.==============================================================================
RSTEMP    RESET     KEY8
          READ      TEMP,KEY8;;
          RETURN
.
RKTEMP    MOVE      ZERO,OVRCD
          READKS    TEMP;PURNO,AADMNO,ERRDESC
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP    RESET     SKEY8
          WRITE     TEMP,SKEY8;PURNO,AADMNO,ERRDESC
          RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                        Enter selected Hospital code                        *
.******************************************************************************
KHOS0000  MOVE      ZERO,EXIT 
          COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          MOVE      "7",EVERT
          DISPLAY   *P1:EVERT,*EL,"Hospital Id   : ";
          MOVE      TWENTY7,ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
.
          OPEN      PATHSPA1,"pathspaf"
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS3000,KHOS9500
.
KHOS2000  DISPLAY   *P25:EVERT,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS3000  DISPLAY   *P1:24,"Hospital Code not entered for Multi Hospital ":
                           "environment ",*W1;
          PRINT     *1,"Hospital Code not entered for Multi Hospital ":
                       "environment"
          MOVE      SP20,PTHSNAME                                     *I-125462
          MOVE      SP10,PTHSHOSP
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.*                                  WSED0000                                  * 
.*                        Write Start & End Dates                             * 
.****************************************************************************** 
WSED0000  COMPARE   ONE,IBCNMHOS
          GOTO      WSED9999 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          PACK      KEY27,PTHSHOSP,PRGID,Z70
          CALL      RSPMWAE1
          CALL      RPPMWAE1
          BRANCH    OVRCD,WSED2000
.
          MATCH     PTHSHOSP,PMWAHOSP
          GOTO      WSED2000 IF NOT EQUAL
.
          MATCH     STRTDATE,PMWASDAT * allow to re-run last extract range
          IF        @EQUAL
            MATCH     ENDDATE,PMWAEDAT
            IF        @EQUAL
              MOVE      ONE,RERUNFL
              GOTO      WSED2000
            ENDIF
          ENDIF
.
          MATCH     PRGID,PMWAPROG
          GOTO      WSED2000 IF NOT EQUAL
.
          COMPARE   ONE,ADHOCEXT
          GOTO      WSED9000 IF EQUAL       * don't check dates if adhoc=yes
.
          MATCH     STRTDATE,PMWAEDAT
          GOTO      WSED9100 IF NOT LESS
.
          MATCH     ENDDATE,PMWAEDAT
          GOTO      WSED9100 IF NOT LESS
.
          MATCH     STRTDATE,PMWAEDAT
          GOTO      WSED9100 IF NOT LESS
.
WSED2000  PACK      PMWAHOSP,PTHSHOSP,SP70
          PACK      PMWAPROG,PRGID,SP70
          PACK      PMWASDAT,STRTDATE,SP70
          PACK      PMWAEDAT,ENDDATE,SP70
.
          CALL      IBACLOCK
          PACK      PMWACDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMWACDAT
          MOVE      CTIMEIS,PMWACTIM
.
          PACK      PMWAWEBC,PASSCODE,SP70
.
.                                           * don't write dates if adhoc=yes
          IF        ADHOCEXT <> 1
            PACK      KEY27,PMWAHOSP,PMWAPROG,PMWASDAT,PMWAEDAT,SP70
            CALL      RAPMWAE1
            IF        !@EQUAL
              CALL      WRPMWAE1
            ENDIF
          ENDIF
          GOTO      WSED9000
.
WSED9000  MOVE      ZERO,EXIT
          GOTO      WSED9999
.
WSED9100  DISPLAY   *P1:24,"Extract already run up to ",PMWAEDAT
          CALL      HITENTER
          PRINT     "Extract already run up to ",PMWAEDAT
          MOVE      ONE,EXIT
          GOTO      WSED9999
.
WSED9999  RETURN
+
.**********************************************************************
.* Keyin Adhoc extraction yes or no
.**********************************************************************
KADH0000    MOVE       ZERO,ADHOCEXT
            MOVE      ZERO,RERUNFL
            MOVE       "N",ANS
.
            KEYIN      *P1:5,*EL,"Adhoc Extraction (Y/N) : ":
                       *P26:5,*V2LON,*RV,ANS;
            RESET      ANS
            REP        UPPLOW,ANS
.
            MATCH     "Y",ANS
            IF         @EQUAL
              MOVE       ONE,ADHOCEXT
              MOVE       ONE,RERUNFL
              GOTO       KADH8000
            ENDIF
.
            MATCH     "N",ANS
            IF         @EQUAL
              MOVE       ZERO,ADHOCEXT
              GOTO       KADH8000
            ENDIF
.
            GOTO    KADH9100
.
KADH8000    MOVE    ZERO,EXIT
            GOTO    KADH9999
.
KADH9100    MOVE    ONE,EXIT
            GOTO    KADH9999
.
KADH9999    RETURN
.
.**********************************************************************
.* Keyin web user id
.**********************************************************************
KUSR0000  MOVE      SP70,WEBUSRID
          KEYIN     *P1:17,*EF:
                    *P1:17,"Web User Id : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
          MATCH     SP70,WEBUSRID
          GOTO      KUSR9100 IF EQUAL
.
          PACK      KEY10,WEBUSRID,SP70
.>>>>     CALL      RDWBSE1
          BRANCH    OVRCD,KUSR9100
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9100  MOVE      ONE,EXIT
.
KUSR9999  RETURN
+
.**********************************************************************
.* Calculate contracted leave days
.* and add to qualified newborn care days if qualified when on leave
.**********************************************************************
PRSCLW00  MOVE      ZERO,NBRDAYS
          UNPACK    SP30,CDYSFDTE,CDYSTDTE  * Clear the from date & to date
.
. ----- Loop thru the leave records looking for reqd Leave records -----
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2                * Position on & read the transfer
PRSCLW10  CALL      RDKTRAN2                  file
          BRANCH    OVRCD,PRSCLW20  
.
          MATCH     AADMNO,TADMN
          GOTO      PRSCLW20 IF NOT EQUAL   * Different admission number
.
          MATCH     TDATE,TODATE
          GOTO      PRSCLW20 IF LESS        * end of Tran records
.
. ----- Only looking for "L" records -----
.
          MATCH     "L",TMOVE
          GOTO      PRSCLW10 IF NOT EQUAL   * some other record
.
. ----- Leave record found -----
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,PRSCLW10
.
          MATCH     ANSC,THCSCOD
          GOTO      PRSCLW15 IF EQUAL       * "contracted can be C or R"
.
          MATCH     ANSR,THCSCOD
          GOTO      PRSCLW10 IF NOT EQUAL   * Not "contracted"
.
PRSCLW15  MOVE      ZERO,QUALIFNB           * Not qualified newborn
          MOVE      TATYPE,ADMTYPE
          CALL      CQNB0000                * Check for qualified care
          IF        EXIT=1
            MOVE      ONE,QUALIFNB          * Yes qualified newborn
            MOVE      ZERO,EXIT
          ENDIF
.
          MATCH     FROMDATE,TDATE
          IF        @LESS
            PACK      CDYSFDTE,FROMDATE     * Set up from date
          ELSE
            PACK      CDYSFDTE,TDATE        * Set up from date
          ENDIF
.
.
. ----- Get the return record from the transfer file -----
.
          CALL      RDKTRAN2                * Read the next transfer record
          BRANCH    OVRCD,PRSCLW20
.
          MATCH     AADMNO,TADMN
          GOTO      PRSCLW20 IF NOT EQUAL   * Different admission number
.
          MATCH     "R",TMOVE
          GOTO      PRSCLW20 IF NOT EQUAL   * Return record
.
. ----- Return record found -----
.
          MATCH     TDATE,TODATE            * C-53489 setup todate
          IF        @LESS
            PACK      CDYSTDTE,TODATE        * Set up to date
          ELSE
            PACK      CDYSTDTE,TDATE        * I-53489
          ENDIF
          CALL      CALCU000              * Calculate diff between 2 dates
.
          IF        QUALIFNB = 1
            ADD       CDYSDAYS,WORKQUAL   * Include contracted leave in
          ENDIF                           * qualified newborn days
.
          UNPACK    SP30,CDYSFDTE,CDYSTDTE  * Clear the from date & to date
          GOTO      PRSCLW10
.
. ----- Record out of date range, work out contract leave days -----
.
PRSCLW20  MATCH     SP8,CDYSFDTE
          GOTO      PRSCLW90 IF EQUAL       * Not a leave record, exit
.
          PACK      CDYSTDTE,TODATE         * Set up to date
          CALL      CALCU000                * Calculate diff between 2 dates
.
          IF        QUALIFNB = 1
            ADD       CDYSDAYS,WORKQUAL   * Include contracted leave in
          ENDIF                           * qualified newborn days
.
PRSCLW90  MOVE      ZERO,EXIT
PRSCLW99  RETURN
+
.******************************************************************************
.*                                  CALCU000                                  *
.*                     Calculate Difference Between 2 Dates                   *
.******************************************************************************
CALCU000  REP       " 0",CDYSFDTE
          REP       " 0",CDYSTDTE
          MATCH     CDYSFDTE,CDYSTDTE
          GOTO      CALCU100 IF LESS        * To date < from date, exit
.
          CALL      CALCDAYS                * Calculate diff between 2 dates
          SUB       ONE,CDYSDAYS            * We dont want inclusive dates
          ADD       CDYSDAYS,NBRDAYS        * Accumulate contract leave days
          GOTO      CALCU900
.
. ----- Error with the dates, set to zero -----
.
CALCU100  MOVE      ZERO,NBRDAYS
.
CALCU900  MOVE      ZERO,EXIT
CALCU999  RETURN
.
.------------------------------------------------------------------------------
. Get patatraf data for discharge - Reason for discharge delay (028)
. Input  AURNO
.        DAADMNO
.        DIM3     - Type
. Return EXIT = 0, patatraf record found
.        EXIT = 1, No patatraf record found
.------------------------------------------------------------------------------
GPTATR00  MOVE      ZERO,EXIT
          PACK      KEY35,AURNO,DAADMNO,DIM3,Z70
          CALL      RSPTATR3
          CALL      RPPTATR3
          BRANCH    OVRCD,GPTATR98
.
          MATCH     AURNO,PTARURNO
          GOTO      GPTATR98 IF NOT EQUAL
.
          MATCH     DAADMNO,PTARVISN
          GOTO      GPTATR98 IF NOT EQUAL
.
          MATCH     DIM3,PTARTYPE
          GOTO      GPTATR98 IF NOT EQUAL
.
          MATCH     "1",PTARDELR
          GOTO      GPTATR98 IF EQUAL
.
          GOTO      GPTATR99
.
GPTATR98  MOVE      ONE,EXIT
.
GPTATR99  RETURN
+
.==============================================================================
          INC       STD001IO
.
          INC       CALCDAYS
          INC       CLPATATR
          INC       CLPATHSP
          INC       CALCAGE
          INC       PATHSPKY
          INC       PATM10T9                * Map ICD10 to ICD9 codes
          INC       TFILENAM                     * Generate Temp File Name
          INC       SEXDSCIO
.
          INC       IBAPOSIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       OPRDEAIO/INC 
          INC       OPRDEBIO/INC
          INC       PATASFIO/INC
          INC       PATCMPIO/INC            * Complex mapping file
          INC       PATCODIO/INC            * Codes file
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC            * Discharge file
          INC       PATECDIO/INC            * Patient episode disease file
          INC       PATECOIO/INC            * Patient episode operation file
          INC       PATECMIO/INC
          INC       PATICDIO/INC
          INC       PATICOIO/INC
          INC       PATICUIO/INC
          INC       PATDGWIO/INC
          INC       PATFN1IO/INC
          INC       PATLINIO/INC
          INC       PATMA1IO/INC            * Patient master file
          INC       PMSPX2IO/INC
          INC       PATHSPIO/INC            * Hospital Details table
          INC       PATATRIO/INC            
          INC       PATMI1IO/INC            * Patient admission file
          INC       PATVISIO/INC            * Patient visit record
          INC       PRSPMIIO/INC            * PMI Triggers
          INC       PMSCCDIO/INC
          INC       PMSEXTIO/INC
          INC       PMSWAEIO/INC            * WA Extract ranges
          INC       PMSWTRIO/INC            * WA Trigger file
          INC       PMSVX1IO/INC            * Visit extension file
          INC       PATPGRIO/INC
          INC       PATTRNIO/INC            * Transfer file
          INC       PATWR1IO/INC
          INC       PATWVEIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
+
.


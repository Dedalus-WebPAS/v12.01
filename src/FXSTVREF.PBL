.***************************************************************************
.* System    :   Allied Health                                             *
.* Program   :   FXSTVREF                                                  *
.* Desc      :   Close All referrals for STV Hospital in the home          *
.***************************************************************************
.* Date      :   25/10/2005                                                *
.* Author    :   Sandra Barcham                                            *
.* Function  :   This program will close all referrals for department      *
.*           :   SVH                                                       *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC      ALLCONFD/INC
          INC      ALLREFFD/INC
          INC      ALLENCFD/INC
          INC      PATCODFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CLOSFLAG  FORM      1
CLOSEREA  DIM       3
CODE      DIM       3
DIM3      DIM       3
SAVKEY22  DIM       22
.
PRGID     INIT      "FXSTVREF"
PRGNAM    INIT      "Close Referral for STV"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      REAS0000               * Close reason
          BRANCH    EXIT,ML0100
.
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      ALLREFA4,"allrefaf"
          OPEN      ALLENCA1,"allencaf"
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run":
                    *P5:15,*V2LON,"NOTE:",*HOFF:
                    *P5:16,"This program will close all referrals":
                    *P5:17,"For Department SVH"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      "SVH",DIM3
          PACK      KEY22,DIM3,SP70
          CALL      RSALREF4
PROC1000  CALL      RKALREF4
          BRANCH    OVRCD,PROC9999
.
          MATCH     DIM3,ALREDEPT
          GOTO      PROC9999 IF NOT EQUAL
.
          DISPLAY   *P20:24,ALREVISN
.
          MATCH     "1",ALRESTAT
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      SAVKEY22,ALREDEPT,ALRESTAT,ALREHCP,ALREVISN,SP70
          MOVE      "2",ALRESTAT            * Populate all the fields and write
          MOVE      SP3,ALRERINA            * in database
          MOVE      SP8,ALREDINA
          MOVE      SP8,ALRETINA
          MOVE      SP8,ALREDINU
          MOVE      SP10,ALREUINA
          PACK      ALREUDAT,CCC,CYY,CMM,CDD,SP70 * current date
          REP       " 0",ALREUDAT
          CLOCK     TIME,ALREUTIM           * current time
          PACK      ALREDCLO,CCC,CYY,CMM,CDD,SP70
          REP       " 0",ALREDCLO
          CLOCK     TIME,ALRETCLO           * Close time
          MOVE      CLOSEREA,ALRERCLO       * Close reason

          CALL      UPALREF4
.
          MOVE      SAVKEY22,KEY22
          CALL      RSALREF4
          GOTO      PROC1000
.
PROC9999  RETURN
+
+
.***************************************************************************
.*                               REAS0000                                  *
.*                          Close referral reason                          *
.***************************************************************************
REAS0000  MOVE      "22",ECOL
          MOVE      "11",EVERT
          MOVE      "LL",CODE
          MOVE      SP3,CLOSEREA
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND
          MOVE      ZERO,CKYIMODE
.
          DISPLAY   *P1:EVERT,"Reason for Closure :",*EF;
          CALL      PATCODKY                * Keyin a codes file code
          BRANCH    EXIT,REAS9999,REAS9999
.
          MOVE      ACODE,CLOSEREA
          DISPLAY   *P32:EVERT,*EL,TDESC;
         
.
REAS9999  RETURN
.
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODKY
.
          INC       ALLREFIO/INC
          INC       ALLENCIO/INC
          INC       PATCODIO/INC

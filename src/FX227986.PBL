.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   FX227986                                                    *
.* Desc      :   Fixit for Eclipse Claim Status                              *
.*****************************************************************************
.* Date      :   22/09/2010                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through the pmsectaf file looking    *
.*               for records with a status of Remittance Reported, a final   *
.*               Assessment Report and a released remittance payment.  The   *
.*               first option allows the user to simply report the           *
.*               records found, while the second option also allows the      *
.*               automatic update of the record status to Closed.            *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBACONFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATHSPFD/INC
          INC       PMSEAHFD/INC
          INC       PMSECAFD/INC
          INC       PMSECTFD/INC
          INC       PMSERDFD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      10
CURRDTTM  DIM       16
.
DIM3      DIM       3
DIM20     DIM       20
.
SAVKEY35  DIM       35
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
.
PRGID     INIT      "FX227986"
PRGNAM    INIT      "Fixit for Eclipse Claim Status"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN2000:      * proceed with fixit (report only)
                            MAIN1000       * proceed with fixit (report & upd.)
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
          GOTO      MAIN0100
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsectaf";
          OPEN      PMSECTA1,"pmsectaf"
.
          DISPLAY   *P64:24,"pmseahaf";
          OPEN      PMSEAHA2,"pmseahaf"
.
          DISPLAY   *P64:24,"pmsecaaf";
          OPEN      PMSECAA1,"pmsecaaf"
.
          DISPLAY   *P64:24,"pmserdaf";
          OPEN      PMSERDA2,"pmserdaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patfx1af";
          OPEN      PATFX1A1,"patfx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION   1 = Run Fixit (report only)                        *
.*                        2 = Run Fixit (report and update)                  *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (report only)":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit (report and update)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run fixit (report only)
                            OPTN9000             * run fixit (report and upd.)
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through pmsectaf looking for records with a status of         *
.*        Remittance Reported, a Final Assessment Report and a released      *
.*        remittance payment.                                                *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER                 * initialise counter
          DISPLAY   *P1:24,*EL,"Processing:";
.
          CALL      IBACLOCK                     * get current date/time
          PACK      CURRDTTM,CCC,CYY,CMM,CDD,CTIMEIS
          REP       " 0",CURRDTTM
.
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
.
          CALL      HEAD0000                     * print 1st page report header
.
          MOVE      SP3,PTHSHOSP                 * initialise hospital code
          COMPARE   ZERO,IBCNMHOS                * using multi-hospital ?
          GOTO      PROC1000 IF EQUAL            * no
.
.         Using multi-hospital, so loop through pmsectaf for each
.         hospital code.
.
          MOVE      SP3,KEY3
          CALL      RSPTHSP1                     * position at start of file
PROC0500  CALL      RKPTHSP1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
.         Loop through pmsectaf for the hospital code and a status of
.         Remittance Reported (14).
.
PROC1000  PACK      KEY35,PTHSHOSP,ONE,FOUR,SP30,SP5
PROC1500  CALL      RSPMECT1                     * position on rem. reported
PROC2000  CALL      RKPMECT1                     * read next record
          BRANCH    OVRCD,PROC8000               * eof - finished
.
          COMPARE   TEN4,PMECFLAG                * same status still ?
          GOTO      PROC8000 IF NOT EQUAL        * no - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,PMECHOSP,PMECFLAG,PMECHFND,PMECADMN:
                                       PMECINVN,PMECBATN
.
          BRANCH    PMECEETP,PROC2000            * DVA claim - ignore
.
.         A claim record has been found with a status of Remittance Reported,
.         so determine the participant code from the health fund.
.
          PACK      KEY14,PMECHFND,ZERO,ZERO,ZERO,ZERO,SP20
          CALL      RDFUND1                      * HF on file ?
          BRANCH    OVRCD,PROC2000               * no - ignore record
.
          MOVE      PMECHFND,KEY6
          CALL      RDPTFX11                     * HF extension on file ?
          BRANCH    OVRCD,PROC2000               * no - ignore record
.
          MATCH     SP3,PTFXECLP                 * blank participant code ?
          IF        @EQUAL
            MOVE      PTHFHGRP,DIM3              * yes - use HF Group
          ELSE
            MOVE      PTFXECLP,DIM3              * no - use eclipse part.
          ENDIF
.
PROC4000  MOVE      PMECINVN,DIM20               * load a/c reference id
          SQUEEZE   DIM20
.         PACK      DIM20,DIM20,SP20
.
.         Now check if there is a final Assessment Report for the claim.
.
          PACK      KEY62,PMECTRID,DIM3,DIM20,Z70
          CALL      RSPMEAH2                     * position on trans. id
PROC5000  CALL      RPPMEAH2                     * read previous record
          BRANCH    OVRCD,PROC2000               * eof - no assessment report
.
          MATCH     PMECTRID,PMAHFTID            * same claim trans. id ?
          GOTO      PROC2000 IF NOT EQUAL        * no - no assessment report
.
          MATCH     DIM3,PMAHFBID                * same fund brand. id ?
          GOTO      PROC2000 IF NOT EQUAL        * no - no assessment report
.
          MATCH     DIM20,PMAHARID               * same invoice number ?
          GOTO      PROC2000 IF NOT EQUAL        * no - no assessment report
.
          MATCH     ANSA,PMAHCFAC                * assessment code "A" ?
          GOTO      PROC5000 IF NOT EQUAL        * no - get next pmseahaf record
.
.         A final Assessment Report for the claim was found, so check if
.         there is a payment which has been released.
.
          PACK      KEY82,PMECTRID,SP30,SP30,SP30
          CALL      RSPMERD2                     * position on transaction id
          CALL      RKPMERD2                     * read next record
.
          MATCH     PMEDTRID,PMECTRID            * same transaction id still ?
          GOTO      PROC2000 IF NOT EQUAL        * no - ignore record
.
          MATCH     "2",PMEDSTAT                 * payment released ?
          IF        !@EQUAL
            MATCH     "3",PMEDSTAT               * no - released to suspense ?
            IF        !@EQUAL
              GOTO      PROC2000                 * no - ignore record
            ENDIF
          ENDIF
.
.         A payment has been released for the claim, so increment
.         the counter as the status of this claim should have been updated
.         to Closed.
.
          ADD       ONE,COUNTER                  * increment counter
.
          COMPARE   SIXTY,CLNO                   * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          PRINT     *1,PIPE,*3,PMECTRID,*28,PIPE,*30,DIM3,*42,PIPE:
                    *44,DIM20,*132,PIPE
          ADD       ONE,CLNO                     * increment line count
.
.         If we are running the update option, then update the status of the
.         claim record to Closed and write an audit record as Viewed.
.
          BRANCH    COPTION,PROC2000             * report only option
.
          PACK      SAVKEY35,PMECHOSP,PMECFLAG,PMECHFND,PMECADMN,PMECINVN:
                             PMECBATN
.
          MOVE      TEN5,PMECFLAG                * set status to Closed
          CALL      UPPMECT1                     * update pmsectaf record
.
          MOVE      PMECINVN,PMEAINVN            * load pmsecaaf variables
          UNPACK    CURRDTTM,PMEADATE,PMEATIME
          MOVE      PMECBATN,PMEABATN
          MOVE      PMECFLAG,PMEASTAT
          MOVE      " 5",PMEATYPE
          PACK      PMEAOPER,PRGID,SP10      
          MOVE      PMECTRID,PMEATRID
          MOVE      SP4,PMEAEROR
          PACK      PMEAEROT,SP30,SP30,SP30,SP10
          PACK      PMEASPAR,SP30,SP1
.
          PACK      KEY26,PMEAINVN,PMEADATE,PMEATIME,PMEATYPE
          CALL      RAPMECA1
          IF        OVRCD = 1
            CALL      WRPMECA1                   * write pmsecaaf record
          ENDIF
.
          MOVE      SAVKEY35,KEY35
          GOTO      PROC1500                     * get next pmsectaf record
.
PROC8000  BRANCH    IBCNMHOS,PROC0500            * using multi-hospital
.
PROC9000  DISPLAY   *P1:24,*EL,*V2LON,COUNTER,*HOFF," records ";
          IF        COPTION = 1
            DISPLAY   *P20:24,"found.  ";
          ELSE
            DISPLAY   *P20:24,"updated.  ";
          ENDIF
          CALL      HITENTER
.
          CALL      LINE0000
          PRINT     *N,*1,"*** End of Report ***"
.
PROC9999  RETURN
+
.*****************************************************************************
.*                            HEAD0000                 Called by: PROC0000   *
.*                       Print page heading                                  *
.*****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Transaction ID          ",*28,PIPE:
                    *30,"Participant",*42,PIPE,*44,"Invoice ",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SIX,CLNO                     * increment line count
.
HEAD9999  RETURN
+
.*****************************************************************************
.*                            LINE0000                 Called by: PROC0000   *
.*                      Draw line across page                     HEAD0000   *
.*****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATHSPIO/INC
          INC       PMSEAHIO/INC
          INC       PMSECAIO/INC
          INC       PMSECTIO/INC
          INC       PMSERDIO/INC

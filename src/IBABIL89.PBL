.******************************************************************************
.* System   : PATIENT MANAGEMENT                                              *
.* Program  : IBABIL89                                                        *
.* Name     : Patient Condition Report                                        *
.******************************************************************************
.* Date     : 17/10/1989                                                      *
.* Author   : K.Peak                                                          *
.* Mods.    :                                                                 *
.*        V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                     *
.*                  Added alpanumeric visit number generation                 *
.*                  -PRTB0000,VOLP0000,WARD6000,GETP0000                      *
.*        V10.06.01 23/06/2015 Nicole Torrisi CAR 314560                      *
.*                  Fixed inactive wards appearing on report (WARD0000)       *
.*        V10.03.01 27/06/2013 Ebon Clements  CAR 287511                      *
.*                  Changes for WEXTN and NSEXTN length change from 4 to 20   *
.*        V10.02.02 25/06/2011 Steve Armstrong    CAR 240722                  *
.*                  Mods to cater for changes to PATLOCFD.                    *
.*                       - LSNAME & LGNAME extended to 40 chars.              *
.*                       - PTLCGNM2 added.                                    *
.*        V10.02.01 03/06/2011 Peter McMullen CAR 240725                      *
.*                  Use Short ward name in place of code                      *
.*                  and restructure to get rid of empty ward bug              *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*        V9.10.01  14/10/2008  I Farnell   CAR 180870                        *
.*                  changed TDESC to ACODE to print code and not 1st 3 char   *
.*                  of description at PRT4000
.******************************************************************************
.*       V9.00.B01  16/05/2001  Dean Cramer                                   *
.*                  Change so (Y/N/C) options appear for all report options   *
.******************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*       V5.07.B02  27/01/1999  Nicole Harrington 507 rebounds                *
.*                  Mods to use HEAD132                                       *
.*       V5.07.B01  13/11/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.01.01  06/07/1989  M.Ohleiter                                    *
.*                  Changes for 8 characters U/R and Admission Nos. - r5.01   *
.*        V5.01.02  30/08/1989  S. O'Gorman      SRF 102161                   *
.*                  Removed Diagnosis & added Patient's Admission Number      *
.*        V5.01.03  26/09/1989  Graeme Williams  SRF 102439                   *
.*                  Fixed nursing station options                             *
.*        V5.01.04  17/10/1989  K.Peak                                        *
.*                  Rewrote program.                                          *
.*                  Added standby & onleave patients. SRF 102674              *
.*        V5.01.121 27/11/1990  Glen Hobby                                    *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.122 20/05/1991  Steve O'Gorman                                *
.*                  Fixed errors from the new Compiler                        *
.*        V5.01.123 30/08/1991  Justin Coates                                 *
.*                  If on leave must be short-term leave                      *
.*        V5.01.124 26/02/1992  D.Di.Paola       SRF 113271                   *
.*                  Display patient condition                                 *
.*        V5.01.125 11/05/1992  ROD              SRF 114447                   *
.*                  Added parameter to determine the display of patient       *
.*                  condition                                                 *
.*        V5.01.126 30/07/1992  i chung          SRF 116034                   *
.*                  Fixed not to print extra ward for Particular Ward option. *
.*                  Added ward totals.                                        *
.*                  Added "?" facility for particular ward/nursing stat. opts.*
.*        V5.01.127 11/06/93 J.Tan    SRF 121660                              *
.*                  Mods to check for active ward/bed                         *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATLOCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATNOBFD/INC
          INC       PATNURFD/INC
          INC       PATONLFD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       PATHSPFD/INC
+
.**********************************************************************
.*                            VARIABLES                               *
.**********************************************************************
AJDAY     FORM      3                  * JULIAN ADMISSION DAY
.
CDESC     DIM       20
CHOSTYP   FORM      1
CJDAY     FORM      3                  * JULIAN CURRENT DAY
COUNT     FORM      2
.
DATYPE    DIM       3
DHHMM     DIM       5
DNAME15   DIM       15                 * DOCTOR NAME SHORTENED
DPSKIP    DIM       3
DRLGTYP   DIM       30
DSEX      DIM       6
.
EOFNUR    DIM       1
EOFWR1    DIM       1
..
GRDPAT    FORM      4                  * grand total of patients
GRDSTBY   FORM      4                  * grand total of standby patients
.
IBCNMHOS  FORM      1                  * Multi hospital indicator from controlf
.
LOCDESC   DIM       25
MIN1      FORM      1
.
PAGENO    FORM      3
PHDAYS    FORM      3
PNAME     DIM       43                 * Formatted Patient's Name
PSKIP     FORM      1
PTCNTLVE  FORM      1                  * using Type of Leave ?
PTCNDPCN  FORM      1                  * print patient condition ?
.
SAVBED    DIM       3
SAVNDESC  DIM       30
SAVNEXTN  DIM       20
SAVNSIST  DIM       30
SAVNSTAT  DIM       3
SAVWEXTN  DIM       20
SAWARD    DIM       3
SBED      DIM       3
SRLGTYP   DIM       3

SWARD     DIM       3
.
TOTPAT    FORM      3                  * total patients
TOTSTBY   FORM      3                  * total standby patients
.
WRDESC    DIM       32
WRSDESC   DIM       10
+
.**********************************************************************
.*                            CONSTANTS                               *
.**********************************************************************
CODEA     INIT      "A "
CODECO    INIT      "CO"                    * patient condition category
CODEM     INIT      "M "
INVUR     INIT      "Invalid UR -"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
Z22       INIT      "zzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBABIL89"
PRGNAM    INIT      "Patient Condition Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                               ML0000                               *
.*                              Mainline                              *
.**********************************************************************
ML0000    CALL      INIT0000           * initialization
.
ML1000    CALL      CLR0000            * clear variables
          CALL      OPTN0000           * select option
          BRANCH    EXIT,ML9000
          BRANCH    OPTION,ML2000,ML3000,ML4000,ML5000
.
.         Particular Ward
.
ML2000    CALL      PWRD0000
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
.         Particular Nursing Station
.
ML3000    CALL      PNUR0000
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
.         All Nursing Station
.
ML4000    CALL      ANUR0000
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
.         All Ward
.
ML5000    CALL      AWRD0000
          BRANCH    EXIT,ML1000
.
ML6000    CALL      END0000            * end of report
          GOTO      ML1000
.
.         ....The End
.
ML9000    CHAIN     PGM
+
.**********************************************************************
.*                       INIT0000                                     *
.*                    Initialisation                                  *
.**********************************************************************
INIT0000  CALL       DISPHEAD
          DISPLAY    *P56:24,"Opening patcodes";
          OPEN       PATCODE1,"patcodes"
.
          DISPLAY    *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY    *P64:24,"patlocaf";
          OPEN       PATLOCA1,"patlocaf"
.
          DISPLAY    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY    *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY    *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY    *P64:24,"patnobef";
          OPEN       PATNOBE1,"patnobef"
.
          DISPLAY    *P64:24,"patnursm";
          OPEN       PATNURS1,"patnursm"
.
          DISPLAY    *P64:24,"patonlvf";
          OPEN       PATONLV1,"patonlvf"
.
          DISPLAY    *P64:24,"pattranf";
          OPEN       PATTRAN2,"pattranf"
.
          DISPLAY    *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY    *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY    *P64:24,"controlf";
          OPEN       CONTROLF,"controlf"
          READ       CONTROLF,TEN;*13,PTCNTLVE
          READ       CONTROLF,TEN3;*245,CHOSTYP
          READ       CONTROLF,TWENTY;*213,PTCNDPCN
          READ       CONTROLF,ZERO;*43,IBCNMHOS
          RETURN
+
.**********************************************************************
.*                       CLR0000                                      *
.*                    Clear Variables                                 *
.**********************************************************************
CLR0000   MOVE       ZERO,PAGENO           * page number
          PACK       WRDESC,SP30,SP30       * Ward Name
          MOVE       SP20,WRSDESC
          MOVE       ZERO,TOTPAT            * Total Patients
          MOVE       ZERO,TOTSTBY           * Total Standby Patients
          MOVE       ZERO,GRDPAT            * Grand Patients Total
          MOVE       ZERO,GRDSTBY           * Grand Standby Patients Total
          RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                         Select OPtioN                              *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          CALL       SCR0000
.
OPTN1000  KEYIN      *P17:10,*EL,*DV,UNDLN1,*P17:10,*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9000 IF EQUAL
.
          BRANCH     OPTION,OPTN9999,OPTN9999,OPTN9999,OPTN9999
          BEEP
          GOTO       OPTN1000
.
OPTN9000  MOVE       TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                            SCR0000                                 *
.*                         SCReeN options                             *
.**********************************************************************
SCR0000   DISPLAY    *P1:3,*EF:
                     *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF," = Particular Ward":
                     *P1:6,*V2LON,TWO,*HOFF," = Particular Nursing Station":
                     *P1:7,*V2LON,THREE,*HOFF," = All Nursing Stations":
                     *P1:8,*V2LON,FOUR,*HOFF," = All Wards":
                     *P1:10,"Select Option : ";
          RETURN
+
.**********************************************************************
.*                       PWRD0000                                     *
.*                    Keyin Paricular Ward                            *
.**********************************************************************
PWRD0000 MOVE       "Particular Ward",CPHDROPT
         MOVE      FALSE,EXIT
         KEYIN      *P1:13,*EF,"Enter Ward Number : ",*P40:13,"( ",*V2LON,"?":
                    *HOFF," for Scan )",*P21:13,*V2LON,SAWARD,*EL;
         RESET      SAWARD
         GOTO       PWRD9000 IF EOS
.
         ENDSET     SAWARD
         APPEND     SP3,SAWARD
         RESET      SAWARD
.
         MATCH      SP3,SAWARD
         GOTO       PWRD9000 IF EQUAL
.
         CMATCH     QUEST,SAWARD
         GOTO       PWRD1000 IF NOT EQUAL
         CALL       PATWRDDS
         CALL       SCR0000
         DISPLAY    *P17:10,*V2LON,OPTION;
         GOTO       PWRD0000
.
PWRD1000 PACK       KEY6 WITH SAWARD,SP6
         CALL       RDWARD1
         BRANCH     OVRCD,PWRD8000
.
         BRANCH     WACTIVE,PWRD8500       * Ignore non-active wards
.
         MOVE       ONE,PSKIP
         MOVE       WBDESC,WRDESC
         MOVE       PTWDSDES,WRSDESC
         MOVE       WEXTN,SAVWEXTN
         DISPLAY    *P30:13,*EL,WRDESC;
.
         CALL       CONTQST                 * Ok to continue ?
         BRANCH     CEXIT,PWRD2000,PWRD0000,PWRD9000
.
PWRD2000 MOVE       "60",CLNO
         DISPLAY    *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :":
                    *P50:24,"Adm : ";
         CALL       WARD0000
         GOTO       PWRD9000
.
PWRD8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Ward does not exist **    ";
          CALL      HITENTER
          GOTO      PWRD0000
.
PWRD8500  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Ward is not active **    ";
          CALL      HITENTER
          GOTO      PWRD0000
.
PWRD9000  MOVE      TRUE,EXIT
.
PWRD9999  RETURN
+
.**********************************************************************
.*                             PNUR0000                               *
.*                  Keyin Paricular Nursing Station                   *
.**********************************************************************

PNUR0000  MOVE      "Particular Nurs. St.",CPHDROPT
          MOVE      FALSE,EXIT
          KEYIN     *P1:13,*EF,"Enter Station Number : ",*P40:13,"( ",*V2LON:
                    "?",*HOFF," for Scan )",*P24:13,*V2LON,SAVNSTAT,*EL;
          RESET     SAVNSTAT
          GOTO      PNUR9000 IF EOS
.
          ENDSET    SAVNSTAT
          APPEND    SP3,SAVNSTAT
          RESET     SAVNSTAT
.
          MATCH     SP3,SAVNSTAT
          GOTO      PNUR9000 IF EQUAL
.
          CMATCH    QUEST,SAVNSTAT
          GOTO      PNUR1000 IF NOT EQUAL
          CALL      PATNURDS
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          CALL      SCR0000
          DISPLAY   *P17:10,*V2LON,OPTION;
          GOTO      PNUR0000
.
PNUR1000  MOVE      SP3,NSTAT
          MOVE      SP3,NWARD
          MOVE      SP3,SAWARD
.
          PACK      KEY6 WITH SAVNSTAT,SP3
          CALL      RDNURS1
          BRANCH    OVRCD,PNUR8000
          DISPLAY   *P32:13,*EL,NSDESC;
          MOVE      NSDESC,SAVNDESC         * N/Station description
          MOVE      NSIST,SAVNSIST          * Sister-in-Charge
          MOVE      NSEXTN,SAVNEXTN         * Ward extension
.
          CALL      SEP0000                 * separate page ?
.
          CALL      CONTQST                 * Ok to Continue ?
          BRANCH    CEXIT,PNUR2000,PNUR0000,PNUR9000
.
PNUR2000  MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
          CALL     NURS0000
.
          GOTO     PNUR9000
.

.
PNUR8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Station does not exist **    ";
          CALL      HITENTER
          GOTO      PNUR0000
.
PNUR9000  MOVE      TRUE,EXIT
.
PNUR9999  RETURN
+
.**********************************************************************
.*                            AWRD0000                                *
.*                           All Wards                                *
.**********************************************************************
AWRD0000  MOVE      "All Wards",CPHDROPT
          MOVE      FALSE,EXIT
          CALL      SEP0000                 * Separate page ?
.
          CALL      CONTQST                 * Ok to Continue ?
          BRANCH    CEXIT,AWRD1000,AWRD9000,AWRD9000
.
AWRD1000  MOVE      "99",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
          MOVE      SP3,SAWARD
          MOVE      SP6,KEY6
          CALL      RDSWARD1
          CALL      RDKWARD1                * read first ward record
          BRANCH    OVRCD,AWRD9999
.
          MOVE      SP1,EOFWR1
AWRD2000  CALL      WARD0000
          MATCH     SP1,EOFWR1
          GOTO      AWRD2000 IF EQUAL
.         BRANCH    EXIT,AWRD2000
.
          CALL      GTOT0000                * grand totals
.
AWRD9000  MOVE      ONE,EXIT
.
AWRD9999  RETURN
+
.**********************************************************************
.*                       ANUR0000                                     *
.*                   All Nursing Stations                             *
.**********************************************************************
ANUR0000  MOVE      "All Nursing Stations",CPHDROPT
          MOVE      FALSE,EXIT
          CALL      SEP0000
.
          CALL      CONTQST                 * Ok to Continue ?
          BRANCH    CEXIT,ANUR0500,ANUR9000,ANUR9000
.
ANUR0500  DISPLAY   *P1:24,*EL,"Station : ",*P20:24,"Ward :",*P40:24,"Bed :";
.
          MOVE      SP6,KEY6
          CALL      RDSNURS1
.
ANUR1000  CALL      RDKNURS1
          BRANCH    OVRCD,ANUR9000
.
ANUR1001  CALL      NURS0000
          BRANCH    EXIT,ANUR1001
.
ANUR9000  MOVE      ONE,EXIT
.
ANUR9999  RETURN
+
.**********************************************************************
.*                             HEAD0000                               *
.*                        Heading for report                          *
.**********************************************************************
HEAD0000  CALL      HEADP000   * std page header

          BRANCH    OPTION,HEAD2000,HEAD1000,HEAD1000,HEAD2000
          GOTO      HEAD9999
.
HEAD1000  PRINT     *1,"SISTER : ",SAVNSIST,*45,"STATION : ",SAVNSTAT:
                    *60,SAVNDESC,*95,"Extension : ",SAVNEXTN,*N
          ADD       TWO,CLNO
          GOTO      HEAD2000
.
HEAD2000  CALL      HEADW000
.
HEAD9999  RETURN
+

.**********************************************************************
.*                             HEADP000                               *
.*                        Just the top heading                        *
.**********************************************************************
HEADP000  ADD       ONE,PAGENO
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      HEAD132
          MOVE      FOUR,CLNO
.
HEADP999  RETURN
+
.**********************************************************************
.*                       HEADW0000                                    *
.*                    Print Ward Heading                              *
.**********************************************************************
HEADW000  PRINT     *40,"WARD    : ",SAWARD,*55,WRDESC:
                    *95,"Extension : ",SAVWEXTN,*N
          ADD       TWO,CLNO
.
          CALL      PGE0000
          PRINT        *1,"| Ward",*16,"| Admission",*28,"| Patient's Name":
                      *72,"| TH | DIS| Adm |      Condition":
                     *115,"| Attending",*132,"|":
                    *N,*1,"| /Bed",*16,"| Number",*28,"| ",*72,"|",*77,"|":
                      *82,"| Type|",*115,"| Doctor",*132,"|"
          ADD       TWO,CLNO
          CALL      PGE0000
.
HEADW900  RETURN
+
.**********************************************************************
.*                             PGE0000                                *
.*                       Print line separator                         *
.**********************************************************************
PGE0000   PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                       PRTB0000                                     *
.*                     Print Bed/Patient                              *
.**********************************************************************
PRTB0000  COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      KEY8,WADMNO
          MATCH     ZEROVISN,WADMNO 
          IF        @EQUAL
            PACK      KEY8,SP30
          ENDIF
          PRINT     *1,"|",WRSDESC,SP1,SAVBED,*16,"| ",KEY8,*28,"|",PNAME:
                    *72,"|",*77,"|",*82,"| ",DATYPE,*88,"|",*90,LOCDESC:
                    *115,"| ",DNAME15,*132,"|"
          MOVE      WWARD,SWARD
          ADD       ONE,CLNO
          CALL      PGE0000
          DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,SAVBED;
.
PRTB999   RETURN
+
.**********************************************************************
.*                             TOT0000                                *
.*                           Print Total                              *
.**********************************************************************
TOT0000   COMPARE   "58",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *73,"Total Patients : ",TOTPAT:
                    *110,"Total Standby : ",TOTSTBY,*N
          ADD       TWO,CLNO
.
TOT9999  RETURN
+
.**********************************************************************
.*                             GTOT0000                               *
.*                        Print Grand Total                           *
.**********************************************************************
GTOT0000  COMPARE   "57",CLNO
          CALL      HEADP000 IF NOT LESS
.
          PRINT     *N,*56,"GRAND TOTALS : ",*72,"Total Patients : ",GRDPAT:
                                            *109,"Total Standby : ",GRDSTBY:
                    *N,*56,"------------"
          RETURN
+
.**********************************************************************
.*                             END0000                                *
.*                          End of Report                             *
.**********************************************************************
END0000   COMPARE   ZERO,PAGENO
          GOTO      NDAT0000 IF NOT EQUAL
          COMPARE   "57",CLNO
          CALL      HEADP000 IF NOT LESS
.
          PRINT     *N," ***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
END9999   RETURN
+
.**********************************************************************
.*                             NDAT0000                               *
.*                       No Data to be Printed                        *
.**********************************************************************
NDAT0000  DISPLAY   *P1:24,*EL,*P25:24,*B,*V2LON:
                    "** No available data on file **",*W2;
          RETURN
+
.*********************************************************************
.*                  VOLP0000                    Called by : lots     *
.*        Check that the patient is on Short-term leave.             *
.*        Para's  : OADMNO        Admission Number                   *
.*        Returns : EXIT = 1      not a valid patient                *
.*********************************************************************
VOLP0000  COMPARE   ONE,PTCNTLVE
          GOTO      VOLP9000 IF NOT EQUAL   * not using Type of Leave
.
          PACK      KEY30,OADMNO,Z22
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,VOLP9100          * no tran record
.
          MATCH     OADMNO,TADMN
          GOTO      VOLP9100 IF NOT EQUAL   * no tran record
.
          MATCH     "L",TMOVE
          GOTO      VOLP9100 IF NOT EQUAL   * not a leave record
.
          MATCH     SP3,PTTRLTYP
          GOTO      VOLP9100 IF EQUAL       * no on-leave type
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          BRANCH    OVRCD,VOLP9100          * invalid Type of Leave
.
          MATCH     SP1,TCINDC1
          GOTO      VOLP9100 IF NOT EQUAL   * not short term leave
.
VOLP9000  MOVE      FALSE,EXIT
          GOTO      VOLP9999
.
VOLP9100  MOVE      TRUE,EXIT
.
VOLP9999  RETURN
++
.**********************************************************************
.*                       NURS0000                                     *
.*                    Process One  Ward                               *
.* This routine assumes we are positioned on the ward master record   *
.* (NWARD = "   ") on entry.                                          *
.**********************************************************************
NURS0000 MOVE      FALSE,EXIT
         MOVE      NSTAT,SAVNSTAT
         MOVE      NSDESC,SAVNDESC         * N/Station description
         MOVE      NSIST,SAVNSIST          * Sister-in-Charge
         MOVE      NSEXTN,SAVNEXTN         * Ward extension
         MOVE      ZERO,GRDPAT             * Grand Patients Total
         MOVE      ZERO,GRDSTBY            * Grand Standby Patients Total
         MOVE      "99",CLNO               * force new page
.
NURS1000 CALL      RDKNURS1
         IF        OVRCD  = 1
           MOVE      "Y",EOFNUR
         ENDIF
         BRANCH    OVRCD,NURS9000
         MATCH     NSTAT,SAVNSTAT
         GOTO      NURS9000 IF NOT EQUAL
.
         PACK      KEY6,NWARD,SP30
         CALL      RDWARD1
         BRANCH    OVRCD,NURS1000          * (should not happen)
         CALL      WARD0000
         GOTO      NURS1000
.
NURS9000 CALL      GTOT0000
         MOVE      TRUE,EXIT
         MATCH     "Y",EOFNUR
         IF        @EQUAL
           MOVE      FALSE,EXIT
         ENDIF
NURS9999 RETURN
+
.**********************************************************************
.*                       WARD0000                                     *
.*                    Process One  Ward                               *
.* This routine assumes we are positioned on the ward master record   *
.* (WBED = "   ") on entry.                                           *
.**********************************************************************
WARD0000 MOVE      FALSE,EXIT
.
         COMPARE   ZERO,WACTIVE
         GOTO      WARD0500 IF EQUAL
.
. If we are processing a single ward we cannot select an inactive one, but
. process all wards will find them, and we just skip over the entire ward set.
.
WARD0100 PACK      KEY6,WWARD,Z70         * set key for end of this ward
         CALL      RDSWARD1
         CALL      RDKWARD1               * skip to next if any
         IF        OVRCD = 1
           MOVE      "Y",EOFWR1
         ENDIF
         BRANCH    OVRCD,WARD9000
         GOTO      WARD0000               * start again
.
WARD0500 COMPARE   ONE,IBCNMHOS         * Using multi hospital
         GOTO      WARD1000 IF NOT EQUAL
.
         PACK      PTHSHOSP,PTHSHOSP,SP70
         MATCH     SP10,PTHSHOSP        * All Hospitals
         GOTO      WARD1000 IF EQUAL
.
         MATCH     PTWDHOSN,PTHSHOSP    * Correct hospital
         GOTO      WARD1000 IF EQUAL
.
         GOTO      WARD0100
.
WARD1000 MOVE      WBDESC,WRDESC
         MOVE      PTWDSDES,WRSDESC
         MOVE      WEXTN,SAVWEXTN
         MOVE      WWARD,SAWARD
         MOVE      SP3,SAVBED
         DISPLAY   *P30:13,*EL,WRDESC;
.
         COMPARE   "52",CLNO              * all 4 for head, 2 for bed
         GOTO      WARD1100 IF NOT LESS
         CALL      HEADW0000
         GOTO      WARD1200
.
WARD1100 CALL      HEAD0000
.
WARD1200 STRIP     WRDESC
         ENDSET    WRDESC
         APPEND    " (continued)",WRDESC
         RESET     WRDESC
.
.  Find patients assigned to the ward, but without a bed
.
WARD2000 PACK      KEY13 WITH WWARD,SP10   * report the no-beds
         CALL      RDSNOBE1
WARD2100 CALL      RDKNOBE1
         BRANCH    OVRCD,WARD3000
.
         MATCH     WWARD,NBWARD
         GOTO      WARD3000 IF NOT EQUAL
         MOVE      NBADMNO,WADMNO
         CALL      GETP0000
         BRANCH    EXIT,WARD2100
         ADD       ONE,TOTPAT
         CALL      PRTB0000
         GOTO      WARD2100
.
WARD3000 PACK      KEY14,WWARD,SP20        * report patients on leave
..          PRINT     *2,"War31000 - read onleave PTCNTLVE=",PTCNTLVE
         CALL      RDSONLV1
WARD3100 CALL      RDKONLV1
         BRANCH    OVRCD,WARD4000
.
         MATCH     WWARD,OWARD
         GOTO      WARD4000 IF NOT EQUAL
         CALL      VOLP0000                * is patient on short term leave >
         BRANCH    EXIT,WARD3100           * not on short term leave
.
         MOVE      OADMNO,WADMNO
         MOVE      "LVE",SAVBED
         MOVE      SP20,WBDESC
         CALL      GETP0000
         BRANCH    EXIT,WARD3100
         ADD       ONE,TOTPAT
         CALL      PRTB0000
         GOTO      WARD3100

WARD4000 BRANCH    WINPUT,WARD8000         * if ward only, straight to totals
.

WARD5000 CALL      RDKWARD1
         IF        OVRCD = 1
           MOVE      "Y",EOFWR1
         ENDIF
         BRANCH    OVRCD,WARD8000
..          PRINT     *2,"Ward5000 - got  ward/bed  ward ",WWARD," ,bed ",WBED," ,Admissno ",WADMNO
         MATCH     SP3,WBED
         GOTO      WARD8000 IF EQUAL
.
         MATCH     WWARD,SAWARD
         GOTO      WARD8000 IF NOT EQUAL
.
         BRANCH    WACTIVE,WARD5000        * Non-active bed, ignore
.
         MOVE      WBED,SAVBED
.
WARD6000 CALL      GETP0000
         MATCH     ZEROVISN,AADMNO 
         IF        !@EQUAL
           ADD       ONE,TOTPAT
         ENDIF
         CALL      PRTB0000
         MATCH     ZEROVISN,WSTBY
         GOTO      WARD5000 IF EQUAL
         MOVE      WSTBY,WADMNO
         MOVE      "STB",SAVBED

         CALL      GETP0000
         BRANCH    EXIT,WARD5000
         ADD       ONE,TOTSTBY
         CALL      PRTB0000
.
         GOTO      WARD5000
.
WARD8000 ADD       TOTPAT,GRDPAT          * add for grand patient total
         ADD       TOTSTBY,GRDSTBY        * add for grand standby total
         CALL      TOT0000
         MOVE      ZERO,TOTPAT            * Total Patients
         MOVE      ZERO,TOTSTBY           * Total Standby Patients
.
.                                  * if new page per ward, force headings
         IF        PSKIP = 0
           MOVE      "99",CLNO
         ENDIF
.
         COMPARE   ONE,WINPUT              * is this a no-bed ward
         GOTO      WARD9000 IF NOT EQUAL
.
                                          * skip to next ward
         PACK      KEY6,WWARD,Z70         * set key for end of this ward
         CALL      RDSWARD1
         CALL      RDKWARD1               * skip to next if any
         IF        OVRCD = 1
           MOVE      "Y",EOFWR1
         ENDIF
         GOTO      WARD9000
.
WARD9000 MOVE      FALSE,EXIT
         MATCH     "Y",EOFWR1
         IF         !@EQUAL
           MOVE       TRUE,EXIT
         ENDIF
WARD9999 RETURN
+
.**********************************************************************
.*                             GETP0000                               *
.*                       Fetch Patient data                           *
.**********************************************************************
GETP0000  PACK      PNAME,SP30,SP30
          MOVE      SP30,DNAME15
          MOVE      SP30,DATYPE
          PACK      KEY8,WADMNO
          MOVE      ZEROVISN,WADMNO
          MOVE      FALSE,EXIT
          PACK      LOCDESC,SP30,SP30
          CALL      RDMISS1
.
.         Check if patient is currently admitted
          BRANCH    OVRCD,GETP9999
          IF        ASTAT = 2
            GOTO      GETP1000
          ENDIF
.
.         Check if patient is on leave
          IF        ASTAT = 4
            GOTO      GETP1000
          ENDIF
.
          MOVE      TRUE,EXIT
          GOTO      GETP9999
.
.         Check if patient is on leave
.
GETP1000  MOVE      AADMNO,WADMNO
.
          MOVE      AURNO,KEY8
          PACK      PSNAME,INVUR,AURNO
          UNPACK    SP30,PTITL,PGNAME
          CALL      RDMAST1
          CALL      RDPMPX21
.                                           * get doctor name
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GETP4000
          MOVE      DGNAME,ANS
          PACK      DNAME15 WITH ANS,DOT,DSNAME
.
.                                           * get admission type
GETP4000  PACK      KEY5 WITH CODEA,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,GETP5000
          MOVE      ACODE,DATYPE
.
GETP5000  PACK      PNAME,PSNAME,SP1,PTITL,SP1,PGNAME
.
.-------- get patient condition description -----
.
          PACK      LOCDESC,SP30
          COMPARE   ONE,PTCNDPCN                 * display patient condition ?
.          GOTO      GETP9000 IF NOT EQUAL
          PACK      KEY88,PTMASNAM,PMPXFNAM,WADMNO
          CALL      RDLOCA1                      * read Patient Location file
          BRANCH    OVRCD,GETP9000
.
          MATCH     SP3,LPCONDC                  * check for spaces
          GOTO      GETP9000 IF EQUAL
.
          PACK      KEY5,CODECO,LPCONDC
          CALL      RDCODE1                      * get desc. from Codes file
          BRANCH    OVRCD,GETP9000
          MOVE      TDESC,LOCDESC
.
GETP9000  MOVE      FALSE,EXIT
.
GETP9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.**********************************************************************
.*                            SEP0000                                 *
.*                        Separate page ?                             *
.**********************************************************************
SEP0000   KEYIN     *P1:16,*EL,"Would you like each Ward on a seperate PAGE (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          RESET     ANS
          GOTO      SEP1000 IF EOS
          REP       UPPLOW,ANS
.
          MATCH     "Y",ANS
          GOTO      SEP2000 IF EQUAL
          MATCH     "N",ANS
          GOTO      SEP1000 IF EQUAL
          BEEP
          GOTO      SEP0000
.
SEP1000   MOVE      ONE,PSKIP               * No page separation
          DISPLAY   *P53:16,*EL,*V2LON,"No";
          GOTO      SEP9999
.
SEP2000   MOVE      ZERO,PSKIP              * Page separation
          DISPLAY   *P53:16,*EL,*V2LON,"Yes";
.
SEP9999   RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       PATNURDS
          INC       PATWRDDS
          INC       PATHSPKY
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATLOCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATNOBIO/INC
          INC       PATNURIO/INC
          INC       PATONLIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PATHSPIO/INC
+

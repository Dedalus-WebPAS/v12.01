.******************************************************************************
.* System    :   Patient Management System                                    *
.* Program   :   CONXMAS                                                      *
.* Desc      :   Delete PMI Entries from the Conversion Load (Pgm FXMASTER)   *
.* Date      :   26/06/2001                                                   *
.* Mods      :                                                                *
.******************************************************************************
.*        V11.04.01 05.02.2024 DA Sarkies        TSK 0936282                  *
.*                  Increased size of variable to hold Healthfund Number      *
.******************************************************************************
.*        V10.00.01 09/04/2010 Steve Armstrong   CAR 219045                   *
.*                  Recompiled for changes PATMRGFD.                          *
.******************************************************************************
.*        V9.02.01  25/10/2001  Mike Laming                                   *
.*                  Add TRAP FORMAT code to reading from txt files            *
.*        V9.00.00  26/06/2001  Mike Laming                                   *
.*                  New program to delete all PMI Master records loaded in    *
.*                  the PMI Conversion - by program FXWEBRCH (orig. FXMASTER) *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       PATAM1FD/INC            * Master audit file
          INC       PATMA1FD/INC            * Master file & Extension 1
          INC       PMSPX2FD/INC            * Master file Extension 2       @@
          INC       PATMRGFD/INC            * Patient U/R Merge Request     @@
.
. ----- PMI file layout - convpmi.txt -----
.
PMITMP    FILE      ASCII, FIXED=2400
.
.Name     Type    Length    Start  Description
.----     ----    ------    -----  -----------
XURNO     DIM       8         1    U/R number/NHI Number
XTIT      DIM       4         9    Patient title
XSURN     DIM       20        13   Patient surname
XGIVN     DIM       25        33   Patient given name
XPSURN    DIM       19        58   Previous Surname   
XADD1     DIM       35        77   Patient address line 1
XADD2     DIM       35       112   Patient address line 2
XADD3     DIM       35       147   Patient address line 3
XADD4     DIM       35       182   Patient address line 4
XPOST     DIM       8        217   Patient postcode
XTELP     DIM       20       225   Telephone no private
XTELB     DIM       20       245   Telephone no business
XSEX      DIM       1        265   Patients sex
XMSTAT    DIM       3        266   Patients marital status
XDOB      DIM       8        269   Patients date of birth (ccyymmdd)
XCOB      DIM       3        277   Patients country of birth
XREL      DIM       3        280   Patients religion
XOCCUP    DIM       14       283   Patients occupation
XSMOK     DIM       1        297   Patient smoker
.                                    0=Unknown
.                                    1=yes
.                                    2=no
XHFUND    DIM       6        298   CHE/Board Code (Health Fund)
XDATDTH   DIM       8        304   Date of death (ccyymmdd)
XDCEASE   DIM       1        312   Deceased indicator
.                                    0=no
.                                    1=yes
XAUTOP    DIM       1        313   Autopsy indicator
.                                    0=no
.                                    1=yes
XNOKNAME  DIM       20       314   NOK name
XNOKADD1  DIM       35       334   NOK address line 1
XNOKADD2  DIM       35       369   NOK address line 2
XNOKADD3  DIM       35       404   NOK address line 3
XNOKADD4  DIM       35       439   NOK address line 4                       @@
XNOKPOST  DIM       8        474   NOK Post Code      
XNOKPPHN  DIM       20       482   NOK private telephone number
XNOKBPHN  DIM       20       502   NOK business telephone number
XNOKREL   DIM       10       522   NOK relationship
XCONNAME  DIM       20       532   contact name
XCONADD1  DIM       35       552   contact address line 1
XCONADD2  DIM       35       587   contact address line 2
XCONADD3  DIM       35       622   contact address line 3
XCONADD4  DIM       35       657   contact address line 4
XCONPOST  DIM       8        692   contact Post Code      
XCONPPHN  DIM       20       700   contact private telephone number
XCONBPHN  DIM       20       720   contact business telephone number
XCONREL   DIM       10       740   contact relationship
XNEANAME  DIM       20       750   Nearest Rel name
XNEAADD1  DIM       35       770   Nearest Rel address line 1
XNEAADD2  DIM       35       805   Nearest Rel address line 2
XNEAADD3  DIM       35       840   Nearest Rel address line 3
XNEAADD4  DIM       35       875   Nearest Rel address line 4
XNEAPOST  DIM       8        910   Nearest Rel Post Code      
XNEAPPHN  DIM       20       918   Nearest Rel private telephone number
XNEABPHN  DIM       20       938   Nearest Rel business telephone number
XNEAREL   DIM       10       958   Nearest Rel relationship
XCARD     DIM       20       968   Community Card Number 
XEXPD     DIM       8        988   Community Card Number Expiry Date 
XEXMT     DIM       3        996   Community Card Exemption Type 
XFAMID    DIM       12       999   Community Card Family Id.     
XREGGP    DIM       8        1011  Registered GP
XCELLPH   DIM       20       1019  Cellular Phone Number
XPADD1    DIM       35       1039  Postal Address Line 1
XPADD2    DIM       35       1074  Postal Address Line 2
XPADD3    DIM       35       1109  Postal Address Line 3
XPADD4    DIM       35       1144  Postal Address Line 4
XPCODE    DIM       8        1179  Postal Address Post Code
XPBIRTH   DIM       20       1187  Place of Birth
XPUSR1    DIM       3        1207  User Defined Field 1
XPUSR2    DIM       3        1210  User Defined Field 2
XPUSR3    DIM       3        1213  User Defined Field 3
XPUSR4    DIM       3        1216  User Defined Field 4
XPUSR5    DIM       3        1219  User Defined Field 5
XPUSR6    DIM       3        1222  User Defined Field 6
XHFTAB    DIM       4        1225  Health Fund Table
XPMEDI    DIM       10       1229  Medicare Number
XPMCCD    DIM       2        1239  Medicare Code
XPLDDT    DIM       8        1241  Last Discharge Date (ccyymmdd)
XREPAT    DIM       10       1249  Repat number
XXDVAC    DIM       5        1259  DVA Card Colour                          @@
XPENNO    DIM       15       1264  Pension number
XPNDTE    DIM       6        1279  Pension no expiry date (ccyymm)
XFNDMM    DIM       19       1285  Fund membership number
.XNATNUMB  DIM       20             National Number                         @@
. ------------------------- Start of Addition for RCH/SVHM ---------------- @@
XPUYN1    DIM       1        1295  User Defined Y/N Field 1 (1=Y)      Numeric
XPUYN2    DIM       1        1296  User Defined Y/N Field 2 (1=Y)      Numeric
XPUYN3    DIM       1        1297  User Defined Y/N Field 3 (1=Y)      Numeric
XPTYPE    DIM       3        1298  Resident Status (Patient Type) (Cat T) 
XXINTR    DIM       1        1301  Interpreter Required? (0=NO,1=YES) 
XXLNG1    DIM       3        1302  Preferred Language 1 (Cat LA)
XXLNG2    DIM       3        1305  Preferred Language 2 (Cat LA)            
XXLNG3    DIM       3        1308  Preferred Language 3 (Cat LA)
XXPEML    DIM       60       1311  Patient's Email Address
XXEDOB    DIM       1        1371  Date of Birth Estimated? 0=NO,1=YES
XXCMOB    DIM       20       1372  Emergency Contact Mobile Number
XXCEML    DIM       60       1392  Emergency Contact Email Address
XXCCRP    DIM       1        1452  Emer. Contact Corres. (0=NO,1=YES)
XXKMOB    DIM       20       1453  NOK Mobile Number
XXKEML    DIM       60       1473  NOK Email Address
XXKCRP    DIM       1        1533  NOK Correspondence (0=NO,1=YES)
XXRMOB    DIM       20       1534  Nearest Relative Mobile Number
XXREML    DIM       60       1554  Nearest Relative Email Address
XXRCRP    DIM       1        1614  Nearest Relative Corres. 0=NO,1=YES
XXMEDC    DIM       8        1615  Medicare Card Valid to (CCYYMMDD)
XXDETY    DIM       3        1623  Death Type (Cat Dy)
XMUKDD    DIM       1        1626  Unknown Date of Death (Y/N)
XXPRIS    DIM       15       1627  PRISM Number
XXCSER    DIM       15       1642  Correctional Services Number
XXPHEI    DIM       4        1657  Patient Height                 Numeric
XXPWEI    DIM       4        1661  Patient Weight                 Numeric   
XXDREC    DIM       8        1665  Date Weight & Height Rec.(CCYYMMDD)
XPSTAT    DIM       1        1673  Merged Record (U/R) (Y or blank)
XRNWUR    DIM       8        1674  New U/R NumbeR
XXMSNA    DIM       20       1682  Mother's Surname
XXMGNA    DIM       25       1702  Mother's Given Name
XXMTLE    DIM       4        1727  Mother's Title
XXMAD1    DIM       35       1731  Mother's Address Line 1
XXMAD2    DIM       35       1766  Mother's Address Line 2
XXMAD3    DIM       35       1801  Mother's Address Line 3
XXMAD4    DIM       35       1836  Mother's Address Line 4
XXMPOS    DIM       8        1871  Mother's Address Post Code
XXMPNO    DIM       20       1879  Mother's Private Number
XXMBNO    DIM       20       1899  Mother's Business Number
XXMMNO    DIM       20       1919  Mother's Mobile Number 
XXMEML    DIM       60       1939  Mother's Email Address
XXMCNT    DIM       3        1999  Mother's Country of Birth (Cat C)
XXMCRP    DIM       1        2002  Mother's Correspondence  0=NO,1=YES
XXMLAB    DIM       1        2003  Mother's Name on Labels  0=NO,1=YES
XXMREL    DIM       10       2004  Relationship to Patient
XXFSNA    DIM       20       2014  Father's Surname
XXFGNA    DIM       25       2034  Father's Given Name
XXFTLE    DIM       4        2059  Father's Title
XXFAD1    DIM       35       2063  Father's Address Line 1
XXFAD2    DIM       35       2098  Father's Address Line 2
XXFAD3    DIM       35       2133  Father's Address Line 3
XXFAD4    DIM       35       2168  Father's Address Line 4
XXFPOS    DIM       8        2203  Father's Address Post Code
XXFPNO    DIM       20       2211  Father's Private Number 
XXFBNO    DIM       20       2231  Father's Business Number
XXFMNO    DIM       20       2251  Father's Mobile Number
XXFEML    DIM       60       2271  Father's Email Address
XXFCNT    DIM       3        2331  Father's Country of Birth (Cat C)
XXFCRP    DIM       1        2334  Father's Correspondence 0=NO,1=YES
XXFLAB    DIM       1        2335  Father's Name on Labels 0=NO,1=YES
XXFREL    DIM       10       2336  Relationship to Patient 
XXUDC1    DIM       50       2346  User Defined Comments 1
XXHOML    DIM       1        2396  Homeless? (0=NO,1=YES) 
XXABRG    DIM       3        2397  Aboriginality (Cat VA)
. --------------------------- End of Addition for RCH/SVHM ---------------- @@
.End of Record               2400
.
XNATNUMB  DIM       20       * National Number - Dummy to avoid deleting code
.
. ----- Program Variables -----
.
DELETED   FORM      6
MISSED    FORM      6 
COUNT     FORM      6                       * Record counter
COUNTIN   FORM      7                                                  *I-90201
COUNTER   FORM      1
DATAERR   FORM      1                                                  *I-90201
DIM3      DIM       3
DIM8      DIM       8
SP80      DIM       80                      * Bigger Space                   @@
.
MSG01     INIT      "U/R Master not found"
.
. ----- Program Constants -----
.
PRGNAM    INIT      "Delete PMI Data from Conversion"
PRGID     INIT      "CONXMAS"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
          BRANCH    EXIT,ML9000
.
          CALL      OPTN0000                * Ask proceed question
          BRANCH    EXIT,ML9000
.
          CALL      PMST0000                * Process the master file
.
ML9000    MATCH     SP6,PGM
          GOTO      ML9999 IF EQUAL
.
          CHAIN     PGM                     * Chain back to appropriate menu
ML9999    SHUTDOWN  ""
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          PACK      SP80,SP30,SP30,SP20                                    * @@
.
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"            
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"            
          DISPLAY   *P64:24,"pmspx2af";                                    * @@
          OPEN      PMSPX2A1,"pmspx2af"                                    * @@
          DISPLAY   *P64:24,"patmrgaf";                                    * @@
          OPEN      PATMRGG1,"patmrgaf"                                    * @@
.
. ------- open convpmi.txt - pmi data ----------------------------------------
.
          MOVE      ZERO,EXIT
          TRAP      INIT9000 IF IO
          OPEN      PMITMP,"convpmi"            * Check if convpmi.txt exists
          TRAPCLR   IO
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9000  NORETURN
          TRAPCLR   IO
          DISPLAY   *P1:24,*EL,*B,"convpmi.txt does not exist.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                            Ask Proceed Question                            *
.******************************************************************************
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,"Note : This program deletes all ":
                    "records in the convpmi.txt file"
.
          DISPLAY   *P1:24,*EL,*HOFF,"Ok to Proceed (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ";
.
OPTN1000  MOVE      SP1,ANS
          KEYIN     *P23:24,*V2LON,*RV,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  PMST0000              Called by: ML0000   *
.*                           Process The Master File                          *
.******************************************************************************
PMST0000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:16,*EF,"U/R number : ":
                    *P1:17,"Surname    : ":
                    *P1:18,"Records    : ":
                    *P1:20,"Started  : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:24,"Processing ",*V2LON,"convpmi.txt ",*HOFF;
          MOVE      ZERO,COUNT
          MOVE      ZERO,COUNTIN                                       *I-90201
          CALL      HEAD0000                * Print the report header
.
. ----- Read the convpmi.txt file -----
.
PMST1000  MOVE      ZERO,DATAERR                                       *I-90201
          TRAP      FERR0000 IF FORMAT                                 *I-90201
          READ      PMITMP,SEQ;XURNO,XTIT,XSURN,XGIVN,XPSURN,XADD1,XADD2:
                               XADD3,XADD4,XPOST,XTELP,XTELB,XSEX,XMSTAT:
                               XDOB,XCOB,XREL,XOCCUP,XSMOK,XHFUND,XDATDTH:
                               XDCEASE,XAUTOP:
                               XNOKNAME,XNOKADD1,XNOKADD2,XNOKADD3,XNOKADD4:
                               XNOKPOST,XNOKPPHN,XNOKBPHN,XNOKREL:
                               XCONNAME,XCONADD1,XCONADD2,XCONADD3,XCONADD4:
                               XCONPOST,XCONPPHN,XCONBPHN,XCONREL:
                               XNEANAME,XNEAADD1,XNEAADD2,XNEAADD3,XNEAADD4:
                               XNEAPOST,XNEAPPHN,XNEABPHN,XNEAREL:
                               XCARD,XEXPD,XEXMT,XFAMID,XREGGP,XCELLPH:
                               XPADD1,XPADD2,XPADD3,XPADD4,XPCODE:
                               XPBIRTH,XPUSR1,XPUSR2,XPUSR3,XPUSR4,XPUSR5:
                               XPUSR6,XHFTAB,XPMEDI,XPMCCD,XPLDDT,XREPAT:
                               XXDVAC,XPENNO,XPNDTE,XFNDMM:
                               XPUYN1,XPUYN2,XPUYN3,XPTYPE,XXINTR,XXLNG1:
                               XXLNG2,XXLNG3,XXPEML,XXEDOB,XXCMOB,XXCEML:
                               XXCCRP,XXKMOB,XXKEML,XXKCRP,XXRMOB,XXREML:
                               XXRCRP,XXMEDC,XXDETY,XMUKDD,XXPRIS,XXCSER:
                               XXPHEI,XXPWEI,XXDREC,XPSTAT,XRNWUR,XXMSNA:
                               XXMGNA,XXMTLE,XXMAD1,XXMAD2,XXMAD3,XXMAD4:
                               XXMPOS,XXMPNO,XXMBNO,XXMMNO,XXMEML,XXMCNT:
                               XXMCRP,XXMLAB,XXMREL,XXFSNA,XXFGNA,XXFTLE:
                               XXFAD1,XXFAD2,XXFAD3,XXFAD4,XXFPOS,XXFPNO:
                               XXFBNO,XXFMNO,XXFEML,XXFCNT,XXFCRP,XXFLAB:
                               XXFREL,XXUDC1,XXHOML,XXABRG
.
          TRAPCLR   FORMAT                                             *I-90201
          GOTO      PMST9000 IF OVER
          BRANCH    DATAERR,PMST1000        * Get next record          *I-90201
.
          ADD       ONE,COUNTIN                                        *I-90201
          CALL      FMUR0000                * format U/R number
.
          ADD       ONE,COUNT
          IF        (COUNT%1000)=0
            DISPLAY   *P14:16,*V2LON,XURNO:
                      *P14:17,XSURN:
                      *P14:18,COUNT;
          ENDIF
.
          PACK      KEY8,DIM8
          CALL      RDMAST1                 * Read the master file           @@
.
          IF        OVRCD = 0      
            MATCH     "Y",XPSTAT                                             
            IF        @EQUAL  
              MOVE      DIM8,PTMROLUR       * Current U/R Number
              MOVE      XRNWUR,PTMRNWUR     * New U/R Number
              MOVE      THREE,PTMRSTAT      * Status - 3=Merge Processed
              PACK      KEY17,PTMRSTAT,PTMROLUR,PTMRNWUR
              CALL      RDPTMRG1
              IF        OVRCD = 0
.                                           * Delete the Merged Record entry
                CALL      DEPTMRG1
              ENDIF
            ENDIF
.                                           * Delete the PMI Master and Extn.1
            CALL      DEMAST1
            CALL      RDPMPX21              * Read Extn.2
.
            IF        OVRCD = 0
              CALL      DEPMPX21            * Delete Extn.2
            ENDIF
            ADD       ONE,DELETED 
          ELSE
            CALL      PRNT0000              * Print an error report 
            ADD       ONE,MISSED
          ENDIF
.
          GOTO      PMST1000
.
. ----- Finished -----
.
PMST9000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:21,"Finished : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:24,*EL,*B,"No more records.  ";
          CALL      HITENTER
.
          PRINT     *N,*1,COUNTIN," 'convpmi.txt' records read.":      *I-90201
                    *N,*1,DELETED," records deleted.",*N:
                    *N,*1,MISSED," records not found.",*N:
                    *N,*1,"*** End of Report ***"
PMST9999  RETURN
+
.******************************************************************************
.*                                     FERR0000                               *
.*            Print TRAP data errors in "convadt.txt" & "convtran.txt"        *
.******************************************************************************
.
FERR0000  MOVE      ONE,DATAERR
          PRINT     *1,XURNO,SP2,XSURN,SP2,XGIVN,SP2:
                    "Data error occurred in this record - Record No. :":
                    COUNTIN,"   - Record bypassed",*N
          READ      PMITMP,SEQ;XURNO        * Bypass rest of record
.
          ADD       ONE,CLNO
.
FERR9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PMST0000 *
.*                           Print The Report Header               & PRNT0000 *
.******************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
.         MOVE      "- Duplicate U/R No's",CPHDROPT                          @@
          CALL      HEAD80                  * Print the report header
. ------- PRINT     *1,"  Duplicate U/R No's"                              * @@
. ------- PRINT     *N;                                                      @@
          CALL      UND80                                                  * @@
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PMST0000 *
.*                            Print An Error Report                           *
.******************************************************************************
PRNT0000  COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS    * Print the report header
.
          PRINT     *1,XURNO,SP2,XSURN,SP2,XGIVN,SP2,MSG01
          ADD       ONE,CLNO
PRNT9999  RETURN
+
.******************************************************************************
.*                           FMUR0000                     Called by: PMST0000 *
.*      Format the U/R by removing leading zeroes and right justifying        *
.******************************************************************************
FMUR0000  MOVE      ZERO,COUNTER
.         WHILE     COUNTER < 7
.           ADD       ONE,COUNTER
.           CMOVE     XURNO,ANS
.           MATCH     SP1,ANS
.           IF        !@EQUAL
.             MATCH     "0",ANS
.             IF        @EQUAL
.               CMOVE     SP1,XURNO
.             ELSE
.               GOTO    FMUR0500
.             ENDIF
.           ENDIF
.           BUMP      XURNO
.         DO
.
FMUR0500  RESET     XURNO
          PACK      DIM8,XURNO,SP8
.
FMUR9999  RETURN
+
+
.==============================================================================
.
          INC       STD001IO
.         INC       PATCOMN2                * Standard patient routines
.
          INC       PATMA1IO/INC            * Master file
          INC       PMSPX2IO/INC            * Master file Extention 2       @@
          INC       PATMRGIO/INC            * Patient U/R Merge Request     @@

.***************************************************************************
.* System    :   Mental Health                                             *
.* Program   :   CREATEMH                                                  *
.***************************************************************************
.* Date      :   03/03/2010                                                *
.* Author    :   Jill Parkinson                                            *
.* Function  :   This program will change all existing admissions to be MH *
.* Mods      :                                                             *
.*     V10.00.01 31/03/2010  Mike Laming  CAR 217575                       *
.*               Recompiled for changes to MEHDIAFD                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       MEHVI1FD/INC
          INC       MEHDIAFD/INC
          INC       MEHLEGFD/INC
          INC       MEHDS1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
MHCJSTAT  DIM       3
RECNUM    FORM      8
.
PRGID     INIT      "CREATEMH"
PRGNAM    INIT      "Create Mental Health Records "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      MEHVI1A1,"mehvi1af"
          OPEN      MEHDIAA1,"mehdiaaf"
          OPEN      MEHDS1A1,"mehds1af"
          OPEN      MEHLEGA1,"mehlegaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,RECNUM
          PACK      KEY9,ONE,SP70
          CALL      RDSMISS2
PROC1000  CALL      RDKMISS2
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,RECNUM
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          COMPARE   FIVE,ASTAT
          GOTO      PROC9999 IF EQUAL
.
          CALL      CRMH0000
          GOTO      PROC1000
.
PROC9999  RETURN
+
.-----------------------------------------------------------------------------
.  Create MH records when patient is transferred to a MH Ward
.-----------------------------------------------------------------------------
CRMH0000  CALL      CLMEHVIS
          CALL      CLMEHDIA
          CALL      CLMEHDIS
.
. Post data to the Mental Health Visit file (mehvisaf)
.
          MOVE      AADMNO,MHVIADMN
          MOVE      AURNO,MHVIURNO
          PACK      KEY8,MHVIADMN           * Write or Update ?
          CALL      RAMHVIS1
          IF        OVRCD = 1
            IF        ASTAT=1
              MOVE      ZERO,MHVISTAT         * pre-admitted
            ENDIF
            IF        ASTAT=2|ASTAT=4
              MOVE      ONE,MHVISTAT         * admitted
            ENDIF
            IF        ASTAT=3
              MOVE      SIX,MHVISTAT         * discharged
            ENDIF
            CALL      WRMHVIS1
          ENDIF
.
          MOVE      AADMNO,MHDIADMN
          MOVE      ADATE,MHDIDATE
          PACK      KEY16,MHDIADMN,MHDIDATE,SP70
          CALL      RAMHDIA1
          IF        OVRCD = 1
            MOVE      "0",MHDIMOHR           * Unsent
            PACK      MHDICDAT,CCC,CYY,CMM,CDD
            REP       " 0",MHDICDAT
            CLOCK     TIME,MHDICTIM
            MOVE      UID,MHDICUID
            CALL      WRMHDIA1
          ENDIF
.
          IF        ASTAT<>3
            GOTO      CRMH9999
          ENDIF
          MOVE      AADMNO,MHDSADMN         * set admission number
          CALL      RAMHDSC1
          IF        OVRCD=1
            CALL      WRMHDSC1
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDDSCH1
          BRANCH    OVRCD,CRMH9999
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,CRMH9999
.
          PACK      DMHLEADM,AADMNO,SP70
          MOVE      DMHLEADM,MHLEADMN
          PACK      MHLEDATE,DDATE,SP70
          PACK      MHLETIME,DTIME,SP70
          PACK      MHLESTAT,PMVXMHLS,SP70
          PACK      KEY21,MHLEADMN,MHLEDATE,MHLETIME,SP70
          CALL      RAMHLEG1
          IF        OVRCD=1
            CALL      WRMHLEG1
          ENDIF
.
CRMH9999  RETURN
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CLMEHDIA
          INC       CLMEHDIS
          INC       CLMEHVIS
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       MEHLEGIO/INC
          INC       MEHVI1IO/INC
          INC       MEHDIAIO/INC
          INC       MEHDS1IO/INC

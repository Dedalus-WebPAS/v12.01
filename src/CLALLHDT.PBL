.*****************************************************************************
.* System    :   Allied Health                                               *
.* Program   :   CLALLHDT                                                    *
.* Desc      :   Clear all records from allhdtaf                             *
.*****************************************************************************
.* Date      :   15/07/1997                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through and delete all records       *
.*               from allhdtaf.                                              *
.* Mods      :                                                               *
.*        V10.14.01  17/04/2019  Steve Armstrong  TSK 0868837                *
.*                   Recompiled for changes to ALLHDTFD.                     *
.*****************************************************************************
.*        V10.04.01  Steve Armstrong  CAR 285465                             *
.*                   Recompiled for changes to ALLHDTFD                      *
.*****************************************************************************
.*        V10.03.01  Steve Armstrong  CAR 272756                             *
.*                   Recompiled for changes to ALLHDTFD                      *
.*****************************************************************************
.*        V10.00.01  Steve Armstrong  CAR 220289                             *
.*                   Recompiled for changes to ALLHDTFD                      *
.*        V10.00.00  Steve Armstrong  CAR 220289                             *
.*                   Created program                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLHDTFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "CLALLHDT"
PRGNAM    INIT      "Fixit to clear allhdtaf"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with clean up
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allhdtaf";
          OPEN      ALLHDTA1,"allhdtaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*         Clear all allhdtaf records                                        *
.*****************************************************************************
.
PROC0000  PACK      KEY42,SP30,SP20
          CALL      RSALHDT1                     * position at start of file
          CALL      RKALHDT1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          PACK      KEY42,ALHDSTAT,ALHDURNO,ALHDVISN,ALHDCONT,ALHDDTTM,ALHDRTYP:
                          ALHDACTN
          CALL      DEALHDT1
          GOTO      PROC0000
.
PROC9000  DISPLAY   *P1:24,*EL,"allhdtaf cleared.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLHDTIO/INC

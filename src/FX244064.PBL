.*******************************************************************************
.*    Program      : FX244064                                                  *
.*    Description  : Update HCP (pmshcpaf) with Doctor Code (patdo1af)         *
.*    Author       : Patrick Adair                                             *
.*    Date         : 23/08/2012                                                *
.*    Modifications:                                                           *
.*******************************************************************************
.
          INC       STD001FD
          INC       PMSHCPFD/INC
          INC       PATDO1FD/INC
.
RECCOUNT  FORM      8
.
PRGNAM    INIT      "Update HCP with Doctor Code"
PRGID     INIT      "FX244064"
VERSION   INIT      "V12.01.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                         * init and open files
          BRANCH    EXIT,MAIN9999                    * failure - innit mate?
.
          CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
. Loop thru doctor file and update doctor if corresponding HCP record exists
MAIN1000  CALL      UPDT0000 
.
MAIN9999  CHAIN     PGM
+
.*******************************************************************************
.*                                  INIT0000                                   *
.* Display heading and open files for processing                               *
.*******************************************************************************
INIT0000  CALL      DISPHEAD
.
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PMSHCPA1,"pmshcpaf"
          MOVE      ZERO,RECCOUNT
INIT9999  RETURN
+
.*******************************************************************************
.*                                  UPDT0000                                   *
.* loop through doctor file                                                    *
.* if a corresponding HCP record exists, then update doctor code on the record *
.*******************************************************************************
UPDT0000  DISPLAY   *P1:20,*EL,"Records Updated : ";
.
          PACK      KEY6,SP70
          CALL      RDSDOCT1
UPDT1000  CALL      RDKDOCT1                     * loop thru doctor file
          BRANCH    OVRCD,UPDT9000               * finish
.
          PACK      KEY10,DCODE,SP70
          CALL      RDPMHCP1                     * check for HCP record
          BRANCH    OVRCD,UPDT1000               * none - get next doctor record
.
          MOVE      DCODE,PMHCLDOC
          CALL      UPPMHCP1                     * update HCP record 
.
          ADD       ONE,RECCOUNT
.          
          GOTO      UPDT1000                     * get next doctor record
.
UPDT9000  CLOSE     PATDO1A1                     * close files
          CLOSE     PMSHCPA1
.
          CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:20,*EL,"HCP Records Updated  : ",RECCOUNT
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
UPDT9999  RETURN
.
+
.*******************************************************************************
.
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC
          INC       STD001IO
